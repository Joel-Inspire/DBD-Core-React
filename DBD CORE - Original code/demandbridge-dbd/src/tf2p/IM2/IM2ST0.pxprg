0010 REM "Import Shippto Setup/Update <IM2ST0>
0020 SETESC 9300; SETERR 9000
0035 REM "5.4 - 04/29/05 - 15.547222 - lms - SSP# 179676
0040 REM "Copyright 2005 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0080 REM "Copied from EC3ST0, E/C program to setup shiptos.  This new program will be called from an Import Read Method program (IM2G03) and either setup a new shipto record or update an existing one.  If the receiving department exists, that's the record we want to update.  Any sort records and also the SH5 records will be written.  This will be record layout number 1, program can be modified to handle other layouts in the future.  WO144867
0085 REM "INTERNAL_USE$ string array will hold info, [1]=filename (always for import module), [2]=last record for import write program (IM2W06), [3]=last record of shipto info,[4]=AR1 record, [5]=FMP cust default record, [10]=last error message, controlled by read pgm (in this case IM2G03)
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,RECORD$,TYPE$,INTERNAL_USE${ALL},RET_CODE,MESSAGE$
0100 SETERR 9000
0110 X0$="IM2ST0",X1$="Import Shipto Setup/Update"
0310 IOLIST FM0$
0320 IOLIST AR1$
0330 IOLIST AR5$
0500 REM "Open files
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O FM0... 02O AR1... 03O AR5... 04O FMP... 05O FMM... 06O ASR... 07O FTD... 08O SH5... 13O ZZPARM "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "Build search strings
0620 IF TYPE$="1" THEN CUST$="00SBGROUPA"
0621 IF TYPE$="1" AND X3$(9,3)="500" THEN CUST$="00SBG  000"
0800 IF INTERNAL_USE$[4]="" OR INTERNAL_USE$[5]="" THEN GOSUB 3000 ELSE AR1$=INTERNAL_USE$[4],FMP$=INTERNAL_USE$[5]; REM "AR1 record and FMP cust default record
1000 REM "Process RECORD$
1008 DIM FM0$(618); RET_CODE=0,MESSAGE$="",FM0$(1,11)="C"+CUST$; REM "preset type & customer code
1010 ON POS(TYPE$="1") GOSUB TYPE_NOT_FOUND,PROCESS_TYPE1
1090 GOTO 9900
2000 TYPE_NOT_FOUND:REM "Record type sent in not found
2010 RET_CODE=3,MESSAGE$="File layout type not found."
2090 TYPE_NOT_FOUND_END:RETURN 
2100 PROCESS_TYPE1:REM "Process type 1 record (WO144867/556)
2110 REC_DEPT$=RECORD$(1,10)+DIM(10)
2115 READ (Z[7],KEY="C"+CUST$+REC_DEPT$,DOM=*NEXT)
2120 K$=KEY(Z[7],END=TYPE1_CREATE)
2125 IF K$(12,20)=REC_DEPT$ THEN GOSUB TYPE1_UPDATE ELSE GOSUB TYPE1_CREATE
2150 TYPE1_PROCESSED:
2190 PROCESS_TYPE1_END:RETURN 
2200 TYPE1_UPDATE:REM "Existing record, extract FM0 and update with current info
2210 DIM FM0$(618); EXTRACT (Z[1],KEY=K$(1,11)+K$(32,4),DOM=*NEXT)IOL=0310; GOTO UPDATE_FM0
2215 GOSUB TYPE1_CREATE; GOTO TYPE1_PROCESSED; REM "Was set to update existing record but got DOM when trying EXTRACT, need to create new record instead
2220 UPDATE_FM0:REM "Have FM0 record, update fields
2225 REMOVE (Z[6],KEY=FM0$(200,10)+FM0$(1,15),DOM=*NEXT); REM "Remove phone number sort record
2230 GOSUB TYPE1_FIELDS
2235 GOSUB WRITE_RECORDS
2240 GOSUB TYPE1_WRITE_SH5
2245 TYPE1_UPDATE_END:RETURN 
2250 TYPE1_FIELDS:REM "Come here for both update and create, these are the fields provided in the incoming record
2255 FM0$(382,20)=REC_DEPT$
2257 FM0$(248,35)=RECORD$(11,35),FM0$(16,35)=RECORD$(11,35); REM "Description, name
2259 FM0$(51,30)=RECORD$(46,30),FM0$(81,30)=RECORD$(76,30); REM "Address lines 1 and 2
2261 FM0$(160,20)=RECORD$(106,20); REM "Attention
2263 PTMP$=RECORD$(136,14); GOSUB PHONE_NUM; FM0$(200,14)=PTMP$; REM "Phone number, strip out all but numbers
2265 FM0$(111,16)=RECORD$(150,16),FM0$(127,2)=RECORD$(166,2); REM "City,state
2267 FM0$(129,9)=STP(RECORD$(168,9),3,"-"); REM "Zip code
2269 FM0$(522,40)=RECORD$(177,40); REM "Email address
2295 TYPE1_FIELDS_END:RETURN 
2300 TYPE1_CREATE:REM "Create new FM0 record
2310 NEED_ST_CODE:GOSUB GET_SHIPTO_CODE; IF RET_CODE>0 THEN MESSAGE$=MESSAGE$+" "+RECORD$(1,10); GOTO TYPE1_CREATE_END
2315 WRITE (Z[1],KEY=FM0$(1,15),DOM=NEED_ST_CODE)IOL=0310
2316 EXTRACT (Z[1],KEY=FM0$(1,15))IOL=0310
2320 GOSUB TYPE1_FIELDS
2325 FM0$(214,9)=FM0$(12,4); REM "Customers location code
2330 FM0$(288,18)=FMP$(28,18); REM "FOB, How to ship, Inside delivery, Ship via 
2332 IF FM0$(305,1)<>" " THEN READ (Z[4],KEY="S"+FM0$(305,1),DOM=*NEXT)SVIA$; FM0$(306,15)=SVIA$(3,15); REM "Ship via description
2335 IF FMP$(24,1)="Y" THEN FM0$(223,1)="I"; REM "FMS Method
2340 IF STP(FM0$(16,35),3)="" THEN FM0$(16,35)=AR1$(11,35); REM "Description
2345 FM0$(224,10)=AR1$(235,10),FM0$(283,1)=AR1$(245,1); REM "Sales tax code and tax exempt status
2350 FM0$(180,20)=AR1$(185,20); REM "Greeting
2355 IF STP(FM0$(160,20),3)="" THEN FM0$(160,20)=AR1$(165,20); REM "Attention/purchasing agent
2360 FM0$(150,10)=AR1$(155,10); REM "Lookup code
2380 GOSUB WRITE_RECORDS
2385 GOSUB TYPE1_WRITE_SH5
2390 TYPE1_CREATE_END:RETURN 
3000 REM "Read customer data into AR1$ and FMP$
3010 DIM AR1$(600); READ (Z[2],KEY=CUST$,DOM=*NEXT)AR1$
3020 INTERNAL_USE$[4]=AR1$
3025 DIM FMP$(243); READ (Z[4],KEY="D"+CUST$,DOM=*NEXT)FMP$; REM "Customer default record
3030 INTERNAL_USE$[5]=FMP$
3045 RETURN 
4100 WRITE_RECORDS:
4110 WRITE (Z[1],KEY=FM0$(1,15))IOL=0310
4115 WRITE (Z[5],KEY=FM0$(1,11)+FM0$(214,9)+FM0$(12,4)); REM "FMM
4120 WRITE (Z[6],KEY=FM0$(200,10)+FM0$(1,15)); REM "ASR
4125 WRITE (Z[7],KEY=FM0$(1,11)+FM0$(382,20)+FM0$(12,4)); REM "FTD
4145 WRITE_RECORDS_END:RETURN 
4150 TYPE1_WRITE_SH5:
4155 SHIPPER$="AW5160",ST$=FM0$(12,4),SHIPVIAS$="235678"
4160 FOR X=1 TO LEN(SHIPVIAS$)
4165 DIM SH5$(36); SH5$(1,10)=CUST$,SH5$(11,4)=ST$,SH5$(15,1)=SHIPVIAS$(X,1),SH5$(16,20)=SHIPPER$; WRITE (Z[8],KEY=SH5$(1,15))SH5$
4170 NEXT X
4195 TYPE1_WRITE_SH5_END:RETURN 
7600 GET_SHIPTO_CODE:REM "Get next higher shipto code for this customer
7610 REM "if here, blank code, find next higher ship to code
7612 READ (Z[1],KEY=FM0$(1,11)+$FF$,DOM=*NEXT)
7614 PREV_KEY_1$=KEP(Z[1],END=*NEXT); GOTO 7616
7615 FM0$(12,4)="0001"; GOTO GET_SHIPTO_CODE_END; REM "if here, we found beginning of file so assign first number
7616 IF PREV_KEY_1$(1,11)<>FM0$(1,11) THEN FM0$(12,4)="0001"; GOTO GET_SHIPTO_CODE_END; REM "if here there are no shiptos for this customer so use the first one
7620 REM "if here we have the last one used in prev_key_1$(12,4), increment it
7622 IF PREV_KEY_1$(12,4)="ZZZZ" THEN RET_CODE=4,MESSAGE$="No higher shipto code can be generated."; GOTO GET_SHIPTO_CODE_END ELSE FM0$(12,4)=PREV_KEY_1$(12,4)
7623 ST_NUM=NUM(FM0$(12,4),ERR=*NEXT); IF ST_NUM>0 AND ST_NUM<9999 THEN FM0$(12,4)=STR(ST_NUM+1:"0000"); GOTO GET_SHIPTO_CODE_END
7625 FOR K_INDEX=15 TO 12 STEP -1
7627 IF FM0$(K_INDEX,1)="Z" THEN FM0$(K_INDEX,1)="0" ELSE IF FM0$(K_INDEX,1)="9" THEN FM0$(K_INDEX,1)="A"; EXITTO GET_SHIPTO_CODE_END ELSE FM0$(K_INDEX,1)=CHR(ASC(FM0$(K_INDEX,1))+1); EXITTO GET_SHIPTO_CODE_END
7629 NEXT K_INDEX
7645 GET_SHIPTO_CODE_END:RETURN 
7700 PHONE_NUM:REM "Given PTMP$, remove all but numbers
7705 PTMP$=STP(PTMP$,2); IF PTMP$="" THEN GOTO 7745 ELSE MY_TMP$=PTMP$,LEN_MY_TMP=LEN(MY_TMP$),PTMP$=""
7710 FOR MY_INDEX=1 TO LEN_MY_TMP; IF POS(MY_TMP$(MY_INDEX,1)="0123456789")<>0 THEN PTMP$=PTMP$+MY_TMP$(MY_INDEX,1) END_IF ; NEXT MY_INDEX
7745 PHONE_NUM_END:RETURN 
8910 DEF FND$(Z9$)=Z9$(1*2+1,2)+"/"+Z9$(7-1*2,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "End
9905 REM FOR I=1 TO 20; IF Z[I]<>0 THEN CLOSE (Z[I]) FI; NEXT I
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9950 EXIT 
9999 END 
