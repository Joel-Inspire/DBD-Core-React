0010 REM "<FO2COM> Gateway Ghost Task Communication
0020 SETESC 9300; SETERR 9000
0035 REM "5.0 - 01/10/02 - 17.908888 - plh - SSP# 142887
0040 REM "Copyright 2002 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="FO2COM",X1$="Gateway Ghost Task Communication"
0135 C9=-1,V0=-5
0136 L4=15,L5=5,L6=24,L7=9,L8=28
0140 M0$="-##,##0",M1$="-##0",M2$="-###.00"
0150 SECONDS=0,COUNTER=0
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 K9$=X3$(9,3)+"GOR",K9=LEN(K9$)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1,LEN(A$)),A[0],A[1],A[2]
0320 IOLIST B$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0610 DIM B1$(50); FIND (Z[1],DOM=0611,KEY=K9$)B1$
0800 GOSUB 6000
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(245)
1035 DIM A$(75),A[2]
1045 A$(1,0+K9)=K9$+A1$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1500
1200 REM "Ghost task to communicate with
1205 A9$=" ",X$="Press the spacebar to change value"
1210 CALL "ZZENTR","V",A{ALL},A9$,X4$,X3$,46,4,1,1,C0,"FOCM","{3"+X$,"","FO"+"2COM02","","",""
1215 IF C0<0 OR C0=4 THEN GOTO 9900
1220 IF A9$="0" THEN GSTASK$="GOR",PGM$="FO2GST"
1225 TSK$=TSK(0),P=POS("G"=TSK$); IF P=0 THEN GOTO 7500 ELSE TSK$=TSK$(P),ATASK$=TSK$
1230 START$=""; FIND (Z[1],KEY=TSK$(1,2)+"START",DOM=1231)START$; IF START$(11,6)=PGM$ AND TSK$(3,1)="2" THEN TSK$=TSK$(1,2); GOTO 1240
1231 IF LEN(TSK$)>3 THEN TSK$=TSK$(4); GOTO 1230
1232 GOSUB 8000; PRINT @(8,18),"Do you wish to start this ghost task?",
1233 A7$=" "; CALL "ZZENTR","Y",A{ALL},A7$,X4$,X3$,46,18,1,1,C0,"","{3"+X$,"","FO2COM04","","",""; IF C0<0 OR C0=4 THEN GOTO 9900
1234 IF A7$<>"Y" THEN GOTO 1200 ELSE GOSUB 7600; GOTO 1200
1240 OPEN (100,ERR=1232)TSK$(1,2); CLOSE (100)
1245 GOSUB 8100; REM "Print current status info
1290 GOTO 1120
1500 REM "
1505 Z9=-1
1510 A1$=K9$
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1525 Z9=0
1590 GOTO 1700
1600 REM "
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630
1630 GOTO 9800
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1820 REM "CALL "ZZPROM","P",X3$,Z,"","","",X0; ON Z GOTO 01850,06700
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310
1865 C9=1
1870 GOSUB 6200
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF C0>3 OR X0<0 THEN GOTO 1800
2040 ON X0 GOTO 2100,1800
2100 REM "Command to perform
2105 A8$=" ",X$="Press the spacebar to change value"
2110 CALL "ZZENTR","V",A{ALL},A8$,X4$,X3$,46,20,1,1,C0,"FOCM1","{3"+X$,"","FO2CO"+"M06","","",""
2115 IF C0=4 THEN GOTO 9900
2130 FIND (Z[13],KEY=TSK$+"CURRENT",DOM=2131)CURRENT$
2140 STOP$="N"
2145 IF A8$="0" THEN COMMAND$="STATUS"; GOSUB 8100
2150 IF A8$="1" THEN COMMAND$="WAIT"; GOSUB 8200
2155 IF A8$="2" THEN COMMAND$="RESET"; GOSUB 8250
2160 IF A8$="3" THEN COMMAND$="STOP"; GOSUB 8300
2190 IF STOP$="Y" THEN GOTO 1200 ELSE GOTO 2020
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(14,4),"Ghost task to communicate with:",
6062 PRINT @(0,6),Z0$,
6070 PRINT @(0,16),Z0$,
6080 PRINT @(6,20),"Select the command you wish to perform:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 X$="0"
6225 CALL "ZZDISP","V",X$,"FOCM",X3$,"","",46,4,X4$
6250 CALL "ZZDISP","V",X$,"FOCM1",X3$,"","",46,20,X4$
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6490 RETURN 
6500 REM "DELETE
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6515 TEMP$=GBL("!CLEAR","RT_PARAMETERS")
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY
6705 IF Z9=0 THEN GOTO 6790
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6715 TEMP$=GBL("RT_PARAMETERS",A$)
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6810 A1$=KEY(Z[1],END=6895)
6820 A$(1,LEN(A1$))=A1$
6825 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6830 GOSUB 6600
6835 GOSUB 6450
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
6902 V9$="2803  2804  4803  2906  2907  4806  6009  6010  6011  "
7500 REM "No ghost tasks available
7510 CALL "ZZPROM",".4",X3$,0,"There are no ghost tasks to communicate with","","",0; GOTO 9900
7600 REM "Start a ghost task
7605 IF B1$(8,2)<>"  " THEN TSK2$=TSK(2); IF POS(B1$(8,2)=TSK2$,2)=0 THEN TSK$=B1$(8,2); GOTO 7620
7610 IF ATASK$(3,1)<>"0" THEN IF LEN(ATASK$)>3 THEN ATASK$=ATASK$(4); GOTO 7610 ELSE CALL "ZZPROM",".4",X3$,0,"All available ghost tasks are active, another ghost task needs to be defined","","",0; GOTO 1200
7615 TSK$=ATASK$(1,2)
7620 READ RECORD (Z[1],KEY=FID(0)+DIM(6))FIDREC$; FIDREC$(1,2)=TSK$; WRITE RECORD (Z[1],KEY=TSK$+DIM(6))FIDREC$
7625 START 1,ERR=7700,PGM$,TSK$
7630 PRINT @(0,18),'CL',
7635 GOSUB 8000; PRINT @(0,10),"Ghost tasking for "+GSTASK$+" has been started as ghost task ID "+TSK$,
7650 S$=GSTASK$+"|STRT|"+FN%CDS$+"|"+X3$(40,3)+"|"+X3$(9,3)+"|"+FID(0)
7655 S1$="echo "+QUO+S$+QUO+" >> "+HWD+DLM+"GW_LOG"
7660 INVOKE S1$
7690 RETURN 
7700 REM "Error trying to start a ghost task
7710 GOSUB 8000; PRINT @(0,10),"An error "+STR(ERR:"000")+" has occurred when trying to start the ghost task",
7711 PRINT @(0,11),GSTASK$+" as ghost task ID "+TSK$+", please contact CSI support",
7720 GOTO 1200
8000 REM "Clear middle section of screen each time something new is displayed
8010 FOR X=7 TO 15
8020 PRINT @(0,X),'CL',
8030 NEXT X
8090 RETURN 
8100 REM "Print current status info
8110 GOSUB 8000
8120 CURRENT$="",START$=""; FIND (Z[1],KEY=TSK$+"CURRENT",DOM=8121)CURRENT$; FIND (Z[1],KEY=TSK$+"START",DOM=8121)START$; GOTO 8122
8121 PRINT @(0,10),"No current information was found, there may be a problem with this ghost task,",@(0,11),"Please contact CSI support",; GOTO 8190
8122 PRINT @(4,9),"Date/time started: "+START$(17,20),
8125 IF CURRENT$(13,6)="FO2UBA" THEN CURPGM$="Import" ELSE IF CURRENT$(13,6)="FO2UAA" THEN CURPGM$="Update"
8130 PRINT @(4,10),"Most current process: "+CURPGM$+"  Started: "+CURRENT$(19,20),
8135 PRINT @(4,11),"Date/time of last file import: "+CURRENT$(49,20),
8140 PRINT @(4,12),"Current wait setting in seconds: "+CURRENT$(39,5),
8145 PRINT @(4,13),"Number of records imported since counter was last reset: "+CURRENT$(44,5),
8190 RETURN 
8200 REM "Change wait setting
8210 GOSUB 8000
8215 PRINT @(4,10),"The current wait setting is "+CURRENT$(39,5)+" seconds",
8220 INPUT (SIZ=5)@(4,11),"Enter wait setting in seconds: ",SECONDS
8223 NEWWAIT$=STR(SECONDS:"00000"),NEWCNT$="*****"
8225 GOSUB 8350; REM "Write COMM record
8230 PRINT @(4,12),"A message has been sent to the ghost task to set the wait to "+STR(SECONDS:"00000")+" seconds",
8245 RETURN 
8250 REM "Reset counter
8260 GOSUB 8000
8265 PRINT @(4,10),"The counter is currently: "+CURRENT$(44,5),
8270 ANSWER$=" "; INPUT (SIZ=1)@(4,11),"Do you wish to reset counter to 0? ",ANSWER$
8275 IF ANSWER$ LIKE "[Yy]" THEN NEWCNT$="00000",NEWWAIT$="*****"; GOSUB 8350; PRINT @(4,12),"A message has been sent to the ghost task to reset the counter to 0",
8295 RETURN 
8300 REM "Stop a ghost process
8305 GOSUB 8000
8310 ANSWER$=" "; INPUT (SIZ=1)@(4,10),"Do you wish to stop the "+GSTASK$+" ghost task?",ANSWER$
8315 IF ANSWER$ LIKE "[Yy]" THEN NEWWAIT$="*****",NEWCNT$="*****",STOP$="Y"; GOSUB 8350; PRINT @(4,12),"A message has been sent to the ghost task to stop",
8330 S$=GSTASK$+"|STOP|"+FN%CDS$+"|"+X3$(40,3)+"|"+X3$(9,3)+"|"+FID(0)
8335 S1$="echo "+QUO+S$+QUO+" >> "+HWD+DLM+"GW_LOG"
8340 INVOKE S1$
8345 RETURN 
8350 REM "Write out COMM record
8360 COMM$=TSK$+"COMM"+X3$(9,3)+"FO2COM"+FN%CDS$+NEWWAIT$+NEWCNT$+STOP$,X$=""; EXTRACT (Z[1],KEY=TSK$+"COMM",DOM=8361)X$; COMM$=X$+COMM$
8361 WRITE (Z[1],KEY=TSK$+"COMM")COMM$
8395 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
