0010 REM "EC Export EC Vendor Information <EC2VENR>
0020 SETESC 9300; SETERR 9000
0035 REM "4.1 - 09/09/98 - 15.98 - kmc - SSP# 098699
0040 REM "Copyright 1997 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0080 REM "Export VENR type info
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,EC_PARM$,EC0$,EC1$,OPTIONS$,Z{ALL},RET$
0091 REM "EC PROG Serial number = 8
0092 EC_SERIAL_NUM=8
0093 CURR_DIR$=LWD
0100 SETERR 9000
0110 X0$="EC2VENR",X1$="Export EC Vendor Information"
0150 FIELD_SEP$=$09$; FIELD_SEP$=ATH(STP(EC_PARM$(95,10),1),ERR=0151)
0160 LINE_SEP$=$0D0A$; LINE_SEP$=ATH(STP(EC_PARM$(105,10),1),ERR=0161)
0500 REM "Open files, IF FILES array is uninitialized or Z[0] <> EC_SERIAL_NUM for this program (close files then reopen, after redim of array)
0505 IF FN%NEA("Z",1)=0 THEN GOTO 0515 ELSE IF Z[0]<>EC_SERIAL_NUM THEN GOTO 0510 ELSE GOTO 0560
0510 FOR TMP=1 TO FN%NEA("FILES",1); IF Z[TMP]<>0 THEN CLOSE (Z[TMP]) END_IF ; NEXT TMP; REM "Close all open files in the FILES array
0515 DIM Z[NUM(X3$(60,3))]; Z[0]=EC_SERIAL_NUM; REM "Dim array and set the serial number
0520 FILE_NAME$="EC4",INDEX=2; GOSUB 9750
0529 REM "Open or Create and open output file in correct DIRectory
0530 Z[1]=UNT; IF Z[1]=14 THEN OPEN (14)FID(0); GOTO 0530; REM "Skip slot 14
0532 CWDIR STP(EC_PARM$(55,40),1)
0534 OPEN LOCK (Z[1],OPT="TEXT",ERR=0535)STP(EC0$(36,30),1); GOTO 0550
0536 SERIAL STP(EC0$(36,30),1),10,0
0538 OPEN LOCK (Z[1],OPT="TEXT")STP(EC0$(36,30),1)
0540 TMP$=FN%XFD$(Z[1],0); IF DEC(TMP$(38,4))>0 THEN READ (Z[1],IND=DEC(TMP$(38,4))-1,ERR=0541)
0550 CWDIR CURR_DIR$
1000 REM "Prepare data to write out here and put into OUT$
1010 DIM EC4[1]; FIND (Z[2],KEY=EC1$(5),DOM=1011)EC4$,EC4[0],EC4[1]; GOTO 1020
1012 REM "Didn't find record!! flag error
1013 RET$="Missing data for key "+EC1$(5); GOTO 7500
1020 OUT$="VENR"+FIELD_SEP$+EC4$(1,10)+FIELD_SEP$+STP(EC4$(11,40),1); REM "EC Vendor code, EC Vendor name
1022 OUT$=OUT$+FIELD_SEP$+STP(EC4$(51,40),1)+FIELD_SEP$+STP(EC4$(91,40),1)+FIELD_SEP$+STP(EC4$(131,40),1)+FIELD_SEP$+STP(EC4$(171,40),1)+FIELD_SEP$+STP(EC4$(211,40),1)+FIELD_SEP$+STP(EC4$(251,40),1)+FIELD_SEP$+STP(EC4$(291,40),1); REM "Attention, address1, address2, city, region(state),postal code (zip), country
1024 OUT$=OUT$+FIELD_SEP$+STP(EC4$(331,24),1)+FIELD_SEP$+STP(EC4$(355,24),1); REM "Phone number, fax number
1026 OUT$=OUT$+FIELD_SEP$+STR(EC4[0])+FIELD_SEP$+EC4$(395,1); REM "Adjustment factor, communication type
1028 OUT$=OUT$+FIELD_SEP$+EC4$(396,1)+FIELD_SEP$+STP(EC4$(397,40),1)+FIELD_SEP$+STP(EC4$(437,20),1)+FIELD_SEP$+STP(EC4$(457,20),1); REM " EDI TYPE, RAS phone book, RAS User Name, RAS Password
1030 OUT$=OUT$+FIELD_SEP$+STP(EC4$(477,40),1)+FIELD_SEP$+STP(EC4$(517,24),1)+FIELD_SEP$+STP(EC4$(541,24),1)+FIELD_SEP$+STP(EC4$(565,20),1)+FIELD_SEP$+STP(EC4$(585,20),1); REM "VAN mailbox, VAN primary phone, VAN alt phone, VAN user name, VAN password
1032 OUT$=OUT$+FIELD_SEP$+STP(EC4$(605,40),1)+FIELD_SEP$+STP(EC4$(645,20),1)+FIELD_SEP$+STP(EC4$(665,20),1)+FIELD_SEP$+STP(EC4$(685,20),1)+FIELD_SEP$+STP(EC4$(705,40),1); REM "FTP machine, FTP ip address, FTP login, FTP password, FTP DIRectory
1034 OUT$=OUT$+FIELD_SEP$+EC4$(755,1)+FIELD_SEP$+EC4$(745,10)+FIELD_SEP$+EC4$(756,1); REM "Other Vendor, Parent Account, Deleted?
1036 DATE_IN$=X3$(21,6); CALL "ZZDISP","DX",DATE_IN$,"",X3$,DATE$,"",0,0,X4$
1038 OUT$=OUT$+FIELD_SEP$+DATE$+FIELD_SEP$+"TopForm"; REM "Last updated on from system date, updated by = TopForm
1040 OUT$=OUT$+FIELD_SEP$+STP(EC4$(757,20),1)+FIELD_SEP$+STP(EC4$(777,20),1)+FIELD_SEP$+STP(EC4$(797,20),1)+FIELD_SEP$+STP(EC4$(817,20),1); REM "Sender ID, Sender Auth, Recv id, Recv Authorization
1042 OUT$=OUT$+FIELD_SEP$+STR(EC4[1])+FIELD_SEP$+STP(EC4$(837,40),1)+FIELD_SEP$+STP(EC4$(877,24),1); REM "Commission percent, AP Manager , AP manager Phone
1080 IF OUT$<>"" THEN WRITE RECORD (Z[1])OUT$+LINE_SEP$
1095 GOTO 9900
7500 REM "Exit as if an error happened
7501 REM "Expecting that RET$ is already set before here
7505 EXIT 69
9000 REM "Error
9005 CWDIR CURR_DIR$
9010 RET$="ERROR # "+STR(ERR)+" @ LINE "+STR(TCB(5))+" IN "+X0$
9020 EXIT ERR
9750 REM "FILES - Use instead of ZZFLES
9755 Z[INDEX]=UNT; IF Z[INDEX]=14 THEN OPEN (14)FID(0); GOTO 9755; REM "Skip slot 14
9760 IF LEN(FILE_NAME$)<>3 THEN GOTO 9780
9765 OPEN (Z[INDEX],ERR=9780)FILE_NAME$+X3$(9,3); GOTO 9781
9780 OPEN (Z[INDEX])FILE_NAME$
9790 RETURN 
9900 REM "END PROGRAM
9905 CWDIR CURR_DIR$
9930 SETERR 9940; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
