0010 REM "EC Export Vendor Item Information <EC2VITM>
0020 SETESC 9300; SETERR 9000
0035 REM "4.1 - 09/11/98 - 15.92 - kmc - SSP# 098699
0040 REM "Copyright 1997 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0080 REM "Export SHIP type info
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,EC_PARM$,EC0$,EC1$,OPTIONS$,Z{ALL},RET$
0091 REM "EC PROG Serial number = 10
0092 EC_SERIAL_NUM=10
0093 CURR_DIR$=LWD
0100 SETERR 9000
0110 X0$="EC2VITM",X1$="Export Vendor Item Information"
0150 FIELD_SEP$=$09$; FIELD_SEP$=ATH(STP(EC_PARM$(95,10),1),ERR=0151)
0160 LINE_SEP$=$0D0A$; LINE_SEP$=ATH(STP(EC_PARM$(105,10),1),ERR=0161)
0300 IOLIST FM1$,FM1[0],FM1[1],FM1[2],FM1[3],FM1[4],FM1[5],FM1[6],FM1[7],FM1[8],FM1[9],FM1[10],FM1[11],FM1[12],FM1[13],FM1[14],FM1[15],FM1[16],FM1[17],FM1[18],FM1[19],FM1[20],FM1[22],FM1[23],FM1[24],FM1[25],FM1[26],FM1[27],FM1[28],FM1[29],FM1[30],FM1[31]
0310 FM1_IOLIST:IOLIST FM1$,FM1[0],FM1[1],FM1[2],FM1[3],FM1[4],FM1[5],FM1[6],FM1[7],FM1[8],FM1[9],FM1[10],FM1[11],FM1[12],FM1[13],FM1[14],FM1[15],FM1[16],FM1[17],FM1[18],FM1[19],FM1[20],FM1[22],FM1[23],FM1[24],FM1[25],FM1[26],FM1[27],FM1[28],FM1[29],FM1[30],FM1[31]
0320 IC0_IOLIST:IOLIST IC0$,IC0[0],IC0[1],IC0[2],IC0[3],IC0[4],IC0[5],IC0[6],IC0[7],IC0[8],IC0[9],IC0[10],IC0[11],IC0[12],IC0[13],IC0[14],IC0[15],IC0[16],IC0[17],IC0[18],IC0[19],IC0[20],IC0[21],IC0[22],IC0[23],IC0[24],IC0[25],IC0[26],IC0[27],IC0[28],IC0[29],IC0[30],IC0[31],IC0[32]
0340 IC1_IOLIST:IOLIST IC1$,IC1[0],IC1[1],IC1[2],IC1[3],IC1[4],IC1[5],IC1[6],IC1[7],IC1[8],IC1[9],IC1[10],IC1[11],IC1[12],IC1[13]
0500 REM "Open files, IF FILES array is uninitialized or Z[0] <> EC_SERIAL_NUM for this program (close files then reopen, after redim of array)
0505 IF FN%NEA("Z",1)=0 THEN GOTO 0515 ELSE IF Z[0]<>EC_SERIAL_NUM THEN GOTO 0510 ELSE GOTO 0560
0510 FOR TMP=1 TO FN%NEA("FILES",1); IF Z[TMP]<>0 THEN CLOSE (Z[TMP]) END_IF ; NEXT TMP; REM "Close all open files in the FILES array
0515 DIM Z[NUM(X3$(60,3))]; Z[0]=EC_SERIAL_NUM; REM "Dim array and set the serial number
0520 FILE_NAME$="FM1",INDEX=2; GOSUB 9750
0521 FILE_NAME$="IC0",INDEX=3; GOSUB 9750
0522 FILE_NAME$="IC1",INDEX=4; GOSUB 9750
0529 REM "Open or Create and open output file in correct DIRectory
0530 Z[1]=UNT; IF Z[1]=14 THEN OPEN (14)FID(0); GOTO 0530; REM "Skip slot 14
0532 CWDIR STP(EC_PARM$(55,40),1)
0534 OPEN LOCK (Z[1],OPT="TEXT",ERR=0535)STP(EC0$(36,30),1); GOTO 0550
0536 SERIAL STP(EC0$(36,30),1),10,0
0538 OPEN LOCK (Z[1],OPT="TEXT")STP(EC0$(36,30),1)
0540 TMP$=FN%XFD$(Z[1],0); IF DEC(TMP$(38,4))>0 THEN READ (Z[1],IND=DEC(TMP$(38,4))-1,ERR=0541)
0550 CWDIR CURR_DIR$
1000 REM "Prepare data to write out here and put into OUT$
1005 FM1_FOUND=0,IC0_FOUND=0; DIM FM1[31],IC0[32]
1010 FIND (Z[2],KEY=EC1$(5),DOM=1011)IOL=FM1_IOLIST; FM1_FOUND=1; GOTO 1020
1011 FIND (Z[3],KEY=EC1$(5),DOM=1012)IOL=IC0_IOLIST; IC0_FOUND=1; GOTO 1020
1012 REM "Didn't find record!! flag error
1013 RET$="Missing data for key "+EC1$(5); GOTO 7500
1025 IF FM1_FOUND THEN CALL "ZZDISP","AX",FM1$(1,10),"A/R",X3$,CUST_CODE$,"",0,0,X4$; IF LEN(CUST_CODE$)<10 THEN PROD_KEY$=CUST_CODE$+"-"+FM1$(11,10) ELSE PROD_KEY$=FM1$(1,20) END_IF ; MAN_CODE$=FM1$(1,20),PROD_DESC$=STP(FM1$(42,40),1) ELSE PROD_KEY$=IC0$(11,10),MAN_CODE$=IC0$(1,20),PROD_DESC$=STP(IC0$(21,40),1)
1026 IF STP(EC_PARM$(199,10),3," ")="" THEN VENDOR$="TOPFORM" ELSE VENDOR$=STP(EC_PARM$(199,10),1)
1027 IF LEN(PROD_DESC$)>30 THEN PROD_DESC$=PROD_DESC$(1,30)
1030 OUT$="VITM"+FIELD_SEP$+VENDOR$+FIELD_SEP$+PROD_KEY$+FIELD_SEP$+MAN_CODE$; REM "Default Vendor code,formatted item code (cust_code + item code or only 10 char item code),manufacturer code is real code
1032 IF FM1_FOUND THEN PRICE=FM1[3],COST=FM1[1]*FM1[4]/FM1[2] ELSE PRICE=IC0[0],COST=IC0[18]*IC0[14]/IC0[15]
1039 OUT$=OUT$+FIELD_SEP$+STR(COST:"0.00")+FIELD_SEP$+STR(PRICE:"0.00"); REM "Cost (calculted into price u/m), price
1040 OUT$=OUT$+FIELD_SEP$+""+FIELD_SEP$+"0.00"; REM "Vendor UPC blank, trans cost set to 0
1045 GOSUB 7600; OUT$=OUT$+FIELD_SEP$+STR(QTY_ON_HAND); REM "Quantity on hand
1048 OUT$=OUT$+FIELD_SEP$+STR(PACK)+FIELD_SEP$+STR(PACK); REM "Carton pack = quantity minimum and quantity multiple (carton pack set in 7600's)
1049 DATE_IN$=X3$(21,6); CALL "ZZDISP","DX",DATE_IN$,"",X3$,DATE$,"",0,0,X4$
1050 OUT$=OUT$+FIELD_SEP$+DATE$+FIELD_SEP$+"TOPFORM"; REM "Last updated on, last updated by
1055 OUT$=OUT$+FIELD_SEP$+"N"+FIELD_SEP$+"0"; REM "Disposition code set to None, real time available set to NO (0)
1059 IF QTY_ON_HAND>0 THEN AVAIL_CODE$="Available",AVAIL_DESC$="In stock" ELSE AVAIL_CODE$="Unavailable",AVAIL_DESC$="Backordered"
1060 OUT$=OUT$+FIELD_SEP$+AVAIL_CODE$+FIELD_SEP$+"0"+FIELD_SEP$+AVAIL_DESC$; REM "Available code, available lead time (set to 0), Available descrption
1065 OUT$=OUT$+FIELD_SEP$+DATE$; REM "Availablity last updated on
1080 IF OUT$<>"" THEN WRITE RECORD (Z[1])OUT$+LINE_SEP$
1095 GOTO 9900
7500 REM "Exit as if an error happened
7501 REM "Expecting that RET$ is already set before here
7505 EXIT 69
7600 REM "Calculate QTY_ON_HAND from ic1 records
7605 QTY_ON_HAND=0
7607 IF FM1_FOUND THEN IF FM1[0]<>0 THEN PACK=FM1[0] ELSE PACK=FM1[4] END_IF ELSE IF IC0[19]=0 THEN PACK=IC0[14] ELSE PACK=IC0[19]
7610 READ (Z[4],KEY=MAN_CODE$,DOM=7611)
7615 DIM IC1$(53),IC1[13]; READ (Z[4],END=7635)IOL=IC1_IOLIST; IF IC1$(1,20)<>MAN_CODE$ THEN GOTO 7635
7620 QTY_ON_HAND=QTY_ON_HAND+IC1[3]+IC1[4]-IC1[5]+IC1[6]-IC1[7]+IC1[8]
7630 GOTO 7615
7640 QTY_ON_HAND=QTY_ON_HAND/PACK
7641 IF QTY_ON_HAND<0 THEN QTY_ON_HAND=0
7645 RETURN 
9000 REM "Error
9005 CWDIR CURR_DIR$
9010 RET$="ERROR # "+STR(ERR)+" @ LINE "+STR(TCB(5))+" IN "+X0$
9020 EXIT ERR
9750 REM "FILES - Use instead of ZZFLES
9755 Z[INDEX]=UNT; IF Z[INDEX]=14 THEN OPEN (14)FID(0); GOTO 9755; REM "Skip slot 14
9760 IF LEN(FILE_NAME$)<>3 THEN GOTO 9780
9765 OPEN (Z[INDEX],ERR=9780)FILE_NAME$+X3$(9,3); GOTO 9781
9780 OPEN (Z[INDEX])FILE_NAME$
9790 RETURN 
9900 REM "END PROGRAM
9905 CWDIR CURR_DIR$
9930 SETERR 9940; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
