0010 REM "Rebuild Excluded Sales in AR9<GB2UT0>
0020 SETESC 9300; SETERR 9000
0035 REM "4.4 - 01/07/00 - 15.24 - kmc - SSP# 120526
0040 REM "Copyright 1999 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="GB2UT0",X1$="Rebuild Excluded Sales Information"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST FS1$,FS1[0],FS1[1],FS1[2],FS1[3],FS1[4],FS1[5],FS1[6],FS1[7],FS1[8],FS1[9],FS1[10],FS1[11],FS1[12],FS1[13]
0320 IOLIST B$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O FT3...  02O AR9...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 READ (Z[13],KEY="STATA/R")S9$
0590 GOSUB 6000
0600 REM "Get Fiscal year to process
0605 DIM A$(4); K9$=X3$(9,3)+"G/LYE",X$="Enter Fiscal Year to process or |F4 to leave"
0610 CALL "ZZENTR","ZXUX",A{ALL},A$,X4$,X3$,26,8,1,4,C0,"",X$,K9$,"GL2MQA00","GLYE","",""; IF ABS(C0)=4 THEN GOTO 9900 ELSE IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 0610,0611
0615 FIND (Z[13],KEY=K9$+A$(1,4),DOM=0610)GLYE$
0617 CALL "ZZDISP","DX",GLYE$((NUM(GLYE$(13,2))-1)*6+21,6),"",X3$,LAST$,"",0,0,X4$
0618 PRINT @(33,8),STP(STR(NUM(GLYE$(13,2))),2)+" periods ending on "+LAST$
0620 GOSUB 8100
0640 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0800 REM "Set FIRST_DAY and LAST_DAY to compare to see what is in this year, they will be sql date types
0805 FIRST_DAY=FN%DTN(FND$(GLYE$(15,6)),"MM/DD/YYYY")+1; REM "1 day after end of prev year
0810 LAST_DAY=FN%DTN(FND$(GLYE$((NUM(GLYE$(13,2))-1)*6+21,6)),"MM/DD/YYYY")
0900 REM "Clear existing entries
0904 PRINT @(20,17),"Clearing existing records for fiscal year "+A$(1,4),
0905 READ (Z[2],KEY="",DOM=0906)
0910 K2$=KEY(Z[2],END=0945)
0915 C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
0920 IF K2$(11,4)=A$(1,4) AND K2$(15,1)="E" THEN REMOVE (Z[2],KEY=K2$,DOM=0910); GOTO 0910 ELSE READ (Z[2]); GOTO 0910
0945 PRINT @(0,17),'CL',
0950 PRINT @(20,17),"Building records from repeat orders",
0955 READ (Z[1],KEY="",DOM=0956)
1000 REM "BEGIN MAIN PROCESS
1010 DIM FS1$(350),FS1[13]; READ (Z[1],END=5000)IOL=0310
1020 C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
1025 IF FS1$(193,1)<>"Y" THEN GOTO 1010; REM "Only process records to be excluded
1030 REM "For excluded orders, check order date to put into fiscal year, it is not exact, but a rough approx.
1035 ORDER_DATE=FN%DTN(FND$(FS1$(16,6)),"MM/DD/YYYY")
1040 IF ORDER_DATE<FIRST_DAY OR ORDER_DATE>LAST_DAY THEN GOTO 1010
1045 PERIOD=INT((POS(FS1$(16,6)<=GLYE$(21),6)+5)/6); REM "Calculate period from orderdate, and list of period end dates
1050 Q0$="E",Q0=FS1[6],Q1$=FS1$(6,10)+A$(1,4),Q1=PERIOD,F0=Z[2]
1055 GOSUB 7000
1600 GOTO 1000
5000 REM "EOJ
5005 PRINT @(0,17),'CL',
5010 C=T+T2; GOSUB 8150
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This CSI utility program will clear the excluded sales for the year given,",@(0,5),"and will calculate excluded sales based on the flag found in the ",@(0,6),"Repeat order information for that year. NOTE: The order date will be ",@(0,7),"used to determine the FY/AP, not the invoice FY/AP.",
6025 PRINT @(2,8),"Fiscal year to process:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6400 REM "Redisplay screen
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6410 GOSUB 6000
6415 PRINT @(26,8),A$(1,4),
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
7000 REM "Update stats q0$ in period q1 with data q0, key (less Q0$)=Q1$,S9$ is correct stat record
7005 IF Q0=0 THEN GOTO 7090
7010 DIM K[14]; P9$=Q1$+Q0$
7020 GOSUB 7100
7040 K[Q1]=K[Q1]+Q0
7060 GOSUB 7150
7090 RETURN 
7100 REM "Read stats (Packed/Unpacked)
7105 Q=POS(Q0$=S9$(49),17); IF Q=0 THEN EXITTO 7090 ELSE M0$=S9$(Q+48,17)
7110 CALL "ZZPACK",X3$,"E",M0$(3,2),"",0,0,K{ALL},F0,Q0$,Q1$,S9$
7135 GOTO 7190
7150 REM "Write stats
7160 CALL "ZZPACK",X3$,"W",M0$(3,2),"",0,0,K{ALL},F0,Q0$,Q1$,S9$
7190 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F$
8112 CALL "ZZINFO",Z[2],T9,X3$,T2,T0,K,B,D,S0,S1,F$
8115 PRINT @(0,11),"There are "+STR(T+T2)+" records to process"
8129 REM "Set T0, we make sure T0 is > 1, because later on we MOD and look for avalue of 1. IF T0 is 1, then nothing would get reported. We look for a result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T0'th record to get the first report
8130 T0=INT((T+T2)*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T+T2,C
8195 RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 0610,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
