0010 REM "GB Order Tracking Interface <GB2OTS>
0020 SETESC 9300; SETERR 9000
0035 REM "4.4 - 06/15/99 - 18.46 - rds - SSP# 113946
0040 REM "Copyright 1999 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "ORDER$ is FS1,SHIP_DATE$ is "N" from Order Entry and is shipped date coming from Billing, LOC$= Location to put in- sb 2 long
0051 REM "OPTION$, 'D' means delete
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,ORDER$,SHIP_DATE$,COMM$,LOC$,OPTION$
0100 SETERR 9000
0110 X0$="GB2OTS",X1$="Gold Bond Order Tracking Interface"
0165 DEF FND$(Z9$)=STR((ASC(Z9$(1,1))-65)*10+NUM(Z9$(2,1)))+Z9$(7-NUM(X3$(48,1))*2,2)+Z9$(NUM(X3$(48,1))*2+1,2)
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0300 REM "IOLISTS
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O OT0... 02O OT1... 03O OT2... 04O OT3... 05O FS2...  13O ZZPARM "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0550 READ (Z[13],KEY=X3$(9,3)+"G/B",DOM=9900)GB$
1000 REM "BEGIN MAIN PROCESS
1005 REM "Build Fields
1010 CUST_NUM$=ORDER$(6,10),CUST_PO$=ORDER$(28,15),ORDER_NUM$=ORDER$(118,8)
1015 TRANS_DATE$=X3$(21,6),OPER_CODE$=X3$(40,3)
1020 CURR_TIME$=STR(INT(TIM):"00")+STR(FPT(TIM)*100:"00")
1040 READ (Z[5],KEY=ORDER_NUM$+"001",DOM=1060)ORDER_LINE$
1050 ITEM_NUM$=ORDER_LINE$(161,10)+ORDER_LINE$(19,10)
1060 REM 
1080 IF POS("D"=OPTION$)>0 THEN GOSUB 2000; GOTO 1995
1085 IF SHIP_DATE$<>"N" THEN GOTO 1250
1090 OPER_CODE$=ORDER$(169,3),SHIP_DATE$=""
1095 COMM$="Order entered"; IF ORDER$(195,1)="Y" THEN COMM$="Gold Rush "+COMM$
1097 IF ORDER$(198,1)="Y" THEN COMM$=COMM$+"-No Overruns"
1100 REM "Create OT0 Header Record - Only for new order
1104 OLD_KEY$=""; REM "ssp#113946
1105 READ (Z[1],KEY=ORDER_NUM$,DOM=1106)OT0$,SEQ; IF LOC$<>OT0$(73,2) THEN COMM$="Tracking location changed by Order Entry"; GOTO 1195 ELSE IF ITEM_NUM$<>OT0$(53,20) THEN COMM$="Item change old="+STP(OT0$(53,20),2),OT0$(53,20)=ITEM_NUM$; GOTO 1200 ELSE IF CUST_PO$<>OT0$(19,15) THEN OLD_KEY$=OT0$(9,10)+OT0$(19,15)+OT0$(1,8),COMM$="Cust PO changed old="+OT0$(19,15),OT0$(19,15)=CUST_PO$; GOTO 1200 ELSE GOTO 9900; REM "ssp#113946
1110 DIM OT0$(100); SEQ=0
1120 OT0$(1,8)=ORDER_NUM$
1130 OT0$(9,10)=CUST_NUM$
1140 OT0$(19,15)=CUST_PO$
1150 OT0$(34,6)=ORDER$(16,6); REM "Order date
1160 OT0$(40,6)=ORDER$(55,6); REM "Estimated ship date
1170 OT0$(46,1)=ORDER$(226,1); REM "Sure date?
1180 OT0$(47,6)=SHIP_DATE$
1190 OT0$(53,20)=ITEM_NUM$
1195 OT0$(73,2)=LOC$; REM "Save in header so we can see if changed in O/E
1200 WRITE (Z[1],KEY=OT0$(1,8))OT0$,SEQ
1210 REM "write sort records
1215 IF OLD_KEY$<>"" THEN REMOVE (Z[3],KEY=OLD_KEY$,DOM=1216); REM "ssp#113946
1220 WRITE (Z[3],KEY=CUST_NUM$+CUST_PO$+ORDER_NUM$)
1230 WRITE (Z[4],KEY=CUST_NUM$+ORDER_NUM$)
1250 REM "Create OT1 Tracking Record - For Order Entered
1260 DIM OT1$(100)
1270 OT1$(1,8)=ORDER_NUM$
1280 EXTRACT (Z[1],KEY=OT1$(1,8),DOM=1995)OT0$,SEQ; REM "Get Seq number"
1340 OT1$(13,6)=TRANS_DATE$; REM "Tracking date
1350 OT1$(19,4)=CURR_TIME$
1360 OT1$(23,4)=LOC$; OT1$(23,4)=STR(NUM(OT1$(23,4),ERR=1361):"0000")
1370 OT1$(27,40)=COMM$
1380 OT1$(67,3)=OPER_CODE$; REM "Operator code
1390 SEQ=SEQ+1; OT1$(9,4)=STR(SEQ:"0000"); WRITE (Z[2],KEY=OT1$(1,12),DOM=1390)OT1$
1400 REM "Update SEQ to OT0
1410 OT0$(47,6)=SHIP_DATE$
1420 WRITE (Z[1],KEY=OT0$(1,8))OT0$,SEQ
1995 GOTO 9900
2000 REM "Delete OT0 related records
2020 REMOVE (Z[1],KEY=ORDER_NUM$,DOM=2021)
2040 REMOVE (Z[3],KEY=CUST_NUM$+CUST_PO$+ORDER_NUM$,DOM=2041)
2060 REMOVE (Z[4],KEY=CUST_NUM$+ORDER_NUM$,DOM=2061)
2100 REM "Delete OT1 Transaction records
2120 READ (Z[2],KEY=ORDER_NUM$,DOM=2121)
2140 KEY$=KEY(Z[2],END=2190)
2150 IF KEY$(1,8)<>ORDER_NUM$ THEN GOTO 2190
2160 REMOVE (Z[2],KEY=KEY$,DOM=2161)
2170 GOTO 2140
2190 RETURN 
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
