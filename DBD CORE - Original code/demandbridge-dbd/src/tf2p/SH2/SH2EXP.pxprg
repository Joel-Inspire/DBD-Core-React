0010 REM "Export Shipping Data <SH2EXP>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 09/29/20 - 9.867222 - dmm - SSP# 307304
0037 REM "307304-DBSPT-98755;RECVG_DEPT to SH9, SHIPPING_SH9.ini, SH2EXP     
0040 REM "Copyright 2020 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0070 REM "OPTIONS contains F means Force export, ignoring PO3$(2,1) flag
0089 REM OPEN (14) "ZZPARM"; READ (14,KEY=FID(0)+DIM(6)) X3$,*,X4$; LET ORDER$="00008097", OPTIONS$="F"; CLOSE (14) ; GOTO 00100
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,ORDER$,OPTIONS$,RET$
0100 SETERR 9000
0101 REM "If DA Directory not defined then exit
0105 REM CLOSE (14); OPEN (14,ERR=9930)"DA"; CLOSE (14)
0110 X0$="SH2EXP",X1$="Export Shipping Data"
0111 REM "Lengths of header fields
0120 H_LEN$=" 8 43030303030 9 3201510122010"
0121 REM "Lengths of detail fields
0122 D_LEN$=" 8 4 312202040 4 6 7 7 710"
0300 REM "IOLISTS
0310 FS1_IOLIST:IOLIST FS1$,FS1[0],FS1[1],FS1[2],FS1[3],FS1[4],FS1[5],FS1[6],FS1[7],FS1[8],FS1[9],FS1[10],FS1[11],FS1[12],FS1[13]
0320 FS2_IOLIST:IOLIST FS2$,FS2[0],FS2[1],FS2[2],FS2[3],FS2[4],FS2[5],FS2[6],FS2[7],FS2[8],FS2[9],FS2[10],FS2[11],FS2[12],FS2[13],FS2[14],FS2[15],FS2[16],FS2[17],FS2[18],FS2[19],FS2[20],FS2[21],FS2[22],FS2[23],FS2[24],FS2[25],FS2[26],FS2[27],FS2[28],FS2[29]
0321 IOLIST A15$,A15[0],A15[1],A15[2],A15[3],A15[4],A15[5],A15[6],A15[7],A15[8],A15[9],A15[10],A15[11],A15[12],A15[13],A15[14],A15[15],A15[16],A15[17],A15[18],A15[19],A15[20],A15[21],A15[22],A15[23],A15[24],A15[25],A15[26],A15[27],A15[28],A15[29]
0330 PO3_IOLIST:IOLIST PO3$,PO3[0],PO3[1],PO3[2],PO3[3]
0335 IOLIST GENERAL$(1),GENERAL[0],GENERAL[1],GENERAL[2],GENERAL[3]
0370 ICF_IOLIST:IOLIST ICF$,ICF[0],ICF[1],ICF[2],ICF[3],ICF[4],ICF[5],ICF[6],ICF[7],ICF[8],ICF[9],ICF[10],ICF[11],ICF[12]
0380 IOLIST AR1$,AR1[0],AR1[1],AR1[2],AR1[3],AR1[4],AR1[5],AR1[6],AR1[7],AR1[8],AR1[9],AR1[10],AR1[11],AR1[12],AR1[13],AR1[14],AR1[15]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O FS1... 02O FS2... 03O PO3... 04O SH3... 05O SH4... 06O SH2... 07O ICF... 08O EM1... 09O FM0... 10O SH5... 11O SH8...  12O AR1...  13O ZZPARM  14O EML... 15OSFS2...  16O SH9...  17O SHB...  18O FV1...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0525 DIM SH_PARM$(205); READ (Z[13],KEY=X3$(9,3)+"S/H",DOM=9900)SH_PARM$(1)
0530 IF POS("U"=SH_PARM$(189,12))=0 AND STP(SH_PARM$(67,12),3," ")="" THEN GOTO 9900
0600 REM "Check parameters - ssp# 106782
0609 REM "if order$ is 11 long then it is order # + 1 line to do
0610 IF LEN(ORDER$)=11 THEN LINE$=ORDER$(9,3),ORDER$=ORDER$(1,8) ELSE LINE$=""
0611 REM "If S in options then set flag to change sequence # later
0620 IF POS("S"=OPTIONS$)<>0 THEN SET_SEQ=1 ELSE SET_SEQ=0
0625 REM "if Q in options and remaining chars are number, then set flag we have qty to use and set qty to put into line(s)
0627 USE_Q=0,Q_POS=POS("Q"=OPTIONS$); IF Q_POS=0 THEN GOTO 0630 ELSE IF FN%NMV(OPTIONS$(Q_POS+1))=0 THEN GOTO 0630
0629 USE_Q=1,SET_QTY=NUM(OPTIONS$(Q_POS+1))
1000 REM "BEGIN MAIN PROCESS
1005 DIM FS1[13]; REM "ssp #106782
1010 IF SET_SEQ THEN EXTRACT (Z[1],KEY=ORDER$,DOM=9900)IOL=FS1_IOLIST; IF LEN(FS1$)<350 THEN FS1$=FS1$+DIM(350-LEN(FS1$)) END_IF ; FS1$(227,2)=STR(NUM(FS1$(227,2))+1:"00"); WRITE (Z[1],KEY=ORDER$)IOL=FS1_IOLIST ELSE READ (Z[1],KEY=ORDER$,DOM=9900)IOL=FS1_IOLIST; REM "ssp#106782 if set seq then inc seq # else just read it
1013 IF LEN(FS1$)<350 THEN FS1$=FS1$+DIM(350-LEN(FS1$))
1015 DIM GENERAL$(299),GENERAL[3]; FIND (Z[3],KEY=ORDER$+DIM(5),DOM=1016)IOL=0335; REM "ssp$107539 read general instructions here to fill in gaps later
1100 REM "Process FS2 Records
1105 DIM FS2[29],PO3[3]
1110 IF LINE$="" THEN READ (Z[2],KEY=ORDER$,DOM=1111) ELSE READ (Z[2],KEY=ORDER$+LINE$,DOM=1190)IOL=FS2_IOLIST; GOTO 1116
1112 GOSUB SET_READONLY ! SSP#226523
1115 READ (Z[2],END=1190)IOL=FS2_IOLIST
1120 IF FS2$(147,8)<>ORDER$ THEN GOTO 1190 ELSE IF LINE$<>"" THEN IF FS2$(147,8)+FS2$(6,3)<>ORDER$+LINE$ THEN GOTO 1190
1121 REM "Skip messages
1124 IF FS2$(155,1)="M" THEN GOTO 1115
1125 IF FS2$(18,1)="Y" THEN READ (Z[3],KEY=ORDER$+" "+FS2$(6,3),DOM=1126) ELSE READ (Z[3],KEY=ORDER$,DOM=1126)
1130 PO3_KEY$=KEY(Z[3],END=1170); IF FS2$(18,1)="Y" THEN IF PO3_KEY$(1,12)<>ORDER$+" "+FS2$(6,3) THEN GOTO 1170 END_IF ELSE IF PO3_KEY$<>ORDER$+DIM(5) THEN GOTO 1170
1132 DIM PO3[3]; EXTRACT (Z[3],KEY=PO3_KEY$,DOM=1130)IOL=PO3_IOLIST
1134 ! GOSUB SET_READONLY ! SSP191723 ,SSP#226523
1135 DIM FM0$(618); FIND (Z[9],KEY="C"+FS1$(6,10)+PO3$(12,4),DOM=1136)FM0$; REM " SSP# 139226
1136 DIM EM1$(128); FIND (Z[8],KEY=FS1$(6,10)+"     ",DOM=1137)EM1$
1137 DIM SH5$(40); FIND (Z[10],KEY=FS1$(6,10)+PO3$(12,4)+PO3$(150,1),DOM=1138)SH5$
1138 ! GOSUB CLEAR_READONLY ! SSP191723,SSP#226523
1139 REM "If PO3 flag says we haven't exported yet, OR OPTIONS$ has an F in it to force export OR SH_PARM$(189,10)(Scale options) has an O for overwrite mode (meaning we send the whole order each time) then write it out.
1140 IF PO3$(2,1)<>"Y" OR POS("O"=SH_PARM$(189,10))<>0 OR POS("U"=SH_PARM$(189,10))<>0 OR POS("F"=OPTIONS$)<>0 THEN GOSUB WRITE_RECORD; REM "added U option check ssp#112054
1145 IF FS2$(18,1)="Y" THEN PO3$(2,1)="Y"; REM "Set exported to y on special ships
1150 WRITE (Z[3],KEY=PO3_KEY$)IOL=PO3_IOLIST
1165 GOTO 1130
1185 IF LINE$="" THEN GOTO 1112 ! SS#226523
1190 IF POS("G"=OPTIONS$)=0 THEN EXTRACT (Z[3],KEY=ORDER$+DIM(5),DOM=1191)IOL=PO3_IOLIST; PO3$(2,1)="Y"; WRITE (Z[3],KEY=ORDER$+DIM(5))IOL=PO3_IOLIST; REM "Mark general shipping as done
1191 GOSUB CLEAR_READONLY ! SSP#226523
1900 REM "Before exit,try to start ghost task, handle error if already running
1910 ! START 1,ERR=1911,"SH2GST",SH_PARM$(187,2)
1920 START "SH2GST",MID(SH_PARM$,187,2),ERR=1921
1995 GOTO 9900
2000 WRITE_RECORD:REM "Write out detail record, write header record if it is not written yet
2005 WROTE_DETAIL=0; REM "ssp#106782 - we will not write a header unless we write a detail line first.
2010 DIM DETAIL$(299)
2012 DETAIL$(1,8)=ORDER$,DETAIL$(9,4)=PO3$(12,4),DETAIL$(13,3)=FS2$(6,3); REM "Order#, shipto, line number
2013 SHIP_POINT$=PO3$(10,1); REM "Save for use with U option
2016 CALL "ZZDISP","AX",FS2$(161,10),"A/R",X3$,X$,"",0,0,X4$; REM "Formatted Customer #
2017 IF FS2$(155,1)="C" THEN DETAIL$(28,20)=FS2$(161,10)+FS2$(19,10),DETAIL$(48,20)=X$+"-"+FS2$(19,10) ELSE DETAIL$(38,10)=FS2$(19,10),DETAIL$(48,20)=FS2$(19,10); REM "Formcode, formatted formcode
2020 DETAIL$(68,40)=FS2$(50,40),DETAIL$(108,4)=FS2$(124,4); REM "Descption, U/M
2022 DETAIL$(112,6)=STR(FS2[5]); IF USE_Q THEN DETAIL$(118,7)=STR(SET_QTY) ELSE DETAIL$(118,7)=STR(FS2[0]); REM "Qty per U/M, quantity in eaches; ssp#106782
2023 IF FS2[5]=0 THEN QTY=1 ELSE QTY=FS2[0]/FS2[5]
2024 DETAIL$(125,7)=STR(QTY),DETAIL$(132,7)=FS2$(198,7); REM "Quantiy in unit ofmeasure, weight
2025 DETAIL$(139,10)=FS1$(118,8)+FS1$(227,2); REM "ssp#106782 - Sequenced order number
2030 DETAIL$(149,15)=FS2$(161,10); DETAIL$(164,40)=FS2$(50,40); REM "Customer SKU Number and Customer SKU Description; changed by 106782
2050 IF FS2$(2,1)="Y" AND POS("L"=SH_PARM$(189,10))<1 THEN GOSUB LOT_RECORDS ELSE GOSUB WRITE_DETAIL; REM " THE 'L' OPTION IS TO NOT PROCESS LOT INFORMATION
2054 IF STP(PO3$(12,4),3," ")="" THEN TMPS$=PO3$(7,4) ELSE TMPS$=PO3$(12,4); REM "If manual ship to use line# + seq, else use shipping code
2055 IF WROTE_DETAIL THEN IF POS(TMPS$=HEADERS_SENT$)=0 THEN GOSUB WRITE_HEADER; REM "If Detail written, then write header once
2095 WRITE_RECORD_END:RETURN 
2100 WRITE_HEADER:REM "Write out header record
2105 DIM HEADER$(587),SH8$(511),SH9$(978)
2109 SHIP_POINT$=PO3$(10,1); REM "Save for use with U option
2110 HEADER$(1,8)=ORDER$,HEADER$(9,4)=PO3$(12,4),HEADER$(13,30)=PO3$(225,35); REM "Order#, shipto, name
2111 IF STP(PO3$(16,30),3," ")="" THEN HEADER$(43,30)=GENERAL$(16,30) ELSE HEADER$(43,30)=PO3$(16,30); REM "Attention  - ssp#107539
2112 HEADER$(73,30)=PO3$(46,30),HEADER$(103,30)=PO3$(76,30); REM "addr1, addr2
2113 IF STP(HEADER$(73,30),3," ")="" THEN HEADER$(73,30)=HEADER$(103,30),HEADER$(103,30)=""; REM " ssp#107539 - if 1st line blank move second addr line up
2114 IF POS("A"=SH_PARM$(189,10))<>0 THEN HEADER$(133,30)=STP(PO3$(106,16),1) ELSE HEADER$(133,30)=STP(PO3$(106,16),1)+", "+PO3$(122,2) END_IF ; HEADER$(163,9)=PO3$(124,9); REM "City, state, zip - IF Alternate option then don't export state
2115 IF POS("A"=SH_PARM$(189,10))<>0 THEN HEADER$(172,3)=PO3$(135,3),HEADER$(175,35)=PO3$(175,35); HEADER$=HEADER$(1,210); GOTO 2150; REM "If alternate option add 1st 3 chars of inside delivery (for the country code), then Mark for field. THEN SKIP rest of stuff after shorting HEADER$ down to 210 bytes
2116 HEADER$(195,15)=FS1$(28,15),HEADER$(210,10)=FS1$(6,10); REM "Customer P/O,TopForm Customer #
2118 CALL "ZZDISP","AX",FS1$(6,10),"A/R",X3$,X$,"",0,0,X4$; HEADER$(220,12)=X$; IF POS(X3$(9,3)="288",3)>0 THEN HEADER$(220,12)=FS1$(6,10)+DIM(2); REM "Formatted Customer #, SSP259072, 288 wants CUST_CODE_FORMATTED to be the unformatted customer code. 
2120 DIM SH2$(7); SH2$(1)=PO3$(150,1),HEADER$(175,20)=PO3$(210,15); READ (Z[6],KEY=PO3$(150,1),DOM=2121) ! SSP#270191
2121 SH2$=KEY(Z[6],END=2123); IF SH2$(1,1)<>PO3$(150,1) THEN SH2$(1)=PO3$(150,1),HEADER$(175,20)=PO3$(210,15); GOTO 2123 ! SSP#218505!SSP#270191
2122 HEADER$(175,20)=SH2$(2); REM "Scale shipvia code
2130 HEADER$(232,3)=EM1$(121,1),HEADER$(235,14)=FM0$(200,10),HEADER$(262,20)=FM0$(160,20); REM "ASN Flag and Phone Number, and Attn field (changed by 106782 (attn 249->262)
2131 HEADER$(282,20)=SH5$(16,20); REM "Shipper ID Number; changed by ssp# 106782 269-> 282
2132 IF LEN(PO3$)>=382 THEN IF STP(MID(PO3$,363,20),3)>"" THEN HEADER$(282,20)=PO3$(363,20) ! WO190633, shipper id passed into PO3 from WebEC shipper_id tag
2133 HEADER$(252,10)=FS1$(118,8)+FS1$(227,2); REM "ssp#106782 Sequenced order #
2150 HEADER_KEY$=HEADER$(1,12)
2151 IF HEADER_KEY$(9,4)="M   " OR HEADER_KEY$(9,4)="    " THEN HEADER_KEY$(9,4)=PO3$(7,4); REM "If manual shipto the use line#, shippoint to identify
2165 GOSUB 7500; REM "CREATE SH8 RECORD
2170 IF POS("U"=SH_PARM$(189,10))<>0 THEN GOSUB 2400; REM "Handle comma delimited header record
2175 WRITE (Z[4],KEY=HEADER_KEY$)HEADER_KEY$+HEADER$
2180 HEADERS_SENT$=HEADERS_SENT$+HEADER_KEY$(9,4)
2195 WRITE_HEADER_END:RETURN 
2200 LOT_RECORDS:REM "iF lotted item then write out detail records for each lot. Assume DETAIL$ already filled out, so Clear quantity bucket before each lot. Detail record will have quantity based on unit of measure in the line.
2205 LOT_KEY$=DETAIL$(28,20)+FS2$(10,4); DIM ICF$(269),ICF[12]; READ (Z[7],KEY=LOT_KEY$,DOM=2206)
2206 DETAIL_UM=1; DETAIL_UM=NUM(DETAIL$(112,6),ERR=2207); IF DETAIL_UM=0 THEN DETAIL_UM=1; REM "For later use, default to 1 if not number or if 0
2210 READ (Z[7],END=2280)IOL=ICF_IOLIST; IF ICF$(1,LEN(LOT_KEY$))<>LOT_KEY$ THEN GOTO 2280 ELSE IF ICF$(110,11)<>DETAIL$(1,8)+DETAIL$(13,3) THEN GOTO 2210
2213 IF STP(ICF$(125,4),3," ")="" THEN LOT_QTY=ICF[3]*ICF[4] ELSE LOT_QTY=ICF[2]*ICF[3]*ICF[4]
2215 DETAIL$(118,7)=""; REM "Clear current qty
2220 DETAIL$(125,7)=STR(LOT_QTY/DETAIL_UM); REM "qUANTITY IN U/MS
2225 DETAIL$(118,7)=STR(LOT_QTY); REM "Quantity in eaches
2230 DETAIL$(16,12)=ICF$(21,12); REM "Lot ID
2270 TMP_DETAIL$=DETAIL$; GOSUB WRITE_DETAIL; DETAIL$=TMP_DETAIL$; REM "SSP129190, save off DETAIL$ so when we come back from WRITE_DETAIL we can set it back, if U option it changes the length of the record
2275 GOTO 2210
2295 LOT_RECORDS_END:RETURN 
2300 WRITE_DETAIL:REM "Given DETAIL$ write out to SH4 with sequence #
2305 DETAIL_KEY$=DETAIL$(1,15)+"000"
2307 IF POS("U"=SH_PARM$(189,10))<>0 THEN GOSUB 2350; REM "iF comma delimited, convert Detail$
2310 WRITE (Z[5],KEY=DETAIL_KEY$,DOM=2311)DETAIL_KEY$+DETAIL$; GOTO 2340
2315 DETAIL_KEY$(16,3)=STR(NUM(DETAIL_KEY$(16,3))+1:"000"); GOTO 2310
2340 WROTE_DETAIL=1
2345 WRITE_DETAIL_END:RETURN 
2350 REM "given DETAIL$ and d_len$, convert DETAIL$
2355 DTMP$=DETAIL$,DETAIL$="",DPOS=1
2360 FOR II=1 TO LEN(D_LEN$)-1 STEP 2
2365 DLEN=NUM(D_LEN$(II,2)); IF II=1 THEN DETAIL$=DETAIL$+STP(DTMP$(DPOS,DLEN),1)+SHIP_POINT$+"," ELSE DETAIL$=DETAIL$+STP(DTMP$(DPOS,DLEN),1)+"," END_IF ; DPOS=DPOS+DLEN
2370 NEXT II
2395 RETURN 
2400 REM "Given HEADER$ and h_len$, convert HEADER$ to delimited record
2405 HTMP$=HEADER$,HEADER$="",HPOS=1
2407 IF X3$(9,3)="522" AND SHIP_POINT$=" " THEN SHIP_POINT$="0"; REM "SSP119088
2410 FOR II=1 TO LEN(H_LEN$)-1 STEP 2
2415 HLEN=NUM(H_LEN$(II,2)); IF II=1 THEN HEADER$=HEADER$+QUO+STP(HTMP$(HPOS,HLEN),1)+SHIP_POINT$+QUO+"," ELSE IF II=13 THEN GOSUB 2450 ELSE HEADER$=HEADER$+QUO+STP(HTMP$(HPOS,HLEN),1)+QUO+"," END_IF END_IF ; HPOS=HPOS+HLEN
2420 NEXT II
2445 RETURN 
2450 REM "For UPS file split the city and state into separate fields
2455 TMP$=HTMP$(HPOS,HLEN)
2460 P=POS(","=TMP$); IF P=0 THEN CTMP$=TMP$,STMP$="" ELSE CTMP$=TMP$(1,P-1),STMP$=TMP$(P+2)
2465 HEADER$=HEADER$+QUO+STP(CTMP$,1)+QUO+","+QUO+STP(STMP$,1)+QUO+","
2490 RETURN 
3000 REM " READ AR1 AND SET INFORMATION SSP 179676
3010 GOSUB SET_READONLY; DIM AR1[15]; READ (Z[12],KEY=SH8$(181,10),DOM=*NEXT)IOL=0380; GOSUB CLEAR_READONLY; GOTO 3012 ! SSP185573, add READONLY logic
3011 GOSUB CLEAR_READONLY; GOTO 3199
3015 IF POS(SH8$(342,1)="35",1)=0 THEN GOTO 3071 ! SSP#246343
3017 SH8$(465,10)=AR1$(1,10); REM "CUSTOMER ACT
3020 SH8$(343,35)=AR1$(11,35); REM "CUSTOMER NAME
3030 SH8$(378,30)=AR1$(56,30); REM ADDRESS LINE 1
3040 SH8$(408,30)=AR1$(86,30); REM " ADDRESS LINE 2
3050 SH8$(438,16)=AR1$(116,16); REM " CITY
3060 SH8$(454,2)=AR1$(132,2); REM " STATE
3070 SH8$(456,9)=AR1$(134,9); REM "ZIP
3199 RETURN 
5000 REM "EOJ
7400 SET_READONLY:
7410 SET_PARAM 'XI'
7420 SET_READONLY_END:RETURN 
7430 CLEAR_READONLY:
7435 SET_PARAM -'XI'
7440 CLEAR_READONLY_END:RETURN 
7500 REM "WO124784, create SH8 records, needed for DIRect ships for Worldship
7505 SH8$(342,1)=MID(SH5$,36,1); REM " SSP 179676
7506 IF LEN(PO3$)>=362 THEN IF MID(PO3$,362,1)<>DIM(1) THEN SH8$(342,1)=PO3$(362,1) ! WO190633, method payment passed into PO3 from WebEC bill_m.
7507 GOSUB SET_UPS_BILL_METHOD ! [SSP-208740]
7510 SH8$(1,8)=ORDER$,SH8$(9,1)=SHIP_POINT$,SH9$(9,3)=PO3$(7,3) ! SSP 194599
7515 SH8$(10,4)=HEADER$(9,4); REM "Ship to code or if M or blank then PO3(7,4) which is line number plus shipping point
7520 SH8$(14,35)=PO3$(225,35); REM "Name
7525 SH8$(49,30)=HEADER$(43,30),SH8$(79,30)=HEADER$(73,30),SH8$(109,30)=HEADER$(103,30); REM "Attention, address line 1, address line 2!ssp#260394
7530 SH8$(139,16)=PO3$(106,16),SH8$(155,2)=PO3$(122,2),SH8$(157,9)=PO3$(124,9); REM "City, state, zip
7532 IF POS("A"=SH_PARM$(189,10))<>0 THEN GOTO 7590; REM "A option HEADER$ is only 210 long, see 2115
7535 SH8$(166,15)=HEADER$(195,15),SH8$(181,10)=HEADER$(210,10),SH8$(191,12)=HEADER$(220,12); REM "Customer PO, customer code, formatted customer code
7536 GOSUB 3000; REM SPP 179676
7540 SH8$(203,20)=HEADER$(175,20),SH8$(223,3)=HEADER$(232,3),SH8$(226,14)=HEADER$(235,14); REM "Ship via, ASN flag, phone number
7545 SH8$(240,20)=HEADER$(262,20),SH8$(260,20)=HEADER$(282,20),SH8$(280,10)=HEADER$(252,10); REM "Attention, shipper ID, sequenced order number
7550 IF LEN(PO3$)>299 THEN SH8$(290,40)=PO3$(299,40); REM " Email address - SSP# 139226
7555 IF SH_PARM$(205,1)="Y" AND STP(SH8$(290,40),3)>"" THEN SH8$(330,1)="Y",SH8$(331,1)="Y",SH8$(332,10)="e-mail" ! WO179708, set fields for UPS to send tracking info email.  SSP182158, make sure there is an email address.
7570 SH9$(1,8)=SH8$(1,8),SH9$(12,466)=SH8$(9,466) ! SSP 194599
7575 SH9$(478,3)=SH8$(475,3) ! SSP239535, from SSP208740/SSP208752/SSP226108/SSP225748
7580 GOSUB SCALE_COUNTRY_CODE ! WO239405-check SHB for mapped country code
7585 IF MID(AR1$,499,4)<>DIM(4) THEN GOSUB GET_FV1_DATA ! WO248159
7586 SH9$(621,35)=PO3$(175,35) ! WO279632, mark for
7587 IF MID(AR1$,540,1)<>DIM(1) AND MID(AR1$,540,1)<>"N" THEN SH9$(656,1)=MID(AR1$,540,1) ! WO284914, signature required S=Signature Required M=Mature Signature Required
7588 SH9$(657,2)=PO3$(383,2),SH9$(659,15)=PO3$(135,15) ! SSP307210-DBD-61 Country, SSP307296-DBSPT-96495
7589 IF SH9$(13,4)<>DIM(4) THEN GOSUB GET_RECVG_DEPT ! WO307304-DBSPT-98755-DBD-136
7590 WRITE (Z[11],KEY=SH8$(1,9))SH8$
7591 CALL "UPDSH9;INSERT",Z[16],0,SH9$(1,12),"","","","",SH9$,SH9{ALL},OK; IF OK=0 THEN BUSY=0; CALL "UPDSH9;UPDATE",Z[16],SH9$,SH9{ALL},BUSY
7592 GOSUB UPDATE_TEXT
7595 RETURN 
7700 UPDATE_TEXT:
7702 XP=PRM('XI')
7705 READ (Z[14],KNO=1,KEY="SH",DOM=*NEXT)
7710 KYZ14$=KEY(Z[14],END=*RETURN); IF KYZ14$(1,2)<>"SH" THEN GOTO *RETURN
7715 SET_PARAM ('XI'); READ (Z[14],KEY=KYZ14$)A14$; SET_PARAM 'XI'=XP
7720 DIM A15[29]; SH8_FIL=0,UOMCNT=0,TOTCNT=0
7725 READ (Z[15],KEY=SH8$(1,8),DOM=*NEXT)
7730 KYZ15$=KEY(Z[15],END=7750); IF KYZ15$(1,8)<>SH8$(1,8) THEN GOTO 7750
7732 SET_PARAM ('XI'); READ (Z[15],KEY=KYZ15$)IOL=0321; SET_PARAM 'XI'=XP
7735 IF A15$(9,1)<>" " OR A15$(10,4)<>A14$(8,4) THEN GOTO 7730
7737 IF MID(A15$,124,4)=A14$(257,4) THEN UOMCNT=UOMCNT+A15[0] ELSE TOTCNT=TOTCNT+A15[0]
7740 SH8_FIL=UNT
7745 GOTO 7730
7750 IF SH8_FIL>0 THEN {
7760 BASE$=PTH(Z[14]); IF POS("\"=BASE$)<>0 THEN SEP$="\" ELSE SEP$="/"
7765 SH8_FIL$=CVS(A14$(137,60),2)+SEP$+SH8$(1,8)+"-"+SH8$(10,4)
7770 ERASE SH8_FIL$,ERR=*NEXT
7775 SERIAL SH8_FIL$; SH8_FIL=UNT; OPEN LOCK (SH8_FIL,OPT="TEXT")SH8_FIL$
7780 PRINT (SH8_FIL)SH8$+STR(UOMCNT:"########0")+STR(TOTCNT:"#########0"); CLOSE (SH8_FIL)
7782  }
7785 GOTO 7710
7790 RETURN 
9000 REM "ERROR PROCESSING
9001 IF POS("F"=OPTIONS$)>0 THEN GOTO 9900; REM "If doing F options and we get an error just get out
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9945 ESCAPE 
9950 RUN "ZMENU"
9999 END 
12000 SET_UPS_BILL_METHOD:! [SSP-208740]-added routine
12002 REM "This routine will set the UPS Bill Method.
12004 REM "     A = UPS Shipper (SHP)
12006 REM "     B = UPS Receiver (REC)
12008 REM "     C = UPS Third Party (TP )
12010 REM "     D = UPS Consignee Billed (CB )
12020 UPS_BILL_MTHD$="   "
12030 METHOD_PAY$=SH8$(342,1)
12040 IF METHOD_PAY$="A" THEN UPS_BILL_MTHD$="SHP"; REM SSP 226108 225748
12050 IF METHOD_PAY$="B" THEN UPS_BILL_MTHD$="REC"; REM SSP 226108 225748
12060 IF METHOD_PAY$="C" THEN UPS_BILL_MTHD$="TP "; REM SSP 226108 225748
12070 IF METHOD_PAY$="D" THEN UPS_BILL_MTHD$="CB "; REM SSPO 226108 225748
12080 SH8$(475,30)=PAD(UPS_BILL_MTHD$,30)
12195 RETURN 
12199 ! 
12200 SCALE_COUNTRY_CODE:! WO239405, check SHB for state record mapped to country code, if found put into SH9
12210 DIM SHB$(127); READ (Z[17],KEY=SH9$(158,2),DOM=*RETURN)SHB$(1)
12220 SH9$(481,20)=SHB$(3,20)
12240 RETURN 
12245 ! 
12300 GET_FV1_DATA:! WO248159 if customer defaults set to use FV1 data for package references, check for record then find appropriate fields.
12310 DIM FV1$(818); FIND (Z[18],KEY=SH9$(1,8),DOM=*RETURN)FV1$(1)
12320 PKG_1:IF MID(AR1$,499,2)=DIM(2) THEN GOTO PKG_2 ELSE PKG_1=NUM(AR1$(499,2),ERR=PKG_2)
12325 PKG_1$=FV1$(19+(PKG_1-1)*60,60); IF STP(PKG_1$)>"" THEN SH9$(501,60)=PKG_1$
12330 PKG_2:IF MID(AR1$,501,2)=DIM(2) THEN GOTO *RETURN ELSE PKG_2=NUM(AR1$(501,2),ERR=*RETURN)
12335 PKG_2$=FV1$(19+(PKG_2-1)*60,60); IF STP(PKG_2$)>"" THEN SH9$(561,60)=PKG_2$
12390 RETURN 
12395 ! 
12400 GET_RECVG_DEPT:! WO307304-DBSPT-98755-DBD-136
12410 DIM FM02$(618); FIND (Z[9],KEY="C"+FS1$(6,10)+SH9$(13,4),DOM=*RETURN)FM02$(1)
12415 SH9$(674,20)=FM02$(382,20) ! Receiving Department
12445 RETURN 
12449 ! 
56000 REM "189158-Problem with FTP of shipment files to NJ warehouse          
56010 REM "191723-Error 0-OF2UAA-8630, trying to read shipto record
56015 REM "190633-Two WebEC tags, bill_m and shipper_id, data to pass into
56020 REM "190633 - Added MIDs for possible short PO3 records
56030 REM "194599-Issue with the addresses on VP business cards. Pulling      
56032 REM "199335-SH9 file entries for certain orders contain different       
56035 REM "208740-Wants to explore the possibility of adding a shipper # to a 
56036 REM "226108-see ssp 208740 - third party shippers - defaulted to       
56037 REM "226523-File Lock - Printer in use or someting like that            
56038 REM "239405-Create new file to hold map between state and country name, SSP239535-fix for SH9 UPS_BILL_METHOD
56039 REM "246343-Additional work related to Harvey method of payment codes   
56040 REM "248159-Add 2 Package Reference fields to SH9, 2 fields to customer 
56042 REM "258502-SH9 CUST_CODE_FORMATTED, almost always unformatted customer.
56044 REM "259072-SH9 FORMATTED_CUST_CODE, 288 wants to be unformatted cust.  
56046 REM "279632-Add Mark For from general shipping to SH9 and shipping.ini  
56048 REM "284914-Add new Signature Required field to customer and SH9 files. 
56050 REM "307210-International shipping address - add country code           
56052 REM "307296-DBSPT-96495;INSIDE_DELIVERY to SH9, SHIPPING_SH9.ini, SH2EXP
56054 REM "307304-DBSPT-98755-DBD-136;RECVG_DEPT to SH9, SHIPPING_SH9.ini, SH2EXP     
