0010 REM "Bill of Materials I/C Update <BM2ICU>
0020 SETESC 9300; SETERR 9000
0035 REM "4.4 - 12/08/99 - 12.55 - tma - SSP# 119328
0040 REM "Copyright 1999 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "COMMAND is 1 for commit raw materials, -1 for decommit raw materials (Finish goods handled by FM2ODZ), 2 = update raw materials during invoice update, and update finished goods recv'd, 3=determine if BOM item and figure total cost for invoice entry
0051 REM "COMMAND = 4 is update raw material usage stats, RESULT$ is string data needed in format: 1,4=WHSE,5,4=FY,9,1=STAT_TYPE,10,2=PERIOD saved off as IN$ so RESULT$ can be cleared
0052 REM "COMMAND = 5 is create detail transactions in IC8 for BOM items and receipt in IC8 for finished good. RESULT$ is IC8 record filled out in AR2DUB,UPDATE_QTY is qty, RESULT is amount, stored in IN
0055 REM "ORDER_LINE$ is 1,8=order #, 9,3= line #
0056 REM "UPDATE_QTY is used by command 2
0057 REM "RESULT$ and RESULT are used to return info
0058 REM "  command 3 RESULT$="Y" if BOM item, RESULT=Total cost based on BOM
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,COMMAND,ORDER_LINE$,UPDATE_QTY,RESULT$,RESULT
0100 SETERR 9000
0110 X0$="BM2ICU",X1$="Bill of Materials I/C Update"
0150 IN$=RESULT$,IN=RESULT
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29]
0320 IOLIST BM1$,BM1[0],BM1[1],BM1[2]
0330 IOLIST J$,J[0],J[1],J[2],J[3],J[4],J[5],J[6],J[7],J[8],J[9],J[10],J[11],J[12],J[13],J[14],J[15],J[16],J[17],J[18],J[19],J[20]
0331 IOLIST J$,J[0],STR(J[1]),STR(J[2]),J[3],J[4],J[5],J[6],J[7],J[8],J[9],J[10],J[11],J[12],J[13],J[14],J[15],J[16],J[17],J[18],J[19],J[20]
0340 IOLIST IC0$,IC0[0],IC0[1],IC0[2],IC0[3],IC0[4],IC0[5],IC0[6],IC0[7],IC0[8],IC0[9],IC0[10],IC0[11],IC0[12],IC0[13],IC0[14],IC0[15],IC0[16],IC0[17],IC0[18],IC0[19],IC0[20],IC0[21],IC0[22],IC0[23],IC0[24],IC0[25],IC0[26],IC0[27],IC0[28],IC0[29],IC0[30],IC0[31],IC0[32],IC0[33],IC0[34],IC0[35],IC0[36],IC0[37],IC0[38],IC0[39],IC0[40]
0360 IOLIST IC8$,IC8[0],IC8[1]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O FS2...  02O BM1...  03O IC1... 04O IC0... 05O IC9... 06O IC8... 13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 IF COMMAND=4 THEN READ (Z[13],KEY="STATI/C")S9$
0550 DIM M6[1]; CALL "IC2PRE",X3$,Z[13],M6$,M6{ALL},"",0,"",0
0585 RT_PARM$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,RT_PARM$
0600 REM "
1000 REM "BEGIN MAIN PROCESS
1005 DIM A$(40),A[29]; LINES_DONE=0,RESULT$="",RESULT=0; READ (Z[1],KEY=ORDER_LINE$,DOM=9900)IOL=0310
1007 IF A$(9,1)<>" " OR POS(A$(155,1)="IC")=0 THEN GOTO 9900
1020 READ (Z[2],KEY=ORDER_LINE$,DOM=1021)
1025 DIM BM1$(35),BM1[2]; READ (Z[2],END=1090)IOL=0320; IF BM1$(1,11)<>ORDER_LINE$ THEN GOTO 1090
1030 LINES_DONE=LINES_DONE+1
1035 IF COMMAND>=2 AND COMMAND<=5 THEN IF UPDATE_QTY=0 OR A[10]=0 THEN SHIPPED=0 ELSE SHIPPED=INT(BM1[0]*UPDATE_QTY/A[10])
1040 ON ABS(COMMAND) GOSUB 1096,2000,2100,2200,2250,2300,1096
1085 GOTO 1025
1090 REM "End of bill of materials
1091 IF COMMAND=2 AND LINES_DONE>0 THEN GOSUB 2150
1092 IF COMMAND=3 AND LINES_DONE>0 THEN RESULT$="Y"; REM "BOM type item done
1093 IF COMMAND=5 AND LINES_DONE>0 THEN GOSUB 2350
1095 GOTO 9900
1096 RETURN ; REM "Dummy gosub for wrong command case
2000 REM "Process a raw material record
2005 DIM J$(58),J[20]
2009 J$(1)=BM1$(15,20)+A$(10,4)
2010 EXTRACT (Z[3],KEY=J$(1,24),DOM=2011)IOL=0330
2020 J[7]=J[7]+SGN(COMMAND)*BM1[0]
2030 WRITE (Z[3],KEY=J$(1,24))IOL=0331
2035 IF RT_PARM$>"" THEN IF RT_PARM$(7,1)="H" THEN CUST$=J$(1,10); CALL "RT2WOC",ERR=2036,X3$,X4$,CUST$,"IC1...","U",J$(1,24)
2095 RETURN 
2100 REM "Process a raw material record for shipping
2101 REM "The BM1[0] is based on A[10], and should be adjusted to UPDATE_QTY, ie change BM1[0] based on ratio of ordered (A[10]) to shipped [UPDATE_QTY]
2105 DIM J$(58),J[20]
2109 J$(1)=BM1$(15,20)+A$(10,4)
2110 EXTRACT (Z[3],KEY=J$(1,24),DOM=2111)IOL=0330
2115 REM "Shipped calculated at 1035
2120 J[5]=J[5]+SHIPPED
2130 WRITE (Z[3],KEY=J$(1,24))IOL=0331
2135 IF RT_PARM$>"" THEN IF RT_PARM$(7,1)="H" THEN CUST$=J$(1,10); CALL "RT2WOC",ERR=2136,X3$,X4$,CUST$,"IC1...","U",J$(1,24)
2145 RETURN 
2150 REM "If doing command 2 and at least 1 BOM line found, then the item on the line is a finished good, so update the received by the amount shipped
2155 DIM J$(58),J[20]
2159 J$(1)=A$(161,10)+A$(19,10)+A$(10,4)
2160 EXTRACT (Z[3],KEY=J$(1,24),DOM=2161)IOL=0330
2170 J[4]=J[4]+UPDATE_QTY; REM "Adjust received
2180 WRITE (Z[3],KEY=J$(1,24))IOL=0331
2185 IF RT_PARM$>"" THEN IF RT_PARM$(7,1)="H" THEN CUST$=J$(1,10); CALL "RT2WOC",ERR=2186,X3$,X4$,CUST$,"IC1...","U",J$(1,24)
2195 RETURN 
2200 REM "Figure cost based on bill of materials, used same values as will be used in command 2 for BOM shipped quantities
2201 REM "The BM1[0] is based on A[10], and should be adjusted to UPDATE_QTY, ie change BM1[0] based on ratio of ordered (A[10]) to shipped [UPDATE_QTY]
2205 DIM J$(58),J[20]
2209 J$(1)=BM1$(15,20)+A$(10,4)
2210 FIND (Z[3],KEY=J$(1,24),DOM=2211)IOL=0330
2215 REM "SHIPPED calculated at 1035
2220 DIM IC0$(187),IC0[40]; FIND (Z[4],KEY=J$(1,20),DOM=2221)IOL=0340
2230 CALL "FM2EXT",Z[13],0,IC0$(124,4),IC0[15],SHIPPED,J[2],Z,M6[1]; RESULT=RESULT+Z
2245 RETURN 
2250 REM "Update the IC9 stats for BOM materials (U type for now, but under control of calling program
2255 F0=Z[5]
2260 Q1$=BM1$(15,20)+IN$(1,8),Q0$=IN$(9,1),Q1=NUM(IN$(10,2)),Q0=SHIPPED
2275 GOSUB 7000
2295 RETURN 
2300 REM "Given IN$ as IC8 record, change Item#,Type,qty & amount and update foreach BOM item. Use type of Issue since not really selling
2305 DIM IC8[1]
2310 IC8$=IN$,IC8$(1,20)=BM1$(15,20),IC8$(39,1)="I",IC8$(37,2)="00"
2312 DIM J$(58),J[20]; J$(1)=BM1$(15,20)+A$(10,4)
2313 FIND (Z[3],KEY=J$(1,24),DOM=2314)IOL=0330
2315 DIM IC0$(187),IC0[40]; FIND (Z[4],KEY=J$(1,20),DOM=2316)IOL=0340
2317 SHIPPED_COST=0; CALL "FM2EXT",Z[13],0,IC0$(124,4),IC0[15],SHIPPED,J[2],SHIPPED_COST,M6[1]
2320 IC8[0]=SHIPPED,IC8[1]=SHIPPED_COST
2330 WRITE (Z[6],KEY=IC8$(1,38),DOM=2331)IOL=0360; GOTO 2333
2331 IC8_KEY$=IC8$(1,38); CALL "IC2LCC",X3$,X4$,Z[6],37,2,IC8_LAST_ONE$,IC8_KEY$; IC8$(1,38)=IC8_KEY$; GOTO 2330
2345 RETURN 
2350 REM "If here, then we have a finished good, so update a phantom receipt tomatch, most of the record is done, qty in UPDATE_QTY, amount in IN, just make it a receipt, REVERSE QTYS
2355 DIM IC8[1]
2360 IC8$=IN$,IC8$(39,1)="R",IC8$(37,2)="00",IC8[0]=-UPDATE_QTY,IC8[1]=-IN
2380 WRITE (Z[6],KEY=IC8$(1,38),DOM=2381)IOL=0360; GOTO 2383
2381 IC8_KEY$=IC8$(1,38); CALL "IC2LCC",X3$,X4$,Z[6],37,2,IC8_LAST_ONE$,IC8_KEY$; IC8$(1,38)=IC8_KEY$; GOTO 2380
2395 RETURN 
7000 REM "Update stats q0$ in period q1 with data q0
7005 IF Q0=0 THEN GOTO 7090
7010 DIM K[14]; P9$=Q1$+Q0$
7020 GOSUB 7100
7040 K[Q1]=K[Q1]+Q0
7060 GOSUB 7150
7090 RETURN 
7100 REM "Read stats (Packed/Unpacked)
7105 Q=POS(Q0$=S9$(49),17); IF Q=0 THEN EXITTO 7090 ELSE M0$=S9$(Q+48,17)
7110 CALL "ZZPACK",X3$,"E",M0$(3,2),"",0,0,K{ALL},F0,Q0$,Q1$,S9$
7135 GOTO 7190
7150 REM "Write stats
7160 CALL "ZZPACK",X3$,"W",M0$(3,2),"",0,0,K{ALL},F0,Q0$,Q1$,S9$
7190 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
