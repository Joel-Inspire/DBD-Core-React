0010 REM "Get Multiple Finder's Fee as Defaults <FS2MFFF>
0011 REM " This program is called during order entry to set vendor finder fee defaults for an order line. 
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 10/04/18 - 14.281111 - dmm - SSP# 303394
0037 REM "303394-DBA DBSPT-47844: Purchase Order transmission failed on order
0040 REM "Copyright 2018 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,F$,F,ORDKEY$
0095 ! RDKEY$="00025664001"
0096 ! $="0000100000001000010000000010A    "
0100 SETERR 9000
0110 X0$="FS2DAF",X1$="Retrieve Finder's Fee"
0120 DIM S$(40)
0135 C9=-1
0200 REM "
0300 REM "IOLISTS
0310 IOL_ATI:IOLIST ATI$,ATI[0],ATI[1],ATI[2]
0320 IOL_AR1:IOLIST AR1$
0330 IOL_FV4:IOLIST FV4$,FV4[0],FV4[1],FV4[2]
0340 IOL_FV5:IOLIST FV5$,FV5[0],FV5[1],FV5[2]
0360 IOLIST F$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O FM1...  02O FM0...  04O FV4...  05O ATI...  06O FV5...  07O FMP... 13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0540 FIND (Z[13],KEY=X3$(9,3)+"F/M")P4$
0570 REM "F0$=Sold-to customer, F1$=Product code, F2$=Ship from customer, F3$= Item code, F$ will be returned with vendor code + Finder's fee type and F=ff %
0580 F0$=F$(1,10),F1$=F$(11,3),F2$=F$(14,10),F3$=F$(24,10),X=0,VP$=F1$; F$="",F=0
0590 IF STP(F0$,2)="" AND STP(F2$,2)="" THEN GOTO 9900
0600 PROD_CODE$=""; READ (Z[7],KEY="T"+F1$,DOM=*NEXT)PROD_CODE$; REM SSP 199045
1000 REM "Check if Vendor and Product code has a std rate - Vendor is in the customer default record ; if not, then proceed as normal - For now, only do this for ABP"
1005 GOSUB CHECK_DFLT_FV5
1015 DP_FOUND=0
1018 IF MID(PROD_CODE$,47,1)="Y" THEN GOTO 9900 ! SSP 299045 SSP 276772 Do not create default finder fee records in FV5 if product parm "Disallow FinderS Fees  set to Y SSP 289210
1020 IF P4$(184,1)<>"Y" THEN GOTO 2000
1040 GOSUB SET_READONLY; READ (Z[7],KEY="D"+F0$,DOM=*NEXT)A$; GOSUB CLEAR_READONLY; GOTO 1042 ! SSP303394
1041 GOSUB CLEAR_READONLY; GOTO 2000 ! SSP303394, got DOM on line 1040 was DOM=2000
1050 TP_KEY$=F0$; GOSUB DFLT_VEND_PROD
1060 IF NOT(DP_FOUND) THEN GOTO 2000
1070 GOSUB CUST_FEE
1075 GOTO 9900
1080 READ (Z[7],KEY="W"+A$(13,10)+F1$,DOM=1081)B$,B; GOTO 1200
1100 IF F1$<>"   " THEN F1$="   "; GOTO 1080; REM "See if entry set up for all product codes for this vendor "
1120 GOTO 2000
1200 REM "Found it
1220 F$=B$(2,10)+B$(15,1),F=B
1240 GOTO 9900
1299 ! 
2000 REM "Finder's fees for C-types-1st Cust item, 2nd-Ship From Cust, 3rd Cust
2002 IF MID(PROD_CODE$,47,1)="Y" THEN GOTO 9900 ! SSP 299045 SSP 276772 Do not create default finder fee records in FV5 if product parm "Disallow FinderS Fees  set to Y
2005 GOSUB SET_READONLY ! SSP236926
2010 FIND (Z[1],KEY=F0$+F3$,DOM=2011)X$,*,*,*,*,*,*,*,X; TP_KEY$=F0$+F3$; GOSUB ITEM_FEE; IF MFEE_FOUND THEN GOTO 2032
2015 FIND (Z[1],KEY=F2$+F3$,DOM=2025)X$,*,*,*,*,*,*,*,X
2020 TP_KEY$=F2$+F3$; GOSUB ITEM_FEE; IF MFEE_FOUND THEN GOTO 2032
2025 FIND (Z[7],KEY="D"+F0$,DOM=2032)X$,X; REM SSP 199045
2030 TP_KEY$=F0$; GOSUB CUST_FEE
2035 GOSUB CLEAR_READONLY ! SSP236926
2040 GOTO 9900
2099 ! 
4000 ITEM_FEE:
4005 MFEE_FOUND=0; DIM FV4$(135),FV4[3]
4010 FF_KEY$=TP_KEY$
4020 READ (Z[4],KEY=FF_KEY$,DOM=*NEXT)
4025 NEXT_ITEM_FEE:
4030 FF_KEY$=KEY(Z[4],END=END_ITEM_FEE); READ (Z[4],KEY=FF_KEY$)IOL=0330
4040 IF MID(FF_KEY$,1,20)<>TP_KEY$ THEN GOTO END_ITEM_FEE
4042 FIND_FEE_TYPE$=MID(FV4$,24,1),VENDOR$=MID(FV4$,25,10)
4045 FIND_FEE_RATE=FV4[0]
4050 GOSUB ADD_NEW_FV5
4055 MFEE_FOUND=1
4060 GOTO NEXT_ITEM_FEE
4080 END_ITEM_FEE:
4090 RETURN 
4099 ! 
4200 CUST_FEE:
4210 MFEE_FOUND=0; DIM AT1$(125),ATI[3]
4220 FF_KEY$=TP_KEY$
4230 READ (Z[5],KEY=FF_KEY$,DOM=*NEXT)
4250 NEXT_CUST_FEE:
4260 FF_KEY$=KEY(Z[5],END=END_CUST_FEE); READ (Z[5],KEY=FF_KEY$)IOL=0310
4270 IF MID(FF_KEY$,1,10)<>TP_KEY$ THEN GOTO END_CUST_FEE
4280 ! F VP_DFLT THEN GOSUB 4500; GOTO ADD_FV5
4300 FIND_FEE_TYPE$=ATI$(14,1)
4310 VENDOR$=ATI$(15,10)
4320 FIND_FEE_RATE=ATI[0]
4350 IF DP_FOUND THEN GOSUB UPD_VEND_PROD; GOTO ADD_FV5
4390 ADD_FV5:
4400 GOSUB ADD_NEW_FV5
4410 MFEE_FOUND=1
4420 GOTO NEXT_CUST_FEE
4450 END_CUST_FEE:
4490 RETURN 
4499 ! 
5000 ADD_NEW_FV5:
5010 ! This routine will write the default FV5(FS2) records for the order/line number
5020 DIM FV5$(125),FV5[3]; FF_SEQ=0
5030 FV5$(1,11)=ORDKEY$
5035 FV5$(15,1)=FIND_FEE_TYPE$
5040 FV5$(16,10)=VENDOR$
5050 FV5[0]=FIND_FEE_RATE
5060 ADD_SEQ_NUM:
5065 FF_SEQ=FF_SEQ+1; FF_SEQ$=STR(FF_SEQ:"000")
5070 FV5_KEY$=ORDKEY$+FF_SEQ$
5075 FV5$(12,3)=FF_SEQ$
5080 WRITE (Z[6],KEY=FV5_KEY$,DOM=ADD_SEQ_NUM)IOL=IOL_FV5
5090 RETURN 
5099 ! 
6800 SET_READONLY:! SSP236926
6810 SET_PARAM 'XI'
6820 SET_READONLY_END:RETURN 
6825 ! 
6830 CLEAR_READONLY:
6835 SET_PARAM -'XI'
6840 CLEAR_READONLY_END:RETURN 
6845 ! 
7000 CHECK_DFLT_FV5:
7010 DFLT_EXISTS=0
7020 READ (Z[6],KEY=ORDKEY$,DOM=*NEXT)
7025 NEXT_DFLT:
7030 DFLT_KEY$=KEY(Z[6],END=DFLT_EXISTS_EXIT); READ (Z[6],KEY=DFLT_KEY$)DFFV6$
7040 IF DFLT_KEY$(1,LEN(ORDKEY$))<>ORDKEY$ THEN GOTO DFLT_EXISTS_EXIT
7050 REMOVE (Z[6],KEY=DFLT_KEY$)
7060 GOTO NEXT_DFLT
7090 DFLT_EXISTS_EXIT:
7095 RETURN 
7099 ! 
8910 DEF FNS$(Z9$)=Z9$(1,POS("  "=Z9$+"  ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
10000 DFLT_VEND_PROD:
10010 DP_FOUND=0; DIM AT1$(125),ATI[3]
10020 FF_KEY$=TP_KEY$
10030 READ (Z[5],KEY=FF_KEY$,DOM=*NEXT)
10050 NEXT_VP_FEE:
10060 FF_KEY$=KEY(Z[5],END=END_VP_FEE); READ (Z[5],KEY=FF_KEY$)IOL=0310
10070 IF MID(FF_KEY$,1,10)<>TP_KEY$ THEN GOTO END_VP_FEE
10080 VENDOR$=ATI$(15,10)
10090 GOSUB TEST_VEND_PROD
10100 IF DP_FOUND THEN GOTO END_VP_FEE
10110 GOTO NEXT_VP_FEE
10120 END_VP_FEE:
10190 RETURN 
10199 ! 
12000 TEST_VEND_PROD:
12005 F1$=VP$
12010 READ (Z[7],KEY="W"+VENDOR$+F1$,DOM=*NEXT)B$,B; DP_FOUND=1; GOTO TEST_VP_END
12020 IF F1$<>"   " THEN F1$="   "; GOTO 12010; REM "See if entry set up for all product codes for this vendor "
12050 TEST_VP_END:
12060 RETURN 
12099 ! 
12100 UPD_VEND_PROD:
12105 F1$=VP$
12110 READ (Z[7],KEY="W"+VENDOR$+F1$,DOM=*NEXT)B$,B; FIND_FEE_TYPE$=B$(15,1),FIND_FEE_RATE=B; GOTO UPD_VP_END
12120 IF F1$<>"   " THEN F1$="   "; GOTO 12110; REM "See if entry set up for all product codes for this vendor "
12150 UPD_VP_END:
12160 RETURN 
12199 ! 
56000 REM "276772-Multiple Finder's Fees per Customer/Item/Order Line.        
56002 REM "236926-[DT5] Err in email confirmation - 0 - EC3WS1 - 8247         
56003 REM "276772-Multiple Finder's Fees per Customer/Item/Order Line.        
56004 REM "289210-MultiFinders Fee feature is not looking at the Product Code 
56006 REM "303394-DBA DBSPT-47844: Purchase Order transmission failed on order
