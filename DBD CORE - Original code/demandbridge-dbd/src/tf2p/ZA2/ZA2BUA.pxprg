0010 REM "<ZA2BUA> Create Report Program [Mod for BERT/B-Link]
0030 REM "Portions (C) 1985-1988 Basic Ideas, Inc; Atlanta, GA. USA
0035 REM "5.7 - 02/11/10 - 10.418333 - tma - SSP# 236273
0036 REM "ADDED LINE 1325 TO ALLOW FOR PROG PROTECTION WHEN CUSTOM MODS
0037 REM "236273-BERT report: 6932.046:   Error writing temp sort key        
0040 REM "Copyright 2010 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0310 IOLIST A$,A1$,A2$,A3$
0320 IOLIST B$
0390 IOLIST R$,D0$,R0$,D2$
0400 IOLIST D1$
1000 CLEAR 
1030 SETERR 9000
1040 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
1050 DIM S$(65),B$(120)
1055 X$=$22$
1110 X0$="ZA2BUA",R9$="ZZ2OUT"
1200 REM "FILES
1205 DIM Z[NUM(X3$(60,3))]
1207 Q0$="ZB1"; IF MID(X3$,146,6)="ZA2BBZ" THEN Q0$="ZB4"
1210 Z$="01O ZB3...  02O ZA0...  03O ZA1...  04O ZAF... 05O ZA3... 06O ZA2."+".. 07O ZA5... 08O ZAE... 09O "+Q0$+"... 10O ZB2...  11O ZA4... 13O ZZ"+"PARM "
1220 GOSUB 9750
1280 READ (Z[13],KEY=X3$(9,3)+"ZB2")S0$
1285 D0=NUM(S0$(8,4))
1290 READ (Z[13],KEY="INSTALL")P9$
1310 READ (Z[13],KEY=FID(0)+"BERT")A1$,A9,A8
1320 READ (Z[9],KEY=A1$)IOL=0390
1325 IF MID(R$,24,1)="*" THEN CALL "ZZPROM",".4",X3$,0,"This BERT program is protected and cannot"+" be regenerated.","","",0; GOTO 9900
1330 READ (Z[10],KEY=A1$,DOM=1331)IOL=0400
1340 IF MID(X3$,77,1)<>"P" THEN CALL "ZZDROP"
1345 F9$="ZA2UABZA2BU2"
1350 FOR X=1 TO LEN(F9$) STEP 6; ADDR F9$(X,6); NEXT X
1355 F9$=""
1500 PRINT @(0,6),'CE',@(12),"** Redefining Report Parameters **"
1600 REM "BUILD SEMI-TEMP SORT DATABASE
1610 IF MID(R$,102,1)="S" THEN GOSUB 6700
1805 A0$=""
1810 GOSUB 8200
2000 REM 
2005 T2=7025,F8=7510,F3$=R$(60,20); IF MID(R$,24,1)="D" THEN F3$=F3$+R$(81,20),F2$="",F1$="",F0$=""
2010 W$="110 X1$="+X$+FNS$(R$(135,65))+X$; GOSUB 8800
2020 IF MID(R$,200,65)=MID(S$,1,65) THEN GOTO 2060 ELSE W$="7475 W0$="+X$+FNS$(R$(200,65))+X$
2025 IF MID(R$,265,65)<>MID(S$,1,65) THEN W$=W$+",W1$="+X$+FNS$(R$(265,65))+X$
2030 GOSUB 8800
2060 W$="220W3="+R$(124,3); GOSUB 8800
2070 H0=NUM(R$(124,3)); DIM H0$(4*H0)
2100 REM "SORT SEQUENCE
2110 Q$=R$(60,20); IF MID(R$,102,1)<>" " THEN Q$=R$(103,20); IF MID(R$,102,1)<>"P" THEN GOSUB 7400; GOTO 2200
2120 READ (Z[2],KEY=Q$,DOM=2125)G$; GOTO 2140
2130 CALL "ZZPROM","X2ZA2BUA",X3$,0,"","","",0; ON A GOTO 2200,9900
2140 Q$=G$(61,20); W$="7455V1$="+X$+FNS$(Q$)+X$; GOSUB 8800; V1$=G$(1,20)
2142 G0$=G$
2150 IF MID(R$,103,1)<>" " THEN W$="825V0=2"; GOSUB 8800
2220 GOSUB 4000
2500 REM "PROCESS INDIVIDUAL ELEMENTS
2505 IF A1$="" THEN A1$=A$(30,20)
2510 L0=1520,L4=1910,L5=7000,L1=1,L9=0,L8=0; IF POS(MID(R$,24,1)="FLP")>0 THEN L0=4010
2520 READ (Z[1],KEY=R$(1,23),DOM=2521)
2530 IF A$>"" THEN L2$=A$(70,1)
2532 READ (Z[1],END=2900)IOL=0310; IF MID(A$,1,23)<>MID(R$,1,23) THEN GOTO 2900 ELSE IF A1$="" THEN A1$=A$(30,20)
2535 IF MID(A$,81,6)<>MID(S$,1,6) THEN GOSUB 6100
2540 IF MID(A$,70,1)="H" THEN GOSUB 7100 ELSE IF L6>0 THEN GOSUB 7150
2550 T8$=""; IF L1=NUM(MID(A$,24,3)) OR (MID(R$,80,1)<>"1" AND MID(A$,70,1)<>"H") THEN GOTO 2600
2552 IF MID(R$,24,1)="P" THEN IF MID(A$,70,1)="H" THEN W$=STR(L0)+"GOSUB 7300",L0=L0+5; GOSUB 8800; T8$="W="+A$(24,3)+";"; GOTO 2560
2555 FOR X=L1+1 TO NUM(A$(24,3)); W$=STR(L0)+"GOSUB 7300",L0=L0+5; GOSUB 8800; NEXT X
2560 L1=NUM(A$(24,3)),L8=1
2602 IF MID(R$,24,1)="D" THEN IF L1$<>MID(A$,30,20) THEN GOSUB 6400
2603 REM "2604 IS OLD LINE 2605 AT 3/31/92 JSC
2604 REM LET A9$=A1$;      IF A1$=""         A1$=A$(30,20),A9$=A1$      ELSE         A1$=A1$(LEN(A1$)-19,20)
2605 A9$=A1$; IF A1$="" THEN A1$=A$(30,20),A9$=A1$ ELSE A1$=A1$(LEN(A1$)-19,20),A9$=A1$,A8$=""; IF A2$>"" THEN A8$=A2$(LEN(A2$)-19,20)
2610 PRINT @(12,10),A$(30,20),"  ",A$(50,20),
2611 IF MID(A$,50,1)="@" THEN DIM G$(328); I$=A3$; GOTO 2625
2614 GOTO 2620
2615 Z9$="+("+FNS$(A1$)+"/"+FNS$(A$(50,20))+")"; CALL "ZZPROM","X0ZA2BUA",X3$,A,"","","",0; ON A GOTO 2530
2618 IF MID(A$,50,1)="@" THEN I$=A3$; GOTO 2625
2620 READ (Z[3],KEY=A1$+A$(50,20))G$
2622 IF MID(G$,96,3)<>"   " THEN READ (Z[7],KEY=G$(96,3),DOM=2623)J$; G$(81,6)=J$(60,6),G$(93,3)=J$(66,3),G$(102,4)=J$(69,4)
2623 IF POS("T"=MID(R$,338,15))>0 THEN IF MID(G$,81,1)="N" THEN GOSUB 5500
2624 CALL "ZA2BU2",X3$,V0$,V8,V9,A1$,A$(50,20),F1$,I9$,I$,"",E0$,E1$,E2$
2625 IF I$>"" THEN GOSUB 7000; I0$=Q$ ELSE I0$=I9$
2626 IF MID(A$,70,1)="P" THEN GOSUB 6950
2630 P0=NUM(A$(27,3)),L=NUM(A$(71,2)),N0=POS(G$(81,1)="N"); IF P0<1 THEN P0=1
2640 IF N0>0 THEN GOSUB 5800; IF MID(R$,24,1)<>"T" THEN I0$="STR("+I0$+":"+M1$+")"
2665 IF LEN(A9$)>20 OR (LEN(A1$)=20 AND A9$<>MID(A$,30,20)) THEN GOSUB 5200
2700 REM 
2705 IF MID(R$,80,1)="0" AND MID(A$,70,1)<>"H" THEN GOTO 2800 ELSE IF MID(R$,24,1)="T" THEN GOTO 2720
2710 GOSUB 6600
2714 REM "*****************************************
2716 L0=L0+1; GOSUB 8800; GOTO 2800
2720 W$=STR(L0)+"CALL"+X$+"ZZDISP"+X$+","+X$+FNS$(G$(81,6))+"X"+X$+","+I0$+","+X$+FNS$(G$(99,3))+X$+",X3$,Q$ ,"+X$+X$+",0,0,X4$;"
2725 IF POS(MID(G$,81,1)="SZN")>0 THEN Q$=I0$,W$=STR(L0) ELSE Q$="Q$"
2730 W$=W$+"CALL"+X$+R9$+X$+",X3$,V3$,S9$,"; IF MID(G$,81,1)="N" THEN W$=W$+X$+X$+","+Q$+"," ELSE W$=W$+Q$+",0,"
2735 W$=W$+A$(27,3)+",Y5$"
2760 GOSUB 8800; L0=L0+5
2810 REM "PAGE COLUMN HEADINGS
2820 IF POS(MID(R$,24,1)="FL")=0 AND L8=0 THEN GOSUB 5000
2890 GOTO 2530
2900 REM 
2940 IF M2$>"" THEN W$="140"+M2$(1,LEN(M2$)-1); GOSUB 8800
2945 GOSUB 6500
2950 W$="210T1="+STR(T1); GOSUB 8800
3400 REM "SELECTION CRITERIA
3410 I0=1310; IF D1$="" THEN GOTO 3600 ELSE V6=V8; REM ,V8=-1;         V8=0,V8$=""
3420 FOR X=0 TO 12; Q9$=D1$(25+X*75,75); IF POS(" "<>Q9$)=0 THEN GOTO 3590 ELSE IF MID(Q9$,1,1)="|" THEN GOTO 3590 ELSE Q9$=FNS$(Q9$)+" ",I0$=""
3425 IF MID(Q9$,LEN(Q9$)-1,1)="\" THEN Q8$=Q8$+Q9$(1,LEN(Q9$)-2); GOTO 3590 ELSE Q9$=Q8$+Q9$,Q8$=""
3430 IF MID(Q9$,1,2)="##" THEN I$=Q9$(3); GOSUB 7000; W$=STR(I0)+I8$; GOSUB 8800; I0=I0+5; GOTO 3590
3456 IF MID(Q9$,1,1)="#" THEN W$=Q9$(2); GOSUB 8800; GOTO 3590
3457 IF MID(Q9$,1,1)="$" THEN GOSUB 4300; GOTO 3590
3460 I$=Q9$; GOSUB 7000
3470 I0$=Q$
3510 REM 
3515 V6$=""; IF V7$>"" THEN GOSUB 7750
3520 I0$=STR(I0)+V6$+" IF "+I0$+" THEN GOTO "+STR(I0+1)+" ELSE GOTO 1000"
3530 W$=I0$,I0=I0+5; GOSUB 8800
3590 NEXT X
3595 IF V8$>"" THEN GOSUB 7700
3600 GOSUB 7800
3610 Q$=R$(60,20); IF MID(R$,102,1)<>" " THEN Q$=R$(103,20)
3620 CALL "ZA2BAG",X3$,Q$,D0$,W$
3621 REM "-1 AFTER V0$ WAS V9 12/21/92 JSC
3622 IF V1$>"" THEN K0=-1; CALL "ZA2BU3",X3$,V0$,V8,V9,V1$,F1$,"",K0,0,E0$,E1$,E2$
3625 W$=W$+"U1=Z("+STR(V1)+")"
3630 W$=W$+",U="+STR(K0)
3640 GOSUB 8800
3645 IF MID(R$,24,1)="D" THEN GOSUB 5400
3650 IF V1$="" OR V1$=MID(R$,60,20) THEN GOTO 3700
3655 DIM Q$(20,$FF$); Q$=Q$+F1$(21)
3660 CALL "ZA2BU3",X3$,V0$,V8,V9,R$(60,20),Q$,I0$,K,K0,E0$,E1$,E2$
3670 W$="1120U$="+I0$; GOSUB 8800
3710 IF MID(G0$,81,1)="9" THEN W$="1150 CALL"+X$+"ZAPACK"+X$+",X3$,X4$,Z(1),U$,A$,A(ALL)"; GOSUB 8800
3720 IF T5>0 THEN GOSUB 6900
3721 IF POS("R"=MID(R$,338,15))>0 THEN W$="7001 RETURN"; GOSUB 8800
3726 IF POS("E"=MID(R$,338,15))>0 THEN W$="7202 GOTO 7290"; GOSUB 8800
3727 IF POS("S"=MID(R$,338,15))>0 THEN W$="6039 W8$="+X$+"S"+X$; GOSUB 8800
3728 IF MID(R$,80,1)="0" THEN W$="7001 IF T(T,0)<2 GOTO 7010"; GOSUB 8800
3730 IF P5$>"" THEN W$="898 P5$="+X$+P5$+X$; GOSUB 8800
3980 W$="9999END"; GOSUB 8800
3985 IF M5$>"" THEN W$="270P0$="+X$+M5$+X$+";CALL"+X$+"ZZDAPP"+X$+",X3$,"+X$+"!A"+X$+"+"+"P0$"; GOSUB 8800; W$="9907CALL"+X$+"ZZDAPP"+X$+",X3$,"+X$+"!D"+X$+"+P0$"; GOSUB 8800
3990 RUN "ZA2BUB"
3995 GOTO 9900
4000 REM "PROCESS DETAIL LOOKING FOR FILES
4002 GOSUB 7600
4005 GOSUB 4700
4010 READ (Z[1],KEY=R$(1,23),DOM=4011)
4020 READ (Z[1],END=4190)IOL=0310; IF MID(A$,1,23)<>MID(R$,1,23) THEN GOTO 4190
4025 IF A1$="" THEN A1$=A$(30,20)
4030 FOR X=1 TO LEN(A1$) STEP 20
4035 A$(30,20)=A1$(X,20); IF MID(B$,1,20)<>MID(A$,30,20) THEN GOSUB 4800
4050 NEXT X
4090 GOTO 4020
4200 REM 
4220 GOSUB 4900
4290 RETURN 
4300 REM "$ MACRO TYPE
4305 Q9$=Q9$+S$
4310 FIND (Z[5],KEY=Q9$(1,26),DOM=4380)Q9$
4315 Q9$=Q9$(127)
4320 IF LEN(Q9$)<60 THEN GOTO 4380 ELSE W$=Q9$(1,60),Q9$=Q9$(61)
4325 IF POS(" "<>W$)>0 THEN GOSUB 8800
4340 GOTO 4320
4390 RETURN 
4700 REM "INITIAL FILES
4705 F2=0; DIM A$(120); A$(30,20)=R$(60,20),V1=1; GOSUB 4800
4706 IF MID(R$,24,1)="D" THEN A$(30,20)=R$(81,20); IF POS(" "<>MID(A$,30,20))=0 THEN CALL "ZZPROM","X5ZA2BUA",X3$,0,"","","",0; GOTO 9800 ELSE GOSUB 4800
4710 IF V1$>"" THEN IF V1$<>MID(A$,30,20) THEN A$(30,20)=V1$; GOSUB 4800; V1=INT(POS(V1$=F1$,20)/20)+1
4715 IF V1$="" THEN F2=F2+1,F1$=F1$+S$(1,20)
4745 RETURN 
4750 REM "ADD FILE
4754 REM "CHANGED 11/23/92 TO USE 'S' OPTION ON OPENS
4755 IF POS(MID(A$,30,20)=F1$,20)=0 THEN F1$=F1$+A$(30,20),F2=F2+1; F2$=F2$+STR(F2:"00")+"OS"+F0$+" "
4795 RETURN 
4800 REM "GET DATABASE ENTRY
4810 READ (Z[2],KEY=A$(30,20),DOM=4850)IOL=0320
4815 F0$=FNS$(B$(61,20))
4840 GOSUB 4750
4845 RETURN 
4855 Q$="+("+FNS$(A$(30,20))+")"
4860 CALL "ZZPROM","X4ZA2BUA",X3$,A,Q$,"","",0; ON A GOTO 9900
4895 RETURN 
4900 REM "ADD IOLISTS
4905 I9$="",L2=1205,L3=8010
4910 FOR X=1 TO F2
4915 Q$=F1$((X-1)*20+1,20)
4920 CALL "ZA2BU1",X3$,Q$,X,I0$,I1$,E0$,E1$,E2$
4930 IF I0$>"" THEN I0$=STR(300+X*10)+I0$; W$=I0$; GOSUB 8800; I9$=I9$+I1$
4950 REM IF X>1 THEN IF X<>V1 AND (MID(R$,24,1)<>"D"OR (MID(R$,24,1)="D"AND X<>2))          THENGOSUB 5200
4960 NEXT X
4980 W$="510Z$="+X$+F2$+X$; GOSUB 8800
4985 IF I9$>"" THEN W$="120DIM "+I9$(1,LEN(I9$)-1); GOSUB 8800
4995 RETURN 
5000 REM HANDLE PAGE COLUMN HEADINGS
5002 IF POS("H"=MID(R$,338,15))>0 OR POS("H"=MID(A$,87,16))>0 THEN GOTO 5190
5003 IF LEN(A$)>80 THEN IF POS(" "<>MID(A$,107,90))>0 THEN G$(238,90)=A$(107,90)
5005 Q=POS(S$(1,30)=G$(238,90)+S$(1,30),30); IF Q=1 THEN GOTO 5190 ELSE Q=INT(Q/30)-1,Q0=3-Q
5030 Q1=0; IF MID(G$,81,1)<>"N" THEN IF MID(G$,236,2)<"15" THEN Q1=INT(NUM(G$(236,2))/2)
5040 IF POS(MID(G$,81,1)="N")=0 THEN GOTO 5100
5050 Q1=POS("."=M$),Q1=LEN(M$)-SGN(POS("-"=M$(2))); IF Q1=0 THEN Q1=POS("0"=M$); IF Q1=0 THEN Q1=LEN(M$); IF Q1>0 THEN IF MID(M$,Q1,1)="-" THEN Q1=Q1-1
5110 FOR X=0 TO Q
5120 IF POS(MID(G$,81,1)="N")=0 THEN H0$((X+Q0-1)*H0+P0,L)=G$(238+X*30,30); GOTO 5160
5130 Q0$=FNS$(G$(238+X*30,30)),Q2=LEN(Q0$); H0$((X+Q0-1)*H0+P0+Q1-Q2,Q2)=Q0$
5160 NEXT X
5165 IF POS("U"=MID(R$,338,15))=0 THEN GOTO 5190
5170 DIM Q0$(L,"-"); H0$(3*H0+P0,L)=Q0$
5195 RETURN 
5200 REM "ALTERNATE FILE ACCESS
5205 FOR X=1 TO LEN(A9$) STEP 20; Q$=A9$(X,20)
5210 IF POS(Q$=F3$,20)>0 OR POS(Q$=MID(R$,60,20)+MID(R$,81,20),20)>0 THEN GOTO 5285
5212 READ (Z[2],KEY=Q$)IOL=0320
5214 REM "F0 IS FILE NUMBER "Z("
5215 F0=POS(Q$=F1$,20); IF F0>0 THEN F0=1+INT(F0/20) ELSE F0=1/0
5220 CALL "ZA2BU1",X3$,Q$,F0,"",Q1$,E0$,E1$,E2$
5235 CALL "ZA2BU3",X3$,V0$,V8,V9,Q$,F1$,I2$,0,0,E0$,E1$,E2$
5238 IF MID(B$,81,1)="9" THEN W$=STR(L3)+"DIM "+Q1$(1,LEN(Q1$)-1)+";CALL"+X$+"ZAPACK"+X$+",ERR="+STR(L3+1)+",X3$,X4$,Z("+STR(F0)+"),"+I2$+","+Q1$(1,1)+"$,"+Q1$(1,1)+"(ALL)"; L3=L3+5; GOTO 5270
5240 W$=STR(L3)+"DIM "+Q1$(1,LEN(Q1$)-1)+";"+"READ(Z("+STR(F0)+"),KEY="+I2$+",DOM="+STR(L3+1)+")IOL="+STR(300+10*F0:"00000"),L3=L3+5
5270 GOSUB 8800
5275 F3$=F3$+Q$
5285 NEXT X
5295 RETURN 
5300 REM "ACCESS PRIMARY FILE FROM SORT
5335 Q$=F1$(1,20)
5340 CALL "ZA2BU3",X3$,Q$,S$(1,20)+F1$(21),I0$,0,0,E0$,E1$,E2$
5395 RETURN 
5400 REM "ACCESS DETAIL FROM PRIMARY
5401 REM "ALTERNATE SORTS FOR DETAIL TYPE REPORTS NOT SUPPORTED
5410 RETURN 
5415 Q$=R$(81,20)
5420 CALL "ZA2BU3",X3$,V0$,V8,V9,Q$,F1$,I2$,0,0,E0$,E1$,E2$
5445 RETURN 
5500 REM "'T'RUNCATE NUMERICS TO INTEGERS
5510 Q=NUM(G$(104,1)); IF Q=0 THEN GOTO 5580
5520 G$(104,1)="0"
5590 RETURN 
5800 REM "FIND NUMERIC MASK
5805 Q$=G$(102,4); IF LEN(A$)>80 THEN IF MID(A$,103,4)<>MID(S$,1,4) THEN Q$=A$(103,4)
5810 IF MID(Q$,1,2)>"09" THEN Q$=CHR(ASC("A")+NUM(Q$(1,2))-1)+Q$(3,2) ELSE Q$=Q$(2)
5812 IF MID(A$,70,1)="P" THEN DIM M1$(NUM(A$(71,2)),"#"); M1$(LEN(M1$)-4)=".00%",M1$=X$+M1$+X$; GOTO 5820
5815 Q=POS(Q$=M0$,3); IF Q=0 THEN Q=LEN(M0$)/3,M0$=M0$+Q$; CALL "ZZMASK",Q$,M$; M1$="M"+STR(Q)+"$",M2$=M2$+M1$+"="+X$+M$+X$+"," ELSE Q=INT(Q/3),M1$="M"+STR(Q:"0")+"$"; CALL "ZZMASK",M0$(Q*3+1,3),M$
5820 IF MID(A$,70,1)<>" " THEN GOSUB 6000
5890 RETURN 
5900 REM "CONVERT HEADINGS INTO CODE
5920 FOR X=0 TO 3; Q$=H0$(X*H0+1,H0); IF POS(" "<>Q$)=0 THEN GOTO 5935 ELSE P0=1,W$=STR(L0),L0=L0+10
5925 Q=POS(" "<>Q$); IF Q=0 THEN GOTO 5930 ELSE P0=P0+Q-1,Q$=Q$(Q)
5926 Q=POS("  "=Q$+"  ")-1; W$=W$+"Y5$("+STR(P0)+")="+X$+Q$(1,Q)+X$+",",Q$=Q$(Q+1),P0=P0+Q; GOTO 5925
5930 W$=W$(1,LEN(W$)-1),W$=W$+";GOSUB 7300"; GOSUB 8800
5935 NEXT X
5940 H0$(1)=""
5945 RETURN 
6000 REM "TOTALS
6005 IF POS(MID(A$,24,1)="FL")>0 THEN GOTO 6080
6010 T1=T1+1; IF MID(A$,70,1)<>"P" THEN T8=T8+1
6015 IF MID(A$,70,1)="P" THEN W$=STR(L4+10)+T1$(1,LEN(T1$)-1); GOSUB 8800; T1$="",L4=L4+5
6020 T0$="T(T0,"+STR(T1)+")",T1$=T1$+T0$+"="+T0$+"+"+I0$+","
6025 T9$="STR(T(T,"+STR(T1)+"):"+M1$+")"; IF MID(A$,70,1)="A" THEN T9$="STR(T(T,"+STR(T1)+")/T(T,0):"+M1$+")"
6028 IF MID(A$,70,1)="P" THEN W$=STR(L4+10)+T8$+T1$(1,LEN(T1$)-1); GOSUB 8800; T1$="",L4=L4+5
6030 IF MID(P9$,75,1)="0" THEN W$=STR(T2)+"Y5$("+A$(27,3)+")="+T9$ ELSE W$=STR(T2)+"CALL"+X$+"ZZDISP"+X$+","+X$+FNS$(G$(81,6))+"XS"+X$+","+T9$+","+X$+FNS$(G$(99,3))+X$+",X3$,Y5$,"+X$+X$+","+A$(27,3)+",0,X4$"
6035 T2=T2+1; GOSUB 8800; IF T2=7085 THEN CALL "ZZPROM",".4",X3$,0,"PROBLEM: Too many numeric totals ((LIMIT "+"IS 60)","","",0; GOTO 9900
6040 T2$=T2$+"Y5$("+STR(P0)+","+STR(L)+")=T1$(1,"+STR(L)+"),"
6045 IF MID(A$,70,1)<>"P" THEN T5$=T5$+A$(50,20),T6$=T6$+","+"W(T,"+STR(T8)+")",T7$=T7$+",W("+STR(T8)+")=W("+STR(T8)+")+"+I0$ ELSE T5=T5+1
6090 RETURN 
6100 REM "Printing special effects
6101 IF LEN(A$)=80 THEN RETURN 
6102 W$=A$(81,6)+S$(1,6)
6103 S0=POS(W$=P5$,12); IF S0=0 THEN P5$=P5$+W$; GOTO 6103 ELSE S0=INT(S0/12)
6105 W$=STR(L0)+"Y6$("+A$(27,3)+")=CHR("+STR(S0)+")"+",Y6$("+STR(NUM(A$(27,3))+NUM(A$(71,2))-1)+")="+X$+"*"+X$
6110 GOSUB 8800; L0=L0+5
6190 RETURN 
6400 REM "SOURCE DATABASE CHANGE
6405 REM "L9 = LEVEL (0=HEADER STUFF, 1 = LINES STUFF)
6410 IF L1$="" THEN L1$=A$(30,20); GOTO 6490
6415 GOSUB 6500; L9=1
6420 L1$=A$(30,20)
6440 IF MID(R$,24,1)="D" THEN L0=2220,L3=8105,F3$="",L8=0,L4=2400,L5=2700,T2=2720
6495 RETURN 
6500 REM "FLUSH STUFF AT CHANGE
6505 IF MID(R$,24,1)="T" THEN W$=STR(L0)+"IF POS(MID(V3$,6,1)=$2C22$)>0 OR MID(V3$,3,3)="+$22$+"PRN"+$22$+" THEN LET Y5$ = Y5$(1,LEN(Y5$)-1)",L0=L0+5; GOSUB 8800
6510 IF MID(R$,80,1)="1" THEN IF T2$>"" THEN W$=STR(L5+5:"0000")+T2$(1,LEN(T2$)-1)+";GOSUB 7300"; GOSUB 8800; T2$=""
6520 IF T1$>"" THEN W$=STR(L4+10:"0000")+T1$(1,LEN(T1$)-1); GOSUB 8800; T1$=""
6525 IF MID(R$,80+L9*21,1)="1" THEN W$=STR(L0)+"GOSUB 7300"; GOSUB 8800; L0=L0+5
6526 Q=POS("B"=R$(338,15)); IF Q>0 AND MID(R$,80,1)<>"0" THEN Q=NUM(R$(338+Q,1),ERR=3726); L0=L0+15,W$=STR(L0)+" FOR X=1 TO "+STR(Q)+";GOSUB 7300;NEXT X"; GOSUB 8800
6530 IF L9=0 THEN L0=6110 ELSE IF L9=1 AND MID(R$,24,1)="D" THEN L0=2510
6535 GOSUB 5900
6595 RETURN 
6600 REM "RETURN W$ STRING
6610 IF MID(A$,50,1)="@" THEN W$=STR(L0)+T8$+"Y5$("+A$(27,3)+","+STR(L)+")="+I0$; GOTO 6680
6620 IF MID(G$,61,1)<>"S" THEN W$=STR(L0)+T8$+"CALL"+X$+"ZZDISP"+X$+","+X$+FNS$(G$(81,6))+"XS"+X$+","+I0$+","+X$+FNS$(G$(99,3))+X$+",X3$,Y5$,"+X$+X$+","+A$(27,3)+","+STR(L)+",X4$"
6630 IF MID(G$,81,1)="S" OR (MID(P9$,75,1)="0" AND MID(G$,81,1)="N") THEN W$=STR(L0)+T8$+"Y5$("+A$(27,3)+","+STR(L)+")="+I0$
6695 RETURN 
6700 REM "SEMI-PERM. SORTS
6701 REM "REMOVE OLD DATA
6705 Q$=FNS$(R$(103,20)); IF Q$="" THEN Q$="S"+R$(127,5),I0$=""
6710 REM "REMOVE OLD RECORDS
6712 Q$=Q$+S$(1,20-LEN(Q$)),Q=3
6715 READ (Z[Q],KEY=Q$,DOM=6716)
6720 K$=KEY(Z[Q],END=6745); IF MID(K$,1,20)<>Q$ THEN GOTO 6745
6725 REMOVE (Z[Q],KEY=K$)
6730 GOTO 6720
6745 IF Q=3 THEN Q=11; GOTO 6715
6750 REM "BUILD NEW SORT
6755 FOR X=1 TO LEN(D0$) STEP 30
6760 IF MID(D0$,X,20)=MID(S$,1,20) THEN EXITTO 6795
6765 DIM C$(328)
6770 C$(1,20)=Q$,C$(21,20)=D0$(X,20)
6775 READ (Z[6],KEY=D0$(1,20))F$
6778 IF MID(F$,81,1)="N" THEN F$(70,3)="007"
6780 C$(41,40)=F$(21,40),C$(81,6)=F$(61,6)
6782 C$(87,3)="001",C$(90,3)=STR(I0:"000"),C$(93,3)="0"+D0$(23+X-1,2),I0=I0+NUM(D0$(23+X-1,2))
6784 C$(102,4)=F$(73,4),I0$=I0$+"+"+FNS$(D0$(X,20))
6785 WRITE (Z[3],KEY=C$(1,40))C$
6786 C0$=C$(1,20)+" "+C$(87,6)+C$(21,20); IF POS("F"=MID(C$,82,5))>0 THEN C0$(21,1)="C"
6787 WRITE (Z[11],KEY=C0$)
6790 NEXT X
6805 DIM B$(167); B$(1,20)=Q$,B$(21,40)="Semi-Perm sort: "+R$(1,23)
6810 B$(61,8)=Q$,B$(92)=I0$(2)
6815 B$(81,1)="3",B$(82,2)=STR(I0-1:"00")
6820 WRITE (Z[2],KEY=B$(1,20))B$
6890 RETURN 
6900 REM "TOTALS FILE
6910 Q$="ZA2TOT"; CALL "ZZ2BAC",Q$,0,9999,14,A0$; Q$=""
6915 W$="6650"+T7$(2); GOSUB 8800
6945 RETURN 
6950 REM "% OF TOTALS
6955 P=POS(A$(50,20)=T5$,20); IF P=0 THEN RETURN ELSE P=INT(P/20)+1
6960 I0$="(100*  ("+I0$+")/W("+STR(P)+"))",T8$="IF W("+STR(P)+")<>0"
6995 RETURN 
7000 REM "FORMULA TYPE ELEMENT ENCOUNTERED Q9$
7005 O9$=I$
7010 I8$=""
7020 CALL "ZA2BU4",X3$,V0$,V8,V9,M5$,0,I$,F1$,F9$,F9,F8,0,0,A0$,Q$,E0$,E1$,E2$
7025 IF I0=0 AND MID(A$,50,1)<>"@" THEN W$=STR(F8)+FNE$(F8)+I9$+"="+Q$,F8=F8+5; GOSUB 8800
7030 I8$=I8$+Q$; IF POS(" "<>I$)>0 THEN GOTO 7020
7090 RETURN 
7100 REM "DATA FOR SORT HEADINGS
7102 IF L6=0 THEN L6=6255,L0=L6 ELSE L6=L6+5
7103 RETURN 
7105 IF L6=0 THEN L6=6255 ELSE L6=L6+5
7110 L0=L6
7115 ESCAPE 
7145 RETURN 
7150 REM "FLUSH SORT HEADERS
7153 W$="6105 GOSUB 7300"; GOSUB 8800
7155 L0=6205; GOSUB 5900
7165 W$="6395GOSUB 7300; RETURN"; GOSUB 8800
7170 W$="6052 GOSUB 6200"; GOSUB 8800
7180 L0=1520,L6=0,L1=L1+1; IF POS(MID(R$,24,1)="FLP")>0 THEN L0=4010
7195 RETURN 
7200 REM "EXPAND FORMULATED SORT FIELDS
7210 FOR X=1 TO LEN(E4$) STEP 20
7215 IF POS(MID(E4$,X,20)=E0$,20)>0 THEN GOTO 7280
7220 I$=E4$(X,20)
7225 GOSUB 7000
7280 NEXT X
7285 E4$=""
7290 RETURN 
7400 REM "CALCULATE KEY FOR TEMPORARY SORT
7405 IF MID(R$,102,1)="T" THEN W$=X$+"TMP"+X$+"+FID(0)" ELSE W$=FNS$(R$(103,20)); IF W$="" THEN W$=X$+"S"+R$(127,5)+X$ ELSE W$=X$+W$+X$
7410 Q$=W$,W$="7455V1$="+W$; GOSUB 8800
7415 W$="6952Q$="+Q$; GOSUB 8800
7420 W$="825V0=2"; GOSUB 8800
7445 RETURN 
7500 REM "SORT PORTION HEADERS
7505 IF L6=0 THEN L6=6255 ELSE L6=L6+5
7510 ESCAPE 
7590 RETURN 
7600 REM "SETUP VARIABLES FOR SELECTION CRITERIA
7601 IF D1$="" THEN GOTO 7690 ELSE D1$=D1$(1,999)
7602 FOR X=1 TO LEN(D1$(25)) STEP 75; Q9$=D1$(24+X,75); IF POS(" "<>Q9$)=0 THEN GOTO 7670
7610 IF MID(Q9$,1,1)<>"|" THEN GOTO 7670
7615 P=POS("["=Q9$),Q$=Q9$(P+1,POS("]"=Q9$(P+1))-1)
7625 V8$=V8$+Q9$
7660 CALL "ZA2BU5",X3$,Q$,V0$,V8,V9,I9$,"",E0$,E1$,E2$
7665 P=POS(":"=Q$),Q$=Q$(1,P-1),Q$=Q$+S$(1,20-LEN(Q$)),V7$=V7$+Q$
7670 NEXT X
7690 RETURN 
7700 REM 
7710 W$=""
7720 CALL "ZA2BAY",X3$,X4$,W$,V0$,V8,V9,V8$,X$,X{ALL},1,"",D2,D3,E0$,E1$,E2$; IF W$="" THEN GOTO 7790 ELSE D2=D2+1,D3=D3+1
7725 W$="7458"+W$; GOSUB 8800
7730 W$="126DIM V$("+STR(D2)+"),V("+STR(D3)+")"; GOSUB 8800
7735 W$="425 IOLIST V$"; FOR X=0 TO D3; W$=W$+",V("+STR(X)+")"; NEXT X; GOSUB 8800
7745 RETURN 
7750 REM "
7755 FOR Z=1 TO LEN(V7$) STEP 20; Q$=FNS$(V7$(Z,20)); IF POS(Q$=Q9$)=0 THEN GOTO 7790
7760 CALL "ZA2BU2",X3$,V0$,-1,-1,"",Q$,"",I9$,I$,"",E0$,E1$,E2$
7765 IF POS("$"=I9$)>0 THEN V6$="IF POS("+X$+" "+X$+"<>"+I9$+")<>0"
7770 EXITTO 7791
7790 NEXT Z
7795 RETURN 
7800 REM "TEMPORARY SORTS
7805 S9$="",S6$="",S7$="",S7=0,S8$="",K0=0; IF POS(MID(R$,102,1)="TS")=0 THEN GOTO 7890
7808 V1=2,S8$=R$(60,20)
7810 FOR X=1 TO LEN(D0$) STEP 30
7815 Q9$=D0$(X,20),Q$=D0$(X+20,10); IF POS(" "<>Q9$)=0 THEN GOTO 7850
7820 CALL "ZA2BU2",ERR=7821,X3$,V0$,-1,-1,R$(60,20),Q9$,F1$,I9$,I$,"",E4$,E1$,E2$; GOTO 7825
7821 Z$="+("+FNS$(Q9$)+")"
7822 IF ERR=11 THEN CALL "ZZPROM","X1ZA2BUA",X3$,A,Z$,"","",0; ON A GOTO 9900 ELSE GOTO 9000
7825 K0=K0+NUM(Q$(3,2))
7828 IF MID(Q$,7,1)<>"N" THEN IF MID(Q$,5,1)="D" THEN S7$=S7$+";CALL"+$22$+"ZZSRTD"+$22$+",X3$,"+I9$+",Q"+STR(S7:"0")+"$",I9$="Q"+STR(S7:"0")+"$",S7=S7+1,Z$="U$("+STR(K0-NUM(Q$(3,2))+1)+","+Q$(3,2)+")",S6$=S6$+";CALL"+$22$+"ZZSRTD"+$22$+",X3$,"+Z$+",X$;"+Z$+"=X$"
7830 IF POS("$"=I9$)=0 THEN GOSUB 7900
7840 S9$=S9$+I9$+"+",S8$=S8$+D0$(X,20)
7850 NEXT X; S9$=S9$+"STR(I0:"+X$+"0000000000"+X$+")" ! SSP#236273
7851 IF E4$>"" THEN GOSUB 7200
7852 IF S7$>"" THEN W$="6928"+S7$(2); GOSUB 8800; S7$="",S7=0,W$="1007"+S6$(2); GOSUB 8800; M5$=M5$+"ZZSRTD"
7855 W$="6930U$="+S9$; GOSUB 8800
7860 CALL "ZZ2BAC","ZA2SRT",0,9999,14,A0$
7865 W$="6902S8$="+X$+S8$+X$; GOSUB 8800
7895 RETURN 
7900 REM "NUMERIC SORT ELEMENT
7905 IF MID(Q$,5,1)="D" THEN I9$="Z9-"+I9$
7910 I9$="FNZ$("+I9$+")"
7940 RETURN 
7990 RETURN 
8200 REM "MAKE PROGRAM
8210 CALL "ZZ2BAC","ZARPT1",0,9999,14,A0$
8225 F$="R"+R$(127,5); ERASE F$,ERR=8226; GOTO 8225
8230 W$="10REM"+X$+FNS$(R$(135,65))+" <"+F$+">"; GOSUB 8800
8240 W$="105X0$="+X$+F$+X$+",B9$="+X$+R$(1,23)+X$; GOSUB 8800
8272 IF MID(R$,24,1)="T" THEN IF MID(R0$,3,3)="DIF" THEN Q$="ZARDIF" ELSE IF MID(R0$,3,3)="PRN" THEN Q$="ZARPRN" ELSE Q$="ZARASC"
8275 IF MID(R$,24,1)="T" THEN CALL "ZZ2BAC","ZARPTT",0,9999,14,A0$; CALL "ZZ2BAC",Q$,0,9999,14,A0$
8280 IF MID(R$,24,1)="D" THEN CALL "ZZ2BAC","ZARPTD",0,9999,14,A0$
8281 IF MID(R$,24,1)="P" THEN CALL "ZZ2BAC","ZARPTP",0,9999,14,A0$
8282 IF POS("F"=MID(R$,338,15))>0 THEN CALL "ZZ2BAC","ZARPT2",0,9999,14,A0$
8283 IF POS(MID(R$,24,1)="LF")>0 THEN CALL "ZZ2BAC","ZARPTF",0,9999,14,A0$
8284 IF MID(R$,24,1)="L" THEN CALL "ZZ2BAC","ZARPTL",0,9999,14,A0$
8285 Q$=""
8290 RETURN 
8800 REM "ADD CODE TO REPORT
8801 W_TEMP$=W$
8805 W$=CPL(W$),W$=BIN(LEN(W$),3)+W$,A0$=A0$+W$; IF MID(W$,6,1)=$F3$ THEN GOSUB 8850
8806 W$=""; RETURN 
8810 REM IF LEN(W$)<80 THEN WRITE (F0) W$;      RETURN      ELSE         WRITE (F0) W$(1,79);         W$=W$(1,4)+": "+W$(80);         GOTO 8810
8845 RETURN 
8855 Q$="C"+STR(DEC(W$(4,2)):"0000"); READ (Z[8],KEY=Q$,DOM=8860)H$; GOTO 8865
8860 READ (Z[8])H$
8865 H$="+("+Q$(2)+", "+FNS$(H$(9))+")"
8870 CALL "ZZPROM","ERZA2BUA",X3$,A,H$,"","",0
8890 RETURN 
8895 RETURN 
8910 DEF FNS$(Z$)=Z$(1,POS(S$=Z$+S$)-1)
8915 DEF FNI$(Z$)=BIN(LEN(CPL(Z$)),3)+CPL(Z$)
8920 DEF FNZ$(Z)=STR(SGN(SGN(Z)+1))+PCK(Z,6)
8925 DEF FNE$(Z)="SETERR "+STR(Z+1)+";"
9000 REM "ERROR PROCESSING
9005 A8=0
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZAERRM",X3$,Y5,Y6,Y7
9045 SETERR 9000
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF MID(X3$,47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 IF Y5=68 THEN RETRY ELSE ON C9 GOTO 1140,2040
9750 REM "FILES
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9790 RETURN 
9800 REM "COME TO A BAD END
9907 IF A8>=9 THEN REMOVE (Z[13],KEY=FID(0)+"BERT",DOM=9908)
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"CLOSE",Z{ALL},0,0
9915 CALL "ZZDROP"; CALL "ZZDAPP",X3$,"IPL"
9920 IF A8>=9 THEN SETERR 9921; RUN F$
9930 IF MID(X3$,146,6)+"  "<>PGN THEN L0=0; RUN X3$(146,6)
9940 SETERR 9945; RUN "ZMENU"
9950 EXIT 
9999 END 
