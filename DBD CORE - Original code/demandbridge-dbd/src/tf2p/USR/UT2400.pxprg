0010 REM "TopForm Release 4.0 Update <UT2400>
0020 SETESC 9300; SETERR 9000
0035 REM "4.1 - 12/05/96 - 10.19 - dmm - SSP# 078859
0040 REM "Copyright 1996 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="UT2400",X1$="TopForm Release 4.0 Update"
0120 DIM Z0$(80,"-"),S$(40)
0130 K0=20,K1=1
0135 C9=-1
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0509 REM "Stay away from slot Z(11)
0510 Z$="12O ZZE  13O ZZPARM  "
0520 GOSUB 9750
0600 REM "
0610 GOSUB 6000
0640 IF Q1$<>"*" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0660 PRINT @(0,4),'CE',
1000 REM 
1010 GOSUB 4000
1020 H1$="Vendor Message file to 512"
1030 F$="AP8...",F1$="O"; GOSUB 7500; GOSUB 7900
1040 DIM A[4]; A[0]=K,A[1]=512,A[2]=R1,A[3]=D,A[4]=491; GOSUB 8000
1050 H1$="A/R G/L Distribution file to 256 bpr"
1060 F$="ARV...",F1$="O"; GOSUB 7500; GOSUB 7900
1070 DIM A[4]; A[0]=K,A[1]=256,A[2]=R1,A[3]=D,A[4]=255; GOSUB 8000
1080 H1$="Order Req Gateway Header file to 384"
1090 F$="FO0...",F1$="O"; GOSUB 7500; GOSUB 7900
1100 DIM A[4]; A[0]=K,A[1]=384,A[2]=R1,A[3]=D,A[4]=376; GOSUB 8000
2000 REM "Erase files
2010 INVOKE "rm ./tf2z/USR/ZR2* > /dev/null"; REM "Remove Document Repository files from tf2z, They are now in tf2p
3000 REM "Set Parameter Records
3020 REM "BATCH record
3040 GOSUB 4200
3060 REM "Remove W/O paratmeter.  It should only be set for Co#011
3080 IF X3$(9,3)<>"011" THEN REMOVE (Z[13],KEY=X3$(9,3)+"F/W",DOM=3081)
3500 REM "Rebuilds-
3510 REM "Rebuild Invoice Number Sort
3520 REM CALL "AR2UTV",X3$,X4$,"","*"
3990 GOTO 5000
4000 REM "Define missing files F5$=Files to check, G5$=use number of records from this exisiting file or if number use it.
4010 F5$="ASK...FO0...FO2...FO3...FO5...FO6...EM1...EM2...EM3..."
4020 G5$="500   020   020   020   020   020   020   020   020   "
4030 IF F5$="" THEN GOTO 4190 ELSE F4$=F5$(1,6),F5$=F5$(7),G4$=G5$(1,6),G5$=G5$(7)
4040 FIND (Z[12],KEY=F4$(1,3)+"   ")A$
4050 CLOSE (14); OPEN (14,ERR=4051)F4$(1,3)+X3$(9,3); CLOSE (14); GOTO 4180
4055 R1=NUM(G4$,ERR=4056),D=0; GOTO 4080
4060 F$=G4$; F1$="O"; GOSUB 7500
4070 GOSUB 7900; F1$="C"; GOSUB 7500
4080 DIM A[4]; A[2]=R1,A[3]=D,Q$=F4$(1,3)+X3$(9,3)
4090 IF A$(47,1)="S" THEN T0=3,A[0]=NUM(A$(48,2)) ELSE IF A$(47,1)="D" THEN T0=2,A[0]=NUM(A$(48,2)),A[1]=NUM(A$(50,4)) ELSE IF A$(47,1)="I" THEN T0=0,A[1]=NUM(A$(50,4)) ELSE ESCAPE 
4100 GOSUB 7650
4105 H1$="Defined file "+Q$; GOSUB 8800
4110 IF T0<>0 THEN GOTO 4180
4120 F$=F4$; F1$="O"; GOSUB 7500
4130 CALL "ZZINIT",STR(F:"00")
4140 F1$="C"; GOSUB 7500
4180 GOTO 4030
4190 RETURN 
4200 REM "Create BATCH control record in ZZPARM
4210 READ (Z[13],KEY="CMP"+X3$(9,3),DOM=4290)X$; IF X$(194,1)<>"1" THEN GOTO 4290
4215 PRINT @(0,4),'CE',@(10,10),"Rebuilding Batch Control Record",
4220 Q$=""; READ (Z[12],KEY="",DOM=4221)
4230 READ (Z[12],END=4275)X$
4240 IF X$(46,1)<>"1" THEN GOTO 4230
4245 PRINT @(45,10),X$(1,3),
4250 IF X$(4,3)="   " THEN X$(4,3)="..."
4260 Q$=Q$+X$(1,6)+"  "
4270 GOTO 4230
4275 PRINT @(0,4),'CE',
4280 Q$="BATCH"+Q$
4285 WRITE (Z[13],KEY=Q$(1,5))Q$
4290 RETURN 
5000 REM "EOJ
5010 PRINT @(0,4),'CE',@(0,10),"The automated portion of your TopForm Release 4.0 installation is now complete.  Contact CSI Software Support for further instructions at 770 448-1484."
5020 IF Q1$<>"*" THEN CALL "ZZPROM",".4",X3$,Z,"","","",0
5040 GOTO 9900
6000 REM "Bkg
6020 PRINT 'SB',@(0,3),"The TopForm Release 4.0 updates will be performed automatically by this program.",@(0,5),"After answering YES to the Proceed question below, new files will be defined and",@(0,7),"some of you data will be adjusted to be compatible with TopForm Release 4.0.",@(0,9),"Also, various sort file rebuilds will be run automatically."
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7500 REM "Open/Close Files
7505 Z$=""
7510 FOR I=1 TO LEN(F$) STEP 6
7515 Z$=Z$+STR(I:"00")+F1$+" "+F$(1,6)+"  "
7520 NEXT I
7525 GOSUB 9750
7530 F=Z[1]
7540 RETURN 
7600 REM "CREATE TEMP FILE
7610 Q$="-T"+FID(0)
7615 ERASE Q$,ERR=7616; GOTO 7615
7625 IF ERR<>12 THEN GOTO 9000
7630 GOSUB 7650
7635 Z$="11O "+Q$; GOSUB 9750
7640 RETURN 
7650 REM DEFINE FILE
7660 IF T0=0 THEN INDEXED Q$,A[2],A[1],A[3],0 ELSE IF T0=2 THEN DIRECT Q$,A[0],0,A[1],A[3],0 ELSE IF T0=3 THEN SORT Q$,A[0],0,A[3],0 ELSE ESCAPE 
7690 RETURN 
7700 REM "EXPAND FILE -NOTE: Cannot expand strings of Indexed files!!!!
7705 C=0
7707 IF T0<>0 THEN EXTRACT (F,KEY="",DOM=7708) ELSE EXTRACT (F,IND=0)
7710 IF T0<>0 THEN K$=KEY(F,END=7780) ELSE SETERR 7790; I0=IND(F)
7715 READ RECORD (F,END=7780)A$
7718 REM LET C=C+1; IF MOD(C,100)=0 THEN LET H1$="Record: "+STR(C); GOSUB 08800
7719 C=C+1; IF MOD(C,T2)=1 THEN GOSUB 8150
7720 IF A[4]=0 OR T=0 THEN GOTO 7735 ELSE P0=POS($8A$=A$); IF P0=0 THEN P0=LEN(A$)+1,A$=A$+$8A$
7721 IF P0-1>A[4] THEN REM CALL "ZZPROM",".5",X3$,A,"String will be truncated!","","",0; ON A GOTO 07722,07900
7722 DIM S$(A[4]); S$(1)=A$(1,P0-1),A$=S$+A$(P0)
7730 IF LEN(A$)>A[1] THEN IF POS($00$<>A$(A[1]))>0 THEN CALL "ZZPROM",".5",X3$,A,"Record will be truncated!","","",0; ON A GOTO 7731,7900
7735 IF LEN(A$)>A[1] THEN A$=A$(1,A[1])
7750 IF T0=0 THEN WRITE RECORD (Z[11])A$ ELSE WRITE RECORD (Z[11],KEY=K$)A$
7760 GOTO 7710
7790 SETERR 9000
7795 RETURN 
7800 REM "RENAME FILES
7810 ERASE "TX"+FID(0),ERR=7811; GOTO 7810
7815 IF ERR<>12 THEN GOTO 9000
7820 Z$=STR(Z[1]:"00")+"C "+F$(1,6)+"  "; GOSUB 9750; CLOSE (Z[1],ERR=7821)
7830 RENAME F9$,"TX"+FID(0)
7840 Z$="11C "+Q$+"  "; GOSUB 9750; CLOSE (Z[11],ERR=7841)
7845 RENAME Q$,F9$
7850 ERASE "TX"+FID(0)
7860 H2$="*Finished*"; GOSUB 8800
7890 RETURN 
7900 REM 
7910 CALL "ZZINFO",F,X0,X3$,R0,R1,K,B,D,T0,S0,F9$
7940 RETURN 
8000 REM 
8005 GOSUB 8100
8010 H2$="*Starting*"; GOSUB 8800
8040 GOSUB 7600
8050 GOSUB 7700
8060 IF K=0 THEN GOSUB 8200
8070 GOSUB 7800
8090 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T2 to the reporting interval (report every T2 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",F,T9,X3$,T,T2,K,B,D,S0,S1,F$
8115 H2$=STR(T)+" # of records to do "; GOSUB 8800
8129 REM "Set T2, we make sure T2 is > 1, because later on we MOD and look for avalue of 1. IF T2 is 1, then nothing would get reported. We look fora result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T2'th record to get the first report
8130 T2=INT(T*.02); IF T2<1 THEN T2=2
8135 C=0,T1=0
8140 PRINT @(0,4),'CL',
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",4,10,50,T1,T,C
8195 RETURN 
8200 REM "INDEXED FILES
8210 DIM Y[6]; EXTRACT (Z[11],IND=0)IOL=0290
8220 Y[3]=A[2]-1
8230 WRITE (Z[11],IND=0)IOL=0290
8290 RETURN 
8600 REM "Expand String-F=file slot, F0$=key prefix, F0=string length
8605 C=0,H2$="*STARTING*"; GOSUB 8800
8610 READ (F,KEY=F0$,DOM=8611)
8620 K$=KEY(F,END=8690)
8630 IF K$(1,LEN(F0$))<>F0$ THEN GOTO 8690
8640 EXTRACT RECORD (F,KEY=K$)A$
8650 P0=POS($8A$=A$); IF P0=0 THEN ESCAPE 
8660 DIM A1$(F0); A1$(1)=A$(1,P0-1),A1$=A1$+A$(P0),A1$=A1$(1,LEN(A$))
8665 C=C+1; IF MOD(C,100)=0 THEN H2$="Record: "+STR(C); GOSUB 8800
8670 WRITE RECORD (F,KEY=K$)A1$
8680 GOTO 8620
8690 H2$="*FINISHED*"; GOSUB 8800
8695 RETURN 
8800 REM "Display progress  - Eventually write a log file
8810 L=L+1; IF L>15 THEN PRINT @(0,5),'LD',@(0,20),; L=15 ELSE PRINT @(0,L+5),
8815 X$=STR(INT(TIM):"00")+STR(INT(FPT(TIM)*100):"00")
8820 PRINT DAY," @ ",FNT$(X$)," ",H1$," ",H2$
8830 H2$=""
8840 RETURN 
8900 REM "Special handling for FM4"
8902 DIM B[15]
8905 IOLIST B$,B1$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15]
8910 READ (Z[1],KEY="",DOM=8911)
8920 EXTRACT (Z[1],END=8940)IOL=8905
8930 B1$=""; WRITE (Z[1])IOL=8905; GOTO 8920
8940 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; RETRY 
9750 REM 
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9770 ON Z0 GOTO 9771,9900
9790 RETURN 
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
