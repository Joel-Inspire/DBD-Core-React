3500 REM "Get lines section in LS_TEMPLATE$ and generate LINES_SECTION$ containing an entry for each order line from ESE
3502 LINES_SECTION$="",LS_TAGS$="001<line_number>002<line_type>003<from_customer>004<item_code>005<cost_center>006<cost_code>007<item_desc>008<um>009<ship_from>010<quantity>011<qty_per_um>012<session_id>013<worksheet>014<3pinfostart>015<>016<3p_id>017<lotlinestart>018<qty_in_eaches>019<linemessage_start>020<cic_code>021<item_price>022<item_price_ext>023<vp_info_1>024<vp_info_2>025<vp_id>026<proof_url>027<po_code>028<vendor_code>029<vendor_plant>030<weight_total>031<form_memo>032<invoice_unit_price>033<invoice_unit_price_ext>"
3503 REM "Setup LS_TEMPLATE$
3504 LS_TEMPLATE$=LINE$
3505 READ (100,END=3506)LINE$; GOSUB 8150; LS_TEMPLATE$=LS_TEMPLATE$+LINE$+EOL$; IF POS("?tf?wslineend?"=LINE$)=0 THEN GOTO 3505
3508 IF DISPLAY_LINE_NUMBER$<>"" THEN DIM ECE$(499),ECE[1]; READ (Z[3],KEY=WORKSHEET$+DISPLAY_LINE_NUMBER$,DOM=3595)IOL=0330; GOSUB 7600; GOSUB 8250; GOTO 3514 ! ssp #146692
3509 READ (Z[3],KEY=WORKSHEET$,DOM=3510)
3510 DIM ECE$(499),ECE[1]; READ (Z[3],END=3595)IOL=0330; IF ECE$(1,10)<>WORKSHEET$ THEN GOTO 3595 ELSE GOSUB 7600; GOSUB 8250
3514 LS$=LS_TEMPLATE$
3515 LS_INDEX=POS("?tf?"=LS$); IF LS_INDEX=0 THEN GOTO 3591
3516 LSEND_INDEX=POS("?"=LS$(LS_INDEX+4)),LS_TAG$=LS$(LS_INDEX+4,LSEND_INDEX-1),LS1$=LS$(1,LS_INDEX-1),LS2$=LS$(LS_INDEX+LSEND_INDEX+4),LS_POS=POS("<"+LS_TAG$+">"=LS_TAGS$); IF LS_POS=0 THEN LS$=LS1$+LS2$,LS_INDEX=0 ELSE LS_INDEX=NUM(LS_TAGS$(LS_POS-3,3))
3520 ON LS_INDEX GOTO 3589,3521,3522,3523,3524,3525,3526,3527,3528,3529,3530,3531,3532,3533,3534,3535,3536,3537,3538,3539,3540,3541,3542,3543,3544,3545,3546,3547,3548,3549,3550,3551,3589
3521 LS$=LS1$+ECE$(25,3)+LS2$; GOTO 3590; REM "line_number
3522 LS$=LS1$+ECE$(28,1)+LS2$; GOTO 3590; REM "line_type
3523 CALL "ZZDISP","AX",ECE$(29,10),"A/R",X3$,TMP$,"",0,0,X4$; LS$=LS1$+TMP$+LS2$; GOTO 3590; REM "from_customer
3524 LS$=LS1$+STP(ECE$(39,10),1)+LS2$; GOTO 3590; REM "item_code
3525 LS$=LS1$+STP(ECE$(49,9),1)+LS2$; GOTO 3590; REM "cost_center
3526 LS$=LS1$+STP(ECE$(58,9),1)+LS2$; GOTO 3590; REM "cost_code
3527 LS$=LS1$+STP(ECE$(67,40),1)+LS2$; GOTO 3590; REM "item_desc
3528 TMP$=STP(ECE$(107,4),1)+"/"+STP(STR(ECE[1]:"##,###"),2); IF TMP$="/" THEN LS$=LS1$+LS2$; GOTO 3590 ELSE LS$=LS1$+TMP$+LS2$; GOTO 3590; REM "um
3529 LS$=LS1$+STP(ECE$(111,4),1)+LS2$; GOTO 3590; REM "ship_from
3530 LS$=LS1$+STR(ECE[0]/QTY_DIVISOR:"#,###,##0")+LS2$; GOTO 3590; REM "quantity
3531 LS$=LS1$+STR(ECE[1]:"##,###")+LS2$; GOTO 3590; REM "qty_per_um
3532 LS$=LS1$+STP(EC$[1](1,7),1)+LS2$; GOTO 3590; REM "session_id
3533 LS$=LS1$+WORKSHEET$+LS2$; GOTO 3590; REM "worksheet
3534 GOSUB 3950; LS$=LS1$+TPINFO_TEMPLATE$+LS2$; GOTO 3590; REM "3pinfostart
3535 LS$=LS1$+STP(EC7$(44,20),1)+LS2$; GOTO 3590; REM "** not used **
3536 LS$=LS1$+STP(EC7$(14,10),1)+LS2$; GOTO 3590; REM "3p_id
3537 GOSUB 3900; LS$=LS1$+LOT_SECTION$+LS2$; GOTO 3590; REM "lotlinestart - output of lotted information
3538 LS$=LS1$+STR(ECE[0]*ECE[1]:"#,###,###")+LS2$; GOTO 3590; REM "qty_in_eaches
3539 GOSUB 3850; LS$=LS1$+LINEMSG_SECTION$+LS2$; GOTO 3590; REM "linemessage_start
3540 LS$=LS1$+TBL(SGN(LEN(STP(FM1$(350,20)))),STP(FM1$(11,10),1),STP(FM1$(350,20),1))+LS2$; GOTO 3590; REM "cic_code, form code if cic_code is blank
3541 GOSUB 8246; IF FM1(29)=0 THEN ITEMPRICE=FM1(3) ELSE ITEMPRICE=FM1(29) END_IF ; LS$=LS1$+STP(STR(ITEMPRICE:"###,###.00-"),2)+LS2$; GOTO 3590; REM "item_price
3542 GOSUB 8246; IF FM1(29)=0 THEN ITEMPRICE=FM1(3) ELSE ITEMPRICE=FM1(29) END_IF ; ITEMPRICEEXT=ITEMPRICE*ECE(0); ITEM_PRICE_TOTAL+=ITEMPRICEEXT; LS$=LS1$+STP(STR(ITEMPRICEEXT:"###,###.00-"),2)+LS2$; GOTO 3590; REM "item_price_ext
3543 LS$=LS1$+STP(EC7$(24,20),1)+LS2$; GOTO 3590; REM "vp_info_1 ssp#148607
3544 LS$=LS1$+STP(EC7$(44,20),1)+LS2$; GOTO 3590; REM "vp_info_2 ssp#148607
3545 LS$=LS1$+STP(EC7$(14,10),1)+LS2$; GOTO 3590; REM "vp_id ssp#148607
3546 GOSUB 4100; LS$=LS1$+PROOF_URL$+LS2$; GOTO 3590; REM "proof_url ssp#148607
3547 LS$=LS1$+ECE$(129,1)+LS2$; GOTO 3590; REM "po_code
3548 CALL "ZZDISP","AX",ECE$(115,10),"A/P",X3$,TMP$,"",0,0,X4$; LS$=LS1$+TMP$+LS2$; GOTO 3590; REM "vendor_code
3549 LS$=LS1$+ECE$(125,4)+LS2$; GOTO 3590; REM "vendor_plant
3550 GOSUB 8246; IF FM1(18)=0 THEN ITEMWGT=0 ELSE ITEMWGT=FM1(18) END_IF ; ITEMWGTEXT=ITEMWGT*ECE(0); ITEM_WGT_TOTAL+=ITEMWGTEXT; IF ITEMWGTEXT=0 THEN WGT$="n/a" ELSE WGT$=STP(STR(ITEMWGTEXT:"###,###.00-"),2) END_IF ; LS$=LS1$+WGT$+LS2$; GOTO 3590; REM "weight_total "
3551 GOSUB 8246; LS$=LS1$+STP(FM1$(389,60),2)+LS2$; GOTO 3590; REM "form_memo
3552 GOTO 3590; REM "invoice_unit_price
3553 GOTO 3590; REM "invoice_unit_price_ext
3589 LS$=LS1$+LS2$; GOTO 3590; REM "not found replace with a '?'
3590 GOTO 3515
3591 IF LEN(LINES_SECTION$)+LEN(LS$)>64000 THEN PRINT (OUTPUT)LINES_SECTION$,EOL$; LINES_SECTION$=""; REM "ssp#134905
3592 LINES_SECTION$=LINES_SECTION$+LS$; IF DISPLAY_LINE_NUMBER$<>"" THEN GOTO 3595 ELSE GOTO 3510; REM "add to lines section, then get next line if any ssp#146692
3595 RETURN 
7100 REM "build invoice display array
7102 IF ECD$(508,1)="I" THEN INVHDR=Z(31),INVLINE=Z(30) ELSE INVHDR=Z(28),INVLINE=Z(29); REM "set invoice header and line file channels
7103 INVOICE_LINE_COUNT=0; DIM INVOICE$[200,4]; REM "200 lines  (index 4) 1=order line #  2=ART string var$  3=extended sell amt  4=display/print line
7105 DIM GRB(20),GRB$(500),GRT$(500),GRT(10); FIND (INVHDR,KEY=ECD$(509,14),DOM=7145)IOL=0470
7108 INVOICE_LINE_INDEX=GRB(20)
7110 IF INVOICE_LINE_INDEX=0 THEN GOTO 7145 ELSE READ (INVLINE,IND=INVOICE_LINE_INDEX)IOL=0490
7112 INVOICE_LINE_COUNT=INVOICE_LINE_COUNT+1
7115 INVOICE$[INVOICE_LINE_COUNT,1]=GRT$(47,3),INOVICE$[INVOICE_LINE_COUNT,2]=GRT$,INVOICE$[INVOICE_LINE_COUNT,3]=STR(GRT(4)),INVOICE$[INVOICE_LINE_COUNT,4]=GRT$(6,1); REM "assign  1=order line #  2=string var  3=extended sell  4=line print flag
7118 IF GRT$(1,1)<>"S" THEN GOTO 7110; REM "if not special charge then get next line
7120 REM "special charge line 
7122 DIM FMPX$(100),FMPX(2); FIND (Z(9),KEY="X"+GRT$(65,10),DOM=7125)IOL=0392
7123 IF FMPX$(60,1)="Y" THEN INVOICE$[INVOICE_LINE_COUNT,4]="N"; REM "if special charge is freight type then turn off display
7129 GOTO 7110
7145 RETURN 
9999 END 
