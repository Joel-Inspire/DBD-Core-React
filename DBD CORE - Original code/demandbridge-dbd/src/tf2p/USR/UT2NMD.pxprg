0010 REM "Change Panel Position in Nomads .EN Libraries <UT2NMD>
0035 REM "5.7 - 07/09/18 - 14.446666 - jvv - SSP# 300847
0037 REM "300847-NOMADS panel position changes - alter default panel popup   
0040 REM "Copyright 2018 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0100 ENTER PATH$,PATTERN1$,ERR=*NEXT; GOTO SET_PATTERNS
0105 BEGIN 
0110 PATH$="/usr/lib/pvx/tf2g"
0120 PATTERN1$="???"
0199 ! 
0200 SET_PATTERNS:
0210 PATTERN2$="*.EN"
0220 PATTERN3$="*.EN.CUST*"
0299 ! 
1000 PROCESS_LIB:
1050 SELECT SUBDIR$ FROM PATH$ WHERE SUBDIR$ LIKE PATTERN1$
1060 COMPDATAPATH$=PATH$+DLM+SUBDIR$
1065 PRINT "Searching "+COMPDATAPATH$
1070 MPATTERN$=PATTERN2$; GOSUB READ_LIB
1080 MPATTERN$=PATTERN3$; GOSUB READ_LIB
1110 NEXT_LIB:
1160 NEXT RECORD 
1195 END 
1999 ! 
2000 READ_LIB:
2010 SELECT FILENAME$ FROM COMPDATAPATH$ WHERE FILENAME$ LIKE MPATTERN$,ERR=NEXT_FILE
2020 PRINT "Found "+COMPDATAPATH$+DLM+FILENAME$
2030 GOSUB PROCESS_FILE
2040 NEXT_FILE:
2050 NEXT RECORD 
2080 RETURN 
2099 ! 
5000 PROCESS_FILE:
5040 DIM FLD$[1:52]
5050 OPEN (1)COMPDATAPATH$+DLM+FILENAME$
5060 READ (1,KEY="",DOM=*NEXT)
5110 NEXT_REC:
5120 K$=KEY(1,END=END_FILE); READ (1,KEY=K$)FLD${ALL}
5150 IF K$(1,4)="MENU" OR STP(K$(1,4),2)="" THEN GOTO NEXT_REC
5160 IF K$(13,4)<>"0000" THEN GOTO NEXT_REC
5170 FMT$=FLD$[21]
5180 IF POS("R"=FMT$)<>0 THEN GOTO WRITE_REC
5190 XX=POS("+"=FMT$); IF XX<>0 THEN FMT$(XX,1)="R"; GOTO WRITE_REC
5200 FMT$=FMT$+"R"
5220 WRITE_REC:
5222 IF FLD$[6]="q" THEN DPOS$="3" ELSE DPOS$="2"
5225 FLD$[2]=DPOS$,FLD$[3]=DPOS$
5230 FLD$[21]=FMT$
5250 WRITE (1,KEY=K$)FLD${ALL}
5260 GOTO NEXT_REC
5270 ! 
5280 END_FILE:
5290 CLOSE (1)
5295 RETURN 
9999 END 
