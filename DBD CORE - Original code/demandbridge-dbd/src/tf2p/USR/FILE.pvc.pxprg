0010 ! FILE IO CLASS
0015 DEF CLASS "FILE"
0035 REM "5.5 - 09/24/07 - 10.7225 - jme - SSP# 203099
0037 REM "203099-Enhancements to Output Director.  Provide ability for       
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0200 ! start variable list
0210 LOCAL FILENO,_KEYNO
1000 FUNCTION READBYKEY(*)
1010 ENTER KEYNO,SEG1$,SEG2$,SEG3$,SEG4$,SEG5$,SEG6$,SEG7$,SEG8$,ERR=*NEXT
1011 ENTER KEYNO,SEG1$,SEG2$,SEG3$,SEG4$,SEG5$,SEG6$,SEG7$,ERR=*NEXT
1012 ENTER KEYNO,SEG1$,SEG2$,SEG3$,SEG4$,SEG5$,SEG6$,ERR=*NEXT
1013 ENTER KEYNO,SEG1$,SEG2$,SEG3$,SEG4$,SEG5$,ERR=*NEXT
1014 ENTER KEYNO,SEG1$,SEG2$,SEG3$,SEG4$,ERR=*NEXT
1015 ENTER KEYNO,SEG1$,SEG2$,SEG3$,ERR=*NEXT
1016 ENTER KEYNO,SEG1$,SEG2$,ERR=*NEXT
1017 ENTER KEYNO,SEG1$,ERR=*NEXT
1020 ! 
1030 IF KEYNO>=0 THEN _KEYNO=KEYNO ! Allow -1 to leave same key#
1040 ! 
1050 READ (FILENO,KNO=_KEYNO,KEY=SEG1$+SEG2$+SEG3$+SEG4$+SEG5$+SEG6$+SEG7$+SEG8$,DOM=FAILURE)
1060 RETURN 1
1070 ! 
1080 ! Return next record on same key
1090 ! 
1100 FUNCTION READNEXT()
1110 READ (FILENO,KNO=_KEYNO,END=FAILURE)
1120 RETURN 1
1130 ! 
1140 ! Extract/lock current record
1150 ! 
1160 FUNCTION EXTRACT()
1170 PRIMEKEY$=KEC(FILENO,KNO=0)
1180 EXTRACT (FILENO,KEY=PRIMEKEY$,KNO=0,ERR=FAILURE)
1190 RETURN 1
1200 ! 
1210 ! Add a new record
1220 ! 
1230 FUNCTION INSERT(X$)
1235 ENTER PRIMEKEY$
1240 WRITE (FILENO,KEY=PRIMEKEY$,DOM=FAILURE)
1250 RETURN 1
1260 ! 
1270 ! Update existing record -- Must be extracted!!
1280 ! 
1290 FUNCTION UPDATE()
1300 ! IF FIN(FILENO,"EXTRACT")="0" THEN EXIT 13 ! Must extract 1st !note FIN does not work for oracle tables
1310 WRITE (FILENO)
1320 RETURN 1
1330 ! 
1340 ! Delete existing record -- Must be extracted!!
1350 ! 
1360 FUNCTION DELETE()
1370 ! IF FIN(FILENO,"EXTRACT")="0" THEN EXIT 13 ! Must extract 1st !note FIN does not work for oracle tables
1380 REMOVE (FILENO)
1390 RETURN 1
1400 ! 
1410 FAILURE:RETURN 0
8999 END DEF
9999 END 
