0010 REM "Import from req type to Order <IM2REQ>
0020 SETESC 9300; SETERR 9000
0035 REM "3.8 - 10/27/94 - 1.29 - kmc
0040 REM "Copyright 1994 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0080 BEGIN 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="IM2REQ",X1$="Import Requisition information"
0120 DIM Z0$(80,"-"),Z1$(20,"0"),S0$(40)
0130 K0=20,K1=1
0135 C9=-1
0145 M0$="##,###",M1$="00000"
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1]
0320 IOLIST B$,B[0],B[1]
0330 IOLIST C$
0340 IOLIST D$,D[0],D[1],D[2],D[3],D[4],D[5],D[6],D[7],D[8],D[9],D[10],D[11],D[12],D[13],D[14],D[15],D[16],D[17],D[18],D[19],D[20],D[21],D[22],D[23],D[24],D[25],D[26],D[27],D[28],D[29],D[30],D[31],D[32],D[33],D[34],D[35],D[36],D[37],D[38],D[39],D[40]
0350 IOLIST E$
0500 REM "FILES
0502 F0$="IMZ"+X3$(1,3); CALL "ZZTFLE",X3$,F0$,"",20,24,192; REM "File layout will be CS0
0503 F1$="IMY"+X3$(1,3); CALL "ZZTFLE",X3$,F1$,"",200,27,138; REM "File layout will be CS2
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O "+F0$+"  03O "+F1$+"  "+"12O IM0...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0610 GOSUB 6000
0615 GOSUB 7800
0630 GOSUB 8100
0640 CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0650 Z$="01O "+F2$+" "
0655 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0656,9900
0690 S9$="|"
0920 H0$=$0A$,H1$=$1A$,I9=0
0930 GOSUB 8150
1000 REM "Read Data file
1100 P0=POS(H0$=R1$); IF P0=0 THEN READ RECORD (Z[1],END=5000)R2$; R1$=R1$+R2$; GOTO 1100
1110 IF P0<>0 THEN R$=R1$(1,P0-1),R1$=R1$(P0+LEN(H0$)),C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
1112 IF R$(1,1)=$1A$ THEN GOTO 5000
1115 IF R$="" THEN GOTO 1100 ELSE IF R$(LEN(R$),1)=$0D$ THEN R$(LEN(R$),1)=" "
1180 GOSUB 2000
1900 GOTO 1000
2000 REM "Load CS0 data
2010 DIM A$(138),A[1]
2020 REM "customer code
2025 GOSUB 7900; A$(1,10)=X$
2026 A$(1,2)="00"
2040 REM "Ship to
2045 GOSUB 7900; A$(11,4)=X$
2060 REM "Ordered by
2065 GOSUB 7900
2080 REM "Req number
2085 GOSUB 7900; A$(15,10)=X$
2100 REM "Ship to Attn line
2110 GOSUB 7900; A$(39,30)=X$
2120 REM "Req date
2125 GOSUB 7900
2130 X$(1,1)=CHR(ASC(X$(1,1))-ASC("9")+ASC("J"))
2135 A$(33,6)=X$
2140 REM "Ship via code
2145 GOSUB 7900
2160 REM "Ship via description
2165 GOSUB 7900; A$(69,15)=X$
2180 REM "How to ship
2185 GOSUB 7900
2200 REM "Inside Delivery
2205 GOSUB 7900
2220 REM "Ship with
2225 GOSUB 7900
2800 REM "Fill out time & user stamps
2805 A$(90,8)="Teleform",A$(98,3)=X3$(40,3),A$(101,8)=DAY,A$(109,5)=STR(TIM:"#0.00")
2810 A$(114,24)=A$(90,24)
2900 REM "Write out CS0 record
2950 WRITE (Z[2],KEY=A$(1,24))IOL=0310
3000 REM "Get line information
3010 FOR L0=1 TO 10
3020 DIM B$(111),B[2]
3025 B$(1,24)=A$(1,24),B$(25,3)=STR(L0:"000"),B$(28,1)="I"
3050 GOSUB 3100
3090 NEXT L0
3095 GOTO 4000
3100 REM "Get info for 1 line
3120 REM "Customer code
3125 GOSUB 7900; B$(29,10)=X$
3140 REM "Item Code
3145 GOSUB 7900; B$(39,10)=X$
3160 REM "Ship From
3165 GOSUB 7900; B$(49,4)=X$
3180 REM "Unit of Measure
3185 GOSUB 7900; IF X$="" THEN X$="EACH"
3190 B$(107,4)=X$
3200 REM "Qty per unit of measure
3205 GOSUB 7900; X=1; X=NUM(X$,ERR=3206); IF X=0 THEN X=1
3210 B[1]=X
3220 REM "Quantity ordered
3225 GOSUB 7900; X=1; X=NUM(X$,ERR=3226); IF X=0 THEN X=1
3230 B[0]=X
3900 REM "Write out the line if not blank
3910 IF POS(" "<>B$(29,20))<>0 THEN WRITE (Z[3],KEY=B$(1,27))IOL=0320
3995 RETURN 
4000 REM "Make into a REQ
4090 CALL "FM2RTO",X3$,X4$,0,A$,A{ALL},F0$,F1$
4995 RETURN 
5000 REM "EOJ
5005 C=T; GOSUB 8150
5010 GOSUB 5200
5020 CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
5200 REM "COMPLETE CYCLE
5290 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(10,5),"This program loads Teleform Fax information into TopForm",
6025 PRINT @(10,8),"Teleform file name:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7800 REM "Get Teleform File name
7805 DIM F2$(8)
7809 X$="<F4> to exit"
7810 CALL "ZZENTR","S",F2{ALL},F2$,X4$,X3$,30,8,1,8,C0,"",X$,"","IM2REQ00","","",""
7811 IF ABS(C0)=4 THEN GOTO 9900
7820 CLOSE (14); OPEN (14,ERR=7810)F2$
7822 GOSUB 8100
7825 CLOSE (14)
7845 RETURN 
7900 REM "Get next field from R$, fields seperated by S9$ character(see 690)
7910 S9=POS(S9$=R$); IF S9=0 THEN X$=""; GOTO 7995
7915 X$=R$(1,S9-1),R$=R$(S9+1)
7995 RETURN 
8000 REM "Update Finance Charge for this invoice
8010 DIM A[14]; A$(11,7)="9000001",A$(19,1)="F",A[3]=F9,A[2]=F9,A[3]=F9,A[6]=F9
8020 WRITE (Z[2],KEY=A$(1,18),DOM=8021)IOL=0310; GOTO 8040
8030 A$(11,7)=STR(NUM(A$(11,7))+1:"0000000"); GOTO 8020
8040 RETURN 
8100 REM "Get approx total number of records T, by div'ing #sectors @512 per, by the record size we are expecting.
8110 CALL "ZZINFO",14,T9,X3$,T0,T,K,B,D,S0,S1,F$
8112 T=INT(T*512/192)
8115 PRINT @(10,11),'CL',"Estimate ",STR(T)," records to load."
8120 T0=INT(T*.01); IF T0<2 THEN T0=2
8125 T1=0,C1=0
8145 RETURN 
8150 REM "Display horiz. bar graph given total #, T and current #, C
8155 CALL "ZZBARG",X3$,"HG",19,5,50,T1,T,C
8195 RETURN 
8200 REM 'FIX DATE IN PLACE IN R$ BASED ON J
8205 IF POS(" "<>R$(J,10))=0 OR POS("/"=R$(J,10))=0 THEN GOTO 8245
8210 J$=R$(J,10)
8211 J1$=J$(1,POS("/"=J$)-1),J$=J$(POS("/"=J$)+1)
8213 IF LEN(J1$)<2 THEN J1$="0"+J1$
8215 J2$=J$(1,POS("/"=J$)-1),J$=J$(POS("/"=J$)+1)
8217 IF LEN(J2$)<2 THEN J2$="0"+J2$
8220 IF J1$>"12" THEN R$(J,10)=J2$+J$+J1$ ELSE R$(J,10)=J1$+J2$+J$
8245 RETURN 
8700 REM "Get commas out of number
8710 P=POS(","=Q$); IF P>0 THEN Q$=Q$(1,P-1)+Q$(P+1); GOTO 8710
8715 P=POS($0D$=Q$); IF P>0 THEN Q$=Q$(1,P-1)+Q$(P+1)
8720 P=POS("-"=Q$); IF P>0 THEN Q=POS(" "<>Q$); Q$(Q-1,1)="-",Q$(P,1)=""
8745 RETURN 
8920 DEF FNS$(Z9$)=Z9$(1,POS(S0$=Z9$+S0$)-1)
8930 DEF FNQ$(Z9$,Z9)=STR(NUM(Z9$):Z1$(1,Z9))
8940 DEF FNE$(Z9)=CHR(ASC("A")+INT(Z9/10))+STR(MOD(Z9,10))
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 DROP "CA2CVT",ERR=9921
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
