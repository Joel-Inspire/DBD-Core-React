0010 REM "Merging Program for FM3/FM4 (CNVSTAT)
0020 SETESC 9300; SETERR 9000
0035 REM "3.8 - 05/03/95 - 16.42 - jtb
0040 REM "Copyright 1995 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="CNVSTAT",X1$="Merging of Statistics Data"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST AN$,AN[0],AN[1],AN[2],AN[3],AN[4],AN[5],AN[6],AN[7],AN[8],AN[9],AN[10],AN[11],AN[12],AN[13]
0312 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14]
0400 REM 'PROMPT FOR COMPANY
0401 GOSUB 6000
0405 DIM COMP$(6),A[13],AN[13],B[15],BN[15]
0406 COMP$(4,3)=X3$(9,3)
0410 CALL "ZZENTR","SUX",A{ALL},COMP$,X4$,X3$,53,10,4,3,C0,"","{3"+X$,"","","","",""; IF ABS(C0)>6 THEN GOTO 0410
0420 IF ABS(C0)=4 THEN GOTO 9900
0449 REM 'ENTER STATS FILE
0450 CALL "ZZENTR","SUX",A{ALL},COMP$,X4$,X3$,53,11,1,3,C0,"","{3"+X$,"","","","",""; IF ABS(C0)>6 THEN GOTO 0410
0460 IF ABS(C0)=4 THEN GOTO 9900
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O "+COMP$+"N  02O "+COMP$+" 13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0620 GOSUB 8100
0640 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
1000 REM "Process file
1010 K$=KEY(Z[1],END=1500); READ (Z[1],KEY=K$)IOL=0310; REM 'GET MERGE IN FILE
1020 C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
1050 REM 'GET EXISTING RECORD
1052 DIM A[14]
1055 FIND (Z[2],KEY=K$,DOM=1056)IOL=0312; GOTO 1100
1060 REM 'NEW RECORD
1065 A$=AN$
1100 REM 'MERGE RECORDS
1110 FOR X=0 TO 13; A[X]=A[X]+AN[X]; NEXT X
1150 WRITE (Z[2],KEY=K$)IOL=0312
1190 GOTO 1000
2000 REM 'COMPLETE
2020 PRINT @(0,22),'CL',"Processing complete...",; GOTO 9900
5000 REM "EOJ
5010 C=T; GOSUB 8150
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6010 PRINT @(0,3),"This program will merge statistics records from a SOURCE file into",@(0,4),"a DESTINATION file. The source file must be named the 3 character stats   ",@(0,5),"file name + company ### + 'N'  (ie. AR9500N)",@(0,6),"If the record does not exist it will be created. If it does exist  ",@(0,7),"then the source figures will be added to the existing record",
6020 PRINT @(20,10),"Enter company number to process:",@(26,11),"Enter stats file to merge:",
6190 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F$
8115 PRINT @(0,7),"There are "+STR(T)+" records to process"
8129 REM "Set T0, we make sure T0 is > 1, because later on we MOD and look for avalue of 1. IF T0 is 1, then nothing would get reported. We look for a result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T0'th record to get the first report
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,C
8195 RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
