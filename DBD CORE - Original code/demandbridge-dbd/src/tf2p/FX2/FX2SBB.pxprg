0010 REM "Pass output file to spooler <FX2SBB>
0035 REM "5.0 - 02/21/02 - 14.140555 - dmm - SSP# 146274
0040 REM "Copyright 2002 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "OLD VERSION 12/20/93 SAVED AS FX2SBZ
1015 REM "F0=OUTPUT DEVICE
1016 REM "T0$=TAG1, T1$=TAG2
1019 REM " R0$=FAX DEVICE, R1$=ID NUMBER, R2$=PHONE NUMBER
1020 ENTER X3$,F0,T0$,T1$,R0$,R1$,R2$,X9
1021 REM 
1025 SETERR 9000
1040 R0$="",R1$="",R2$=""
1050 X9=-1,S9=0; DIM S$(40)
1060 REM "Get Fax Parameters from ZZPARM in P9$
1065 CLOSE (14); OPEN (14)"ZZPARM"; DIM P9$(727); READ (14,KEY=X3$(9,3)+"FX2")P9$(1)
1110 S7$="TMPFX"+FID(0)
1112 ERASE S7$,ERR=1113; GOTO 1112
1113 V0$="/usr/lib/pvx"; IF POS(X3$(9,3)="100500",3)<>0 THEN V0$="/usr/lib/pvx"
1115 IF P9$(695,1)>="3" THEN S8$=STP(P9$(697,30),1)+"/bin/vfx" ELSE S8$="/usr/vsifax/bin/fx"
1116 IF X3$(77,1)="D" THEN V0$=DLM+"pvx",S8$=STP(P9$(697,30),1)+DLM+"bin"+DLM+"vfx"
1120 S9$=" > "+V0$+"/UTILS/TMPFX"+FID(0)
1130 D0$=DLM+"tmp"+DLM
1135 D1$=V0$+DLM+"UTILS"+DLM
1138 F0$=FID(F0); IF LEN(F0$)<>2 THEN GOTO 9800
1140 IF X3$(77,1)<>"D" THEN F0$="PRN."+FID(0)+F0$ ELSE F0$="PRNTMP."+FID(0)+F0$
1200 REM "BUILD UNIX COMMAND STRING
1210 IF X3$(77,1)<>"D" THEN GOSUB 7000; F8$=F9$ ELSE F8$=D0$+F0$
1215 IF P9$(695,1)>="3" THEN S0$="PATH=$PATH:"+STP(P9$(697,30),1)+"/bin:"+STP(P9$(697,30),1)+"/spool; export PATH;" ELSE S0$="PATH=$PATH:/usr/vsifax/bin:/usr/vsifax/spool; export PATH;"
1230 IF X3$(77,1)="D" THEN S0$="cmd.exe /c more "+F8$+" | "+S8$ ELSE S0$=S0$+"cat "+F8$+"|"+S8$
1240 IF P9$(695,1)>="3" THEN S0$=S0$+" -Ftxt " ELSE S0$=S0$+" -Fmac " END_IF ; S0$=S0$+FNS$(P9$(655,40))+" "
1250 IF T0$>"" THEN S0$=S0$+" -t tg1="+$22$+T0$+$22$
1255 IF T1$>"" THEN S0$=S0$+" -t tg2="+$22$+T1$+$22$
1260 IF P9$(695,1)<"3" THEN S0$=S0$+" -N "
1280 IF X3$(77,1)<>"D" THEN S0$=S0$+S9$
1285 REM LET S0$=S0$+";rm "+F8$
1400 REM 
2000 REM "SEND INFO TO FAX SPOOLER
2040 IF X3$(77,1)<>"D" THEN X=SYS(S0$)
2100 REM "RETRIEVE Rx$ DATA
2110 S9=1
2120 IF X3$(77,1)<>"D" THEN GOSUB 7030
2140 F7$=F9$
2150 Q$="Fax Request id ",P=POS(Q$=F7$); IF P<>1 THEN GOTO 9800
2155 F7$=F7$(LEN(Q$)+1)
2160 P=POS("-"=F7$),R0$=F7$(1,P-1),F7$=F7$(P+1)
2165 P=POS(" "=F7$),R1$=F7$(1,P-1),F7$=F7$(P+1)
2170 REM "NUMBER IS IN PARENS
2175 F7$=F7$(2),P=LEN(F7$),R2$=F7$(1,P-1)
2200 REM 
3000 REM "CLOSE OUTPUT DEVICE
3020 CLOSE (F0)
3040 IF X3$(77,1)="D" THEN X=SYS(S0$)
4000 REM 
4090 GOTO 9900
7000 REM "GET TEMP FILE NAME FROM D0$ DIRECTORY IN F9$
7001 REM "WARNING: If more than one file, only first non-empty one sent
7002 REM 
7020 Q$="umask 0; du -s "+D0$+F0$+">"+D1$+S7$
7025 INVOKE Q$
7030 CLOSE (14); OPEN (14,ISZ=512)S7$
7040 READ RECORD (14,END=9800)F6$
7044 REM "NOTE: If empty SYSTEM type file, BASIC returns 512 $00$s
7045 IF POS($00$<>F6$)=0 THEN GOTO 9800
7050 P=POS($0A$=F6$); IF P=0 THEN GOTO 9800 ELSE F9$=F6$(1,P-1),F6$=F6$(P+1)
7055 IF S9=1 THEN CLOSE (14); RETURN 
7060 P=POS($09$=F9$); IF P>0 THEN S0=NUM(F9$(1,P-1)) ELSE P=POS(" "=F9$); IF P>0 THEN S0=NUM(F9$(1,P-1)) ELSE GOTO 9800
7065 IF S0=0 THEN GOTO 7045 ELSE F9$=F9$(P+1)
7070 IF F9$(1,1)=" " THEN F9$=F9$(2); GOTO 7070
7090 CLOSE (14)
7095 RETURN 
8910 DEF FNS$(Z9$)=Z9$(1,POS(S$=Z9$+S$)-1)
9020 PRINT 'RB',@(0,22),"ERROR ",ERR," AT LINE ",TCB(5)," ",; INPUT *
9040 EXIT ERR
9800 REM 
9830 CLOSE (F0); REM "Close fax device
9840 GOTO 9940
9900 REM 
9920 X9=0
9960 EXIT 
9980 CLOSE (14)
9985 ERASE S7$,ERR=9986
9999 END 
