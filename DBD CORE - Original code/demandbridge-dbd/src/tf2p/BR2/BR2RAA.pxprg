0010 REM "Bank Reconciliation Report <BR2RAA>
0015 REM "Prog Type: O -370032  
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 03/14/14 - 14.501388 - tma - SSP# 189432
0037 REM "189432-Ehancement to Bank rec - tag Monthly Disb and Detail trans  
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0110 X0$="BR2RAA",X1$="Bank Reconciliation Report"
0120 DIM A$(10),A[30],B$(127),B[3],C$(200),D$(52),E$(250),S$(40)
0140 Z0$="#,###,###.00-",Z1$="##,##0-",Z2$=Z0$(3)
0210 T=1,T0=1,T1=10
0220 W3=130
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0250 DEF FNC$(C)=CHR(32+INT(C/95))+CHR(32+MOD(C,95))
0255 DEF FND(Z9$)=(ASC(Z9$(1,1))-32)*95+ASC(Z9$(2,1))-32
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR BR1 - Bank Reconciliation Data
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30]
0320 IOLIST B$,B[0],B[1],B[2]
0325 REM "IOLIST FOR ZYB - Bank Account Masterfile
0330 IOLIST C$,C[0],C[1],C[2]
0335 REM "IOLIST FOR GL1 - General Ledger Master
0340 IOLIST D$
0345 REM "IOLIST FOR ZYA - Banking Institution Data
0350 IOLIST E$
0420 IOLIST X3$,X4$,V1$,V3$,V2$,IOL=0425
0425 IOLIST V$,V[0],V[1]
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01X BR1... 02O BR0...  03O ZYB...  04O GL1...  05O ZYA... 06O BR2...  07O GL6... 13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0530 FIND (Z[13],KEY=X3$(9,3)+"B/R",DOM=0531)P0$
0800 REM "Alternate Sort Info & total dim"
0810 DIM U[10,2],U0$(10),Y[5,1]
0820 CALL "ZZRSEQ",X3$,X0$,V1$,U,U0,U1,U{ALL},Y{ALL},T0,V0,U0$,T5$,U5$,S9$,X9; ON X9 GOTO 0821,9920
0825 IF V0>1 THEN GOSUB 6950
0850 T1=T1+1; DIM T[T0,T1]; U5$(1,20)=Q4$
0890 REM "Open Printer
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0950 REM " Position read"
0960 V2$=V$(72,3)+V$(72,3)
0970 DIM V4$(U); U9=1; FOR X=1 TO U0; V4$(U[X,0],U[X,1])=V2$(U9,U[X,1]); U9=U9+2*U[X,1]; NEXT X
0975 IF U1<>Z[1] THEN U1=Z[1]
0990 EXTRACT (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record and check range
1005 U$=KEY(U1,END=5000)
1010 FOR X=1 TO U0; U2=U[X,0],U3=U[X,0]*2-1,U4=U[X,1]
1020 IF U$(U2,U4)<V2$(U3,U4) THEN READ (U1); EXITTO 1000
1030 IF U$(U2,U4)>V2$(U3+U4,U4) THEN IF X=1 THEN EXITTO 5000 ELSE READ (U1); EXITTO 1000
1040 NEXT X
1100 REM "Get record"
1110 U9$=U$; IF U1=Z[1] THEN GOTO 1150 ELSE U$="",X=0
1120 IF Y[X,0]>0 THEN U$=U$+U9$(Y[X,0],Y[X,1]),X=X+1; GOTO 1120
1130 READ (U1)
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1160 CALL "BR2MAB",X3$,A$,Z[2],A{ALL}
1170 CALL "ZZ2FYP",X3$,A$(16,6),F0$
1200 REM "Reporting criteria
1330 DIM C$(LEN(C$)),C[3]; FIND (Z[3],DOM=1331,KEY=A$(1,3))IOL=0330
1340 D$(1)=""; FIND (Z[4],DOM=1341,KEY=C$(69,12))IOL=0340
1350 DIM E$(LEN(E$)); FIND (Z[5],DOM=1351,KEY=C$(34,4))IOL=0350
1400 REM "Subtotals"
1410 IF U2$="" THEN GOTO 1490 ELSE FOR T=1 TO U0; IF U9$(U[T,0],U[T,1])<>U2$(U[T,0],U[T,1]) THEN EXITTO 1420 ELSE NEXT T; GOTO 1480
1425 GOSUB 3100
1490 T=0,U2$=U9$
1500 GOSUB 6000
1515 Y5$(32)="Bank code:",Y5$(43)=A$(1,3),Y5$(82)="G/L Cash account:"
1520 CALL "ZZDISP","AXS",C$(69,12),"G/L",X3$,Y5$,"",100,0,X4$
1525 GOSUB 7300
1530 Y5$(22)="Account description:",Y5$(43)=C$(4,30),Y5$(82)="Acct Descr:",Y5$(94)=D$(13,35)
1535 GOSUB 7300
1540 Y5$(25)="Institution name:",Y5$(43)=E$(5,30)
1545 GOSUB 7300
1550 Y5$(22)="Bank account number:",Y5$(43)=C$(39,12),Y5$(83)="Fiscal Yr / Period to reconcile:",Y5$(117)=A$(10,4)+"/"+A$(14,2)
1555 GOSUB 7300
1560 Y5$(22)="Bank statement date:"
1565 Y5$(43)=FND$(A$(4,6))
1570 Y5$(96)="Period ending date: "+FND$(A$(16,6))
1575 GOSUB 7300
1580 Y5$(1)=T1$; GOSUB 7300
1590 Y5$(25)="** Bank Statement information **",Y5$(98)="** General Ledger Information **"
1595 GOSUB 7300
1600 Y5$(9)="Bank statement beginning balance:",Y5$(43)=STR(A[0]:Z0$),Y5$(76)="Adj. bank balance as of statement date:",Y5$(116)=STR(A[4]-A[19]+A[17]:Z0$)
1605 GOSUB 7300
1610 Y5$(11)=STR(A[5]:Z1$),Y5$(19)="Deposit items credited:",Y5$(43)=STR(A[1]:Z0$),Y5$(78)="Less outstanding checks (NON-FUTURE):",Y5$(116)=STR(A[14]:Z0$)
1615 GOSUB 7300
1620 Y5$(5)=STR(A[6]:Z1$),Y5$(13)="Checks and other debits paid:",Y5$(43)=STR(A[2]:Z0$),Y5$(84)="Plus Deposits not on statement:",Y5$(116)=STR(A[13]:Z0$)
1625 GOSUB 7300
1630 Y5$(4)="Service charges / adjustments applied:",Y5$(43)=STR(A[3]:Z0$),Y5$(117)="-------------"
1635 GOSUB 7300
1640 Y5$(43)="--------------",Y5$(87)="Equals balance to reconcile:",Y5$(116)=STR(A[4]-A[19]+A[17]-A[14]+A[13]:Z0$)
1645 GOSUB 7300
1650 Y5$(9)="Ending balance per bank statement:",Y5$(44)=STR(A[4]:Z0$)
1655 GOSUB 7300
1660 Y5$(78)="G/L Balance as of period ending date:",Y5$(116)=STR(A[12]:Z0$)
1665 GOSUB 7300
1670 Y5$(18)="Plus / Minus bank errors:",Y5$(44)=STR(-A[19]:Z0$),Y5$(84)="Plus / minus data entry errors:",Y5$(116)=STR(A[20]:Z0$)
1675 GOSUB 7300
1680 Y5$(13)="Plus / minus future G/L items:",Y5$(44)=STR(A[17]:Z0$),Y5$(68)="+/- Service charges / Adjustments to be booked:",Y5$(116)=STR(A[9]:Z0$)
1685 GOSUB 7300
1686 Y5$(97)="Minus NOF Checks:",Y5$(116)=STR(-A[21]:Z0$); GOSUB 7300
1688 Y5$(44)=T1$(1,LEN(Z0$)),Y5$(116)=T1$(1,LEN(Z0$)); GOSUB 7300
1690 Y5$(4)="Adj. bank balance as of statement date:",Y5$(44)=STR(A[4]-A[19]+A[17]:Z0$),Y5$(83)="Adjusted General Ledger balance:",Y5$(116)=STR(A[12]+A[20]-A[21]+A[9]:Z0$)
1695 GOSUB 7300
1700 REM 
1720 GOSUB 2000
1900 REM "Accumulate Totals"
1995 GOTO 1000
2000 REM "Print Detail
2010 X1$="Schedule of Reconciling Items"
2020 GOSUB 6000
2040 C1$="",C2$=""
2090 READ (Z[2],KEY=A$(1,3),DOM=2091)
2100 REM "
2120 READ (Z[2],END=2300)IOL=0320; IF B$(1,3)<>A$(1,3) THEN GOTO 2300
2130 IF B$(4,1)<>C1$ THEN T=1; GOSUB 7000
2132 IF V$(75,1)<>"Y" THEN IF B$(4,1)<>"3" THEN IF POS(B$(79,1)="Y")>0 THEN GOTO 2120
2135 Q0=0,Q1=0,Q2=0,Q3=0; IF B$(4,1)="3" THEN B$(100,6)=A$(10,6)
2160 Y5$(1)=B$(5,9),Y5$(12)=FND$(B$(49,6))
2165 Y5$(25)=B$(14,35)
2166 IF B$(4,1)="3" THEN IF B$(67,12)=S$(1,12) THEN B[0]=0
2168 Y5$(60)=STR(B[0]:Z0$),Y5$(86)=STR(B[1]:Z0$)
2170 IF B$(100,6)>A$(10,6) THEN Q0=B[0]; Y5$(73,LEN(Z0$))=STR(Q0:Z0$) ELSE IF B$(79,1)="N" THEN Y5$(112,1)="*",T[1,6]=T[1,6]+B[0]
2173 IF B$(79,1)="N" THEN Q3=B[0]; Y5$(99,LEN(Z0$))=STR(Q3:Z0$)
2177 REM IF B$(4,1)="3" THEN IF B$(67,12)<>S$(1,12) THEN LET Q2=B(0)
2178 IF POS(B$(79,1)="D")>0 THEN Q2=B[0]-B[1]
2179 IF Q2<>0 THEN Y5$(113)=STR(Q2:Z2$)
2180 IF B$(107,1)="Y" THEN Y5$(130,1)="Y"
2185 GOSUB 7300
2190 REM IF B$(100,6)>A$(10,6) THEN LET Y5$(47)="UPDATED IN FY/AP: "+B$(100,4)+"/"+B$(104,2); GOSUB 07300
2200 REM "Adjustments need to show G/L stuff
2205 IF B$(4,1)<>"3" THEN GOTO 2250
2210 CALL "ZZDISP","AXS",B$(67,12),"G/L",X3$,Y5$,"",1,14,X4$
2215 D$(13)="* Invalid Account *"; IF B$(67,12)=S$(1,12) THEN D$(13,35)="** Memo Entry Only - No G/L Posting" ELSE READ (Z[4],KEY=B$(67,12),DOM=2216)IOL=0340
2220 Y5$(26,35)=D$(13,35); GOSUB 7300
2240 GOSUB 7300
2280 T[1,0]=T[1,0]+1,T[1,1]=T[1,1]+Q0,T[1,2]=T[1,2]+B[0],T[1,3]=T[1,3]+B[1],T[1,4]=T[1,4]+Q2,T[1,5]=T[1,5]+Q3
2295 GOTO 2120
2300 REM 
2305 E9$="END"
2310 IF T[1,0]<>0 THEN GOSUB 7000
2320 REM LET T=0,C2$="Control totals:"; GOSUB 07000
2390 RETURN 
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "Subtotal Logic
3110 FOR T=U0 TO T STEP -1
3130 T$=FNS$(U5$(T*20+1,20)),T6$=U2$(U[T,0],U[T,1]),T7$=T5$(T*4+1,4); GOSUB 3050; IF POS(U0$(T+1,1)="23")>0 THEN GOSUB 7000 ELSE GOSUB 7100
3150 IF POS(U0$(T+1,1)="13")>0 THEN GOSUB 6000
3180 NEXT T
3195 RETURN 
3280 RETURN 
3380 RETURN 
3480 RETURN 
5000 REM "End of Print
5030 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
5035 IF W8=0 THEN GOTO 9900
5037 IF V3$(2,1)="T" THEN GOTO 9900
5040 CALL "ZZPROM","Y",X3$,S3,"Do you wish to update these items?","","",0; ON S3 GOTO 5041,9900
5060 GOTO 7500
5200 REM "
5220 GOSUB 6700
5295 GOTO 9900
6000 REM "Page header
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6090 ELSE ON W5 GOTO 6041,9910
6060 GOSUB 6100
6090 RETURN 
6100 REM "
6195 IF FID(W9)=FID(0) THEN PRINT 'SF',
6199 RETURN 
6200 REM "PRINT DETAIL HEADERS
6205 IF W>W0+8 THEN GOSUB 6000
6215 Y5$(1)=T1$; GOSUB 7300
6220 Y5$(66)="  Item",Y5$(76)="Future GL",Y5$(90)="Cleared",Y5$(106)="Open",Y5$(117)="Diff-",Y5$(126)="Bank"; GOSUB 7300
6225 Y5$(1)="Item No.",Y5$(15)=" Date",Y5$(27)="Payee / Description",Y5$(66)="Amount",Y5$(79)="Amount",Y5$(91)="Amount",Y5$(105)="Amount",Y5$(115)="erence",Y5$(124)="Error?"; GOSUB 7300
6285 Y5$(1)=T1$; GOSUB 7300
6290 RETURN 
6700 REM "Increment Audit Control No.
6705 F9=Z[13]
6710 A2$=X3$(146,6)+"  000100",A0$=X3$(9,3)+"AUDIT",A1$=""; EXTRACT (F9,KEY=A0$,DOM=6716)A0$,A1$
6715 A0=POS(A2$(1,6)=A1$,14); IF A0=0 THEN A1$=A1$+A2$; GOTO 6715 ELSE A2=NUM(A1$(A0+8,4))+1,A2$(9,4)=STR(A2:"0000"),A1$(A0,14)=A2$
6720 WRITE (F9,KEY=A0$)A0$,A1$
6740 RETURN 
6900 REM "SORT
6955 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6970 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,S9; ON S9 GOTO 6971,9920
6990 RETURN 
7000 REM "Totals logic
7001 IF C1$="" THEN GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7010 Y5$(60)=T1$(1,LEN(Z0$)-1),Y5$(73)=T1$(1,LEN(Z0$)-1),Y5$(86)=T1$(1,LEN(Z0$)-1),Y5$(99)=T1$(1,LEN(Z0$)-1),Y5$(112)=T1$(1,LEN(Z2$)-1); GOSUB 7300
7015 Q$=C2$+" "+Q2$+" ("+STR(T[T,0]:"0")+"):",Y5$(56-LEN(Q$))=Q$
7020 Y5$(60)=STR(T[T,2]:Z0$),Y5$(73)=STR(T[T,1]:Z0$),Y5$(86)=STR(T[T,3]:Z0$),Y5$(99)=STR(T[T,5]:Z0$),Y5$(112)=STR(T[T,4]:Z2$)
7040 GOSUB 7300
7050 Y5$(99)=STR(T[T,6]:Z0$)+"*"
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7105 IF E9$="END" THEN GOTO 7190
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7150 REM "Get next total
7155 C1$=B$(4,1),C2$="Check items"; IF C1$="2" THEN C2$="Deposit items" ELSE IF C1$="3" THEN C2$="Service Chg/Adj." ELSE IF C1$="4" THEN C2$="NOF Checks Paid"
7180 GOSUB 6200
7185 Y5$(1)="** Detail of "+C2$+" **"; GOSUB 7300
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     13O ZZPARM  "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7435 DIM V[1]
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V$(71,1)),W3$=V$(19,POS("   "=V$(19,40)+"  ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "Remove paid items
7505 PRINT @(0,20),'CE',@(12),"Phase I.  Updating into history:",
7510 READ (Z[2],KEY=V4$,DOM=7511)
7520 K$=KEY(Z[2],END=7700); READ (Z[2],END=7700)IOL=0320; IF B$(1,3)<>A$(1,3) THEN GOTO 7700
7525 IF B$(79,1)="N" THEN GOTO 7520 ELSE PRINT @(50,20),K$,
7530 IF POS(B$(79,1)="YD")=0 THEN GOTO 7520
7540 IF B$(4,1)="3" THEN IF B$(67,12)<>S$(1,12) THEN GOSUB 7600
7545 IF B$(100,6)>A$(10,6) THEN GOSUB 8000; GOTO 7520; REM "Handle cleared future item
7550 REM "Remove and update history
7555 IF B$(4,1)<>"3" THEN IF STP(B$(61,6),3," ")="" THEN B$(61,6)=A$(10,6)
7557 IF B$(4,1)<>"3" THEN IF B$(79,1)="D" THEN IF STP(B$(122,6),3," ")="" THEN B$(122,6)=A$(10,6); REM "FY/AP of discrepancy
7558 IF B$(4,1)="1" THEN CALL "BR2UAA",X3$,X4$,B$,"" ! SSP#189432
7560 IF B$(4,1)<>"3" THEN F9=Z[6]; GOSUB 7800; B$(61,6)="",B$(122,6)=""
7563 IF B$(4,1)<>"4" THEN GOTO 7566 ELSE IF B[0]<0 THEN B$(4,1)="2",B$(80,20)="NOF Deposit" ELSE B$(4,1)="1",B$(80,20)="NOF Check"
7565 B$(118,1)="1",B[0]=-B[0],B[1]=-B[1]; F9=Z[2]; GOSUB 7800; GOTO 7570
7566 IF B$(79,1)="D" THEN B[0]=B[0]-B[1],B[1]=0,B$(118,1)="2",B$(122,6)=A$(10,6),F9=Z[2]; GOSUB 7800; REM "Seq 01 written in 7800, still need to remove original
7570 REMOVE (Z[2],KEY=K$)
7590 GOTO 7520
7600 REM "POST adjustments to GL6
7610 DIM G$(40); G$(1,1)="A",G$(2,2)=F0$(7,2),G$(4,2)=F0$(5,2)
7615 G$(6,2)="BR",G$(8,6)=X3$(178,6)
7619 REM "Uses the item date for the transaction date
7620 G$(14,12)=B$(67,12),G$(26,2)="  ",G$(28,6)=B$(49,6)
7625 G$(34,2)=G$(6,2)
7630 G1$="Adj "+FNS$(B$(5,9))+" "+B$(14,35)
7645 J0=J0+1,G$(26,2)=FNC$(J0)
7650 WRITE (Z[7],KEY=G$(1,27),DOM=7645)G$,G1$,-B[1]
7655 G$(14,12)=C$(69,12)
7660 J0=J0+1,G$(26,2)=FNC$(J0)
7665 WRITE (Z[7],KEY=G$(1,27),DOM=7660)G$,G1$,B[1]
7690 RETURN 
7695 REM 
7700 REM "Update bank account record ZYB
7705 IF A$(1,LEN(A$))=S$(1,LEN(A$)) THEN GOTO 7790
7710 DIM C[3]; EXTRACT (Z[3],KEY=A$(1,3))IOL=0330
7720 C$(121,6)=A$(4,6),C[1]=A[4]
7730 WRITE (Z[3],KEY=A$(1,3))IOL=0330
7740 REMOVE (Z[1],KEY=A$(1,3))
7790 GOTO 7900
7800 REM "Write BR0 or BR2 - Don't write history if months to retain is 00
7805 IF P0$(7,2)<"01" AND F9=Z[6] THEN GOTO 7840
7810 B$(119,2)=""
7820 WRITE (F9,KEY=B$(1,13)+B$(119,2),DOM=7821)IOL=0320; GOTO 7840
7830 IF B$(119,2)<"99" THEN B$(119,2)=STR(NUM(B$(119,2))+1:"00"); GOTO 7820 ELSE ESCAPE ; REM "More than 99 entries with this check #!!!
7840 RETURN 
7900 REM "Process History File and remove ones based upon parameter
7905 PRINT @(0,20),'CE',@(12),"Phase II.  Removing old history:",
7908 READ (Z[6],KEY="",DOM=7909)
7910 IF P0$(7,2)<"01" THEN GOTO 7990
7920 K$=KEY(Z[6],END=7990); READ (Z[6],KEY=K$)IOL=0320
7925 PRINT @(50,20),K$,
7930 IF FNF(B$(61,6))>=FNF(A$(10,6))-NUM(P0$(7,2)) THEN GOTO 7920
7970 REMOVE (Z[6],KEY=K$)
7980 GOTO 7920
7990 GOSUB 6700
7995 GOTO 9900
8000 REM "Handle cleared future item
8010 IF STP(B$(61,6),3," ")="" THEN B$(61,6)=A$(10,6),B$(118,1)="3"; WRITE (Z[2],KEY=B$(1,13)+B$(119,2))IOL=0320
8045 RETURN 
8905 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8920 DEF FNB$(Z9$)=X3$(214-6*NUM(Z9$),1)
8930 DEF FNF(Z9$)=NUM(Z9$(1,4))*12+NUM(Z9$(5,2))
8940 DEF FNS$(Z$)=Z$(1,POS("   "=Z$+"   ")-1)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 IF Y5=68 THEN RETRY ELSE ON C9 GOTO 1140,2040
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 IF W5=2 THEN GOTO 0010
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 IF %GUI THEN EXIT 
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 RUN "ZMENU"
9999 END 
56000 REM "189432-Ehancement to Bank rec - tag Monthly Disb and Detail trans  
