0010 REM "Order Booking Report <FM2RGA>
0035 REM "5.7 - 09/12/12 - 11.069166 - tma - SSP# 257981
0037 REM "257981-Getting an error running the monthly order booking report.  
0040 REM "Copyright 2012 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 SETERR 9000; SETESC 9300
0110 X0$="FM2RGA",X1$="Order Booking Report"
0120 DIM A$(43),A[8],C$(200),K[3],D$(12),D[1]
0140 Z0$="##,###,###.00-",Z1$="###.00-"
0210 T=1,T0=1,T1=4
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR FSA - Order Booking Report
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8]
0325 REM "IOLIST FOR AR1 - Customer Masterfile
0330 IOLIST C$
0400 IOLIST K$,K[0],K[1],K[2],K[3]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0425 IOLIST V$,V[0]
0430 IOLIST D$,D[0],D[1]
0440 IOLIST FT2$,FT2[0],FT2[1]
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 D0$=FND$(V0$(72,6))
0520 W0$="Report Ending date:  "+D0$
0530 Y$="01X FSA...  03O AR1...  04O AR3...  05OSFSB...  06OSFSC...  07O FSD...  08O FT2...  "; IF V1$(1,3)<>Y$(5,3) THEN Y$=Y$+"02O "+V1$+"  "
0550 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0600 REM "Read Parameters
0610 READ (Z[13],KEY=X3$(9,3)+"A/R",DOM=9900)P$
0620 READ (Z[13],KEY=X3$(9,3)+"F/M",DOM=9900)P1$
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[3]; U=16,U0=3,U[0]=0,U[1]=2,U[2]=6,U[3]=8,T0=2,V1=2,V2=14,U0$="Y   ",T5$="S   AO/P        ",U5$="Division",U6$="Order"; GOTO 0850
0830 DIM U[4]; U=26,U0=4,U[0]=0,U[1]=2,U[2]=10,U[3]=6,U[4]=8,T0=3,V1=2,V2=10,V3=14,U0$="Y    ",T5$="S   AA/RAO/P    ",U5$="Division",U6$="Customer",U7$="Order"; GOTO 0850 ! SSP203444
0840 DIM U[5]; U=30,U0=5,U[0]=0,U[1]=2,U[2]=4,U[3]=10,U[4]=6,U[5]=8,T0=4,V1=2,V2=4,V3=16,V4=8,U0$="YY   ",T5$="S   S   AA/RAO/P",U5$="Division",U6$="Salesperson",U7$="Customer",U8$="Order" ! SSP203444
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0865 READ (Z[5],KEY="",DOM=0880)
0870 IF V0$(78,1)<>"Y" THEN GOTO 0890
0880 GOSUB 8000
0890 REM "Open Printer
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record and check range
1005 U$=KEY(U1,END=5000); READ (U1)
1006 IF U$(1,2)="  " THEN GOTO 1000
1007 A=NUM(U$(1,2),ERR=1000)
1010 U3=1,U2=1
1020 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1030 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN EXITTO 1000
1040 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U=1 THEN EXITTO 5000 ELSE EXITTO 1000
1050 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1180,1180,1130,1150
1130 READ (Z[1],KEY=U$(1,2)+U$(13,15))IOL=0310; GOTO 1190
1150 READ (U1,KEY=U$)IOL=0400
1155 READ (Z[1],KEY=U$(1,2)+U$(17,6)+U$(23,9))IOL=0310
1160 A[5]=K[0],A[6]=K[1],A[7]=K[2],A[8]=K[3]
1170 GOTO 1190
1185 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1195 IF A$(3,6)>V0$(72,6) OR A$(48,1)<>"Y" THEN GOTO 1000
1200 REM "Reporting criteria
1330 DIM C$(LEN(C$)); FIND (Z[3],DOM=1331,KEY=A$(18,10))IOL=0330
1400 REM "Subtotals"
1425 GOSUB 3100
1430 ON T GOTO 1490,1440,1440,1450,1460
1440 GOSUB 6300
1490 T=0
1510 IF W+2>W0 THEN GOSUB 6000
1515 CALL "ZZDISP","AX",A$(9,8),"O/P",X3$,X$,"",0,0,X4$; Y5$(3)=X$; IF A$(49,1)<>" " THEN Y5$(1,2)=A$(49,1)+"-"
1520 Y5$(13)=FND$(A$(3,6))
1530 IF V0<>2 THEN CALL "ZZDISP","AX",A$(18,10),"A/R",X3$,X$,"",0,0,X4$; Y5$(24)=X$,Y5$(35)=C$(11,35)
1540 IF V0$(79,1)="Y" AND P1$(192,1)="Y" THEN G0=A[5]-A[6]+A[8] ELSE G0=A[5]-A[6]
1550 Y5$(68)=STR(A[5]:Z0$),Y5$(82)=STR(A[6]:Z0$),Y5$(96)=STR(G0:Z0$)
1560 IF A[5]<>0 THEN Y5$(111)=STR(G0*100/A[5]:Z1$)
1570 Y5$(117)=STR(A[7]:Z0$)
1590 GOSUB 7300
1900 REM "Accumulate Totals"
1990 T[T0,0]=T[T0,0]+1,T[T0,1]=T[T0,1]+A[5],T[T0,2]=T[T0,2]+A[6],T[T0,3]=T[T0,3]+G0,T[T0,4]=T[T0,4]+A[7]
1993 DIM A[8]
1995 GOTO 1000
2500 REM "Subtitle
2510 IF W+5>W0 THEN GOSUB 6000
2520 GOSUB 7300
2525 IF V0<>2 THEN Y5$(24)="Customer"
2530 Y5$(76)="Order",Y5$(85)="+ --------------- Estimated --------------- +"
2535 GOSUB 7300
2540 Y5$(4)="Order",Y5$(15)="Date"
2550 IF V0<>2 THEN Y5$(24)="Code",Y5$(36)="Customer name" ! ssp#251751
2560 Y5$(75)="Amount",Y5$(90)="Cost"; IF V0$(79,1)="Y" THEN Y5$(98)="Adjusted GP" ELSE Y5$(97)="Gross Profit"
2565 Y5$(113)="GP%",Y5$(120)="Commission"
2570 GOSUB 7300
2590 RETURN 
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break
3110 IF V1=0 THEN RETURN ELSE IF T0$="END" THEN GOTO 3120 ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; IF P$(13,1)<>"Y" THEN GOSUB 7100; GOTO 3160 ELSE GOSUB 7000
3155 IF U0$(1,1)="Y" THEN GOSUB 6000
3157 IF T0$="END" THEN RETURN 
3160 U1$=U9$(1,V1)
3195 RETURN 
3200 REM "Second highest order break
3210 IF V2=0 THEN RETURN ELSE IF T0$="END" THEN GOTO 3220 ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3232 X$=U2$ ! SSP#232958
3235 IF V0=2 THEN CALL "ZZDISP","AX",U2$(1,10),"A/R",X3$,X$,"",0,0,X4$ ! SSP#232958
3240 IF V0=1 THEN CALL "ZZDISP","AX",U2$(7,8),"O/P",X3$,X$,"",0,0,X4$ ! SSP#232958
3250 T=2,T$=U6$,T6$=X$,F0=1; GOSUB 3050; GOSUB 7000 ! SSP#232958
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3257 IF T0$="END" THEN RETURN 
3260 U2$=U9$(V5,V2),F0=0
3280 RETURN 
3300 REM "Next to lowest break
3310 IF V3=0 THEN RETURN ELSE IF T0$="END" THEN GOTO 3320 ELSE IF U3$=U9$(V6,V3) THEN GOSUB 3400; RETURN 
3320 GOSUB 3420
3330 IF U3$="" THEN GOTO 3360
3335 IF V0=3 THEN CALL "ZZDISP","AX",U3$(1,10),"A/R",X3$,X$,"",0,0,X4$ ! SSP#232958
3340 IF V0=2 THEN CALL "ZZDISP","AX",U3$(7,8),"O/P",X3$,X$,"",0,0,X4$ ! SSP#232958
3350 T=3,T$=U7$,T6$=X$; GOSUB 3050; GOSUB 7000 ! SSP#232958
3355 IF U0$(3,1)="Y" THEN GOSUB 6000
3357 IF T0$="END" THEN RETURN 
3360 U3$=U9$(V6,V3)
3380 RETURN 
3400 REM "Lowest
3410 IF V4=0 THEN RETURN ELSE IF T0$="END" THEN GOTO 3420 ELSE IF U4$=U9$(V7,V4) THEN RETURN 
3430 IF U4$="" THEN GOTO 3460
3432 X$=U4$ ! SSP#232958
3440 IF V0=3 THEN CALL "ZZDISP","AX",U4$(1,8),"O/P",X3$,X$,"",0,0,X4$ ! SSP#232958
3450 T=4,T$=U8$,T6$=X$; GOSUB 3050; GOSUB 7000 ! SSP#232958
3455 IF U0$(4,1)="Y" THEN GOSUB 6000
3460 U4$=U9$(V7,V4)
3480 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5210
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; GOSUB 7000
5200 REM "
5210 IF V0<>3 THEN GOTO 5295
5215 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
5216 IF V3$(1,2)="IT" OR W8=0 THEN GOTO 9900
5220 X$="Do you wish to update the Order Booking Detail to the Monthly Summary file?"
5225 IF %MULT_RPT_DRV_MODE OR %TOD THEN GOTO 9900 ! SSP 228228 SSP 240570
5230 CALL "ZZPROM","Y",X3$,S3,X$,"","",0
5240 IF S3=0 THEN GOSUB 8500
5295 GOTO 9900
6000 REM "Page header
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W4$,W5$; ON W5 GOTO 6041,9900
6060 GOSUB 6100
6070 IF F0<>1 THEN GOSUB 2500
6080 GOSUB 7300
6090 RETURN 
6100 REM "
6195 IF FID(W9)=FID(0) THEN PRINT 'SF',
6199 RETURN 
6300 REM "Salesperson
6310 IF V0=1 THEN GOTO 6390 ELSE IF V0=2 THEN GOTO 6400
6320 IF W+2>W0 THEN F0=1; GOSUB 6000; F0=0
6330 DIM E$(200); FIND (Z[4],KEY=K$(3,4),DOM=6331)E$
6340 Y5$(5)="Salesperson:  "+K$(3,4),Y5$(25)=E$(5,35)
6350 GOSUB 7300; GOSUB 7300
6360 GOSUB 2500
6390 RETURN 
6400 REM "Customer
6420 CALL "ZZDISP","AX",A$(18,10),"A/R",X3$,X$,"",0,0,X4$
6425 IF W+2>W0 THEN F0=1; GOSUB 6000; F0=0
6427 GOSUB 7300
6430 Y5$(5)="Customer:  "+X$+"  "+C$(11,35)
6440 GOSUB 7300; GOSUB 7300
6450 GOSUB 2500
6490 RETURN 
7000 REM "Totals logic
7001 IF T=T0 THEN IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7010 Y5$(68)=T1$(1,13),Y5$(82)=T1$(1,13),Y5$(96)=T1$(1,13),Y5$(111)=T1$(1,5),Y5$(117)=T1$(1,13)
7020 GOSUB 7300
7030 Y5$(10)=T3$(1,T0+1-T)+" "
7040 IF T$<>"" THEN Y5$(11)=T$+" "+"Totals:"
7050 Y5$(68)=STR(T[T,1]:Z0$),Y5$(82)=STR(T[T,2]:Z0$),Y5$(96)=STR(T[T,3]:Z0$)
7060 IF T[T,1]<>0 THEN Y5$(111)=STR(T[T,3]*100/T[T,1]:Z1$)
7070 Y5$(117)=STR(T[T,4]:Z0$)
7080 GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600),T4=MOD(T3,60),T3=INT(T3/60)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")+":"+STR(T4:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 W=W+1
7330 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     13O ZZPARM  "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7435 DIM V[1]
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1) ! SSP#257981
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
7900 REM "Create FT2, Order Booking by Customer records
7910 DIM FT2$(16),FT2[1]; FT2$=K$(7,16)
7915 EXTRACT (Z[8],KEY=FT2$,DOM=7916)IOL=0440
7920 FT2[0]=FT2[0]+K[0],FT2[1]=FT2[1]+K[1]
7925 WRITE (Z[8],KEY=FT2$)IOL=0440
7930 DIM DIV$(16),DIV[1]; DIV$=K$(7,2)+"z"+DIM(7)+K$(17,6)
7935 EXTRACT (Z[8],KEY=DIV$,DOM=7936)DIV$,DIV[0],DIV[1]
7940 DIV[0]=DIV[0]+K[0],DIV[1]=DIV[1]+K[1]
7945 WRITE (Z[8],KEY=DIV$)DIV$,DIV[0],DIV[1]
7950 DIM TOT$(16),TOT[1]; TOT$="zzz"+DIM(7)+K$(17,6)
7955 EXTRACT (Z[8],KEY=TOT$,DOM=7956)TOT$,TOT[0],TOT[1]
7960 TOT[0]=TOT[0]+K[0],TOT[1]=TOT[1]+K[1]
7965 WRITE (Z[8],KEY=TOT$)TOT$,TOT[0],TOT[1]
7995 RETURN 
8000 REM "Sort
8010 IF S9$="X" THEN GOTO 8090 ELSE S0=Z[1],S1=Z[6]
8020 CALL "ZZSORT",X3$,"","FSC...",S9$,"","",0,0,0,S0,S1,S9; ON S9 GOTO 8021,9920
8100 REM "CRITERIA CHECK
8110 F1$=FID(0); IF F1$(1,1)<>"G" THEN PRINT @(0,18),'CE',; GOSUB 8800
8120 CALL "ZZINIT",STR(Z[5]:"00")
8130 CALL "ZZDAPP",X3$,"!AAR2XAA"
8140 READ (Z[1],KEY="",DOM=8141)
8150 K1$=KEY(Z[1],END=8180); READ (Z[1])IOL=0310
8151 C=C+1; IF MOD(C,T6)=1 THEN GOSUB 8850
8152 A=NUM(K1$(1,2),ERR=8150)
8153 IF A$(3,6)>V0$(72,6) THEN GOTO 8150
8155 EXTRACT (Z[1],KEY=A$(1,17))IOL=0310
8170 GOSUB 8200
8173 A$(48,1)="Y"
8174 WRITE (Z[1],KEY=A$(1,17))IOL=0310
8175 GOTO 8150
8180 READ (Z[5],KEY="",DOM=8181)
8183 P0$=X3$(9,3)+"R"+DAY+STR(TIM*100:"0000")
8185 WRITE (Z[5],KEY="")P0$
8190 IF F1$(1,1)<>"G" THEN C=T5; GOSUB 8850; PRINT @(30,18),'CL'
8195 CALL "ZZDAPP",X3$,"!DAR2XAA"
8199 RETURN 
8200 REM "Commissions
8210 DIM L[6],M[20]
8220 L[0]=A[5],L[1]=A[6],L[2]=A[7],L[3]=A[8]
8230 CALL "AR2XAA",X3$,L{ALL},0,3,M{ALL},A{ALL},0,4
8240 FOR I0=0 TO 4
8250 DIM K$(31),K[3]
8253 IF A$(28+I0*4,4)="    " THEN GOTO 8280
8255 K$=A$(1,2)+A$(28+I0*4,4)+A$(18,10)+A$(3,6)+A$(9,9)
8258 I1=I0*4
8260 K[0]=M[I1],K[1]=M[I1+1],K[2]=M[I1+2],K[3]=M[I1+3]
8270 WRITE (Z[5],KEY=K$)IOL=0400
8280 NEXT I0
8290 RETURN 
8500 REM "Write to Monthly Order Summary
8501 IF V2$(1,2)="  " AND V2$(13,2)<>"  " THEN V2$(1,2)=V2$(13,2)
8502 IF V2$(3,2)="~~" AND V2$(23,2)<>"~~" THEN V2$(3,2)=V2$(23,2)
8503 U1$=""
8505 DIM S[1],T[1]
8510 READ (Z[5],KEY=V2$(1,2),DOM=8511)
8520 READ (Z[5],END=8680)IOL=0400
8525 IF K$(1,2)>V2$(3,2) THEN GOTO 8680
8528 U3=1,U2=1
8530 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
8532 IF K$(U2,U[U9])<V2$(U3,U[U9]) THEN EXITTO 8520
8534 IF K$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN EXITTO 8520
8536 NEXT U9
8538 READ (Z[1],KEY=K$(1,2)+K$(17,6)+K$(23,9))IOL=0310
8540 IF A$(48,1)<>"Y" OR A$(3,6)>V0$(72,6) THEN GOTO 8520
8550 DIM D$(12),D[1]; D$=K$(1,6)+K$(17,6)
8560 EXTRACT (Z[7],KEY=D$,DOM=8561)IOL=0430
8570 D[0]=D[0]+K[0],D[1]=D[1]+K[1]
8580 WRITE (Z[7],KEY=D$(1,12))IOL=0430
8590 DIM S$(12),S[1],T$(12),T[1],KEYS$(12)
8600 KEYS$=K$(1,2)+"z   "+K$(17,6)
8610 EXTRACT (Z[7],KEY=KEYS$,DOM=8611)KEYS$,S[0],S[1]
8620 S[0]=S[0]+K[0],S[1]=S[1]+K[1]
8630 WRITE (Z[7],KEY=KEYS$)KEYS$,S[0],S[1]
8640 T$="zzz   "+K$(17,6)
8645 EXTRACT (Z[7],KEY=T$,DOM=8646)T$,T[0],T[1]
8650 T[0]=T[0]+K[0],T[1]=T[1]+K[1]
8655 WRITE (Z[7],KEY=T$(1,12))T$,T[0],T[1]
8657 IF P$(117,1)="Y" THEN GOSUB 7900; REM "Create FT2, Order booking by customer records
8670 GOTO 8520
8700 REM "Remove booked FSA records
8710 READ (U1,KEY="",DOM=8711)
8720 U$=KEY(U1,END=8750); READ (U1)
8725 READ (Z[1],KEY=U$(1,2)+U$(17,6)+U$(23,9),DOM=8720)IOL=0310
8728 U3=1,U2=1
8730 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
8732 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN EXITTO 8720
8734 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN EXITTO 8720
8736 NEXT U9
8738 IF A$(48,1)="Y" AND A$(3,6)<=V0$(72,6) THEN REMOVE (Z[1],KEY=A$(1,17))
8740 GOTO 8720
8750 CALL "ZZINIT",STR(Z[5]:"00")+STR(Z[6]:"00")
8790 RETURN 
8800 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T5 to total # to do (by calling ZZINFO) and T50 to the reporting interval (report every T50 number of records) this is typically 2% of total; T5his also prints a message (@8115),soadjust accordingly
8810 CALL "ZZINFO",Z[1],T9,X3$,T5,T6,K,B,D,S0,S1,F$
8830 T6=INT(T5*.02); IF T6<=1 THEN T6=2
8845 RETURN 
8850 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T5 and current #, C, and time counter T7 (T7 is init'ed here and used here, just don't use it somewhere else)
8855 CALL "ZZBARG",X3$,"HG",19,10,50,T7,T5,C
8895 RETURN 
8905 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8940 DEF FNS$(Z$)=Z$(1,POS("   "=Z$+"   ")-1)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 IF Y5=68 THEN RETRY ELSE ON C9 GOTO 1140,2040
9900 REM "End program
9910 IF V0<>3 THEN CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF NOT(NUL(Y4$)) THEN RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
56000 REM "203444-Order Booking Report in sequence 3 is not printing          
56001 ! "228228-Ability to split a report into multiple PDFs based on sales 
56002 REM "232958-Order Booking report is displaying a compressed date        
56003 REM "240570-GUI Printing - Support for queueing and scheduling reports. 
