0010 REM "Order Line Releated Maintenance <FM2ODZ>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 05/21/24 - 15.227485 - crg - SSP# 307483
0037 REM "307483-DBD-457:Support for IMS price in purchase order line        
0040 REM "Copyright 2024 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0087 REM "C0=command, 1=add, -1=maint delete  (modify line) -2=delete line stuff only, -3=-1&-2, 99=Rebuild mode (FM2UT9)
0088 REM "A$,A(ALL)=sales order line detail, B$=sales order data
0090 CLEAR ; SETERR 0100; ENTER X3$,C0,A$,A{ALL},B$
0100 SETERR 9000
0110 X0$="FM2ODZ"
0120 DIM S0$(10),H[3]
0125 DIM PO3[4]
0130 BY_LINE$=""
0140 IF A$="ALL" THEN F9$="Y"
0145 REM "I8$ > "" if coming fro order/invoice interface screen
0150 P=POS("~"=MID(B$,500)); IF P>0 THEN P=P+499 ! SSP#259101!SSP#261806 
0160 IF P=0 THEN I8$="" ELSE I8$=B$(P+1),B$=B$(1,P-1)
0170 FID_0$=FID(0); IF %IN_WEBEC$="Y" OR %OS2INV=1 OR %IN_BOFILL=1 OR %IN_GUI$="Y" THEN FID_0$(1,1)="G"; REM SSP 187122, SSP 180159
0210 IF FID_0$(1,1)<>"G" AND NOT(%GUI) THEN PRINT @(0,22),'CE',@(23,22),"Adjusting sales order information", ! SSP 202907
0230 DIM DFT[4] ! [197451]-moved the dim
0300 REM + Begin Building IOLISTs
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29]
0311 IOLIST A$,A[0],STR(A[1]),A[2],A[3],STR(A[4]),A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],STR(A[15]),A[16],A[17],STR(A[18]),A[19],A[20],A[21],STR(A[22]),A[23],A[24],A[25],A[26],A[27],A[28],A[29]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15]
0340 IOLIST D$,D[0],D[1],D[2],D[3],D[4],D[5],D[6],D[7]
0341 IOLIST D$,D[0],STR(D[1]),D[2],D[3],STR(D[4]),D[5],D[6],D[7]
0380 IOLIST H$,H[0],H[1],H[2],H[3]
0390 IOLIST I$,I[0],I[1],I[2],I[3],I[4],I[5],I[6],I[7],I[8],I[9],I[10],I[11],I[12],I[13],I[14],I[15],I[16],I[17],I[18],I[19],I[20],I[21],I[22],I[23],I[24],I[25],I[26],I[27]
0391 IOLIST I$,STR(I[0]),I[1],I[2],I[3],I[4],I[5],I[6],I[7],STR(I[8]),I[9],I[10],I[11],I[12],I[13],I[14],I[15],I[16],STR(I[17]),I[18],I[19],I[20],STR(I[21]),I[22],I[23],I[24],I[25],I[26],I[27]
0400 IOLIST J$,J[0],J[1],J[2],J[3],J[4],J[5],J[6],J[7],J[8],J[9],J[10],J[11],J[12],J[13],J[14],J[15],J[16],J[17],J[18],J[19],J[20]
0401 IOLIST J$,J[0],STR(J[1]),STR(J[2]),J[3],J[4],J[5],J[6],J[7],J[8],J[9],J[10],J[11],J[12],J[13],J[14],J[15],J[16],J[17],J[18],J[19],J[20]
0410 IOLIST A2$,A2[0],A2[1],A2[2],A2[3],A2[4],A2[5],A2[6],A2[7],A2[8],A2[9],A2[10],A2[11],A2[12],A2[13],A2[14],A2[15],A2[16],A2[17],A2[18],A2[19],A2[20],A2[21],A2[22],A2[23],A2[24],A2[25],A2[26],A2[27],A2[28],A2[29]
0411 IOLIST O$,O[0],STR(O[1]),O[2],O[3],O[4],O[5]
0412 IOLIST DFT$,DFT[0],DFT[1],DFT[2],DFT[3]
0480 IOLIST FMPS$ ! [197451]-IOLIST for FMPS
0485 IOLIST PO3$,PO3[0],PO3[1],PO3[2],PO3[3] ! [197451]-IOLIST for PO3
0499 REM + End Building IOLISTs
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X FS2...  02O FS5...  03O FS6...  04O FS7...  05O FS8...  06O IC1...  08O PO3...  09O PO2...  10O FS9...  11O PO6... 12O AR1...  13O ZZPARM  25O FMP...  26O FM0...  27O RFS...  28O RF3...  29O FS1...  30OSFS2...  "
0515 IF I8$>"" THEN Z$=Z$+"14O FS3...  "; REM "SSP# 111764
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 RFS=Z[27],RF3=Z[28]
0540 READ (Z[13],KEY=X3$(9,3)+"F/M")S9$
0541 READ (Z[13],KEY=X3$(9,3)+"AR1")AR1PARM$ ! SSP 222237
0545 GB$=""; FIND (Z[13],KEY=X3$(9,3)+"G/B",DOM=0546)GB$
0585 RT_PARM$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,RT_PARM$
0588 PO_ACK$="N"; IF S9$(323,1)="Y" THEN PO_ACK$="Y" ! SSP 245709
0590 HAS_WMS=0; IF (FN%HAS_MODULE("WW")) THEN HAS_WMS=1 ! SSP268441
0790 IF F9$<>"Y" THEN GOTO 0900
0791 IF POS(X3$(146,6)="SM2RASSMGRASOS2INVICGZSU")<>0 THEN GOTO 0796; REM "SSP 183935 Don't ask to reprint if importing from United; SSP 302176
0794 IF (B$(2,1)="Y" AND B$(246,4)<>"WEB ") OR POS("D"=B$(232,5))>0 THEN GOTO 0796; REM "Background processing, don't ask suspend packing list question. WO233493, if background processed type order but is an EC type, then we need to go to 795, we will check there to see if we are currently in EC and if so we will go to next line.
0795 IF %IN_WEBEC$="Y" THEN GOTO *NEXT END_IF ; IF S9$(242,1)="Y" AND I8$="" AND B$(128,1)<>"Y" AND C0>0 AND C0<>99 THEN GOSUB 8400; IF B$(314,1)="Y" THEN B$(128,1)="S" ELSE IF B$(128,1)="A" THEN IF %IN_GUI$="Y" THEN PROCESS "ZZGASK","../ZZG/ZZ.EN",MSG("FMGODH_10"),_ANS$; IF _ANS$="YES" THEN B$(128,1)="S" ELSE B$(128,1)="N" END_IF ; ELSE CALL "ZZPROM","Y",X3$,S3,"Suspend Packing List Printing?","","",0; IF S3=0 THEN B$(128,1)="S" ELSE B$(128,1)="N"; REM " SSP 180159.  WO233493, coming here now from 794 if background processed type order, but if we are currently in EC we need to go on and not ask any questions. ! SSP246794 jdf
0800 REM "Process all lines"
0805 DIM A[29]
0810 READ (Z[1],KEY=B$(118,8),DOM=0811)
0820 K1$=KEY(Z[1],END=0870)
0825 IF B$(118,8)<>K1$(1,8) THEN GOTO 0870
0830 READ (Z[1],KEY=K1$)IOL=0310
0840 DID_GOSUB=1; GOSUB 0900; DID_GOSUB=0
0860 GOTO 0820
0890 GOTO 9905
0900 REM "
0901 W9$=A$(10,1)
0902 IF POS(B$(47,1)="RW")>0 OR POS(X3$(9,3)="092499568543537539541542584585601602605449590522629130",3)>0 OR %IN_WEBEC$="Y" THEN IF A$(10,1)<>"8" OR POS(X3$(9,3)="470474",3)<>0 THEN W9$="0" END_IF ELSE W9$=A$(10,1); REM "SSP138569, if company 543 then change 1st position of whse to 0 even if it's an O1 type order !SSP#216139!223653!SSP#228035 SSP 232445!SSP#296654 SSP302093!SSP#307376
0903 IF POS(X3$(9,3)="020")>0 THEN W9$=A$(10,1)
0904 IF S9$(285,1)="Y" THEN W9$=A$(10,1)
0905 IF POS(X3$(9,3)="499583")<>0 AND A$(10,1)="8" THEN W9$="0"; REM "SSP156216
0907 O9$=A$(147,8)
0910 IF FID_0$(1,1)<>"G" AND NOT(%GUI) THEN PRINT @(30,23),"Order Line ",A$(6,3),
0920 IF ABS(C0)=1 OR C0=99 AND POS(A$(9,1)=P$+" ")=0 THEN P$=P$+A$(9,1)
0929 REM "If a Work Order, build up string for FW2ODL, cus#(10)+inv #(20)+order#(8)+location(4)
0930 IF B$(43,1)="Y" AND POS(A$(155,1)="IC")>0 THEN W$=W$+B$(6,10)+A$(161,10)+A$(19,10)+O9$+A$(10,4)
0990 ON C0 GOTO 1000,2000
1000 REM "Delete out
1002 ! SSP268441 IF POS(A$(155,1)="MN")=0 THEN CALL "UPDRF3;DEL_FOR_WMS",ERR=*NEXT,RF3,A$
1003 IF HAS_WMS THEN IF POS(A$(155,1)="MNS")=0 THEN IF B$(63,1)=DIM(1) THEN CALL "UPDRFS;DELETE_ORDER_LINE",ERR=*NEXT,RFS,A$(147,8),A$(6,3) ! SSP268441, now remove directly from RFS. Make sure no invoicing or picking in progress.
1005 IF C0=-2 THEN GOTO 3000
1020 IF A$(155,1)="M" THEN GOTO 1120
1040 GOSUB 7000; REM "Adjust customer's open order amount
1060 GOSUB 7050; REM "Remove FS5 sort key
1080 GOSUB 8600; REM "Take off committed
1100 IF C0=-1 AND I8$<>"" THEN REMOVE (Z[14],KEY=I8$(1,8)+O9$+A$(6,3)+" ",DOM=1101); REM "Remove FS3 record
1120 GOSUB 7500; REM "Remove PO6 records
1300 IF C0=-3 THEN GOTO 3000
1900 REM "EOD
1990 GOTO 9900
2000 REM "Add in
2004 IF HAS_WMS THEN IF POS(A$(155,1)="MSN")=0 AND A$(9,1)=DIM(1) AND A[0]>0 THEN IF B$(314,1)<>"Y" AND B$(194,1)<>"Y" AND B$(63,1)=DIM(1) THEN CALL "UPDRFS;CREATE_RFS",ERR=*NEXT,X3$,X4$,A$,A{ALL},B$(6,10) ! SSP268441, create RFS directly now, first check if WMS installed then if order not suspended or on hold and not invoicing or picking in progress
2005 ! SSP268441 IF POS(A$(155,1)="MSN")=0 AND A[0]>0 THEN IF B$(314,1)<>"Y" THEN CALL "UPDRF3;UPD_FOR_WMS",ERR=*NEXT,RF3,Z[26],Z[12],A$,B$(6,10),X3$,RFS ! SSP180032 - NOTE:if WMS, routine changes print flag to not print p/l, 248845 - If suspended order then do not allow picking in WMS, SSP#249444 - Prevent special charges from showing up in RFS
2010 IF A$(9,1)<>" " AND A$(219,1)<>"Y" THEN GOSUB 8100; REM "Set up PO line
2020 REM "IF A$(155,1)="M" THEN GOTO 02200
2120 IF A$(155,1)<>"M" THEN GOSUB 7000; REM "Adjust customer open order amount
2140 IF A$(155,1)<>"M" THEN GOSUB 7050; REM "FS5 sort key
2160 GOSUB 7300; REM "Write out FS6,7,8,9 records
2180 IF A$(155,1)<>"M" THEN GOSUB 8600; REM "Put on committed
2200 GOSUB 7500; REM "PO6 file
2210 IF A$(155,1)<>"M" AND (C0=1 OR C0=99) AND S9$(280,1)="Y" AND B$(54,1)="B" AND B$(240,1)="Y" THEN CALL "FM2ICA",X3$,X4$,"FM2ODZ",C0,A$,A{ALL},B$; REM "WO111482, Inventory Costing
2900 REM "EOA
2990 GOTO 9900
3000 REM "Do delete only things
3010 GOSUB 8000; REM "Remove shipping instructions
3020 GOSUB 7100; REM "Remove FS6,7,8,9 records
3025 CALL "BM2DEL",ERR=3026,X3$,X4$,B$(118,8)+A$(6,3)
3030 IF A$(2,1)="Y" THEN GOSUB 3100
3040 IF F9$="Y" THEN TYPE$="ALL" ELSE TYPE$=A$
3041 IF B$(54,1)="B" THEN CALL "FM2ICA",X3$,X4$,"FM2ODZ",C0,TYPE$,A{ALL},B$; REM "WO111482, Inventory Costing
3050 ! F PO_ACK$="Y" THEN CALL "FM2ODW;ORDER_LINE_DELETE",X3$,A$(147,8)+A$(6,3),"O"
3090 GOTO 9900
3100 REM "Lot Inventory
3110 IF A$(155,1)="I" THEN DIM C0$(10) ELSE C0$=A$(161,10)
3120 J$=C0$+A$(19,10)+A$(10,4)+O9$+A$(6,3)
3130 IF A[0]<0 THEN GOSUB 3200 ELSE CALL "IC2LCA",X3$,"","D",0,J$,0
3140 RETURN 
3200 REM "Remove Order entry returns from ICN
3203 Z$="02C FS5...  07O ICN...  "
3204 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 3205,9900
3205 READ (Z[7],KEY=C0$+A$(19,10),DOM=3206)
3210 J8$=KEY(Z[7],END=3240); READ (Z[7],KEY=J8$)J9$
3215 IF J9$(1,20)<>C0$+A$(19,10) THEN GOTO 3240
3220 IF J9$(110,11)<>A$(147,8)+A$(6,3) THEN GOTO 3210
3225 REMOVE (Z[7],KEY=J8$,DOM=3226)
3230 GOTO 3210
3241 Z$="07C ICN...  02O FS5...  "
3242 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 3243,9900
3245 RETURN 
7000 REM "Adjust customer open order amount
7002 IF POS(B$(54,1)="BS")>0 THEN GOTO 7040; REM "To be consistant before change
7005 DIM C$(100),C[15]
7010 EXTRACT (Z[12],KEY=B$(6,10),DOM=7040,ERR=7040)IOL=0330
7020 C[13]=C[13]+SGN(C0)*A[6]
7025 IF C[13]<0 THEN C[13]=0
7030 WRITE (Z[12],KEY=B$(6,10))IOL=0330; IF RT_PARM$<>"" THEN CUST$=B$(6,10); CALL "RT2WOR",ERR=7031,X3$,X4$,CUST$,"AR1...","U",B$(6,10)
7040 RETURN 
7050 REM "Add or rem FS5 sort key
7054 IF A$(14,1)="Y" AND (C0=-1 OR C0=-3) THEN GOTO 7056
7055 IF POS(A$(155,1)="IC ")=0 THEN GOTO 7090; REM "WO106216, changed from IC
7060 IF A$(155,1)="I" THEN DIM I$(20); I$(11,10)=A$(19,10) ELSE I$=A$(161,10)+A$(19,10)
7062 IF A$(155,1)=" " THEN IF STP(I$(1,10),3," ")="" THEN I$(1,10)=B$(6,10); REM "WO106216
7065 I$=I$+A$(10,4)+A$(147,8)+A$(6,3)
7070 IF C0=-1 OR C0=-3 THEN REMOVE (Z[2],KEY=I$,DOM=7071) ELSE GOTO 7080
7075 REMOVE (Z[2],KEY=A$(161,10)+I$(11),DOM=7076)
7078 GOTO 7090
7080 WRITE (Z[2],KEY=I$)
7090 RETURN 
7100 REM "Remove FS6,7,8,9 records
7102 GOSUB 7200; IF T$=" " THEN GOTO 7190
7104 FOR I=1 TO LEN(T$)
7105 DIM D$(163),D[7] ! SSP 307357
7110 IF T$(I,1)="M" THEN A$(19,10)="M"+B$(120,6)+A$(6,3)
7115 D$(1)=B$(6,10)+T$(I,1)+A$(19,10)+B$(118,8)
7120 GOSUB 7450
7130 READ (Z[3],KEY=D$(1,30),DOM=7131)IOL=0340
7132 REM "If invoiced already then DO NOT remove
7133 IF POS(" "<>D$(40,14))<>0 THEN A[10]=A[10]-A[0]; EXITTO 7300
7140 REMOVE (Z[3],KEY=D$(1,30),DOM=7141)
7141 IF RT_PARM$<>"" THEN CUST$=D$(1,10); CALL "RT2WOC",ERR=7142,X3$,X4$,CUST$,"FS6...","D",D$(1,30)
7160 REMOVE (Z[4],KEY=A$(29,3)+D$(11,1)+D$(12,10)+D$(1,10)+D$(22,8)+D$(30,1),DOM=7161)
7165 REMOVE (Z[5],KEY=D$(1,10)+D$(22,8)+D$(11,11)+D$(30,1),DOM=7166)
7170 REMOVE (Z[10],KEY=D$(22,8)+D$(12,10)+D$(30,1),DOM=7171)
7174 IF T$(I,1)="M" THEN A$(19,10)=DIM(10)
7175 NEXT I
7180 CALL "FM2ODS",X3$,D$,"D"
7190 RETURN 
7200 REM "Set T$ to type for FS6 records
7204 T$=" "
7205 ON POS(A$(155,1)=" CINSM") GOTO 7240,7210,7215,7220,7225,7230,7235
7210 T$="B"; GOTO 7240
7215 T$="D"; GOTO 7240
7220 T$="F"; GOTO 7240
7225 T$="H"; GOTO 7240
7230 T$="J"; GOTO 7240
7235 T$="M"; GOTO 7240
7240 DIM T[1]; T[0]=A[10]
7250 REM "Hidden Warehouse
7255 IF S9$(195,1)<>"Y" THEN GOTO 7290
7265 REM "Hidden Warehouse situation coming up
7280 CALL "FM2MAK",X3$,X4$,A$,A{ALL},B$,B{ALL},T$,T{ALL}
7290 RETURN 
7300 REM "Setup & write out FS6,7,8 records
7302 IF C0=99 THEN RETURN 
7303 IF A$(155,1)="M" AND S9$(263,1)<>"Y" THEN RETURN 
7304 GOSUB 7200
7306 FOR I=1 TO LEN(T$)
7308 DIM D$(163),D[7] ! SSP 307357
7309 IF T$(I,1)="M" THEN A$(19,10)="M"+B$(120,6)+A$(6,3); REM "Message line
7310 D$(1)=B$(6,10)+T$(I,1)+A$(19,10)+B$(118,8)+" "+A$(29,3)+B$(16,6)
7312 GOSUB 7450; GOSUB 7400
7315 D$(54,15)=B$(28,15),D$(81)=A$(113,6)+A$(94,10)+A$(90,4)+A$(124,4)
7318 D$(106,5)="D"+A$(10,4),D$(111,1)=B$(54,1)
7319 D$(114,3)=A$(6,3),D$(117,3)="001" ! SSP307357 
7320 D[0]=T[I-1],D[1]=A[4],D[2]=A[5],D[3]=A[13]
7321 IF D[1]=0 THEN D[1]=A[6],D[4]=A[3]; REM " GOTO 07325
7322 IF A[1]=0 THEN D[4]=A[3]; GOTO 7325 ELSE D[4]=A[1]
7323 IF A[2]<>0 AND A[2]<>A[5] THEN D[4]=D[4]*A[5]/A[2] ELSE D[4]=D[4]
7325 WRITE (Z[3],KEY=D$(1,30))IOL=0341; IF RT_PARM$<>"" THEN CUST$=D$(1,10); CALL "RT2WOC",ERR=7326,X3$,X4$,CUST$,"FS6...","U",D$(1,30)
7330 WRITE (Z[4],KEY=D$(31,3)+D$(11,1)+D$(12,10)+D$(1,10)+D$(22,8)+D$(30,1))
7334 WRITE (Z[5],KEY=D$(1,10)+D$(22,8)+D$(11,11)+D$(30,1))
7335 IF T$(I,1)="M" THEN A$(19,10)=DIM(10)
7336 NEXT I
7337 CALL "FM2ODS",X3$,D$,"W"; GOSUB 7350
7338 REM IF POS(" "<>A$(161,10))>0 AND A$(161,10)<>B$(6,10) THEN CALL "FM2UBI",X3$,A$(19,10),B$(6,10),B$(155,1),A$(161,10),"",0
7340 RETURN 
7350 REM "Write out FS9 record if necessary
7360 IF POS(A$(155,1)="MN")>0 OR (POS(A$(155,1)=" C")>0 AND NOT(NUL(A$(161,10))) AND A$(161,10)<>B$(6,10)) THEN GOTO *NEXT ELSE GOTO 7390 ! Write FS9 entries for "NM" line types; and for custom items with a FROM customer (but additionally check against ordering customer in FS1 because GUI currently writes a FROM cust code to FS2 for ALL custom items) ! 221331
7365 DIM D1$(81)
7370 D1$(1)=D$(22,8)+D$(12,10)+D$(30,1)+A$(29,61)
7375 WRITE (Z[10],KEY=D1$(1,19))D1$
7390 RETURN 
7400 REM "Check for old record and set up last inv stuff
7410 READ (Z[3],KNO=0,KEY=D$(1,30),DOM=7440)IOL=0340; D$(34,6)=B$(16,6) ! SSP193156, make sure order date is correct/current; merged SSP 208056 fix from GUI for 180159
7420 IF POS(" "<>D$(40,14))=0 THEN REMOVE (Z[4],KEY=D$(31,3)+D$(11,1)+D$(12,10)+D$(1,10)+D$(22,8)+D$(30,1),DOM=*NEXT) ! ssp#287219
7425 D$(31,3)=A$(29,3) ! SSP#287219
7440 RETURN 
7450 REM "Compute seq no for FS6 record
7460 D9=NUM(A$(6,3),ERR=7461)
7465 IF D9>=90 THEN D9=D9+10
7470 IF D9>206 THEN D9=206
7480 D$(30,1)=CHR(D9+48)
7490 RETURN 
7500 REM "Carton Label Printing
7501 IF POS(X3$(146,6)="SM2RASSMGRASOS2INVICGZSU")<>0 THEN RETURN ! SSP 302176 
7505 IF C0=99 OR I8$<>"" OR POS("D"=B$(232,5))>0 THEN RETURN ; REM "D=coming from BO fill report or other process that we don't want to reprint anything
7506 IF B$(314,1)="Y" THEN RETURN ; REM SSP# 189962 - If suspended order, do not allow (re)prints
7510 X$="C"+O9$+A$(9,1)+A$(6,3); IF %IN_GUI$="Y" THEN M$=MSG("FMGODH_11",A$(6,3),A$(50,40)) ELSE M$="Reprint Carton Labels for line "+A$(6,3)+": "+A$(50,40); REM "180159
7515 IF C0<0 OR B$(194,1)="Y" THEN GOSUB 7900; GOTO 7550
7520 IF (A[9]<=0 AND H[1]<=0) THEN GOTO 7550; REM "Mod cwj 3/3/98 SSP# 085719 - add H(1) to if ...
7525 IF B$(127,1)="Y" AND NUM(PRT$)<=1 THEN IF %IN_GUI$="Y" THEN CALL "FMGODH.1",M$,S3,"Y"; ELSE CALL "ZZPROM",".RP",X3$,S3,M$,"","",0; END_IF ; PRT$=STR(S3); IF S3=1 OR S3=3 THEN GOSUB 7900; GOTO 7550; REM W/O 115875; REM "S3=0 REPRINT,S3=1 DO NOR REPRINT,S3=2 REPRINT ALL, S3=3 NO TO ALL
7527 IF PRT$="3" THEN GOSUB 7900; GOTO 7550; REM W/O 115875
7530 WRITE (Z[11],KEY=X$)
7551 WHSE$=A$(12,2)
7554 ! [197451]-Removed lines 7552-7555 since they were the same as the 8700 routine.
7555 GOSUB 8700 ! [197451]-CHECK FOR SUBSTITUTE WHSE CODE
7557 GOSUB CREATE_PO6_RECORDS ! [197451] get the pick priority
7560 X$="L"+W9$+A$(11,1)+WHSE$+PICK_PRIORITY$+O9$+A$(6,3) ! [197451] added the pick priority
7561 IF %IN_GUI$="Y" THEN M$=MSG("FMGODH_12",A$(6,3),A$(50,40)) ELSE M$="Reprint Packing List for line "+A$(6,3)+": "+A$(50,40) ! [197451] moved7561: this line from 7560 to here
7562 IF A$(10,4)="DS  " OR A$(9,1)<>" " THEN W9$=A$(215,1); IF POS(X3$(9,3)="092499568543537539541542584585601602605449590",3)>0 THEN W9$="0" ! SSP#260577
7563 IF (A$(9,1)<>" " AND POS("P"=A$(156,5))>0 AND A$(10,4)="DS  ") OR (A$(9,1)<>" " AND A$(10,4)=DIM(4) AND A$(215,4)<>DIM(4)) THEN X$(2,4)=W9$+A$(216,3); REM "W9$ value set in line 7562 above if 10,4 was DS was setting to D 920;!SSP#237301 SSP260577
7564 IF GB$<>"" THEN GOSUB 7600; REM "Special GB stuff
7565 IF C0<0 AND POS("B"=B$(232,5))<>0 THEN GOTO 7590 ELSE IF C0<0 OR B$(194,1)="Y" THEN GOSUB 7900; GOTO 7590 ELSE IF POS("P"=A$(156,5))=0 THEN GOTO 7590
7567 IF S9$(305,1)="Y" THEN GOTO 7570
7568 IF %IN_WEBEC$<>"Y" THEN IF B$(128,1)="Y" AND NUM(PRT1$)<=1 THEN IF %IN_GUI$="Y" THEN CALL "FMGODH.1",M$,S3,""; ELSE CALL "ZZPROM",".RP",X3$,S3,M$,"","",0; END_IF ; PRT1$=STR(S3); IF S3=1 OR S3=3 THEN GOSUB 7900; GOTO 7590; REM W/O 115875, SSP 224654
7569 GOTO 7575
7570 IF %IN_WEBEC$<>"Y" THEN IF B$(128,1)="Y" AND NUM(PRT1$)<=1 AND %OF2INV$<>"YES" AND %OS2INV$<>"YES" THEN IF %IN_GUI$="Y" THEN CALL "FMGODH.1",M$,S3,""; ELSE CALL "ZZPROM",".RZ",X3$,S3,M$,"","",0; PRINT @(0,21),'CL',; END_IF ; PRT1$=STR(S3); IF S3=4 THEN GOSUB 7700; GOTO 7590 ELSE IF S3=1 OR S3=3 THEN GOSUB 7900; GOTO 7590; REM W/O 115875, SSP 216996, SSP 180159
7575 IF PRT1$="3" THEN GOSUB 7900; GOTO 7590; REM W/O 115875
7578 READ (Z[11],KEY=X$+"P",DOM=7580); GOTO 7590
7580 IF S9$(305,1)<>"Y" AND B$(128,1)<>"S" THEN WRITE (Z[11],KEY=X$); IF B$(53,1)="Y" THEN WRITE (Z[11],KEY="B"+A$(10,1)+X$(3,3)+X$(8))
7581 IF B$(128,1)<>"S" AND S3<>3 AND (S9$(305,1)="Y" AND POS(X$(14,3)=PO6_DEL$)>0 OR BY_LINE$="") THEN WRITE (Z[11],KEY=X$); IF B$(53,1)="Y" THEN WRITE (Z[11],KEY="B"+A$(10,1)+X$(3,3)+X$(8))
7582 BY_LINE$=""
7590 RETURN 
7600 REM "Do special key for Gold Bond, WO106451
7602 REM "If first line use it's product code, else look for line 001 product code. If no line 001 use this line's prod  code, MUST reposition file on Z[1]!!
7605 PROD_CODE$=A$(29,3); IF A$(6,3)="001" THEN GOTO 7620
7610 DIM FS2$(200); FS2$(29,3)=A$(29,3); FIND (Z[1],KEY=A$(147,8)+"001",DOM=7611)FS2$
7611 PROD_CODE$=FS2$(29,3)
7612 READ (Z[1],KEY=A$(147,8)+A$(6,3))
7620 X$="L!"+B$(195,1)+PROD_CODE$+DIM(2)+X$(2); REM "Two spaces are for now unused # copies field, we pick it up during printing
7645 RETURN 
7700 REM "SELECT BY LINE TO REPRINT PACKING LIST SSP# 158577
7701 PO6_DEL$="",BY_LINE$="YES"
7702 PRINT @(0,6),'CE',
7703 DIM LIST$(153)
7704 DIM Q$(8)
7705 Q$(1,8)=A$(147,8)
7707 LIST$(1,8)=Q$(1,8),LIST$(9,3)="001"
7708 M$="Enter F4 to End or F1 to search"
7710 CALL "ZZENTR","ZUX",A{ALL},LIST$,X4$,X3$,1,6,9,3,C0,"","{2"+M$,Q$,"FS2DAB02","06FS2A","",""; IF C0=-4 THEN C0=1; GOTO 7850
7717 DIM A2[29]
7720 READ (Z[1],KEY=LIST$(1,11),DOM=7710)IOL=0410; WHSE$=A2$(12,2) ! SSP#261608
7721 IF POS("P"=A2$(156,5))=0 THEN CALL "ZZPROM",".4",X3$,Z,"Line: "+A2$(6,3)+" does not contain the packing list print flag, line will not print.","","",0; GOTO 7750 ! SSP178848, this prompt is to reprint PL's only, but if you entered a line that didn't contain the P print flag, it was still printing on a PL.
7725 GOSUB 8700 ! [197451]-CHECK FOR SUBSTITUTE WHSE CODE
7727 GOSUB CREATE_PO6_RECORDS ! [197451] get the pick priority
7730 X$="L"+W9$+A2$(11,1)+WHSE$+PICK_PRIORITY$+O9$+A2$(6,3) ! [197451]-Added the pick priority
7740 IF B$(128,1)<>"S" THEN WRITE (Z[11],KEY=X$); IF B$(53,1)="Y" THEN WRITE (Z[11],KEY="B"+A2$(10,1)+X$(3,3)+X$(8))
7741 PO6_DEL$=PO6_DEL$+A2$(6,3)
7743 CALL "ZZPROM",".4",X3$,Z,"Order: "+A2$(147,8)+" Line: "+A2$(6,3)+" will be Reprinted","","",0
7750 GOTO 7702
7851 PRINT @(0,6),'CE',
7860 S3=3
7862 REM " REMOVE THE REST OF THE LINES
7863 READ (Z[1],KEY=LIST$(1,8),DOM=*NEXT)
7864 FS2_KEY$=KEY(Z[1],END=7899)
7880 IF LIST$(1,8)<>FS2_KEY$(1,8) THEN GOTO 7899
7885 READ (Z[1],KEY=FS2_KEY$)IOL=0410
7887 IF POS(A2$(6,3)=PO6_DEL$,3)>0 THEN GOTO 7864
7890 REMOVE (Z[11],KEY="L"+A2$(10,4)+PICK_PRIORITY$+FS2_KEY$(1,8)+A2$(6,3),DOM=*NEXT); REM " SSP# 161966 [197451] added pick priority
7895 GOTO 7864
7899 RETURN 
7900 REM "Remove PO6 Records
7905 F8$=X$
7910 REMOVE (Z[11],KEY=F8$,DOM=7911)
7920 IF F8$(1,1)<>"L" THEN REMOVE (Z[11],KEY=F8$+"P",DOM=7921)
7930 IF B$(53,1)="Y" AND F8$(1,1)="L" THEN F8$(1,1)="B"; GOTO 7910
7935 IF F8$(1,1)="L" AND B$(128,1)<>"Y" THEN B$(128,1)=" "
7940 RETURN 
8000 REM "Remove shipping instructions
8010 K$=B$(118,8)+" "+A$(6,3)
8020 READ (Z[8],KEY=K$,DOM=8021)
8030 K0$=KEY(Z[8],END=8045)
8035 IF K0$(1,LEN(K$))<>K$ THEN GOTO 8045
8040 REMOVE (Z[8],KEY=K0$,DOM=8030); GOTO 8030
8045 RETURN 
8100 REM "Setup P/O line"
8102 IF C0=99 THEN RETURN 
8106 DIM I$(170),I[27]
8110 IF A$(155,1)="M" THEN I$(1,1)=A$(1,1),I$(6,1)=A$(3,1),I$(7,3)=A$(6,3),I$(10,1)="M",I$(11,3)=A$(6,3),I$(28,40)=A$(50,40),I$(82,9)=A$(147,8)+A$(9,1); GOTO 8160 ! SSP268108, was I$(1)=A$(1,6)+A$(6,3)+"M", Carton Label Item Code, Line Complete?, line number, line type, sales order line number
8120 IF MID(AR1PARM$,76,1)="Y" THEN GOSUB VENDOR_COMM_SUB ! SSP 222237
8140 REM "Setup line
8142 I$(10,1)="C"
8145 EXTRACT (Z[9],KEY=A$(147,8)+A$(9,1)+A$(6,3),DOM=8146)IOL=0390
8146 ! IF %IN_GUI$="Y" THEN READ (Z[29],KEY=A$(147,8),KNO=0)FS1$; ESCAPE ; BB=0; IF FS1$(54,1)="B" AND A(7)<>A(0) THEN BB=A(7) END_IF ; A[10]=A[0]+A(16)+BB ! SSP 220814, SSP 180159 ! SSP 220814, SSP 224766
8147 ! IF %IN_GUI$="Y" AND %FROM_ARGEAC.1 THEN A[10]=A[10]-A(7) ! SSP 220814, SSP 180159
8150 I$(1,9)=S0$(1,5)+I$(6,1)+A$(6,3),I$(11)=A$(6,3)+A$(18,1),I$(25,3)=A$(29,3),I$(28)=A$(50,40)+A$(104,9)+A$(120,4)+A$(119,1),I[0]=A[1],I[1]=A[0],I[11]=A[10],I[2]=A[2],I[3]=A[9],I[4]=A[3],I$(1,1)=A$(1,1),I$(101,10)=A$(19,10)
8151 I$(82,9)=A$(147,8)+A$(9,1),I$(15,2)=A$(48,2),I$(17,8)=A$(32,8),I$(111,8)=A$(40,8),I[5]=A[12]
8155 IF A$(14,1)="Y" THEN I$(91,10)=B$(6,10) ELSE IF A$(155,1)="C" THEN I$(10,1)="I",I$(91,10)=A$(161,10) ELSE I$(10,1)=A$(155,1)
8156 IF A$(14,1)="Y" THEN IF STP(A$(161,10),1)>"" AND A$(161,10)<>B$(6,10) THEN I$(91,10)=A$(161,10) ! SSP182112
8170 IF A$(18,1)<>"Y" THEN GOTO 8280
8175 REM "Check special shipping for D locations.
8180 K$=A$(147,8)+" "+A$(6,3)
8190 READ (Z[8],KEY=K$,DOM=8191)
8200 K0$=KEY(Z[8],END=8280)
8210 IF K0$(1,LEN(K$))<>K$ THEN GOTO 8280
8220 READ (Z[8],KEY=K0$)IOL=0380
8230 IF H$(11,1)<>"D" THEN GOTO 8270
8239 REM "NOT SURE ABOUT SETTING I$(91,10) BEFORE THE CALL"
8240 IF A$(155,1)<>"S" THEN I$(10,1)="I",I$(91,10)=B$(6,10); CALL "FM2ODY",X3$,X4$,I$,I{ALL},A$,A{ALL} ! SSP 212523 - Do not setup IC0, IC1 for 'S'pecial charges lines
8250 GOSUB 8300
8270 GOTO 8200
8280 REM 
8285 I(24)=A(26); I[25]=A[27]; WRITE (Z[9],KEY=I$(82,9)+I$(7,3))IOL=0391 ! [200130],307483
8290 RETURN 
8300 REM "FIX ORDER LINE
8320 IF A$(155,1)="I" THEN A$(155,1)="C"
8330 A$(161,10)=B$(6,10); WRITE (Z[1],KEY=A$(147,8)+A$(6,3))IOL=0311
8390 RETURN 
8400 REM "Any Packing Lists to print?
8410 READ (Z[1],KEY=B$(118,8),DOM=8411)
8420 READ (Z[1],END=8480)X$
8430 IF X$(147,8)<>B$(118,8) THEN GOTO 8480
8440 IF POS("P"=X$(156,5))>0 THEN B$(128,1)="A"; GOTO 8490
8450 GOTO 8420
8490 RETURN 
8500 SET_READONLY:
8510 SET_PARAM 'XI'
8520 SET_READONLY_END:RETURN 
8530 CLEAR_READONLY:
8535 SET_PARAM -'XI'
8540 CLEAR_READONLY_END:RETURN 
8600 REM "Put on or take off committed based on C0
8602 IF A$(9,1)<>" " OR POS(A$(155,1)="IC")=0 THEN GOTO 8640
8604 IF POS(" "<>A$(10,4))=0 OR A$(10,2)="DS" THEN GOTO 8640
8605 DIM J$(58),J[20]
8606 IF A$(155,1)="I" THEN DIM C0$(10) ELSE C0$=A$(161,10)
8609 J$(1)=C0$+A$(19,10)+A$(10,4)
8610 EXTRACT (Z[6],KEY=J$(1,24),DOM=8611)IOL=0400
8620 J[7]=J[7]+SGN(C0)*A[0],J[11]=J[11]+SGN(C0)*A[8]
8625 IF A[7]>A[0] THEN J[7]=J[7]+SGN(C0)*(A[7]-A[0]) ! SSP# 151754 if shipped > ordered then committed was understated, add in the difference here if needed
8630 WRITE (Z[6],KEY=J$(1,24))IOL=0401
8635 IF RT_PARM$>"" THEN IF RT_PARM$(7,1)="H" THEN CUST$=J$(1,10); CALL "RT2WOC",ERR=8636,X3$,X4$,CUST$,"IC1...","U",J$(1,24)
8638 IF GB$<>"" THEN ORDER_LINE$=A$(147,8)+A$(6,3); CALL "BM2ICU",X3$,X4$,1*SGN(C0),ORDER_LINE$,0,"",0
8640 RETURN 
8700 REM "CHECK FOR SUBSTITUTE WHSE CODE
8701 ! [197451]-Modified this routine to remove duplicate code at 7551-7555
8702 ! [197451]-The WHSE$ string is initially set before the call to this routine.
8705 IF POS(X3$(9,3)="101288",3)<>0 AND A$(10,1)="8" THEN RETURN ! SSP#300757 SSP307430-DBD-333-DBSPT-149880
8710 GOSUB SET_READONLY ! SSP249924
8720 READ (Z[25],KEY="D"+MID(C$,1,10),DOM=8750)IOL=0412
8730 IF DFT[3]<>0 THEN WHSE$=STR(DFT[3]:"00")
8750 GOSUB CLEAR_READONLY; RETURN ! SSP249924 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9049 REM "132100
9050 ON Y7 GOTO 9060,9100,9910,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9902 IF DID_GOSUB THEN RETURN 
9905 IF F9$="Y" AND P$>"" THEN CALL "PO2MAH",X3$,"C"+STR(C0:"00"),"!"+B$(118,8)+P$(1,1),STR(SGN(C0)); P$=P$(2); GOTO 9905
9906 IF W$<>"" THEN CALL "FW2ODL",X3$,C0,W$
9907 ANSWER$=DIM(10),ANSWER$=GBL("ANSWER",ANSWER$); REM "SSP118724
9908 IF FID_0$(1,1)<>"G" THEN PRINT @(0,22),'CE',
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
11000 CREATE_PO6_RECORDS:! [197451]-Added routine
11002 FMP_LFN=Z[25] ! set the FMP LFN
11004 PO3_LFN=Z[8] ! set the PO3 LFN
11010 ORDER_DIV$=A$(147,2) ! set the Order Division from FS2
11012 ORDER_NUM$=A$(149,6) ! set the Order Number from FS2
11014 ORDER_LINE_NUM$=A$(6,3) ! set the Order Line Number from FS2
11016 PO_CODE$=A$(9,1) ! set the PO code from FS2
11018 PRINT_FLAGS$=A$(156,5) ! set the print flag from FS2
11019 IF POS("P"=PRINT_FLAGS$)=0 THEN GOTO 11095; REM SSP 213947  
11020 SPECIAL_SHIPPING_FLAG$=A$(18,1) ! set the special shipping flag from FS2
11021 IF A$(10,4)="DS  " OR A$(9,1)<>" " THEN W9$=A$(215,1); IF POS(X3$(9,3)="092499568543537539541542584585601602605449590",3)>0 THEN W9$="0" ! SSP#260577
11022 WHSE_CODE_PKLIST$=W9$+A$(216,3) ! set the warehouse code packing list from FS2!SSP#260577
11030 GOSUB PROCESS_PO6_RECORDS ! generate the PO6 records and write them
11095 RETURN 
11200 READ_SHIP_VIA_CODES:! [197451]-Added routine
11201 ! This routine reads the FMPS file to get the picking 
11202 ! priority for the specified ship via code
11210 PICK_PRIORITY$="~~" ! clear the field
11220 FMPS_KEY$="S"+SHIP_VIA_CODE$ ! set the FMPS key
11230 READ (FMP_LFN,KEY=FMPS_KEY$,DOM=*RETURN)IOL=0480 ! read the FMPS record
11240 PICK_PRIORITY$=MID(FMPS$,278,2); PICK_PRIORITY$=PAD(PICK_PRIORITY$,2) ! set the pick priority
11295 RETURN 
11300 PROCESS_PO6_RECORDS:! [197451]-Added routine
11310 IF (SPECIAL_SHIPPING_FLAG$<>"Y") THEN {! no special shipping for the line
11320 TMP_PO3_KEY$=ORDER_DIV$+ORDER_NUM$+"     ",PICK_PRIORITY$=DIM(2) ! set the PO3 key!SSP#212259
11325  } ELSE {! special shipping 
11330 TMP_PO3_KEY$=ORDER_DIV$+ORDER_NUM$+" "+ORDER_LINE_NUM$+" ",PICK_PRIORITY$=DIM(2) ! set the PO3 key SSP#207082
11335  }
11340 READ (PO3_LFN,KEY=TMP_PO3_KEY$,DOM=*NEXT,ERR=9000)IOL=0485; GOTO 11344 ! read the PO3 record SSP#207082
11342 READ (PO3_LFN,END=*RETURN)IOL=0485 ! SSP#207082
11344 IF MID(PO3$,166,9)+MID(PO3$,7,3)<>TMP_PO3_KEY$(1,12) THEN GOTO 11695 ! SSP#207082
11345 SHIP_VIA_CODE$="" ! clear the field
11350 SHIP_VIA_CODE$=PO3$(150,1) ! set the ship via code
11355 GOSUB READ_SHIP_VIA_CODES ! read the FMPS file to get the pick priority
11695 RETURN 
12000 REM "Vendor based commission code substitution, to apply one house charge per vendor on sales order - WO 222237
12005 VENDOR_COMM_SUB:
12010 IF NUL(MID(AR1PARM$,75,1)) OR MID(AR1PARM$,76,1)<>"Y" THEN GOTO *RETURN ! Feature not enabled
12020 IF NUL(A$(9,1)) OR A$(155,1)="M" THEN GOTO *RETURN ! Not a PO line or is a message line or already applied substitution for this vendor
12030 READ (Z[30],KEY=A$(147,8)+A$(94,10)+"Y",KNO=2,DOM=*NEXT)
12035 VCSKEY$=KEY(Z[30],KNO=2,END=12060); IF MID(VCSKEY$,1,19)=A$(147,8)+A$(94,10)+"Y" THEN GOTO *RETURN ! Commission code substitution already applied for this vendor and order
12060 NEWCOMMCODE$=""; CALL "AR2COM","VENDCOM",NEWCOMMCODE$,A{ALL},0,0,0,A$ ! SSP 217566 SSP 225300
12070 IF A$(128,5)<>NEWCOMMCODE$ THEN A$(128,5)=NEWCOMMCODE$; A$(251,1)="Y"; WRITE (Z[1],KEY=A$(147,8)+A$(6,3))IOL=0311 ! If a different commission code was returned, assume substitution and apply it
12075 NEWCOMMCODE$=""; IF %MSLC THEN CMKEY$="O"+B$(118,8)+B$(6,10)+A$(6,3); CALL "AR2MCM","VENDCOM",NEWCOMMCODE$,A{ALL},0,0,0,CMKEY$,0,A$ ! SSP 217566 SSP 225300
12095 RETURN 
56000 REM + Modification History
56010 REM "193156-Reusing order numbers, some order history records have the
56012 REM "193611-Stopover order. When you enter several messages lines tied  
56014 REM "180032-Wireless Warehousing                                        
56016 REM "198733-Issue with XML order going through when user is in the      
56017 REM "195127-Shipping station/Harvey interface.  need to add field for   
56018 REM "197451-WMS pick ticket priority by Ship Via priority/
56020 ! "202907-Companion items broken in GUI. Add special charge or message
56022 REM "206519-15 minutes to access 200 line order                         
56023 REM 187122 213947 194096-List of Issues with importing Invoices from United
56024 REM "216139-Packing Lists will not print.                               
56025 REM "216996-Server keeps timing out, so that WebEC customer keep getting
56026 REM "180159-Consolidate FM2ODH and FMGODH, FM2ODZ and FMGODZ into one   
56027 ! "220814-Order 01-565090 was entered and received in gui. The order  
56028 REM "222237-Modification to charge house charges by vendor.             
56029 REM "224654-seeing an alternate reprint panel in O/P order entry.       
56030 REM "221331-Description Line in Order History is not displaying when    
56032 REM "233493-Retrieve/process DSSI PO files, create/send DSSI PO Acks.
56033 REM "237301-Sometime the packing list print all on one, sometime multi  
56034 REM "246794-Over Credit Limit / Suspend order - possible issue          
56036 REM "249444-WMS - questions about how to handle a special charge        
56038 REM "249924-DBE: RT 15622 Error 0, FM2ODZ, 8720.    
56039 REM "259101-Unable to delete orders 10-701649-A, 10-701650-A and         Tilda in cust po causing PE
56041 REM "261608-Packing List images are creating their own whse code        
56042 REM "268108-See 267266/522; PO2 M types have NY in the unused field and 
56044 REM "268441-WMS - Picking                                               
56045 REM "287219-We have several orders (00-S0157Z, 01-007363, 15-070042,etc) Product code in OH doesn't match SJ
56046 REM "300757-Vendor Warehouse Code in being written over.  Packing lists 
56047 REM "302176-Utility for External Warehouse Zero Ship correction.        
56048 REM "307357-Update FS6 to allow for more line numbers                   
56049 REM "307376-DBSPT-120579 Don't recognize 1st digit of warhouse for O1   
56050 REM "307430-DBD-333;Add 101 to some of 288's company specfic code       
56051 REM "307483-DBD-457:Support for IMS price in purchase order line        
