0010 REM "Add or delete order line effects from system<FM2ODL>
0011 REM "tma - rem'd line only Monarch uses pgm this is causing him problINPUT @(0,22),'CE','RB',"*** CALL SUPPORT - YOU ARE IN PROGRAM FM2ODL!! ***",*
0020 SETESC 9300; SETERR 9000
0035 REM "5.0 - 03/07/02 - 12.036666 - wab - SSP# 134991
0040 REM "Copyright 2002 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0087 REM "C0=command, 1=add, -1=maint delete  (modify line) -2=delete line stuff only, -3=-1&-2
0088 REM "A$,A(ALL)=sales order line detail, B$=sales order data
0090 CLEAR ; SETERR 0100; ENTER X3$,C0,A$,A{ALL},B$
0100 SETERR 9000
0110 X0$="FM2ODL"
0120 DIM S0$(10)
0170 FID_0$=FID(0); IF %IN_WEBEC$="Y" THEN FID_0$(1,1)="G"
0210 IF FID_0$(1,1)<>"G" THEN PRINT @(0,22),'CE',@(23,22),"Adjusting sales order information",
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15]
0340 IOLIST D$,D[0],D[1],D[2],D[3],D[4],D[5],D[6],D[7]
0341 IOLIST D$,D[0],STR(D[1]),D[2],D[3],STR(D[4]),D[5],D[6],D[7]
0390 IOLIST I$,I[0],I[1],I[2],I[3],I[4],I[5],I[6],I[7],I[8],I[9],I[10],I[11],I[12],I[13],I[14],I[15],I[16],I[17],I[18],I[19]
0391 IOLIST I$,STR(I[0]),I[1],I[2],I[3],I[4],I[5],I[6],I[7],STR(I[8]),I[9],I[10],I[11],I[12],I[13],I[14],I[15],I[16],I[17],I[18],I[19]
0400 IOLIST J$,J[0],J[1],J[2],J[3],J[4],J[5],J[6],J[7],J[8],J[9],J[10],J[11],J[12],J[13],J[14],J[15],J[16],J[17],J[18],J[19],J[20]
0401 IOLIST J$,J[0],STR(J[1]),STR(J[2]),J[3],J[4],J[5],J[6],J[7],J[8],J[9],J[10],J[11],J[12],J[13],J[14],J[15],J[16],J[17],J[18],J[19],J[20]
0410 IOLIST O$,O[0],O[1],O[2],O[3],O[4],O[5]
0411 IOLIST O$,O[0],STR(O[1]),O[2],O[3],O[4],O[5]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O AR1...  02O FS5...  03O FS6...  04O FS7...  05O FS8...  06O IC1...  07O PO6...  08O PO3...  09O PO2...  10O FS9...  11O FS3...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0585 RT_PARM$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,RT_PARM$
0800 REM "Get I8$
0805 P=POS("~"=B$)
0810 IF P=0 THEN I8$="" ELSE I8$=B$(P+1),B$=B$(1,P-1)
0819 REM "Req p/l ship from whse 0xxx not 9xxx
0820 IF POS(B$(47,1)="RW")>0 THEN IF A$(10,1)="9" OR POS(X3$(9,3)="470474",3)<>0 THEN W9$="0" ELSE W9$=A$(10,1)
0900 ON C0 GOTO 1000,2000
1000 REM "Delete out
1005 IF C0=-2 THEN GOTO 3000
1009 IF A$(155,1)="M" THEN GOTO 1035
1010 GOSUB 7000
1015 GOSUB 7050
1025 GOSUB 8600
1026 IF C0=-1 THEN GOSUB 8700
1030 X$="C"+B$(118,8)+" "+A$(6,3); GOSUB 7900
1032 X$="C"+B$(118,8)+A$(9,1)+A$(6,3); GOSUB 7900
1035 X$="L"+W9$+A$(11,3)+B$(118,8)+A$(6,3); GOSUB 7900
1040 IF C0=-3 THEN GOTO 3000
1090 GOTO 9900
2000 REM "Add in
2005 IF A$(155,1)="M" THEN IF POS("P"=A$(156,5))<>0 THEN GOTO 2035 ELSE GOTO 2040
2007 REM CHECK FOR LOT TYPE PRICING, PUT EXT INTO PRICE AND/OR COST
2008 IF A[4]=0 THEN A4$="*",A[4]=A[6]
2009 IF A[1]=0 THEN A5$="*",A[1]=A[3]
2010 GOSUB 7000
2015 GOSUB 7050
2020 GOSUB 7300
2025 GOSUB 8600
2030 IF A[9]<>0 AND A$(9,1)=" " THEN IF B$(127,1)<>"Y" THEN WRITE (Z[7],KEY="C"+B$(118,8)+" "+A$(6,3)) ELSE CALL "ZZPROM",".Y",X3$,S3,"Reprint Carton Labels?","","",0; IF S3=0 THEN WRITE (Z[7],KEY="C"+B$(118,8)+" "+A$(6,3)) ELSE REMOVE (Z[7],DOM=2031,KEY="C"+B$(118,8)+" "+A$(6,3))
2035 IF A$(9,1)=" " AND POS("P"=A$(156,5))>0 THEN IF B$(128,1)<>"Y" THEN WRITE (Z[7],KEY="L"+W9$+A$(11,3)+B$(118,8)+A$(6,3)) ELSE CALL "ZZPROM",".Y",X3$,S3,"Reprint on Packing List?","","",0; IF S3=0 THEN WRITE (Z[7],KEY="L"+W9$+A$(11,3)+B$(118,8)+A$(6,3)) ELSE REMOVE (Z[7],DOM=2036,KEY="L"+W9$+A$(11,3)+B$(118,8)+A$(6,3))
2040 IF A$(9,1)<>" " THEN GOSUB 8100
2045 GOSUB 8750
2090 GOTO 9900
3000 REM "Do delete only things
3010 GOSUB 8000
3020 GOSUB 7100
3030 IF A$(2,1)="Y" THEN GOSUB 3100
3090 GOTO 9900
3100 REM "Lot Inventory
3110 IF A$(155,1)="I" THEN DIM C0$(10) ELSE C0$=A$(161,10)
3120 J$=C0$+A$(19,10)+A$(10,4)+A$(147,8)+A$(6,3)
3130 CALL "IC2LCA",X3$,"","D",0,J$,0
3140 RETURN 
7000 REM "Adjust customer open order amount
7005 DIM C$(100),C[15]
7010 EXTRACT (Z[1],KEY=B$(6,10),DOM=7040)IOL=0330
7020 C[13]=C[13]+SGN(C0)*A[6]
7025 IF C[13]<0 THEN C[13]=0
7030 WRITE (Z[1],KEY=B$(6,10))IOL=0330; IF RT_PARM$<>"" THEN CUST$=B$(6,10); CALL "RT2WOR",ERR=7031,X3$,X4$,CUST$,"AR1...","U",B$(6,10)
7040 RETURN 
7050 REM "Add or rem FS5 sort key
7054 IF A$(14,1)="Y" AND (C0=-1 OR C0=-3) THEN GOTO 7056
7055 IF POS(A$(155,1)="IC")=0 THEN GOTO 7090
7060 IF A$(155,1)="I" THEN DIM I$(20); I$(11,10)=A$(19,10) ELSE I$=A$(161,10)+A$(19,10)
7065 I$=I$+A$(10,4)+A$(147,8)+A$(6,3)
7070 IF C0=-1 OR C0=-3 THEN REMOVE (Z[2],KEY=I$,DOM=7071) ELSE GOTO 7080
7075 REMOVE (Z[2],KEY=A$(161,10)+I$(11),DOM=7076)
7078 GOTO 7090
7080 WRITE (Z[2],KEY=I$)
7090 RETURN 
7100 REM "Remove FS6,7,8,9 records
7105 DIM D$(105),D[7]
7110 GOSUB 7200; IF T$=" " THEN GOTO 7190
7115 D$(1)=B$(6,10)+T$+A$(19,10)+B$(118,8)
7120 GOSUB 7450
7130 READ (Z[3],KEY=D$(1,30),DOM=7131)IOL=0340
7132 REM "If invoiced already then DO NOT remove
7133 IF POS(" "<>D$(40,14))<>0 THEN A[10]=A[10]-A[0]; GOTO 7300
7140 REMOVE (Z[3],KEY=D$(1,30),DOM=7141)
7141 IF RT_PARM$<>"" THEN CUST$=D$(1,10); CALL "RT2WOC",ERR=7142,X3$,X4$,CUST$,"FS6...","D",D$(1,30)
7160 REMOVE (Z[4],KEY=A$(29,3)+D$(11,1)+D$(12,10)+D$(1,10)+D$(22,8)+D$(30,1),DOM=7161)
7165 REMOVE (Z[5],KEY=D$(1,10)+D$(22,8)+D$(11,11)+D$(30,1),DOM=7166)
7170 REMOVE (Z[10],KEY=D$(22,8)+D$(12,10)+D$(30,1),DOM=7171)
7190 RETURN 
7200 REM "Set T$ to type for FS6 records
7204 T$=" "
7205 ON POS(A$(155,1)=" CINS") GOTO 7240,7210,7215,7220,7225,7230
7210 T$="B"; GOTO 7240
7215 T$="D"; GOTO 7240
7220 T$="F"; GOTO 7240
7225 T$="H"; GOTO 7240
7230 T$="J"; GOTO 7240
7240 RETURN 
7300 REM "Setup & write out FS6,7,8 records
7305 DIM D$(105),D[7]; GOSUB 7200
7310 D$(1)=B$(6,10)+T$+A$(19,10)+B$(118,8)+" "+A$(29,3)+B$(16,6)
7312 GOSUB 7450; GOSUB 7400
7315 D$(54,15)=B$(28,15),D$(81)=A$(113,6)+A$(94,10)+A$(90,4)+A$(124,4)
7320 D[0]=A[10],D[1]=A[4],D[2]=A[5],D[3]=A[13]
7321 IF A4$="*" OR A5$="*" THEN D[4]=A[3] ELSE IF A[2]<>0 AND A[2]<>A[5] THEN D[4]=A[1]*A[5]/A[2] ELSE D[4]=A[1]
7325 WRITE (Z[3],KEY=D$(1,30))IOL=0341; IF RT_PARM$<>"" THEN CUST$=D$(1,10); CALL "RT2WOC",ERR=7326,X3$,X4$,CUST$,"FS6...","U",D$(1,30)
7330 WRITE (Z[4],KEY=D$(31,3)+D$(11,1)+D$(12,10)+D$(1,10)+D$(22,8)+D$(30,1))
7335 WRITE (Z[5],KEY=D$(1,10)+D$(22,8)+D$(11,11)+D$(30,1))
7337 GOSUB 7350
7340 RETURN 
7350 REM "Write out FS9 record if necessary
7360 IF A$(155,1)<>"N" THEN GOTO 7390
7365 DIM D1$(81)
7370 D1$(1)=D$(22,8)+D$(12,10)+D$(30,1)+A$(29,61)
7375 WRITE (Z[10],KEY=D1$(1,19))D1$
7390 RETURN 
7400 REM "Check for old record and set up last inv stuff
7410 READ (Z[3],KEY=D$(1,30),DOM=7440)IOL=0340
7440 RETURN 
7450 REM "Compute seq no for FS6 record
7460 D9=NUM(A$(6,3),ERR=7461)
7465 IF D9>=90 THEN D9=D9+10
7470 IF D9>206 THEN D9=206
7480 D$(30,1)=CHR(D9+48)
7490 RETURN 
7500 REM "FIX FW0 & FW1 - MFG
7510 CLOSE (14); OPEN (14)"FW0"+X3$(9,3)
7520 EXTRACT (14,DOM=7530,KEY=B$(6,10)+A$(161,10)+A$(19,10)+A$(147,8))Q3$,Q6,Q7,Q8
7523 CALL "FW2ODL",X3$,-1,Q3$(11,20)+Q3$(317,80),A$(10,4),Q8,A[12]
7524 CLOSE (14); OPEN (14)"FW0"+X3$(9,3)
7525 REMOVE (14,KEY=Q3$(1,38)); Q3$(96,3)="",Q3$(31,8)=Q3$(40,8),Q3$(40,8)=""; IF POS(" "<>Q3$(31,8))>0 THEN WRITE (14,KEY=Q3$(1,38))Q3$,Q6,Q7,Q8
7530 CLOSE (14); OPEN (14)"FW1"+X3$(9,3)
7535 REMOVE (14,KEY=A$(147,8)+A$(161,10)+A$(19,10),DOM=7536)
7545 CLOSE (14); RETURN 
7900 REM "Remove PO6 Records
7910 REMOVE (Z[7],KEY=X$,DOM=7911)
7920 REMOVE (Z[7],KEY=X$+"P",DOM=7921)
7940 RETURN 
8000 REM "Remove shipping instructions
8010 K$=B$(118,8)+" "+A$(6,3)
8020 READ (Z[8],KEY=K$,DOM=8021)
8030 K0$=KEY(Z[8],END=8045)
8035 IF K0$(1,LEN(K$))<>K$ THEN GOTO 8045
8040 REMOVE (Z[8],KEY=K0$,DOM=8030); GOTO 8030
8045 RETURN 
8100 REM "Setup P/O line"
8102 IF POS(X3$(77,1)="UP")=0 THEN CALL "ZZDAPP",X3$,"DZZDISPZZENTR"
8106 DIM I$(145),I[19]
8110 IF A$(155,1)="M" THEN I$(1)=A$(1,6)+A$(6,3)+"M",I$(28,40)=A$(50,40),I$(82,9)=A$(147,8)+A$(9,1); GOTO 8160
8140 REM "Setup line
8142 I$(10,1)="C"
8145 EXTRACT (Z[9],KEY=A$(147,8)+A$(9,1)+A$(6,3),DOM=8146)IOL=0390; CALL "PO2MAH",X3$,"C",A$(147,8)+A$(9,1),"-1"
8150 I$(1,9)=S0$(1,5)+I$(6,1)+A$(6,3),I$(11)=A$(6,3)+A$(18,1),I$(25,3)=A$(29,3),I$(28)=A$(50,40)+A$(104,9)+A$(120,4)+A$(119,1),I[0]=A[1],I[1]=A[0],I[11]=A[10],I[2]=A[2],I[3]=A[9],I$(1,1)=A$(1,1),I[4]=A[3],I$(101,10)=A$(19,10)
8151 I$(82,9)=A$(147,8)+A$(9,1),I$(15,2)=A$(48,2),I$(17,8)=A$(32,8),I$(111,8)=A$(40,8),I[5]=A[12]
8155 IF A$(14,1)="Y" THEN I$(91,10)=B$(6,10) ELSE IF A$(155,1)="C" THEN I$(10,1)="I",I$(91,10)=A$(161,10) ELSE I$(10,1)=A$(155,1)
8160 WRITE (Z[9],KEY=I$(82,9)+I$(7,3))IOL=0391
8170 IF I[3]<>0 THEN IF B$(127,1)<>"Y" THEN WRITE (Z[7],KEY="C"+I$(82,9)+I$(7,3)) ELSE CALL "ZZPROM",".Y",X3$,S3,"Reprint Carton Labels?","","",0; IF S3=0 THEN WRITE (Z[7],KEY="C"+I$(82,9)+I$(7,3)) ELSE REMOVE (Z[7],DOM=8171,KEY="C"+I$(82,9)+I$(7,3))
8175 IF A$(155,1)<>"M" THEN CALL "PO2MAH",X3$,"C",I$(82,9),"1"
8176 IF POS(X3$(77,1)="UP")=0 THEN CALL "ZZDAPP",X3$,"AZZDISPZZENTR"
8190 RETURN 
8525 SET_READONLY_END:RETURN 
8530 CLEAR_READONLY:REM "Restore previous setting
8535 SET_PARAM 'XI'=SAVE_XI ! restore previous setting
8545 CLEAR_READONLY_END:RETURN 
8600 REM "Put on or take off committed based on C0
8602 IF A$(9,1)<>" " OR POS(A$(155,1)="IC")=0 THEN GOTO 8640
8604 IF POS(" "<>A$(10,4))=0 OR A$(10,2)="DS" THEN GOTO 8640
8605 DIM J$(58),J[20]
8606 IF A$(155,1)="I" THEN DIM C0$(10) ELSE C0$=A$(161,10)
8609 J$(1)=C0$+A$(19,10)+A$(10,4)
8610 EXTRACT (Z[6],KEY=J$(1,24),DOM=8611)IOL=0400
8620 J[7]=J[7]+SGN(C0)*A[0],J[11]=J[11]+SGN(C0)*A[8]
8630 WRITE (Z[6],KEY=J$(1,24))IOL=0401
8631 IF RT_PARM$>"" THEN IF RT_PARM$(7,1)="H" THEN CUST$=J$(1,10); CALL "RT2WOC",ERR=8631,X3$,X4$,CUST$,"IC1...","U",J$(1,24)
8632 IF B$(43,1)<>"Y" THEN GOTO 8640
8635 IF C0<-1 THEN GOSUB 7500
8640 RETURN 
8700 REM "Add back invoiced quantities into A(17)
8704 IF I8$="" THEN RETURN ELSE DIM O[5]
8705 FIND (Z[11],KEY=I8$(1,8)+A$(147,8)+A$(6,3)+" ",DOM=8740)IOL=0410
8710 A[16]=A[16]-O[0],A[21]=A[21]-O[2],A[17]=O[0],A[18]=O[1],A[20]=O[2],A[8]=O[3]
8740 RETURN 
8750 REM "Take invoiced quantits from line to FS3
8752 REM " WAS   *** IF I8$="" OR (A(17)=0 AND A(8)=0 AND A(20)=0) THEN RETURN ELSE DIM O(5)
8755 IF I8$="" THEN RETURN ELSE DIM O[5]
8758 O[0]=A[17],O[1]=A[18],O[2]=A[20],O[3]=A[8]; IF O[2]=0 THEN O[4]=A[3]; GOTO 8762
8760 IF A[6]=0 THEN O[4]=0 ELSE O[4]=A[3]*A[20]/A[6]
8761 REM "Use entire unbilled cost "
8762 IF A$(9,1)<>" " THEN IF A[19]<>0 THEN O[4]=A[19]
8765 A[16]=A[16]+A[17],A[21]=A[21]+A[20],A[17]=0,A[20]=0
8775 O$=I8$(1,8)+A$(147,8)+A$(6,3)+" "+I8$(9,6)+"    "+I8$(15)
8780 WRITE (Z[11],KEY=O$(1,20))IOL=0411
8790 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9905 IF FID_0$(1,1)<>"G" THEN PRINT @(0,22),'CL',
9907 IF A4$="*" THEN A[4]=0
9908 IF A5$="*" THEN A[1]=0
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
