0010 REM "Compute Qty Break Pricing <FM2OA5>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 12/17/18 - 14.595277 - jvv - SSP# 300331
0037 REM "300331-Comp 589; Support for PO items in QBG pricing, bypass Fixed 
0040 REM "Copyright 2018 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0044 GOTO 0090
0045 BEGIN ; OPEN (1)"ZZPARM"; READ (1,KEY=FID(0)+"      ")X3$; CLOSE (1)
0050 DIM A$(200); A$(118,8)="00006920",M6=2; GOTO 0091
0090 CLEAR ; SETERR 0100; ENTER X3$,A$,M6
0095 REM A$(351,1)=CUSTOMER'S QBC PRICE CODE
0100 SETERR 9000
0110 X0$="FM2OA5"
0170 FID_0$=FID(0); IF %IN_WEBEC$="Y" THEN FID_0$(1,1)="G"
0210 IF FID_0$(1,1)<>"G" THEN PRINT @(10,22),"Combining like item groups for quantity break pricing",
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19],B[20],B[21],B[22],B[23],B[24],B[25],B[26],B[27],B[28],B[29]
0321 IOLIST B$,B[0],STR(B[1]),B[2],B[3],STR(B[4]),B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],STR(B[15]),B[16],B[17],STR(B[18]),B[19],B[20],B[21],STR(B[22]),B[23],B[24],B[25],B[26],B[27],B[28],B[29]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20],C[21],C[22],C[23],C[24],C[25],C[26],C[27],C[28],C[29],C[30],C[31],C[32],C[33],C[34],C[35],C[36],C[37],C[38],C[39],C[40]
0340 IOLIST D$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O FS2...  03O IC0...  04O FMP...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0540 DIM D$(244); GOSUB SET_READONLY; FIND (Z[4],KEY="D"+A$(6,10),DOM=0541)IOL=0340 ! SSP259411
0541 GOSUB CLEAR_READONLY ! SSP259411
0560 PO_QBG=0; FIND (Z[13],KEY=%C$+"I/C",DOM=*NEXT)IC_PARM$; IF MID(IC_PARM$,73,1)="Y" THEN PO_QBG=1 ! SSP 300331
0700 IF M6=0 THEN M6=2
0800 DIM B[29],C[40],G[100,1]
1000 REM "Process lines for "I" type lines with Quantity Break Group Codes
1010 GOSUB SET_READONLY ! SSP#226523
1020 READ (Z[2],KEY=A$(118,8),DOM=1021)
1040 READ (Z[2],END=1300)IOL=0320
1050 IF A$(118,8)<>B$(147,8) THEN GOTO 1300
1055 IF MID(B$,235,3)<>DIM(3) THEN GOTO 1040 ! 188067
1060 IF B$(10,1)="9" THEN GOTO 1040 ! SSP 300331
1065 IF PO_QBG THEN IF POS(B$(155,1)="IC ")=0 THEN GOTO 1040 ELSE GOTO 1080 ! SSP 300331
1070 IF POS(B$(155,1)="IC")=0 OR B$(4,1)="Y" THEN GOTO 1040 ! SSP 300331
1080 READ (Z[3],KEY=B$(161,10)+B$(19,10),DOM=1040)IOL=0330
1090 IF C$(113,3)="   " THEN GOTO 1040
1100 REM IF C$(113,3)="   " AND D$(107,1)<>"Y" THEN GOTO 01040
1110 REM "G$=string of quantity break group codes, G(X,0)=total quantity of group, G(X,1)=number of lines items in group
1120 X=INT((POS(C$(113,3)=G$,3)+2)/3); IF X=0 THEN G$=G$+C$(113,3); GOTO 1120
1130 Q=B[10]; CALL "FM2EXT",Z[13],2,B$(124,4),B[5],Q,0,0,M6
1140 G[X,0]=G[X]+Q,G[X,1]=G[X,1]+1
1150 REM "Build line number string
1160 L$=L$+B$(6,3)+C$(113,3)
1250 REM 
1280 GOTO 1040
1300 REM "Process groups with two or more members
1301 GOSUB CLEAR_READONLY ! SSP#226523
1305 IF L$="" THEN GOTO 1360
1310 FOR I=1 TO LEN(L$) STEP 6
1320 X=(POS(L$(I+3,3)=G$,3)+2)/3
1340 IF G[X,1]>1 OR D$(107,1)<>DIM(1) THEN GOSUB 2000
1350 NEXT I
1900 GOTO 9900
2000 REM "Reprice order line - Warning don't reset I or X.
2020 EXTRACT (Z[2],KEY=A$(118,8)+L$(I,3))IOL=0320
2025 IF B$(214,1)="Y" THEN GOTO 2390; REM "If special pricing was used on the line, don't change the price here
2040 READ (Z[3],KEY=B$(161,10)+B$(19,10))IOL=0330
2045 REM "Test for variable U/M
2050 Q=B[10]; CALL "FM2EXT",Z[13],2,B$(124,4),B[5],Q,0,0,M6; IF Q<>B[10] THEN Q=G[X,0]*C[14] ELSE Q=G[X,0]
2060 REM "Pick-a-price using group quantity - D$(107,1)="Y" means customer gets lowest QBG price
2080 PRECISION M6
2100 DIM P[25,1]; P[0,1]=C[0]
2120 FOR J=1 TO 6; P[J,0]=C[1+(J-1)*2],P[J,1]=C[2+(J-1)*2],P[J+6,0]=C[21+(J-1)*2],P[J+6,1]=C[22+(J-1)*2]; NEXT J
2140 B[4]=P[0,1]*B[5]/C[14]; IF P[1,0]=0 THEN GOTO 2221
2150 IF POS(D$(107,1)="N ",1)<>0 THEN GOTO 2160
2151 IF D$(107,1)="Y" THEN GOTO 2160
2152 COLUMN=ASC(D$(107,1))-64
2154 B[4]=P[COLUMN,1]*B[5]/C[14]; IF B[4]<>0 THEN GOTO 2221
2155 COLUMN=COLUMN-1; GOTO 2154
2160 FOR J=1 TO 12
2180 IF Q<P[J,0] AND MID(D$,107,1)<>"Y" THEN J=99; GOTO 2220
2200 IF P[J,0]<>0 THEN B[4]=P[J,1]*B[5]/C[14]
2220 NEXT J
2225 REM "Apply trade discount
2230 B[22]=B[4],B[4]=B[4]-B[4]*B[23]*.01
2240 CALL "FM2EXT",Z[13],0,B$(124,4),B[5],B[0],B[4],E,M6
2250 B[6]=E
2260 WRITE (Z[2])IOL=0321
2390 RETURN 
8500 SET_READONLY:! SSP#226523
8510 SET_PARAM 'XI' ! SSP#226523
8525 SET_READONLY_END:RETURN 
8530 CLEAR_READONLY:REM "Restore previous setting
8535 SET_PARAM -'XI' ! restore previous setting!SSP#226523
8545 CLEAR_READONLY_END:RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "END PROGRAM
9905 IF FID_0$(1,1)<>"G" THEN PRINT @(0,22),'CL',
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "188067-New Pricing changes. Standard GP Percentage for a           
56001 REM "226523-File Lock - Printer in use or someting like that            
56002 REM "259411-EC error msg 000-EC3WS1-8463 on two DBE orders.             
56003 REM "259411-EC error msg 000-EC3WS1-8463 on two DBE orders.             
56004 REM "300331-Comp 589; Support for PO items in QBG pricing, bypass Fixed 
