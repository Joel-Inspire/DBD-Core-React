0010 REM "Customer Defaults <FM2SHA>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 08/20/15 - 8.847777 - jvv - SSP# 276772
0036 REM "Mod for RON format - non-fms customer; 4/3/91 JSC
0037 REM "276772-Multiple Finder's Fees per Customer/Item/Order Line.        
0040 REM "Copyright 2015 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0042 REM "NOTE: FM2SHD & B also maintains string!!!
0050 IF J9<>0 THEN GOTO 8500
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q3$,Q9$
0100 SETERR 9000
0110 X0$="FM2SHA",X1$="Customer Defaults"
0120 DIM S$(20)
0130 K0=10,K1=1
0135 C9=-1
0140 M0$="-###.00",M1$="##.00"
0145 GOSUB 8950
0150 V8$="0008101399"
0200 REM "
0230 IF Q9$<>"" THEN X2=-1; PRINT @(0,6),'CE',
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 K9$="D",K9=LEN(K9$)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1),A[0],A[1],A[2],A[3],A[4],A[5]
0311 IOLIST Q3$
0312 IOLIST Q4$
0313 IOLIST Q2$
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15]
0330 IOLIST C$,C[0],C[1],C[2]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0509 REM + [201806]-Modified line 510 to also open XPD
0510 Z$="01X FMP...  02O AR1...  03O AP4...  04OSFMP...  05O FM1...  06O ZZPARM 07O FM0...  18O XPD...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0585 RT_PARM$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,RT_PARM$
0590 READ (Z[1],KEY=K9$,DOM=0591)
0600 REM "
0700 REM "Get date"
0720 D0$=X3$(15,6)
0725 READ (Z[6],KEY=X3$(9,3)+"A/R",DOM=9800)P0$
0730 READ (Z[6],KEY=X3$(9,3)+"AR1",DOM=9800)P2$
0740 READ (Z[6],KEY=X3$(9,3)+"F/M",DOM=9800)P1$
0742 MFF_PARM=0; IF MID(P1$,337,1)="Y" THEN MFF_PARM=1 ! SSP 276772
0745 READ (Z[6],KEY=X3$(9,3)+"I/C",DOM=9800)P3$
0780 GOSUB 6000
0790 IF P0$(118,1)<>"Y" THEN GOTO 0830
0800 REM "Open File if using foreign currency on invoice print
0810 Z$="080 ASN...  "
0820 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0821,9900
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(397),B[15],C$(227),C[2],Q3$(20),Q4$(20),Q2$(20)
1035 DIM A$(244),A[5]
1045 A$(1,0+K9)=K9$+A1$
1050 IF Q9$<>"" THEN GOTO 1100
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1105 IF X3$(188,8)="DEFAULTS" THEN READ (Z[13],KEY="DFLT"+X0$,DOM=2000)IOL=0310; GOSUB 6600; GOSUB 6200; GOTO 2000
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9700
1140 ON X0 GOTO 1200,1500
1200 REM "Customer code
1205 IF Q9$>"" THEN A$(1+K9,10)=Q9$(1,10),C0=0; GOTO 1212
1210 CALL "ZZENTR","AXUX",A{ALL},A$,X4$,X3$,18,3+V0,1+K9,10,C0,"A/R","{1"+X$,"","FM2SHA00","AR1","AR2MAB",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9700,6970
1220 B$(1)=""; IF A$(1+K9,10)<>S$(1,10) THEN FIND (Z[2],KEY=A$(1+K9,10),DOM=1210)IOL=0320
1225 GOSUB 6450
1240 GOTO 1120
1500 REM "
1505 Z9=-1
1510 A1$=K9$+A$(LEN(K9$)+1,10),A$(1,LEN(K9$))=K9$
1515 DIM A[5]; FIND (Z[1],KEY="D"+S$(1,10),DOM=1517)IOL=0310; REM " SSP 174696
1516 A$(1,LEN(A1$))=A1$
1520 DIM A[5]; READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1525 Z9=0
1530 FIND (Z[3],KEY=A$(12+K9,10),DOM=1531)IOL=0330
1535 FIND (Z[4],KEY="F"+A$(27+K9,1),DOM=1536)IOL=0311
1540 FIND (Z[4],KEY="H"+A$(28+K9,1),DOM=1541)IOL=0312
1545 FIND (Z[4],KEY="S"+A$(44+K9,1),DOM=1546)IOL=0313
1550 DIM D1$(60); FIND (Z[4],KEY="a"+A$(2,10)+A$(109,3),DOM=1551)D1$
1590 GOTO 1700
1600 REM "
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630
1630 IF Q9$<>"" THEN GOTO 9700 ELSE GOTO 1000
1650 READ (Z[13],KEY="FM2SHB"+"DFLT",DOM=1651)V9$,A2$
1655 READ (Z[13],KEY="DFLT"+X0$,DOM=1656)IOL=0310; A$(1,LEN(A1$))=A1$; A$(46,2)=A2$(46,2),A$(52,10)=A2$(52,10),A$(67,41)=A2$(67,41),A$(109,4)=A2$(109,4),A$(124,118)=A2$(124,118),A$(107,1)="N" ! SSP#259269
1660 GOSUB 6200
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1820 CALL "ZZPROM",STR(6+C9),X3$,Z,"","","V",X0; ON Z GOTO 1850,6500,6750,6700
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310
1865 C9=1
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF INT(C0)=3 THEN GOSUB 7300 ELSE IF C0>3 OR X0<0 THEN GOTO 1800
2030 IF MFF_PARM THEN GOTO 2035 ELSE GOTO 2040 ! SSP 276772
2035 ON X0 GOTO 2350,3650,2400,3800,2450,2700,2500,2550,2600,2650,4000,2250,3000,2750,3500,3700,3750,2300,3400,3450,1800
2040 ON X0 GOTO 2350,3650,2400,3800,2450,2700,2500,2550,2600,2650,2100,2150,2200,2250,3000,2750,3500,3700,3750,2300,3400,3450,1800
2100 REM "Finder's fee
2105 X$="Finder's fee based upon: S=sales, G=gross profit, C=cost, A=Adj GP, Blank=none"; REM ADDED TYPE A - SSP# 132459
2110 CALL "ZZENTR","SRU",A{ALL},A$,X4$,X3$,15,11+V0,11+K9,1,C0,"","{3"+X$,"ASGCF ","FM2SHA02","","",""; REM " ADDED TYPE A - SSP# 132459
2120 IF A$(12,1)=" " THEN A$(13,10)="",A[0]=0,C$(1)=""; PRINT @(15,12),'CL',@(58,11),'CL',
2140 GOTO 2020
2150 REM "Vendor code
2155 IF A$(11+K9,1)=" " THEN GOTO 2020
2157 REMOVE (Z[13],KEY=U0$,DOM=2158)
2160 CALL "ZZENTR","AU",A{ALL},A$,X4$,X3$,15,12,12+K9,10,C0,"A/P","{3"+X$,"","FM2SHA04","AP4","X",""; IF ABS(C0)=102 THEN J9=1,Q1$="",Z8$="AP2MAB"; RUN "ZZ2STP" ELSE IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2160,2161
2165 IF U0$<>"" AND U1$<>"" THEN WRITE (Z[13],KEY=U0$)U0$,U1$
2170 FIND (Z[3],KEY=A$(12+K9,10),DOM=2160)IOL=0330
2175 PRINT @(35,12+V0+L0),C$(11,35),
2190 GOTO 2020
2200 REM "Rate
2205 IF A$(11+K9,1)=" " THEN GOTO 2020
2210 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,58,11,0,320,C0,M0$,"{3"+X$,"","FM2SHA06","","",""
2240 GOTO 2020
2250 REM "Summary billing (I=Invoices, R=Requisitions)
2255 X$="I=invoices, R=requisitions, W=warehouse releases"
2260 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,33,14+V0,113,4,C0,"","{3"+X$,"","FM2SHA08","","",""
2265 FOR Q=0 TO 3; IF POS(A$(113+Q,1)=" RIW")=0 THEN PRINT 'RB',; EXITTO 2250 ELSE NEXT Q
2270 IF POS(" "<>A$(113,4))>0 THEN A$(49,1)="Y"; PRINT @(77,14),A$(49,1),
2290 GOTO 2020
2300 REM "Print management?
2305 J8$=A$(24,1)
2310 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,59,19,23+K9,1,C0,"","{3"+X$,"","FM2SHA10","","",""
2320 IF A$(24,1)="Y" AND A$(49,1)<>"Y" THEN CALL "ZZPROM",".4",X3$,0,"WARNING - Monthly Detail Shipment reporting is set to 'N'!","","",0
2330 IF A$(24,1)<>J8$ THEN IF A$(24,1)="N" THEN CALL "ZZPROM",".4",X3$,0,"All existing forms for this customer will now have their IMS Status changed to N","","",0; GOSUB 7550 ELSE CALL "ZZPROM",".Y",X3$,Z,"Set all of this customer's items to item management?","","",0; ON Z GOSUB 7550,7590 ! SSP#230454
2340 GOTO 2020
2350 REM "customer PO number required?
2360 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,35,6+V0,24+K9,1,C0,"","{3"+X$,"","FM2SHA12","","",""
2390 GOTO 2020
2400 REM "Print sales order?
2405 X$="Enter the Thank You letter type designator"
2410 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,26,7+V0,25+K9,1,C0,"","{3"+X$,"","FM2SHA14","","",""
2440 GOTO 2020
2450 REM "Send order acknowledgements?
2460 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,35,8+V0,26+K9,1,C0,"","{3"+X$,"","FM2SHA16","","",""
2490 GOTO 2020
2500 REM "FOB
2510 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,58,6+V0,27+K9,1,C0,"","{3"+X$,"F","FM2SHA18","FMPF","FM2SBA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2510,2511
2520 IF A$(27+K9,1)<>" " THEN FIND (Z[4],KEY="F"+A$(27+K9,1),DOM=2510)IOL=0311 ELSE DIM Q3$(LEN(Q3$))
2525 PRINT @(59,6+V0),"-",Q3$(3,15),
2540 GOTO 2020
2550 REM "How to ship
2560 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,58,7+V0,28+K9,1,C0,"","{3"+X$,"H","FM2SHA20","FMPH","FM2SDA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2560,2561
2570 IF A$(28+K9,1)<>" " THEN FIND (Z[4],KEY="H"+A$(28+K9,1),DOM=2560)IOL=0312 ELSE DIM Q4$(LEN(Q4$))
2575 PRINT @(59,7+V0),"-",Q4$(3,15),
2590 GOTO 2020
2600 REM "Inside delivery
2610 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,58,8+V0,29+K9,15,C0,"","{3"+X$,"","FM2SHA22","","",""
2640 GOTO 2020
2650 REM "Ship via
2660 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,58,9+V0,44+K9,1,C0,"","{3"+X$,"S","FM2SHA24","FMPS","FM2SCA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2660,2661
2670 IF A$(44+K9,1)<>" " THEN FIND (Z[4],KEY="S"+A$(44+K9,1),DOM=2660)IOL=0313 ELSE DIM Q2$(LEN(Q2$))
2675 PRINT @(59,9+V0),"-",Q2$(3,15),
2690 GOTO 2020
2700 REM "Freight markup table
2705 X$="Table for marking up freight and adding handling charges"
2710 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,35,9+V0,49+K9,2,C0,"","{3"+X$,"M","FM2SHA55","FMPM","FM2MPH",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2710,2711
2720 FIND (Z[4],KEY="M"+A$(49+K9,2),DOM=2721); GOTO 2730
2725 IF A$(49+K9,2)<>"  " THEN GOTO 2700
2740 GOTO 2020
2750 REM "Detail Shipment Reporting?
2755 IF POS(" "<>A$(113,4))>0 THEN A$(49,1)="Y"; PRINT @(77,14),A$(49,1),; GOTO 2020
2760 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,77,14+V0,48+K9,1,C0,"","{3"+X$,"","FM2SHA27","","",""
2790 GOTO 2020
2800 REM "Customer's current fiscal year
2805 IF A$(24,1)<>"Y" THEN GOTO 2020
2808 X$="Enter Customer's current fiscal year (4-digit)"
2810 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,48,17,51+K9,4,C0,"","{3"+X$,"","FM2SHA27","","",""
2820 IF ABS(C0)<>2 AND A$(51+K9,4)<"1980" THEN GOTO 2810
2840 GOTO 2020
2850 REM "Reorder notice format
2855 IF A$(24,1)<>"Y" THEN GOTO 2020
2856 X$="Leave blank if no reorder notices are to be generated."
2860 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,48,18+V0,108,1,C0,"","{3"+X$,"","FM2SHA59","","",""
2890 GOTO 2020
2900 REM "Main customer contact
2902 IF A$(24,1)<>"Y" THEN GOTO 2020
2905 Q$="a"+A$(2,10)
2910 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,21,18,109,3,C0,"","{3"+X$,Q$,"FM2SHA59","FM2MPI","FM2MPI",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2910,2911
2915 DIM D1$(60); IF A$(109,3)<>S$(1,3) THEN READ (Z[4],KEY="a"+A$(2,10)+A$(109,3),DOM=2910)D1$
2940 GOTO 2020
3000 REM "PRINT SUMMARY BILLS?
3005 IF POS(" "<>A$(113,4))=0 THEN A$(117,4)=""; PRINT @(33,15),A$(113,4),; GOTO 2020 ELSE IF POS(" "<>A$(117,4))>0 THEN GOTO 3007
3006 FOR Q=0 TO 3; IF A$(113+Q,1)<>" " THEN A$(117+Q,1)="N"; NEXT Q
3007 X$="Enter Y or N under each respective Summary Bill code above"
3010 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,33,15,117,4,C0,"","{3"+X$,"","FM2SHA65","","",""
3020 FOR Q=0 TO 3; IF A$(113+Q,1)<>" " AND POS(A$(117+Q,1)="YN")=0 OR (A$(113+Q,1)=" " AND A$(117+Q,1)<>" ") THEN PRINT 'RB',; EXITTO 3010 ELSE NEXT Q
3040 GOTO 2020
3050 REM "Low Stock Notice Format
3055 IF A$(24,1)<>"Y" THEN GOTO 2020
3060 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,77,18+V0,112,1,C0,"","{3"+X$,"","FM2SHA59","","",""
3090 GOTO 2020
3100 REM "CUSTOMER CYCLE 31,19
3105 IF A$(24,1)<>"Y" THEN GOTO 2020
3110 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,21,19,46,2,C0,"","{3"+X$,"","FM2SHA59","","",""
3140 GOTO 2020
3150 REM "Alt I/C Customer
3190 GOTO 2020
3200 REM "Customer's Current Accounting Period
3205 IF A$(24,1)<>"Y" THEN GOTO 2020
3210 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,77,17,55+K9,2,C0,"","{3"+X$,"","FM2SHA29","","",""
3220 IF A$(55+K9,2)<"01" OR A$(55+K9,2)>"13" THEN GOTO 3210
3240 GOTO 2020
3300 REM "DOES CUSTOMER HAVE MULTIPLE LOCATIONS
3310 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,77,19,124,1,C0,"","{3"+X$,"","FM2SHA29","","",""
3340 GOTO 2020
3350 REM "STD LOCATION CODE
3360 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,48,19,125,4,C0,"","{3"+X$,"","FM2SHA29","","",""
3390 GOTO 2020
3400 REM "Reorder notice format
3410 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,59,20,108,1,C0,"","{3"+X$,"","FM2SHB20","","",""
3440 GOTO 2020
3450 REM "Reduce on-hand by committed
3455 IF A$(108,1)=" " THEN A$(133,1)="0"; PRINT @(59,21),FNB$(A$(133,1)),; GOTO 2020
3460 CALL "ZZENTR","B",A{ALL},A$,X4$,X3$,59,21,133,1,C0,"","{3"+X$,"","FM2SHB22","","",""
3490 GOTO 2020
3500 REM "Consolidated Pick List
3505 IF POS(P1$(160,1)="AS")=0 THEN GOTO 2020
3510 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,77,15,66,1,C0,"","{3"+X$,"","FM2SHB24","","",""
3540 GOTO 2020
3550 REM "Customer always gets lowest QBG price? (Y/N)- MOVED TO ANOTHER SCREEN
3560 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,59,20,107,1,C0,"","{3"+X$,"","FM2SHB29","","",""
3590 GOTO 2020
3600 REM "Foreign Currency Used - MOVED TO ANOTHER SCREEN
3610 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,59,21,90,3,C0,"","{3"+X$,"","FM2SHB30","","",""
3640 GOTO 2020
3650 REM "Unique P/O number required
3655 X$="Y means that each order MUST have a unique Customer P/O number"
3660 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,49,6,23,1,C0,"","{3"+X$,"","FM2SHB32","","",""
3690 GOTO 2020
3700 REM "Skip print and summary bill update of 9### Releases on W type orders - SSP# 151339
3705 X$="No-Print lines do NOT update the FMY or FMZ File"
3710 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,69,16,102,1,C0,"","{3"+X$,"","FM2SHB32","","",""
3740 GOTO 2020
3750 REM " Update to FMZ File?
3751 IF A$(102,1)<>"Y" THEN A$(103,1)="  "; PRINT @(18,17+V0),'CL',; GOTO 2020
3752 IF A$(102,1)="Y" THEN PRINT @(52,17),"Update FMZ File?",
3755 X$="Y = Update FMZ file on 9### Releases on W Type Orders"
3760 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,69,17+V0,103,1,C0,"","{3"+X$,"","FM2SHA27","","",""
3790 GOTO 2020
3800 REM " Thank you letter type - all or manual
3801 IF A$(26,1)=" " THEN A$(105,1)=""; PRINT @(42,7+V0)," ",; GOTO 2020
3805 X$="A=All Orders get Thank you, M=Only Non-eCommerce Orders get Thank You" ! SSP#241569
3810 CALL "ZZENTR","SRU",A{ALL},A$,X4$,X3$,42,7+V0,104+K9,1,C0,"","{3"+X$,"MA","FM2SHA14","","",""
3840 GOTO 2020
4000 REM "Multiple Finders Fee Routine
4010 CALL "ZZPROM","MF",X3$,Z,"","","",0; ON Z GOTO 2020,4020
4020 MF_KEY$=A$(2,10)
4030 CALL "ARGATI",X3$,X4$,MF_KEY$ ! SSP 225300
4040 GOTO 2020 ! SSP 225300
4050 ! 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(3,3+V0),"Customer code:",@(38,3+V0),"Name:",; IF P0$(15,1)="Y" THEN PRINT @(0,4+V0),"Bill-to customer:",@(38,4+V0),"Name:",@(0,5+V0),Z0$, ELSE PRINT @(0,5+V0),Z0$,
6070 PRINT @(32,5),"ORDER PROCESSING",@(3,6),"Is Customer PO Number required?",@(37,6),"Unique req:",@(51),"F.O.B:",
6080 PRINT @(0,7),"Thank you letter Default:",@(29),"Which orders:",@(45),"How to ship:",
6081 REM "PRINT @(4,7),"Thank you letter type default?",@(45),"How to ship:",
6090 PRINT @(0,8),"Normally print Shipping Ack'ments?",@(41),"Inside Delivery:",
6100 PRINT @(13,9),"Freight Markup Table:",@(48),"Ship Via:",
6110 PRINT @(0,10),Z0$,@(34,10),"FINDERS FEE",; IF MFF_PARM THEN PRINT @(20,11),"** MULTIPLE FINDERS FEE **" ELSE PRINT @(9,11),"Type:",@(52),"Rate:",@(2,12),"Vendor Code:",@(29),"Name:", ! SSP 276772
6115 PRINT @(0,13),Z0$,@(18,13),"SUMMARY BILLING / DETAIL SHIPMENT REPORTING",@(15,14),"Summary Bill for:",@(42),"Monthly Shipment Detail Reporting?",@(0,15),"Print Invoice for each Shipment?",
6118 IF POS(P1$(160,1)="AS")>0 THEN PRINT @(53,15),"Consolidated Pick List?",
6119 PRINT @(8,16),"Set No-Print Flag to Yes for 9### Releases on W Type Orders?",
6120 PRINT @(52,17),"Update FMZ File?",
6122 PRINT @(0,18),Z0$,
6125 PRINT @(4,19),"Do you wish to treat this customer as an IMS customer?" ! SSP#230454
6130 PRINT @(13,20),"Format code for reorder notices (blank=none):"
6135 PRINT @(26,21),"Reduce on-hand by committed qty?"
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 PRINT @(35,6),A$(25,1),@(49,6),A$(23,1),@(58,6),A$(28,1),"-",Q3$(3,15),@(26,7),A$(26,1),@(58,7),A$(29,1),"-",Q4$(3,15),@(35,8),A$(27,1),@(58,8),A$(30,15),@(35,9),A$(50,2),@(58,9),A$(45,1),"-",Q2$(3,15),
6221 PRINT @(42,7+V0),A$(104+K9,1); REM " SSP# 151247
6222 IF MFF_PARM THEN GOTO 6226 ELSE PRINT @(15,11),A$(12,1),@(58,11),A[0]:M0$, ! SSP 276772
6224 CALL "ZZDISP","A",A$(12+K9,10),"A/P",X3$,"","",15,12+V0,X4$; PRINT @(35,12),C$(11,35),
6226 PRINT @(33,14),A$(113,4),@(77,14),A$(49,1),@(33,15),A$(117,4),; IF POS(P1$(160,1)="AS")>0 THEN PRINT @(77,15),A$(66,1),
6228 PRINT @(59,19),A$(24,1),@(59,20),A$(108,1),@(59,21),FNB$(A$(133,1)),
6229 IF A$(102,1)="Y" THEN PRINT @(52,17),"Update FMZ File?",@(69,17+V0),A$(103,1), ELSE PRINT @(52,17),"                ",
6241 PRINT @(69,16),A$(102,1); REM " SSP# 151339
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 CALL "ZZDISP","A",A$(1+K9,10),"A/R",X3$,"","",18,3+V0,X4$
6464 PRINT @(44,3+V0+L0),B$(11,35),
6469 IF P0$(15,1)<>"Y" THEN GOTO 6486
6470 CALL "ZZDISP","A",B$(46,10),"A/R",X3$,"","",18,4+V0,X4$
6480 FIND (Z[4],KEY=B$(46,10),DOM=6486)IOL=0330
6485 PRINT @(44,4),C$(11+K9,35),
6490 RETURN 
6500 REM "DELETE
6505 IF X3$(188,8)="DEFAULTS" THEN REMOVE (Z[13],KEY="DFLT"+X0$,DOM=9900); GOTO 9900
6508 REM + [201806]-Changed the DOM value from 6511 to 6521 at line 6510
6509 REM + [201806]-Added line 6520 to update XPD
6510 REMOVE (Z[1],KEY=A1$,DOM=6521)
6520 CALL "UPDFMP;DUPLICATE_XPD",Z[18],A$,A{ALL},"D"
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6605 FIND (Z[2],KEY=A$(1+K9,10),DOM=6606)IOL=0320
6610 DIM D1$(60); FIND (Z[4],KEY="a"+A$(2,10)+A$(109,3),DOM=6611)D1$
6690 RETURN 
6700 REM "ADD ENTRY
6702 IF X3$(188,8)="DEFAULTS" THEN WRITE (Z[13],KEY="DFLT"+X0$)IOL=0310; GOTO 9900
6705 IF Z9=0 THEN GOTO 6750
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6719 REM + [201806]-Added line 6720 to update XPD
6720 CALL "UPDFMP;DUPLICATE_XPD",Z[18],A$,A{ALL},"U"
6780 IF A$(24,1)="Y" THEN GOSUB 7500
6785 CALL "FM2SHH",X3$,X4$,A1$(2),""
6787 CALL "FM2SHJ",X3$,X4$,A1$(2),""
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6825 A$(1,LEN(A1$))=A1$
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
7300 REM 
7310 ON FPT(C0)*10 GOTO 7390,7325,7330,7390
7325 IF V8$="" THEN X0=X0+4; GOTO 7390 ELSE X0=NUM(V8$(POS(STR(X0:"00")<=V8$,2),2)); GOTO 7390
7330 IF V8$="" THEN X0=X0-6; GOTO 7335 ELSE X0=POS(STR(X0-1:"00")<=V8$,2)-2; IF X0<0 THEN X0=0; GOTO 7390 ELSE X0=NUM(V8$(X0,2)); GOTO 7390
7335 IF X0<0 THEN X0=0; GOTO 7390
7390 RETURN 
7500 REM "Print Mgt Parameters
7520 CALL "FM2SHB",X3$,X4$,A1$(2),""
7525 CALL "FM2SHF",X3$,X4$,A1$(2),""
7527 CALL "FM2SHG",X3$,X4$,A1$(2),""
7530 C9=-1
7540 GOSUB 6400
7545 RETURN 
7550 REM "Change FMS flag in FM1
7560 READ (Z[5],KEY=A$(2,10),DOM=7561)
7570 EXTRACT RECORD (Z[5],END=7590)X$
7572 IF A$(2,10)<>X$(1,10) THEN GOTO 7590
7575 X$(108,1)=A$(24,1)
7577 PRINT @(10,22),X$(11,10),
7580 WRITE RECORD (Z[5])X$; IF RT_PARM$<>"" THEN CUST$=X$(1,10); CALL "RT2WOC",ERR=7581,X3$,X4$,CUST$,"FM1...","U",X$(1,20)
7585 GOTO 7570
7590 X$=""; IF A$(24,1)="Y" THEN GOSUB 7600 ELSE Q$=" "; GOSUB 7604
7595 PRINT @(0,21),'CE',; RETURN 
7600 REM "Set location flags
7602 Q$="I"; PRINT @(0,21),'CL',@(16),"Enter the default IMS method for locations:",; CALL "ZZENTR","SU",A{ALL},Q$,X4$,X3$,64,21,1,1,C0,"","{3"+X$,"","","","","" ! SSP#230454
7603 IF POS(Q$="IC")=0 THEN RETURN 
7604 PRINT @(0,21),'CE',@(0),"Processing location:",
7605 READ (Z[7],KEY="C"+A$(2,10),DOM=7610)
7610 K$=KEY(Z[7],END=7690); READ RECORD (Z[7])G$; IF G$(1,11)<>"C"+A$(2,10) THEN GOTO 7690
7620 G$(223,1)=Q$
7630 WRITE RECORD (Z[7],KEY=K$)G$; IF RT_PARM$<>"" THEN CUST$=K$(2,10); CALL "RT2WOC",ERR=7631,X3$,X4$,CUST$,"FM0...","U",K$
7635 PRINT @(21,21),K$(12,4),
7650 GOTO 7610
7695 PRINT @(0,21),'CE',; RETURN 
8500 REM "Return from Set-up
8510 IF Q1$="" OR J9=0 THEN GOSUB 6400
8520 J9=0; C0=0
8530 IF Q1$>"" THEN A$(12+K9,10)=Q1$; GOSUB 6400; GOTO 2161 ELSE GOTO 2160
8888 CALL "ZZENTR","B",A{ALL},A$,X4$,X3$,60,15,133,1,C0,"","{3"+X$,"","FM2SHB22","","",""
8920 DEF FNB$(Z9$)=X3$(214-6*NUM(Z9$),1)
8950 REM "Program flow logic
8960 DIM U0$(6); U0$(1)=FID(0); U0$=U0$+"PF"
8970 CLOSE (14); OPEN (14)"ZZPARM"; FIND (14,KEY=U0$,DOM=8990)U0$,U1$
8980 Q1=NUM(U1$(7,2)),Q9$=U1$(9,Q1),U1$=U1$(Q1+9)
8990 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9715 IF A$(24,1)="Y" THEN U1$="FM2FYE"+"10"+A$(2,10)+U1$
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9725 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9730 RUN U1$(1,6)
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q4$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "FM2SHB"
9999 END 
56000 REM "198988-Manage Customer Supplied Inventory. Item Setup, Purch ord  
56002 REM "201806-Modify FM2SHA to also call UPDFMP to update XPD after       
56003 REM "241569-Change wording from WebEc to Ecommerce in Customer defaults 
56004 REM "276772-Multiple Finder's Fees per Customer/Item/Order Line.        
