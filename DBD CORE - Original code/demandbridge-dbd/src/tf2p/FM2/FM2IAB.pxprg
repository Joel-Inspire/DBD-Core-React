0010 REM "Custom Item File - Status information <FM2IAB>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 08/24/15 - 12.348055 - jvv - SSP# 276772
0037 REM "276772-Multiple Finder's Fees per Customer/Item/Order Line.        
0040 REM "Copyright 2015 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "********* Note ***********
0051 REM "For now, quanities are not being converted to case packs since,
0052 REM "comb qty is cost unit dependent & others are sell unit depndt. 
0053 REM "This could be confusing. Plus putting .00 on qtys makes screen
0054 REM "somewhat unreadable.  To initiate qty conversion, see FM2IAA stmts 1500-1600,6600-6700; FM2IAB stmts2750-2799,3850-3859
0100 SETERR 9000
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1,LEN(A$)),A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],IOL=0312
0312 IOLIST A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30],A[31]
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15]
0330 IOLIST C$
0340 IOLIST D$,D[0],D[1],D[2]
0341 IOLIST D1$
0350 IOLIST E$,E[0],E[1]
0360 IOLIST F$,F[0],F[1]
0400 IF J=0 THEN GOTO 0490 ELSE J0=J,J=0; ON J0 GOTO 0490,0401,0402,0403
0401 GOSUB 6400; ON C0 GOTO 2360,2361
0402 GOSUB 6400; ON C0 GOTO 3160,3161
0403 GOSUB 6400; ON C0 GOTO 2410,2411
0500 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
1800 L0=1; RUN "FM2IAA"
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF INT(C0)=3 THEN GOSUB 7300 ELSE IF C0>3 OR X0<0 THEN GOTO 1800
2030 IF MFF_PARM THEN GOTO 2035 ELSE GOTO 2040 ! SSP 276772
2035 ON X0 GOTO 2100,2150,2200,2250,2300,4250,2350,2400,2600,2750,2700,2450,3850,2550,4000,4150,4400,4050,4100,4550,4500,3250,3200,3900,4200,4600,4300,4350,5000,3400,3350,3500,3300,3550,3600,3650,3700,3750,3800,3650,3700,3750,3800,1800 ! SSP 276772
2040 ON X0 GOTO 2100,2150,2200,2250,2300,4250,2350,2400,2600,2750,2700,2450,3850,2550,4000,4150,4400,4050,4100,4550,4500,3250,3200,3900,4200,4600,4300,4350,3050,3100,3150,3400,3350,3500,3300,3550,3600,3650,3700,3750,3800,3650,3700,3750,3800,1800
2100 REM "Product code
2110 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,11,6,21+K9,3,C0,"","{3"+X$,"T","FM2IAA04","FMPP","FM2SAA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2110,2111
2120 FIND (Z[3],KEY="T"+A$(21+K9,3),DOM=2110)IOL=0330
2130 PRINT @(21,22),"Product code: ",A$(21+K9,3)," - ",C$(5,15),
2140 GOTO 2020
2150 REM "Left-right
2160 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,16,6,24+K9,8,C0,"","{3"+X$,"","FM2IAA06","","",""
2190 GOTO 2020
2200 REM "Top-bottom
2210 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,25,6,32+K9,8,C0,"","{3"+X$,"","FM2IAA08","","",""
2240 GOTO 2020
2250 REM "Number of plys
2260 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,35,6,40+K9,2,C0,"","{3"+X$,"","FM2IAA10","","",""
2270 IF NUM(MID(A$,40,2),ERR=2260)<0 THEN GOTO 2260
2290 GOTO 2020
2300 REM "Description
2310 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,39,6,42+K9,40,C0,"","{3"+X$,"","FM2IAA12","","",""
2340 GOTO 2020
2350 REM "Vendor code
2360 CALL "ZZENTR","AU",A{ALL},A$,X4$,X3$,8,7,82+K9,10,C0,"A/P","{3"+X$,"","FM2IAA14","AP4","X",""; IF ABS(C0)=102 THEN J=1; RUN "FM2IAE" ELSE IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2360,2361
2365 IF MID(X3$,330,1)="1" THEN IF POS(MID(A$,82+K9,2)="0001",2)=0 THEN CALL "ZZPROM",".4",X3$,0,"You must use a vendor in division 00 or 01!","","",0; GOTO 2360
2370 D$(1)=""; IF POS(" "<>MID(A$,82+K9,10))>0 THEN FIND (Z[4],KEY=A$(82+K9,10),DOM=2360)IOL=0340 ELSE IF POS("QS2DAB"=U1$)=0 THEN GOTO 2360
2375 PRINT @(27,7),D$(11,35),
2390 GOTO 2020
2400 REM "Plant code
2405 J9$="P"+A$(82+K9,10)
2406 IF POS(" "<>MID(A$,82+K9,10))=0 THEN A$(92+K9,4)=""; GOTO 2020
2410 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,69,7,92+K9,4,C0,"","{3"+X$,J9$,"FM2IAA16","FM0P","X",""; IF ABS(C0)=102 THEN J=3; RUN "FM2IAE" ELSE IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2410,2411
2415 IF POS(" "<>MID(A$,92+K9,4))=0 THEN GOTO 2440
2420 FIND (Z[5],KEY="P"+A$(82+K9,14),DOM=2410)
2440 GOTO 2020
2450 REM "Cost Unit
2455 Q4=Q3
2460 CALL "ZZENTR","SUX",A{ALL},A$,X4$,X3$,7,11,96+K9,4,C0,"","{3"+X$,"U/M","FM2IAA18","U/M","ZZ2MUA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2460,2461
2470 FIND (Z[6],KEY="U/M"+A$(96+K9,4),DOM=2460)IOL=0360
2475 IF ABS(C0)=4 THEN C0=0
2490 GOTO 2020
2550 REM "P/O Last cost
2560 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,22,11,1,621,C0,M1$,"{3"+X$,"","FM2IAA22","","",""
2570 IF A$(108,1)="Y" THEN SP0=Z[12]; CALL "UPDSP0;DETERMINE_FMS",SP0,A$(1,10),A$(11,10),A[29],A[1],NEWFMS,A[30],A[3],A[2],A[4]; IF NEWFMS<>A[29] THEN PRINT @(22,12),NEWFMS:M1$; A[29]=NEWFMS ! SSP188067
2575 CALL "UPDSP0;GET_IGP",Z[19],A$(1,20),A[3],A[1],NEWPRICE,A[2],A[4],FOUNDSP0; IF FOUNDSP0 AND NEWPRICE<>A[3] THEN A[3]=NEWPRICE; PRINT @(22,10),A[3]:M1$
2590 GOTO 2020
2600 REM "Selling Unit
2605 Q2=Q1
2610 CALL "ZZENTR","SUX",A{ALL},A$,X4$,X3$,7,10,100+K9,4,C0,"","{3"+X$,"U/M","FM2IAA24","U/M","ZZ2MUA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2610,2611
2615 FIND (Z[6],KEY="U/M"+A$(100+K9,4),DOM=2610)IOL=0350
2620 IF ABS(C0)=4 THEN C0=0
2640 GOTO 2020
2700 REM "Selling price"
2710 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,22,10,3,621,C0,M1$,"{3"+X$,"","FM2IAA28","","",""
2712 FMSPERCENT=A[30]
2715 IF A$(108,1)="Y" THEN SP0=Z[12]; CALL "UPDSP0;DETERMINE_FMS",SP0,A$(1,10),A$(11,10),A[29],A[1],NEWFMS,FMSPERCENT,A[3],A[2],A[4]; IF NEWFMS<>A[29] THEN PRINT @(22,12),NEWFMS:M1$; A[29]=NEWFMS ! SSP188067
2716 IF FMSPERCENT<>A[30] THEN A[30]=FMSPERCENT; PRINT @(43,12),A[30]:"-###.00" ! fmspercent is 0 if using special pricing fms by cost
2720 ! REPLACED BY LINE 2715; IF A[30]<>0 THEN A[29]=A[30]*A[3]/100; PRINT @(22,12),A[29]:M1$
2740 GOTO 2020
2750 REM "Selling Qty/Unit
2752 P1=A[4]; IF MID(E$,20,1)<>"Y" THEN GOTO 2760
2754 IF E[0]<>0 THEN A[4]=E[0] ELSE A[4]=E[1]
2756 PRINT @(13,10),A[4]:M6$,; GOTO 2761
2760 CALL "ZZENTR","NX&",A{ALL},A$,X4$,X3$,13,10,4,601,C0,M6$,"{3"+X$,"","FM2IAA30","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2760,2761
2765 IF MID(E$,20,1)<>"Y" THEN Q1=A[4] ELSE Q1=E[1]
2768 REM "*** For now, do not convert
2769 Q1=1
2770 IF Q1<>Q2 THEN GOSUB 7800
2780 IF P1<>A[4] THEN GOSUB 7900
2790 GOTO 2020
3050 REM "Finders fee (Gp, Sales)
3055 X$="Finder's fee based upon: S=sales, G=gross profit, C=cost, Blank=none"
3060 CALL "ZZENTR","SRUX",A{ALL},A$,X4$,X3$,45,14,115+K9,1,C0,"","{3"+X$,"GSCF ","FM2IAA42","","",""
3090 GOTO 2020
3100 REM "Finders fee percentage
3105 IF MID(A$,115+K9,1)=" " THEN A[7]=0; PRINT @(53,14),A[7]:M3$,; GOTO 2020
3110 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,52,14,7,420,C0,M4$,"{3"+X$,"","FM2IAA44","","",""
3140 GOTO 2020
3150 REM "Finders fee vendor code
3155 IF MID(A$,115+K9,1)=" " THEN A$(116+K9,10)=""; PRINT @(69,14),'CL',; GOTO 2020
3160 CALL "ZZENTR","AUX",A{ALL},A$,X4$,X3$,69,14,116+K9,10,C0,"A/P","{3"+X$,"","FM2IAA46","AP4","X",""; IF ABS(C0)=102 THEN J=2; RUN "FM2IAE" ELSE IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3160,3161
3165 IF C0<0 THEN GOTO 3190
3166 IF POS(" "<>MID(A$,116+K9,10))=0 THEN DIM D1$(100); GOTO 3175
3170 FIND (Z[4],KEY=A$(116+K9,10),DOM=3160)IOL=0341
3175 PRINT @(25,22),D1$(11,35),
3190 GOTO 2020
3200 REM "Quantity per case
3210 CALL "ZZENTR","N&",A{ALL},A$,X4$,X3$,71,10,0,601,C0,M6$,"{3"+X$,"","FM2IAA48","","",""
3240 GOTO 2020
3250 REM "Weight per case"
3255 X$="Enter weight of one carton in pounds"
3260 CALL "ZZENTR","N.",A{ALL},A$,X4$,X3$,70,9,18,521,C0,M4$,"{3"+X$,"","FM2IAA50","","",""
3290 GOTO 2020
3300 REM "Ending number
3310 CALL "ZZENTR","R",A{ALL},A$,X4$,X3$,42,17,140+K9,9,C0,"","{3"+X$,"","FM2IAA52","","",""
3340 GOTO 2020
3350 REM "Last sales order
3360 CALL "ZZENTR","A",A{ALL},A$,X4$,X3$,15,17,132+K9,8,C0,"O/P","{3"+X$,"","FM2IAA54","","",""
3390 GOTO 2020
3400 REM "Last order date
3410 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,1,17,126,6,C0,"","{3"+X$,"","FM2IAA37","","",""
3440 GOTO 2020
3500 REM "Quantity on last order"
3505 DIM J[1]; J[0]=A[16]/Q1
3510 CALL "ZZENTR","NX",J{ALL},A$,X4$,X3$,26,17,0,802,C0,M2$,"{3"+X$,"","FM2IAA35","","",""
3520 IF C0>0 THEN A[16]=J[0]*Q1
3540 GOTO 2020
3550 REM "prev. job number"
3560 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,54,17,149,12,C0,"","{3"+X$,"","FM2IAA51","","",""
3590 GOTO 2020
3600 REM "Prev job date"
3610 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,68,17,161,6,C0,"","{3"+X$,"","FM2IAA33","","",""
3640 GOTO 2020
3650 REM "# of inv
3655 IF X0=37 THEN I0=20,I1=8 ELSE I0=21,I1=12; REM SSP 214083
3660 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,15,I0,I1,401,C0,M0$,"{3"+X$,"","FM2IAA61","","",""
3690 GOTO 2020
3700 REM "Quanity last years
3705 IF X0=38 THEN I0=20,I1=9 ELSE I0=21,I1=13; REM SSP 214083
3707 DIM J[1]; J[0]=A[I1]/Q1
3710 CALL "ZZENTR","NX",J{ALL},A$,X4$,X3$,26,I0,0,802,C0,M2$,"{3"+X$,"","FM2IAA63","","",""
3720 IF C0>0 THEN A[I1]=J[0]*Q1
3740 GOTO 2020
3750 REM "Net sales
3755 IF X0=39 THEN I0=20,I1=10 ELSE I0=21,I1=14; REM SSP 214083
3760 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,38,I0,I1,822,C0,M5$,"{3"+X$,"","FM2IAA65","","",""
3770 IF A[I1]=0 THEN PRINT @(68,I0),0:M3$, ELSE PRINT @(68,I0),A[I1+1]*100/A[I1]:M3$,
3790 GOTO 2020
3800 REM "Gross profit
3805 IF X0=40 THEN I0=20,I1=11 ELSE I0=21,I1=15; REM SSP 214083
3810 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,53,I0,I1,822,C0,M5$,"{3"+X$,"","FM2IAA67","","",""
3820 IF A[I1-1]=0 THEN PRINT @(68,I0),0:M3$, ELSE PRINT @(68,I0),A[I1]*100/A[I1-1]:M3$,
3840 GOTO 2020
3850 REM "Cost qty/unit"
3852 P2=A[2]; IF MID(F$,20,1)<>"Y" THEN GOTO 3860
3854 IF F[0]<>0 THEN A[2]=F[0] ELSE A[2]=F[1]
3856 PRINT @(13,11),A[2]:M6$,; GOTO 3861
3860 CALL "ZZENTR","NX&",A{ALL},A$,X4$,X3$,13,11,2,601,C0,M6$,"{3"+X$,"","FM2IAA30","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3860,3861
3865 IF MID(F$,20,1)<>"Y" THEN Q3=A[2] ELSE Q3=F[1]
3868 REM "*** For now, do not convert
3869 Q3=1
3870 IF Q3<>Q4 THEN GOSUB 7850
3880 IF P2<>A[2] THEN GOSUB 7950
3890 GOTO 2020
3900 REM "Combination Qty"
3905 DIM J[1]; J[0]=A[17]/Q3
3907 X$="Enter total quantity with number of orders right of decimal"
3910 CALL "ZZENTR","N.&X",J{ALL},A$,X4$,X3$,65,11,0,822,C0,M5$(2),"{3"+X$,"","FM2IAA30","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3910,3911
3920 IF C0>0 THEN A[17]=J[0]*Q3
3940 GOTO 2020
4000 REM "Forms Management?
4004 IF LEN(C1$)>24 THEN IF POS(MID(C1$,24,1)="YU")>0 THEN GOTO 4006
4005 A$(108,1)="N"; PRINT @(7,12),A$(108,1),; GOTO 2020
4007 X$="Keep Item Management Statistics on this Item?" ! ssp#230733
4008 Q$=A$(108,1)
4010 CALL "ZZENTR","YX",A{ALL},A$,X4$,X3$,7,12,108,1,C0,"","{3"+X$,"","FM2IAA35","","",""
4012 IF MID(A$,108,1)="N" AND Q$="Y" THEN CALL "ZZPROM",".Y",X3$,S3,"VERIFY: You are taking this item off forms mgt!","","",0; ON S3 GOTO 4013,4010
4015 IF C0>0 THEN IF MID(A$,108,1)="N" THEN A[29]=0,A[30]=0; PRINT @(22,12),S$(1,LEN(M1$)),@(43,12),S$(1,LEN(M3$)),
4020 IF A$(108,1)="Y" THEN SP0=Z[12]; CALL "UPDSP0;DETERMINE_FMS",SP0,A$(1,10),A$(11,10),A[29],A[1],NEWFMS,A[30],A[3],A[2],A[4]; IF NEWFMS<>A[29] THEN PRINT @(22,12),NEWFMS:M1$; A[29]=NEWFMS ! SSP188067
4040 GOTO 2020
4050 REM "Numbering?
4055 X$="Is this a numbered item?"
4060 CALL "ZZENTR","SRU",A{ALL},A$,X4$,X3$,49,9,104,1,C0,"","{3"+X$," YN","FM2IAA45","","",""
4090 GOTO 2020
4100 REM "Stock item?
4105 X$="Enter Y if stock item"; IF MID(P2$,187,1)="Y" THEN X$=X$+", S for Supply Item"
4106 Q$=" YN"; IF MID(P2$,187,1)="Y" THEN Q$=Q$+"S"
4110 CALL "ZZENTR","SRU",A{ALL},A$,X4$,X3$,49,10,105,1,C0,"","{3"+X$,Q$,"FM2IAA45","","",""
4140 GOTO 2020
4150 REM "FMS price to using locations
4155 IF MID(A$,108,1)<>"Y" THEN GOTO 2020 ELSE IF MID(C1$,195,1)="1" AND C0<>2 THEN GOTO 2020
4157 X$="Price per "+A$(100,4)+" customer charges using locations"
4160 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,22,12,29,621,C0,M1$,"{3"+X$,"","FM2IAA82","","",""
4190 GOTO 2020
4200 REM "Item Function Code"
4210 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,65,12,173,12,C0,"","{3"+X$,"","FM2IAA86","","",""
4240 GOTO 2020
4250 REM "Revision date
4255 X$="If applicable, enter item revision date or level"
4260 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,72,5,167,6,C0,"","{3"+X$,"","FM2IAA87","","",""
4290 GOTO 2020
4300 REM "Item Group
4305 IF MID(P2$,149,1)<>"Y" THEN GOTO 2020
4309 J9$=A$(82,10),J8$=A$(291,10)
4310 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,7,14,291,10,C0,"","{3"+X$,J9$,"FM2IAA88","FE0","FE2MAA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 4310,4311
4315 IF POS(" "<>MID(A$,291,10))=0 THEN A$(301,10)=""; PRINT @(28,14),A$(301,10); GOTO 4330
4320 J9$=A$(82,10)+A$(291,10)
4325 CALL "IC2MAE",X3$,J9$,0,F9; IF F9=1 THEN GOTO 4310
4340 GOTO 2020
4350 REM "Pricing Features
4355 IF MID(P2$,149,1)<>"Y" OR POS(" "<>MID(A$,291,10))=0 THEN GOTO 2020
4359 J9$=A$(82,10)+A$(291,10),J8$=A$(301,10); READ (Z[8],KEY=J9$,DOM=4360)X$; GOSUB 8000
4360 REM CALL "ZZENTR","SUF",A(ALL),A$,X4$,X3$,28,14,301,10,C0,"","{3"+X$,J9$,"FM2IAA89","-FE1","FE2MBA",""; IF ABS(C0)>4 THEN GOSUB 06400; ON C0 GOTO 04360,04361
4361 GOSUB 7500
4365 IF J9$=MID(A$,301,10) OR POS(" "<>MID(A$,301,10))=0 THEN GOTO 4380
4370 J9$=A$(82,10)+A$(291,20)
4375 CALL "IC2MAE",X3$,J9$,1,F9; A$(301,10)=J9$(21,10); PRINT @(28,14),A$(301,10),; IF F9=1 THEN GOTO 4360
4380 REM PRINT @(0,15),'CE','SB'; GOSUB 06090; GOSUB 06238
4390 GOTO 2020
4400 REM "FMS %
4405 IF MID(C1$,195,1)<>"1" OR MID(A$,108,1)<>"Y" THEN GOTO 2020
4408 IF C1<>0 THEN X$="NOTE: Default IMS % is "+STR(C1:M3$)+"%"
4410 CALL "ZZENTR","NX.",A{ALL},A$,X4$,X3$,43,12,30,320,C0,M3$,"{3"+X$,"","FM2IAA82","","",""
4412 FMSPERCENT=A[30]
4415 IF C0>0 THEN IF A$(108,1)="Y" THEN SP0=Z[12]; CALL "UPDSP0;DETERMINE_FMS",SP0,A$(1,10),A$(11,10),A[29],A[1],NEWFMS,FMSPERCENT,A[3],A[2],A[4]; IF NEWFMS<>A[29] THEN PRINT @(22,12),NEWFMS:M1$; A[29]=NEWFMS ! SSP188067
4417 IF A[30]<>FMSPERCENT THEN A[30]=FMSPERCENT; PRINT @(43,12),A[30]:"-###.00" ! fms percent is 0 if using special pricing fms by cost
4420 IF A[30]<>0 THEN IF A[30]<100 THEN CALL "ZZPROM",".6A",X3$,S3,"NOTE: IMS % less than 100 means that the IMS price can be LESS than Sell price!","","",0; IF S3=1 THEN GOTO 4410
4440 GOTO 2020
4500 REM "Department part of cost code
4502 IF LEN(C1$)<235 THEN GOTO 2020 ELSE IF MID(C1$,236,1)<>"Y" THEN GOTO 2020
4503 Q$="f"+A$(1,10)
4504 J0$=""; FIND (Z[3],KEY="f"+A$(1,10)+A$(341,9),DOM=4505)J0$; J0$=FNS$(J0$(21,30))
4505 X$="DEFAULT dept for cost center acctg."; IF J0$>"" THEN X$=X$+" Currently "+J0$
4510 CALL "ZZENTR","AU",A{ALL},A$,X4$,X3$,49,11,341,9,C0,"FMB","{3"+X$,Q$,"FM2IAA66","FM2MPM","FM2MPM",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 4510,4511
4515 IF MID(A$,341,9)<>MID(S$,1,9) THEN X$=""; IF C0>0 THEN FIND (Z[3],KEY="f"+A$(1,10)+A$(341,9),DOM=4510)J0$
4540 GOTO 2020
4550 ! Contract expiration date, WO155894
4553 IF MID(P2$,304,1)<>"Y" THEN GOTO 2020
4554 IF A$(105,1)<>"Y" THEN GOTO 2020 ! Stock item flag
4555 X$="Contract Expiration Date:"
4560 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,53,22,380,6,C0,"","{3"+X$,"","FM2IAA68","","",""
4590 GOTO 2020
4600 REM "Tax Type Flag ssp 201823 
4605 X$="Tax Types - C=1/2, T=Taxable, E=Exempt, ' '= Don't Process"; REM SSP 201823 
4610 CALL "ZZENTR","SRU",A{ALL},A$,X4$,X3$,78,13,387,1,C0,"","{3"+X$,"CTE ","IC2MAB86","","",""
4640 GOTO 2020
5000 REM "Multiple Finders Fee Routine
5010 CALL "ZZPROM","MF",X3$,Z,"","","",0; ON Z GOTO 2020,5020
5020 MF_KEY$=A$(1,20)
5030 CALL "FMGFV4",X3$,X4$,MF_KEY$ ! SSP 225300
5040 GOTO 2020 ! SSP 225300
5050 ! 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(0,3),"Customer code:",@(28,3),"Name:",@(0,4),Z0$,
6070 PRINT @(0,5),"Item code",@(11,5),"P/C",@(16,5),"Left-Rgt Top-Botm Plys",@(40,5),"Item description",@(62),"Rev date:",@(0,7),"Vendor:",@(21,7),"Name:",@(62,7),"Plant:",@(0,8),Z0$,
6075 PRINT @(7,9),"Unit  Qty/Unit",@(25),"Amt/Unit",@(53),"Ctn weight:",
6076 PRINT @(0,10),"Price:",@(38,9),"Numbering?",@(55,10),"Ctn Pack:",@(37,10),"Stock item?",
6077 PRINT @(1,11),"Cost:",@(54),"Combo Qty:",
6078 PRINT @(2,12),"FMS?",@(11),"FMS Price:",@(55),"Function:",
6079 IF MID(C1$,236,1)="Y" THEN PRINT @(37,11),"Department:",
6080 IF C1$>"" THEN IF MID(C1$,195,1)="1" THEN PRINT @(37,12),"IMS %",
6088 PRINT @(0,13),Z0$,@(28,13),"1234567890",@(5),"Feature Driven Pricing",@(51),"Finder's Fee",@(69),"Tax Type:",@(0,14),"Group:",@(18,14),"Features:",@(38,14),"|Type:",@(47),"Rate:",@(61),"Vendor:",
6090 PRINT @(0,15),Z0$,@(32,15)," Previous Order ",@(1,16),"Order date",@(14,16),"Sales order",@(28,16),"Quantity",@(42,16),"Ending no.",@(55,16),"Job number",@(69,16),"Job date",@(0,18),Z0$,
6100 PRINT @(14,19),"No. of inv.",@(28,19),"Quantity",@(42,19),"Net sales",@(54,19),"Gross profit",@(70,19),"G.P.%",@(1,20),"Year to date:",@(0,21),"All last year:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 PRINT @(11,6),A$(21+K9,3),@(16,6),A$(24+K9,8),@(25,6),A$(32+K9,8),@(35,6),A$(40+K9,2),@(39,6),A$(42+K9,40),@(72,5),A$(167,6),
6224 CALL "ZZDISP","A",A$(82+K9,10),"A/P",X3$,"","",8,7,X4$
6228 PRINT @(27,7),D$(11,35),@(69,7),A$(92+K9,4),
6230 PRINT @(70,9),A[18]:M4$,@(7,10),A$(100,4),@(13,10),A[4]:M6$,@(22,10),A[3]:M1$,@(71,10),A[0]:M6$,@(7,11),A$(96,4),@(13,11),A[2]:M6$,@(22,11),A[1]:M1$,@(49,9),A$(104,1),@(49,10),A$(105,1),@(7,12),A$(108,1),@(22,12),A[29]:M1$,@(65,12),A$(173,12),
6232 PRINT @(7,14),A$(291,10),@(28,14),A$(301,10),@(45,14),A$(115,1),@(53,14),A[7]:M3$,; IF MID(P2$,149,1)<>"Y" THEN DIM X$(10); X$(2)="n/a"; PRINT @(7,14),X$,@(28,14),X$,
6233 IF LEN(C1$)>235 THEN IF MID(C1$,236,1)="Y" THEN CALL "ZZDISP","A",A$(341,9),"FMB",X3$,"","",49,11,X4$
6235 CALL "ZZDISP","A",A$(116,10),"A/P",X3$,"","",69,14,X4$
6238 CALL "ZZDISP","A",A$(132,8),"O/P",X3$,"","",15,17,X4$
6240 PRINT @(1,17),FND$(A$(126,6)),@(42,17),A$(140,9),@(54,17),A$(149,12),@(68,17),FND$(A$(161,6)),
6241 PRINT @(78,13),A$(387,1); REM SSP 207282
6245 PRINT @(15,20),A[8]:M0$,@(38,20),A[10]:M5$,@(53,20),A[11]:M5$,; IF A[10]=0 THEN PRINT @(68,20),0:M3$, ELSE PRINT @(68,20),A[11]*100/A[10]:M3$,
6250 PRINT @(15,21),A[12]:M0$,@(38,21),A[14]:M5$,@(53,21),A[15]:M5$,; IF A[14]=0 THEN PRINT @(68,21),0:M3$, ELSE PRINT @(68,21),A[15]*100/A[14]:M3$,
6300 GOSUB 7800; GOSUB 7850
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 CALL "ZZDISP","A",A$(1+K9,10),"A/R",X3$,"","",15,3,X4$
6464 PRINT @(34,3),B$(11,35),@(0,6),A$(11+K9,10),
6490 RETURN 
7300 REM 
7310 ON FPT(C0)*10 GOTO 7390,7325,7330,7390
7325 IF V8$="" THEN X0=X0+4; GOTO 7390 ELSE X0=NUM(V8$(POS(STR(X0:"00")<=V8$,2),2)); GOTO 7390
7330 IF V8$="" THEN X0=X0-6; GOTO 7335 ELSE X0=POS(STR(X0-1:"00")<=V8$,2)-2; IF X0<0 THEN X0=0; GOTO 7390 ELSE X0=NUM(V8$(X0,2)); GOTO 7390
7335 IF X0<0 THEN X0=0; GOTO 7390
7390 C0=3; RETURN 
7500 REM "NEW FEATURES STUFF
7505 X$=A$(301,10),Z$=A$(82,10)+A$(291,10)
7520 CALL "IC2MAH",X3$,X4$,Z$,X$
7530 A$(301,10)=X$
7550 GOSUB 6400
7590 RETURN 
7800 REM "Display Price dependent quantities (Q1)
7810 IF Q1=0 THEN Q1=1; RETURN 
7820 PRINT @(26,17),A[16]/Q1:M2$,@(26,20),A[9]/Q1:M2$,@(26,21),A[13]/Q1:M2$,
7840 RETURN 
7850 REM "Display Cost dependent quantities (Q3)
7860 IF Q3=0 THEN Q3=1; RETURN 
7870 PRINT @(65,11),A[17]/Q3:M5$(2),
7890 RETURN 
7900 REM "Change price if price qty/unit changed
7920 IF P1<>0 THEN A[3]=A[3]*A[4]/P1
7930 IF P1<>0 AND P1<>A[4] THEN A[29]=A[29]*A[4]/P1; PRINT @(22,12),A[29]:M1$,
7940 RETURN 
7950 REM "Change cost if cost qty/unit changed
7960 IF P2<>0 THEN A[1]=A[1]*A[2]/P2
7990 RETURN 
8000 REM "Display Special instructions
8010 PRINT @(0,15),'CE',
8015 IF POS(" "<>MID(X$,63,60*7))=0 THEN PRINT @(10,16),"...No Special Instructions for this Item Group...",; GOTO 8050
8017 GOTO 8050
8020 FOR Z=0 TO 6
8030 PRINT @(10,15+Z),X$(63+Z*60,60),
8040 NEXT Z
8050 X$=""; RETURN 
8900 REM "Function Statements
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNS$(Z9$)=Z9$(1,POS(S$=Z9$+S$)-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF MID(X3$,47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9705 IF U0$="" THEN GOTO 9900
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9725 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9730 RUN U1$(1,6)
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "188067-New Pricing changes. Standard GP Percentage for a           
56001 REM "201823-Provide the ability to set the taxable flag  by Item   
56002 REM "214083-Can no longer get to Year To Date G.P % on customer item    
56003 REM "273243-Program; FM2IAZ F/M-S1 #1 need to change FMS veribage       
56004 REM "276772-Multiple Finder's Fees per Customer/Item/Order Line.        
