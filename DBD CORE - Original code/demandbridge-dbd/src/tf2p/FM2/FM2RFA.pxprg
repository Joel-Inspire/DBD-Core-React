0010 REM "Client Reorder Notice Printing <FM2RFA>ORIG
0025 REM "(C) Copyright 1987 - All Rights Reserved
0035 REM "5.7 - 09/28/21 - 15.700372 - jvv - SSP# 307379
0036 REM "MOD TO USE CUSTOMER PURCH AGENT IF NO CONTACT
0037 REM "307379-Error in Reorder print if special character in Item Number. 
0038 REM "MOD 6/10/91 FOR ORDER GRAPH, 6/18 FOR 'O' SYSTEM PARAM 7701,1365
0039 REM "ADDED LINES 1322,1323 FOR CUSTOMER CONTACT BY ITEM PROBLEM
0040 REM "Copyright 2021 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 SETERR 9000; SETESC 9300
0110 X0$="FM2RFA",X1$="Client Reorder Notice Printing"
0120 DIM A$(80),A[25],B$(325),B[31],C$(195),S9$(15),S$(30),P1$(161),E$(599)
0140 Z0$="##,###.0-",Z1$="###,##0-",Z2$="###,##0",Z3$="##0",Z4$="####",Z5$="###,##0-",Z6$="#,##0",Z7$="##,##0.00",Z8$="###" ! SSP227003 jdf,SSP#231506
0170 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):Z4$) ! SSP227003 jdf
0175 DEF FNS$(Z9$)=Z9$(1,POS(S$=Z9$+S$)-1)
0180 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
0210 T=1,T0=1,T1=1
0215 T9=3,T9$="JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC"
0220 W3=79
0225 REM LET W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0236 DIM B0$(208,"*"),B1$(208,"*"),H$(300,"*"),B2$(208,"*")
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR FM5- REORDER NOTICE
0310 IOLIST A$,A[0],A[1],A[2],STR(A[3]:"000"),A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24]
0311 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24]
0315 REM "IOLIST AR1 - CUSTOMER MASTER
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19],B[20],B[21],B[22],B[23],B[24],B[25],B[26],B[27],B[28],B[29],B[30],B[31]
0325 REM "IOLIST FM1 - ITEMS MASTER
0350 IOLIST E$
0380 IOLIST H$,H[0],H[1],H[2],H[3],H[4],H[5]
0390 IOLIST I$(1),I9$,I[0],I[1],I[2],I[3],I[4],I[5],I[6],I[7]
0400 IOLIST J$,J[0],J[1],J[2],J[3],J[4],J[5]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O FM5...  02O FM1...  03O ZY3...  040 ZY4...  05O AR1...  06O ZY2    07O FMP... 08O FMI...  09O FM4...  10O FS6... 12O ZY2  13O ZZPARM  15O FMU...  "
0520 GOSUB 9700; ON Z0 GOTO 0521,9900
0530 READ (Z[13],KEY=X3$(9,3)+"F/M")FM_PARMS$
0531 READ (Z[13],KEY="STATF/M")F9$
0535 FOR X=0 TO 9; F1$=F1$+F9$(49+X*17,5); NEXT X
0560 READ (Z[13],KEY=X3$(9,3)+"FMS")F8$
0585 RT_PARM$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,RT_PARM$
0700 REM "Open Printer
0720 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0721,9920
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[5]; U=26,U0=5,U[0]=0,U[1]=1,U[2]=1,U[3]=10,U[4]=4,U[5]=10,V2=1,V3=10,V4=4,U0$="      ",T5$="                         "
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0920 U1=Z[1]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0966 IF V0$(112,1)="Y" THEN ALT_KEY=1 ELSE ALT_KEY=0; REM " SSP# 160702
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,KNO=ALT_KEY,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(U1,KNO=ALT_KEY,END=5000)
1006 IF ALT_KEY<>0 THEN HOLD$=U$; DIM U$(26); U$=HOLD$(1,12)+HOLD$(16,14); HOLD$=""
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN READ (U1,KNO=ALT_KEY,END=5000); EXITTO 1000
1030 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U9=1 THEN EXITTO 5000 ELSE READ (U1,KNO=ALT_KEY,END=5000); EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 IF S9$(9,2)<>U9$(1,2) THEN GOSUB 6500
1150 READ (Z[1],KEY=U$,KNO=0,DOM=1000)IOL=0311
1170 DIM H$(250),H[10]; H$(25,1)="1"; READ (Z[8],KEY=A$(3,10)+A$(17,10)+A$(13,4),DOM=1171)IOL=0380; A[6]=H[0]
1180 IF MID(H$,25,1)<>"1" THEN GOTO 1000
1190 GOSUB 7900; ON Q GOTO 1191,1000
1195 IF F$="*" THEN GOSUB 5300; GOTO 1000
1200 REM 
1220 IF MID(E$,1,10)<>MID(U9$,3,10) THEN GOSUB 6400 ! SSP227003 jdf
1300 REM "Read other data
1310 READ (Z[2],KEY=U9$(3,10)+U9$(17,10),DOM=1000)IOL=0320
1315 IF B$(195,1)="I" THEN GOTO 1000
1320 H8$=B$; GOSUB 8000
1322 DIM B1$(208); B1$(45)=E$(56,163),B1$(15,30)=E$(11,30); READ (Z[7],KEY="a"+E1$(2,10)+E1$(109,3),DOM=1323)B1$(1)
1323 X7$=""; FIND (Z[7],KEY="a"+E1$(2,10)+B$(209,3),DOM=1324)X7$; B1$(1)=X7$
1330 IF POS(" "<>B1$(75,79))=0 THEN B1$(45,109)=E$(56,109)
1332 IF FNS$(B1$(154,20))="" THEN B1$(154,20)=E$(165,20)
1335 IF FNS$(B1$(15,30))="" OR POS(X3$(9,3)="055",3)>0 THEN B1$(15,30)=E$(11,30)
1340 F0$(1)=""
1360 GOSUB 7800
1365 IF E1$(24,1)<>"Y" OR E1$(61,1)="O" OR F8$(13,1)="O" THEN GOSUB 8200
1400 DIM FMU$(107); READ (Z[15],KEY=B$(1,20)+"001",DOM=1401)FMU$; REM "Read FMU for field 48
1510 C0=B[25],C0$=B$(238,4); IF C0=0 THEN C0=B[0],C0$=B$(100,4); IF C0=0 THEN C0=1,C0$="EA"
1511 C0_DIV=B[25]; IF C0_DIV=0 THEN C0_DIV=B[0]; IF C0_DIV=0 THEN C0_DIV=1; REM "SSP# 133632 
1513 PMODE=0,X$=FID(W9),FORM_CODE$="RO"+DIM(8)+U$(2,1); CALL "EF2PRM",ERR=1514,X3$,X4$,X$,P6$,F6$,FORM_CODE$,FORM_PARM$; IF P6$(144,2)="JF" THEN JETFORM_MODE=1,JETFORM_DEV=W9,PMODE=JETFORM_MODE,Z1$="###,###,##0-" ELSE IF P6$(144,2)="UF" THEN UNFORM_MODE=2,UNFORM_DEV=W9,PMODE=UNFORM_MODE ! SSP#231506 SSP 241023 !SSP#246748
1515 GOSUB 4000
1520 GOSUB 6000
1900 REM "Accumulate Totals"
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
3100 REM "Customer break
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3135 T=1,T$=U5$,T6$=U1$
3155 IF U0$(1,1)="Y" THEN GOSUB 6000
3160 U1$=U9$(1,V1)
3165 DIM C$(195); READ (Z[5],KEY=A$(1,10),DOM=1321)IOL=0320
3167 D1$=A$(21,6)
3170 GOSUB 7500
3175 GOSUB 2000
3180 L1$="1"
3185 GOSUB 2500
3187 GOSUB 7700
3190 RETURN 
3200 REM "Item break
3290 RETURN 
3300 REM "Print fields 32-53
3310 N=32; GOSUB 7500; REM "Supply whse name???
3320 N=33; GOSUB 7500; REM "Std whse address???
3330 N=34; GOSUB 7500; IF Q>0 THEN IF B1$>"" THEN Q$=B1$(12,3); GOSUB 7600; REM "Contact code
3340 N=35; GOSUB 7500; IF Q>0 THEN IF B1$>"" AND B1$(194,14)<>S$(1,14) THEN CALL "ZZDISP","PX",B1$(194,14),"",X3$,Q$,"",0,0,X4$; GOSUB 7600; REM "Contact phone
3350 N=36; GOSUB 7500; IF Q>0 THEN IF B1$>"" AND B1$(132,12)<>S$(1,12) THEN CALL "ZZDISP","POX",B1$(132,12),"",X3$,Q$,"",0,0,X4$; GOSUB 7600; REM "Contact fax
3360 N=37; GOSUB 7500; IF Q>0 THEN IF B1$>"" THEN Q$=B1$(15,30); GOSUB 7600; REM "Alternate company name
3370 IF O>0 THEN GOSUB 8550; REM "Fields 38-41
3380 N=42; GOSUB 7500; REM "Total inv value???
3390 N=43; GOSUB 7500; IF Q>0 THEN Q$=A$(65,16); GOSUB 7600; REM "Notice reason
3400 N=44; GOSUB 7500; IF Q>0 THEN Q$=STR(A[2]/C0:Z5$); GOSUB 7600; REM "Current supply in units  ! SSP227003 jdf
3410 N=45; GOSUB 7500; IF Q>0 THEN Q$=STR(A[1]/C0:Z2$); GOSUB 7600; REM "Avg usage in units  ! SSP227003 jdf
3420 N=46; GOSUB 7500; IF Q>0 THEN Q$=B$(345,5); GOSUB 7600; REM "Item dept code
3430 N=47; GOSUB 7500; REM "Std cost ctr???
3440 N=48; GOSUB 7500; IF Q>0 THEN Q$=FMU$(30,6); GOSUB 7600; REM "Paper 1st ply
3450 N=49; GOSUB 7500; IF Q>0 THEN Q$=STR(A[5]/C0:Z5$); GOSUB 7600; REM "ROP in units    ! SSP227003 jdf
3460 N=50; GOSUB 7500; REM "Order date invoiced???
3470 N=51; GOSUB 7500; REM "Order qty inv units???
3480 N=52; GOSUB 7500; REM "Usage hist special???
3490 RETURN 
4000 REM "Set Print Image to Data
4001 DIM F0$(F0*F1)
4005 IF JETFORM_MODE OR UNFORM_MODE THEN GOSUB IMAGE_LIBRARY_SETUP; REM SSP 190630 SSP 241023
4006 IF JETFORM_MODE AND FIRST=0 THEN CALL "EF2JET",X3$,X4$,P6$,"N",JETFORM_DEV,FORM_PARM$,JOB_MODIFIER$,RET$; REM "If in jetform mode, start a new file
4007 IF UNFORM_MODE AND FIRST=0 THEN CALL "EF2UNF",X3$,X4$,P6$,"N",UNFORM_DEV,FORM_PARM$,JOB_MODIFIER$,RET$; REM "If in unform mode, start a new file SSP 241023
4008 IF P6$="" THEN GOTO *NEXT ELSE GOSUB 8360; CALL "EF2FAX",X3$,X4$,P6$,F6$,W9,X$,J6$,PMODE,EM$; FIRST=1 ! SSP 241023
4010 N=1; GOSUB 7500; IF Q=0 THEN GOTO 4050
4015 Q$=FND$(A$(27,6)); GOSUB 7600
4050 REM "Reminder date
4055 N=N+1
4100 REM "Client Rep
4110 GOSUB 7500; IF Q>0 THEN Q$=B1$(154,20); GOSUB 7600
4150 REM "CLIENT NUMBER
4155 GOSUB 7500; IF Q=0 THEN GOTO 4200
4160 CALL "ZZDISP","AX",A$(3,10),"A/R",X3$,Q$,"",0,0,X4$
4165 GOSUB 7600
4200 REM "CUSTOMER NAME
4210 GOSUB 7500; IF Q=0 THEN GOTO 4250
4220 IF FNS$(B1$(15,30))<>"" THEN Q$=B1$(15,30) ELSE Q$=E$(11,40)
4230 GOSUB 7600
4250 REM "Client Address
4255 GOSUB 7500; IF Q=0 THEN GOTO 4300
4260 FOR Z0=0 TO 2; IF Z0<2 THEN Q$=B1$(45+Z0*30,30) ELSE Q$=FNS$(B1$(105,16))+", "+B1$(121,2)+"  "+B1$(123,5)
4265 GOSUB 7600
4270 FOR Y=1 TO Q; P[Y-1,0]=P[Y-1,0]+1; NEXT Y
4280 NEXT Z0
4300 REM "Items Manager
4305 GOSUB 7500; IF Q>0 THEN Q$=FNS$(B0$(5,30)); GOSUB 7600
4350 REM "SERVICE REP
4360 N=N+1
4400 REM "ITEM CODE
4410 GOSUB 7500; IF Q=0 THEN GOTO 4450
4420 Q$=B$(11,10); GOSUB 7600
4450 REM "item descr
4460 GOSUB 7500; IF Q=0 THEN GOTO 4500 ELSE Q$=B$(42,40); GOSUB 7600
4500 REM "Item size
4510 GOSUB 7500; IF Q>0 THEN Q$=H9$; GOSUB 7600
4520 REM "Unit of issue
4530 GOSUB 7500; IF Q>0 THEN GOSUB 8400; GOSUB 7600
4550 REM "NO OF PARTS
4560 GOSUB 7500; IF Q>0 THEN Q$=B$(40,2); GOSUB 7600
4580 REM "Type of item(Z(7),KEY=
4585 GOSUB 7500; IF Q=0 THEN GOTO 4600 ELSE FIND (Z[7],KEY="T"+B$(21,3),DOM=4586)Q$; Q$=Q$(5,15); GOSUB 7600
4600 REM "Last order date
4602 GOSUB 8150; GOSUB 7500; IF Q=0 OR O=0 THEN GOTO 4630
4603 Q0=O,Q1=Q0-Q+1; IF Q1<1 THEN Q1=1
4605 FOR Z=Q0 TO Q1 STEP -1
4608 IF POS("**"=O$)<>0 THEN DIM O$(LEN(O$),"0")
4610 Q$=FND$(O$((Z-1)*23+9,6)); Q$=Q$(1,3)+Q$(9)
4612 IF JETFORM_MODE THEN GOSUB JETFORM_PRINT; GOTO 4620
4615 S0=FNP(P[O-Z,0],P[O-Z,1]),S1=LEN(Q$); F0$(S0,S1)=Q$
4620 NEXT Z
4630 REM "Last qty ordered
4640 GOSUB 7500; IF Q=0 OR O=0 THEN GOTO 4660
4642 Q0=O,Q1=Q0-Q+1; IF Q1<1 THEN Q1=1
4643 FOR Z=Q0 TO Q1 STEP -1
4644 Q=NUM(O$((Z-1)*23+22,2))
4645 IF V0$(111,1)="Y" THEN Q$=STR(O[Q]/C0_DIV:Z1$) ELSE Q$=STR(O[Q]:Z1$); REM "SSP 133632  ! SSP227003 jdf
4647 IF JETFORM_MODE THEN GOSUB JETFORM_PRINT; GOTO 4650
4648 S0=FNP(P[O-Z,0],P[O-Z,1]),S1=LEN(Q$); F0$(S0,S1)=Q$
4650 NEXT Z
4660 REM "Reorder pt in qty
4670 GOSUB 7500; REM "SSP 133632
4675 IF Q>0 AND A[5]>0 AND V0$(111,1)="Y" THEN Q$=STR(A[5]/C0_DIV:Z1$) ELSE IF Q>0 AND A[5]>0 THEN Q$=STR(A[5]:Z1$); REM "SSP 133632  ! SSP227003 jdf
4680 IF Q>0 AND A[5]>0 THEN GOSUB 7600; REM "SSP# 133632
4690 REM "Reorder pt in days
4700 GOSUB 7500; IF Q>0 AND A[4]>0 THEN Q$=STR(A[4]:Z3$); GOSUB 7600 ! SSP227003 jdf
4720 REM "Avg usage/month
4730 GOSUB 7500; REM "SSP 133632
4735 IF Q>0 AND V0$(111,1)="Y" THEN Q$=STR(A[1]/C0_DIV:Z1$) ELSE Q$=STR(A[1]:Z1$); REM "SSP 133632  ! SSP227003 jdf
4737 IF Q>0 THEN GOSUB 7600; REM "SSP 133632 
4740 REM "Qty in inventory
4750 GOSUB 7500; REM "SSP 133632
4755 IF Q>0 AND V0$(111,1)="Y" THEN Q$=STR(A[2]/C0_DIV:Z1$) ELSE Q$=STR(A[2]:Z1$); REM "SSP 133632  ! SSP227003 jdf
4760 IF Q>0 THEN GOSUB 7600
4770 REM "no of days supply
4780 GOSUB 7500; IF Q>0 THEN Q$=STR(A[3]:Z3$); GOSUB 7600
4790 REM "Sug reorder qty
4800 GOSUB 7500; IF Q>0 AND A[6]>0 AND V0$(111,1)<>"Y" THEN Q$=STR(A[6]:Z1$); GOSUB 7600 ELSE IF Q>0 AND A[6]>0 AND V0$(111,1)="Y" THEN Q$=STR(A[6]/C0_DIV:Z1$); GOSUB 7600
4820 REM "USAGE HIST
4825 GOSUB 7500; IF Q=0 THEN GOTO 4850 ELSE GOSUB 7700
4826 REM " 4825 WAS - GOSUB 07500; IF Q=0 THEN LET N=N+1; GOTO 04850 ELSE GOSUB 07700
4850 GOSUB 7500; IF Q=0 THEN GOTO 4880
4855 FOR Y=0 TO 2; Q$=FNS$(H$(26+Y*60,60)); IF Q$>"" THEN GOSUB 7600; P[0,0]=P[0,0]+1
4860 NEXT Y
4880 REM "REVISION CODE
4885 GOSUB 7500; IF Q>0 THEN Q$=B$(167,6); GOSUB 7600
4890 GOSUB 7500; REM "THIS IS COMPANY ADDRESS  NOT IN SERVICE
4900 GOSUB 7500; IF Q>0 THEN Q$=B$(24,8); GOSUB 7600
4905 GOSUB 7500; IF Q>0 THEN Q$=B$(32,8); GOSUB 7600
4910 GOSUB 7500; IF Q=0 THEN GOTO 4925 ELSE IF MID(E1$,24,1)<>"Y" OR MID(E1$,61,1)="O" THEN Q$=O2$; GOTO 4920
4912 Q$="??/??/??"; CALL "ZZDATE",ERR=4913,X3$,"",A$(27,6),Q$,A[3],0,0,0,0,0,0,"","",""
4920 Q$=FND$(Q$); GOSUB 7600
4930 GOSUB 7500; IF Q>0 THEN CALL "ZZDISP","POX",B1$(132,12),"",X3$,Q$,"",0,0,X4$; GOSUB 7600
4935 GOSUB 7500; IF Q>0 THEN CALL "ZZDISP","PX",B1$(194,14),"",X3$,Q$,"",0,0,X4$; GOSUB 7600
4940 N=53; GOSUB 7500; IF Q>0 THEN Q$=B2$(5,30); GOSUB 7600
4950 REM "CIC CODE
4970 N=54; GOSUB 7500; IF Q>0 THEN Q$=B$(350,20); GOSUB 7600
4975 N=55; GOSUB 7500; IF Q>0 THEN Q$=STR(H[3]:Z8$); GOSUB 7600 ! SSP227003 jdf
4980 GOSUB 3300
4983 DATE$=FND$(A$(27,6)); NEW_DATE$=DATE$(7,4)+DATE$(1,2)+DATE$(4,2)
4984 N=56; GOSUB 7500; IF Q>0 THEN Q$=STP("PDFFILE:"+X3$(9,3)+DLM+"RO"+DLM+X3$(40,3)+DLM+J4$+SUB(SUB(SUB(SUB(B$(11,10),QUO,""),"'",""),"&",""),"/","")+NEW_DATE$,3); GOSUB 7600 ! 190630 SSP 307379
4985 FIRST=0
4990 RETURN 
5000 REM "End of Print
5005 IF F$="*" THEN GOTO 9900
5010 PRINT @(0,14),'CE',
5015 IF FID(W9)<>FID(0) THEN PRINTER$=FID(W9); CLOSE (W9); OPEN (W9)PRINTER$
5020 CALL "ZZPROM","Y",X3$,S3,"Did the Notices print correctly?","","",0
5030 ON S3 GOTO 5040,9916
5045 F$="*"; GOTO 0990
5300 REM "Update
5330 A$(39,6)=X3$(21,6); IF A$(33,6)=S$(1,6) THEN A$(33,6)=X3$(21,6)
5350 PRINT @(12,14),"Updating Reorder Print Dates..",U$
5360 WRITE (Z[1],KEY=U$)IOL=0310
5370 REM "FORM MASTERFILE
5375 IF U$(13,4)<>S$(1,4) THEN GOTO 5400
5380 EXTRACT (Z[2],KEY=U$(3,10)+U$(17,10),DOM=5395)IOL=0320
5385 IF B$(254,1)="3" THEN B$(261,30)="Notice Reprinted "+FND$(X3$(21,6)) ELSE B$(254,1)="3",B$(255,6)=X3$(21,6),B$(261,30)="Notice Printed"
5390 WRITE (Z[2],KEY=U$(3,10)+U$(17,10))IOL=0320; IF RT_PARM$<>"" THEN CUST$=U$(3,10); CALL "RT2WOC",ERR=5391,X3$,X4$,CUST$,"FM1...","U",U$(3,10)+U$(17,10)
5395 RETURN 
5400 REM "LOCATION RECORD (LOW STOCK)
5405 GOTO 5480; REM "DISCONNECT FOR NOW, 5/16/90 JSC
5410 DIM I[7]; EXTRACT (Z[9],KEY=U$(3,10)+U$(17,10)+U$(13,4))IOL=0390
5420 I$(38,1)="4"; IF I$(39,6)=S$(1,6) THEN I$(39,6)=X3$(21,6) ELSE I$(45,30)="Reprinted "+FND$(X3$(21,6))
5460 WRITE (Z[9],KEY=I$(1,24))IOL=0390
5490 GOTO 5320
5500 REM "
5520 GOTO 9900
6000 REM "Print item
6005 W=0
6007 IF JETFORM_MODE THEN GOTO 6090; REM "If jetform mode, skip printing
6020 FOR X=1 TO F0
6030 Y5$=F0$(FNP(X,1),F1)
6040 GOSUB 7300
6050 NEXT X
6090 RETURN 
6400 REM "Customer change
6410 FIND (Z[5],KEY=U9$(3,10))IOL=0350
6420 FIND (Z[7],KEY="D"+E$(1,10))E1$; IF E1$(24,1)<>"Y" THEN GOTO 6475
6422 IF LEN(A$)>57 THEN E1$(52,6)=A$(57,6)
6423 IF F8$(13,1)="O" OR E1$(24,1)<>"Y" OR E1$(61,1)="O" THEN GOTO 6475
6425 READ (Z[7],KEY="E"+E$(1,10)+E1$(52,4),DOM=6426)P1$(1); P0=NUM(P1$(16,2)); GOTO 6430
6427 CALL "ZZPROM","4",X3$,0,"The fiscal year is not set for this customer: "+E$(1,10)+"/"+E1$(52,4),"","",0; RESET ; GOTO 1000
6430 D0$=E1$(52,4),D0=1; IF F8$(13,1)="P" THEN D0=NUM(E1$(56,2))-P0; IF D0<0 THEN D0$=STR(NUM(D0$)-1:"0000"),D0=D0+P0; READ (Z[7],KEY="E"+E$(1,10)+D0$)P1$(1)
6450 REM "GET PERIOD ENDING DATES
6451 IF E1$(61,1)<>"C" THEN D0=D0+1
6452 IF POS(X3$(9,3)="128")=0 THEN D0=D0-1; REM "WAS IF POS(X3$(9,3)="019055")=0 THEN LET D0=D0-1
6455 T9$="",T9=3; FOR X=0 TO P0-1
6460 READ (Z[12],KEY=" "+P1$(24+D0*6+2,2))X9$
6465 X$=P1$(106+D0*3,3); IF X$<>"   " THEN T9$=T9$+X$ ELSE T9$=T9$+X9$(72,3)
6468 D0=D0+1; IF D0=P0 THEN D0$=STR(NUM(D0$)+1:"0000"); READ (Z[7],KEY="E"+E$(1,10)+D0$)P1$(1); D0=D0-P0
6470 NEXT X
6475 IF B0$(1,4)<>E$(278,4) THEN GOSUB 8100
6490 RETURN 
6500 REM "Change format code
6505 Q$="+("+U$(1,1)+"-"+U$(2,1)+")"
6510 CALL "ZZPROM","X0FM2RFA",X3$,S3,Q$,"","",0; ON S3 GOTO 6511,9900
6520 READ (Z[13],KEY="FMFORMAT"+U$(1,2),DOM=6522)S9$; GOTO 6525
6522 CALL "ZZPROM",".4",X3$,0,"The reorder notice format has not been setup: "+U$(1,2),"","",0; GOTO 9900
6525 F0=NUM(S9$(14,2)),F1=NUM(S9$(11,3))
6530 DIM F0$(F0*F1)
6540 IF LEN(S9$)<840 THEN S9$=S9$+" "; GOTO 6540
6550 REM "TEST PATTERN
6560 CALL "ZZPROM","L1ZZPRIN",X3$,S3,"","","",0; ON S3 GOTO 6595,6570,6565,9900
6565 CALL "FM2ZBA"; GOTO 6520
6570 DIM A$(80,"*"),B$(386,"*"),C$(195,"*"),L[42],E$(200,"*"),FMU$(107,"*"),O(20)
6571 A$(27,6)="I91231",A$(45,6)=A$(27,6),C0=1,C0_DIV=1
6572 FOR X=1 TO 12; L[19+X]=X,A[X]=999; NEXT X; P0=12
6573 DIM T9$(60,"*"); T9=3
6574 PMODE=0,X$=FID(W9),FORM_CODE$="RO"+DIM(8)+U$(2,1); CALL "EF2PRM",ERR=1514,X3$,X4$,X$,P6$,F6$,FORM_CODE$,FORM_PARM$; IF P6$(144,2)="JF" THEN JETFORM_MODE=1,JETFORM_DEV=W9,PMODE=JETFORM_MODE,Z1$="###,###,##0-" ELSE IF P6$(144,2)="UF" THEN UNFORM_MODE=2,UNFORM_DEV=W9,PMODE=UNFORM_MODE ! SSP#231506 SSP 241023 !SSP#246748
6575 GOSUB 4000
6578 GOSUB 6000
6579 IF FID(W9)<>FID(0) THEN PRINTER$=FID(W9); CLOSE (W9); OPEN (W9)PRINTER$
6580 GOTO 6560
6595 T9$="",H$=""; RETURN 
6900 REM "SORT
6905 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6940 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,X9
6990 RETURN 
7000 REM "Totals logic
7003 GOTO 7190
7005 IF T=T0 THEN IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7007 IF W+3>W0 THEN GOSUB 6000
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" "+Q2$+" ("+STR(T[T,0]:"0")+"):"
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):Z3$) ! SSP227003 jdf
7230 T3=INT(FPT(T2)*3600),T4=MOD(T3,60),T3=INT(T3/60)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")+":"+STR(T4:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7305 W=W+1
7310 IF V3$(2,1)="T" THEN IF W>W0 THEN CALL "ZZPROM","5",X3$,S3,"","","",0; IF S3=1 THEN GOTO 9900 ELSE W=0
7330 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1)
7485 FOR U7=1 TO LEN(V1$); IF V1$(U7,1)=" " THEN V1$(U7,1)="."; NEXT U7 ELSE NEXT U7 ! SSP#262397
7490 RETURN 
7500 REM "RETURN POSITION VALUES
7505 DIM P[3,1]
7510 Q=(N-1)*12,Q$=S9$(16+Q,12),N=N+1
7515 IF POS(" "<>Q$)=0 THEN Q=0; RETURN 
7520 Q=0; FOR X=1 TO LEN(Q$) STEP 4; IF Q$(X,4)=S$(1,4) THEN GOTO 7530
7525 P[Q,0]=NUM(Q$(X,2)),P[Q,1]=NUM(Q$(X+2,2)),Q=Q+1
7530 NEXT X
7590 RETURN 
7600 REM "SET POSITIONS WITH Q$
7605 IF JETFORM_MODE THEN GOSUB JETFORM_PRINT; GOTO 7690
7610 FOR X=1 TO Q
7615 S0=FNP(P[X-1,0],P[X-1,1]),S1=LEN(Q$)
7620 F0$(S0,S1)=Q$
7625 NEXT X
7690 RETURN 
7700 REM "Usage History Graph
7702 IF MID(E1$,24,1)<>"Y" OR MID(E1$,61,1)="O" OR MID(F8$,13,1)="O" THEN GOSUB 8200
7705 Q=0; FOR X=7 TO 12+7-1; Q=Q+ABS(A[X]); NEXT X; ON SGN(Q) GOTO 7750,7706; REM "SSP 133632
7706 IF V0$(111,1)<>"Y" THEN GOTO 7709; REM "SSP 133632
7707 FOR COUNT=7 TO 25; A[COUNT]=A[COUNT]/C0_DIV; NEXT COUNT; REM "SSP 133632 
7709 REM "If not jetform then put graph into F0$ and leave this routine, we're done
7710 IF JETFORM_MODE=0 THEN CALL "ZZGRPP",X3$,"G",A{ALL},"",P[0,1],P[0,0]+P[1,0],7,P0+7-1,0,P[1,0],P[1,1],0,0,0,F0$,F0,F1,W9,T9$,T9; GOTO 7750
7715 REM "If jetform_mode, then call ZZGRPP to load TMP$ with graph, starting at the first line, Strip trailing blanks off of graph, then print each line of the graph as a "CHART" field, be sure to catch the last line which will be shorter since the blanks were stripped off.
7720 REM "DIM TMP$(LEN(F0$)); CALL "ZZGRPP",X3$,"G",A{ALL},"",1,1+P[1,0],7,P0+7-1,0,P[1,0],P[1,1],0,0,0,TMP$,F0,F1,W9,T9$,T9
7722 REM "TMP$=STP(TMP$,1)
7730 REM "FOR TMP=1 TO INT(LEN(TMP$)/F1); PRINT (W9)"^field CHART",'LF',TMP$((TMP-1)*F1+1,F1); NEXT TMP
7747 REM "PRINT (W9)"^field CHART",'LF',TMP$((TMP-1)*F1+1)
7750 REM 
7795 RETURN 
7800 REM "Get Usage Info (FMC or FMD)
7810 DIM L[45]
7820 REM IF U9$(1,1)="R" THEN READ (Z(10),KEY=B$(1,20)+"    ",DOM=07821) IOL=00400; GOTO 07890
7830 REM "LOW STOCK NOTICE
7895 RETURN 
7900 REM "PROCESS?
7905 Q=0; IF H$>"" THEN IF H$(25,1)<>"1" THEN Q=1; GOTO 7790
7910 IF V0$(72,1)="N" AND A$(33,6)<>S$(1,6) THEN Q=1; GOTO 7790
7915 IF V0$(72,1)="R" AND A$(33,6)=S$(1,6) THEN Q=1; GOTO 7790
7920 IF V0$(72,1)<>"N" THEN IF A$(33,6)<>S$(1,6) THEN IF A$(33,6)>V0$(73,6) THEN Q=1; GOTO 7790
7995 RETURN 
8000 REM "Build desc from H8$
8010 H9$=FNS$(H8$(24,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>H8$(32,8))<>0 THEN H9$=H9$+" x "
8020 H9$=H9$+FNS$(H8$(32,8)); IF H9$<>"" THEN IF POS(" "<>H8$(32,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
8045 H8$=""
8050 RETURN 
8100 REM "GET SLSMN DATA
8110 Y$="12C ZY2...  12O AR3... "; GOSUB 9750
8120 DIM B0$(80); READ (Z[12],KEY=E$(278,4),DOM=8121)B0$
8125 DIM B2$(80); READ (Z[12],KEY=E$(282,4),DOM=8126)B2$
8130 Y$="12C AR3...  12O ZY2    "; GOSUB 9750
8140 RETURN 
8150 REM "PRIOR ORDERS
8152 H1$="B"; IF B$(105,1)="Y" THEN H1$="F"
8153 CALL "FM2UZD",X3$,X4$,B$(1,20),O$,O{ALL},O1$,O2$,O0,O1,O2,H0$,H1$,""; GOTO 8185
8155 DIM O[20],O$(0); Q$=A$(3,10)+H1$+A$(17,10),O=0; READ (Z[10],KEY=Q$,DOM=8156)
8160 DIM J[5]; READ (Z[10],END=8190)IOL=0400; IF J$(1,21)<>Q$ THEN GOTO 8190
8162 IF J$(34,6)="      " THEN GOTO 8160
8163 P=POS(J$(22,8)=O$,20); IF P>0 THEN P=INT(P/20); O[P]=O[P]+J[0]; GOTO 8160
8164 IF V0$(79,1)="Y" THEN IF J[5]>J[0] THEN J[0]=J[5],J$(34,6)=J$(48,6)
8165 IF POS("L"=V0$(59,12))>0 THEN IF J$(34,6)>O$(9,6) THEN O$="",O=0
8166 O$=O$+J$(22,8)+J$(34,6)+J$(48,6),O[O]=J[0],O=O+1
8170 IF O<20 THEN GOTO 8160
8185 O=LEN(O$)/23
8186 IF POS("O"=V0$(59,12))>0 THEN O$=""
8190 IF O$="" THEN DIM O$(23); O$(1)=B$(132,8)+B$(126,6),O=1,O[0]=B[16]
8195 RETURN 
8200 REM "GET ORDER HISTORY FOR GRAPH
8220 CALL "FM2UZD",X3$,X4$,B$(1,20),O$,O{ALL},O1$,O2$,O0,O1,O2,H0$,H1$,""; IF O$="" THEN GOTO 8290
8230 REM "SET USAGE FIELDS / T9$
8232 T9$="",T9=5,P0=0
8235 X0=0,X1=LEN(O$)/23; IF X1>10 THEN X0=X1-10
8240 FOR X=X0 TO X1-1
8245 Q$=FND$(O$(X*23+9,6)); T9$=T9$+Q$(1,2)+"/"+Q$(9,2)
8249 Q=NUM(O$(X*23+22,2))
8250 A[7+X-X0]=O[Q]
8255 P0=P0+1
8260 NEXT X
8270 A$(51,6)=O2$,A[1]=O0
8274 REM "********
8275 A[1]=O1
8295 RETURN 
8360 REM "Setup Fax String, for Vendor output put in CUSTOMER number ONLY
8365 DIM X$(51*2),EM$(6*21); REM " SSP# 163056                                  
8366 CALL "ZZDISP","AX",A$(3,10),"A/R",X3$,J4$,"",0,0,X4$
8367 J1$=A$(3,10),J2$=B1$(132,12),J3$="A/R",J5$="C",EM$(1,21)="C"+A$(3,10)
8370 X$(1)=J5$+J2$,X$(27)=J5$+":"+J4$; REM "Vendor or Customer (f9$="Y")        
8375 REM "IF B$(209,3)=DIM(3) THEN GOTO 8381
8380 X$(52)="R"+B1$(132,12); X$(78)="R:"+J4$+"-"+B1$(12,3),EM$(22,21)="R"+A$(3,10)+B1$(12,3)
8390 IF F8$="Y" THEN FOR J7=0 TO 1; X$(2+J7*51,25)=P6$(150,12),EM$(2+J7*21,20)="ALIGNMENTPATTERN"; NEXT J7; REM "If alignment pattern, replace with align  pat from parameters                                                       
8392 J6$=STP("RON:"+J4$+"-"+MID(A$,17,10),3) ! 288658
8393 REM "CALL "ZZDISP","AX",A$(3,10),"O/P",X3$,J6$,"",0,0,X4$; J6$="840:"+J6$; REM "Tag 1 for fax                                                            
8395 RETURN 
8400 REM "GET PKG
8404 UM$="CASE"; IF B[0]=B[4] THEN UM$=STP(B$(100,4),3," ")
8405 Q$=""; IF B[0]<>0 THEN Q$=Q$+", "+UM$+"/"+STR(B[0]:Z6$) ! SSP227003 jdf
8410 FOR X=0 TO 3
8415 IF B$(238+X*4,4)<>"    " THEN Q$=Q$+"  "+FNS$(B$(238+X*4,4))+"/"+FNT$(STR(B[25+X]:Z6$)) ! SSP227003 jdf
8420 NEXT X
8440 IF Q$>"" THEN Q$=Q$(3)
8445 RETURN 
8500 JETFORM_PRINT:REM "For jetform mode add field nominated value with N as the field name and Q$ as the value
8505 REM "IF JETFORM_MODE THEN CALL "EF2JET",X3$,X4$,P6$,"N",JETFORM_DEV,FORM_PARM$,JOB_MODIFIER$,RET$; REM "If in jetform mode, start a new file
8506 REM "IF P6$="" THEN GOTO 8508 ELSE GOSUB 8360; CALL "EF2FAX",X3$,X4$,P6$,F6$,W9,X$,J6$,JETFORM_MODE,EM$
8510 FIELD=N-1; REM "N has already been incremented to the next field
8515 PRINT (W9)"^field "+STP(P6$(146,3),1)+STR(FIELD:"000"),'LF',Q$
8545 JETFORM_PRINT_END:RETURN 
8560 REM "prior order po, cust po, price, unit
8561 REM "K loop for above items
8562 IF O=0 THEN GOTO 8595
8563 N=38
8565 FOR K=0 TO 3
8568 GOSUB 7500; IF Q=0 THEN GOTO 8590
8572 Q0=O,Q1=Q0-Q+1; IF Q1<1 THEN Q1=1
8573 FOR Z=Q0 TO Q1 STEP -1
8574 S0=FNP(P[O-Z,0],P[O-Z,1]),S1=LEN(Q$); IF LEN(O$)=0 THEN GOTO 8585
8575 Q$=O$((Z-1)*23+1,8); GOSUB 8800; IF J$="" THEN GOTO 8585
8577 IF K=0 THEN Q$=J$(22,8) ELSE IF K=1 THEN Q$=J$(54,15) ELSE IF K=2 THEN Q$=STR(J[1]:Z7$) ELSE IF K=3 THEN Q$=FNS$(J$(101,4))+"/"+STR(J[2]) ! SSP227003 jdf
8578 IF JETFORM_MODE THEN GOSUB JETFORM_PRINT; GOTO 8585
8580 F0$(S0,S1)=Q$
8585 NEXT Z
8590 NEXT K
8595 RETURN 
8800 REM "GET FS6 RECORD
8805 J$="",Q$=A$(3,10)+H1$+A$(17,10)+Q$(1,8)
8810 READ (Z[10],KEY=Q$,DOM=8811)
8815 DIM J$(113),J[5]; READ (Z[10],END=8840)IOL=0400; IF J$(22,8)<>Q$(22,8) THEN J$=""
8840 Q$=""
8845 RETURN 
8910 DEF FNT$(Z9$)=Z9$(POS(" "<>Z9$))
8950 DEF FNQ$(Z9,Z9$)=STR(Z9:Z9$)
8955 DEF FNP(Z8,Z9)=(Z8-1)*F1+Z9
8960 DEF FNU$(Z9)=STR(Z9/C0:Z3$)+C0$
8965 DEF FNV$(Z9)=STR(Z9/C0:Z3$)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
9790 RETURN 
9900 REM "End program
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 IF %GUI THEN CALL Y4$,ERR=*NEXT
9927 IF %GUI THEN EXIT 
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 RUN "ZMENU"
9999 END 
11000 IMAGE_LIBRARY_SETUP:! If needed, setup parameters to add image library i nfo to Jetform call SSP 190630
11005 IF %HAS_IMAGE_LIB THEN {
11020 %IL_ADD_DOC_TYPE$="A3"
11025 REM %IL_ADD_TF1$=A$(3,10) ! Customer Number SSP 190630
11029 %IL_ADD_TF1$=""; REM SSP 190630
11030 %IL_ADD_TF2$=PAD(A$(3,10),10)+PAD(A$(17,10),10) ! Customer + Item;REM SSOP 190630
11040 %IL_ADD_IMAGE_PATH$=STP(FN%ZZDISP$(MID(A$,3,10),"A/R"))+SUB(SUB(SUB(SUB(A$(17,10),QUO,""),"'",""),"&",""),"/","")+DTE(0:"_%Y%M%D_%H%M%S") ! SSP# 185008!SSP#242353 SSP 307379
11045 %IL_FORMAT$=U$(2,1) ! 186095
11075  } ELSE {
11080 %IL_ADD_DOC_TYPE$="",%IL_ADD_TF1$="",%IL_ADD_TF2$="",%IL_ADD_IMAGE_PATH$="",%IL_FORMAT$="" ! 186095
11085  }
11095 RETURN 
11099 ! ******************************************************************
56000 REM "190630-Need to add pdf field to Reorder Notices and add to Image Library
56001 REM "195257-Import existing Invoice PDFs into Image Library 
56002 REM "227003-For some of our items, the value of fields on the reorder   
56004 REM "241023-Modify printing programs to use Unform                      
56006 REM "244985-Reorder notices will not print a test pattern using Unform
56007 REM "246748-Reorder Notices no longer emailing                          
56008 REM "262397-Proactive change, validate all form printing programs for   
56009 REM "288658-Can we add the item code to the subject line for the Reorder
56010 REM "307379-Error in Reorder print if special character in Item Number. 
