0010 REM "Build Stats from Order History <FM2UTK>
0020 SETESC 9300; SETERR 9000
0035 REM "3.5 - 12/09/92 - 22.42
0040 REM "Copyright 1992 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="FM2UTK",X1$="Build Stats from Order History"
0120 DIM Z0$(80,"-"),A[7],S$(50)
0130 K0=20,K1=1,C1=100
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7]
0320 IOLIST B$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O FS6...  02O AR9...  03O ARA...  04O ARX...  05O AP9...  06O APS...  07O AR1...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0540 READ (Z[13],KEY=X3$(9,3)+"A/P")P0$
0550 READ (Z[13],KEY="STATA/R")F0$
0560 READ (Z[13],KEY="STATAR3")F1$
0570 READ (Z[13],KEY="STATARX")F2$
0580 READ (Z[13],KEY="STATA/P")F3$
0600 REM "
0610 GOSUB 6000
0625 DIM D$(6); CALL "ZZENTR","D",A{ALL},D$,X4$,X3$,50,12,1,6,C0,"",X$,"","FM2UTK00","","",""
0640 IF Q1$<>"*" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0700 REM "G3$=date fye prior to the first on-file G/L fiscal year record ended.  G1$=string of period ending date, G2$=string of fiscal years associated with G1$
0710 K$=X3$(9,3)+"G/LYE"; READ (Z[13],KEY=K$,DOM=0720)
0720 READ (Z[13],END=0800)G$
0730 IF G$(1,LEN(K$))<>K$ THEN GOTO 0800
0750 IF G1=0 THEN G1=NUM(G$(13,2)),G3$=G$(15,6)
0760 G1$=G1$+G$(21,6*G1),G2$=G2$+G$(9,4)
0790 GOTO 0720
0800 REM 
0810 G2=LEN(G2$)/4
0910 READ (Z[1],KEY="",DOM=0911)
0995 PRINT @(20,14),'CE',"Processing order no.- ",
1000 REM 
1010 K$=KEY(Z[1],END=5000); READ (Z[1],KEY=K$)IOL=0310
1020 C=C+1; IF MOD(C,C1)=0 OR C=1 THEN PRINT @(42,14),A$(1,10),"  ",A$(22,8),
1040 IF A$(48,6)<=G3$ THEN GOTO 1000
1060 IF A[6]=0 AND A[7]=0 THEN GOTO 1000
1070 IF POS(" "<>D$)>0 AND A$(48,6)>D$ THEN GOTO 1000
1100 REM 
1120 P0=POS(A$(48,6)<=G1$); IF P0=0 THEN GOTO 1000
1140 P1=INT((P0-1)/(G1*6))
1160 P$=G2$(P1*4+1,4),P=MOD(P0-1,72)/6+1
1170 READ (Z[7],KEY=A$(1,10),DOM=1500)IOL=0320
1200 REM "Customer Stats
1240 S9$=F0$,Q$=A$(1,10),F=Z[2]
1260 GOSUB 2000
1300 REM "Salesperson Stats
1320 IF POS(" "<>B$(278,4))=0 THEN GOTO 1500
1340 S9$=F1$,Q$=B$(278,4),F=Z[3]
1360 GOSUB 2000
1400 REM "Salesperson-Product Code
1420 S9$=F2$,Q$=A$(1,2)+B$(278,4)+S$(1,9)+A$(31,3)+" "+" ",F=Z[4]
1430 GOSUB 2000
1500 REM "Vendor Purchases
1510 IF POS(" "<>A$(87,10))=0 THEN GOTO 1900
1520 S9$=F3$,Q$=A$(87,10),F=Z[5]
1530 GOSUB 2100
1540 IF P0$(70,1)<>"Y" OR POS(" "<>B$(278,4))=0 THEN GOTO 1900
1550 Q$=A$(87,10)+B$(278,4),F=Z[6]
1560 GOSUB 2100
1900 REM 
1990 GOTO 1000
2000 REM "Sales & Profit Types
2010 Q$=Q$+P$+"S",Q=A[6]
2020 GOSUB 2200
2030 Q$(LEN(Q$),1)="P",Q=A[6]-A[7]
2040 GOSUB 2200
2090 RETURN 
2100 REM "Purchases types
2110 Q$=Q$+P$+"P",Q=A[7]
2130 GOSUB 2200
2140 RETURN 
2200 REM "Update bucket
2220 DIM K[14]; GOSUB 7200
2240 K[P]=K[P]+Q
2260 GOSUB 7250
2290 RETURN 
5000 REM "EOJ
5020 IF Q1$<>"*" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This CSI utility program processes the Order History Sort files FS6 and for each record with an invoice date creates or accumlates to existing stat records for Customer Sales and Profit, Salesperson Sales and Profit, Sales by Product Code Sales and Profit, and Vendor Purchases.  Order history prior to the oldest G/L fiscal year record will be ignored",@(0,10),"Progress will be reported every ",C1," entries.",
6050 PRINT @(0,12),"Only process invoiced order history on or before:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7200 REM "READ STATISTICAL DATA KEY=Q$
7205 S1$=Q$(1,LEN(Q$)-1),S0$=Q$(LEN(Q$))
7215 X9=POS(Q$(LEN(Q$),1)=S9$(49),17); IF X9<>0 THEN M0$=S9$(X9+49+1,3)
7230 CALL "ZZPACK",X3$,"E","","",0,0,K{ALL},F,S0$,S1$,S9$
7245 RETURN 
7250 REM "WRITE STATS, KEY=Q$
7255 S1$=Q$(1,LEN(Q$)-1),S0$=Q$(LEN(Q$))
7260 CALL "ZZPACK",X3$,"W","","",0,0,K{ALL},F,S0$,S1$,S9$
7290 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
