0010 REM "F/M Stats Customer/Location Update <FM2UFT>
0020 SETESC 9300; SETERR 9000
0035 REM "5.6 - 11/13/08 - 22.446666 - crg - SSP# 215368
0037 REM "215368-Create Monthly Budget feature by customer location. In WebEC
0040 REM "Copyright 2008 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0260 DEF FND$(Z9$)=STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0400 REM "SET VALUES FOR TESTING
0500 REM + Begin Opening Files
0501 ! [201811]-Added lines 500-799 to open XPD and XPE
0510 XPD_LFN=HFN; XPD_FILE$="XPD"+%C$
0520 OPEN (XPD_LFN)XPD_FILE$
0550 XPE_LFN=HFN; XPE_FILE$="XPE"+%C$
0560 OPEN (XPE_LFN)XPE_FILE$
0799 REM + End Opening Files
1010 X0$="FM2UFT"
1020 ENTER X3$,F9$,F0,F0$,F1,F1$,D0$,D1$,U0$,U9,U1,U2,U3
1025 ! IF WHO="crg" THEN ESCAPE 
1030 F3$=MID(F1$,1,14)
1040 IF LEN(U0$)>1 THEN U1$=U0$(2),U0$=U0$(2,1)
1050 IF POS("E"=U1$)>0 THEN D0$=D1$
1200 REM "FIND STARTING DATE RECORD
1210 Q0=LEN(F0$); READ (F0,KEY=F0$,DOM=1211)
1215 READ (F0,END=1300)Q$
1220 IF Q$(1,LEN(F0$))<>F0$ THEN GOTO 1300 ELSE P1$=Q$
1224 REM "Q$ IS LAST DATE
1225 P0=NUM(P1$(Q0+5,2)),Q$=P1$(Q0+7+P0*6,6)
1250 IF Q$<D0$ THEN GOTO 1215
1280 GOTO 1400
1300 REM "DATE RECORD NOT THERE
1310 IF P1$>"" THEN GOSUB 7000; GOTO 1225
1350 REM "THERE ARE NO DATE RECORDS FOR THIS CUST
1360 CALL "ZZPROM","4",X3$,0,"There are no FMS date records for customer "+F1$(1,10),"","",0
1365 GOTO 1200
1380 EXIT 11
1400 REM 
1420 D$=D0$
1450 REM "CALCULATE TOTAL NUMBER OF DAYS
1460 CALL "YDYSBT",X3$,D0$,D1$,D9
1465 IF D9<=0 THEN D9=1
1470 U0=U9
2000 REM "GET CORRECT STAT RECORD FOR BASE DATE D$
2020 IF P1$(Q0+7+P0*6,6)<D$ THEN GOSUB 4000
2030 IF F2$="" THEN GOSUB 4100
2100 REM "DETERMINE PERIOD P1 FROM DATE D$
2120 P=POS(D$<=P1$(24),6),P2$=P1$(23+P,6)
2125 P2=INT(P/6)+1
2150 REM "NUMBER OF DAYS LEFT IN PERIOD
2160 CALL "YDYSBT",X3$,D$,P2$,P3
2165 IF P3=0 THEN P3=1
2200 REM "For part of period, pro-rate
2205 IF D9=0 THEN GOTO 2400 ELSE IF D9<P3 THEN P3=D9
2210 U=INT(P3*U0/D9)
2215 K[P2]=K[P2]+U9 ! L[P2]=L[P2]+U-U1
2216 REM PRINT @(26,14),"Usage for period: ",P2,
2220 D9=D9-P3,U0=U0-U
2230 IF U0=0 THEN GOTO 2400
2250 REM "Get next reference date D$
2260 D$=P2$,P2=P2+1
2270 IF P2<=P0 THEN GOTO 2300
2280 GOSUB 4000; P$=STR(NUM(P$)+1:"0000")
2285 READ (F0,KEY=F0$+P$,DOM=2286)P1$; GOTO 2290
2286 GOSUB 7000
2290 GOSUB 4100
2295 P2=1
2300 REM "
2310 P2$=P1$(24+(P2-1)*6,6)
2320 CALL "YDYSBT",X3$,D$,P2$,P3
2325 IF D9<P3 THEN P3=D9
2360 GOTO 2200
2400 REM "DONE WITH USAGE
2410 GOSUB 4000
2440 GOTO 9900
4000 REM "FLUSH CURRECT STAT RECORD
4005 Z9$=FN%FID$(F1),Z9$=Z9$(4,6) ! [205703]-changed FID() to FN%FID$()
4020 CALL "ZZPACK",X3$,"W",F8$,"",0,0,K{ALL},F1,U0$,F2$,F9$
4030 ! CALL "ZZPACK",X3$,"W",F8$,"",0,0,L{ALL},F2,U0$,F3$+P$,F9$
4090 F2$=""
4095 RETURN 
4100 REM "GET STAT RECORD  FOR PERIOD P$
4105 P=POS(U0$=F9$(49),17),F8$=F9$(50+P,3),P$=P1$(12,4),F2$=F1$+P$
4130 DIM K[24]
4140 CALL "ZZPACK",X3$,"R",F8$+"","",0,0,K{ALL},F1,U0$,F2$,F9$
4150 ! DIM L[24]; CALL "ZZPACK",X3$,"R",F8$+"","",0,0,L{ALL},F2,U0$,F3$+P$,F9$
4195 RETURN 
7000 REM "GET NEXT YEAR PERIOD END RECORD
7010 Q$=P1$(NUM(P1$(Q0+5,2))*6+18,6),Y=NUM(P1$(Q0+1,4))+1
7015 P1$(Q0+7,LEN(Q$))=Q$,P1$(Q0+1,4)=STR(Y:"0000")
7020 CALL "FM2UBZ",X3$,"",P1$,Q0
7050 P1_KEY$=P1$(1,Q0+4) ! [201811]-build the key
7060 WRITE (F0,KEY=P1_KEY$)P1$ ! [201881]-write to FMP
7070 IF P1$(1,1)="D" THEN WRITE (XPD_LFN,KEY=P1_KEY$)P1$ ! [201811]-write XPD
7080 IF P1$(1,1)="E" THEN WRITE (XPE_LFN,KEY=P1_KEY$)P1$ ! [201811]-write XPE
7090 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,1
9045 REM 
9050 ON Y7 GOTO 9060,9100,9190,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9190 GOSUB 6600; GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9700 REM "FILES
9740 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 0521,9900
9790 RETURN 
9900 REM "END PROGRAM
9920 CLOSE (XPD_LFN) ! [201811]- close XPD
9925 CLOSE (XPE_LFN) ! [201811]- close XPE
9940 EXIT 
9999 END 
56000 REM + Modification History
56001 REM "215368-Create Monthly Budget feature by customer location. In WebEC
