0010 REM "E-Mail order status <FM2EM0>
0020 SETESC 9300; SETERR 9000
0035 REM "5.0 - 02/25/02 - 15.905555 - dmm - SSP# 146432
0040 REM "Copyright 2002 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="FM2EM0",X1$="E-Mail Order Status"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$
0320 IOLIST B$
0350 IOLIST FS1$,FS1[0],FS1[1],FS1[2],FS1[3],FS1[4],FS1[5],FS1[6],FS1[7],FS1[8],FS1[9],FS1[10],FS1[11],FS1[12],FS1[13]
0360 IOLIST FS2$,FS2[0],FS2[1],FS2[2],FS2[3],FS2[4],FS2[5],FS2[6],FS2[7],FS2[8],FS2[9],FS2[10],FS2[11],FS2[12],FS2[13],FS2[14],FS2[15],FS2[16],FS2[17],FS2[18],FS2[19],FS2[20],FS2[21],FS2[22],FS2[23],FS2[24],FS2[25],FS2[26],FS2[27],FS2[28],FS2[29]
0370 IOLIST SM4$
0390 IOLIST M$,M[0],M[1]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O ZP4...  02O ZP0...  03O SM6... 04O FS4... 05O FS1... 06O FS2... 07O SM4... 08O FT3... 09O FT4... 13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 GB$=""; FIND (Z[13],KEY=X3$(9,3)+"G/B",DOM=0531)GB$
0535 DIM CMP$(196); FIND (Z[13],KEY="CMP"+X3$(9,3),DOM=0536)CMP$
0550 REM "Get date from system date, because if run in background, we won't have the correct date
0555 DTMP$=FN%NTD$(JUL(0,0,0),"YYYYMMDD"),TODAY$=CHR(INT((NUM(DTMP$(1,4))-1900)/10)+65)+DTMP$(4,5),DTMP$=FN%NTD$(JUL(0,0,0)-1,"YYYYMMDD"),YESTERDAY$=CHR(INT((NUM(DTMP$(1,4))-1900)/10)+65)+DTMP$(4,5)
0600 REM "
0605 GOSUB 6000
0607 DIM A$(4); PRINT @(21,8),'CL',
0610 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,21,8,1,4,C0,"",X$,"","FM2EM002","ZP0","",""; IF ABS(C0)=4 THEN GOTO 9900 ELSE IF ABS(C0)>4 THEN PRINT @(0,3),'CE',; GOSUB 6000; IF ABS(C0)>4 THEN C0=ABS(C0)-5 END_IF ; ON C0 GOTO 0610,0611
0615 FIND (Z[2],KEY=A$(1,4),DOM=0610)ZP0$
0620 PRINT @(27,8),ZP0$(5,40),
0640 IF Q1$="" THEN CALL "ZZPROM","0",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900,0607
0925 D0$=%DATAPATH$
0950 READ (Z[1],KEY=A$(1,4),DOM=0951)
0955 PRINT @(9,10),"Processing:",
1000 REM "BEGIN MAIN PROCESS
1005 KEY_1$=KEY(Z[1],END=5000); IF KEY_1$(1,4)<>A$(1,4) THEN GOTO 5000
1010 EXTRACT RECORD (Z[1],KEY=KEY_1$,DOM=1095)ZP4$
1020 PRINT @(21,10),ZP4$(5,6),
1030 IF STP(ZP4$(464,30),3," ")="" THEN GOTO 1095
1050 GOSUB 1100; REM "Build message file
1060 IF FILENAME$="" THEN GOTO 1095 ELSE COM$="mail -s "+QUO+STP(CMP$(7,40),1)+" Order status"+QUO+" "+STP(ZP4$(464,30),1)+" < "+D0$+"/"+FILENAME$+" ; rm -r "+D0$+"/"+FILENAME$+" > /dev/null"
1070 REM " PRINT @(0,15),'CE',COM$,; INPUT *
1080 INVOKE COM$
1085 ZP4$(264,6)=TODAY$,ZP4$(270,1)="E",ZP4$(271,40)="Sent Order Status via email"
1090 WRITE RECORD (Z[1],KEY=KEY_1$)ZP4$
1095 READ (Z[1],KEY=KEY_1$,DOM=1000); GOTO 1000; REM "Reposition file in all cases before getting next
1100 REM "Create output file
1105 FILENAME$=STP("EM"+ZP4$(1,10),3)
1106 ERASE FILENAME$,ERR=1107
1107 SERIAL FILENAME$,0,0
1110 FSLOT=UNT; OPEN LOCK (FSLOT,OPT="TEXT")FILENAME$
1115 DO_MAIN_HEADER=1,DO_ORDER_HEADER=1,DO_SHIP_HEADER=1,DO_OPEN_HEADER=0,ORDERS_DONE$=""
1120 REM "Process 'shipped' orders using information from the shipping system in SM6
1121 READ (Z[3],KEY=ZP4$(363,10)+YESTERDAY$,DOM=1122)
1125 KEY_3$=KEY(Z[3],END=1145); READ (Z[3]); IF KEY_3$(1,10)<>ZP4$(363,10) OR (KEY_3$(11,6)<>TODAY$ AND KEY_3$(11,6)<>YESTERDAY$) THEN GOTO 1145
1127 DIM FS1$(350),FS1[13]; FIND (Z[5],KEY=KEY_3$(17,8),DOM=1128)IOL=0350; LINE_FILE=Z[6]; GOTO 1129
1128 DIM FS1$(350),FS1[13]; FIND (Z[8],KEY=KEY_3$(17,8),DOM=1140)IOL=0350; LINE_FILE=Z[9]
1130 GOSUB 1300
1135 GOSUB 1400
1140 GOTO 1125
1145 IF DO_SHIP_HEADER=0 THEN FOR I=1 TO 4; GOSUB 7300; GOSUB 7300; NEXT I; REM "If we printed any lines, the space some before starting the on order
1150 REM "Now process unshipped open orders
1154 LINE_FILE=Z[6],DO_SHIP_HEAER=0,DO_ORDER_HEADER=1
1155 READ (Z[4],KEY=ZP4$(363,10),DOM=1156)
1160 K4$=KEY(Z[4],END=1180); IF K4$(1,10)<>ZP4$(363,10) THEN GOTO 1180 ELSE READ (Z[4])
1162 DIM FS1$(350),FS1[13]; FIND (Z[5],KEY=K4$(11,8),DOM=1160)IOL=0350
1165 GOSUB 1300
1175 GOTO 1160
1190 CLOSE (FSLOT)
1195 RETURN 
1300 REM "PRINT INFO FOR AN ORDER IN FS1$
1304 IF POS(FS1$(118,8)=ORDERS_DONE$,8)<>0 THEN GOTO 1395 ELSE ORDERS_DONE$=ORDERS_DONE$+FS1$(118,8); REM "Only show an order once in the email
1305 IF DO_MAIN_HEADER THEN GOSUB 7500
1309 IF DO_SHIP_HEADER THEN GOSUB 7700 ELSE IF DO_OPEN_HEADER THEN GOSUB 7750
1310 IF DO_ORDER_HEADER THEN GOSUB 7550
1312 DO_LINE_HEADER=1
1315 DIM Y5$(70); Y5$(1)=FS1$(28,15)
1320 CALL "ZZDISP","AXS",FS1$(118,8),"O/P",X3$,Y5$,"",17,0,X4$
1322 CALL "ZZDISP","DXS",FS1$(16,6),"",X3$,Y5$,"",40,0,X4$
1323 IF STP(FS1$(55,6),3," ")="" THEN GOTO 1324 ELSE CALL "ZZDISP","DXS",FS1$(55,6),"",X3$,Y5$,"",52,0,X4$
1324 GOSUB 7300
1325 IF GB$<>"" THEN GOSUB 7600
1350 REM "Do order lines
1355 READ (LINE_FILE,KEY=FS1$(118,8),DOM=1356)
1360 DIM FS2$(229),FS2[29]; READ (LINE_FILE,END=1380)IOL=0360; IF FS2$(147,8)<>FS1$(118,8) THEN GOTO 1380
1362 IF DO_LINE_HEADER THEN GOSUB 7650
1363 GOSUB 3200
1365 DIM Y5$(100); Y5$(10)=STR(NUM(FS2$(6,3)):"  0"),Y5$(15)=FS2$(19,10)
1367 QTY=FS2[0]/J; IF FPT(QTY)=0 THEN Y5$(40)=STR(QTY:"###,###") ELSE Y5$(40)=STR(QTY:"###,###.00")
1368 Y5$(51)=E3$
1369 GOSUB 7300
1370 DIM Y5$(80); Y5$(15)=FS2$(50,40); GOSUB 7300
1378 GOTO 1360
1390 GOSUB 7300; GOSUB 7300
1395 RETURN 
1400 REM "Given KEY_3$, read first SM4 record & print tracking info, then read thru SM6 records (z[3]) for this order to print them & use them up
1402 DO_SHIPPING_HEADER=1
1405 DIM SM4$(150); READ (Z[7],KEY=KEY_3$(17),DOM=1440)IOL=0370
1410 IF DO_SHIPPING_HEADER THEN GOSUB 7800
1415 DIM Y5$(80)
1417 CALL "ZZDISP","DXS",SM4$(15,6),"",X3$,Y5$,"",11,0,X4$
1419 IF STP(SM4$(21,4),3," ")="" THEN GOTO 1420 ELSE CALL "ZZDISP","TXS",SM4$(21,4),"",X3$,Y5$,"",25,0,X4$
1421 Y5$(36)=SM4$(37,12),Y5$(57)=SM4$(49,20)
1435 GOSUB 7300
1440 KEY_3$=KEY(Z[3],END=1441); IF KEY_3$(1,10)<>ZP4$(363,10) OR KEY_3$(17,8)<>FS1$(118,8) OR (KEY_3$(11,6)<>TODAY$ AND KEY_3$(11,6)<>YESTERDAY$) THEN GOTO 1441 ELSE READ (Z[3]); GOTO 1405
1443 IF DO_SHIPPING_HEADER=0 THEN GOSUB 7300; GOSUB 7300; REM "If we printed any, add a line to space before the next order
1445 RETURN 
3200 REM "Set J based on u/m
3210 DIM M$(20),M[1]
3215 READ (Z[13],KEY="U/M"+FS2$(124,4),DOM=3216)IOL=0390
3220 IF M$(20,1)<>"Y" THEN J=FS2[5],J0=FS2[5] ELSE J=M[1]; IF M[0]<>0 THEN J0=M[0] ELSE J0=M[1]
3225 IF J=0 THEN J=1
3230 E3$=STP(FS2$(124,4),1)+"/"+STP(STR(J0:"##,##0"),2)
3240 RETURN 
5000 REM "EOJ
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(10,4),"This program will email an order status report to ",@(10,5),"each entry on the specified Telemarketing List."
6025 PRINT @(10,8),"List code: ",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7300 REM "Output line Y5$ to fslot
7305 PRINT (FSLOT)STP(Y5$,1)
7310 Y5$=""
7345 RETURN 
7500 REM "Do Main Header
7510 Y5$="Order Tracking Information from "+STP(CMP$(7,40),1); GOSUB 7300
7515 Y5$="For "+FN%NTD$(JUL(0,0,0),"Day, Month DD,YYYY at HH:MI"); GOSUB 7300
7520 GOSUB 7300
7540 DO_MAIN_HEADER=0
7545 RETURN 
7550 REM "Do Open Status Header
7555 DIM Y5$(75); Y5$(1)="Your Number",Y5$(17)="Our Order Number",Y5$(40)="Order date",Y5$(52)="Est Ship Date"; GOSUB 7300
7560 GOSUB 7300
7590 DO_ORDER_HEADER=0
7595 RETURN 
7600 REM "If GB then add GB infor
7610 GB_STR$=""
7615 IF FS1$(195,1)="Y" THEN GB_STR$=GB_STR$+" Gold Rush! "
7620 IF FS1$(198,1)="Y" THEN GB_STR$=GB_STR$+" No Overruns! "
7625 IF FS1$(199,2)<>"  " THEN GB_STR$=GB_STR$+" In Dept: "+FS1$(199,2)+" "
7640 IF STP(GB_STR$,1)>"" THEN Y5$=DIM(15)+GB_STR$; GOSUB 7300
7645 RETURN 
7650 REM "Do Line section Header
7655 GOSUB 7300
7660 DIM Y5$(75); Y5$(10)="Line",Y5$(15)="Item / Description",Y5$(40)="Quantity",Y5$(51)="Unit/Measure"
7670 GOSUB 7300
7690 DO_LINE_HEADER=0
7695 RETURN 
7700 REM "Print shipping header
7709 GOSUB 7300
7710 DIM Y5$(75); Y5$(1)="These are the orders that shipped yesterday or today:"; GOSUB 7300; GOSUB 7300
7740 DO_SHIP_HEADER=0
7745 RETURN 
7750 REM "Print open orders header
7760 GOSUB 7300
7765 DIM Y5$(75); Y5$(1)="These are your remaining unshipped orders:"; GOSUB 7300; GOSUB 7300
7790 DO_OPEN_HEADER=0
7795 RETURN 
7800 REM "Print shipping section header
7810 DIM Y5$(80); Y5$(11)="Date Shipped",Y5$(25)="Time",Y5$(36)="Shipped via",Y5$(57)="Tracking Number"
7830 GOSUB 7300
7840 DO_SHIPPING_HEADER=0
7845 RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000
9511 IF ABS(C0)>4 THEN C0=ABS(C0)-5
9512 PRINT @(0,3),'CE',; GOSUB 6000
9520 ON C0 GOTO 0610,0611
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
