0010 REM "Move from RFQ spec files to Forms Spec files <FM2OBB>
0035 REM "5.2 - 10/14/03 - 20.496111 - kmc - SSP# 133604
0040 REM "Copyright 2003 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0070 REM "X$=RFQ, RFQ line # to load into FM files
0090 SETERR 9000
0095 ENTER X3$,X$,C9$
0110 X0$="FM2OBB"
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O QS1...  02O QS2...  03O QSS...  04O QSU...  05O QSV...  06O FM1...  07O FM2...  08O FMS...  09O FMU...  10O FMV...  11O QSF...  12O FSF...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0585 RT_PARM$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,RT_PARM$
1000 REM "Read QS1 record, then check to see if item exists in FM1
1010 READ RECORD (Z[1],KEY=X$,DOM=9900)A$
1014 REM "strip off first string.
1015 A$=A$(12),A$(1,10)=C9$
1020 READ RECORD (Z[6],KEY=A$(1,20),DOM=1200)A0$
1030 REM "FM1 record found, so remove FM2 key, update fields from A$, write out FM1, and FM2
1035 IF %GUI THEN MSGBOX MSG("FM2OBB_1"),MSG("MB_ATTN"),"?,YESNO",_YESNO$; IF _YESNO$="YES" THEN GOTO 1036 ELSE GOTO 9900 END_IF ELSE CALL "ZZPROM",".Y",X3$,Z,"Update existing item master with RFQ spec?","","",0; ON Z GOTO 1036,9900
1040 REMOVE (Z[7],KEY=A0$(21,3)+A0$(24,16)+A0$(40,2)+A0$(11,10)+A0$(1,10),DOM=1041)
1045 A0$(21,61)=A$(21,61)
1070 WRITE RECORD (Z[6],KEY=A0$(1,20))A0$; IF RT_PARM$<>"" THEN CUST$=A0$(1,10); CALL "RT2WOC",ERR=1071,X3$,X4$,CUST$,"FM1...","U",A0$(1,20)
1075 WRITE (Z[7],KEY=A0$(21,3)+A0$(24,16)+A0$(40,2)+A0$(11,10)+A0$(1,10))
1100 REM "Remove all record from FMU & FMV
1110 FOR I=9 TO 10
1120 READ (Z[I],KEY=A0$(1,20),DOM=1121)
1125 K$=KEY(Z[I],END=1150)
1130 IF MID(K$,1,20)<>MID(A0$,1,20) THEN GOTO 1150
1135 REMOVE (Z[I],KEY=K$,DOM=1136)
1145 GOTO 1125
1150 NEXT I
1190 GOTO 1300
1200 REM "FM1 not found, so write out QS1 record, and create & write out FM2 record
1205 A0$=A$
1210 WRITE RECORD (Z[6],KEY=A$(1,20))A0$; IF RT_PARM$<>"" THEN CUST$=A$(1,10); CALL "RT2WOC",ERR=1211,X3$,X4$,CUST$,"FM1...","U",A$(1,20)
1220 WRITE (Z[7],KEY=A0$(21,3)+A0$(24,16)+A0$(40,2)+A0$(11,10)+A0$(1,10))
1300 REM 'NOW DO FMS FILE & FSF
1310 READ (Z[3],KEY=X$,DOM=1330)B$; B$=B$(12),B$(1,10)=C9$
1320 WRITE (Z[8],KEY=B$(1,20))B$
1330 READ (Z[11],KEY=X$,DOM=1400)B$; B$=B$(12),B$(1,10)=C9$
1340 WRITE (Z[12],KEY=B$(1,20))B$
1400 REM " Transfer records from QSU and QSV to FMU and FMV
1410 FOR I=4 TO 5
1415 READ (Z[I],KEY=X$,DOM=1416)
1420 K$=KEY(Z[I],END=1490)
1425 IF K$(1,LEN(X$))<>X$ THEN GOTO 1490
1430 READ (Z[I],KEY=K$)C$; C$=C$(12),C$(1,10)=C9$
1440 WRITE (Z[I+5],KEY=C$(1,23))C$
1480 GOTO 1420
1490 NEXT I
1500 GOTO 9900
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 RETURN 
9900 REM 
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9940 EXIT 
9999 END 
