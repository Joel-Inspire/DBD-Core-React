0010 REM "Monthly Activity Report <FM2CBA>
0015 REM "Prog Type: BS-3.0A    
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 07/02/13 - 9.690833 - tma - SSP# 264139
0037 REM "264139-FMR-RB Monthly Activity report.  Customer Code on report is 
0040 REM "Copyright 2013 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 SETERR 9000; SETESC 9300
0105 X0$="FM2CBA"
0110 X1$="Monthly Activity Report"
0120 DIM A$(365),A[17],E[31],D$(243),E$(448)
0140 M0$="-####,###.00",M1$="-##,###,##0"
0200 F2$="AdjustmentReceipt   Release   Transfer  "
0210 T1=1
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),Y5$(W3),T3$(20,"*"),S$(20)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0310 IOLIST A$(1),A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17]
0330 IOLIST C$
0340 IOLIST D$
0345 IOLIST D0$
0350 IOLIST E$,E[0],E[1],E[2],E[3],E[4],E[5],E[6],E[7],E[8],E[9],E[10],E[11],E[12],E[13],E[14],E[15],E[16],E[17],E[18],E[19],E[20],E[21],E[22],E[23],E[24],E[25],E[26],E[27],E[28],E[29],E[30],E[31]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,IOL=0425
0425 IOLIST V$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Z$="01O FMZ...  03O AR1...  04O FMP...  05O FM1...  "
0520 GOSUB 9750
0800 REM "Alternate Sort Info & total dim"
0810 DIM U[10,2],U0$(10),Y[5,1]
0820 DIM U[3,1]; U[0,0]=1,U[1,0]=1,U[1,1]=10,U[2,0]=11,U[2,1]=10,U[3,0]=21,U[3,1]=5,T0=3,U0$="222200",T5$="    AA/RS   S   D   ",U5$="Customer            Item code           From location       Activity date       ",U1=Z[2],U=31
0825 V0=2
0828 U0=T0; REM "LET T0=T0-1,U0=T0
0840 GOSUB 6900
0850 DIM T[T0,T1]; U5$=Y5$(1,20)+U5$,U5$(1,20)=Q4$
0860 DIM T0[4]
0890 REM "Open Printer
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0950 REM " Position read"
0970 DIM V4$(U); U9=1; FOR X=1 TO U0; V4$(U[X,0],U[X,1])=V2$(U9,U[X,1]); U9=U9+2*U[X,1]; NEXT X
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record and check range
1005 U$=KEY(U1,END=5000)
1010 FOR X=1 TO U0; U2=U[X,0],U3=U[X,0]*2-1,U4=U[X,1]
1020 IF U$(U2,U4)<V2$(U3,U4) THEN READ (U1); EXITTO 1000
1030 IF U$(U2,U4)>V2$(U3+U4,U4) THEN IF U=1 THEN EXITTO 5000 ELSE READ (U1); EXITTO 1000
1040 NEXT X
1100 REM "Get record"
1110 U9$=U$; IF V0<2 THEN GOTO 1150 ELSE U$="",X=0,B$=U9$
1120 U1$=U9$(U+1) ! SSP#205455
1130 READ (U1)
1150 READ (Z[1],KEY=U1$)IOL=0310 ! SSP#205455
1200 REM "Reporting criteria
1205 GOSUB 8000
1210 IF A$(122,5)="Tp: 6" THEN A$(35,1)="I"; REM "CONVERSION
1220 P0=POS(A$(35,1)="ARIT"); IF P0=0 THEN GOTO 1000 ELSE F3$=F2$((P0-1)*10+1,10)
1225 IF V$(73,1)="P" AND A$(216,1)<>"P" THEN GOTO 1000 ELSE IF V$(73,1)="I" AND A$(216,1)<>" " THEN GOTO 1000
1300 REM 
1310 DIM E[31]; FIND (Z[5],KEY=A$(1,10)+A$(16,10),DOM=1311)IOL=0350
1400 REM "Subtotals"
1410 IF U2$="" THEN T=0; GOTO 1430 ELSE FOR T=1 TO U0; IF U9$(U[T,0],U[T,1])<>U2$(U[T,0],U[T,1]) THEN EXITTO 1420 ELSE NEXT T; GOTO 1480
1425 GOSUB 3100
1430 IF T=0 THEN GOSUB 6500; GOSUB 6000
1490 T=0,U2$=U9$
1510 IF W+2>W0 THEN GOSUB 6000
1515 IF F1$=A$(16,10) THEN GOTO 1530 ELSE F1$=A$(16,10),FIRST$="Y"
1520 Y5$(2)=A$(16,10)
1523 Y5$(14)=A$(86,6)
1525 Y5$(23)=A$(46,40)
1530 IF A$(35,1)="R" THEN Y5$(64)=A$(12,4) ELSE Y5$(64)=A$(93,4)
1535 Y5$(69,5)=FND$(A$(26,6))
1540 Y5$(75)=A$(115,6); IF A$(122,15)<>S$(1,15) THEN Y5$(75)=A$(122,15)
1545 IF X3$(9,3)="027" OR X3$(9,3)="022" THEN Y5$(75)=A$(115,6)
1550 GOSUB 7500
1560 Y5$(88)=F3$
1580 IF A$(35,1)="I" THEN Y5$(99)=A$(12,4),A5=0-A5
1590 A6=A[1]; IF A$(35,1)="I" THEN A6=0-A6; REM "ssp099753
1720 Y5$(104)=STR(A6:M1$); REM "ssp099753
1740 Y5$(118)=STR(A5:M0$)
1800 GOSUB 7300
1805 REM "IF JL$=A$(16,10) THEN GOSUB 07300; GOTO 01811
1810 REM "IF POS(" "<>E$(350,20))>0 THEN GOSUB 07300; LET Y5$(23)=E$(350,20); GOSUB 07300
1820 REM "LET JL$=A$(16,10)
1830 IF FIRST$="Y" THEN FIRST$="N"; IF POS(" "<>E$(350,20))>0 THEN Y5$(23)=E$(350,20); GOSUB 7300; REM "if this is the first record for this item and there is a CIC then print it
1900 REM "Accumulate Totals"
1905 REM "P0 Set at 1220
1910 T0[P0]=T0[P0]+A5
1920 T[T0,1]=T[T0,1]+A5
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,X6$,"",0,0,X4$; T6$=X6$ ! SSP#264139
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "Subtotal Logic
3110 Q=T; FOR T=U0 TO Q STEP -1
3130 T$=FNS$(U5$(T*20+1,20)),T6$=U2$(U[T,0],U[T,1]),T7$=T5$(T*4+1,4); GOSUB 3050; IF POS(U0$(T+1,1)="23")>0 THEN GOSUB 7000 ELSE GOSUB 7300; GOSUB 7100
3150 IF POS(U0$(T+1,1)="13")>0 THEN GOSUB 6000
3180 NEXT T
3195 RETURN 
3280 RETURN 
3380 RETURN 
3480 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5050 T=0; GOSUB 3100
5200 REM "
5295 GOTO 9900
6000 REM "Page header
6010 F1$=""
6020 W0$=""
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; ON W5 GOTO 6041,9906
6045 IF T0$="END" THEN GOTO 6090
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6150 Y5$(2)="Item ",Y5$(13)="Rev Date",Y5$(23)="Description",Y5$(64)="Whse",Y5$(71)="Date",Y5$(79)="Ref No.",Y5$(88)="Activity",Y5$(99)="Loc.",Y5$(107)="Quantity",Y5$(122)="Value"
6160 GOSUB 7300
6195 IF FID(W9)=FID(0) THEN PRINT 'SF',
6199 RETURN 
6500 REM "Customer Break
6510 DIM C$(400); C$(11,35)="Customer "+A$(1,10)+" Not found"; FIND (Z[3],DOM=6511,KEY=A$(1,10))IOL=0330
6515 DIM D$(243); FIND (Z[4],KEY="D"+A$(1,10),DOM=6516)IOL=0340
6520 DIM D0$(50); FIND (Z[4],KEY="E"+A$(1,10)+D$(52,4),DOM=6521)IOL=0345
6525 D1$=""; IF POS(" "<>D$(52,6))<>0 THEN D1=NUM(D$(56,2),ERR=6526); D1$=D0$(18+D1*6,6)
6530 IF POS(" "<>D1$)=0 THEN W2$=FNS$(C$(11,35)) ELSE W2$=FNS$(C$(11,35))+" for period ending "+FND$(D1$)
6535 IF U2$<>"" THEN W8=.1
6540 RETURN 
6700 REM "CHECK EXIST SORT
6710 Q=0; CALL "ZZINFO",0,Q,X3$,R0,0,0,0,0,0,0,Q$+""; IF Q<0 THEN GOTO 6790 ELSE Q=-1
6720 READ (Z[13],KEY=V1$,DOM=6790)S9$,Z$,R2
6730 IF S9$<>S8$ THEN GOTO 6790 ELSE IF R2<>R0 OR R0<>R1 THEN GOTO 6790
6740 DIM Z9$(22); CALL "ZZDISP","DXS",Z$(1,6),"",X3$,Z9$,"",1,0,X4$
6750 CALL "ZZDISP","TXS",Z$(7,4),"",X3$,Z9$,"",13,0,X4$
6755 Z9$="+("+Z9$+")"
6780 CALL "ZZPROM","S3",X3$,A,Z9$,"","",0; IF A>1 THEN GOTO 9900 ELSE Q=A-1
6795 RETURN 
6800 REM "WRITE SORT CONTROLRECORD
6870 Z$=X3$(21,6)+STR(TIM*100:"0000")
6880 WRITE (Z[13],KEY=V1$)S8$,Z$,R0
6890 RETURN 
6900 REM "Write Temp Sort
6902 S8$="FM_DETAIL_ACTIVITY  SHIP_FROM_CUSTOMER  CUSTOMER_CODE       FORM_CODE           FROM_LOCATION       ACTIVITY_DATE       "
6905 Z9=10^12/2; GOSUB 6950; IF Q=0 THEN GOTO 6945 ELSE READ (Z[1],KEY="",DOM=6906)
6910 Q$=STR(U1:"00"); CALL "ZZINIT",Q$
6912 GOSUB 8100
6915 U$=KEY(Z[1],END=6945); U1$=U$; READ (Z[1])IOL=0310 ! SSP#205455
6920 REM IF POS(" "<>A$(92,5))=0 THEN GOTO 06915
6921 IF A$(11,1)="C" THEN IF POS(" "<>A$(92,5))=0 THEN GOTO 6915
6930 U$=A$(36,10)+A$(16,10)+A$(92,5)+A$(26,6)+U1$ ! SSP#205455
6932 WRITE (U1,KEY=U$)
6935 C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150; R1=R1-1; REM "IF POS("G"=FID(0))<>1  THEN PRINT @(S9,22),R1:"##,##0",                                          
6940 GOTO 6915
6945 GOSUB 6800; RETURN 
6950 REM "Create Temporary Sort Filt TMP
6955 Q$="TMP"+FID(0),D0=0; FIND (Z[13],KEY=X3$(9,3)+"ZB2",DOM=6956)P9$; D0=NUM(P9$(8,4))
6960 CALL "ZZINFO",Z[1],0,X3$,R0,0,0,0,0,0,0,""; R1=R0; GOSUB 6700; ON Q+1 GOTO 6961,6975
6962 ! IF R1>1000 THEN Z$="+("+STR(R1)+")"; CALL "ZZPROM","S4",X3$,A,Z$,"","",0; ON A GOTO 6963,9900!SSP#229934
6965 ERASE Q$,ERR=6966; GOTO 6965
6970 SORT Q$,U+34,0,D0,0 ! SSP#205455
6972 CALL "ZZPROM","S2",X3$,0,S9$,"","R",0; S9=2+LEN(S9$); REM "IF POS("G"=FID(0))<>1 THEN PRINT @(0,22),'CL',@(0),S9$,
6975 Z$="02O "+Q$+"  "
6980 GOSUB 9750; ON Z0 GOTO 6981,9900
6985 U1=Z[2]
6995 RETURN 
7000 REM "Totals logic
7001 IF T=0 THEN RETURN ELSE IF T[T,0]<2 THEN GOSUB 7300; GOTO 7100
7002 IF T[T,0]<2 THEN GOSUB 7300; GOTO 7100
7004 IF W+3>W0 THEN GOSUB 6000
7005 IF T<>1 THEN Y5$(118,13)=T1$(1,13); GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" "+Q2$+" ("+STR(T[T,0]:"0")+"):"
7025 IF T<>1 THEN Y5$(118)=STR(T[T,1]:M0$); GOSUB 7300; GOTO 7095
7040 IF T0[3]<>0 THEN Y5$(44)="   Releases: "+STR(T0[3]:M0$); GOSUB 7300
7045 IF T0[2]<>0 THEN Y5$(44)="   Receipts: "+STR(T0[2]:M0$); GOSUB 7300
7050 IF T0[1]<>0 THEN Y5$(44)="Adjustments: "+STR(T0[1]:M0$); GOSUB 7300
7055 IF T0[4]<>0 THEN Y5$(44)="  Transfers: "+STR(T0[4]:M0$); GOSUB 7300
7065 Y5$(41)="Total Activity: "+STR(T[1,1]:M0$); GOSUB 7300
7075 DIM T0[4]
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 AND T0$="END" THEN GOSUB 7200; GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7390 REM "IF W>=W0 THEN GOSUB 06000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP 13O ZZPARM  "
7420 GOSUB 9750; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7455 V1$="TMP"+FID(0)
7475 W0$="(By Customer / Item / Location)"
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "GET VALUE AMT IN A5
7502 C1$=""
7505 IF A$(35,1)="S" THEN GOTO 7511
7510 IF V$(72,1)<>"B" THEN IF V$(75,1)="Y" THEN GOTO 7550
7520 A5=A[5]; IF V$(72,1)="F" THEN A5=A[3] ELSE IF V$(72,1)="B" THEN A5=A[6]
7530 IF POS(D$(237,1)="B1")>0 THEN GOSUB 8300; IF C2<>0 THEN A5=C2*A[1]/C1,C1$="*"
7545 RETURN 
7550 REM "RE-PRICE USING CURRENT
7560 IF E[4]=0 THEN A5=0 ELSE IF V$(72,1)="F" THEN A5=A[1]*E[29]/E[4] ELSE A5=A[1]*E[3]/E[4]
7595 RETURN 
7990 SETERR 9000
7995 RETURN 
8000 REM "READ ALTERNATE RECORDS
8090 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[1],T9,X3$,B1,B0,K,B,D,S0,S1,F$
8115 IF %GUI THEN ML_MESS.CTL'VALUE$="There are "+STR(B1)+" records to process"
8129 REM "Set B0, we make sure T0 is > 1, because later on we MOD and look for avalue of 1. IF B0 is 1, then nothing would get reported. We look for a result of 1 because this causes the first record to automatically start the reporting instead of waiting until the B0'th record to get the first report
8130 B0=INT(B1*.02); IF B0<=1 THEN B0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter B2 (B2 is init'ed here and used here, just don't use it somewhere else)
8152 IF %TOD THEN RETURN ! SSP 240570
8155 IF %GUI THEN CALL "ZZBARG",X3$,"HG",7.2,31,28,B2,B1,C ELSE CALL "ZZBARG",X3$,"HG",19,10,50,B2,B1,C
8195 RETURN 
8300 REM "Get Bundled Sell Price per unit
8320 Q0$=A$(1,10)+A$(16,10)
8325 Q1$=A$(231,8)
8330 CALL "FM2UZG",X3$,0,D$(237,1),Q0$,Q1$,C0$,C1,C2
8390 RETURN 
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8992 DEF FNZ$(Z)=CHR(ASC("B")+SGN(Z))+BIN(INT(ABS(Z)),6)
8994 DEF FNS$(Z$)=Z$(1,POS(S$=Z$+S$)-1)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 IF Y5=68 THEN RETRY ELSE ON C9 GOTO 1140,2040
9750 REM "OPEN FILES ROUTINE
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9790 RETURN 
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9927 ERASE "TMP"+FID(0),ERR=*NEXT ! SSP#222788
9930 IF NOT(NUL(Y4$)) THEN CALL Y4$,ERR=*NEXT ELSE RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
56000 ! CHANGES SINCE 03/01/2006
56005 REM "190474-Additional Services - Enhance TF to provide for tax codes   
56006 REM "205455-Oracle - FFN usage in ZZFLES to be replaced with FN%FFN     
56007 REM "222788-Print Management Monthly Activity by Whse report leaves     
56008 REM "240570-GUI Printing - Support for queueing and scheduling reports. 
56009 REM "264139-FMR-RB Monthly Activity report.  Customer Code on report is  not masking correctly
