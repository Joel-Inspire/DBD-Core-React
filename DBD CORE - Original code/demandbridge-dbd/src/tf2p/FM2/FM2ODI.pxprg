0010 REM "A/R Open Order Inquiry <FM2ODI>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 05/31/18 - 13.413611 - tma - SSP# 301091
0037 REM "301091-PxPlus 2017 - when in A/R Open Order Inquiry, if you have a 
0040 REM "Copyright 2018 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0100 SETERR 9000
0105 SETERR 0106; ENTER X3$,X4$,Q0$,Q1$; X9=1
0106 SETERR 9000
0110 X0$="FM2ODI",X1$="A/R Open Order Inquiry",K9$="",K9=0,F9$="",CUST$=""
0120 DIM Z0$(80,"-"),B$(168)
0130 K0=9,K1=2,V1=7,V2=20
0135 C9=-1
0140 M0$="-#,###,###.00",M1$="-###.00%"
0145 IF Q0$="" AND X9=0 THEN GOSUB 8950
0155 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
0160 DEF FNS$(Z9$)=Z9$(1,POS("      "=Z9$+"      ")-1)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0200 REM "
0230 X2=-1; PRINT @(0,5),'CE',
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12]
0320 IOLIST B$(1)
0330 IOLIST C$
0350 IOLIST E$,E[0],E[1],E[2]
0370 IOLIST G$
0390 IOLIST M$,M[0],M[1]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O FS2...  02ORFS1...  05O PO3...  13O ZZPARM  07ORAR1...  08O FM0...  09O PO1...  10O FS4...  11O FSO...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 DIM SEC$(255); FIND (Z[13],KEY="osec"+X3$(40,3),DOM=0531)SEC$
0590 READ (Z[1],KEY=K9$,DOM=0591)
0600 REM "
0610 FIND (Z[13],KEY=X3$(9,3)+"A/R",DOM=9800)P0$
0615 GB$=""; FIND (Z[13],KEY=X3$(9,3)+"G/B",DOM=0616)GB$
0620 GOSUB 6000
0690 READ (Z[1],KEY=K9$,DOM=0691)
0800 REM 
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(391),B[15],CUST_INFO$(600)
1035 DIM A$(21+K9),A[12]
1036 %IL_IMAGE_TYPE$="",%IL_IMAGE_KEY$=""
1045 A$(1,0+K9)=K9$+A1$,N=0
1050 IF CUST$<>"" THEN Q1$=CUST$
1060 IF Q1$<>"" THEN GOTO 1100 ELSE PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9700
1140 ON X0 GOTO 1200,1300,1500
1200 REM "Customer code
1205 IF Q1$<>"" THEN A$(1)=Q1$; GOSUB 6450; GOTO 1220
1210 CALL "ZZENTR","AXUX",A{ALL},A$,X4$,X3$,18,3+V0,1+K9,10,C0,"A/R","","","AR2MAD00","AR1","AR2MAB",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9700,6970
1220 FIND (Z[7],KEY=A$(1+K9,10),DOM=1210)CUST_INFO$
1221 A1$=A$(1,10),CUST$=A$(1,10)
1225 PRINT @(44,3),CUST_INFO$(11,35),
1230 IF POS(" "<>B$(46,10))=0 THEN GOTO 1240
1235 FIND (Z[7],KEY=B$(46,10),DOM=1240)IOL=0330
1236 IF P0$(15,1)="Y" THEN CALL "ZZDISP","A",B$(46,10),"A/R",X3$,"","",18,4+V0,X4$; PRINT @(44,4),B$(11,35),
1240 GOTO 1120
1300 REM "Ship-to location
1305 J$="C"+A1$
1306 X$="Leave blank to display all Ship-to's"
1310 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,18,5+V0,12,4,C0,"","{1"+X$,J$,"AR2M02","FM0B","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1310,1311
1315 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1310,6800,9700,6970
1317 IF C0=4 THEN GOTO 9700
1318 IF A$(12,4)="    " THEN GOTO 1319 ELSE FIND (Z[8],KEY="C"+A1$(1,10)+A$(12,4),DOM=1300)
1320 PRINT @(18,5),A$(12,4),
1330 JL$=A$(12,4),SHIP_TO$=A$(12,4)
1500 REM "Display lines"
1502 DIM T[1]; T0=0,T1=0,T2=0,T3=0; GOSUB 2150
1505 IF F9$>"" THEN READ (Z[11],KEY=A1$(1,10)+F9$,DOM=1506) ELSE READ (Z[10],KEY=A1$(1,10),DOM=1506)
1510 IF F9$>"" THEN A2$=KEY(Z[11],END=2216); READ (Z[11]) ELSE A2$=KEY(Z[10],END=2216); READ (Z[10])
1515 IF A2$(1,10)<>A1$(1,10) THEN GOTO 2216
1520 IF F9$>"" THEN READ (Z[2],KEY=A2$(26),DOM=1510)IOL=0320 ELSE READ (Z[2],KEY=A2$(11),DOM=1510)IOL=0320
1521 IF %HAS_IMAGE_LIB THEN %IL_IMAGE_TYPE$="ORDER",%IL_IMAGE_KEY$=MID(B$,118,8)
1522 IF JL$="    " THEN GOTO 1528
1523 READ (Z[5],KEY=A2$(11),DOM=1524)
1525 H$=""; XT$=KEY(Z[5]); READ (Z[5],KEY=XT$)H$
1526 IF A2$(11)<>XT$(1,8) THEN GOTO 1510
1527 IF H$(12,4)<>JL$ THEN GOTO 1510
1530 GOSUB 8400
1600 REM "Initialize
1620 READ (Z[1],KEY=B$(118,8),DOM=1621)
1700 REM "Read through lines
1710 K$=KEY(Z[1],END=2200)
1720 IF K$(1,8)<>B$(118,8) THEN GOTO 2200
1730 IF L>V2 THEN GOSUB 2100
1740 READ (Z[1],KEY=K$)IOL=0310
1745 IF K0$="" THEN K0$=K$
1750 PRINT @(1,L),A$(6,3),@(7,L),A$(50,40),; IF A$(155,1)="M" THEN GOTO 1790 ELSE PRINT @(44,L),A[6]:M0$,; IF SEC$(63,1)<>"Y" THEN PRINT @(58,L),A[6]-A[3]:M0$,
1755 IF A$(48,1)="Y" OR A[8]<>0 THEN PRINT @(4,L),'BB',"*",'EB',
1760 IF SEC$(63,1)<>"Y" THEN IF A[6]=0 THEN PRINT @(72,L),0:M1$, ELSE PRINT @(72,L),(A[6]-A[3])*100/A[6]:M1$,
1790 L=L+1,T0=T0+A[6],T1=T1+A[3]
1791 IF A$(155,1)<>"M" THEN IF A$(9,1)=" " THEN GOSUB 4000 ELSE GOSUB 4100
1792 IF A$(155,1)<>"M" THEN GOSUB 3000
1795 GOTO 1710
2100 REM "new page
2110 CALL "ZZPROM","X0FM2ODI",X3$,Z,"* indicates back-ordered line, ** Packing List Printing Suspended","","V",X0
2120 ON Z GOTO 2110,2130,2400,2250,2300,2500,2300,2300,2300,2110
2140 PRINT @(0,V1+2),'CE',
2150 L=V1+2,K0$=""
2190 RETURN 
2200 REM "End routine
2205 IF L+1>V2 THEN GOSUB 2100
2209 PRINT @(0,L),Z0$,@(25-LEN(D0$),L+1),
2210 IF B$(128,1)="S" THEN PRINT @(10),'BB',"**",'EB',
2211 PRINT @(17),"Sales order ",D0$," total:",@(44),T0:M0$,; IF SEC$(63,1)<>"Y" THEN PRINT @(58),T0-T1:M0$,
2212 IF SEC$(63,1)<>"Y" THEN IF T0=0 THEN PRINT @(72,L+1),0:M1$, ELSE PRINT @(72,L+1),(T0-T1)*100/T0:M1$,
2213 PRINT @(0,L+2),Z0$
2214 IF POS(B$(54,1)="BS")=0 THEN T2=T2+T0,T3=T3+T1 ELSE T[0]=T[0]+T0,T[1]=T[1]+T1
2215 L=L+3,T0=0,T1=0; GOSUB 2100; GOTO 1510 ! SSP 218031
2216 GOSUB 7700
2219 CALL "ZZPROM","X0FM2ODI",X3$,Z,"* indicates a back-ordered line, ** Packing List Printing Suspended","","V",0; IF Z>5 THEN GOSUB 2120; GOTO 2340
2221 ON Z GOTO 2222,2222,2400,2222,2300,2500
2250 PRINT @(0,V1+2),'CE','CF',
2290 IF X9>0 THEN GOTO 9900
2295 IF Q1$<>"" THEN GOTO 1000 ELSE GOTO 1000
2300 REM "Input order number
2305 IF X9=1 THEN CALL "ZZPROM","4",X3$,0,"This option not allowed through the Assistant feature","","",0; GOTO 2219
2308 IF Z=6 THEN TMPX$="Enter an order number to view the order's notepad" ELSE IF Z=7 THEN TMPX$="Enter an order number to view the order's shipping information" ELSE IF Z=8 THEN TMPX$="Enter an order number to start the current display at" ELSE TMPX$="Enter an order number to go to that order"
2310 DIM Q1$(8); PRINT @(0,21),'CE',@(29,21),"Order number:",; CALL "ZZENTR","AUX",A{ALL},Q1$,X4$,X3$,42,21,1,8,C0,"O/P",TMPX$,A$(1,10),"FM2ODI10","FS1","",""
2315 IF C0<0 THEN GOTO 2350
2320 Q2$=Q1$
2330 IF Z=6 THEN GOSUB 3400; GOTO 2130 ELSE IF Z=7 THEN GOSUB 3450; GOTO 2130 ELSE IF Z=8 THEN GOTO 3500 ELSE GOTO 3300
2340 PRINT @(0,3+V0),'CE',; GOSUB 6000; GOSUB 6200
2400 REM "Redisplay logic
2405 F9$=""
2410 GOSUB 2140
2420 GOTO 1500
2500 REM "Starting Cust P/O#
2510 DIM F9$(15); PRINT @(10,22),"Enter the starting Customer P/O No to display:",; CALL "ZZENTR","S",A{ALL},F9$,X4$,X3$,57,22,1,15,C0,"","",A1$(1,10),"FM2ODI12","FSO","",""
2590 GOTO 2410
3000 REM "Print location information
3005 GOSUB 3200
3010 D1=A[0]
3020 DIM E$(175),E[2]
3030 READ (Z[5],KEY=A$(147,8)+" "+A$(6,3)+" ",DOM=3031)
3040 READ (Z[5],END=3070)IOL=0350
3045 IF E$(166,9)+E$(7,3)<>A$(147,8)+" "+A$(6,3) THEN GOTO 3070
3049 E[0]=E[0]-E[2]; REM "Sub prev shipped off
3050 D1=D1-E[0]
3055 IF L>V2 THEN GOSUB 2100
3056 GOSUB 3100
3058 IF POS(" "<>E$(12,4))=0 THEN E1$=E$(11,1) ELSE E1$=E$(11,1)+"-"+E$(12,4)
3060 PRINT @(5,L),E[0]/J:M0$," ",E3$,@(32,L),E1$,@(40),E0$,
3061 L=L+1
3065 GOTO 3040
3071 IF D1<=0 THEN GOTO 3090
3075 READ (Z[5],KEY=A$(147,8)+"     ",DOM=3090)IOL=0350
3076 IF L>V2 THEN GOSUB 2100
3077 GOSUB 3100
3078 IF POS(" "<>E$(12,4))=0 THEN E1$=E$(11,1) ELSE E1$=E$(11,1)+"-"+E$(12,4)
3080 PRINT @(5,L),D1/J:M0$," ",E3$,@(32,L),E1$,@(40),E0$,
3085 L=L+1
3090 RETURN 
3100 REM "Format location name
3105 IF POS(" "<>E$(12,4))=0 OR LEN(E$)<259 THEN E0$=FNS$(E$(16,30)) ELSE E0$=FNS$(E$(225,35))
3110 IF LEN(E0$)<>0 THEN E0$=E0$+"-"
3120 IF POS(" "<>E$(106,16))<>0 THEN E0$=E0$+FNS$(E$(106,16))+", "+E$(122,2)
3140 RETURN 
3200 REM "Set J based on u/m
3210 DIM M$(20),M[1]
3215 READ (Z[13],KEY="U/M"+A$(124,4),DOM=3216)IOL=0390
3220 IF M$(20,1)<>"Y" THEN J=A[5],J0=A[5] ELSE J=M[1]; IF M[0]<>0 THEN J0=M[0] ELSE J0=M[1]
3225 IF J=0 THEN J=1
3230 E3$=FNS$(A$(124,4))+"/"+FNR$(STR(J0:"##,##0"))
3240 RETURN 
3300 REM "Run order program
3310 U0$=X3$(1,6)+"PF",Q$="12"+A1$+"*-"
3320 U1$="FM2ODM"+STR(LEN(Q2$):"00")+Q2$+X0$+Q$+U1$
3330 WRITE (Z[13],KEY=U0$)U0$,U1$
3350 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
3390 RUN "FM2ODM"
3400 REM "Do Order Notepad
3405 PRINT @(0,3),'CE',
3410 CALL "FM2NPA",X3$,X4$,"",Q1$
3440 PRINT @(0,3+V0),'CE',; GOSUB 6000; GOSUB 6200
3445 RETURN 
3450 REM "Shipping info
3455 PRINT @(0,3),'CE',
3460 CALL "SM2MCA",ERR=3461,X3$,X4$,"",Q1$
3490 PRINT @(0,3+V0),'CE',; GOSUB 6000; GOSUB 6200
3495 RETURN 
3500 REM "Position file at order number in Q1$,
3505 TMP$=Q1$; TMP$(LEN(TMP$),1)=CHR(ASC(TMP$(LEN(TMP$),1))-1); REM "Reduce the ordernumber's last digit by one, so if they type in the exact number we will go to it after the read
3510 READ (Z[10],KEY=A1$(1,10)+TMP$,DOM=3511)
3511 IF %HAS_IMAGE_LIB THEN %IL_IMAGE_TYPE$="ORDER",%IL_IMAGE_KEY$=Q1$
3535 DIM T[1]; T0=0,T1=0,T2=0,T3=0; GOSUB 2140
3545 GOTO 1510
4000 REM "Print ship from information
4005 GOSUB 8200
4010 IF POS(" "<>A$(10,4))<>0 THEN PRINT @(28,L),'SB',"From:",'SF',@(34),A$(10,4),
4015 DIM E4$(225); FIND (Z[8],KEY="D"+E4$(1,10)+A$(10,4),DOM=4016)E4$
4020 GOSUB 8350
4080 L=L+1
4090 RETURN 
4100 REM "Print P/O information
4105 GOSUB 8200
4110 PRINT @(20,L),'SB',"P/O:",'SF',
4120 CALL "ZZDISP","A",A$(147,8)+A$(9,1),"P/O",X3$,"","",25,L,X4$
4125 DIM E$(300); FIND (Z[9],KEY=A$(147,8)+A$(9,1),DOM=4126)E$
4140 GOSUB 8300
4150 PRINT 'SB',@(20,L+1),"Plant Job Number:",@(53),"Scheduled: ",'SF',
4160 PRINT @(38,L+1),E$(248,12),; IF POS(" "<>E$(291,6))>0 THEN PRINT @(63,L+1),">> ",FND$(E$(291,6))," <<", ELSE PRINT @(66,L+1),"N/A", ! SSP#301091
4165 L=L+2
4170 IF LEN(E$)<327 THEN GOTO 4190 ELSE IF POS(" "<>E$(314,14))=0 THEN GOTO 4190
4180 PRINT @(20,L),'SB',"Rush per:",@(45),"No. of Working Days:",'SF',
4185 PRINT @(30,L),E$(314,12),@(66,L),E$(326,2),
4187 L=L+1
4190 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(3,3+V0),"Customer code:",@(38,3+V0),"Name:",; IF P0$(15,1)="Y" THEN PRINT @(0,4+V0),"Bill-to customer:",
6062 PRINT @(9,5+V0),"Ship-to:",
6065 PRINT @(0,6+V0),Z0$,
6070 PRINT @(0,V1),"Line",@(7,V1),"Description",@(31,V1),"Location",@(46,V1),"Sales amount",@(60,V1),"Gross Profit",@(76,V1),"GP%",@(0,V1+1),Z0$,
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 IF P0$(15,1)="Y" THEN CALL "ZZDISP","A",A$(11+K9,10),"A/R",X3$,"","",18,4+V0,X4$; PRINT @(44,4),B$(11,35),
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6456 REM "????IF LEN(A1$)<LEN(A$) THEN LET A1$=A$
6460 CALL "ZZDISP","A",CUST$,"A/R",X3$,"","",18,3+V0,X4$
6462 REM "GOSUB 07600
6464 PRINT @(44,3),CUST_INFO$(11,35),@(18,5+V0),SHIP_TO$,
6490 RETURN 
6500 REM "DELETE
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6606 FIND (Z[7],KEY=A$(1+K9,10),DOM=6607)IOL=0320
6690 RETURN 
6700 REM "ADD ENTRY
6704 %IL_IMAGE_TYPE$="",%IL_IMAGE_KEY$=""
6705 IF Z9=0 THEN GOTO 6790
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6815 GOSUB 6600
6820 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6825 A$(1,LEN(A1$))=A1$
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
6902 V9$="1203  6104  7404  1806  6106  0009  1109  3909  1711  1713  1715  5511  6311  5513  5515  "
7500 REM "CUSTOM PROGRAMMING ROUTINES
7600 REM "Print S/O stuff
7610 CALL "ZZDISP","A",B$(6,10),"A/R",X3$,"","",31,3,X4$
7615 IF G$(1,10)<>B$(6,10) THEN DIM G$(100); FIND (Z[8],KEY=B$(6,10),DOM=7616)IOL=0037
7620 PRINT @(44,3),G$(11,35),
7640 RETURN 
7700 REM "Print customer total
7705 IF L+3>V2 THEN GOSUB 2100
7710 PRINT @(0,L),Z0$,@(15,L+1),"Total customer sales orders:",@(44,L+1),T2:M0$,@(58,L+1),T2-T3:M0$,
7715 IF T2=0 THEN PRINT @(72,L+1),0:M1$, ELSE PRINT @(72,L+1),(T2-T3)*100/T2:M1$,
7720 IF T[0]=0 AND T[1]=0 THEN GOTO 7740 ELSE X$=""
7725 PRINT @(14,L+2),"Total bill as shipped orders:",
7730 PRINT @(44,L+2),T[0]:M0$,@(58,L+2),T[0]-T[1]:M0$,; IF T[0]=0 THEN PRINT @(72,L+2),0:M1$ ELSE PRINT @(72,L+2),(T[0]-T[1])*100/T[0]:M1$,
7735 IF X$="" THEN L=L+1,T[0]=T[0]+T2,T[1]=T[1]+T3; X$="Y"; PRINT @(37,L+2),"Total:",; GOTO 7730
7740 RETURN 
8200 REM "Print order type & item code
8210 IF A$(155,1)<>" " THEN PRINT @(7,L),A$(155,1),"-" ELSE PRINT @(7,L),A$(19,10),; GOTO 8240
8215 IF A$(155,1)<>"I" AND A$(155,1)<>"C" THEN PRINT @(9,L),A$(19,10), ELSE CALL "ZZDISP","A",A$(19,10),"ICE",X3$,"","",9,L,X4$
8240 RETURN 
8300 REM "Display Vendor, Plant name
8310 DIM E3$(40)
8325 E3$(1)=FNS$(E$(21,35))+"-"+FNS$(E$(146,16))+", "+E$(162,2)
8335 PRINT @(40,L),E3$,; E3$=""
8340 RETURN 
8350 REM "Display Ship from city/state
8355 IF POS(" "<>E4$(111,16))=0 THEN GOTO 8390
8360 DIM E5$(15)
8365 IF LEN(FNS$(E4$(111,16)))<12 THEN E5$(1)=FNS$(E4$(111,16))+","+E4$(127,2) ELSE E5$(1)=FNS$(E4$(111,16))
8370 PRINT @(40,L),E5$,
8390 RETURN 
8400 REM "Print Order information
8410 IF L+4>V2 THEN GOSUB 2100
8420 CALL "ZZDISP","AX",B$(118,8),"O/P",X3$,D0$,"",0,L,X4$; D0$=FNS$(D0$); IF POS(B$(54,1)="BS")>0 THEN D0$=B$(54,1)+"-"+D0$
8425 PRINT @(0,L),D0$,@(13,L),FND$(B$(16,6)),; IF POS(" "<>B$(55,6))<>0 THEN PRINT @(24),FND$(B$(55,6)),
8430 PRINT @(36),"PO#: ",B$(28,15),
8432 IF POS(" "<>B$(129,18))>0 THEN PRINT @(54),"From: ",B$(129,18),
8434 DIM SHIP_INFO$(299); READ (Z[5],KEY=B$(118,8)+DIM(5),DOM=8435)SHIP_INFO$
8435 L=L+1
8436 IF GB$<>"" THEN ONE_PRINTED=0; IF B$(226,1)="Y" THEN PRINT @(24,L),'BB',"SURE DATE",'EB',; ONE_PRINTED=1 END_IF ; IF POS(" "<>SHIP_INFO$(210,15))<>0 THEN PRINT @(50,L),'SB',"Ship via: ",'SF',SHIP_INFO$(210,15),; ONE_PRINTED=1 END_IF ; IF ONE_PRINTED THEN L=L+1
8439 IF B$(63,1)="Y" THEN PRINT @(7,L),"(Invoicing in Progress)",; L=L+1
8440 RETURN 
8950 REM "Program flow logic
8960 DIM U0$(6); U0$(1)=FID(0),U0$=U0$+"PF"; CLOSE (14); OPEN (14)"ZZPARM"
8970 FIND (14,KEY=U0$,DOM=8990)U0$,U1$
8975 IF U1$="" THEN Q1$=""; GOTO 8990
8980 Q1=NUM(U1$(7,2)),Q1$=U1$(9,Q1),U1$=U1$(Q1+9)
8985 IF Q1=11 AND Q1$(11,1)="*" THEN Q1$=Q1$(1,10); PRINT @(0,3),'CE',
8990 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9705 %IL_IMAGE_TYPE$="",%IL_IMAGE_KEY$=""
9706 IF X9=1 THEN GOTO 9900
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9730 RUN U1$(1,6)
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9905 %IL_IMAGE_TYPE$="",%IL_IMAGE_KEY$=""
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "218031-Problem with Image Library in A/R, S1, #7                   
56001 REM "301091-PxPlus 2017 - when in A/R Open Order Inquiry, if you have a 
