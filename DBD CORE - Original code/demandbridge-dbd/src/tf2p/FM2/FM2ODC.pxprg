0010 REM "Sales Order Shipping instructions <FM2ODC>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 09/02/15 - 9.126388 - tma - SSP# 279494
0037 REM "279494-Able to use a warehouse that doesn't exist in Special       
0040 REM "Copyright 2015 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0070 CALLED=0; SETERR 0071; ENTER X3$,X4$,Q0$,Q5$; CALLED=1; GOTO 0090
0080 IF O0=1 THEN O0=0; GOTO 2261
0085 IF O1<>0 THEN GOTO 8500
0086 IF O2<>0 THEN O2=0; GOTO 1630
0090 IF CALLED THEN CLEAR EXCEPT X3$,X4$,Q0$,Q5$,CALLED ELSE CLEAR 
0100 SETERR 9000
0110 X0$="FM2ODC",X1$="Special Shipping instructions",K9$="",K9=0
0120 DIM Z0$(80,"-"),S0$(20),A1$(6)
0125 V8$="00051499"
0130 K0=6,K1=1
0135 C9=-1
0140 M0$="##,###,###.00-",M1$="##,###"
0145 IF CALLED=0 THEN GOSUB 8950
0200 REM "
0230 IF Q5$<>"" THEN X2=-1; PRINT @(0,3),'CE',
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 V0=0,K9=4
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1,LEN(A$)),A[0],A[1],A[2],A[3]
0320 IOLIST B$
0330 IOLIST C$
0331 IOLIST C1$
0332 IOLIST C2$
0333 IOLIST C3$
0334 IOLIST C4$
0335 IOLIST C5$
0336 IOLIST C6$
0340 IOLIST D$
0350 IOLIST E$
0370 IOLIST G$
0380 IOLIST H$,H[0],H[1],H[2],H[3],H[4],H[5],H[6],H[7],H[8],H[9],H[10],H[11],H[12],H[13],H[14],H[15],H[16],H[17],H[18],H[19],H[20],H[21],H[22],H[23],H[24],H[25],H[26],H[27],H[28],H[29]
0400 IOLIST I$,I[0],I[1]
0420 IOLIST P9$,P[0],P[1],P[2]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X PO3...  02O FS1...  03O FMP...  04O AP4...  05O AR1...  06O ZZPARM  07O FM0...  08O FS2...  09O IC0...  10O AR5...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 READ (Z[6],KEY=X3$(9,3)+"I/C")P0$
0540 READ (Z[6],KEY=X3$(9,3)+"F/M")P4$
0550 READ (Z[6],KEY=X3$(9,3)+"A/R")AR$; REM "SSP# 137035
0555 READ (Z[6],KEY=X3$(9,3)+"AR3",DOM=*NEXT)ARPARM3$; REM "SSP 235445
0590 READ (Z[1],KEY=K9$,DOM=0591)
0595 READ (Z[6],KEY=X3$(9,3)+"G/B",DOM=0596)GB$; GB$="Y"
0600 REM "
0620 GOSUB 6000
0690 READ (Z[1],KEY=K9$,DOM=0691)
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(297),D$(100),E$(100),G$(75),C1$(522),C2$(75),C3$(75),C4$(75),C5$(75),H[29],H$(150),WHSE$(100),VENDOR$(100),PLANT$(100)
1035 DIM A$(428),A[3]; REM " SSP# 139226
1045 A$(166,9)=A1$
1050 IF Q5$<>"" THEN GOTO 1100
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9700
1140 ON X0 GOTO 1200,1250,1300,1500
1200 REM "S/O number
1202 IF Q5$<>"" THEN A$(166,9)=Q5$(1,8)+" "; GOSUB 6450; GOTO 1220 ELSE IF LEN(A1$)>7 THEN A$(166,9)=A1$(1,8)+" "; GOTO 1212
1210 CALL "ZZENTR","AXU",A{ALL},A$,X4$,X3$,10,3,166,8,C0,"O/P","{1"+X$,"","FM2ODC00","FS1","FM2ODC",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9700,6970
1220 EXTRACT (Z[2],KEY=A$(166,8),DOM=1210)IOL=0320
1222 IF MID(B$,313,1)="Y" THEN P4$(280,1)="N" ! SSP172433       
1225 GOSUB 7600
1240 GOTO 1120
1250 REM "Line number"
1253 IF LEN(Q5$)>8 THEN A$(7,3)=Q5$(9),C0=0; PRINT @(10,4),A$(7,3),; GOTO 1262
1254 IF LEN(A1$)>9 THEN A$(7,3)=A1$(10,3); PRINT @(10,4),A$(7,3),; GOTO 1262
1255 Q$=A$(166,8),J9$=A$(7,3)
1260 CALL "ZZENTR","ZX",A{ALL},A$,X4$,X3$,10,4,7,3,C0,"","{1"+X$,Q$,"FM2ODC01","FS2","FM2ODB",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1260,1261
1261 IF J9$<>"   " AND J9$<>A$(7,3) THEN GOSUB 7440
1262 IF ABS(C0)=2 AND LEN(A1$)>0 THEN A1$=A1$(1,6)
1263 IF INT(ABS(C0))=3 THEN GOTO 7000
1265 IF C0<0 THEN IF -C0=1 THEN C0=1 ELSE C0=ABS(C0); GOTO 1120
1270 FIND (Z[8],KEY=A$(166,8)+A$(7,3),DOM=1260)IOL=0380
1280 FIND (Z[4],KEY=H$(94,10),DOM=1281)IOL=0340
1285 GOSUB 8400
1287 GOSUB 8200
1288 GOSUB 7400
1290 GOTO 1120
1300 REM "Sequence number
1305 Q$=A$(166,9)+A$(7,3)
1310 CALL "ZZENTR","SZUX",A{ALL},A$,X4$,X3$,31,4,10,1,C0,"","{1"+X$,Q$,"FM2ODC03","PO3","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1310,1311
1311 IF C0<>-4 AND P4$(280,1)="Y" AND B$(54,1)="B" AND A$(10,1)<>"1" THEN CALL "ZZPROM",".4",X3$,Z,"You may not have more than one shipping location for this B type order","","",0; GOTO 1310; REM "WO111482
1312 IF ABS(C0)=2 AND LEN(A1$)>8 THEN A1$=A1$(1,9)
1315 IF C0=-1 THEN GOTO 1310 ELSE IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1316,6800,9700,6970
1320 IF A$(10,1)=" " THEN GOTO 1310
1330 REM GOSUB 08600; REM "New to check for invalid costing U/M for split ships
1340 GOTO 1120
1500 REM "
1505 Z9=-1
1510 A1$=K9$+A$(166,8)+" "+A$(LEN(K9$)+7,4),A$(1,LEN(K9$))=K9$
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1525 X9=0
1530 C8=2; GOSUB 7700
1535 FIND (Z[3],KEY="F"+A$(129+K9,1),DOM=1536)IOL=0332
1540 FIND (Z[3],KEY="H"+A$(130+K9,1),DOM=1541)IOL=0333
1545 FIND (Z[8],KEY=A$(166,8)+A$(7,3),DOM=1546)IOL=0380
1550 DIM C6$(240); FIND (Z[3],KEY="D"+B$(6,10),DOM=1551)IOL=0336
1555 IF A$(3,4)<>"    " THEN FIND (Z[7],KEY="D"+DIM(10)+A$(3,4),DOM=1556)WHSE$
1556 IF A$(295,4)<>"    " THEN FIND (Z[7],KEY="P"+A$(285,10)+A$(295,4),DOM=1557)PLANT$
1557 IF A$(285,10)<>"          " THEN FIND (Z[4],KEY=A$(285,10),DOM=1558)VENDOR$
1590 GOTO 1700
1600 REM "
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630
1630 GOTO 1000
1660 GOSUB 8000
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1820 CALL "ZZPROM",STR(6+C9),X3$,Z,"","","V",X0; ON Z GOTO 1850,6500,1630,6700
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 IF C9<>1 THEN EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310
1865 C9=1
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1,C9=1
2020 X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF INT(C0)=3 THEN GOSUB 7300 ELSE IF C0>3 OR X0<0 THEN GOTO 1800
2040 ON X0 GOTO 2100,2150,2950,2200,2250,3150,3200,2500,2550,2600,2650,2900,2700,2850,4300,2750,2800,3000,4100,4000,3100,4150,4200,4250,4350,1800; REM " SSP# 139226
2100 REM "Location type
2103 IF POS(B$(54,1)="BS")>0 THEN A$(7+K9,1)="D"; PRINT @(15,7),A$(7+K9,1),; GOTO 2130
2105 X$="C=Customer",J0$="C"
2106 IF H$(9,1)<>" " THEN IF POS(H$(155,1)="IC")<>0 OR H$(14,1)="Y" THEN X$=X$+", D=Distributor"; J0$=J0$+"D"
2110 CALL "ZZENTR","SRUX",A{ALL},A$,X4$,X3$,15,7+V0,7+K9,1,C0,"","{3"+X$,J0$,"FM2ODC02","WPC","",""
2120 IF C0<0 THEN GOTO 2135
2130 C8=0; GOSUB 7700; GOSUB 6230
2135 C0=1
2140 GOTO 2020
2150 REM "Ship to
2153 IF P4$(279,1)="Y" AND A$(11,1)="C" THEN GOTO 4900; REM "Enter Rec Dept,WO101301
2155 GOSUB 7900
2156 IF A$(7+K9,1)="D" AND POS(B$(54,1)="BS")=0 THEN X$="Enter Warehouse Location 9000 or higher" ELSE IF POS(B$(54,1)="BS")<>0 THEN X$="Enter warehouse location 0001-8999" ELSE X$="Enter Customer location or 'M' for Manual ship to, F10 by phone"
2157 IF A$(7+K9,1)="C" THEN IF C6$(24,1)="Y" THEN X$="Must enter a valid location for IMS customers, F10 by phone"
2158 SHIP_TO$=A$(K9+7,5)
2160 SETERR 9000; CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,15,8+V0,8+K9,4,C0,"","{3"+X$,J$,"FM2ODC04",J0$,"X",""; IF ABS(C0)=102 THEN Z8$="FM2LAA",Q1$=J$,O1=1; RUN "ZZ2STP" ELSE IF ABS(C0)>4 AND C0<>-7 THEN GOSUB 6400; ON C0 GOTO 2160,2161
2161 IF A$(7+K9,1)="C" THEN IF C0=-7 THEN C0=1,ST$="ST"+J$,ST_KEY$=J$+A$(8+K9,4); CALL "ZZ2PLU",X3$,ST_KEY$,1,ST$; IF ST$="" THEN GOTO 2150 ELSE A$(8+K9,4)=ST_KEY$(12,4); PRINT @(15,8+V0),A$(8+K9,4),; REM "WO103371, shipto lookup by phone
2162 IF POS(B$(54,1)="BC")>0 AND (A$(8+K9,4)<"0000" OR A$(8+K9,4)>"8999") THEN GOTO 2160; REM "110781 Make logic match PO P1
2163 IF A$(7+K9,1)="D" AND A$(8+K9,1)="M" THEN GOTO 2160
2164 IF C0<>-2 AND A$(7+K9,1)="D" AND A$(8+K9,4)<"9000" AND POS(B$(54,1)="BC")=0 THEN GOTO 2160; REM " REMARK to allow Distributor Owned Goods
2165 IF C6$(24,1)="Y" AND POS(H$(155,1)="SM")=0 THEN IF A$(7+K9,1)="C" THEN READ (Z[7],KEY="C"+B$(6,10)+A$(8+K9,4),DOM=2160)
2166 IF POS(B$(54,1)="BC")>0 OR A$(7+K9,1)="D" THEN FIND (Z(7),KEY="D"+DIM(10)+A$(8+K9,4),DOM=2160) ! SSP#279494
2170 IF SHIP_TO$=A$(K9+7,5) THEN C8=2; GOSUB 7700 ELSE C8=1; GOSUB 7700
2175 GOSUB 6225
2180 IF C0>0 THEN GOSUB 8300 ELSE GOSUB 6225; REM "SSP# 137035 
2190 GOTO 2020
2200 REM "Attn
2205 IF A$(11,1)="C" THEN X$="/ + whse code to use whse address"
2210 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,10,9+V0,12+K9,30,C0,"","{3"+X$,"","FM2ODC06","","",""
2230 IF A$(11,1)="C" AND A$(16,1)="/" AND NUM(A$(17,4),ERR=2231)>0 THEN FIND (Z[7],KEY="D"+S0$(1,10)+STR(NUM(A$(17,4)):"0000"),DOM=2231)IOL=0331; GOSUB 7800; GOTO 2210
2240 GOTO 2020
2250 REM "Address
2252 IF A$(8+K9,4)<>"    " THEN IF C0<>2 THEN GOTO 2020 ELSE C0=1
2255 A9$=A$(42+K9,87)+S0$(1,12)
2260 CALL "ZY2AAA",X3$,X4$,1,10+V0,"D","",2,A9$,"",1,C0,C3 ! SSP#215495
2265 IF ABS(C0)>4 THEN A$(42+K9,87)=A9$; GOSUB 6400; GOTO 2260
2280 A$(42+K9,87)=A9$; GOSUB 7843 ! 245342
2285 IF GB$="Y" THEN A$(269,10)=A$(122,2); GOTO 3170; REM "Sales tax code by state, for Gold Bond only
2290 GOTO 2020
2500 REM "F.O.B
2510 CALL "ZZENTR","SXU",A{ALL},A$,X4$,X3$,62,8+V0,129+K9,1,C0,"","{3"+X$,"F","FM2ODC18","FMPF","X",""; IF ABS(C0)=102 THEN Z8$="FM2SBA",Q1$="F",O1=2; RUN "ZZ2STP" ELSE IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2510,2511
2515 IF C0<0 THEN GOTO 2020
2520 FIND (Z[3],KEY="F"+A$(129+K9,1),DOM=2510)IOL=0332
2525 PRINT @(63,8+V0),"-",C2$(3,15),
2540 GOTO 2020
2550 REM "How to ship
2560 CALL "ZZENTR","SXU",A{ALL},A$,X4$,X3$,62,9+V0,130+K9,1,C0,"","{3"+X$,"H","FM2ODC20","FMPH","X",""; IF ABS(C0)=102 THEN Q1$="H",Z8$="FM2SDA",O1=3; RUN "ZZ2STP" ELSE IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2560,2561
2565 IF C0<0 THEN GOTO 2020
2570 FIND (Z[3],KEY="H"+A$(130+K9,1),DOM=2560)IOL=0333
2575 PRINT @(63,9+V0),"-",C3$(3,15),
2590 GOTO 2020
2600 REM "Inside delivery
2610 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,64,10+V0,131+K9,15,C0,"","{3"+X$,"","FM2ODC22","","",""
2640 GOTO 2020
2650 REM "Ship via
2660 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,62,11+V0,146+K9,1,C0,"","{3"+X$,"S","FM2ODC24","FMPS","X",""; IF ABS(C0)=102 THEN Z8$="FM2SCA",Q1$="S",O1=4; RUN "ZZ2STP" ELSE IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2660,2661
2665 IF C0<0 OR A$(150,1)=" " THEN GOTO 2020
2670 FIND (Z[3],KEY="S"+A$(146+K9,1),DOM=2660)IOL=0334
2675 PRINT @(64,11+V0),C4$(3,15),
2680 A$(210,15)=C4$(3,15)
2690 GOTO 2020
2700 REM "Ship with
2710 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,64,12+V0,147+K9,15,C0,"","{3"+X$,"","FM2ODC26","FMP","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2710,2711
2740 GOTO 2020
2750 REM "Quantity to ship
2751 IF (A[0]-A[2])/I0<0 THEN GOTO 2790; REM "96121-DXM
2753 REM "If lot line, all qty must go to one  place
2754 IF H$(2,1)="Y" THEN A[0]=H[0]; GOTO 2775
2755 DIM Q[1]; Q0=A[0]-A[2],Q[1]=Q0/I0
2760 CALL "ZZENTR","NX.",Q{ALL},A$,X4$,X3$,31,16+V0,1,822,C0,M0$,"{3"+X$,"","PO2MAD28","","",""
2770 A[0]=Q[1]*I0+A[2]
2775 IF C0>=0 THEN GOSUB 8100; H0=H[0]-Q0+A[0]-A[2]; GOSUB 7500
2790 GOTO 2020
2800 REM "Labels
2810 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,67,16+V0,1,400,C0,"###0","{3"+X$,"","FM2ODC30","","",""
2840 GOTO 2020
2850 REM "Mark for
2860 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,44,13+V0,171+K9,35,C0,"","{3"+X$,"","PO2MAD32","","",""
2890 GOTO 2020
2900 REM "Ship via description
2905 IF A$(150,1)<>" " AND C0<>2 THEN GOTO 2020
2910 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,64,11,210,15,C0,"","{3"+X$,"","PO2MAE25","","",""
2940 GOTO 2020
2950 REM "Company name
2955 IF A$(8+K9,1)<>"M" AND ABS(C0)<>2 THEN GOTO 2020
2960 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,21,8+V0,225,35,C0,"","{3"+X$,"","FM2ODC05","","",""
2990 GOTO 2020
3000 REM "Carton Label Format Code
3002 IF P4$(169,1)<>"Y" THEN GOTO 2020
3003 A$(1,1)=H$(1,1); PRINT @(72,16+V0),A$(1,1)
3005 X$="Enter the carton label flexible format code to use"
3010 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,72,16+V0,1,1,C0,"","{3"+X$,"FORMATCL","FM2ODC32","FFA","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3010,3011
3020 IF P4$(169,1)<>"Y" THEN GOTO 2020
3030 IF A$(1,1)<>" " THEN FIND (Z[13],KEY="FORMATCL"+A$(1,1),DOM=3010)
3035 IF A$(1,1)<>" " THEN H$(1,1)=" ",H[9]=0
3040 GOTO 2020
3100 REM "Department code
3105 IF P4$(182,1)<>"Y" THEN GOTO 2020
3110 CALL "ZZENTR","AU",A{ALL},A$,X4$,X3$,67,17+V0,260,9,C0,"FM"+C6$(235,1),"{3"+X$,"FORMATCL","FM2ODC34","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3010,3011
3140 GOTO 2020
3150 REM "SALES TAX CODE
3155 IF B$(54,1)="B" AND A$(12,1)="8" THEN A$(269,10)=" "; GOTO 2020
3157 GOSUB 7841
3160 CALL "ZZENTR","AU",A{ALL},A$,X4$,X3$,62,6,269,10,C0,"AR5","{3"+X$,"","AR2MAB32","AR5","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3160,3161
3170 IF POS(" "<>A$(269,10))>0 THEN FIND (Z[10],KNO=0,DOM=3160,KEY=A$(269,10))
3190 GOTO 2020
3200 REM "Taxable?
3205 IF E$(245,1)="Y" THEN X$="Customer is Tax exempt, are you sure location is taxable??" ELSE X$=""; REM SSP 187704
3206 REM "IF E$(245,1)="Y" THEN X$="Customer is Tax exempt, location must be taxable No" ELSE X$=""
3210 CALL "ZZENTR","SUR",A{ALL},A$,X4$,X3$,62,7,279,1,C0,"","{3"+X$,"YNC","FM2ODD38","","",""
3215 REM "IF E$(245,1)="Y" AND A$(279,1)<>"N" THEN GOTO 3210 SSP 187704 let ship to be taxable even if customer is exempt
3240 GOTO 2020
4000 REM "Carton Label Format Code #2 - cwj 2/24/98 SSP#085719
4002 IF P4$(169,1)<>"Y" THEN GOTO 2020
4003 A$(280,1)=H$(220,1); PRINT @(72,16+V0),A$(280,1),
4005 X$="Enter the 2nd carton label flexible format code to use"
4010 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,72,16+V0,280,1,C0,"","{3"+X$,"FORMATCL","FM20DC32","FFA","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 4010,4011
4020 IF P4$(169,1)<>"Y" THEN GOTO 4020
4030 IF A$(280,1)<>" " THEN FIND (Z[13],KEY="FORMATCL"+A$(280,1),DOM=4010)
4035 IF A$(280,1)<>" " THEN H$(220,1)=" ",H[24]=0
4040 GOTO 2020
4100 REM "Number of 2nd set of labels - cwj 2/24/98 SSP#085719
4102 IF P4$(169,1)<>"Y" THEN GOTO 2020; REM "cwj 5/27/98 ssp#099150
4105 X$="Enter the number of 2nd carton labels"; REM "cwj 5/27/98 ssp#099150
4110 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,67,16+V0,281,4,C0,"","{3"+X$,"","FM2ODC30","","",""
4140 GOTO 2020
4150 REM "Warehouse code for ship to address on PO printing only or blank, WO94734
4154 IF A$(285,10)<>"          " THEN GOTO 4190
4155 X$="Warehouse code for ship to for PO printing only, or blank"
4157 GOSUB 7915
4160 CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,16,19+V0,3,4,C0,"","{3"+X$,J$,"FM2ODC40",J0$,"X",""; C8=2; IF ABS(C0)=102 THEN Q1$=J$,O1=1,Z8$="FM2LAA"; RUN "ZZ2STP" ELSE IF ABS(C0)>4 THEN GOSUB 6400; ON CO GOTO 4160,4161
4170 DIM WHSE$(100); IF A$(3,4)<>"    " THEN FIND (Z[7],KEY=J$+A$(3,4),DOM=4160)WHSE$
4175 PRINT @(22,19+V0),WHSE$(16,35),
4190 GOTO 2020
4200 REM 4200"Vendor code
4205 IF A$(3,4)<>"    " THEN GOTO 4240
4206 X$="Vendor code for PO printing only or blank."
4210 CALL "ZZENTR","AXU",A{ALL},A$,X4$,X3$,16,20+V0,285,10,C0,"A/P","{1"+X$,"","","AP4","",""; IF ABS(C0)>4 THEN GOSUB 6400
4215 IF A$(285,10)<>"          " THEN READ (Z[4],KEY=A$(285,10),DOM=4200)VENDOR$ ELSE GOTO 4225
4216 PRINT @(28,20+V0),VENDOR$(11,35),
4220 CALL "ZZDISP","A",A$(285,10),"A/P",X3$,"","",16,20+V0,X4$
4225 IF A$(285,10)="          " THEN A$(295,4)="    "; PRINT @(16,21+V0),A$(295,4),'CL',@(16,20+V0),'CL',
4230 REM IF a$(285,2)<>"00" AND a$(285,2)<>"  " THEN READ (z[10],KEY=a$(285,2),DOM=4235); GOTO 4240 ELSE GOTO 4240
4235 REM CALL "ZZPROM","4",x3$,s3,"Division "+a$(285,2)+" is not on file. Press |<Return>","","",0; GOTO 4200
4240 GOTO 2020
4250 REM 4300"Plant code
4252 IF A$(285,10)="          " OR A$(3,4)<>"    " THEN GOTO 4275
4254 X$="Plant code for PO printing only or blank."
4255 CALL "ZZENTR","SUZX",A{ALL},A$,X4$,X3$,16,21+V0,295,4,C0,"A/P","{1"+X$,"P"+A$(285,10),"","FM0P","",""; IF ABS(C0)>4 THEN GOSUB 6400
4260 IF A$(295,4)="    " THEN PRINT @(16,21+V0),'CL',; GOTO 4275
4265 IF A$(295,4)<>"    " THEN READ (Z[7],KEY="P"+A$(285,14),DOM=4270)PLANT$; PRINT @(28,21+V0),PLANT$(16,35),; GOTO 4275
4270 CALL "ZZPROM","4",X3$,S3,"Plant code "+A$(295,4)+" is not on file. Press |<Return>","","",0; GOTO 4250
4275 GOTO 2020
4300 REM " EMAIL ADDRESS - SSP# 139226
4305 IF A$(299,40)=DIM(40) AND LEN(C1$)>522 THEN A$(299,40)=C1$(522,40); REM " SSP# 139226
4310 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,40,14,299,40,C0,"","{3"+X$,"","PO2MAD32","","",""; REM " SSP# 139226
4340 GOTO 2020; REM " SSP# 139226
4350 REM " Account Number SSP# 197118
4360 X$="Account Number"
4370 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,65,18,347,15,C0,"","{3"+X$,"","PO2MAD34","","",""
4390 GOTO 2020; REM ssp # 197118
4900 REM "Entry and lookup by Receiving Department, WO101301
4905 X$="Press Enter to skip -or- Enter Receiving Department (Lookup available)"
4910 DIM TEMP$(20); CALL "ZZENTR","SU",A{ALL},TEMP$,X4$,X3$,15,8+V0,1,20,C0,"","{3"+X$,A$(11,1)+B$(6,10),"FM2ODC99","FTD","",""; IF C0=6 THEN A$(12,4)=TEMP$; GOSUB 6400; C0=1; GOTO 2161
4915 IF POS(" "<>TEMP$)=0 THEN GOSUB 6400; GOTO 2155
4920 CALL "FS2DAM",X3$,X4$,1,A$(11,1)+B$(6,10),"",TEMP$,TEMP1$
4925 IF POS(" "<>TEMP1$)=0 THEN GOTO 4910
4930 A$(12,4)=TEMP1$,C0=1; PRINT @(15,8+V0),A$(12,4),; GOSUB 6400
4940 GOTO 2161
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(0,3),"Order no:",@(21,3),"Customer:",@(1,4),"Line no:",@(15,4),"Shipping Point:",@(0,5),Z0$,
6070 PRINT @(46,6),"Sales Tax Code:",@(0,7),"Location type:",@(56),"Tax?:",@(6,8),"Ship to:",
6075 CALL "ZY2AAA",X3$,X4$,1,10,"D","",0,"","",1,0,0 ! SSP#215495
6080 PRINT @(57,8),"FOB:",@(4,9),"Attn:",@(49,9),"How to ship:",@(45,10),"Inside delivery:",@(52,11),"Ship via:",@(51,12),"Ship with:",@(30,13),"Mark for:",@(55,18),"Account #:",; REM " SSP# 139226, SSP#197118
6090 PRINT @(33,14+V0),"Email: ",@(4,15+V0),"UM",@(16,15+V0),"Qty/UM",@(30,15+V0),"Quantity to ship",@(56,15+V0),"Pack",@(67,15+V0),"Labels",; REM " SSP# 139226
6095 PRINT @(0,17+V0),"Total on line: ",@(1,18+V0),"Left to ship:",
6097 PRINT @(4,19+V0),"Whse code:",
6100 IF P4$(182,1)="Y" THEN PRINT @(55,17+V0),"Cost code:",
6110 PRINT @(2,20+V0),"Vendor code:",@(3,21+V0),"Plant code:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 PRINT @(15,7+V0),A$(7+K9,1),@(15,8+V0),A$(8+K9,4),
6225 GOSUB 7800; REM "SSP#137035 
6230 CALL "ZZDISP","A",A$(269,10),"AR5",X3$,"","",62,6,X4$; PRINT @(62,7),A$(279,1),@(62,8+V0),A$(129+K9,1),"-",@(64,8+V0),C2$(3,15),@(62,9+V0),A$(130+K9,1),"-",@(64,9+V0),C3$(3,15),
6233 GOSUB 8225; GOSUB 7500
6235 PRINT @(64,10+V0),A$(131+K9,15),@(62,11+V0),A$(146+K9,1),@(64,11+V0),A$(210,15),@(64,12+V0),A$(147+K9,15),@(40,13+V0),A$(171+K9,35),@(31,16+V0),(A[0]-A[2])/I0:M0$,@(67,16+V0),A[1]:"###0",@(72),A$(1,1),@(65,18),A$(347,15),; REM " SSP# 139226,SSP#197118
6240 PRINT @(40,14),A$(299,40),; REM " SSP# 139226
6260 IF P4$(182,1)="Y" THEN CALL "ZZDISP","A",A$(260,9),"FM"+C6$(235,1),X3$,"","",67,17,X4$
6270 IF A[0]-A[2]<0 THEN PRINT @(31,16+V0),"Over Ship by ",ABS(A[0]-A[2])/I0; REM "96121-DXM
6275 PRINT @(16,19+V0),A$(3,4),@(22,19+V0),WHSE$(16,35),; REM "WO94734
6280 CALL "ZZDISP","A",A$(285,10),"A/P",X3$,"","",16,20+V0,X4$; REM "W/O 100863
6285 PRINT @(16,21+V0),A$(295,4); REM "W/O 100863
6290 PRINT @(28,20+V0),VENDOR$(11,35),@(28,21+V0),PLANT$(16,35),; REM "W/O 100863
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>0 THEN GOSUB 6200 ELSE GOSUB 6450; REM "Mod by cwj 2/27/98 SSP# 095662 at request of tma.
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 CALL "ZZDISP","A",A$(166,8),"O/P",X3$,"","",10,3,X4$
6464 GOSUB 7600
6465 PRINT @(10,4),A$(7,3),@(31,4),A$(10,1),
6490 RETURN 
6500 REM "DELETE
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6514 REM "If D9000 take care of FS5
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6605 FIND (Z[2],KEY=A$(166,8),DOM=6606)IOL=0320
6610 GOSUB 7600
6615 FIND (Z[8],KEY=A$(166,8)+A$(7,3),DOM=6616)IOL=0380
6620 FIND (Z[4],KEY=H$(94,10),DOM=6621)IOL=0340
6625 FIND (Z[3],KEY="D"+H$(94,10),DOM=6626)C6$
6690 RETURN 
6700 REM "ADD ENTRY
6705 IF Z9=0 THEN GOTO 6780
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6715 H$(136,1)=A$(279,1); REM SSP 187704 SET THE FS2 RECORD TO MATCH THE SPECIAL SHIPPING TAXABLE FLAG
6720 WRITE (Z[8],KEY=A$(166,8)+A$(7,3),DOM=6721)IOL=0380
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6812 IF LEN(A1$)>8 AND A1$(9,1)<>" " THEN A1$=""; READ (Z[1]); GOTO 6810
6815 GOSUB 6600
6820 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6821 IF A1$(1,LEN(Q5$))<>Q5$ THEN GOTO 6896
6822 IF LEN(A1$)>=13 THEN IF A1$(10,4)="    " THEN READ (Z[1],END=6895); GOTO 6805
6825 A$(1,LEN(A1$))=A1$,A$(166,9)=A1$(1,9)
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
6896 PRINT 'RB',; READ (Z[1],KEY=Q5$,DOM=1140); GOTO 1140
7000 REM "Up and down arrow on line #
7005 READ (Z[8],KEY=A$(166,8)+A$(7,3),DOM=7006)
7010 ON 10*FPT(C0)+2 GOTO 7040,7020
7020 REM "Down arrow
7025 K$=KEY(Z[8]); IF K$(1,8)=A$(166,8) THEN A$(7,3)=K$(9,3) ELSE PRINT 'RB',; A$(7,3)=""
7030 GOTO 7080
7040 REM "Up arrow
7055 CALL "ZZKEYP",ERR=7056,Z[8],K$; GOTO 7060
7056 CALL "ZZKEYL",ERR=7057,Z[8],K$
7060 IF LEN(K$)<11 THEN PRINT 'RB',; A$(7,3)=""; GOTO 7080 ELSE IF K$(1,8)<>A$(166,8) THEN PRINT 'RB',; A$(7,3)=""; GOTO 7080
7065 A$(7,3)=K$(9,3)
7080 PRINT @(10,4),A$(7,3),
7090 GOTO 1266
7300 REM "Special control logic
7310 ON FPT(C0)*10 GOTO 7390,7325,7330,7390
7320 REM "PRIOR/NEXT SECTION
7325 IF V8$="" THEN X0=X0+4; GOTO 7390 ELSE X0=NUM(V8$(POS(STR(X0:"00")<=V8$,2),2)); GOTO 7390
7330 IF V8$="" THEN X0=X0-6; GOTO 7335 ELSE X0=POS(STR(X0-1:"00")<=V8$,2)-2; IF X0<0 THEN X0=0; GOTO 7390 ELSE X0=NUM(V8$(X0,2)); GOTO 7390
7335 IF X0<0 THEN X0=0; GOTO 7390
7390 RETURN 
7400 REM "H0 is quantity shipped so far
7405 H0=0,REC_TODATE=0; DIM P[2] ! SSP#229823
7410 H0$=H$(147,8)+" "+H$(6,3)
7415 READ (Z[1],KEY=H0$,DOM=7416)
7420 K$=KEY(Z[1],END=7436); IF K$(1,12)<>H0$ THEN GOTO 7436
7425 READ (Z[1],KEY=K$)IOL=0420
7430 H0=H0+P[0]-P[2],REC_TODATE=REC_TODATE+P[2] ! SSP#229823
7435 GOTO 7420
7436 GOSUB 7500
7437 RETURN 
7439 REM "Verify that shipping records tie to qty ordered on line
7440 IF POS(B$(47,1)="WR")<>0 THEN H2=H[0] ELSE IF B$(54,1)="B" THEN H2=H(10)-H(7) ELSE IF H[0]=H[7] THEN H2=H[0] ELSE H2=H[0]-H[7] END_IF ; IF H0=H2 THEN GOTO 7490
7441 IF H0=0 THEN GOTO 7550
7442 IF P[0]-P[2]<0 OR H0<0 OR (MID(U1$,1,6)="PO2MAF" AND (H0+H2=H[0] OR H0=P(0) OR H(7)+H2=H0)) THEN GOTO 7490; REM '96121-DXM !SSP#229823
7443 IF H0=H2 OR REC_TODATE=H2 THEN GOTO 7490 ! SSP#229823
7445 IF I0=0 THEN I0=1 END_IF ; H1$=FNR$(STR(H0/I0:"#####0"))+" of "+FNR$(STR(H2/I0:"#####0"))+" on line "+H$(6,3)+" have been distributed"
7450 IF H0<H2 THEN H1$="Only "+H1$
7455 CALL "ZZPROM",".4",X3$,Z,H1$,"","",0
7460 A1$=H0$; EXITTO 1000
7490 RETURN 
7500 REM "Display total
7510 IF I0<>0 THEN PRINT @(16,18+V0),(H[0]-H0)/I0:M0$,
7540 RETURN 
7550 REM "Cancel special shipping
7555 IF POS(B$(54,1)="BS")>0 THEN CALL "ZZPROM",".4",X3$,0,"Special shipping required for Order Type: "+B$(54,1),"","",0; GOTO 7445
7560 CALL "ZZPROM",".Y",X3$,Z,"Do you wish to cancel special shipping instructions?","","",0
7565 ON Z GOTO 7566,7442 ! SSP#229823 
7570 EXTRACT (Z[8],KEY=A$(166,8)+A$(7,3),DOM=7576)IOL=0380
7575 H$(18,1)="N"; WRITE (Z[8],KEY=A$(166,8)+A$(7,3))IOL=0380
7590 GOTO 7510
7600 REM "Display P/O info
7630 IF B$(6,10)<>E$(1,10) THEN DIM E$(100); FIND (Z[5],KEY=B$(6,10),DOM=7631)IOL=0350
7634 CALL "ZZDISP","A",B$(6,10),"A/R",X3$,"","",31,3,X4$
7635 PRINT @(44,3),E$(11,35),
7690 RETURN 
7700 REM "Get proper default & load into address fields
7705 IF POS(" "<>C1$(248,35))>0 THEN J9$=C1$(248,35) ELSE J9$=C1$(16,35)
7706 IF C8=0 OR LEN(C1$)<618 THEN DIM C1$(618); REM " SSP# 139226
7710 IF POS(" "<>A$(8+K9,4))=0 AND MID(C6$,24,1)<>"Y" THEN GOTO 7750
7715 ON POS(A$(7+K9,1)="DPC") GOTO 7730,7717,7719,7721
7717 C0$="D"+S0$(1,10); GOTO 7725
7719 C0$="P"+D$(1,10); GOTO 7725
7721 C0$="C"+B$(6,10); GOTO 7725
7725 C0$=C0$+A$(8+K9,4)
7727 IF C8=0 THEN RETURN 
7730 IF A$(12,4)<>"M   " OR MID(C6$,24,1)="Y" THEN DIM C1$(618); FIND (Z[7],KEY=C0$,DOM=7740)IOL=0331; REM " SSP# 139226
7732 IF LEN(C1$)<618 THEN GOTO 7735; REM " SSP# 139226
7733 A$(299,40)=MID(C1$,522,40) ! SSP275930 IF A$(299,40)=DIM(40) THEN A$(299,40)=C1$(522,40); REM " SSP# 139226
7734 PRINT @(40,14),A$(299,40),; REM " SSP# 139226
7735 IF C8=2 THEN RETURN ELSE GOTO 7800
7740 IF MID(C6$,24,1)="Y" THEN EXITTO 2150 ELSE IF C8<>1 THEN GOTO 7735 ELSE IF A$(8+K9,1)="M" THEN C1$(16,35)=J9$; GOTO 7735 ELSE EXITTO 2150
7750 REM "Blank code case"
7760 ON POS(A$(7+K9,1)="DPC") GOTO 7735,7735,7770,7780
7770 C1$(16,35)=D$(11,35),C1$(51)=D$(46,163); GOTO 7735
7780 C1$(16,35)=E$(11,35),C1$(51)=E$(56,163); REM LET C1$(224,10)=B$(76,10),C1$(283,1)=B$(77,1); GOTO 07735
7800 REM "Set & display shipto stuff"   
7801 IF C0<0 OR C8=2 THEN GOTO 7830; REM "SSP#137035 - SSP# 143139 - CHECK IF NEW ENTRY - ONLY FLIP TAXABLE FLAG ON NEW ONE
7802 A$(12+K9,30)=C1$(160,20),A$(42+K9,87)=C1$(51,87),A$(225,35)=C1$(16,35); REM "SSP#137035 
7803 IF C1$(1,1)="D" AND C1$(12,1)="8" AND POS(" "<>MID(C1$,582,10))<>0 THEN A$(285,10)=C1$(582,10),A$(295,4)=C1$(592,4)
7805 IF C1$(1,1)="D" AND E$(245,1)="Y" AND AR$(128,1)="Y" THEN A$(269,10)=E$(235,10),A$(279,1)=E$(245,1); GOTO 7816; REM "IF TYPE D, TAX EXEMPT IN CUSTOMER MASTER = Y, AND FLAG AT 128,1 IS Y, SET TO WHAT'S IN CUSTOMER MASTER - BOTH TAX CODE AND EXEMPT-SSP#137035
7806 IF C1$(1,1)="D" AND AR$(128,1)="Y" AND E$(235,2)<>C1$(224,2) THEN A$(269,10)=C1$(224,10),A$(279,1)=E$(245,1); GOTO 7816; REM "FLAG AT (128,1) SET, TYPE D, STATES NOT SAME, USE CODE FROM LOCATION-SSP#137035
7808 IF C1$(1,1)="D" AND AR$(128,1)="Y" AND E$(235,2)=C1$(224,2) THEN IF AR$(129,1)="Y" THEN A$(269,10)=C1$(224,10),A$(279,1)=E$(245,1); GOTO 7816; REM " FLAG AT (128,1) SET, TYPE D, STATES SAME - IF FLAG AT (129,1) = Y, USE LOCATION -SSP#137035 
7810 IF C1$(1,1)="D" AND AR$(128,1)="Y" AND E$(235,2)=C1$(224,2) THEN IF AR$(129,1)<>"Y" THEN A$(269,10)=E$(235,10),A$(279,1)=E$(245,1); GOTO 7816; REM "FLAG AT (128,1) SET, TYPE D, STATES SAME - IF FLAG AT (129,1)<>Y USE CUSTOMER MASTER -SSP#137035 
7811 IF C1$(1,1)="D" AND AR$(128,1)<>"Y" THEN A$(279,1)=E$(245,1); REM " SSP# 143139 If type D and flag not set, must use what's in Customer Master
7812 IF C1$(1,1)="D" AND AR$(128,1)<>"Y" THEN A$(269,10)=E$(235,10); REM " SSP# 143139 If type D and flag not set, must use what's in Customer Master
7814 IF C1$(1,1)="C" THEN A$(269,10)=C1$(224,10),A$(279,1)=C1$(283,1); REM "SSP# 137035 
7815 IF POS(" "<>A$(269,10))=0 THEN A$(269,11)=B$(76,11),C8=2; IF C8=2 THEN GOTO 7817; REM " SSP# 143139 - ONLY FLIP FLAG ON NEW ONE
7816 IF A$(279,1)="Y" THEN A$(279,1)="N" ELSE A$(279,1)="Y"; REM IF E$(245,1)="Y" THEN A$(279,1)="N"; REM "Tax exempt to Taxable?-displays as Tax Exempt? on A/R Cust Mast - as Tax? on this screen and if Customer is Tax exempt, taxable must be N  - SSP 187704 ALLOW TAXABLE SHIP TO IF CUSTOMER IS EXEMPT
7817 IF H$(136,1)="C" AND A$(279,1)="Y" AND A$(269,2)="IL" THEN A$(279,1)="C"
7820 IF POS(" "<>C1$(248,35))>0 THEN PRINT @(21,7),C1$(16,35),; A$(225,35)=C1$(248,35)
7825 IF A$(12,4)="M   " THEN C1$(1)="" ELSE PRINT @(21,7+V0),C1$(16,35),
7830 PRINT @(21,8+V0),A$(225,35),@(10,9+V0),A$(12+K9,30),
7840 CALL "ZY2AAA",X3$,X4$,1,10+V0,"D","",1,A$(42+K9,87)+S0$(12),"",1,0,0 ! SSP#215495
7841 IF AR$(128,1)="N" AND C1$(1,1)="D" THEN GOTO 7890
7842 IF AR$(129,1)="Y" AND C1$(1,1)="D" THEN GOTO 7890
7843 IF AR$(134,1)<>"Y" THEN GOTO 7890
7844 READ (Z[10],KEY=E$(235,10),KNO=0,DOM=*NEXT)CUST_TAX$
7845 IF MID(CUST_TAX$,182,1)="Y" THEN GOTO 7890
7846 READ (Z[10],KEY=C1$(224,10),KNO=0,DOM=*NEXT)SHIP_TAX$
7847 IF MID(SHIP_TAX$,182,1)="Y" THEN GOTO 7890
7848 ! IF MID(C1$,224,7)=MID(A$,122,7) THEN A$(269,10)=C1$(224,10); GOTO 7895; REM SSP 212287, 245342
7849 IF (POS(X3$(9,3)="101118590",3)<>0 OR MID(ARPARM3$,9,1)="Y") AND MID(C1$,224,7)=A$(122,7) THEN G$(1,10)=C1$(224,10); GOTO 7857; REM SSP 212287 SSP 219276 SSP 230142 SSP 235445, 245342  ! SSP252208 jdf
7850 REM "245342-Lookup tax code using city, state and zip using called program - replaced lines 7850-7854 with line 7855
7855 CALL "TW2FUN;LOOKUP_TAX_CODE",ERR=*NEXT,X3$,X4$,A$(122,2),A$(124,5),A$(106,16),"",T$,T{ALL}; G$(1)=T$(1) ! 245342
7856 A$(269,10)=G$(1,10)
7892 IF A$(269,10)=DIM(10) THEN A$(269,10)=E$(235,10); REM " SSP# 143139 If no tax code in Location record use what's in Customer Master
7893 IF POS(X3$(9,3)="101")<>0 AND B$(6,2)="13" AND G$(1,10)="IL"+DIM(8) THEN A$(269,10)="ILCH"+DIM(6); REM SSP 207882
7895 RETURN 
7900 REM "Set J$& J0$ to high order part for location
7905 J$=""
7910 ON POS(A$(7+K9,1)="DPC") GOTO 7940,7915,7920,7925
7915 J0$="FM0D",J$="D"+S0$(1,10); GOTO 7940
7920 J0$="FM0P",J$="P"+B$(7,10); GOTO 7940
7925 J0$="FM0C",J$="C"+B$(6,10); GOTO 7940
7940 RETURN 
8000 REM "Set defaults from Customer O/P defaults
8010 DIM C6$(240); FIND (Z[3],KEY="D"+B$(6,10),DOM=8011)IOL=0330; A$(133,18)=C$(28,18),C6$=C$
8015 FIND (Z[6],KEY=X3$(1,6)+"FD",DOM=8016)IOL=0330
8020 IF A$(133,1)=" " THEN A$(133,1)=C$(17,1)
8021 IF A$(134,1)=" " THEN A$(134,1)=C$(18,1)
8022 IF A$(150,1)=" " THEN A$(150,1)=C$(19,1)
8035 FIND (Z[3],KEY="F"+A$(133,1),DOM=8036)IOL=0332
8040 FIND (Z[3],KEY="H"+A$(134,1),DOM=8041)IOL=0333
8045 FIND (Z[3],KEY="S"+A$(150,1),DOM=8046)IOL=0334; A$(210,15)=C4$(3,15)
8059 REM "Set qty to left to ship
8060 A[0]=H[0]-H0
8080 GOSUB 6230
8085 A$(260,9)=H$(205,9)
8090 RETURN 
8100 REM "Calculate # of carton labels
8110 IF H[12]=0 THEN A[1]=0 ELSE IF H[9]+H[24]<>0 THEN A[1]=INT(A[0]/H[12]*1.1+.9); IF A[1]<0 THEN A[1]=0
8120 PRINT @(67,16+V0),A[1]:"###0",
8140 RETURN 
8200 REM "Set U/M stuff
8210 DIM I$(20),I[1]; FIND (Z[13],KEY="U/M"+H$(124,4),DOM=8211)IOL=0400
8215 IF I$(20,1)="Y" THEN I0=I[1] ELSE I0=H[5]
8225 REM "Print line defaults
8226 IF I0=0 THEN I0=1
8230 PRINT 'SB',@(3,16+V0),H$(124,4),@(16,16+V0),H[5]:M1$,@(55,16+V0),H[12]:M1$,@(16,17+V0),H[0]/I0:M0$,'SF',
8240 RETURN 
8300 REM "Set defaults from Ship To
8305 IF A$(11,5)="C    " THEN GOTO 8000
8310 READ (Z[7],KEY=A$(11,1)+B$(6,10)+A$(12,4),DOM=8390)C7$
8320 IF C7$(288,1)<>" " THEN A$(133,1)=C7$(288,1)
8325 IF C7$(289,1)<>" " THEN A$(134,1)=C7$(289,1)
8330 IF POS(" "<>C7$(290,15))>0 THEN A$(135,15)=C7$(290,15)
8335 IF POS(" "<>C7$(306,15))>0 THEN A$(210,15)=C7$(306,15),A$(150,1)=C7$(305,1)
8345 FIND (Z[3],KEY="F"+A$(133,1),DOM=8346)IOL=0332
8350 FIND (Z[3],KEY="H"+A$(134,1),DOM=8351)IOL=0333
8355 GOSUB 6225
8390 RETURN 
8400 REM "Check special shipping flag
8405 IF H$(18,1)="Y" THEN GOTO 8440
8410 CALL "ZZPROM",".Y",X3$,Z,"This line does not have special shipping, would you like to special ship it?","","",0
8415 IF Z=1 THEN EXITTO 1260
8420 H$(18,1)="Y"
8425 WRITE (Z[8],KEY=A$(166,8)+A$(7,3))IOL=0380
8440 RETURN 
8500 REM "Handle return from setup
8510 O2=O1,O1=0,C0=0; ON O2 GOTO 8590,8515,8520,8525,8530
8515 IF LEN(Q1$)>11 THEN A$(12,4)=Q1$(12); GOSUB 6400; GOTO 2161 ELSE GOSUB 6400; GOTO 2160
8520 IF LEN(Q1$)>1 THEN A$(133,1)=Q1$(2,1); GOSUB 6400; GOTO 2511 ELSE GOSUB 6400; GOTO 2510
8525 IF LEN(Q1$)>1 THEN A$(134,1)=Q1$(2,1); GOSUB 6400; GOTO 2561 ELSE GOSUB 6400; GOTO 2560
8530 IF LEN(Q1$)>1 THEN A$(150,1)=Q1$(2,1); GOSUB 6400; GOTO 2661 ELSE GOSUB 6400; GOTO 2660
8590 GOTO 0090
8600 REM "CHECK FOR COSTING BY LOT
8610 DIM KK$(13),FLAG$(1)
8620 DIM II$(20); FIND (Z[13],KEY="U/M"+H$(120,4),DOM=8221)II$
8630 A1$=K9$+A$(166,8)+" "+A$(LEN(K9$)+7,3)
8640 READ (Z[1],KEY=A1$,DOM=8641)
8650 KK$=KEY(Z[1],END=8690)
8660 IF KK$(1,12)=A1$(1,12) AND A$(10,1)<>KK$(13,1) AND II$(21,1)="Y" THEN CALL "ZZPROM","4",X3$,S3,"This Cost U/M is not valid for split shipments","","",0; EXITTO 1300
8690 RETURN 
8900 REM "Functions
8910 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
8950 REM "Program flow logic
8960 DIM U0$(6); U0$(1)=FID(0),U0$=U0$+"PF"
8970 CLOSE (14); OPEN (14)"ZZPARM"; FIND (14,KEY=U0$,DOM=8990)U0$,U1$
8980 Q1=NUM(U1$(7,2)),Q5$=U1$(9,Q1),U1$=U1$(Q1+9)
8990 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 OR ERR=65 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; C8=2; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9705 GOSUB 7440
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9715 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9730 RUN U1$(1,6)
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q5$=A1$(K9+1); EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "197118-Account number field was not showing in Gen. Shipping screen
56010 REM "202306-Ship Via code modifications; Open up lowercase letters,
56011 REM "207882-Needs to setup their Illinois customers to have correct 
56012 REM "212287-Voicemail from Dawn regarding sales tax defaults     
56013 REM "219276-Veratax enhancements related to new taxing requirements
56014 REM "235445-Modify sales tax programs to enabled require logic based on 
56015 REM "240346-CHUI Order Entry/Req entry displays that you need a valid   
56016 REM "245342-Sales tax code lookup - ability to factor City into lookup
56017 REM "252208-Sales tax code lookup not working correctly for Suwanee, GA 
56018 REM "275930-WO274948; manual order, switch ship-to, email addr doesn't  
56019 REM "279494-Able to use a warehouse that doesn't exist in Special       
