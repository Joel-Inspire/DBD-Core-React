0010 REM "Sales Order - Summary Screen <FM2ODT>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 03/18/14 - 16.442222 - jvv - SSP# 262321
0037 REM "262321-WMS:  While in WMS PO receiving - able to delete po  - stock
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0095 CALLED$="Y"; IF Q0$="AR" THEN Q0$="",FROM_AR$="Y"
0100 SETERR 9000
0110 X0$="FM2ODT",X1$="Sales Order Summary Screen",K9$="",K9=0
0120 IF Q0$="R" THEN X1$="Repeat Order Summary Screen"
0130 K0=9,K1=2,V1=5,V2=17
0135 C9=-1
0140 M0$="##,###,##0.00-",M1$="###.00%"
0150 DEF FNQ$(Z8,Z9$)=STR(Z8:Z9$(1,POS(".00"=STR(Z8:Z9$)+".00")-1))
0155 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
0160 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0200 REM "
0230 X2=-1; PRINT @(0,5),'CE',
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0296 DIM L0$(LEN(M0$),X3$(235,1)),L1$(LEN(M1$),X3$(235,1)); L0$=MNM('GS')+L0$+MNM('GE'),L1$=MNM('GS')+L1$+MNM('GE')
0297 DIM M$(20),M[1]
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29]
0320 IOLIST B$
0350 IOLIST E$,E[0],E[1],E[2]
0370 IOLIST G$
0390 IOLIST M$,M[0],M[1]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X FS2...  02ORFS1...  05O PO3...  06O ZZPARM  07ORAR1...  08O FM0...  09O PO1...  15O ZY9     16O FTO... "
0515 IF Q0$="R" THEN Z$="01X FT4...  02ORFT3...  05O FT5...  06O ZZPARM  07ORAR1...  08O FM0...  09O PO1...  10O AP4...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0590 READ (Z[1],KEY=K9$,DOM=0591)
0595 READ (Z[6],KEY=X3$(9,3)+"G/B",DOM=0596)GB$; GB$="Y"
0600 REM "
0620 GOSUB 6000
0690 READ (Z[1],KEY=K9$,DOM=0691)
0800 REM 
0810 IF CALLED$<>"Y" THEN GOSUB 8950
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(297),G$(100)
1035 DIM A$(228),A[29]
1045 A$(1,6)=K9$+A1$
1050 IF Q1$<>"" THEN GOTO 1100
1055 GOTO 1100
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1500
1200 REM "S/O number
1202 IF Q1$<>"" THEN A1$=Q1$,A$(1)=Q1$; GOSUB 6450
1205 IF LEN(A1$)>0 THEN A$(1,8)=A1$(1,8); GOTO 1212
1210 CALL "ZZENTR","AXUX",A{ALL},A$,X4$,X3$,12,3,1,8,C0,"O/P","{1"+X$,"","FM2ODT00","FS1","FM2ODA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9800,6970
1220 EXTRACT (Z[2],KEY=A$(1,8),DOM=1210)IOL=0320
1225 FIND (Z[7],KEY=B$(6,10),DOM=1226)IOL=0370
1235 GOSUB 7600
1240 GOTO 1120
1500 REM "Display lines"
1600 REM "Initialize
1605 T0=0,T1=0
1610 GOSUB 2150
1620 READ (Z[1],KEY=B$(118,8),DOM=1621)
1700 REM "Read through lines
1710 K$=KEY(Z[1],END=2200)
1720 IF K$(1,8)<>B$(118,8) THEN GOTO 2200
1730 IF L>V2 THEN GOSUB 2100
1735 L=L+1
1740 READ (Z[1],KEY=K$)IOL=0310
1745 IF K0$="" THEN K0$=K$
1750 PRINT @(0,L),A$(6,3),@(7,L),A$(50,40),; IF A$(155,1)="M" THEN GOTO 1790 ELSE PRINT @(45,L),A[6]:M0$,@(59,L),A[6]-A[3]:M0$,
1760 IF A[6]=0 THEN PRINT @(73,L),0:M1$, ELSE PRINT @(73,L),(A[6]-A[3])*100/A[6]:M1$,
1790 L=L+1,T0=T0+A[6],T1=T1+A[3]
1791 IF A$(155,1)<>"M" THEN IF A$(9,1)=" " THEN GOSUB 4000 ELSE GOSUB 4100
1792 IF A$(155,1)<>"M" THEN GOSUB 3000
1795 GOTO 1710
2100 REM "new page
2105 IF CALLED$="Y" AND FROM_AR$<>"Y" THEN CALL "ZZPROM","X0FM2ODT",X3$,Z,"","","V",0; ON Z GOTO 2110,2130,2400,2221,2300,2500,2600,3700
2110 CALL "ZZPROM","X0PO2MAT",X3$,Z,"","","V",X0
2120 ON Z GOTO 2110,2130,2400,2221,2300
2140 PRINT @(0,V1+1),'CE',
2150 L=V1,K0$=""
2190 RETURN 
2200 REM "End routinge
2210 PRINT @(45,L),L0$,@(59,L),L0$,@(73,L),L1$,@(26,L+1),"Sales order total:",@(45,L+1),T0:M0$,@(59,L+1),T0-T1:M0$,
2215 IF T0=0 THEN PRINT @(73,L+1),0:M1$, ELSE PRINT @(73,L+1),(T0-T1)*100/T0:M1$,
2216 IF Q0$="R" THEN CALL "ZZPROM",".Y",X3$,Z,"Is this the correct repeat order?","","",0; IF Z=0 THEN LINE_NO$="Y"; GOTO 2250 ELSE LINE_NO$="N"; GOTO 2250
2217 IF CALLED$="Y" AND FROM_AR$<>"Y" THEN CALL "ZZPROM","X0FM2ODT",X3$,Z,"","","V",0; ON Z GOTO 2221,2221,2400,2221,2300,2500,2600,3700
2218 CALL "ZZPROM","X0PO2MAT",X3$,Z,"","","V",0
2220 ON Z GOTO 2221,2221,2400,2221,2300
2250 PRINT @(0,V1+1),'CE',
2255 IF Q0$<>"R" THEN IF GB$="Y" THEN X$="Enter the first tracking location",TRACK$=B$(199,2); PRINT @(0,10),"Tracked to department:",@(23,10),TRACK$,; CALL "ZZENTR","SZ",TRACK{ALL},TRACK$,X4$,X3$,23,10,1,2,C0,"","{3"+X$,"","FM2ODT02","","",""; LINE_NO$="GB"+TRACK$; IF TRACK$="  " THEN GOTO 2255 ELSE IF CALLED$<>"Y" THEN EXTRACT RECORD (Z[2],KEY=A1$)FS1$; FS1$(199,2)=TRACK$; WRITE RECORD (Z[2],KEY=A1$)FS1$; REM "GB-production department
2256 IF Q0$<>"R" THEN IF GB$="Y" THEN X$="Enter number of additional work order copies to print",COPIES$=B$(201,2); PRINT @(5,12),"Number of copies:",@(23,12),COPIES$,; CALL "ZZENTR","Z",COPIES{ALL},COPIES$,X4$,X3$,23,12,1,2,C0,"","{3"+X$,"","FM2ODT04","","",""; LINE_NO$=LINE_NO$+COPIES$; IF CALLED$<>"Y" THEN EXTRACT RECORD (Z[2],KEY=A1$)FS1$; FS1$(201,2)=COPIES$; WRITE RECORD (Z[2],KEY=A1$)FS1$; REM "GB-number of addt'l copies of packing list
2260 IF CALLED$="Y" THEN GOTO 9900
2290 IF Q1$<>"" THEN GOTO 9700 ELSE GOTO 1000
2300 REM "Handle input of a line number"
2310 DIM Q1$(3); PRINT @(30,22),"Line number:",; CALL "ZZENTR","ZX",A{ALL},Q1$,X4$,X3$,42,22,1,3,C0,"","","","PO2MAT10","","",""
2315 IF C0<0 THEN GOTO 2350
2320 Q2$=B$(118,8)+STR(NUM(Q1$):"000")+"XX",LINE_NO$=Q1$
2330 GOTO 3300
2340 PRINT @(0,3+V0),'CE',; GOSUB 6000
2400 REM "Redisplay logic
2410 GOSUB 2140
2420 GOTO 1500
2500 REM "WO100078, return to order header in req entry
2510 LINE_NO$="OH"; GOTO 9900
2600 REM "WO105287, order notepad
2610 X$=B$(118,8); CALL "FM2NPA",X3$,X4$,"D1",X$; PRINT @(0,V1),'CL',; GOSUB 6000; GOTO 2400
3000 REM "Print location information
3005 GOSUB 3200
3010 D1=A[0]
3020 DIM E$(175),E[2]
3030 READ (Z[5],KEY=A$(147,8)+" "+A$(6,3)+" ",DOM=3031)
3040 READ (Z[5],END=3070)IOL=0350
3041 E[0]=E[0]-E[2]
3045 IF E$(166,9)+E$(7,3)<>A$(147,8)+" "+A$(6,3) THEN GOTO 3070
3050 D1=D1-E[0]
3055 IF L>V2 THEN GOSUB 2100; L=L+1
3056 GOSUB 3100
3058 IF POS(" "<>E$(12,4))=0 THEN E1$=E$(11,1) ELSE E1$=E$(11,1)+"-"+E$(12,4)
3060 PRINT @(5,L),FNQ$(E[0]/J,M0$)," ",E3$,@(32,L),E1$,@(40),E0$,
3061 L=L+1
3065 GOTO 3040
3071 IF D1<=0 THEN GOTO 3090
3075 READ (Z[5],KEY=A$(147,8)+"     ",DOM=3090)IOL=0350
3076 IF L>V2 THEN GOSUB 2100; L=L+1
3077 GOSUB 3100
3078 IF POS(" "<>E$(12,4))=0 THEN E1$=E$(11,1) ELSE E1$=E$(11,1)+"-"+E$(12,4)
3080 PRINT @(5,L),FNQ$(D1/J,M0$)," ",E3$,@(32,L),E1$,@(40),E0$,
3085 L=L+1
3090 RETURN 
3100 REM "Format location name
3105 IF POS(" "<>E$(12,4))=0 THEN E0$=FNS$(E$(16,30)) ELSE E0$=FNS$(E$(225,35))
3110 IF LEN(E0$)<>0 THEN E0$=E0$+"-"
3120 IF POS(" "<>E$(106,16))<>0 THEN E0$=E0$+FNS$(E$(106,16))+", "+E$(122,2)
3130 IF LEN(E0$)>40 THEN E0$=E0$(1,40)
3140 RETURN 
3200 REM "Set J based on u/m
3205 IF A$(124,4)=M$(4,4) THEN GOTO 3220
3210 DIM M$(20),M[1]
3215 READ (Z[6],KEY="U/M"+A$(124,4),DOM=3216)IOL=0390
3220 IF M$(20,1)<>"Y" THEN J=A[5],J0=A[5] ELSE J=M[1]; IF M[0]<>0 THEN J0=M[0] ELSE J0=M[1]
3225 IF J=0 THEN J=1
3230 E3$=FNS$(A$(124,4))+"/"+FNR$(STR(J0:"##,##0"))
3240 RETURN 
3300 REM "Run lines program
3305 IF CALLED$="Y" THEN GOTO 9900
3310 U0$=X3$(1,6)+"PF",Q$=STR(LEN(A1$):"00")+A1$
3320 U1$="FM2ODB"+STR(LEN(Q2$):"00")+Q2$+X0$+Q$+U1$
3330 WRITE (Z[6],KEY=U0$)U0$,U1$
3350 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
3390 RUN "FM2ODB"
3700 REM "Job Notes SSP 185579
3710 CALL "ZZGMBA",X3$,X4$,"O",A1$
3795 GOTO 2400
3799 ! ******************************************************
4000 REM "Print ship from information
4005 GOSUB 8200
4009 IF POS(" "<>A$(10,4))=0 THEN GOTO 4015
4010 PRINT @(28,L),'SB',"From:",'SF',@(34),A$(10,4),
4015 DIM E4$(225); FIND (Z[8],KEY="D"+E4$(1,10)+A$(10,4),DOM=4016)E4$
4020 GOSUB 8350
4080 L=L+1
4090 RETURN 
4100 REM "Print P/O information
4105 GOSUB 8200
4110 PRINT @(20,L),'SB',"P/O:",'SF',
4120 CALL "ZZDISP","A",A$(147,8)+A$(9,1),"P/O",X3$,"","",25,L,X4$
4123 IF Q0$="R" THEN DIM E$(300),AP4$(300); READ (Z[10],KEY=A$(94,10),DOM=4130)AP4$; E$(21,35)=AP4$(11,35),E$(146,16)=AP4$(106,16),E$(162,2)=AP4$(122,2); GOSUB 8300; GOTO 4180
4125 DIM E$(300); FIND (Z[9],KEY=A$(147,8)+A$(9,1),DOM=4126)E$; GOSUB 8300; GOTO 4180
4130 CALL "ZZDISP","AX",A$(94,10),"A/P",X3$,E3$,"",0,0,X4$
4140 IF Q0$<>"R" THEN E3$="** P/O Closed for Vendor "+E3$; GOSUB 8335 ELSE E3$="** Master not found for Vendor "+E3$; GOSUB 8335
4180 L=L+1
4190 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 IF CALLED$<>"Y" THEN PRINT @(0,3),"Order no:",@(21,3),"Customer:",@(0,4),Z0$,
6070 PRINT @(0,V1),"Line",@(7,V1),"Description",@(31,V1),"Location",@(46,V1),"Sales amount",@(60,V1),"Gross Profit",@(76,V1),"GP%",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 OR CALLED$="Y" THEN GOTO 6445
6460 CALL "ZZDISP","A",A$(1,8),"O/P",X3$,"","",10,3,X4$
6462 GOSUB 7600
6490 RETURN 
6500 REM "DELETE
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6605 FIND (Z[2],KEY=A$(1,8),DOM=6606)IOL=0320
6690 RETURN 
6700 REM "ADD ENTRY
6705 IF Z9=0 THEN GOTO 6790
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6815 GOSUB 6600
6820 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6825 A$(1,LEN(A1$))=A1$
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
6902 V9$="1203  6104  7404  1806  6106  0009  1109  3909  1711  1713  1715  5511  6311  5513  5515  "
7500 REM "CUSTOM PROGRAMMING ROUTINES
7600 REM "Print S/O stuff
7605 IF CALLED$="Y" THEN GOTO 7640
7610 CALL "ZZDISP","A",B$(6,10),"A/R",X3$,"","",31,3,X4$
7615 IF G$(1,10)<>B$(6,10) THEN DIM G$(100); FIND (Z[8],KEY=B$(6,10),DOM=7616)IOL=0037
7620 PRINT @(44,3),G$(11,35),
7640 RETURN 
8200 REM "Print order type & item code
8210 IF A$(155,1)<>" " THEN PRINT @(7,L),A$(155,1),"-" ELSE PRINT @(7,L),A$(19,10),; GOTO 8240
8215 IF A$(155,1)<>"I" AND A$(155,1)<>"C" THEN PRINT @(9,L),A$(19,10), ELSE CALL "ZZDISP","A",A$(19,10),"ICE",X3$,"","",9,L,X4$
8240 RETURN 
8300 REM "Display Vendor, Plant name
8310 DIM E3$(40)
8325 E3$(1)=FNS$(E$(21,35))+"-"+FNS$(E$(146,16))+", "+E$(162,2)
8335 PRINT @(40,L),E3$,; E3$=""
8340 RETURN 
8350 REM "Display Ship from city/state
8355 IF POS(" "<>E4$(111,16))=0 THEN GOTO 8390
8360 DIM E5$(15)
8365 IF LEN(FNS$(E4$(111,16)))<12 THEN E5$(1)=FNS$(E4$(111,16))+","+E4$(127,2) ELSE E5$(1)=FNS$(E4$(111,16))
8370 PRINT @(40,L),E5$,
8390 RETURN 
8950 REM "Program flow logic
8960 U0$=X3$(1,6)+"PF"
8970 FIND (Z[6],KEY=U0$,DOM=8990)U0$,U1$
8975 IF U1$="" THEN Q1$=""; GOTO 8990
8980 Q1=NUM(U1$(7,2)),Q1$=U1$(9,Q1),U1$=U1$(Q1+9)
8990 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9730 RUN U1$(1,6)
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q0$=LINE_NO$,Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "262321-WMS:  While in WMS PO receiving - able to delete po  - stock
