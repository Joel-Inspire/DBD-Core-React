0010 REM "Order History Report <FM2RMA>
0035 REM "5.6 - 09/03/08 - 15.838333 - jir - SSP# 221450
0040 REM "Copyright 2008 DemandBridge, Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 SETERR 9000; SETESC 9300
0110 X0$="FM2RMA",X1$="Order History Report"
0120 DIM A$(195),A[21],M$(20),M[2]
0130 DIM G$(50),G0$(50)
0140 Z0$="#,###,##0.00-",Z1$="##,##0.00#",Z2$="###,##0.00-",Z3$="##,##0",Z4$="##,###,##0-",Z5$="###.0-",Z6$="####0"
0160 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
0175 DEF FNJ$(Z9$)=Z9$(POS(" "<>Z9$)+POS("         "=Z9$))
0180 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
0210 T=1,T0=1,T1=1
0220 W3=130
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0280 X0$="FM2RMA",X1$="Order History Report"
0300 REM "I/O lists
0305 REM "IOLIST FOR FM1 - Customer Job Master
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7]
0330 IOLIST C$
0350 IOLIST E$,E[0],E[1],E[2],E[3],E[4],E[5],E[6],E[7],E[8],E[9],E[10],E[11],E[12],E[13],E[14],E[15],E[16],E[17],E[18],E[19],E[20]
0351 IOLIST E$,E[0],E[1],E[2],E[3],E[4],E[5],E[6],E[7],E[8],E[9],E[10],E[11],E[12],E[13],E[14],E[15],E[16],E[17],E[18]
0352 IOLIST E$
0353 IOLIST E$,E[0],E[1],E[2]
0390 IOLIST M$,M[0],M[1]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O FS6...  03O ARU...  04O AR1...  05O AR3...  06O FM1...  07O FS7...  08O FS8...  09O IC0...  10O FMP...  11O FS9...  13O ZZPARM  14O ASJ...  "; IF V1$(1,3)<>Y$(5,3) THEN Y$=Y$+"02O "+V1$+"  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0700 REM "Open Printer
0720 REM 
0750 REM "Company description
0760 READ (Z[13],KEY="CMP"+X3$(9,3))C0$
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0819 REM "Set U and U() not to look at order number, that is done later"
0820 DIM U[2]; U=14,U0=2,U[0]=0,U[1]=4,U[2]=10,T0=3,T1=6,V1=4,V2=10,V3=8,U0$="Y    ",T5$="S   AA/RAO/P    ",U5$="Salesperson",U6$="Customer number",U7$="Order number"; GOTO 0850
0829 REM "Set U and U() not to look at item code, that is done later"
0830 DIM U[2]; U=14,U0=2,U[0]=0,U[1]=4,U[2]=10,T0=3,T1=6,V1=4,V2=10,V3=10,U0$="Y   ",T5$="S   AA/RS      ",U5$="Salesperson",U6$="Customer number",U7$="Item code"; GOTO 0850
0840 DIM U[1]; U=3,U0=1,U[0]=0,U[1]=3,T0=1,T1=6,V1=3,U0$="Y   ",T5$="S       ",U5$="Product code"; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
0995 IF V0$(71,1)+V0$(88,1)="1Y" THEN F9$="Y" ELSE F9$=""
1000 REM "Read next record
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN READ (U1,END=5000); EXITTO 1000
1030 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U=1 THEN EXITTO 5000 ELSE READ (U1,END=5000); EXITTO 1000
1040 NEXT U9
1042 READ (U1)
1045 ON V0-1 GOTO 8100,8150,8200
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1155 ESCAPE ; IF %X3$(333,2)<>"  " AND A$(22,2)<>%X3$(333,2) THEN GOTO 1000 ! SSP 221450
1160 READ (Z[4],KEY=A$(1,10),DOM=*NEXT)CUST$; GOSUB VALIDATE_REP; IF NOT(VAL_REP) THEN GOTO 1995
1200 REM "Check dates
1205 REM IF V0$(86,1)="Y" THEN IF A$(48,6)="      " THEN GOTO 01995
1210 IF A$(D9,6)<V0$(72,6) OR A$(D9,6)>V0$(78,6) THEN GOTO 1995
1220 IF A$(111,1)="B" AND V0$(90,1)="Y" THEN GOTO 1995 ! WO158746, exclude B type orders
1300 REM "Read other data
1305 REM "Set description & size data
1310 GOSUB 7500
1320 GOSUB 7800
1370 GOSUB 6200
1400 REM "Subtotals"
1415 GOSUB 3100
1420 ON T GOTO 1470,1430,1440,1450,1460
1440 IF V0<=2 THEN GOSUB 6300
1450 IF V0=2 THEN GOSUB 6400
1490 T=0
1500 REM "Print Data
1510 IF W+3>W0 THEN GOSUB 6000
1519 IF F0$=A$(22,8) THEN GOTO 1530 ELSE F0$=A$(22,8)
1520 CALL "ZZDISP","AXS",A$(22,8),"O/P",X3$,Y5$,"",1,0,X4$; IF A$(111,1)<>" " THEN Y5$(1,12)=A$(111,1)+"-"+Y5$(1,10)
1525 CALL "ZZDISP","DXS",A$(34,6),"",X3$,Y5$,"",13,0,X4$
1530 Y5$(24)=E0$(22)
1535 A0=A[0]/M0
1538 IF A0=0 THEN GOTO 1541
1540 IF FPT(A0)=0 THEN Y5$(63)=STR(A0:Z4$) ELSE Y5$(63)=STR(A0:Z2$)
1545 A5=A0*A[1],A6=A0*A[4]
1547 IF A0=0 OR MID(M$,21,1)="Y" THEN A5=A[1],A6=A[4]
1548 IF M[0]<>0 THEN A5=A5/M[0],A6=A6/M[0]
1549 A7=A5-A6; IF A5=0 THEN A8=0 ELSE A8=A7*100/A5
1550 IF V0$(59,1)<>"A" AND V0$(87,1)<>"Y" THEN IF A[4]<>0 THEN Y5$(76)=STR(A[4]:Z1$)
1555 Y5$(88)=STR(A[1]:Z1$),Y5$(98)=STR(A5:Z0$); IF V0$(59,1)<>"A" AND V0$(87,1)<>"Y" THEN Y5$(111)=STR(A7:Z0$),Y5$(125)=STR(A8:Z5$)
1560 P3=A8
1585 T[T0,1]=T[T0,1]+A0,T[T0,2]=T[T0,2]+A5,T[T0,3]=T[T0,3]+A7
1590 GOSUB 7300
1605 GOSUB 8000
1620 IF V0$(85,1)="N" OR POS(" "<>A$(40,8))=0 OR A$(105,1)="X" THEN Y5$(24)=H9$,Y5$(51)=FNS$(A$(101,4))+"/"+FNJ$(STR(A[2]:Z6$)); GOTO 1690
1624 IF F1$=A$(40,8) THEN GOTO 1635 ELSE F1$=A$(40,8)
1625 CALL "ZZDISP","AXS",A$(40,8),"AR6",X3$,Y5$,"",2,0,X4$
1630 CALL "ZZDISP","DXS",A$(48,6),"",X3$,Y5$,"",13,0,X4$
1635 Y5$(24)=H9$,Y5$(62-LEN(M0$))=M0$
1636 GOTO 1690
1638 A0=A[5]/M0
1639 IF A0=0 THEN GOTO 1641
1640 IF FPT(A0)=0 THEN Y5$(63)=STR(A0:Z4$) ELSE Y5$(63)=STR(A0:Z2$)
1645 A5=A[6],A6=A[7]
1647 IF M$(21,1)="Y" OR A0=0 THEN P0=A5 ELSE IF M[0]<>0 THEN P0=A5*M[0]/A0 ELSE P0=A5/A0
1649 A7=A5-A6; IF A5=0 THEN A8=0 ELSE A8=A7*100/A5
1650 IF M$(21,1)="Y" OR A0=0 THEN P1=A6 ELSE IF M[0]<>0 THEN P1=A6*M[0]/A0 ELSE P1=A6/A0
1651 IF A[4]<>P1 OR A[1]<>P0 OR P3<>A8 THEN Y5$(75)="*"
1652 IF V0$(59,1)<>"A" AND V0$(87,1)<>"Y" THEN IF P1<>0 THEN Y5$(76)=STR(P1:Z1$)
1653 IF P0<>0 THEN Y5$(88)=STR(P0:Z1$)
1655 Y5$(98)=STR(A5:Z0$); IF V0$(59,1)<>"A" AND V0$(87,1)<>"Y" THEN Y5$(111)=STR(A7:Z0$),Y5$(125)=STR(A8:Z5$)
1685 T[T0,4]=T[T0,4]+A0,T[T0,5]=T[T0,5]+A5,T[T0,6]=T[T0,6]+A7
1690 GOSUB 7300
1695 IF V0$(89,1)="Y" THEN GOSUB SHIP_DETAIL
1900 REM "Accumulate Totals"
1990 T[T0,0]=T[T0,0]+1
1993 REM "Totals are accumulated in 1500's and 1600's
1995 ON V0-1 GOTO 8115,8165,1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3125 P1=1
3130 IF U1$="" THEN GOSUB 6200; GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; GOSUB 7000
3160 U1$=U9$(1,V1)
3170 GOSUB 6200
3177 IF U0$(1,1)="Y" THEN GOSUB 6000
3180 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$,T6$=U2$; GOSUB 3050; GOSUB 7000
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3260 U2$=U9$(V5,V2)
3280 RETURN 
3300 REM "Next break
3310 IF V3=0 THEN RETURN ELSE IF U3$=U9$(V6,V3) THEN GOSUB 3400; RETURN 
3320 GOSUB 3420
3330 IF U3$="" THEN GOTO 3360
3350 T=3,T$=U7$,T6$=U3$; GOSUB 3050; GOSUB 7000
3355 IF U0$(3,1)="Y" THEN GOSUB 6000
3360 U3$=U9$(V6,V3)
3365 F0$="",F1$=""
3380 RETURN 
3400 REM "Last break
3410 IF V4=0 THEN RETURN ELSE IF U4$=U9$(V7,V4) THEN GOSUB 3500; RETURN 
3420 GOSUB 3520
3430 IF U4$="" THEN GOTO 3460
3450 T=4,T$=U8$,T6$=U4$; GOSUB 3050; GOSUB 7000
3455 IF U0$(4,1)="Y" THEN GOSUB 6000
3460 U4$=U9$(V7,V4)
3480 RETURN 
3580 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END",W0$=""
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; GOSUB 7000
5200 REM "
5290 GOTO 9900
6000 REM "Page header
6035 IF T0$="END" THEN W0$=""
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6060 ELSE ON W5 GOTO 6041,9910
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6090 RETURN 
6100 REM "
6108 IF V0$(85,1)<>"N" THEN Y5$(1)="Order no /" ELSE Y5$(1)="Order no"
6109 Y5$(13)="Order"
6110 Y5$(23)="Description /",Y5$(51)="Unit of",Y5$(104)="Price",Y5$(117)="Gross"
6112 IF V0$(59,1)="A" OR V0$(87,1)="Y" THEN Y5$(117)=""
6115 GOSUB 7300
6118 IF V0$(85,1)<>"N" THEN Y5$(2)="Invoice no"
6120 Y5$(13)="Date",Y5$(23)="Size",Y5$(51)="Measure",Y5$(64)="Quantity",Y5$(80)="Cost",Y5$(90)="Price",Y5$(102)="Extension",Y5$(117)="Profit",Y5$(126)="GP%"
6122 IF V0$(59,1)="A" OR V0$(87,1)="Y" THEN Y5$(80,4)="",Y5$(117)=""
6125 GOSUB 7300
6190 RETURN 
6200 REM "Break #1 header
6205 IF V0=3 THEN GOTO 6250
6208 IF U9$(1,4)<>T6$ THEN G0$(1,39)=G$(1,39)
6210 DIM G$(50); READ (Z[5],KEY=U9$(1,4),DOM=6211)G$
6215 W0$=U9$(1,4)+" - "+FNS$(G$(5,35)); IF V0$(90,1)="Y" THEN W0$=W0$+" - Excluding B Type Orders"
6240 RETURN 
6250 REM "Product code
6255 DIM G$(50); READ (Z[10],KEY="T"+U9$(1,3),DOM=6256)G$
6260 W0$=U9$(1,3)+" - "+FNS$(G$(5,15)); IF V0$(90,1)="Y" THEN W0$=W0$+" - Excluding B Type Orders"
6280 RETURN 
6300 REM "Break #2 header
6307 IF W+3>W0 THEN GOSUB 6000
6308 GOSUB 7300
6310 DIM H$(50); FIND (Z[4],KEY=U9$(5,10),DOM=6311)H$
6315 CALL "ZZDISP","AXS",U9$(5,10),"A/R",X3$,Y5$,"",1,0,X4$
6320 Y5$(12)=H$(11,35)
6325 GOSUB 7300
6390 RETURN 
6400 REM "Break #3 header
6405 IF W+4>W0 THEN GOSUB 6000
6410 Y5$(2)="Item code: "+U9$(15,10)
6420 GOSUB 7300
6490 RETURN 
6900 REM "SORT
6905 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6940 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,X9
6990 RETURN 
7000 REM "Totals logic
7001 IF T[T,0]<2 THEN IF T=T0 THEN GOSUB 7300; GOTO 7100 ELSE GOTO 7100
7002 IF V0<>3 THEN IF W+4>W0 THEN IF U9$(1,4)<>T6$ THEN W0$=G0$(1,4)+"-"+FNS$(G0$(5,35)); IF V0$(90,1)="Y" THEN W0$=W0$+" - Excluding B Type Orders"
7004 IF W+4>W0 THEN GOSUB 6000
7015 Y5$(98)=T1$(1,LEN(Z0$)); IF V0$(59,1)<>"A" AND V0$(87,1)<>"Y" THEN Y5$(111)=T1$(1,LEN(Z0$)),Y5$(125)=T1$(1,LEN(Z5$))
7020 IF T<2 THEN GOSUB 7310 ELSE GOSUB 7300
7025 Y5$(1)=T3$(1,T0+1-T)+" "+T$
7032 IF T[T,2]=0 THEN A9=0 ELSE A9=T[T,3]*100/T[T,2]
7035 Y5$(98)=STR(T[T,2]:Z0$); IF V0$(59,1)<>"A" AND V0$(87,1)<>"Y" THEN Y5$(111)=STR(T[T,3]:Z0$),Y5$(125)=STR(A9:Z5$)
7040 IF T<2 THEN GOSUB 7310 ELSE GOSUB 7300
7050 IF T[T,5]=0 AND T[T,6]=0 THEN GOTO 7070
7052 IF T[T,5]=0 THEN A9=0 ELSE A9=T[T,6]*100/T[T,5]
7055 Y5$(98)=STR(T[T,5]:Z0$); IF V0$(59,1)<>"A" AND V0$(87,1)<>"Y" THEN Y5$(111)=STR(T[T,6]:Z0$),Y5$(125)=STR(A9:Z5$)
7060 IF T<2 THEN GOSUB 7310; GOSUB 7310 ELSE GOSUB 7300; GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7310; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7310
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600),T4=MOD(T3,60),T3=INT(T3/60)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")+":"+STR(T4:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7310
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7305 IF F9$="Y" THEN Y5$(1)="",W=1; GOTO 7395
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7390 IF W>W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     13O ZZPARM  "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7460 D9=34; IF V0$(86,1)="Y" THEN D9=48
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1)
7481 IF V0=1 OR V0=2 THEN V1$="ARU   "
7484 IF V0$(86,1)="Y" THEN D0$="Invoiced " ELSE D0$="Ordered "
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7486 D0$=D0$+" from "; IF V0$(72,6)="      " THEN D0$=D0$+"Start" ELSE CALL "ZZDISP","DX",V0$(72,6),"",X3$,X$,"",0,0,X4$; D0$=D0$+X$
7487 IF V0$(78,1)=$7E$ THEN X$="Last" ELSE CALL "ZZDISP","DX",V0$(78,6),"",X3$,X$,"",0,0,X4$
7488 D0$=D0$+" thru "+X$
7494 X1$=X1$+" "+D0$
7495 RETURN 
7500 REM "Read in inventory or custom item data save into E0$, E0=1 means not found
7501 REM "E0$=prod code,left-rght,top-bot,plys,desc.; if product code blank then set it
7502 REM "Set E1$=unit of meas., E1=qty/unit
7504 E0=0; DIM E0$(23)
7505 ON POS(A$(11+K9,1)="BDFHJM") GOTO 7590,7550,7510,7515,7600,7570,7600
7510 C0$=A$(1+K9,10); GOTO 7520
7515 DIM C0$(10)
7520 DIM E$(139),E[20]; FIND (Z[9],KEY=C0$+A$(12+K9,10),DOM=7555)IOL=0350; GOTO 7525
7521 E0=1
7525 E0$=E$(61,21)+E$(21,40),E1$=E$(120,4),E1=E[14]
7545 GOTO 7590
7550 REM "Custom item case
7555 DIM E$(167),E[18]
7560 FIND (Z[6],KEY=A$(1+K9,10)+A$(12+K9,10),DOM=7561)IOL=0351; GOTO 7565
7561 E0=1
7565 E0$=E$(21,61),E1$=E$(100,4),E1=E[4]
7569 GOTO 7590
7570 REM "Special stuff
7575 DIM E$(60),E0$(61),E[20]
7580 FIND (Z[10],KEY="X"+A$(12+K9,10),DOM=7581)IOL=0353; GOTO 7582
7581 E0=1
7585 E0$(1,3)=E$(52,3),E0$(22)=E$(12,40),E1$=E$(55,4),E1=E[0]
7590 RETURN 
7600 REM "Read in non stock info from FS9 and put into E0$
7610 DIM E$(81),E[20]
7620 FIND (Z[11],KEY=A$(22+K9,8)+A$(12+K9,10)+A$(30+K9,1),DOM=7621)IOL=0352; GOTO 7625
7621 E0=1
7630 E0$=E$(20,61),E1=0; DIM E1$(4)
7640 GOTO 7590
7800 REM "Create M0$ unit of measure string set M0
7810 DIM M$(20); FIND (Z[13],KEY="U/M"+A$(101,4),DOM=7811)IOL=0390
7815 IF M$(20,1)="Y" THEN M0=M[0] ELSE M0=A[2]
7817 IF M$(20,1)="Y" AND M[0]=0 THEN M0=M[1]
7820 IF M0=0 THEN M0=1,M0$=""; GOTO 7840
7830 IF M$(20,1)<>"Y" THEN M0$=FNS$(A$(101,4))+"/"+FNR$(STR(M0:Z3$)) ELSE IF M[0]=0 THEN M0$=FNS$(A$(101,4)) ELSE M0$="",M0=1
7840 RETURN 
8000 REM "Build desc from A$
8010 H9$=FNS$(E0$(4,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>E0$(12,8))<>0 THEN H9$=H9$+" x "
8020 H9$=H9$+FNS$(E0$(12,8)); IF H9$<>"" THEN IF POS(" "<>E0$(12,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
8030 IF NUM(E0$(20,2))<>0 THEN H9$=H9$+E0$(20,2)+" Ply "
8050 RETURN 
8100 REM "Read for customer/order sort
8105 J7$=U$
8110 READ (Z[8],KEY=U$(5,10)+V2$(29,8),DOM=8111)
8115 U8$=KEY(Z[8],END=1000); READ (Z[8])
8120 IF U8$(1,10)<>J7$(5,10) THEN GOTO 1000
8125 IF U8$(11,8)>V2$(37,8) THEN GOTO 1000
8130 U$=U8$(1,10)+U8$(19,1)+U8$(20,10)+U8$(11,8)+U8$(30,1)
8135 U9$=J7$+U$(22,8)
8140 READ (Z[1],KEY=U$,DOM=8115)IOL=0310
8142 READ (Z[4],KEY=A$(1,10),DOM=*NEXT)CUST$; GOSUB VALIDATE_REP; IF NOT(VAL_REP) THEN GOTO 1000
8145 GOTO 1200
8150 REM "Read for customer/item sort
8155 J7$=U$
8160 EXTRACT (Z[1],KEY=U$(5,10)+V2$(29,1)+V2$(31,10),DOM=8161)
8165 U8$=KEY(Z[1],END=1000)
8170 IF U8$(1,10)<>J7$(5,10) THEN GOTO 1000
8174 IF U8$(11,1)>V2$(30,1) THEN GOTO 1000
8175 IF U8$(12,10)<V2$(31,10) THEN READ (Z[1],KEY=U8$(1,21)+$FF$,DOM=8165); GOTO 8165; REM "This is a low part of the key,now we must check start as well as stop
8176 IF U8$(12,10)>V2$(41,10) THEN READ (Z[1],KEY=U8$(1,21)+$FF$,DOM=8165); GOTO 8165
8180 U$=U8$
8185 U9$=J7$+U$(12,10)
8190 READ (Z[1],KEY=U$,DOM=8165)IOL=0310
8191 IF %X3$(333,2)<>"  " AND A$(22,2)<>%X3$(333,2) THEN GOTO 8165 ! SSP 221450
8192 READ (Z[4],KEY=A$(1,10),DOM=*NEXT)CUST$; GOSUB VALIDATE_REP; IF NOT(VAL_REP) THEN GOTO 1000
8195 GOTO 1200
8200 REM "Read for product code sort
8205 U9$=U$
8210 U$=U9$(15,10)+U9$(4,1)+U9$(5,10)+U9$(25,8)+U9$(33,1)
8220 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
8225 IF %X3$(333,2)<>"  " AND A$(22,2)<>%X3$(333,2) THEN GOTO 1000 ! SSP 221450
8230 READ (Z[4],KEY=A$(1,10),DOM=*NEXT)CUST$; GOSUB VALIDATE_REP; IF NOT(VAL_REP) THEN GOTO 1000
8240 GOTO 1200
8300 SHIP_DETAIL:REM "FROM FILE ASJ
8310 READ (Z[14],KEY=A$(1,10)+A$(22,8),DOM=8311)
8320 READ (Z[14],END=END_SHIP_DETAIL)ASJ$,ASJ
8330 IF ASJ$(1,18)<>A$(1,10)+A$(22,8) THEN GOTO END_SHIP_DETAIL
8332 IF ASJ$(19,1)<>A$(30,1) OR ASJ$(29,10)<>A$(12,10) THEN GOTO 8320
8335 IF W+3>W0 THEN GOSUB 6000
8340 Y5$(2)=ASJ$(40,5),Y5$(8)=ASJ$(45,35); IF FNS$(ASJ$(80,30))>"" THEN Y5$(45)="ATTN: "+ASJ$(80,30)
8345 Y5$(79)="Qty: "+STR(ASJ:Z4$); GOSUB 7300
8350 Y5$(8)=ASJ$(110,30),Y5$(45)=ASJ$(140,30),Y5$(76)=ASJ$(170,16),Y5$(93)=ASJ$(186,2)
8355 CALL "ZZDISP","AX",ASJ$(188,9),"ZIP",X3$,X$,"",0,0,X4$; Y5$(96)=X$
8360 GOSUB 7300
8380 GOTO 8320
8390 END_SHIP_DETAIL:RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 IF Y5=68 THEN RETRY ELSE ON C9 GOTO 1140,2040
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF NOT(NUL(Y4$)) THEN RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
10000 VALIDATE_REP:
10010 VAL_REP=0
10020 IF CUST$(278,4)<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,X3$,CUST$(278,4),OK; IF OK THEN VAL_REP=1; GOTO *RETURN
10030 IF CUST$(282,4)<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,X3$,CUST$(282,4),OK; IF OK THEN VAL_REP=1; GOTO *RETURN
10040 IF CUST$(286,4)<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,X3$,CUST$(286,4),OK; IF OK THEN VAL_REP=1; GOTO *RETURN
10050 IF CUST$(290,4)<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,X3$,CUST$(290,4),OK; IF OK THEN VAL_REP=1; GOTO *RETURN
10060 IF CUST$(294,4)<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,X3$,CUST$(294,4),OK; IF OK THEN VAL_REP=1; GOTO *RETURN
10080 IF CUST$(278,20)=DIM(20) THEN VAL_REP=1
10090 RETURN 
10098 ! 
10099 ! 
56000 REM "191822-Work order to allow sales reps to only see their own        
56002 REM "193458-Customers not printing ONACCTS on the open invoice          
56010 ! "221450-if you restrict operator to a division you cannot exit out  
