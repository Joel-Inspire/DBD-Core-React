0010 REM "Custom Item Delete Routine <FM2UAB>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 08/21/14 - 10.305555 - dmm - SSP# 271844
0037 REM "271844-Custom Item Delete routine should bypass M and S type open  
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$,Q2$,Q3$
0100 SETERR 9000
0110 X0$="FM2UAB",X1$="Custom Item Delete Routine",K9$="",K9=0
0120 DIM Z0$(80,"-"),Y[4],F[6],K[14]
0130 SAVE_XI=PRM('XI'); SET_PARAM 'XI'
0160 DEF FNS$(Z9$)=Z9$(1,POS("  "=Z9$+"  ")-1)
0200 REM "
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0300 REM "IOLISTS
0310 IOLIST A$
0320 IOLIST B$
0360 IOLIST F$,F[0],F[1],F[2],F[3],F[4],F[5],F[6]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01ORFM1...  02OSFS1...  03O FS2...  04OSFM2...  05OSFM3...  06O FM4...  07O FMS...  08O FMU...  09O FMV...  10O FM5...  11O FT1...  " ! SSP#271144
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0585 RT_PARM$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,RT_PARM$
0600 READ (Z[13],KEY="STAT"+"F/M")S9$
1000 REM "
1020 IF %GUI THEN PRINT 'MESSAGE'("Checking item for activity",0), ELSE PRINT @(25,22),"Checking item for activity...", ! ssp 188540
1100 REM "Open orders?
1105 READ (Z[1],KEY=Q1$(1,20),DOM=5000)F$
1110 READ (Z[2],KEY="",DOM=1111)
1120 READ (Z[2],END=2000)IOL=0310
1125 IF %GUI THEN PRINT 'MESSAGE'("Customer :"+FN%ZZDISP$(F$(1,10),"A/R")+" Item:"+F$(11,10)+" Order: "+FN%ZZDISP$(A$(118,8),"O/P")), ELSE PRINT @(0,23),'CL',@(12),"Customer: ",; CALL "ZZDISP","A",F$(1,10),"A/R",X3$,"","",26,23,X4$; PRINT @(37,23),"Item: ",F$(11,10)," Order: ",A$(118,8)
1130 IF A$(6,10)<>F$(1,10) THEN GOTO 1120
1200 REM "Order line detail
1210 READ (Z[3],KEY=A$(118,8),DOM=1211)
1215 READ (Z[3],END=1290)IOL=0320
1220 IF B$(147,8)<>A$(118,8) THEN GOTO 1290
1225 IF POS(B$(155,1)="MS")>0 THEN GOTO 1215 ! SSP271844, if M or S type line then not a match
1240 IF B$(19,10)=F$(11,10) THEN GOTO 5000
1250 GOTO 1215 ! SSP271844, get next order line for this order
1290 GOTO 1120
2000 REM "Delete Custom Item Information
2001 IF %GUI THEN PRINT 'MESSAGE'("Deleting all item data..",0), ELSE PRINT @(0,22),'CE',@(12,22),"Deleting all item data..."
2005 IF Q3$<>"P" THEN GOTO 2100
2010 IF %GUI THEN PRINT 'MESSAGE'("Custom item No: "+Q1$(11,10),0) ELSE PRINT @(0,19),'CL',@(12),"Custom Item No:  ",@(29),Q1$(11,10),
2100 REM "Product locator file
2110 REMOVE (Z[4],KEY=F$(21,19)+F$(40,2)+F$(11,10)+F$(1,10),DOM=2111)
2114 REM "Customer's Item Code
2115 REMOVE (Z[11],KEY=F$(1,10)+F$(350,20)+F$(11,10),DOM=2116)
2120 REM "Item spec - comp
2130 REMOVE (Z[7],KEY=F$(1,20),DOM=2131)
2140 REM "Item spec - ply
2150 READ (Z[8],KEY=F$(1,20),DOM=2151)
2160 K$=KEY(Z[8],END=2200)
2170 READ (Z[8])
2180 IF K$(1,20)<>F$(1,20) THEN GOTO 2200
2190 REMOVE (Z[8],KEY=K$)
2195 GOTO 2160
2200 REM "Item spec - comment
2210 READ (Z[9],KEY=F$(1,20),DOM=2211)
2220 K$=KEY(Z[9],END=2270)
2230 READ (Z[9])
2240 IF K$(1,20)<>F$(1,20) THEN GOTO 2270
2250 REMOVE (Z[9],KEY=K$)
2260 GOTO 2220
2270 REM "Reorder notice
2280 REMOVE (Z[10],KEY=F$(1,20),DOM=2281)
2300 REM "Item by location
2310 READ (Z[6],KEY=F$(1,20),DOM=2311)
2320 K$=KEY(Z[6],END=2400)
2330 IF K$(1,20)<>F$(1,20) THEN GOTO 2400
2340 REMOVE (Z[6],KEY=K$); IF RT_PARM$<>"" THEN CUST$=K$(1,10); CALL "RT2WOC",ERR=2341,X3$,X4$,CUST$,"FM4...","D",K$
2350 GOTO 2320
2400 REM "Combine Stats
2430 READ (Z[5],KEY=F$(1,20),DOM=2431)
2440 K$=KEY(Z[5],END=2600)
2445 IF K$(1,20)<>F$(1,20) THEN GOTO 2600
2446 GOTO 2545
2450 Q1$=K$(1,28),Q0$=K$(29,1),P9$=Q1$+Q0$
2460 DIM K$(14)
2470 GOSUB 7100
2545 REMOVE (Z[5],KEY=K$)
2546 IF RT_PARM$<>"" THEN CUST$=K$(1,10); CALL "RT2WOC",ERR=2547,X3$,X4$,CUST$,"FM3...","D",K$
2547 READ (Z[5],KEY=K$,DOM=2548)
2550 GOTO 2440
2600 REM "History
2603 Z$="02C FS1...  03C FS2...  04C FM2...  05C FM3...  06C FM4...  07C FMS...  "
2605 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 2606,9900
2607 Z$="02O FS6...  03O FS7...  04O FS8...  05O ASJ...  06O ASC...  07O FSY...  "
2610 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 2611,9900
2611 REM LET Z8$=FID(Z(2))
2612 REM PRINT Z8$(4,6)
2700 REM "Order History
2710 T9$="BD"
2715 FOR I=1 TO LEN(T9$)
2720 READ (Z[2],KEY=F$(1,10)+T9$(I,1)+F$(11,10),DOM=2721)
2730 K$=KEY(Z[2],END=2990); READ (Z[2])C$
2740 IF K$(1,21)<>F$(1,10)+T9$(I,1)+F$(11,10) THEN GOTO 2990
2750 REMOVE (Z[2],KEY=K$)
2751 IF RT_PARM$<>"" THEN CUST$=K$(1,10); CALL "RT2WOC",ERR=2752,X3$,X4$,CUST$,"FS6...","D",K$
2760 REM "Sorts
2770 REMOVE (Z[3],KEY=C$(31,3)+C$(11,1)+C$(12,10)+C$(1,10)+C$(22,8)+C$(30,1),DOM=2771)
2780 REMOVE (Z[4],KEY=C$(1,10)+C$(22,8)+C$(11,1)+C$(12,10)+C$(30,1),DOM=2781)
2790 X$=C$(1,10)+C$(22,9); READ (Z[5],KEY=X$,DOM=2791)
2800 K$=KEY(Z[5],END=2850); READ (Z[5])ASJ$
2810 IF K$(1,LEN(X$))<>X$ THEN GOTO 2850
2815 IF ASJ$(29,10)<>C$(12,10) THEN GOTO 2800
2820 REMOVE (Z[5],KEY=K$)
2830 GOTO 2800
2850 REM "Change Z[5] from FS9 to ASJ, add ASC and FSY, SSP151214
2860 REMOVE (Z[6],KEY=C$(1,10)+C$(54,15)+C$(11,1)+C$(12,10)+C$(22,8)+C$(30,1),DOM=2861)
2870 F9$=C$(48,6)
2875 CALL "ZZDATE",X3$,"","G10101",F9$,D0,1,0,0,0,0,0,"","",""
2880 Q$=C$(1,10)+F9$+C$(12,10)+C$(11,1)+C$(22,8)+C$(30,1)
2885 Q$(11,6)=STR(100000-D0:"000000")
2890 REMOVE (Z[7],KEY=Q$,DOM=2891)
2950 REM 
2960 GOTO 2730
2990 NEXT I
3000 REM "Add additional files to routine, SSP125807
3003 Z$="02C FS6...  03C FS7...  04C FS8...  05C ASJ...  06C ASC...  07C FSY...  08C FMU...  09C FMV...  10C FM5...  11C FT1...  "
3005 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 3006,9900
3007 Z$="02O FMC...  03O FMD...  04O FMI...  05O FST...  06O AS7...  07O KT0...  08O FTL...  09O FSW...  10O FSF...  11O FT0...  12O FS0...  14O FTM...  15O PP3...  16O ZZO...  "
3010 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 3011,9900
3020 FOR X=2 TO 9
3025 READ (Z[X],KEY=F$(1,20),DOM=3026)
3030 K$=KEY(Z[X],END=3050); READ (Z[X])REC$
3035 IF K$(1,20)<>F$(1,20) THEN GOTO 3050
3040 REMOVE (Z[X],KEY=K$); IF X=8 THEN REMOVE (Z[14],KEY=K$(21,4)+K$(1,20),DOM=3041)
3045 GOTO 3030
3050 NEXT X
3060 REMOVE (Z[10],KEY=F$(1,20),DOM=3061)
3070 REMOVE (Z[11],KEY=F$(1,20),DOM=3071)
3080 REMOVE (Z[12],KEY=F$(1,20),DOM=3081)
3090 READ (Z[7],KEY="",DOM=3091); REM "Read through KIT file for components
3092 K$=KEY(Z[7],END=3095); READ (Z[7],KEY=K$)KT0$
3093 IF LEN(K$)<40 THEN REMOVE (Z[7],KEY=K$,DOM=3092); GOTO 3092 ! SSP#225134
3094 IF K$(21,20)=F$(1,20) THEN REMOVE (Z[7],KEY=K$) ELSE GOTO 3092
3095 REMOVE (Z[15],KEY=F$(1,20),DOM=*NEXT) ! WO171176
3097 READ (Z[16],KEY="003"+F$(1,20)+DIM(30),DOM=*NEXT)
3098 ZZO_KEY$=KEY(Z[16],END=3104); READ (Z[16])
3099 IF ZZO_KEY$(1,23)<>"003"+F$(1,20) THEN GOTO 3104
3100 REMOVE (Z[16],KEY=ZZO_KEY$)
3101 GOTO 3098
3104 ! If WebEC is present, then open ECR and remove
3105 CALL "ZZ2PRP","EC",RESULT$,DATE$; IF RESULT$<>"Y" THEN GOTO 3125
3110 Z$="17O ECR...  18O IC0...  " ! SSP 217512
3115 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 3116,9900
3119 FIND (Z[18],KEY=F$(1,20),DOM=*NEXT); GOTO 3121 ! SSP 217512 - If IC0 exists, do not remove ECR
3120 REMOVE (Z[17],KEY=F$(1,20),DOM=*NEXT)
4090 REMOVE (Z[1],KEY=F$(1,20),DOM=4091)
4091 IF RT_PARM$<>"" THEN CUST$=F$(1,10); CALL "RT2WOC",ERR=4092,X3$,X4$,CUST$,"FM1...","D",F$(1,20)
4100 REM "Deleted Return to Overlay
4110 Q2$="Y"
4140 GOTO 9900
5000 REM "No Delete Return to Overlay
5010 Q2$="N"
5090 GOTO 9900
5900 REM "REMOVE LINES
5910 IF A0=0 THEN GOTO 5990
5920 EXTRACT (F,IND=0,ERR=5960)IOL=0320
5925 IF Y[4]=-2 THEN READ (F,IND=0); WAIT 0; GOTO 5920 ELSE Y[4]=-2; WRITE (F,IND=0)IOL=0320
5930 A1=Y[1],Y[1]=A0,Y[0]=Y[0]-1
5935 READ (F,IND=A0,ERR=5940)A; IF A>0 THEN Y[0]=Y[0]-1,A0=A; GOTO 5935
5940 WRITE (F,IND=A0)A1
5950 Y[4]=-1; WRITE (F,IND=0)IOL=0320
5955 GOTO 5990
5960 IF ERR=0 THEN RETRY ELSE GOTO 9000
5990 RETURN 
7000 REM "Update stats
7010 DIM K[14]; P9$=Q1$+Q0$
7020 GOSUB 7100
7030 FOR Q1=0 TO 13
7040 K[Q1]=K[Q1]+J[Q1]
7050 NEXT Q1
7060 GOSUB 7150
7090 RETURN 
7100 REM "Read stats (Packed/Unpacked)
7105 Q=POS(Q0$=S9$(49),17); IF Q=0 THEN EXITTO 7090 ELSE M0$=S9$(Q+48,17)
7110 CALL "ZZPACK",X3$,"E",M0$(3,2),"",0,0,K{ALL},Z[5],Q0$,Q1$,S9$
7135 GOTO 7190
7150 REM "Write stats
7160 CALL "ZZPACK",X3$,"W",M0$(3,2),"",0,0,K{ALL},Z[5],Q0$,Q1$,S9$
7190 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 9900
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9905 SET_PARAM 'XI'=SAVE_XI
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 ! "188540-Program FMGUPA - Period End for Order Processing. Need to   
56010 REM "152118-When you delete a custom form, it does not delete the 
56011 REM "217512-Delete an item from inventory it deletes from buyer catalog  
56012 REM "271144-Error 14 during OP monthend                                 
56014 REM "271844-Custom Item Delete routine should bypass M and S type open  
