0010 REM "Physical Count Sheets <FM2PRA>
0035 REM "5.7 - 01/06/12 - 9.366666 - tma - SSP# 251631
0036 REM "MOD 6/22 TO USE FROZEN FM4 DATA
0037 REM "251631-Changes to Count Sheet printing in cycle processing         
0040 REM "Copyright 2012 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="FM2PRA",X1$="Items Management Count Sheets" ! ssp#251631
0120 DIM A$(60),A[10],B$(46),B[15],C$(640),D$(448),D[31],S$(50),E$(21),E[1],J1$(46),I[20],F$(45)
0140 Z0$="-###,##0",Z1$="-###,##0"
0142 D0$="_________"
0210 T=1,T0=1,T1=2
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR FM6 - Physical Count Data Entry
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10]
0315 REM " IOLIST FOR FM4 - Item/Location Master
0320 IOLIST B$,B9$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15]
0325 REM "IOLIST FOR FM0 - Warehouse Locations
0330 IOLIST C$
0335 REM "IOLIST FOR FM1 - Item Masterfile
0340 IOLIST D$,D[0],D[1],D[2],D[3],D[4],D[5],D[6],D[7],D[8],D[9],D[10],D[11],D[12],D[13],D[14],D[15],D[16],D[17],D[18],D[19],D[20]
0350 IOLIST E$,E[0],E[1]
0360 IOLIST F$
0380 IOLIST I$,I[0],I[1],I[2],I[3],I[4],I[5],I[6],I[7],I[8],I[9],I[10],I[11],I[12]
0400 IOLIST J1$
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O FM6...  02O FM4...  03O FM0...  04O FM1...  05S AR1...  06O FM8... 07O FMP...  08O FS6...  09O FS2...  10O FST...  11O FS1...  13O ZZPARM  "; IF V1$(1,3)<>Y$(5,3) THEN Y$=Y$+"02O "+V1$+"  " ! SSP#229934
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0700 REM "Open Printer
0720 REM 
0750 IF POS("F"=V0$(59,12))<>0 THEN DIM OCC_FILES[26]; REM "File output for 449
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(14)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[5]; U=41,U0=5,U[0]=0,U[1]=10,U[2]=4,U[3]=16,U[4]=10,T0=2,V1=10,V2=4,U0$="YYY  ",T5$="                "; GOTO 0850
0821 V3=16
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0895 IF POS("F"=V0$(59,12))=0 THEN CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN READ (U1,END=5000); EXITTO 1000
1030 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U0=1 THEN EXITTO 5000 ELSE READ (U1,END=5000); EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1154 REM "CHECK TO SEE IF ISSUES BASED FORM
1155 IF A$(82,1)="2" THEN GOTO 1000
1300 REM "Read other data
1320 READ (Z[2],KEY=A$(1,10)+A$(31,10)+A$(11,4),DOM=1321)IOL=0320
1340 READ (Z[4],KEY=A$(1,10)+A$(31,10),DOM=1341)IOL=0340
1345 IF F$(1,14)<>A$(1,14) THEN FIND (Z[6],KEY=A$(1,14))IOL=0360
1348 IF V0$(72,1)="1" THEN IF F$(27,1)>"1" THEN READ (U1,KEY=F$(1,14)+$FF$,DOM=1000)
1350 H8$=D$
1400 REM "Subtotals"
1415 GOSUB 3100
1420 ON T GOTO 1470,1430,1440,1450,1460
1430 GOSUB 6300
1490 T=0
1500 REM "Print Data
1503 IF POS("F"=V0$(59,12))<>0 THEN GOSUB 7700; GOTO 1995
1505 IF W+2>W0 THEN GOSUB 6000
1550 IF K$<>"" THEN IF U$(1,LEN(U$)-1)=K$ THEN GOTO 1570
1570 GOSUB 8000; Y5$(1)=A$(31,10),Y5$(12)=H9$
1575 Y5$(46)=" "+FNS$(A$(42,4))+"/"+FNR$(STR(A[2]:Z0$)),Y5$(62)="__________ "
1576 IF POS(X3$(9,3)="303")=0 AND A$(42,4)="M   " THEN Y5$(47,10)=""
1578 IF A$(50,4)<>S$(1,4) THEN Y5$(75)=A$(50,4)+"/"+FNR$(STR(A[7]:Z0$))+"  "+D0$ ELSE Y5$(75)=D0$+" "+D0$
1580 IF A$(54,4)<>S$(1,4) THEN Y5$(95)=A$(54,4)+"/"+FNR$(STR(A[8]:Z0$))+" "+D0$ ELSE Y5$(95)="Broken Pk "+" "+D0$
1600 IF V0$(73,1)<>"1" THEN GOTO 1620
1605 Q=B[3]+B[6]+B[7],Q$=""; IF B[6]<>0 OR B[7]<>0 THEN Q$="*"
1610 Y5$(115)=STR(Q:Z1$)+Q$
1615 Y5$(124)=STR(B[0]:Z0$)
1618 GOSUB 7300; REM "Added ssp92059
1620 GOSUB 7500
1660 IF V0$(73,1)<>"1" THEN GOSUB 7300; REM "Added IF ssp92059
1670 IF V0$(75,1)="Y" THEN GOSUB 7600
1680 GOSUB 7300
1700 K$=U$(1,LEN(U$)-1)
1900 REM "Accumulate Totals"
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3115 T9$="END"
3120 GOSUB 3220
3125 T9$=""
3130 IF U1$="" THEN GOTO 3160
3140 T=1,T$=U5$,T6$=U1$; GOSUB 3050; GOSUB 7000
3150 U1$=U9$(1,V1); GOSUB 6300
3155 IF U0$(1,1)="Y" AND T0$<>"END" THEN GOSUB 6000
3160 U1$=U9$(1,V1); GOSUB 6300
3180 RETURN 
3200 REM "Next highest break
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3270
3240 T=2,T$=U5$,T6$=U2$; GOSUB 3050; GOSUB 7000
3250 U2$=U9$(V5,V2); GOSUB 6400
3255 IF T9$="END" THEN RETURN 
3260 IF U0$(2,1)="Y" THEN GOSUB 6000
3270 U2$=U9$(V5,V2); GOSUB 6400
3280 RETURN 
3300 REM "COUNT STOP CHANGE
3310 IF U3$="" THEN GOTO 3370 ELSE IF U$(V6,3)=U3$ THEN GOTO 3380
3315 GOSUB 7300; GOSUB 7300
3320 GOSUB 6500
3325 IF POS(X3$(9,3)="057137500",3)=0 THEN W=999
3370 U3$=U$(V6,3)
3380 RETURN 
3480 RETURN 
5000 REM "End of Print
5005 IF POS("F"=V0$(59,12))<>0 THEN GOSUB 7800
5010 GOTO 5200
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 GOSUB 7200
5200 REM "
5290 GOTO 9900
6000 REM "Page header
6005 IF POS("F"=V0$(59,12))<>0 THEN GOTO 6090
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6090 ELSE ON W5 GOTO 6041,9910
6045 ON W5 GOTO 6046,9900
6047 GOSUB 7300; Y5$(31)="Counted by: ___________   Date: ____________"; GOSUB 7300; GOSUB 7300
6050 GOSUB 6200
6055 GOSUB 6100
6070 J$=A$(1,4)
6090 RETURN 
6100 REM "
6125 GOSUB 7300
6199 RETURN 
6200 REM "Column titles"
6210 Y5$(77)="+----- ALTERNATE UNITS OF ISSUE -----+"
6215 IF V0$(73,1)="1" THEN Y5$(118)="Last ct   Avg"
6220 GOSUB 7300
6230 Y5$(1)="Item code",Y5$(13)="Description",Y5$(48)="Pack",Y5$(53)="U/M",Y5$(64)="Count",Y5$(74)="Alt 1 pack",Y5$(87)="Count"
6232 Y5$(95)="Alt 2 Pack   Count"
6233 IF V0$(73)="1" THEN Y5$(118)="+ Shpmts  Usg"
6235 GOSUB 7300
6249 RETURN 
6300 REM "Customer header
6305 IF POS("F"=V0$(59,12))<>0 THEN GOSUB 7800; REM "Doing OCC, move files etc
6310 DIM J1$(46); FIND (Z[5],KEY=B$(1,10),DOM=6311)IOL=0400
6320 W0$=FNS$(J1$(11,35))
6330 CALL "ZZDISP","AX",J1$(1,10),"A/R",X3$,Q$,"",0,0,X4$
6335 W0$=W0$+" ("+Q$+")"
6340 IF W8<>0 THEN W8=.1
6390 RETURN 
6400 REM "Location header
6410 DIM C$(LEN(C$)); FIND (Z[3],KEY="C"+A$(1,10)+U2$,DOM=6411)IOL=0330
6415 I0=NUM(C$(246,2))
6417 DIM H$(120); EXTRACT (Z[6],KEY=C$(2,14))H$; IF V3$(2,1)<>"T" THEN H$(27,1)="2"; WRITE (Z[6],KEY=C$(2,14))H$ ELSE READ (Z[6])
6420 W1$=FNS$(C$(16,35)); IF POS(" "<>C$(111,16))<>0 THEN W1$=W1$+" - "+FNS$(C$(111,16))+", "+C$(127,2)
6440 W1$="Location: "+A$(11,4)+" "+W1$
6442 IF H$(35,1)<>"I" THEN W1$=W1$+" - Frozen as of: "+FND$(H$(15,6)) ELSE W1$=W1$+" - Interim on "+FND$(H$(15,6))
6446 IF A$(11,4)="    " THEN W1$="Main "+W1$
6455 GOSUB 6500
6490 RETURN 
6500 REM "Get count stop
6505 W2$=""; IF U$(15,16)=S$(1,16) THEN GOTO 6540
6510 W2$="Count Stop: "+U$(15,3)
6515 FIND (Z[7],KEY="d"+U$(1,14)+U$(15,3),DOM=6516)V8$; W2$=W2$+" "+FNS$(V8$(19,30))
6540 RETURN 
6900 REM "SORT
6905 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6940 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,X9
6990 RETURN 
7000 REM "Totals logic
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600),T4=MOD(T3,60),T3=INT(T3/60)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")+":"+STR(T4:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7330 REM 
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7460 IF POS("B"=V0$(59,12))<>0 THEN RUN "FM2PRB"
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"  ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "LOOK FOR OPEN ORDER
7505 Q9$="",Q9=0
7510 READ (Z[8],KEY=A$(1,10)+"B"+A$(31,10),DOM=7511)
7515 K$=KEY(Z[8],END=7580); READ (Z[8])IOL=0380
7518 IF I$(1,10)+I$(12,10)<>A$(1,10)+A$(31,10) THEN GOTO 7590 ELSE IF I$(11,1)<>"B" THEN GOTO 7515
7520 FIND (Z[9],KEY=I$(22,8)+STR(ASC(I$(30,1))-48:"000"),DOM=7515)I1$
7522 FIND (Z[11],KEY=I$(22,8),DOM=7515)ORD_HDR$
7525 IF ORD_HDR$(6,10)+I1$(19,10)<>A$(1,10)+A$(31,10) THEN GOTO 7515
7530 Q9$=FND$(I$(34,6)); Q9$=Q9$(1,3)+Q9$(9,2),Q9=I[0]
7550 Y5$(116)=STR(Q9:Z1$),Y5$(127)=Q9$
7595 RETURN 
7600 REM "Look for Count Stop's Bin Location in FST
7610 FIND (Z[10],KEY=U$(1,10)+A$(31,10)+U$(11,7),DOM=7690)X$
7620 IF POS(" "<>X$(28,16))>0 THEN Y5$(14)="Bin location: "+X$(28,16)
7690 RETURN 
7700 REM "Build OCC file instead of printing--449
7702 PRINT @(12,10),"Creating record: ",J1$(411,6),"  ",C$(382,10),"  ",A$(31,10),'CL',
7703 IF C$(375,1)=" " THEN GOTO 7790; REM "This location is not OCC
7705 DIM OCC$(70)
7710 OCC$(1,6)=J1$(411,6),OCC$(7,10)=C$(382,10); REM "1st 6 of alt cust code,1st 10 of receiving dept
7715 REM "DIM COUNT_STOP$(49); FIND (Z[10],KEY=A$(1,10)+A$(31,10)+A$(11,4)+"   ",DOM=07716) COUNT_STOP$
7720 REM "LET OCC$(17,8)=COUNT_STOP$(28,8); REM "1ST 8 OF BIN NUMBER
7721 OCC$(17,8)=A$(18,8); REM "1st 8 of bin location
7725 IF STP(D$(350,10),3," ")="" THEN OCC$(25,10)=A$(31,10) ELSE OCC$(25,10)=D$(350,10); REM "If there is a CIC use 1st 10 of that otherwise use item code
7728 OCC$(35,30)=D$(42,30); REM "1st 30 of item description
7730 DIM ISSUE_UNIT$(21); FIND (Z[13],KEY="U/M"+A$(42,4),DOM=7731)ISSUE_UNIT$
7735 OCC$(65,1)=ISSUE_UNIT$(8,1); REM "1st char of U/M description
7740 OCC$(66,5)="00000"; REM "Count will go here on the way back in
7750 ARRAY_NUM=ASC(C$(375,1),ERR=7790)-64
7755 IF OCC_FILES[ARRAY_NUM]=0 THEN GOSUB 7900
7760 WRITE (OCC_FILES[ARRAY_NUM])OCC$
7790 RETURN 
7800 REM "OCC--Customer break, move last customer's files, dim array for new cust
7810 FOR X=1 TO 26
7815 IF OCC_FILES[X]<>0 THEN GOSUB 7850
7820 NEXT X
7825 DIM OCC_FILES[26]
7840 RETURN 
7850 REM "OCC--close and re-name files using system call
7852 PRINT @(12,10),"Closing and re-naming files for customer: ",OCC$(1,6),'CL',
7855 CLOSE (OCC_FILES[X])
7860 OCC_FILE$="TW"+OCC$(1,1)+OCC$(3,2)+CHR(X+64)+".ASC"
7870 OCC_TMP$="OCC"+CHR(X+64)
7880 TF2D$=%DATAPATH$
7885 ERASE TF2D$+DLM+"occ_out"+DLM+OCC_FILE$,ERR=*PROCEED; RENAME TF2D$+DLM+OCC_TMP$ TO TF2D$+DLM+"occ_out"+DLM+OCC_FILE$
7890 RETURN 
7900 REM "OCC--Create and open text files
7902 PRINT @(12,10),"Creating and opening text files",'CL',
7905 OCC_TMP$="OCC"+CHR(ARRAY_NUM+64)
7907 ERASE OCC_TMP$,ERR=7908
7910 SERIAL OCC_TMP$,0,0
7915 SLOT=NUM(X3$(60,3))+ARRAY_NUM
7920 OCC_FILES[ARRAY_NUM]=SLOT
7925 OPEN LOCK (SLOT,OPT="TEXT")OCC_TMP$
7940 RETURN 
8000 REM "Build desc from H8$
8005 H9$=H8$(42,40); RETURN 
8010 H9$=FNS$(H8$(24,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>H8$(32,8))<>0 THEN H9$=H9$+"x"
8020 H9$=H9$+FNS$(H8$(32,8)); IF H9$<>"" THEN IF POS(" "<>H8$(32,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
8030 IF NUM(H8$(40,2))<>0 THEN H9$=H9$+H8$(40,2)+" Ply "
8040 H9$=H9$+FNS$(H8$(42,40)); H8$=""
8050 RETURN 
8900 REM "Functions
8910 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
8920 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
8930 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8940 DEF FNQ$(Z9,Z9$)=STR(Z9:Z9$)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 IF POS("F"=V0$(59,12))=0 THEN CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF NOT(NUL(Y4$)) THEN CALL Y4$,ERR=*NEXT ELSE RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
