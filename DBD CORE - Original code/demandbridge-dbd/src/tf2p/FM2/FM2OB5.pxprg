0010 REM "Sales order support <FM2OB5>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 08/24/15 - 13.586388 - jvv - SSP# 276772
0037 REM "276772-Multiple Finder's Fees per Customer/Item/Order Line.        
0040 REM "Copyright 2015 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0085 SETERR 9000
0088 GOSUB CHECK_MFF ! SSP 276772
0090 X0$="FM2OB5",C7=0
0091 IF L3=1 THEN L3=0,C0=0; GOSUB 6400; IF LEN(Q1$)>10 THEN J9$(1,10)=Q1$(11),A$(19,10)=J9$; CALL "ZZDISP","A",J9$,"ICE",X3$,"","",1,7,X4$; GOTO 5400 ELSE GOTO 5450
0092 CALL "ZZ2PRP","OS",RESULT$,DATE$; REM " IF RESULT$="Y" THEY HAVE OFFICE SUPPLY SYSTEM
0093 CALL "FS2DAK",X3$,X4$,0,B$(6,10),CIC$,"",""
0095 ON L2 GOTO 0600,2300,5800,5450,8200,0635
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0298 FROM$="FM2OB5"
0299 CALL "ZZ2PRP","OS",RESULT$,DATE$; REM " IF RESULT$="Y" THEY HAVE OFFICE SUPPLY SYSTEM
0300 REM "IOLISTS
0310 IOLIST A$(1,LEN(A$)),A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20],C[21],C[22],C[23],C[24],C[25],C[26],C[27],C[28],C[29],C[30],C[31]
0340 IOLIST D$
0350 IOLIST E$,E[0],E[1],E[2]
0351 IOLIST E1$
0390 IOLIST I$,I[0],I[1],I[2],I[3],I[4],I[5],I[6],I[7],I[8],I[9],I[10],I[11],I[12],I[13],I[14],I[15],I[16],I[17],I[18],I[19],I[20],I[21],I[22],I[23],I[24],I[25],I[26],I[27],I[28],I[29],I[30],I[31],I[32],I[33],I[34],I[35],I[36],I[37],I[38],I[39],I[40]
0410 IOLIST S5$,S[0],S[1]
0420 IOLIST T5$,T[0],T[1]
0500 REM 
0510 GOTO 1890
0600 REM "return from setup of lines
0605 GOSUB 8950
0610 A1$=Q1$
0620 L1=2,L2=5
0630 RUN "FM2ODB"
0635 X0=6,C9=1,Z8=-1,C0=0,Z9=1
0640 PRINT @(0,3),'CE',; GOSUB 6000; GOSUB 6200
0650 GOTO 5000
2040 REM "Go back to inventory stuff
2045 GOTO 5460
2300 REM "Enter custom item
2302 C8=0,C0$=B$(6,10),C7=5,N0=0
2303 IF POS(" "<>A$(19,10))<>0 THEN GOTO 2340; REM "SSP119416
2304 IF POS(" "<>A$(19,10))=0 THEN A9$="" ELSE A9$=A$(19,10)
2305 IF A$(2,1)="Y" AND POS(A$(155,1)="IC")<>0 THEN GOTO 2340; REM "JEL 3/10/98
2309 IF CIC$="Y" AND POS(" "<>A$(19,10))=0 THEN GOTO 4100
2310 X$="Enter Custom Item Code"; CALL "ZZENTR","SUX",A{ALL},A$,X4$,X3$,1,7,19,10,C0,"","{3"+X$,C0$,"FM2ODB12","FM1","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2310,2311
2312 IF ABS(C0)=2 OR A$(155,1)="N" THEN GOTO 2340 ELSE IF POS(" "<>A$(19,10))=0 THEN GOTO 2309
2315 IF A9$=A$(19,10) THEN IF A$(15,3)="   " OR B$(52,1)="Y" THEN GOTO 2340 ELSE CALL "FM2OBC",X3$,X4$,A$,A{ALL},B$; GOTO 2340
2320 GOTO 5000
2340 L0=3; GOTO 9900
2700 REM "Input customer specific #
2702 IF A$(2,1)="Y" AND POS(A$(155,1)="IC")<>0 THEN GOTO 2340
2703 IF POS(" "<>A$(19,10))<>0 THEN GOTO 2340; REM "SSP119416
2704 IF POS(" "<>A$(161,10))=0 THEN A$(161,10)=B$(6,10)
2705 FIND (Z[8],KEY=A$(161,10),DOM=2706)C0$; GOSUB 2750
2710 CALL "ZZENTR","AUX",A{ALL},A$,X4$,X3$,10,5,161,10,C0,"A/R","{3"+X$,"","FM2OB501","AR1","",""; IF ABS(C0)>4 THEN IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2710,2711
2720 FIND (Z[8],KEY=A$(161,10),DOM=2710)C0$
2725 GOSUB 2750
2740 IF ABS(C0)>1 THEN L0=3; GOTO 9900 ELSE GOTO 5457
2750 REM "Cust spec. display
2755 PRINT @(0,5),'CL','SB',"Cust. no:",@(25,5),"Name:",'SF',
2760 CALL "ZZDISP","A",A$(161,10),"A/R",X3$,"","",10,5,X4$
2765 FIND (Z[8],KEY=A$(161,10),DOM=2710)C0$
2770 PRINT @(31,5),C0$(11,35),
2790 RETURN 
3000 REM "Supplier item number search
3005 DIM I2$(34); C7=4,I0=-1
3010 GOSUB 3400
3020 X$="",C0=ABS(C0),I0=I0+1; IF C0=2 THEN IF I0>0 THEN I0=I0-2
3025 IF ABS(C0)=4 THEN GOTO 3550 ELSE IF INT(C0)=3 OR ABS(C0)>3 OR I0<0 THEN I0=999; GOTO 3040
3040 ON I0 GOTO 3150,3100,3200,3300
3100 REM "Plant
3105 J1$="P"+A$(94,10)
3110 CALL "ZZENTR","SUZX",A{ALL},I2$,X4$,X3$,26,21,11,4,C0,"","{3"+X$,J1$,"FM2ODB74","FM0P","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3110,3111
3115 IF I2$(11,4)="    " OR I2$(11,4)="0000" THEN GOTO 3140
3120 FIND (Z[9],KEY=J1$+I2$(11,4),DOM=3110)
3140 GOTO 3020
3150 REM "Vendor
3160 CALL "ZZENTR","AU",A{ALL},I2$,X4$,X3$,8,21,1,10,C0,"A/P","{3"+X$,"","FM2ODB76","AP4","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3160,3161
3170 IF ABS(C0)<>4 THEN FIND (Z[5],KEY=I2$(1,10),DOM=3160)
3180 IF C0>0 THEN CALL "AP2MAY",X3$,I2$(1,10),2,17,X$; IF X$="Y" THEN GOSUB 6400
3190 GOTO 3020
3200 REM "Supplier item number
3205 J1$=I2$(1,14)
3210 CALL "ZZENTR","S",A{ALL},I2$,X4$,X3$,49,21,15,20,C0,"","{3"+X$,J1$,"FM2ODB78","/FM2OBI","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3210,3211
3240 GOTO 3020
3300 REM "Lookup Item number"
3310 Z$="12C PO1...  12O ICD...  "
3320 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 3321,9900
3330 READ (Z[12],KEY=I2$,DOM=3331)
3334 I3$=KEY(Z[12],END=3370)
3335 IF I3$(1,LEN(I2$))<>I2$ THEN GOTO 3370
3340 J9$(1)=I3$(45)
3370 Z$="12C ICD...  12O PO1...  "
3375 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 3376,9900
3380 IF J9$(1)<>I3$(45) THEN GOTO 3200
3390 PRINT @(0,21),'CL','SB',@(7,21),"Bill now:",'SF',
3395 C7=3,C0=0; GOTO 5462
3400 REM "
3410 PRINT @(0,21),'CL','SB',@(0,21),"Vendor:",@(20,21),"Plant:",@(32,21),"Supplier Number:",'SF',
3415 CALL "ZZDISP","A",I2$(1,10),"A/P",X3$,"","",8,21,X4$
3416 PRINT @(26,21),I2$(11,4),@(49,21),I2$(15,20),
3420 RETURN 
3440 GOSUB 3400; GOTO 3040
3550 DIM I2$(LEN(I2$))
3560 IF POS(" "<>A$(19,10))<>0 THEN J9$=A$(19,10) ELSE DIM J9$(10)
3570 GOTO 3390
4000 REM "Customer Item Number Entry  - Stock Items
4005 X$="Press Enter to skip -or- Enter Customer's Item Code (Lookup available)"
4006 IF A$(155,1)="I" AND A$(9,1)<>DIM(1) AND RESULT$="Y" THEN X$="Enter to Skip or Enter CIC or F10 to Enter Office Supply Num"
4010 DIM TEMP$(20); CALL "ZZENTR","SU",A{ALL},TEMP$,X4$,X3$,1,7,1,20,C0,"","{3"+X$,B$(6,10),"FM2ODB15","FT1","",""; IF C0=ABS(7) THEN GOTO 6500 ELSE IF ABS(C0)>4 THEN GOSUB 6400; IF C0=5 THEN GOTO 4010 ELSE J9$=TEMP$(1,10); GOTO 5461
4015 IF POS(" "<>TEMP$)=0 THEN GOTO 5459
4020 CALL "FS2DAK",X3$,X4$,1,B$(6,10),"",TEMP$,TEMP1$
4025 IF POS(" "<>TEMP1$)=0 THEN GOTO 4010
4030 J9$=TEMP1$,C0=1
4040 GOTO 5461
4100 REM "Customer Item Number Entry  - Custom Items
4105 X$="Press Enter to skip -or- Enter Customer's Item Code (Lookup available)"
4110 DIM TEMP$(20); CALL "ZZENTR","SU",A{ALL},TEMP$,X4$,X3$,1,7,1,20,C0,"","{3"+X$,B$(6,10),"FM2ODB15","FT1","",""; IF ABS(C0)>4 THEN GOSUB 6400; IF C0=5 THEN GOTO 4110 ELSE A$(19,10)=TEMP$(1,10); GOTO 2311
4115 IF POS(" "<>TEMP$)=0 THEN X$=""; GOTO 2310
4120 CALL "FS2DAK",X3$,X4$,1,B$(6,10),"",TEMP$,TEMP1$
4125 IF POS(" "<>TEMP1$)=0 THEN GOTO 4110
4130 A$(19,10)=TEMP1$,C0=1
4140 GOTO 2311
5000 REM "Do forms modify/setup
5005 DIM C[31],C$(448)
5010 FIND (Z[3],KEY=B$(6,10)+A$(19,10),DOM=5040)IOL=0330
5020 GOSUB 7700
5021 CALL "AP2MAY",X3$,A$(94,10),2,17,X$; IF X$="Y" THEN GOSUB 6400
5022 IF P3$(243,1)="A" THEN Z=17; CALL "FS2DAJ",X3$,X4$,B$(6,10)+A$(19,10),0,Z; IF Z<>0 THEN GOSUB 6400
5023 CALL "ZZPROM","10",X3$,Z,"Modify item master before entering order data?","","",0; ON Z GOTO 5050,5024
5025 X0=X0+7
5030 L0=3; GOTO 9900
5040 CALL "ZZPROM","Y",X3$,Z,"Do you wish to set up this item?","","",0
5045 ON Z GOTO 5048,5047
5047 A$(19,10)=A9$,L0=4; PRINT @(1,7),A$(19,10),; GOTO 9900
5048 IF CIC$="Y" THEN USED$="",CIC_ITEM$="",TF_ITEM$=""; CALL "FS2DAK",X3$,X4$,2,B$(6,10)+A$(19,10),USED$,CIC_ITEM$,TF_ITEM$; IF USED$="Y" THEN CALL "ZZPROM",".4",X3$,0,STP(TF_ITEM$,1)+" is using "+STP(CIC_ITEM$,1)+" as it's CIC, you may not add this item code.","","",0; GOTO 2310
5049 IF A$(14,1)<>"N" THEN GOSUB 5200
5050 REM "Run forms setup
5060 U1$=X0$+STR(LEN(A1$):"00")+A1$+U1$
5065 U1$="FM2IAC20"+B$(6,10)+A$(19,10)+U1$
5070 U1$="FM2IAA"+"20"+B$(6,10)+A$(19,10)+U1$
5090 Q2$=U1$(1,6); GOTO 5900
5200 REM "Transfer from another customer?"
5204 DIM N$(20),N0$(100),C$(448),C[31]
5205 GOSUB 5390
5206 REM "Customer Code
5207 C7=1,N0=1
5210 CALL "ZZENTR","AXU",A{ALL},N$,X4$,X3$,30,12,1,10,C0,"A/R","{3"+X$,"","FM2ODB03","AR1","",""; IF ABS(C0)>4 THEN GOSUB 6400; GOSUB 5390; ON C0 GOTO 5210,5211
5215 IF POS(" "<>N$(1,10))=0 THEN EXITTO 5050
5220 FIND (Z[8],KEY=N$(1,10),DOM=5210)N0$
5225 PRINT @(42,12),N0$(11,35),
5250 REM "Item code
5255 C0$=N$(1,10),C7=2,N0=1
5257 IF POS(" "<>N$(11,10))=0 THEN N$(11,10)=A$(19,10)
5260 CALL "ZZENTR","SUX",A{ALL},N$,X4$,X3$,30,13,11,10,C0,"","{3"+X$,C0$,"FM2ODB13","FM1","",""; IF ABS(C0)>4 THEN GOSUB 6400; GOSUB 5390; ON C0 GOTO 5260,5261
5265 IF C0=-2 THEN GOTO 5206 ELSE IF POS(" "<>N$(11,10))=0 THEN EXITTO 5050
5270 FIND (Z[3],KEY=N$,DOM=5260)IOL=0330
5275 PRINT @(42,13),C$(42,40),
5300 REM "Convert to new & write out
5305 Q$=C$
5310 C$(1,20)=B$(6,10)+A$(19,10),C$(109)="",C[6]=0,C[7]=0,C[8]=0,C[9]=0,C[10]=0,C[11]=0,C[12]=0,C[13]=0,C[14]=0,C[15]=0,C[16]=0,C[17]=0,C$(254,1)="0",C$(255,6)=X3$(15,6)
5315 C$(238,16)=Q$(238,16),C$(212,3)=Q$(212,3),C[24]=0
5316 C$(387,1)=Q$(387,1); REM SSP  201823
5320 WRITE (Z[3],KEY=C$(1,20))IOL=0330
5330 GOSUB 8300
5350 GOTO 5050
5380 RETURN 
5390 PRINT @(0,11),'CE',@(0,12),"Copy this item from customer:",@(24,13),"item:",
5391 CALL "ZZDISP","A",N$(1,10),"A/R",X3$,"","",30,12,X4$
5392 PRINT @(42,12),N0$(11,35),@(24,13),"item:",@(30,13),N$(11,10),@(42,13),C$(42,40),
5395 RETURN 
5400 REM "Get & default stock item
5405 DIM J[10],J$(58)
5410 FIND (Z[10],KEY=C0$+A$(19,10),DOM=5411)IOL=0390; IF POS(I$(118,1)="USC")<>0 THEN A$(234,1)="Y",INV$=I$ END_IF ; GOTO 5415 ! SSP#261073
5412 A$(19,10)=STR(NUM(A$(19,10),ERR=5460)); FIND (Z[10],KEY=C0$+A$(19,10),DOM=5460)IOL=0390
5413 IF POS(I$(118,1)="USC")<>0 THEN READ (Z[25],KEY=A$(19,10),DOM=*NEXT)OS2$; REM " SSP 168567
5414 IF OS2$<>"" THEN PRINT @(5,5),OS2$(11,15),; OS2$=""; REM SSP 204226
5415 IF B$(43,1)="Y" AND POS(A$(155,1)="IC")>0 THEN GOTO 5600
5416 IF POS(I$(118,1)="USC")<>0 THEN CALL "OS2OBC",X3$,X4$,A$(19,10),I{ALL},B$,"FM2OB5",TOPS_PRICE,"",TOPS_COST,QUANTITY; REM LAST FIELD IS "" THE USE CUSTOMER DEFAULTS FOR LOWEST PRICE;REM SSP 211032
5420 A$(50,40)=I$(21,40),A$(29,21)=I$(61,21),A$(120,8)=I$(116,8),A[1]=I[18],A[2]=I[15],A[5]=I[14],A[12]=I[19]; IF F0$="" THEN A$(90,14)=I$(92,4)+I$(82,10)
5421 A[4]=I[0],A$(120,4)=I$(124,4),A[10]=A[14],A[0]=0,A[3]=0,A[6]=0,A[22]=A[4],A[23]=B[9],A[4]=A[4]-A[4]*B[9]*.01
5422 IF POS(" "<>I2$)<>0 THEN A$(90,14)=I2$(11,4)+I2$(1,10)
5423 IF A$(234,1)="Y" THEN A$(94,10)=INV$(82,10),A$(90,4)=INV$(92,4); REM "IF PRICE<>0 THEN A[4]=PRICE IF TOPS ITEMS MUST HAVE TOPS VENDOR
5424 IF B$(43,1)="Y" THEN IF POS(A$(155,1)="I")>0 AND A[2]<>0 THEN A[15]=A[1]*1000/A[2],X0=X0-3
5425 X0=X0+7
5426 IF TOPS_COST<>0 AND POS(I$(118,1)="USC")<>0 THEN A[1]=I[17]; REM " SSP 211032 should only do this on a TOPS item
5427 IF P2$(50,1)="Y" THEN A$(4,1)="N"
5428 GOSUB 8400
5430 GOSUB 7710
5435 IF P3$(243,1)="A" THEN CALL "FS2DAJ",X3$,X4$,A$(161,10)+A$(19,10),1,17
5437 PROMPT$="***DISCONTINUED***"
5438 IF A$(155,1)="I" THEN ITEM_CUST$=DIM(10) ELSE ITEM_CUST$=B$(6,10)
5439 CALL "UPDSP0;GET_PRICE",0,B$(6,10),ITEM_CUST$+A$(19,10),A[4],A[1],SP0_PRICE,SP0_PRICE_TYPE$,A[2],A[5]; IF SP0_PRICE_TYPE$<>"" THEN A[4]=SP0_PRICE,A$(235,3)=SP0_PRICE_TYPE$; PRINT @(44,13),A$(235,3)
5440 L0=3; GOTO 9900
5450 REM "Input item code
5452 IF B$(43,1)="Y" THEN IF POS(" "<>A$(19,10))<>0 THEN GOTO 5440
5453 IF POS(" "<>A$(19,10))<>0 THEN GOTO 5440; REM "SSP119416
5454 IF A$(2,1)="Y" AND POS(A$(155,1)="IC")<>0 THEN GOTO 5440
5455 DIM J9$(10); J9$(1,10)=A$(19,10),A9$=A$(19,10),C7=3
5456 IF C2$<>"*" AND A$(155,1)="C" THEN GOTO 2700
5457 IF A$(155,1)="I" THEN DIM C0$(10) ELSE C0$=A$(161,10)
5458 IF CIC$="Y" AND POS(" "<>J9$)=0 THEN GOTO 4000
5459 IF A$(155,1)="I" AND A$(9,1)<>DIM(1) AND RESULT$="Y" THEN X$="? to lookup with Vendor's(Supplier's) num - F10 - Office Supply" ELSE X$="Enter ? to lookup with Vendor's (supplier's) number"; REM " DON'T FORGET TO ALL RESULT$!
5460 C2$="*"; CALL "ZZENTR","AUX",A{ALL},J9$,X4$,X3$,1,7,1,10,C0,"ICE","{3"+X$,C0$,"FM2ODB13","IC0","X",""; IF C0=-7 THEN GOTO 6500 ELSE IF ABS(C0)=102 THEN GOSUB 8000; L3=1,Z8$="IC2MAB"; RUN "ZZ2STP" ELSE IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 5460,5461
5461 IF J9$(1,1)="?" THEN GOTO 3000
5462 C2$=""
5463 IF STP(A$(161,10),3)<>"" THEN FROM_CUST$=A$(161,10) ELSE FROM_CUST$=B$(6,10) ! WO155894
5464 DIM FM1$(448); FIND (Z[3],KEY=FROM_CUST$+J9$,DOM=*NEXT)FM1$; IF STP(FM1$(185,10),3," ")="" OR FM1$(185,10)=A$(161,10) OR (FM1$(185,10)=FM1$(1,10) AND A$(155,1)="I") THEN GOTO *NEXT ELSE A$(161,10)=FM1$(185,10),A$(155,1)="C"; GOSUB 2750; C0$=A$(161,10) ! SSP#222548!SSP#229172
5465 CONTRACT$="Y"; IF MID(P3$,304,1)="Y" AND MID(FM1$,105,1)="Y" AND MID(FM1$,380,6)<>DIM(6) AND MID(FM1$,380,6)<X3$(21,6) THEN CALL "ZZPROM",".4",X3$,0,"Warning! Contract expiration date is "+FND$(FM1$(380,6))+".","","",0; CONTRACT$="N" ! WO155894 
5466 IF A9$=J9$ AND POS(" "<>J9$)<>0 OR (C0<=-2 AND POS(" "<>A9$)=0) THEN GOSUB 5500; L0=3; GOTO 9900 ELSE A$(19,10)=J9$
5468 IF POS(" "<>A$(19,10))=0 THEN GOTO 5458; REM "USE TO BE GOTO 5458
5470 GOTO 5400
5500 REM "Replace old value from A9$
5510 A$(19,10)=A9$
5520 CALL "ZZDISP","A",A$(19,10),"ICE",X3$,"","",1,7,X4$
5530 IF A$(155,1)="C" THEN GOSUB 2750
5540 RETURN 
5600 REM "CHECK FW0
5610 CLOSE (14); OPEN (14)"FW0"+X3$(9,3)
5620 Q3$=""; FIND (14,DOM=5645,KEY=B$(6,10)+A$(161,10)+A$(19,10)+A$(147,8))Q3$
5630 Q3$=Q3$(96,3); IF Q3$=A$(6,3) OR Q3$="   " THEN GOTO 5645
5635 CALL "ZZPROM",".4",X3$,0,"Item is already on this order, line "+Q3$,"","",0; CLOSE (14); GOTO 5460
5645 CLOSE (14); GOTO 5416
5900 REM "SETUP RUN SEQUENCE, P$=program to run
5910 REM "Return from specs to here, then come to 600's here, goto FM2ODB to reread line info then back to 600's here then to 5000 here to finish up
5915 L0=0,L2=0
5920 WRITE (Z[1],KEY=A1$)IOL=0310
5930 CLOSE (14); OPEN (14)"ZZPARM"; WRITE (14,KEY=U0$)U0$,U1$
5985 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
5990 SETERR 5995; RUN Q2$(1,6)
5995 SETERR 9000; GOSUB 6400; GOTO 9900
6000 REM "BACKGROUND
6010 CALL "FM2OB1",X3$,X4$,6000,N0,A$,A{ALL},B$,E$,E1$,H$,0,0,E2$,E4$,P3$
6190 RETURN 
6200 REM "DISPLAY DATA
6210 CALL "FM2OB1",X3$,X4$,6200,N0,A$,A{ALL},B$,E$,E1$,H$,S[2],T[2],E2$,E4$,P3$
6215 DIM C0$(50)
6220 IF A$(155,1)="C" THEN FIND (Z[8],KEY=A$(161,10),DOM=6221)C0$ ELSE GOTO 6230
6221 PRINT @(0,5),'CL','SB',"Cust. no:",@(25,5),"Name:",'SF',
6222 CALL "ZZDISP","A",A$(161,10),"A/R",X3$,"","",10,5,X4$
6223 PRINT @(31,5),C0$(11,35),
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 CALL "FM2OB1",X3$,X4$,6400,N0,A$,A{ALL},B$,E$,E1$,H$,S[2],T[2],E2$,E4$,P3$
6430 IF C7=4 THEN GOSUB 3400
6440 IF ABS(C0)>1 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6460 CALL "FM2OB1",X3$,X4$,6450,N0,A$,A{ALL},B$,E$,E1$,H$,S[2],T[2],E2$,E4$,P3$
6490 RETURN 
6500 REM " Office Supply Input
6501 Z$="12C PO1...  12O OSD...  "
6502 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 6503,9900
6505 DIM OS0$(25)
6510 REM "X$="OS"
6520 REM "C0=1; CALL "OS2PLU",X3$,J9$,1,X$
6530 CALL "ZZENTR","SUUX",A{ALL},OS0$,X4$,X3$,1,7,1,20,C0,"","{8"+X$,"","IC2TAA 04","OSD","",""; IF ABS(C0)=6 THEN C0=7; IF ABS(C0)>1 THEN GOSUB 6400; ON C0 GOTO 6530,6531,6534
6531 READ (Z[12],KEY=OS0$(1,20),DOM=6532)
6532 OS0_KEY$=KEY(Z[12])
6533 IF OS0_KEY$(1,20)<>OS0$(1,20) THEN GOTO 6530 ELSE OS0$(1,10)=OS0_KEY$(22,10)
6536 REM "IF POS(INV$(108,1)="ID")=0 THEN GOTO 6550
6537 REM "X2$="*** UNITED ITEM IS DISCONTINUED ***"+INV$(128,40)
6540 REM "CALL "ZZPROM","4",X3$,Z,X2$,"","",0; ON Z GOTO 4000
6545 REM "PRINT @(0,21),'CL',
6550 CALL "OS2C02",X3$,X4$,OS0$(1,10),Q1$ ! Line restored to backup copy until ref#218428 can be moved to release ! CALL "OS2C02",X3$,X4$,OS0$(1,10)+"|"+B$(6,10),Q1$ ! SSP#218428
6551 READ (Z[10],KEY=DIM(10)+OS0$(1,10),DOM=6552)INV$; IF POS(INV$(118,1)="USC")<>0 THEN A$(234,1)="Y"; REM SSP 187899
6552 IF OSP_PARMS$(449,5)<>"" THEN A$(156,5)=OSP_PARMS$(449,5)
6560 Z$="12C OSD...  12O PO1...  25O OS2...  "
6562 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 6563,9900
6564 READ (Z[25],KEY=OS0$(1,10),DOM=*NEXT)OS2$
6566 IF OS2$<>"" THEN PRINT @(5,5),OS2$(11,15),; REM "@(1,7),DIM(20),;REM SSP 204226
6570 A$(19,10)=OS0$(1,10); GOTO 5410
7000 REM "Find the Vendor from the first PO line on the order SSP 217590
7002 SEQ=0
7005 FIRST_VENDOR$=DIM(10),SEQ=0
7010 SEQ=SEQ+1; SEQ$=STR((SEQ):"000")
7020 READ RECORD (Z[1],KEY=A1$(1,8)+SEQ$,DOM=7099)FS2$
7022 IF FS2$(9,1)<>A$(9,1) THEN GOTO 7010
7023 IF A$(147,8)<>A1$(1,8) THEN RETURN 
7040 FIRST_VENDOR$=FS2$(94,10)
7051 IF A$(6,3)=SEQ$ AND C$(82,10)<>FIRST_VENDOR$ THEN FIRST_VENDOR$=C$(82,10); REM SSP
7099 RETURN 
7300 REM 
7310 ON FPT(C0)*10 GOTO 7390,7325,7330,7390
7325 IF V8$="" THEN X0=X0+4; GOTO 7390 ELSE X0=NUM(V8$(POS(STR(X0:"00")<=V8$,2),2)); GOTO 7390
7330 IF V8$="" THEN X0=X0-6; GOTO 7335 ELSE X0=POS(STR(X0-1:"00")<=V8$,2)-2; IF X0<0 THEN X0=0; GOTO 7390 ELSE X0=NUM(V8$(X0,2)); GOTO 7390
7335 IF X0<0 THEN X0=0; GOTO 7390
7390 C0=3; RETURN 
7500 REM "Display Price dependent quantities
7510 IF S[2]=0 THEN S[2]=1; RETURN 
7520 PRINT @(19,13),A[0]/S[2]:M0$,
7524 RETURN 
7525 REM "Display Cost dependent quantities
7530 IF T[2]=0 THEN T[2]=1; RETURN 
7540 PRINT @(19,14),A[0]/T[2]:M0$,
7549 RETURN 
7550 REM "Change price if price qty/unit changed
7560 IF S[4]<>0 THEN A[4]=A[4]*A[5]/S[4]; PRINT @(33,13),A[4]:M1$,A$(235,3)
7574 RETURN 
7575 REM "Change cost if cost qty/unit changed
7580 IF T[4]<>0 THEN A[1]=A[1]*A[2]/T[4]; PRINT @(33,14),A[1]:M1$,
7599 RETURN 
7600 REM "Display customer
7610 CALL "FM2OB1",X3$,X4$,7600,N0,A$,A{ALL},B$,E$,E1$,H$,S[2],T[2],E2$,E4$,P3$
7690 RETURN 
7700 REM "Default based on forms info
7701 GOSUB 7000
7702 IF A$(9,1)<>" " THEN IF POS(C$(254,1)="03467")>0 THEN CALL "FM2OBZ",X3$,A$,C$,Z[3],Z[4],"","",Z9; IF X9=1 THEN EXITTO 2300; REM SSP217590
7703 A$(19,85)=C$(11,71)+C$(92,4)+C$(82,10),A$(120,4)=C$(96,4),A$(124,4)=C$(100,4),A[1]=C[1],A[2]=C[2],A[4]=C[3],A[5]=C[4],A[10]=C[16]+A[14],A[0]=C[16],A[12]=C[0],A[22]=A[4],A[4]=A[22]-A[22]*A[23]*.01; IF A$(14,1)="Y" AND FIRST_VENDOR$<>DIM(10) AND FIRST_VENDOR$<>A$(94,10) THEN A$(94,10)=FIRST_VENDOR$; WRITE (Z[1],KEY=A1$)IOL=0310; IF C$(115,1)<>" " THEN A$(135,1)=C$(115,1),A$(137,10)=C$(116,10),A[11]=C[7]; REM SSP 217590
7704 IF A$(155,1)="I" THEN ITEM_CUST$=DIM(10) ELSE ITEM_CUST$=B$(6,10)
7705 CALL "UPDSP0;GET_PRICE",0,B$(6,10),ITEM_CUST$+A$(19,10),A[4],A[1],SP0_PRICE,SP0_PRICE_TYPE$,A[2],A[5]; IF SP0_PRICE_TYPE$<>"" THEN A[4]=SP0_PRICE,A$(235,3)=SP0_PRICE_TYPE$; WRITE (Z[1],KEY=A1$)IOL=0310; PRINT @(44,13),A$(235,3)
7706 IF F1=1 THEN GOSUB 8295; F1=0
7707 GOSUB 7800
7710 GOSUB 7750
7712 X$=C$(1,10)+A$(29,3)+C$(1,20); IF MFF_PARM THEN CALL "FS2MFF",X3$,X4$,X$,X,A$(147,8)+A$(6,3) ELSE CALL "FS2DAF",X3$,X4$,X$,X; IF X$>"" THEN A$(135,1)=X$(11,1),A$(137,10)=X$(1,10),A[11]=X ! SSP 276772
7715 FIND (Z[5],KEY=A$(94,10),DOM=7716)IOL=0350
7720 FIND (Z[5],KEY=A$(137,10),DOM=7721)IOL=0351
7722 FIND (Z[13],KEY="U/M"+A$(124,4),DOM=7723)IOL=0410
7723 IF S5$(20,1)<>"Y" THEN S[2]=A[5] ELSE S[2]=S[1]
7724 FIND (Z[13],KEY="U/M"+A$(120,4),DOM=7725)IOL=0420
7725 IF T5$(20,1)<>"Y" THEN T[2]=A[2] ELSE T[2]=T[1]
7726 IF S[2]=0 THEN S[2]=1
7727 IF T[2]=0 THEN T[2]=1
7730 IF POS(" "<>A$(15,3))=0 THEN GOSUB 7950
7731 IF POS(B$(54,1)="BS")>0 THEN A$(18,1)="Y"
7732 IF P3$(241,1)="Y" THEN X=12; CALL "FS2DAI",X3$,A$(161,10)+A$(19,10),X
7733 GOSUB 8500
7734 CALL "FM2OB1",X3$,X4$,6080,N0,A$,A{ALL},B$,E$,E1$,H$,0,0,E2$,E4$,P3$
7735 IF A$(9,1)=DIM(1) OR STP(P1$(75,1),2)="" OR P1$(76,1)="Y" THEN GOTO 7742 ELSE CC3$=A$(128,5); CALL "AR2COM","VENDCOM",CC3$,A{ALL},Z[13],0,0,A$; A$(128,5)=CC3$ ! SSP  217566, SSP 222237  SSP 225300
7738 IF %MSLC THEN CMKEY$="O"+A$(147,8)+B$(6,10)+A$(6,3); CALL "AR2MCM","VENDCOM",CC3$,A{ALL},Z[13],0,0,CMKEY$,0,A$ ! SSP 225300
7742 GOSUB 6200; GOSUB 7900; WRITE (Z(1),KEY=A1$)IOL=0310 ! SSP 217566
7744 IF MID(E$,246,1)="I" THEN CALL "ZZPROM",".4",X3$,0,"The item vendor is inactive, please use another vendor","","",0
7745 IF LEN(E1$)>245 AND E1$(246,1)="I" THEN CALL "ZZPROM",".4",X3$,0,"This Finder's Fee Vendor is inactive, please use another vendor","","",0 ! ssp#231040
7748 RETURN 
7750 REM "Defaults based on product code info
7760 FIND (Z[4],KEY="T"+A$(29,3),DOM=7761)IOL=0340
7770 REM LET A$(119,1)=D$(21,1)
7775 IF D$(22,1)="N" THEN A[9]=0
7780 GOSUB 8100
7790 RETURN 
7800 REM "Figure starting number A$(104,9) from  C$(140,9)
7801 REM "Assumes C$(140,9) is right justified!
7810 Z8=POS(" "<>C$(140,9)); IF Z8=0 THEN GOTO 7840
7815 DIM Z9$(10-Z8,"0")
7820 A$(104,9)=STR(NUM(C$(140,9),ERR=7822)+1:Z9$); GOTO 7830
7822 SETERR 9000; A$(104,9)=C$(140,9)
7830 IF A$(112,1)=" " THEN A$(104,9)=" "+A$(104,8); GOTO 7830
7840 RETURN 
7900 REM "Calc ext & labels, note - divide is before mult on purpose
7910 CALL "FM2OB4",A{ALL},S{ALL},T{ALL},M0$,M6$,M2$,M3$,S5$,T5$,D$(22,1),A$,I$
7945 RETURN 
7950 REM "Feature Driven Pricing
7955 IF P3$(149,1)="N" OR POS(" "<>C$(291,20))=0 THEN GOTO 7995
7960 IF P3$(150,1)="Y" THEN A$(4,1)="R"
7965 CALL "FM2OBA",X3$,A$,C$(291,20),A{ALL},Q8,Q9,M6[1]; A[1]=Q8,A[4]=Q9
7995 RETURN 
8000 REM "Set Q1$ for setup
8010 Q1$=""; IF A$(155,1)="C" THEN Q1$=B$(6,10)
8040 RETURN 
8100 REM "Set taxable to N if never taxed
8102 IF B$(86,1)<>"Y" THEN A$(136,1)="N" ELSE IF A$(136,1)=" " THEN A$(136,1)="Y"; REM SSP 187704
8105 ON POS(D$(23,1)="NC") GOTO 8130,8110,8120
8110 A$(136,1)="N"; IF B$(86,1)<>"Y" THEN GOTO 8134 ELSE GOTO 8130; REM IF CUSTOMER IS TAXABLE, CHECK THE ITEM TOO
8120 IF A$(136,1)="Y" THEN A$(136,1)="C"
8130 IF POS(A$(155,1)="IC")=0 THEN GOTO 8133 ! SSP#226820
8131 IF MID(I$,119,1)=" " THEN GOTO 8132 ELSE IF MID(I$,119,1)="E" THEN A$(136,1)="N" ELSE IF MID(I$,119,1)="C" THEN A$(136,1)="C" ELSE IF MID(I$,119,1)="T" THEN A$(136,1)="Y"; REM SSP 201823
8132 GOTO 8134
8133 IF MID(C$,387,1)=" " THEN GOTO 8134 ELSE IF MID(C$,387,1)="E" THEN A$(136,1)="N" ELSE IF MID(C$,387,1)="C" THEN A$(136,1)="C" ELSE IF MID(C$,387,1)="T" THEN A$(136,1)="Y"; REM SSP 201823
8134 PRINT @(42,17),A$(136,1),
8135 IF P3$(186,1)="Y" THEN IF POS(" "<>D$(38,5))>0 THEN A$(128,5)=D$(38,5)
8136 IF ARS$(122,1)="Y" THEN FIND (Z[14],KEY=B$(93,4)+A$(29,3),DOM=8137)ASQ$; A$(128,5)=ASQ$(8,5); PRINT @(17,17),A$(128,3),; REM "WO89451
8140 RETURN 
8200 REM "No longer used
8205 L0=3; GOTO 9900
8300 REM "Copy records for item copy, keep Z(1) open to write out stuff
8310 Z$="02C FS1...  03C FM1...  04C FMP...  05C AP4...  06C PO6...  02O FMS...  03O FMU...  04O FMV...  05O FM2...  06O FSF...  "
8315 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 8316,9900
8319 REM "Write out to FM2 file
8320 WRITE (Z[5],KEY=C$(21,3)+C$(24,16)+C$(40,2)+C$(11,10)+C$(1,10))
8325 FOR I=2 TO 4
8330 GOSUB 8350
8335 NEXT I
8337 I=6; GOSUB 8350
8340 RETURN 
8350 REM "Transfer records with key N$ in file Z(I) to key B$(6,10)+A$(19,10)
8355 EXTRACT (Z[I],KEY=N$,DOM=8356)
8357 K$=KEY(Z[I],END=8380)
8358 IF MID(K$,1,LEN(N$))<>N$ THEN GOTO 8380
8360 READ RECORD (Z[I],END=8380)A8$
8365 A8$(1,20)=B$(6,10)+A$(19,10)
8370 WRITE RECORD (Z[I],KEY=A8$(1,LEN(K$)))A8$
8373 READ (Z[I],KEY=K$,END=8380)
8375 GOTO 8357
8390 RETURN 
8400 REM "C-types Get finder's fee from this customer then source customer
8405 IF P3$(184,1)="Y" THEN GOTO 8415
8410 IF A$(155,1)<>"C" THEN GOTO 8440
8415 X$=B$(6,10)+A$(29,3)+A$(161,10)+A$(19,10); IF MFF_PARM THEN CALL "FS2MFF",X3$,X4$,X$,X,A$(147,8)+A$(6,3); RETURN ELSE CALL "FS2DAF",X3$,X4$,X$,X ! SSP 276772
8430 IF X$>"" THEN A$(135,1)=X$(11,1),A$(137,10)=X$(1,10),A[11]=X
8440 RETURN 
8550 REM "Display Item Notes
8555 IF P3$(283,1)<>"Y" THEN GOTO 8590
8560 FS2DAN$=A$(161,10)+A$(19,10); IF A$(14,1)="Y" THEN FS2DAN$(1,10)=B$(6,10)
8570 CALL "FS2DAN",X3$,FS2DAN$,12
8590 RETURN 
8900 REM "FNS
8915 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8950 REM "Program flow logic
8960 DIM U0$(6); U0$(1)=FID(0),U0$=U0$+"PF"
8970 CLOSE (14); OPEN (14)"ZZPARM"; FIND (14,KEY=U0$,DOM=8990)U0$,U1$
8975 IF U1$="" THEN GOTO 8990 ELSE IF U1$(1,6)<>X0$ THEN GOTO 8990
8980 Q1=NUM(U1$(7,2)),Q1$=U1$(9,Q1),U1$=U1$(Q1+9)
8990 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C7 GOTO 9900,5200,9530,5456,3440,2310
9530 GOSUB 5390; GOTO 5250
9700 REM 
9710 GOTO 9900
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9940 SETESC 9350
9950 RUN "FM2OB0"
9999 END 
10000 CHECK_MFF:! SSP 276772
10005 MFF_PARM=0
10010 CHL=UNT; OPEN (CHL,ERR=END_CHECK_MFF)"ZZPARM"
10020 FIND (CHL,KEY=X3$(9,3)+"F/M")OPPARM$
10030 MFF_PARM=0; IF MID(OPPARM$,337,1)="Y" THEN MFF_PARM=1 ! SSP 276772
10050 END_CHECK_MFF:
10060 CLOSE (CHL,ERR=*NEXT)
10070 RETURN 
10090 ! 
56000 REM "188067-New Pricing changes. Standard GP Percentage for a           
56001 REM "201823-Provide the ability to set the taxable flag  by Item   
56002 REM "204226-Issue when TOPS furniture item is ordered.
56003 REM "203458-Addition of S.P. Richards to TOPS module                    
56004 REM "212083-Entered in OP-O1 order with 4 lines, all for the same vendor
56005 REM "211032-Issue with cost extensions not being correct on order.  
56006 REM "261073-In manual order entry United orders PO  Send Order Data is set to N
56007 REM "276772-Multiple Finder's Fees per Customer/Item/Order Line.        
