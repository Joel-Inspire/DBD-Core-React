0010 REM "Forms Status Report <FM2ROH> QBF
0015 REM "Prog Type: O -3.0A
0020 SETESC 9300; SETERR 9000
0035 REM "5.4 - 06/07/05 - 15.7075 - jir - SSP# 184815
0036 REM "10/14/91; LINE 1340 ADDED TO ALLOW SKIP OF NON-FMS FORMS
0040 REM "Copyright 2005 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0110 X0$="FM2ROH",X1$="Forms Status Report"
0115 M0$="######0-",M1$="##0-"
0120 DIM A$(33),A[75],C$(448),C[28],S$(40),D$(397)
0210 T=1,T0=1,T1=15
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,"FM2ROA",X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0280 P0=11
0300 REM "I/O lists
0305 REM "IOLIST FOR FMD - Forms Mgt Report Info
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30],A[31],A[32],A[33],A[34],A[35],A[36],A[37],A[38],A[39],A[40],A[41],A[42],A[43],A[44],A[45],A[46],A[47],A[48],A[49],A[50],A[51],A[52],A[53],A[54],A[55],A[56],A[57],A[58],A[59],A[60],A[61],A[62],A[63],A[64],A[65],A[66],A[67],A[68],A[69],A[70],A[71],A[72],A[73],A[74],A[75]
0325 REM "IOLIST FOR FM1 - Customer Job Master
0330 IOLIST C$(1),C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20],C[21],C[22],C[23],C[24],C[25],C[26],C[27],C[28]
0340 IOLIST D$(1)
0350 IOLIST E$
0370 IOLIST G$
0420 IOLIST X3$,X4$,V1$,V3$,V2$,IOL=0425
0425 IOLIST V$,V[0],V[1]
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0508 IF V$(72,1)="0" THEN Q$="FMC" ELSE Q$="FMD"
0509 IF V$(71,2)="10" THEN Q$="FMC" ELSE Q$="FMD"
0510 Y$="01X "+Q$+"...  03O FM1... 04O AR1... 05O FMP... 06O ZY2    07O FM0...  "; IF V1$(1,3)<>Y$(5,3) THEN Y$=Y$+"02X "+V1$+"  "
0520 GOSUB 9750; IF Z0>0 THEN GOTO 9911
0530 IF POS("M"=V$(59,12))=0 AND POS("$"=V$(59,12))>0 THEN GOTO 0580
0535 K0$=""; CALL "ZZ2SRT",X3$,X4$,Z[13],Z[1],34,K0$,K1,K1$
0540 Y$="10O "+K0$; GOSUB 9750
0800 REM "Alternate Sort Info & total dim"
0805 REM LET V1$=Q$+"..."
0810 DIM U[10,2],U0$(10),Y[5,1]
0820 CALL "ZZRSEQ",X3$,"FM2ROA",V1$,U,U0,U1,U{ALL},Y{ALL},T0,V0,U0$,T5$,U5$,S9$,X9; ON X9 GOTO 0821,9920
0825 IF V0>1 THEN GOSUB 6950
0830 IF V$(71,2)="11" THEN U0$(3,1)="2"
0850 DIM T[T0,T1]; U5$(1,20)=Q4$
0890 REM "Open Printer
0895 CALL "ZZPRIN","FM2ROH",X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM 
0910 Q9=0; IF POS("M"=V$(59,12))>0 THEN Q9=1
0920 IF V$(72,1)="0" AND V$(71,1)<>"3" THEN U1=Z[1]
0950 REM " Position read"
0970 DIM V4$(U); U9=1; FOR X=1 TO U0; V4$(U[X,0],U[X,1])=V2$(U9,U[X,1]); U9=U9+2*U[X,1]; NEXT X
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991); GOTO 1000
0995 READ (U1,KEY="",DOM=0996)
1000 REM "Read next record and check range
1005 U$=KEY(U1,END=5000)
1010 FOR X=1 TO U0; U2=U[X,0],U3=U[X,0]*2-1,U4=U[X,1]
1020 IF U$(U2,U4)<V2$(U3,U4) THEN READ (U1); EXITTO 1000
1030 IF U$(U2,U4)>V2$(U3+U4,U4) THEN IF X=1 THEN EXITTO 5000 ELSE READ (U1); EXITTO 1000
1040 NEXT X
1100 REM "Get record"
1110 U9$=U$; IF V0<2 THEN GOTO 1150 ELSE U$="",X=0
1115 IF V0=2 THEN U$=U9$(1,10)+U9$(16,10)+U9$(11,5); GOTO 1130
1120 IF Y[X,0]>0 THEN U$=U$+U9$(Y[X,0],Y[X,1]),X=X+1; GOTO 1120
1130 READ (U1)
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1160 IF Q9=1 THEN GOSUB 6900; GOTO 1000
1200 REM "Additional Reads
1210 IF D$(1,10)<>A$(1,10) THEN GOSUB 8200
1220 IF MID(D$,1,10)+MID(A$,21,5)<>MID(G$,2,10)+MID(G$,1,1)+MID(G$,12,4) THEN READ (Z[7],KEY=A$(21,1)+D$(1,10)+A$(22,4),DOM=1221)IOL=0370
1330 DIM C$(LEN(C$)); FIND (Z[3],DOM=1331,KEY=A$(1,20))IOL=0330
1340 IF POS("F"=V$(59,12))>0 THEN IF C$(108,1)<>"Y" THEN GOTO 1000
1400 REM "Subtotals"
1410 IF U2$="" THEN GOTO 1490 ELSE FOR T=1 TO U0; IF U9$(U[T,0],U[T,1])<>U2$(U[T,0],U[T,1]) THEN EXITTO 1420 ELSE NEXT T; GOTO 1480
1425 GOSUB 3100
1490 T=0,U2$=U9$
1510 IF W>W0 THEN GOSUB 6000
1515 IF V$(71,2)="11" THEN IF T[T0-1,0]>0 THEN GOTO 1525
1520 Y5$(1)=A$(11,10),Y5$(12)=C$(42,40)
1522 IF V$(71,1)="3" THEN GOSUB 8000; GOTO 1530
1525 IF V$(71,2)="11" OR V$(71,2)="31" THEN Y5$(52)=A$(21,5); GOTO 1540
1530 Y5$(52)=C$(100,4),Y5$(57)=STR(C[4]:"####")
1540 Y5$(63-P0)=STR(A[0]:M0$),Y5$(72-P0)=STR(A[1]:M0$)
1545 COMMITTED=A[49]*NUM(E$(226,1)); REM "WO92875
1550 Y5$(81-P0)=STR(A[2]:M0$),Y5$(90-P0)=STR(A[3]-COMMITTED:M0$); REM " IF NUM(E$(226,1))=1 AND A[3]-COMMITTED<0 THEN LET Y5$(90-P0)=STR(0:M0$)
1560 Y5$(99-P0)=STR(A[4]:M0$)
1570 Y5$(108-P0)=STR(A[5]:M1$),Y5$(113-P0)=STR(A[6]:M1$)
1575 Q$=""; IF A[8]>0 THEN Q$="*"
1580 Y5$(118-P0)=STR(A[9]:M0$)+Q$
1585 Q$=FND$(A$(26,6)); Q$=Q$(1,3)+Q$(9,2)
1590 Y5$(127-P0)=Q$
1595 Y5$(133-P0)=STR(A[50]:M0$)
1680 GOSUB 7300
1700 REM "LINE 2
1701 IF X3$(9,3)="029" AND V$(72,1)="0" THEN GOTO 1705
1702 ON V0 GOTO 1800,1800,1705,1800
1705 GOSUB 8100
1710 Y5$(12)=S8$
1720 IF C$(238,4)<>"    " THEN Y5$(52)=C$(238,4),Y5$(57)=STR(C[25]:"####")
1780 GOSUB 7300
1785 GOSUB 7300
1809 IF V$(71,2)="11" THEN IF T[T0-1,0]>0 THEN GOTO 1811
1810 IF V$(74,1)<>"Y" THEN IF POS(" "<>C$(350,20))>0 THEN Y5$(12)=C$(350,20); GOSUB 7300
1900 REM "Accumulate Totals"
1910 T[T0,0]=T[T0,0]+1
1920 FOR X=0 TO 8; IF X=3 THEN T[T0,X+1]=T[T0,X+1]+A[X]-COMMITTED ELSE T[T0,X+1]=T[T0,X+1]+A[X]
1921 NEXT X
1922 REM LET T(T0,6)=A(22),T(T0,7)=A(23)
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "Subtotal Logic
3110 FOR T=U0 TO T STEP -1
3130 T$=FNS$(U5$(T*20+1,20)),T6$=U2$(U[T,0],U[T,1]),T7$=T5$(T*4+1,4); GOSUB 3050; IF POS(U0$(T+1,1)="23")>0 THEN GOSUB 7000 ELSE GOSUB 7100
3150 IF POS(U0$(T+1,1)="13")>0 THEN GOSUB 6000
3180 NEXT T
3195 RETURN 
3280 RETURN 
3380 RETURN 
3480 RETURN 
5000 REM "End of Print
5005 IF Q9=1 THEN Q9=0,U1=Z[10],V0=2; PRINT @(0,22),'CL',; GOTO 0995
5010 GOTO 5090
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5050 T=0; GOSUB 3100
5200 REM "
5295 GOTO 9900
6000 REM "Page header
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6090 ELSE ON W5 GOTO 6041,9910
6060 GOSUB 6200
6065 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6105 Y5$(110)="* = On Order"
6110 CALL "ZZRPRT",0,Y5$,V9$; GOSUB 7300
6115 CALL "ZZRPRT",1,Y5$,V9$; Y5$(52)=Y5$(52+P0); Y5$(110)="Last   Last"; GOSUB 7300
6120 CALL "ZZRPRT",2,Y5$,V9$; Y5$(52)=Y5$(52+P0); Y5$(108)="  Order Order      On"; GOSUB 7300
6125 CALL "ZZRPRT",3,Y5$,V9$; Y5$(52)=Y5$(52+P0); Y5$(110)=" Qty   Date     B/O"; GOSUB 7300
6195 IF FID(W9)=FID(0) THEN PRINT 'SF',
6199 RETURN 
6200 REM "CUSTOMER HEADERS
6205 Y5$(1)=T1$; GOSUB 7300
6210 IF V$(71,1)<>"3" THEN Y5$(1)="Client:",Y5$(9)=FNS$(D$(11,35))+" "+D1$ ELSE Y5$(1)="Group: "+U9$(1,3)
6215 IF V$(71,1)="2" THEN Y5$(80)="Location: "+FNS$(MID(G$,16,35))+" ("+A$(21,5)+")"
6225 GOSUB 7300
6230 IF V$(71,1)="2" THEN Y5$(90)=MID(G$,51,30); GOSUB 7300 ELSE GOSUB 7300
6232 IF V$(71,1)="2" THEN Y5$(90)=MID(G$,81,30); GOSUB 7300 ELSE GOSUB 7300
6250 Y5$(1)=S7$
6252 IF V$(71,1)="2" THEN CALL "ZZDISP","AX",MID(G$,129,9),"ZIP",X3$,Q$,"",0,0,X4$
6255 IF V$(71,1)="2" THEN Y5$(90)=FNS$(MID(G$,111,16))+", "+MID(G$,127,2)+"  "+Q$
6260 GOSUB 7300
6290 Y5$(1)=T1$; GOSUB 7300
6295 RETURN 
6900 REM "SORT
6910 IF POS("M"=V$(59,12))>0 THEN Q$=U$(1,10)+STR(A[1]:"000000000")+U$(11)
6915 IF POS("$"=V$(59,12))>0 THEN Q$=U$(1,10)+STR(A[13]:"000000000")+U$(11)
6940 WRITE (Z[10],KEY=Q$)
6945 PRINT @(30,22),Q$,
6990 RETURN 
7000 REM 
7001 IF T[T,0]<2 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7010 IF T$>"" THEN T$=T3$(1,T0+1-T)+" "+T$
7015 IF T$<>"" THEN Y5$(20+T0+2-T-P0)=T$+" "+Q2$+" ("+STR(T[T,0]:"0")+"):"
7020 IF V$(71,1)<>"3" AND V$(71,2)<>"11" THEN GOTO 7050
7030 Y5$(63-P0)=STR(T[T,1]:M0$),Y5$(72-P0)=STR(T[T,2]:M0$),Y5$(81-P0)=STR(T[T,3]:M0$)
7032 IF NUM(E$(226,1))=1 AND T[T,4]<=0 THEN Y5$(90-P0)=STR(0:M0$) ELSE Y5$(90-P0)=STR(T[T,4]:M0$)
7033 Y5$(99)=STR(T[T,5]:M0$)
7035 Q=0; SETERR 7036; Q=(T[T,4]+T[T,5])/T[T,3]; IF Q>999 THEN Q=999 ELSE IF Q<0 THEN Q=0
7036 SETERR 9000; Y5$(108-P0)=STR(Q*30:M1$); REM ",Y5$(113-P0)=STR(T(T,7):M1$)
7040 Y5$(118-P0)=STR(T[T,9]:M0$)
7050 GOSUB 7300
7080 GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300; GOSUB 7300
7096 IF V$(71,2)="11" THEN GOSUB 7300; GOSUB 7300; GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     13O ZZPARM  "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7435 DIM V[1]
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7455 IF POS("M"=V$(59,12))>0 THEN X1$="Min / Max Usage Report"
7456 IF POS("$"=V$(59,12))>0 THEN X1$="Costed Min/Max Report"
7480 X3$(178,7)=Y4$,V0=NUM(V$(71,1)),W3$=V$(19,POS("   "=V$(19,40)+"  ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "Customer Change
7510 READ (Z[6],KEY=U$(1,10))IOL=0360
7515 READ (Z[7],KEY="D"+F$(1,10))IOL=0370
7525 REM "Modified stmts:
7540 D0$=MID(G$,52,4)
7545 P0$=MID(G$,52,4),P1$=MID(G$,56,2),P0=NUM(P1$)
7550 CALL "GL2RZA",X3$,"E"+U$(1,10),P0$,P1$,W2$,Z[8],Z[7]
7590 RETURN 
7600 REM "Prior year periods
7610 IF NUM(FNE$(A1$(41,2)))<NUM(D1$) THEN F2=12; GOTO 7630 ELSE IF NUM(FNE$(A1$(41,2)))>NUM(D1$) THEN F2=0; GOTO 7630
7620 P0=POS(A1$(43,2)=H7$,2); F2=P0/2+.5
7640 RETURN 
7650 REM "Current year periods
7660 IF NUM(FNE$(A1$(41,2)))<NUM(D0$) THEN F1=12; GOTO 7680 ELSE IF NUM(FNE$(A1$(41,2)))>NUM(D0$) THEN F1=0; GOTO 7680
7670 P0=POS(A1$(43,2)=H7$,2); F1=P0/2+.5
7680 P0=POS(V0$(78,2)=H7$,2); F3=P0/2-.5
7685 F4=F1-F3
7695 RETURN 
7700 REM "Read current year usage
7710 X9=POS("U"=F1$,5),Q$="R",X$="U"
7715 DIM G[14]
7720 CALL "ZZPACK",X3$,Q$,F1$(X9+2,3),"",0,0,G{ALL},Z[5],X$,D$(1,24)+D0$,F9$
7725 Q[0]=Q[0]+G[P0]; FOR X=1 TO P0; Q[1]=Q[1]+G[X]; NEXT X
7740 RETURN 
7750 REM "Unpack prior year
7760 X9=POS("U"=F1$,5),Q$="R",X$="U"
7770 CALL "ZZPACK",X3$,Q$,F1$(X9+2,3),P9$,0,0,H{ALL},Z[5],X$,A$(1,24)+D1$,F9$
7790 RETURN 
7800 REM "GET OPEN P0 QTY FOR THIS FORM/LOC IN P9
7815 READ (Z[9],KEY=D$(1,24),DOM=7816)
7820 K$=KEY(Z[9],END=7890); IF K$(1,24)<>D$(1,24) THEN GOTO 7890 ELSE READ (Z[9])
7825 DIM J[4]; READ (Z[10],KEY=K$(25,12),DOM=7820)IOL=0400
7830 Q[5]=Q[5]+J[1]
7850 GOTO 7820
7895 RETURN 
7900 REM "CONVERT VALUES FOR PUBLIC CONSUMPTION
7910 FOR X=0 TO 5
7915 Q[X]=Q[X]/I0
7920 NEXT X
7990 RETURN 
8000 REM "FROM CUSTOMER CODE
8010 CALL "ZZDISP","AX",A$(1,10),"A/R",X3$,Q$,"",0,0,X4$
8020 Q$=Q$+", "+A$(21,5)
8022 Q$=" ("+Q$+")"
8025 Y5$(51-LEN(Q$))=Q$
8040 RETURN 
8100 REM "RETURN STATUS IN S9$
8105 S9$=""
8110 IF A[5]>0 AND A[6]>0 THEN IF A[5]<A[6] THEN S9$="^ BELOW MINIMUM ^"; GOTO 8180
8180 CALL "FM2MAZ",X3$,C$,S8$
8185 P=POS(C$(42,40)=S8$+S$); IF P>0 THEN S8$=S8$(1,P-1)
8190 RETURN 
8200 REM "Customer Change
8210 READ (Z[4],KEY=A$(1,10))IOL=0340
8220 READ (Z[5],KEY="D"+D$(1,10))IOL=0350
8225 P1$=E$(56,2),P0$=E$(52,4); CALL "GL2RZA",ERR=1000,X3$,"E"+D$(1,10),P0$,P1$,S7$,Z[6],Z[5]
8235 Q$=D$(1,10); GOSUB 8500; D1$="("+X$+")"
8279 REM "RESET PAGE NUMBER FOR EACH CUSTOMER
8280 IF W8<>0 THEN W8=.1
8290 RETURN 
8500 REM "RETUR  CUST CODE FROM Q$ IN X$
8510 CALL "ZZDISP","AX",Q$,"A/R",X3$,X$,"",0,0,X4$
8520 FIND (Z[4],KEY=Q$,DOM=8521)Q$; Q$=FNS$(Q$(11,35))
8540 RETURN 
8905 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8920 DEF FNB$(Z9$)=X3$(214-6*NUM(Z9$),1)
8940 DEF FNS$(Z$)=Z$(1,POS("   "=Z$+"   ")-1)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 IF Y5=68 THEN RETRY ELSE ON C9 GOTO 1140,2040
9750 REM "FILES
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
9790 RETURN 
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF NOT(NUL(Y4$)) THEN CALL Y4$,ERR=*NEXT ELSE RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
