0010 REM "Inventory Costing Transaction Report <FM2ICR>
0035 REM "5.7 - 10/13/22 - 9.789361 - tma - SSP# 307421
0037 REM "307421-DBSPT-149198 Using incorrect variable to build sorts in I/C 
0040 REM "Copyright 2022 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0080 REM "WO111482, copied from Valued Inventory Report
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 SETERR 9000; SETESC 9300
0110 X0$="FM2ICR",X1$="Inventory Costing Transaction Report"
0120 DIM A$(150),A[15],B$(187),B[40],C$(214),D$(32),E$(150),E[15],J$(46),FTG$(150),FTG[15],FTH$(150),FTH[15]
0130 DIM INV_WIP[50]; INV_WIP$=""
0140 Z0$="-##,###,##0.00",Z1$="-##,##0.00#",Z2$="-##,##0"
0150 LENGTH=LEN(Z1$)
0210 T=1,T0=0,T1=3
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR FTE - Inventory Costing Header
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15]
0315 REM "IOLIST FOR IC0 - Item Masterfile
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19],B[20],B[21],B[22],B[23],B[24],B[25],B[26],B[27],B[28],B[29],B[30],B[31],B[32],B[33],B[34],B[35],B[36],B[37],B[38],B[39],B[40]
0325 REM "IOLIST FOR FM0 - Warehouse Locations
0330 IOLIST C$
0335 REM "IOLIST FOR FMP2 - Product code file layout
0340 IOLIST D$
0345 REM "IOLIST FOR FTF - Inventory Costing Transactions
0350 IOLIST E$,E[0],E[1],E[2],E[3],E[4],E[5],E[6],E[7],E[8],E[9],E[10],E[11],E[12],E[13],E[14],E[15]
0370 IOLIST G$,G[0],G[1]
0385 IOLIST L$,L[0],L[1],L[2],L[3],L[4],L[5],L[6],L[7],L[8],L[9],L[10],L[11],L[12]
0390 IOLIST I$,I[0],I[1]
0400 IOLIST J$
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0430 IOLIST FTG$,FTG[0],FTG[1],FTG[2],FTG[3],FTG[4],FTG[5],FTG[6],FTG[7],FTG[8],FTG[9],FTG[10],FTG[11],FTG[12],FTG[13],FTG[14],FTG[15]
0440 IOLIST FTH$,FTH[0],FTH[1],FTH[2],FTH[3],FTH[4],FTH[5],FTH[6],FTH[7],FTH[8],FTH[9],FTH[10],FTH[11],FTH[12],FTH[13],FTH[14],FTH[15]
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0509 REM "Slot 10 will be used for temp file for unallocated only or non-zero only
0510 Y$="01O FTE...  05O IC0...  03O FTF...  04O FMP...  06O AR1...  07O FM1...  08O ICF...  09O AR3...  11O FTG...  12O FTH...  13O ZZPARM  14O FS1...  "; IF V1$(1,3)<>Y$(5,3) THEN Y$=Y$+"02O "+V1$+"  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0595 READ (Z[13],KEY=X3$(9,3)+"I/C")P0$
0700 REM "Open Printer
0720 REM 
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830
0820 DIM U[4]; U=34,U0=4,U[0]=0,U[1]=4,U[2]=10,U[3]=10,U[4]=10,T0=4,V1=4,V2=10,V3=10,V4=10,U0$="Y    ",T5$="S   AA/RAA/RS   ",U5$="Salesperson",U6$="Customer",U7$="Item's customer code",U8$="Item code"; GOTO 0850
0830 DIM U[2]; U=8,U0=1,U[0]=0,U[1]=8,T0=2,T1=2,V1=8,U0$="Y   ",T5$="AO/P",U5$="Order number"; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0880 IF V0$(72,1)="Y" THEN GOTO 8100; REM "Create and open temp sort, unallocated only
0885 IF V0$(73,1)="Y" THEN GOTO 8200; REM "Create and open temp sort, only non-zero orders
0890 IF V0$(74,1)="Y" THEN GOTO 8300; REM "Create and open temp sort, actual in progress only
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 IF V0$(71,1)="4" THEN V4$(14,1)=CHR(ASC(V4$(14,1))-1); READ (U1,KEY=V4$(1,14),DOM=0991) ELSE READ (U1,KEY=V4$,DOM=0991)
0995 D1$=X3$(21,6); IF V0=4 THEN D1=NUM(V0$(80,3),ERR=0996),D0$=D1$; CALL "ZZDATE",X3$,"",D0$,D1$,-D1,0,0,0,0,0,0,"","",""
0998 F0=0; IF V0=4 THEN F0=NUM(V0$(83,7),ERR=0999)
1000 REM "Read next record
1005 U$=KEY(U1,END=5000); READ (U1)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])>=V2$(U3,U[U9]) THEN GOTO 1030 ELSE EXTRACT (U1,KEY=U$(1,U2-1)+V2$(U3,U[U9]),DOM=1021)
1025 EXITTO 1000
1030 IF U$(U2,U[U9])<=V2$(U3+U[U9],U[U9]) THEN GOTO 1040 ELSE READ (U1,KEY=U$(1,U2-1)+$FF$,DOM=1032)
1035 EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140,1145
1130 U$=U9$(1,11); GOTO 1155
1150 U$=U9$(35,11)
1155 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1160 IF Q5=1 OR Q5=3 OR Q5=4 THEN GOTO 1993
1165 IF Q5=2 THEN GOSUB 5400; GOTO 1000; REM "Moving records to history
1300 REM "Read other data
1320 DIM B$(LEN(B$)),H8$(40); FIND (Z[5],DOM=1321,KEY=A$(26,20))IOL=0320; H8$=B$(21,40); GOTO 1331
1325 DIM FM1$(448); FIND (Z[7],KEY=A$(26,20),DOM=1326)FM1$; H8$=FM1$(42,40); GOTO 1331
1330 DIM SPECIAL$(61); FIND (Z[4],KEY="X"+A$(36,10),DOM=1331)SPECIAL$; H8$=SPECIAL$(12,40)
1340 DIM D$(LEN(D$)); FIND (Z[4],KEY="T"+A$(46,3),DOM=1341)IOL=0340
1350 DIM J$(46); FIND (Z[6],KEY=A$(16,10),DOM=1351)IOL=0400
1400 REM "Subtotals"
1415 GOSUB 2400
1500 REM "Print Data
1515 GOSUB 2300; REM "FTE record
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+A[3]
1920 T[T0,2]=T[T0,2]+A[4]
1990 T[T0,0]=T[T0,0]+1
1993 GOSUB 2000
1995 GOTO 1000
2000 REM "Print FTF records
2005 DIM TOTALS_1[5],TOTALS_2[5]; TOT_REC=0
2010 IF Q5<>1 AND Q5<>3 AND Q5<>4 THEN GOSUB 2600
2015 READ (Z[3],KEY=A$(1,11),DOM=2016)
2016 LINE$="001"
2020 K$=KEY(Z[3],END=2080); READ (Z[3],KEY=K$)IOL=0350
2021 IF K$(1,11)<>A$(1,11) THEN GOTO 2080
2022 IF Q5=1 THEN IF E$(15,3)<>"000" AND E[6]<>0 THEN PRINT @(30),U$(1,11),; GOSUB 8450; WRITE (Z[10],KEY=SORT_KEY$); GOTO 2020 ELSE GOTO 2020
2023 IF Q5=3 THEN IF E$(15,3)="000" AND (E[3]<>0 OR E[4]<>0 OR E[5]<>0 OR E[6]<>0) THEN PRINT @(30),U$(1,11),; GOSUB 8450; WRITE (Z[10],KEY=SORT_KEY$); GOTO 2020 ELSE GOTO 2020; REM "SSP118531!SSP#307421 DBSPT_149198
2024 IF Q5=4 THEN IF E$(15,3)="000" AND E[5]<>0 THEN PRINT @(30),U$(1,11),; GOSUB 8450; WRITE (Z[10],KEY=SORT_KEY$); GOTO 2020 ELSE GOTO 2020 ! SSP#307421 DBSPT_149198
2025 IF E$(12,3)<>LINE$ THEN GOSUB 2100
2028 IF W+4>W0 THEN GOSUB 6000; GOSUB 2600
2029 IF E$(15,3)="000" THEN GOSUB 2500; FTF0$=E$; DIM FTF0[15]; FOR XX=0 TO 15; FTF0[XX]=E[XX]; NEXT XX; GOSUB 7650; GOTO 2070
2030 GOSUB 2650; REM "Print FTF info in E$
2070 IF E$(15,3)<>"000" THEN TOT_UNALLOC=TOT_UNALLOC+E[6],TOTALS_1[0]=TOTALS_1[0]+1,TOT_REC=TOT_REC+E[2],TOTALS_1[1]=TOTALS_1[1]+B9; FOR X=2 TO 5; TOTALS_1[X]=TOTALS_1[X]+E[X+1]; NEXT X ELSE TOTALS_2[0]=TOTALS_2[0]+1,TOTALS_2[1]=TOTALS_2[1]+B9; FOR X=2 TO 5; TOTALS_2[X]=TOTALS_2[X]+E[X+1]; NEXT X
2072 IF E$(15,3)="000" OR STP(E$(56,12),3," ")="" THEN GOTO 2075
2073 P=POS(E$(56,12)=INV_WIP$); IF P=0 THEN INV_WIP$=INV_WIP$+E$(56,12); GOTO 2073
2074 P1=INT(P/12),DIMMED=FN%NEA("INV_WIP",1); IF P1>DIMMED THEN CALL "INSERT_ARRAY;NUMERIC",INV_WIP{ALL},DIMMED,50 END_IF ; INV_WIP[P1]=INV_WIP[P1]+E[5]
2075 GOTO 2020
2080 IF Q5<>1 AND Q5<>3 AND Q5<>4 THEN GOSUB 2150
2095 RETURN 
2100 REM "Print totals for each line if more than one transaction
2101 IF TOT_REC=0 THEN GOTO 2145; REM " SSP 174788 TO CORRECT ERROR 42 AT 2105
2104 IF W+5>W0 THEN GOSUB 6000; GOSUB 2600
2105 IF TOT_REC<>FTF0[2] OR TOTALS_1[2]<>FTF0[3] OR TOTALS_1[3]<>FTF0[4] OR TOTALS_1[4]<>FTF0[5] OR TOTALS_1[5]<>FTF0[6] THEN Y5$(6)="->***  Cleared Detail:",Y5$(62)=FNQ$(FTF0[2]-TOTALS_1[1],Z0$),Y5$(93,LEN(Z1$))=STR(FTF0[3]-TOTALS_1[2]:Z1$),Y5$(106)=STR(FTF0[4]-TOTALS_1[3]:Z1$),Y5$(80,LEN(Z1$))=STR(FTF0[5]-TOTALS_1[4]:Z1$),Y5$(119)=STR(FTF0[6]-TOTALS_1[5]:Z1$); GOSUB 7300
2110 IF TOTALS_1[0]>=1 THEN Y5$(62,LENGTH)=T1$(1,LENGTH),Y5$(80,LENGTH)=T1$(1,LENGTH),Y5$(93,LENGTH)=T1$(1,LENGTH),Y5$(106,LENGTH)=T1$(1,LENGTH),Y5$(119,LENGTH)=T1$(1,LENGTH); GOSUB 7300
2115 SUMMARY=1; FTFH$=E$,E$=FTF0$; DIM FTFH[15]; FOR XX=0 TO 15; FTFH[XX]=E[XX],E[XX]=FTF0[XX]; NEXT XX
2116 GOSUB 2650
2117 E$=FTFH$; FOR XX=0 TO 15; E[XX]=FTFH[XX]; NEXT XX
2135 GOSUB 7300; GOSUB 7300
2140 DIM TOTALS_1[5]; LINE$=E$(12,3),TOT_REC=0
2145 RETURN 
2150 REM "Print total of transactions for this order if more than one
2155 GOSUB 2100
2160 IF TOTALS_2[0]<=1 THEN GOSUB 7300; GOTO 2190
2165 IF W+4>W0 THEN GOSUB 6000; GOSUB 2600
2170 Y5$(62,LENGTH)=T1$(1,LENGTH),Y5$(80,LENGTH)=T1$(1,LENGTH),Y5$(93,LENGTH)=T1$(1,LENGTH),Y5$(106,LENGTH)=T1$(1,LENGTH),Y5$(119,LENGTH)=T1$(1,LENGTH)
2175 GOSUB 7300
2180 CALL "ZZDISP","AX",A$(1,8),"O/P",X3$,X$,"",0,0,X4$; Y5$(5)="Order "+X$+" Total ("+STR(TOTALS_2[0])+"):",Y5$(62)=FNQ$(TOTALS_2[1],Z0$),Y5$(93)=STR(TOTALS_2[2]:Z1$),Y5$(106)=STR(TOTALS_2[3]:Z1$),Y5$(80,LEN(Z1$))=STR(TOTALS_2[4]:Z1$),Y5$(119)=STR(TOTALS_2[5]:Z1$)
2185 GOSUB 7300; GOSUB 7300
2190 DIM TOTALS_2[5]
2195 RETURN 
2200 REM "Print customer heading
2205 IF V0=1 THEN IF W+4>W0 THEN GOSUB 6000
2210 CALL "ZZDISP","AX",J$(1,10),"A/R",X3$,X$,"",0,0,X4$; Y5$(1)=X$,Y5$(15)=J$(11,35)
2215 GOSUB 7300
2220 IF V0=1 THEN GOSUB 7300
2245 RETURN 
2250 REM "Set heading for salesperson
2255 IF V0=2 THEN W0$="By Order Number"; GOTO 2295
2260 DIM J1$(50); FIND (Z[9],KEY=U9$(1,4),DOM=2261)J1$
2270 W0$=U9$(1,4)+" - "+FNS$(J1$(5,35))
2295 RETURN 
2300 REM "Print FTE data
2305 GOSUB 7600
2308 IF W+8>W0 THEN GOSUB 6000
2310 CALL "ZZDISP","AX",A$(1,8),"O/P",X3$,X$,"",0,0,X4$; Y5$(3)=X$,Y5$(18)=A$(9,3)
2315 Y5$(24)=FNQ$(A0,Z0$),Y5$(43)=FNQ$(B9,Z0$),Y5$(60)=A0$,Y5$(74)=STR(A[3]:Z1$),Y5$(90)=STR(A[4]:Z1$)
2325 GOSUB 7300
2330 IF A$(61,1)="Y" THEN Y5$(1)="*****THIS ORDER WAS DELETED WITH NON-PRODUCT COSTS, A TRANSACTION WAS CREATED TO MOVE COSTS TO UNALLOCATED, NEED TO MAKE JE*****"; GOSUB 7300; DELETED$="Y"; REM "SSP129970
2335 GOSUB 7300
2337 IF V0=2 THEN GOSUB 2200; GOSUB 2350
2340 RETURN 
2350 REM "Print FTE item and description data
2353 IF W+4>W0 THEN GOSUB 6000
2360 CALL "ZZDISP","AX",A$(26,20),"I/C",X3$,X$,"",0,0,X4$; Y5$(1)=X$
2363 GOSUB 8000
2365 Y5$(13)=H9$
2370 GOSUB 7300
2375 IF V0=2 THEN GOSUB 7300
2390 RETURN 
2400 REM "Bring subtotal logic here so they can be done right on V0=2
2415 GOSUB 3100
2420 IF V0=2 THEN ON T GOTO 2470,2470,2440,2450,2460
2425 IF V0=3 THEN ON T GOTO 2470,2430,2430,2450,2460
2426 IF V0=1 THEN ON T GOTO 2470,2430,2430,2460,2440
2430 GOSUB 2200
2440 GOSUB 6300
2460 IF V0=4 THEN GOSUB 2350
2480 T=0
2490 RETURN 
2500 REM "Print seq 000 info
2505 CALL "ZZDISP","AX",E$(19,10),"A/P",X3$,VEND$,"",0,0,X4$
2510 Y5$(6)="Line: "+E$(12,3),Y5$(18)="Item: "+E$(68,10)+"  P/O code: "+E$(18,1)+"  Vendor: "+VEND$
2530 GOSUB 7300
2545 RETURN 
2600 REM "Print FTF record header
2605 IF W+4>W0 THEN GOSUB 6000
2610 Y5$(8)="Seq",Y5$(13)="Invoice #",Y5$(25)="Inv date",Y5$(38)="Audit #",Y5$(48)="G/L Acct no ",Y5$(80)="Act in Prog",Y5$(93)="Est in Inv",Y5$(106)="Act in Inv",Y5$(119)="Unallocated"
2615 GOSUB 7300
2620 Y5$(38)="U/M",Y5$(48)="Ordered",Y5$(64)="Received"
2625 GOSUB 7300
2640 GOSUB 7300
2645 RETURN 
2650 REM "Given E$ and E[all] print seq info, if SUMMARY then print in summary style with totals if needed (based on count )
2658 IF SUMMARY THEN Y5$(6)=DIM(5,"-")+"Line "+E$(12,3)+" Summary "+DIM(5,"-"); GOTO 2664 ELSE Y5$(8)=E$(15,3)
2660 Y5$(13)=E$(29,10),Y5$(25)=FND$(E$(39,6)); IF E$(45,6)<>"      " THEN Y5$(38)=STR(NUM(E$(45,6),ERR=*NEXT)/100:"###0.00")
2662 IF POS(" "<>E$(56,12))>0 THEN CALL "ZZDISP","AX",E$(56,12),"G/L",X3$,X$,"",0,0,X4$; Y5$(48)=X$
2664 Y5$(93)=STR(E[3]:Z1$),Y5$(106)=STR(E[4]:Z1$),Y5$(80,LEN(Z1$))=STR(E[5]:Z1$),Y5$(119)=STR(E[6]:Z1$)
2666 GOSUB 7300
2668 GOSUB 7650
2670 IF A0<>0 OR B9<>0 THEN Y5$(43-LEN(A0$))=A0$,Y5$(45)=FNQ$(A0,Z0$),Y5$(62)=FNQ$(B9,Z0$); GOSUB 7300
2690 SUMMARY=0
2695 RETURN 
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$; T6$=T9$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOSUB 2250; GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; GOSUB 7000
3155 IF U0$(1,1)="Y" AND T0$<>"END" THEN U1$=U9$(1,V1); GOSUB 2250; GOSUB 6000
3160 U1$=U9$(1,V1)
3180 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$,T6$=U2$; GOSUB 3050; GOSUB 7000
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3260 U2$=U9$(V5,V2)
3280 RETURN 
3310 IF V3=0 THEN RETURN ELSE IF U3$=U9$(V6,V3) THEN GOSUB 3400; RETURN 
3320 GOSUB 3420
3330 IF U3$="" THEN GOTO 3360
3350 T=3,T$=U7$,T6$=U3$; GOSUB 3050; GOSUB 7000
3355 IF U0$(3,1)="Y" THEN GOSUB 6000
3360 U3$=U9$(V6,V3)
3380 RETURN 
3410 IF V4=0 THEN RETURN ELSE IF U4$=U9$(V7,V4) THEN GOSUB 3500; RETURN 
3420 GOSUB 3520
3430 IF U4$="" THEN GOTO 3460
3450 T=4,T$=U8$,T6$=U4$; GOSUB 3050; GOSUB 7000
3455 IF U0$(4,1)="Y" THEN GOSUB 6000
3460 U4$=U9$(V7,V4)
3480 RETURN 
3580 RETURN 
5000 REM "End of Print
5005 REM "Q5=1, temp sort unallocated only, Q5=2, move to history, Q5=3, temp sort non-zero orders only, Q5=4, temp sort actual in progress only
5010 IF Q5=1 THEN Q5=0,V1$="TMP"+FID(0); GOTO 0881
5015 IF Q5=2 THEN GOTO 9920; REM "Done with move phase
5017 IF Q5=3 THEN Q5=0,V1$="TMP"+FID(0); GOTO 0886
5019 IF Q5=4 THEN Q5=0,V1$="TMP"+FID(0); GOTO 0891
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; IF V0=3 THEN GOSUB 6000; GOSUB 7000 ELSE GOSUB 7000
5200 REM "Print inventory work in progress recap
5210 IF INV_WIP$="" THEN GOTO 5260
5215 IF W+4>W0 THEN GOSUB 6000
5220 GOSUB 7300; Y5$(7)="**Inventory Work in Progress**"; GOSUB 7300
5225 P=LEN(INV_WIP$)/12
5230 FOR X=1 TO P
5235 CALL "ZZDISP","AX",INV_WIP$((X-1)*12+1,12),"G/L",X3$,X$,"",0,0,X4$
5240 Y5$(5)=X$
5245 Y5$(25)=STR(INV_WIP[X-1]:Z0$)
5250 GOSUB 7300
5255 NEXT X
5260 REM "print total unallocated
5270 IF TOT_UNALLOC=0 THEN GOTO 5286
5275 IF W+3>W0 THEN GOSUB 6000
5280 GOSUB 7300; GOSUB 7300
5285 Y5$(5)="****Total Unallocated****"+STR(TOT_UNALLOC:Z0$); GOSUB 7300
5287 IF DELETED$<>"Y" THEN GOTO 5290
5288 IF W+3>W0 THEN GOSUB 6000
5289 GOSUB 7300; GOSUB 7300; Y5$(3)="***Check for orders deleted with invoiced costs and make appropriate JE's to reduce Inventory Work in Progress***"; GOSUB 7300
5290 GOTO 9900
5300 REM "Ask to move records to history if order closed & lots gone
5305 IF %MULT_RPT_DRV_MODE OR %TOD THEN GOTO 9920 ! SSP 228228 SSP 240570
5310 CALL "ZZPROM","X0FM2ICR",X3$,Z,"","","",0; ON Z GOTO 5311,9920
5320 PRINT @(0,20),'CE',@(23,20),"Now moving:",
5330 Q5=2; GOTO 0900
5400 REM "Using FTE records, read for order, if closed then read for any remaining lots in original warehouse, if none then create FTG/FTH records and remove FTE/FTF records
5410 READ (Z[14],KEY=A$(1,8),DOM=5411); GOTO 5490; REM "Read for order header
5420 READ (Z[8],KEY=A$(26,20)+A$(54,4),DOM=5421)
5425 K$=KEY(Z[8],END=5445); READ (Z[8],KEY=K$)ICF$
5430 IF K$(1,24)<>A$(26,20)+A$(54,4) THEN GOTO 5445
5435 IF ICF$(41,8)<>A$(1,8) THEN GOTO 5425
5440 GOTO 5490; REM "Found a match, don't do move
5445 REM "No order header, not lots, start move process
5448 PRINT @(35,20),A$(1,8)+" "+A$(9,3),
5450 READ (Z[3],KEY=A$(1,11),DOM=5451)
5455 K$=KEY(Z[3],END=5480); READ (Z[3],KEY=K$)IOL=0350
5457 IF K$(1,11)<>A$(1,11) THEN GOTO 5480
5460 FTH$=E$; FOR X=0 TO 15; FTH[X]=E[X]; NEXT X
5465 WRITE (Z[12],KEY=FTH$(1,17))IOL=0440
5470 REMOVE (Z[3],KEY=K$)
5475 GOTO 5455
5480 FTG$=A$; FOR X=0 TO 15; FTG[X]=A[X]; NEXT X
5485 WRITE (Z[11],KEY=FTG$(1,11))IOL=0430
5487 REMOVE (Z[1],KEY=A$(1,11))
5490 RETURN 
6000 REM "Page header
6010 IF V0$(72,1)="Y" THEN W2$="Only Orders With Unallocated Cost" ELSE IF V0$(73,1)="Y" THEN W2$="Only Non-Zero Orders" ELSE IF V0$(74,1)="Y" THEN W2$="Only Orders with Actual in Progress"
6020 IF T$="Report" THEN W0$=""
6035 IF T0$="END" THEN W0$=""
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6060 ELSE ON W5 GOTO 6041,9910 ! SSP#241151
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6115 Y5$(3)="Item Code",Y5$(13)="Description",Y5$(28)="Quantity",Y5$(47)="Quantity",Y5$(75)="Estimated",Y5$(91)="Invoiced"
6120 GOSUB 7300
6125 Y5$(3)="Order #",Y5$(15)="Sequence #",Y5$(28)="Ordered",Y5$(47)="Received",Y5$(64)="UM",Y5$(74)="in Inventory",Y5$(89)="Cost to Date"
6130 GOSUB 7300
6199 RETURN 
6200 REM "Break #1 header
6201 RETURN 
6210 IF W+2>W0 THEN GOSUB 6000
6220 Y5$(1)=A$(21,4),Y5$(7)=C$(16,35)
6225 GOSUB 7300
6230 GOSUB 7300
6299 RETURN 
6300 REM "Break #2 header
6305 IF V0=1 THEN GOSUB 2350; RETURN 
6399 RETURN 
6900 REM "SORT
6905 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6940 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,X9
6990 RETURN 
7000 REM "Totals logic
7001 IF T[T,0]<2 OR T=3 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(90)=T1$(1,14); GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" Total"+" ("+STR(T[T,0]:"0")+"):"
7020 Y5$(86)=STR(T[T,2]:Z0$); GOSUB 7300
7040 IF V0=4 AND F0<>0 THEN Y5$(90)="Finance charge @ "+FNR$(FNS$(STR(F0:"##0.0##"))+" %")+":",Y5$(118)=STR(T[T,2]:Z0$); GOSUB 7300
7065 IF T$="Report" THEN Y5$(86)=T2$(1,14); GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed"+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600),T4=MOD(T3,60),T3=INT(T3/60)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")+":"+STR(T4:"00")
7260 Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7330 REM 
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"  ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
7600 REM "Figure numerics, put total into A0, UM string into A0$
7601 REM "This will change the values of A() also
7605 DIM I$(20),I[1]
7610 FIND (Z[13],KEY="U/M"+A$(49,4),DOM=7611)IOL=0390
7620 IF I$(20,1)="Y" THEN I0=I[1],A0$=A$(49,4) ELSE I0=A[0],A0$=FNS$(A$(49,4))+"/"+FNR$(STR(A[0]:Z2$))
7625 IF I0=0 THEN I0=1
7630 A[1]=A[1]/I0,A[5]=A[5]/I0
7640 A0=A[1],B9=A[5]
7645 RETURN 
7650 REM "Same routine as 7600's for E[]
7655 DIM I$(20),I[1]
7660 FIND (Z[13],KEY="U/M"+E$(51,4),DOM=7661)IOL=0390
7670 IF I$(20,1)="Y" THEN I0=I[1],A0$=E$(51,4) ELSE I0=E[0],A0$=FNS$(E$(51,4))+"/"+FNR$(STR(E[0]:Z2$))
7675 IF I0=0 THEN I0=1
7680 REM "LET E[1]=E[1]/I0, E[2]=E[2]/I0
7690 A0=E[1]/I0,B9=E[2]/I0
7695 RETURN 
8000 REM "Build desc from Y8$
8005 H9$=H8$; RETURN 
8010 H9$=FNS$(H8$(64,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+"x"
8020 H9$=H9$+FNS$(H8$(72,8)); IF H9$<>"" THEN IF POS(" "<>Y8$(72,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
8030 IF NUM(H8$(80,2))<>0 THEN H9$=H9$+H8$(80,2)+" Ply "
8040 H9$=H9$+FNS$(H8$(21,40)); H8$=""
8050 RETURN 
8100 REM "Create and open temp sort file for printing only unallocated orders
8105 GOSUB 8400
8110 Y$=FILE$+" Sort Unallocated Only"
8115 CALL "ZZ2SRT",X3$,X4$,Z[13],FILE_CHANNEL,KEY_SIZE,Y$,Z,"YY"
8120 IF Z=2 THEN GOTO 9920
8123 Y$="10O "+Y$,Y9$=Y$
8125 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
8130 Q5=1
8140 GOTO 0900
8200 REM " Create and open temp sort file for printing only non-zero orders
8205 GOSUB 8400
8210 Y$=FILE$+" Sort Non-Zero Orders Only"
8215 CALL "ZZ2SRT",X3$,X4$,Z[13],FILE_CHANNEL,KEY_SIZE,Y$,Z,"YY"
8220 IF Z=2 THEN GOTO 9920
8223 Y$="10O "+Y$,Y9$=Y$
8225 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
8230 Q5=3
8240 GOTO 0900
8300 REM "Create and open temp sort file for printing only orders with actual in progress
8305 GOSUB 8400
8310 Y$=FILE$+" Sort Actual in Progress Only"
8315 CALL "ZZ2SRT",X3$,X4$,Z[13],FILE_CHANNEL,KEY_SIZE,Y$,Z,"YY"
8320 IF Z=2 THEN GOTO 9920
8323 Y$="10O "+Y$,Y9$=Y$
8325 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
8330 Q5=4
8340 GOTO 0900
8400 REM "Set file and key size for temp file based on sequence we're in
8410 IF V0=1 THEN FILE$="FTJ",KEY_SIZE=45,FILE_CHANNEL=Z[2] ELSE IF V0=2 THEN FILE$="FTE",KEY_SIZE=11,FILE_CHANNEL=Z[1]
8445 RETURN 
8450 REM "Set sort key based on sequence we're in
8460 IF V0=1 THEN SORT_KEY$=A$(12,34)+A$(1,11) ELSE IF V0=2 THEN SORT_KEY$=A$(1,11)
8495 RETURN 
8900 REM "Function Statements
8905 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
8920 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
8940 DEF FNC$(Z9$)=Z9$(1,POS(" "=Z9$+" ")-1)
8950 DEF FNQ$(Z9,Z9$)=STR(Z9:Z9$(1,POS(".00"=STR(Z9:Z9$)+".00")-1))
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9915 IF V3$(2,1)<>"T" THEN GOTO 5300
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 IF %GUI THEN EXIT 
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 RUN "ZMENU"
9999 END 
56000 ! "228228-Ability to split a report into multiple PDFs based on sales 
56001 REM "241151-Inventory Costing Transaction Report has two minor issues   
56002 REM "240570-GUI Printing - Support for queueing and scheduling reports. 
56003 REM "307421-DBSPT-149198 Using incorrect variable to build sorts in I/C 
