0010 REM "Custom Forms Sales Analysis <FM2RJA>
0035 REM "5.7 - 01/06/12 - 9.202222 - tma - SSP# 251767
0036 REM "10/30/91 Mod to fix range logic JSC
0037 REM "251767-changes on Custom Item Sales Analysis. Panel is FMGRJS      
0040 REM "Copyright 2012 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 SETERR 9000; SETESC 9300
0110 X0$="FM2RJA",X1$="Custom Item Sales Analysis" ! ssp#251767
0120 DIM A$(195),A[21],D$(50),E$(100),F$(100)
0140 Z0$="####,##0-",Z1$="###,###,##0-",Z2$="-###.0%"
0160 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0210 T=1,T0=1,T1=1
0220 W3=130
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0280 X0$="FM2RJA",X1$="Custom Item Sales Analysis" ! ssp#251767
0300 REM "I/O lists
0305 REM "IOLIST FOR FM1 - Customer Job Master
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21]
0330 IOLIST C$
0340 IOLIST D$
0350 IOLIST E$
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O FM1...  03O FMP...  04O AR3...  05O AR1...  "; IF V1$(1,3)<>Y$(5,3) THEN Y$=Y$+"02O "+V1$+"  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0700 REM "Open Printer
0720 REM 
0750 REM "Company description
0760 READ (Z[13],KEY="CMP"+X3$(9,3))C0$
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[2]; U=20,U0=2,U[0]=0,U[1]=10,U[2]=10,T0=1,T1=8,V1=10,U0$="Y   ",T5$="AA/R            ",U5$="Customer"; IF V0$(72,1)="N" THEN U0$="    "; GOTO 0850 ELSE GOTO 0850
0830 DIM U[4]; U=21,U0=4,U[0]=0,U[1]=3,U[2]=8,U[3]=8,U[4]=2,T0=4,T1=8,V1=3,V2=8,V3=8,V4=2,U0$="Y    ",T5$="S   S   S   S   ",U5$="Product Code",U6$="Left-Right",U7$="Top-Bottom",U8$="Plys"; GOTO 0850
0840 DIM U[2]; U=14,U0=2,U[0]=0,U[1]=4,U[2]=10,T0=2,T1=8,V1=4,V2=10,U0$="Y   ",T5$="S   AA/R        ",U5$="Salesperson",U6$="Customer"; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(U1,END=5000)
1006 IF V0=3 THEN READ (U1)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN READ (U1,END=5000); EXITTO 1000
1030 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U9=1 THEN EXITTO 5000 ELSE READ (U1,END=5000); EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,8300
1130 U$=U9$(32,10)+U9$(22,10); READ (U1); GOTO 1150
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1155 IF %X3$(333,2)<>"  " AND A$(1,2)<>%X3$(333,2) THEN GOTO 1000 ! SSP 221450
1160 DIM A1$(LEN(A$)); A1$(1,LEN(A$))=A$
1300 REM "Read other data
1400 REM "Subtotals"
1415 GOSUB 3100
1420 ON T GOTO 1470,1430,1430
1430 GOSUB 6200
1490 T=0
1500 REM "Print Data
1505 IF V0$(72,1)="N" THEN GOTO 1900
1510 IF W+4>W0 THEN GOSUB 6000
1520 H8$=A$(1,83); GOSUB 8000
1540 Y5$(1)=A1$(11,10),Y5$(13)=H9$
1560 Y5$(81)="YTD "+STR(A[9]:Z1$)
1570 IF A[10]<>0 THEN I=A[11]*100/A[10] ELSE I=0
1580 Y5$(85+LEN(Z1$))=STR(A[10]:Z0$)+STR(A[11]:Z0$)+STR(I:Z2$)+STR(A[8]:Z0$)
1600 GOSUB 7300
1610 IF V0=2 THEN GOSUB 7650; Y5$(13)=F$(11,35)
1620 Y5$(71)="All Last Year",Y5$(85)=STR(A[13]:Z1$)
1630 IF A[14]<>0 THEN I=A[15]*100/A[14] ELSE I=0
1640 Y5$(85+LEN(Z1$))=STR(A[14]:Z0$)+STR(A[15]:Z0$)+STR(I:Z2$)+STR(A[12]:Z0$)
1800 GOSUB 7300; GOSUB 7300
1900 REM "Accumulate Totals"
1990 T[T0,0]=T[T0,0]+1
1992 T[T0,1]=T[T0,1]+A[9],T[T0,2]=T[T0,2]+A[10],T[T0,3]=T[T0,3]+A[11],T[T0,4]=T[T0,4]+A[8]
1994 T[T0,5]=T[T0,5]+A[13],T[T0,6]=T[T0,6]+A[14],T[T0,7]=T[T0,7]+A[15],T[T0,8]=T[T0,8]+A[12]
1995 IF V0=3 THEN GOTO 8315 ELSE GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,X$,"",0,0,X4$; T6$=X$ ! SSP#251767
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3125 P1=1
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; GOSUB 7000
3160 U1$=U9$(1,V1)
3175 IF V0=1 THEN P9=0; GOSUB 7700 ELSE IF V0=2 THEN GOSUB 7600 ELSE IF V0=3 THEN GOSUB 8400
3177 IF U0$(1,1)="Y" THEN GOSUB 6000
3180 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$,T6$=U2$; IF V0=2 THEN GOSUB 8200; GOSUB 7000 ELSE GOSUB 3050; GOSUB 7000
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3260 U2$=U9$(V5,V2)
3280 RETURN 
3300 REM "Next break
3310 IF V3=0 THEN RETURN ELSE IF U3$=U9$(V6,V3) THEN GOSUB 3400; RETURN 
3320 GOSUB 3420
3330 IF U3$="" THEN GOTO 3360
3350 T=3,T$=U7$,T6$=U3$; IF V0=2 THEN GOSUB 8150; GOSUB 7000 ELSE GOSUB 3050; GOSUB 7000
3355 IF U0$(3,1)="Y" THEN GOSUB 6000
3360 U3$=U9$(V6,V3)
3380 RETURN 
3400 REM "Last break
3410 IF V4=0 THEN RETURN ELSE IF U4$=U9$(V7,V4) THEN GOSUB 3500; RETURN 
3420 GOSUB 3520
3430 IF U4$="" THEN GOTO 3460
3450 T=4,T$=U8$,T6$=U4$; IF V0=2 THEN GOSUB 8100; GOSUB 7000 ELSE GOSUB 3050; GOSUB 7000
3455 IF U0$(4,1)="Y" THEN GOSUB 6000
3460 U4$=U9$(V7,V4)
3480 RETURN 
3580 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; GOSUB 7000
5200 REM "
5290 GOTO 9900
6000 REM "Page header
6035 IF T0$="END" THEN W0$=""
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6060 ELSE ON W5 GOTO 6041,9910
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6065 P1=0
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6110 REM "Sub header
6120 IF W+2>W0 THEN GOSUB 6000
6150 Y5$(1)="Item",Y5$(13)="Description",Y5$(89)="Quantity  $Sales   Gross Profit  #Invoices"
6160 GOSUB 7300
6190 RETURN 
6200 REM "Break #1 header
6205 IF V0<>3 THEN GOTO 6280
6208 IF W+4>W0 THEN GOSUB 6000
6210 DIM E$(100); FIND (Z[5],KEY=U9$(5,10),DOM=6211)IOL=0350
6215 CALL "ZZDISP","AX",U9$(5,10),"A/R",X3$,X$,"",0,0,X4$
6220 Y5$(1)=X$+" "+E$(11,35)
6225 GOSUB 7300
6230 GOSUB 7300
6280 RETURN 
6300 REM "Break #2 header
6390 RETURN 
6900 REM "SORT
6905 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6940 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,X9
6990 RETURN 
7000 REM "Totals logic
7001 IF T[T,0]<2 THEN IF T<>T0 THEN GOTO 7100 ELSE IF V0$(72,1)<>"N" THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7010 IF V0$(72,1)="N" THEN Y5$(1)=T$ ELSE Y5$(1)=T3$(1,T0+1-T)+" "+T$
7020 Y5$(81)="YTD "+STR(T[T,1]:Z1$)
7025 IF T[T,2]<>0 THEN I=T[T,3]*100/T[T,2] ELSE I=0
7030 Y5$(85+LEN(Z1$))=STR(T[T,2]:Z0$)+STR(T[T,3]:Z0$)+STR(I:Z2$)+STR(T[T,4]:Z0$)
7035 GOSUB 7300
7040 Y5$(71)="All Last Year",Y5$(85)=STR(T[T,5]:Z1$)
7045 IF T[T,6]<>0 THEN I=T[T,7]*100/T[T,6] ELSE I=0
7050 Y5$(85+LEN(Z1$))=STR(T[T,6]:Z0$)+STR(T[T,7]:Z0$)+STR(I:Z2$)+STR(T[T,8]:Z0$)
7055 GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600),T4=MOD(T3,60),T3=INT(T3/60)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")+":"+STR(T4:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     13O ZZPARM  "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"  ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
7600 REM "Load product code into title
7605 IF U1$<>MID(C$,2,3) THEN DIM C$(30); FIND (Z[3],KEY="T"+U1$,DOM=7606)IOL=0330
7610 W0$=U1$+" - "+FNS$(C$(5,15))
7640 RETURN 
7650 REM "Get customer name for item
7660 IF A$(1,10)<>F$(1,10) THEN DIM F$(100); FIND (Z[5],KEY=A$(1,10),DOM=7661)F$
7690 RETURN 
7700 REM "Load customer name into title
7710 GOSUB 7650
7720 W0$=FNS$(F$(11,35))
7740 RETURN 
8000 REM "Build desc from H8$
8010 H9$=FNS$(H8$(24,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>H8$(32,8))<>0 THEN H9$=H9$+" x "
8020 H9$=H9$+FNS$(H8$(32,8)); IF H9$<>"" THEN IF POS(" "<>H8$(32,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
8030 IF NUM(H8$(40,2))<>0 THEN H9$=H9$+H8$(40,2)+" Ply "
8040 H9$=H9$+FNS$(H8$(42,40)); H8$=""
8050 RETURN 
8100 REM "Ply subtotal title
8110 T$=FNS$(U2$); IF T$<>"" THEN T$=T$+QUO; IF POS(" "<>U3$)<>0 THEN T$=T$+" x "
8120 T$=T$+FNS$(U3$); IF T$<>"" THEN IF POS(" "<>U3$)<>0 THEN T$=T$+QUO+" " ELSE T$=T$+" "
8130 IF NUM(U4$)<>0 THEN T$=T$+U4$+" Ply"
8140 RETURN 
8150 REM "Top Bottom subtotal title
8160 T$=FNS$(U2$); IF T$<>"" THEN T$=T$+QUO; IF POS(" "<>U3$)<>0 THEN T$=T$+" x "
8170 T$=T$+FNS$(U3$); IF T$<>"" THEN IF POS(" "<>U3$)<>0 THEN T$=T$+QUO+" " ELSE T$=T$+" "
8190 RETURN 
8200 REM "Left Right subtotal title
8210 T$=FNS$(U2$); IF T$<>"" THEN T$=T$+QUO; IF POS(" "<>U3$)<>0 THEN T$=T$+" wide"
8240 RETURN 
8300 REM "Logic to print one (U8$(5,10)) Customer's records out of FM1
8305 U8$=U$
8310 READ (Z[1],KEY=U8$(5,10),DOM=8311)
8315 U$=KEY(Z[1],END=8390)
8317 IF U$(1,10)<>U8$(5,10) THEN GOTO 8390
8325 READ (Z[1],KEY=U$,DOM=8315)IOL=0310
8327 IF %X3$(333,2)<>"  " AND A$(1,2)<>%X3$(333,2) THEN GOTO 8315 ! SSP 221450
8330 GOTO 1160
8390 GOTO 1000
8400 REM "Load Salesperson into title
8405 IF U1$<>D$(1,4) THEN DIM D$(50); FIND (Z[4],KEY=U1$(1,4),DOM=8406)IOL=0340
8410 W0$=U1$+" - "+FNS$(D$(5,35))
8440 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 IF Y5=68 THEN RETRY ELSE ON C9 GOTO 1140,2040
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF NOT(NUL(Y4$)) THEN RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
56000 ! "221450-if you restrict operator to a division you cannot exit out  
