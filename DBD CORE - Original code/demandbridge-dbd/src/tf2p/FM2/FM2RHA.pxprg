0010 REM "Monthly Order Booking Summary <FM2RHA>
0015 REM "Prog Type: O -2.7     
0035 REM "5.7 - 09/12/12 - 11.071388 - tma - SSP# 257981
0036 REM "ADDED LINE 932 SO THAT RANGE WOULD WORK
0037 REM "257981-Getting an error running the monthly order booking report.  
0040 REM "Copyright 2012 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 SETERR 9000; SETESC 9300
0110 X0$="FM2RHA",X1$="Monthly Order Booking Summary"
0120 DIM A$(43),A[7],C$(200),K[14],Q[99]
0140 Z0$="##,###,###.00-",Z1$="###.00-"
0210 T=1,T0=1,T1=4
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR FSD - Monthly Order Booking Summary
0310 IOLIST A$,A[0],A[1]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0425 IOLIST V$,V[0]
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0520 W0$="For fiscal year "+V0$(73,4)+" period "+V0$(77,2)+"  ending "+FND$(V0$(79,6))
0530 Y$="01XLFSD...  03O ARA...  04O AR3...  05O ARH...  "; IF V1$(1,3)<>Y$(5,3) THEN Y$=Y$+"02O "+V1$+"  "
0550 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0600 REM "Read Parameters
0610 READ (Z[13],KEY=X3$(9,3)+"A/R",DOM=9900)P$
0620 READ (Z[13],KEY="STATAR3")F9$,F0
0630 FOR X=0 TO 9; F1$=F1$+F9$(49+X*17,5); NEXT X
0700 GOSUB 7500
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0820 DIM U[3]; U=12,U0=2,U[0]=0,U[1]=2,U[2]=4,U[3]=6,T0=2,V1=2,V2=4,U0$=" Y   ",U5$="Division",U6$="Salesperson",T5$="                "
0830 IF V0$(72,1)="Y" THEN X1$=X1$+" - Totals Only Version"
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0890 REM "Open Printer
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0920 V2$="  ~~"+V2$
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0932 GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record and check range
1005 U$=KEY(U1,END=5000); READ (U1)
1010 U3=1,U2=1
1020 FOR U9=2 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1024 IF U[U9]=2 AND U$(1,2)="zz" THEN GOTO 1050
1025 IF U[U9]=4 AND U$(3,1)="z" THEN GOTO 1050
1030 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN EXITTO 1000
1040 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U9=1 THEN EXITTO 5000 ELSE EXITTO 1000
1050 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1130 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1140 IF A$(7,6)>V0$(79,6) THEN GOTO 1000
1150 IF V0$(72,1)="Y" AND A$(3,1)<>"z" THEN GOTO 1000
1160 IF P$(13,1)<>"Y" AND A$(1,3)<>"zzz" AND A$(3,1)="z" THEN GOTO 1000
1200 REM "Reporting criteria
1400 REM "Subtotals"
1425 GOSUB 3100
1430 ON T GOTO 1490,1440,1440,1450,1460
1440 GOSUB 6300
1490 T=0
1510 IF W+2>W0 THEN GOSUB 6000
1515 REM "IF POS(X3$(9,3)="500322")>0 THEN LET A[1]=A[1]*1.1
1520 A0=A0+A[0],A1=A1+A[1],A2=A2+(A[0]-A[1])
1540 Y5$(1)=A$(9,2)+"/"+A$(11,2)
1550 Y5$(7)=STR(A[0]:Z0$),Y5$(22)=STR(A0:Z0$)
1555 IF Q0<>0 THEN IF V0$(85,1)<>"Y" THEN Y5$(39)=STR(A0*100/Q0:Z1$) ELSE Y5$(39)=STR(A2*100/Q0:Z1$)
1560 Y5$(46)=STR(A[1]:Z0$),Y5$(61)=STR(A[0]-A[1]:Z0$)
1565 IF A[0]<>0 THEN Y5$(77)=STR((A[0]-A[1])*100/A[0]:Z1$)
1570 Y5$(85)=STR(A2:Z0$)
1580 IF A0<>0 THEN Y5$(101)=STR((A0-A1)*100/A0:Z1$)
1590 GOSUB 7300
1900 REM "Accumulate Totals"
1990 T[T0,0]=T[T0,0]+1,T[T0,1]=T[T0,1]+A[5],T[T0,2]=T[T0,2]+A[6],T[T0,3]=T[T0,3]+(A[5]-A[6]),T[T0,4]=T[T0,4]+A[7]
1993 DIM A[7]
1995 GOTO 1000
2500 REM "Subtitle
2510 IF W+5>W0 THEN GOSUB 6000
2520 Y5$(13)="Orders",Y5$(28)="Period",Y5$(38)="Percent",Y5$(50)="+ --------- Estimated --------- +",Y5$(91)="Period"
2530 GOSUB 7300
2540 Y5$(1)="Date",Y5$(13)="Booked",Y5$(28)="to-date",Y5$(38)="of quota",Y5$(52)="Cost",Y5$(63)="Gross profit",Y5$(79)="GP%",Y5$(89)="to-date GP",Y5$(103)="GP%"
2545 IF POS("A"=V0$(59,12))>0 THEN Y5$(13,15)="Entered",Y5$(41,11)="Target",Y5$(52,11)="Adj. cost",Y5$(63,16)="Contribution",Y5$(79,10)="CB%",Y5$(97,6)="CB",Y5$(103,3)="CB%"
2560 GOSUB 7300
2590 RETURN 
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break
3110 IF V1=0 THEN RETURN ELSE IF T0$="END" THEN GOTO 3120 ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; IF P$(13,1)<>"Y" OR V0=2 THEN GOSUB 7100; GOTO 3160 ELSE GOSUB 7000
3153 IF T0$="END" THEN RETURN 
3155 IF U0$(1,1)="Y" THEN GOSUB 6000
3160 U1$=U9$(1,V1)
3195 RETURN 
3200 REM "Second highest order break
3210 IF V2=0 THEN RETURN ELSE IF T0$="END" THEN GOTO 3220 ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$,T6$=U2$,F0=1; GOSUB 3050; GOSUB 7000
3253 IF T0$="END" THEN RETURN 
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3260 U2$=U9$(V5,V2),F0=0,A0=0,A1=0,A2=0
3280 RETURN 
3300 REM "Next to lowest break
3310 IF V3=0 THEN RETURN ELSE IF T0$="END" THEN GOTO 3320 ELSE IF U3$=U9$(V6,V3) THEN GOSUB 3400; RETURN 
3320 GOSUB 3420
3330 IF U3$="" THEN GOTO 3360
3350 T=3,T$=U7$,T6$=U3$; GOSUB 3050; GOSUB 7000
3353 IF T0$="END" THEN RETURN 
3355 IF U0$(3,1)="Y" THEN GOSUB 6000
3360 U3$=U9$(V6,V3)
3380 RETURN 
3400 REM "Lowest
3410 IF V4=0 THEN RETURN ELSE IF T0$="END" THEN GOTO 3420 ELSE IF U4$=U9$(V7,V4) THEN RETURN 
3430 IF U4$="" THEN GOTO 3460
3450 T=4,T$=U8$,T6$=U4$; GOSUB 3050; GOSUB 7000
3455 IF U0$(4,1)="Y" THEN GOSUB 6000
3460 U4$=U9$(V7,V4)
3480 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5200 REM "
5210 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
5215 IF V3$(2,1)="T" THEN GOTO 9900
5218 IF %MULT_RPT_DRV_MODE OR %TOD THEN GOTO 9900 ! SSP 229934 SSP 240570
5220 X$="Are you ready to update monthly statistics and clear the booking summary?"
5230 CALL "ZZPROM","Y",X3$,S3,X$,"","",0
5240 IF S3=0 THEN GOSUB 8500; IF P$(117,1)="Y" THEN CALL "ZZPROM","4",X3$,Z,"Remember to also run and update in sequence 2 for customer stats.","","",0; Y4$="FM2RHS"
5295 GOTO 9900
6000 REM "Page header
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W4$,W5$; ON W5 GOTO 6041,9900
6060 GOSUB 6100
6070 IF F0<>1 THEN GOSUB 2500
6080 GOSUB 7300
6090 RETURN 
6100 REM "
6195 IF FID(W9)=FID(0) THEN PRINT 'SF',
6199 RETURN 
6300 REM "Salesperson
6310 IF A$(3,1)="z" THEN GOTO 6400
6320 IF W+2>W0 THEN F0=1; GOSUB 6000; F0=0
6330 DIM E$(200); FIND (Z[4],KEY=A$(3,4),DOM=6331)E$
6335 X$="Q",J9$=A$(3,4); GOSUB 7700; Q0=K[NUM(V0$(77,2))]
6340 Y5$(5)="Salesperson:  "+A$(3,4),Y5$(25)=E$(5,35)
6350 GOSUB 7300
6360 Y5$(5)="Monthly Quota:  ",Y5$(21)=STR(Q0:Z0$); IF POS("A"=V0$(59,12))>0 THEN Y5$(13,8)="Target:"
6370 GOSUB 7300; GOSUB 7300
6380 GOSUB 2500
6390 RETURN 
6400 REM "Division Total
6410 IF A$(1,2)="zz" THEN GOTO 6500 ELSE IF P$(13,1)<>"Y" THEN GOTO 6490
6420 IF W+2>W0 THEN F0=1; GOSUB 6000; F0=0
6430 DIM E$(60); FIND (Z[5],KEY=A$(1,2),DOM=6431)E$
6440 Y5$(5)="Division:  "+A$(1,2),Y5$(23)=E$(3,30)
6445 GOSUB 7300
6460 Y5$(5)="Monthly Quota:  ",Y5$(21)=STR(Q[NUM(A$(1,2))]:Z0$); IF POS("A"=V0$(59,12))>0 THEN Y5$(13,8)="Target:"
6470 GOSUB 7300; GOSUB 7300
6480 GOSUB 2500
6485 Q0=Q[NUM(A$(1,2))]
6490 RETURN 
6500 REM "Company Total
6510 IF W+2>W0 THEN F0=1; GOSUB 6000; F0=0
6520 Y5$(5)="Company Total"
6530 GOSUB 7300
6540 Y5$(5)="Monthly Quota:  ",Y5$(21)=STR(Q1:Z0$); IF POS("A"=V0$(59,12))>0 THEN Y5$(13,8)="Target:"
6550 GOSUB 7300; GOSUB 7300
6580 GOSUB 2500
6585 Q0=Q1
6590 RETURN 
7000 REM "Totals logic
7001 IF T=T0 THEN IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600),T4=MOD(T3,60),T3=INT(T3/60)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")+":"+STR(T4:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 W=W+1
7330 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     13O ZZPARM  "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7435 DIM V[1]
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1) ! SSP#257981
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7486 V1$="FSD..."
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7510 REM "Add up Quotas
7515 J9$=""
7520 READ (Z[1],KEY="",DOM=7521)
7530 READ (Z[1],END=7570)IOL=0310
7540 IF A$(3,1)="z" THEN GOTO 7530
7542 IF J9$=A$(3,4) THEN GOTO 7530
7545 X$="Q",J9$=A$(3,4)
7550 GOSUB 7700
7560 Q[NUM(A$(1,2),ERR=7565)]=Q[NUM(A$(1,2))]+K[NUM(V0$(77,2))]
7562 Q1=Q1+K[NUM(V0$(77,2))]
7565 GOTO 7530
7590 RETURN 
7700 REM "Unpack Salesperson quota
7705 DIM K[14]
7710 X9=POS(X$=F1$,5),Q$="R"; IF X9=0 THEN RETURN 
7720 CALL "ZZPACK",X3$,Q$,F1$(X9+2,3),"",0,0,K{ALL},Z[3],X$,J9$+V0$(73,4),F9$
7725 GOTO 7740; REM "IF POS(X3$(9,3)="500322",3)=0 THEN GOTO 07740
7730 DIM E$(220); FIND (Z[4],KEY=A$(3,4),DOM=7731)E$
7735 IF A$(1,2)<>E$(211,2) THEN DIM K[14]
7740 RETURN 
7750 REM "Pack Salesperson quota
7760 X9=POS(X$=F1$,5),Q$="W"; IF X9=0 THEN RETURN 
7770 CALL "ZZPACK",X3$,Q$,F1$(X9+2,3),"",0,0,K{ALL},Z[3],X$,A$(3,4)+V0$(73,4),F9$
7790 RETURN 
8500 REM "Update monthly statistics
8510 READ (Z[1],KEY="",DOM=8511)
8520 KEY$=KEY(Z[1],END=8690); READ (Z[1],KEY=KEY$)IOL=0310
8525 IF A$(7,6)>V0$(79,6) THEN GOTO 8520
8530 IF A$(3,1)="z" THEN GOTO 8670
8550 X$="B",J9$=A$(3,4); GOSUB 7700
8560 K[NUM(V0$(77,2))]=K[NUM(V0$(77,2))]+A[0]
8570 X$="B"; GOSUB 7750
8580 X$="D",J9$=A$(3,4); GOSUB 7700
8600 K[NUM(V0$(77,2))]=K[NUM(V0$(77,2))]+A[0]-A[1]
8620 X$="D"; GOSUB 7750
8670 REMOVE (Z[1],KEY=KEY$,DOM=8671)
8680 GOTO 8520
8690 RETURN 
8905 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8940 DEF FNS$(Z$)=Z$(1,POS("   "=Z$+"   ")-1)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 IF Y5=68 THEN RETRY ELSE ON C9 GOTO 1140,2040
9900 REM "End program
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF NOT(NUL(Y4$)) AND STP(Y4$)<>"FM2RHS" AND STP(Y4$)<>"ZMENU" THEN IF %GUI THEN CALL Y4$,ERR=9931 ELSE RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
56000 ! "229934-Would like the Inventory Status report to be one of the     
56002 REM "240570-GUI Printing - Support for queueing and scheduling reports. 
