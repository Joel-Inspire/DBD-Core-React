0010 REM "<FM2CCG> Warehouse Release Report-Tully-Wihr-Depts
0015 REM "Prog Type: BS-3.0A    
0020 SETESC 9300; SETERR 9000
0035 REM "5.6 - 10/14/08 - 13.338055 - tma - SSP# 166906
0036 REM "ADDED LINE 6215 FOR FPF CUSTOMER LOCATION CODE IN FM0 214,9)
0037 REM "166906-Warehouse release report will not pull bundled sell         
0040 REM "Copyright 2008 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0092 REM "Breaks by either receiving dept(R) or billing dept(D)
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 SETERR 9000; SETESC 9300
0105 X0$="FM2CCG"
0110 X1$="Warehouse Release Report"
0120 DIM A$(428),A[14],E$(448),E[31],F$(221),F[6],B$(335)
0125 DIM R[10]
0140 M0$="-##,###.00",M1$="-##,###,##0",M2$="$##,###,###.00-",M4$="#,###,##0-",M5$="###,##0",M6$="-####,###.00"
0210 T1=5
0220 W3=131
0221 REM "LET W3=122
0225 W=999
0230 DIM T1$(W3,"-"),Y5$(W3),T3$(20,"*"),S$(20)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13]
0320 IOLIST B$
0330 IOLIST C$
0340 IOLIST D$
0345 IOLIST D0$
0350 IOLIST E$,E[0],E[1],E[2],E[3],E[4],E[5],E[6],E[7],E[8],E[9],E[10],E[11],E[12],E[13],E[14],E[15],E[16],E[17],E[18],E[19],E[20],E[21],E[22],E[23],E[24],E[25],E[26],E[27],E[28],E[29],E[30],E[31]
0360 IOLIST F$,F[0],F[1],F[2],F[3],F[4],F[5],F[6]
0380 IOLIST H$,H[0],H[1],H[2],H[3],H[4],H[5]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,IOL=0425
0425 IOLIST V$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Z$="01O FMZ...  02O FM0...  03O AR1...  04O FMP...  05O FM1...  06O ICI...  07O ICJ...  "
0520 GOSUB 9750
0530 READ (Z[13],KEY=X3$(9,3)+"I/C")P0$
0540 IF X3$(56,3)="DMY" THEN M2$(1,1)="#"
0800 REM "Alternate Sort Info & total dim"
0810 DIM U[10,2],U0$(10),Y[5,1]
0820 DIM U[35,1]; U[0,0]=1,U[1,0]=1,U[1,1]=10,U[2,0]=11,U[2,1]=20,U[3,0]=31,U[3,1]=10,U[4,0]=41,U[4,1]=6,U[5,0]=47,U[5,1]=3,U[6,0]=50,U[6,1]=5,T0=7,U0$="0111000",T5$="    AA/RS   S   D   Z   S   ",U5$="Customer code       Receiving DepartmentForm code           Activity date       Seq counter         Location            ",U1=Z[9],U=54
0828 T0=T0-1,U0=T0
0850 DIM T[T0,T1]; U5$=Y5$(1,20)+U5$,U5$(1,20)=Q4$
0860 K0$="[128]"; CALL "ZZ2SRT",X3$,X4$,Z[13],Z[1],32,K0$,K1,K1$
0865 Z$="08O "+K0$; GOSUB 9750
0870 F0$="{S}FM2CCG Department Sort"; CALL "ZZ2SRT",X3$,X4$,Z[13],Z[1],54,F0$,F1,F1$
0875 Z$="09O "+F0$; GOSUB 9750
0880 GOSUB 8700; REM "Redo V2$, write dept sort file
0890 REM "Open Printer
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0950 REM " Position read"
0970 DIM V4$(U); U9=1; FOR X=1 TO U0; V4$(U[X,0],U[X,1])=V2$(U9,U[X,1]); U9=U9+2*U[X,1]; NEXT X
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record and check range
1005 U$=KEY(U1,END=5000)
1006 REM "IF V$(79,9)=S$(1,9) THEN GOTO 01010 ELSE IF U$(1,10)>V2$(11,10) THEN GOTO 05000 ELSE IF U$(11,5)>V2$(26,5) THEN GOTO 05000
1007 REM "READ (Z[2],KEY=U$(11,1)+U$(1,10)+U$(12,4),DOM=01008) X$; IF X$(236,9)<>V$(79,9) THEN READ (U1,KEY=U$(1,15)+$FF$,DOM=01000) ; GOTO 01000
1010 FOR X=1 TO U0; U2=U[X,0],U3=U[X,0]*2-1,U4=U[X,1]
1020 IF U$(U2,U4)<V2$(U3,U4) THEN READ (U1); EXITTO 1000
1030 IF U$(U2,U4)>V2$(U3+U4,U4) THEN IF X=1 THEN EXITTO 5000 ELSE READ (U1); EXITTO 1000
1040 NEXT X
1100 REM "Get record"
1110 U9$=U$; REM "IF V0<2 THEN GOTO 01150 ELSE LET U$="", X=0, B$=U9$
1130 READ (U1)
1150 READ (Z[1],KEY=U$(1,10)+U$(50,5)+U$(31,19),DOM=1000)IOL=0310
1160 REM "IF A$(11,1)="D" THEN IF A$(35,1)="D" THEN GOTO 01000
1165 REM IF POS(A$(35,1)="ID")=0 THEN GOTO 01000
1166 IF POS("P"=V$(59,12))=0 THEN IF A$(35,1)="C" THEN GOTO 1000
1170 IF V$(88,1)="Y" THEN IF A$(35,1)<>"I" OR A$(92,2)="D0" THEN GOTO 1000
1190 IF V$(73,1)<>"Y" AND A$(35,1)="D" THEN GOTO 1000
1195 IF V$(74,1)="P" AND A$(216,1)<>"P" THEN GOTO 1000 ELSE IF V$(74,1)="I" AND A$(216,1)<>" " THEN GOTO 1000
1200 REM "Reporting criteria
1205 GOSUB 7500; GOSUB 8000
1210 IF TYPE$="Billing" THEN BDEPT_HEADER$=A$(385,20)
1300 REM 
1310 DIM E$(448); FIND (Z[5],KEY=A$(1,10)+A$(16,10),DOM=1311)IOL=0350
1320 IF V$(76,1)="N" AND E$(108,1)<>"Y" THEN GOTO 1000
1325 IF A$(11,1)="D" THEN IF A$(137,2)="SJ" THEN GOTO 1000
1400 REM "Subtotals"
1410 IF U2$="" THEN Q=1; GOTO 1450 ELSE FOR T=1 TO U0; IF U9$(U[T,0],U[T,1])<>U2$(U[T,0],U[T,1]) THEN EXITTO 1420 ELSE NEXT T; GOTO 1480
1425 GOSUB 3100
1450 ON Q GOTO 1490,1455,1460,1490
1455 GOSUB 6500
1460 GOSUB 8100; GOSUB 6000
1490 T=0,U2$=U9$
1510 IF W+2>W0 THEN GOSUB 6000
1515 IF STP(E$(350,20),3," ")="" THEN Y5$(2)=A$(16,10) ELSE Y5$(2)=E$(350,20)
1520 Y5$(23)=A$(86,6)
1525 Y5$(30)=A$(46,40)
1530 X$=FND$(A$(26,6)),Y5$(66)=X$(1,6)+X$(9,2)
1534 IF A$(35,1)="D" THEN Y5$(75)="DS"
1535 Y5$(78)=A$(115,6); IF A$(122,15)<>S$(1,15) THEN Y5$(78)=A$(122,15)
1536 IF POS("B"=V$(59,12))>0 THEN Y5$(78)=A$(115,6)
1537 IF A$(35,1)="C" THEN Y5$(78)="Cust Count"
1545 IF V$(77,1)<>"Y" THEN Y5$(94)=FNQ$(A[1],M1$) ELSE GOSUB 8200; Y5$(90)=X$
1550 IF A$(35,1)<>"C" THEN GOSUB 7500 ELSE A5=0; GOTO 1580
1575 Y5$(108)=STR(A5:M0$)
1600 REM "Other data"
1610 GOSUB 7300
1620 IF POS("A"=V$(59,12))>0 THEN IF POS(" "<>A$(276,40))>0 THEN Y5$(30)=A$(276,40); GOSUB 7300
1700 REM "Lot stuff
1705 IF V$(77,1)<>"Y" THEN GOTO 1800
1900 REM "Accumulate Totals"
1910 R[2]=R[2]+A[6],R[3]=R[3]+A[7],R[4]=R[4]+A[8]
1912 IF A[6]=0 THEN R[1]=R[1]+A5 ELSE IF E$(105,1)="Y" THEN R[5]=R[5]+A5 ELSE R[6]=R[6]+A5
1915 R[9]=R[9]+1,T[T0,5]=T[T0,5]+1,P=POS(A$(113,9)=R0$,9); IF P=0 THEN R0$=R0$+A$(113,9),R[10]=R[10]+1,T[T0,4]=T[T0,4]+1
1920 T[T0,1]=T[T0,1]+A5,T[T0,2]=T[T0,2]+A[8],T[T0,3]=T[T0,3]+A[7]
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T9$,"",0,0,X4$
3080 IF T7$(1,1)="S" THEN T$=T$+" "+T6$ ELSE T$=T$+" "+T9$
3090 RETURN 
3100 REM "Subtotal Logic
3110 Q=T; FOR T=U0 TO Q STEP -1
3130 T$=FNS$(U5$(T*20+1,20)),T6$=U2$(U[T,0],U[T,1]),T7$=T5$(T*4+1,4); GOSUB 3050; IF POS(U0$(T+1,1)="123")>0 THEN GOSUB 7000 ELSE GOSUB 7100
3140 IF Q=1 AND T=2 THEN S0$="*"; GOSUB 6000
3150 IF POS(U0$(T+1,1)="23")>0 THEN GOSUB 6000
3180 NEXT T
3195 RETURN 
3280 RETURN 
3380 RETURN 
3480 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5290
5025 IF V$(78,1)="O" THEN W8=0
5030 T0$="END"
5050 T=0; GOSUB 3100
5200 REM "
5295 GOTO 9900
6000 REM "Page header
6020 IF V$(78,1)="O" THEN IF W7>0 THEN W=0,W8=1; GOTO 6041
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; ON W5 GOTO 6041,9906
6045 IF T0$="END" THEN GOTO 6090
6049 IF S0$="*" THEN S0$=""; GOTO 6070
6050 GOSUB 6200
6060 IF E9$<>"RECAP" THEN GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6150 Y5$(2)="Item",Y5$(22)="Revised",Y5$(32)="Description",Y5$(68)="Date",Y5$(78)="Req No",Y5$(98)="Quantity",Y5$(113)="Value"; GOSUB 7300
6195 IF FID(W9)=FID(0) THEN PRINT 'SF',
6199 RETURN 
6200 REM "Print Department Description
6210 IF TYPE$="Billing" THEN Y5$(2)=FNS$(BDEPT_HEADER$); GOSUB 7300; GOSUB 7300; GOSUB 7300; REM "GOTO 6270
6215 Y5$(2)=FNS$(B$(382,20)),Y5$(25)=FNS$(B$(16,35))+" ("+B$(1,1)+"-"+B$(12,4)+")"; GOSUB 7300
6230 IF FNS$(B$(51,30))>"" THEN Y5$(25)=B$(51,30); GOSUB 7300
6240 IF FNS$(B$(81,30))>"" THEN Y5$(25)=B$(81,30); GOSUB 7300
6245 CALL "ZZDISP","AX",B$(129,9),"ZIP",X3$,X$,"",0,0,X4$
6250 Y5$(25)=FNS$(B$(111,16))+", "+B$(127,2)+".   "+FNS$(X$)
6270 GOSUB 7300; GOSUB 7300
6290 RETURN 
6500 REM "Customer Break
6510 DIM C$(400); FIND (Z[3],DOM=6511,KEY=A$(1,10))IOL=0330
6515 DIM D$(60); FIND (Z[4],KEY="D"+A$(1,10),DOM=6516)IOL=0340
6520 DIM D0$(200); FIND (Z[4],KEY="E"+A$(1,10)+D$(52,4),DOM=6521)IOL=0345
6525 D1$=""; IF POS(" "<>D$(52,6))<>0 THEN D1=NUM(D$(56,2),ERR=6526); D1$=D0$(18+D1*6,6)
6530 IF POS(" "<>D1$)=0 THEN W1$=FNS$(C$(11,35)) ELSE W1$=FNS$(C$(11,35))+" for period ending "+FND$(D1$)
6535 IF U2$<>"" THEN W8=.1
6540 RETURN 
6600 REM "RECAP BY LOCATION HEADERS
6610 Y5$(1)=TYPE$+" Department",Y5$(40)="    Reqs   Lines     Value         Tax    Subtotal     Freight       Total"
6620 GOSUB 7300
6640 RETURN 
7000 REM "Totals logic
7001 IF T=2 THEN GOSUB 8600
7002 IF T[T,0]<2 THEN IF POS(X3$(9,3)="449500",3)=0 THEN GOSUB 7300; GOTO 7100
7003 IF W+3>W0 THEN IF TYPE$="Billing" THEN BDEPT_HEADER$=U2$(11,20); GOSUB 6000; BDEPT_HEADER$=A$(385,20) ELSE GOSUB 6000
7005 GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" "+Q2$+" ("+STR(T[T,0]:"0")+"):"
7025 Y5$(POS("):"=Y5$)+2)=STR(T[T,1]:M2$)
7085 GOSUB 7300
7090 IF T=1 THEN GOSUB 7700
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7105 IF V$(78,1)="O" THEN IF T=1 THEN W7=1
7110 IF T=0 AND T0$="END" THEN GOSUB 7200; GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7202 IF POS("S"=V$(59,12))>0 THEN RETURN 
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7305 IF V$(78,1)="O" THEN IF W7>0 THEN Y5$(1)=""; GOTO 7395
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP 13O ZZPARM  "
7420 GOSUB 9750; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7455 V1$="FMZ..."
7460 GOSUB 7600
7465 IF V$(78,1)="O" THEN W7=1
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "GET VALUE AMT IN A5
7505 IF A$(35,1)="S" THEN GOTO 7511
7510 IF V$(72,1)<>"B" THEN IF V$(75,1)="Y" THEN GOTO 7550
7520 A5=A[5]; IF V$(72,1)="F" THEN A5=A[3] ELSE IF V$(72,1)="B" THEN A5=A[6]
7530 IF POS(V$(72,1)="12")>0 THEN GOSUB 8300; IF C2<>0 THEN A5=C2*A[1]/C1,C1$="*" ! SSP#166906
7545 RETURN 
7550 REM "RE-PRICE USING CURRENT
7560 IF E[4]=0 THEN A5=0 ELSE IF V$(72,1)="F" THEN A5=A[1]*E[29]/E[4] ELSE A5=A[1]*E[3]/E[4]
7595 RETURN 
7600 REM "HEADINGS
7602 IF POS("X"=V$(59,12))>0 THEN GOTO 7631
7610 W0$="Sell Price"; IF V$(72,1)="F" THEN W0$="FMS Price" ELSE IF V$(72,1)="B" THEN W0$="Billed Amt"
7615 W0$="Valuation based on "+W0$; IF V$(75,1)="Y" THEN W0$=W0$+" (Current)"
7620 IF V$(74,1)="P" THEN X1$=X1$+" (Periodic)" ELSE IF V$(74,1)="I" THEN X1$=X1$+" (Interim)"
7625 W1$="Includes Factory Shipments"; IF V$(73,1)<>"Y" THEN W1$="Excludes Factory Shipments"
7630 W0$=W0$+"; "+W1$,W1$=""
7635 IF V$(71,1)<>"1" OR V2$(51,12)="      ~~~~~~" THEN GOTO 7660
7640 W2$="Activity from: "; IF V2$(51,1)="      " THEN W2$=W2$+FNS$(X3$(240,12)) ELSE W2$=W2$+FND$(V2$(51,6))
7645 W2$=W2$+" to "; IF V2$(57,6)="~~~~~~" THEN W2$=W2$+FNS$(X3$(252,12)) ELSE W2$=W2$+FND$(V2$(57,6))
7660 REM "FORM RANGE
7665 IF V2$(31,20)=S$(1,10)+"~~~~~~~~~~" THEN GOTO 7680 ELSE W3$="Item codes: "; IF V2$(31,10)=S$(1,10) THEN W3$=W4$+FNS$(X3$(240,12)) ELSE W3$=W3$+FNS$(V2$(31,10))
7670 W3$=W3$+" to "; IF V2$(41,10)="~~~~~~~~~~" THEN W3$=W3$+FNS$(X3$(252,12)) ELSE W3$=W3$+FNS$(V2$(41,10))
7675 IF W2$="" THEN W2$=W3$,W3$="" ELSE W2$=W3$+"; "+W2$,W3$=""
7695 RETURN 
7700 REM "Customer Recap
7702 IF POS(V$(78,1)="YO")=0 THEN GOTO 7880
7705 E9$="RECAP",S0$="*",W7=0
7710 GOSUB 6000
7715 E9$=""
7720 GOSUB 7300; GOSUB 7300; Y5$(22)="Customer Recap Sheet"; GOSUB 7300; GOSUB 7300
7730 Y5$(1)="Value of distributor owned items shipped:",Y5$(60)=STR(R[6]:M2$)
7735 GOSUB 7300; GOSUB 7300
7740 Y5$(1)="Value of chargeable items shipped:",Y5$(60)=STR(R[1]:M2$)
7745 GOSUB 7300; GOSUB 7300
7760 Y5$(1)="Total value of items shipped:",Y5$(60)=STR(R[6]+R[1]:M2$)
7765 GOSUB 7300; GOSUB 7300
7780 IF R[3]+R[4]<>0 THEN Y5$(1)="Delivery charges / Other",Y5$(60)=STR(R[3]+R[4]:M2$); GOSUB 7300; GOSUB 7300
7785 Y5$(1)="Distributor Stock Item Purchases:",Y5$(60)=STR(R[5]:M2$); GOSUB 7300; GOSUB 7300
7790 Y5$(1)="Total:",Y5$(60)=STR(R[6]+R[1]+R[3]+R[4]+R[5]:M2$); GOSUB 7300; GOSUB 7300
7795 GOSUB 7300; Y5$(1)="Total requisitions:",Y5$(40)=STR(R[10]:M1$); GOSUB 7300; GOSUB 7300
7800 Y5$(1)="Total line items:",Y5$(40)=STR(R[9]:M1$); GOSUB 7300; GOSUB 7300
7810 GOSUB 7300; GOSUB 7300
7815 GOSUB 7900
7880 DIM R[10],R0$(0)
7885 IF V$(78,1)="O" THEN W7=1
7890 RETURN 
7900 REM "RECAP BY LOCATION
7905 DIM H$(30),H[10],R[10]
7910 GOSUB 6600
7920 READ (Z[8],KEY=U2$(1,10),DOM=7921)
7925 READ (Z[8],END=7980)IOL=0380
7928 IF H$(1,10)<>U2$(1,10) THEN GOTO 7980
7940 Y5$(1)=H$(11,20)
7945 Y5$(40)=STR(H[3]:M5$)+STR(H[4]:M5$)+STR(H[0]:M6$)+STR(H[1]:M6$)+STR(H[0]+H[1]:M6$)+STR(H[2]:M6$)+STR(H[0]+H[1]+H[2]:M6$)
7950 FOR X=0 TO 5; R[X]=R[X]+H[X]; NEXT X
7960 GOSUB 7300; GOTO 7925
7980 IF H$<>"" THEN H$=""; FOR X=0 TO 5; H[X]=R[X]; NEXT X; GOSUB 7300; Y5$(14)="Customer total:"; GOTO 7945
7990 SETERR 9000
7995 RETURN 
8000 REM "READ ALTERNATE RECORDS
8090 RETURN 
8100 REM "
8105 IF L0$=A$(11,1)+A$(1,10)+A$(12,4) THEN GOTO 8140 ELSE L0$=A$(11,1)+A$(1,10)+A$(12,4); IF L0$(1,1)<>"C" THEN L0$(2,10)=""
8110 DIM B$(522); B$(1)=L0$; READ (Z[2],KEY=L0$,DOM=8111)IOL=0320
8120 L0$=A$(11,1)+A$(1,10)+A$(12,4)
8140 RETURN 
8205 IF A[0]=0 THEN A$(180,4)="M",A[0]=1000
8210 X$=FNS$(A$(180,4))+"/"+STR(A[0])
8215 Q=A[1]/A[0]
8220 IF A$(180,4)<>"M   " AND FPT(Q)=0 THEN X$=FNQ$(Q,M0$)+" "+X$ ELSE X$="  "+FNQ$(A[1],M1$)
8245 RETURN 
8300 REM "Get Bundled Sell Price per unit !ssp#166906
8320 Q0$=A$(1,10)+A$(16,10)
8325 Q1$=A$(231,8)
8326 IF POS("B"=V$(59,12))<>0 THEN X9$="B" ELSE X9$="1"
8330 CALL "FM2UZG",X3$,0,"B",Q0$,Q1$,C0$,C1,C2
8390 RETURN 
8500 REM "Display Qty and set J0 to xpos of input of field J1
8501 REM "J1 -1=display, 0=qty, 1=primary unit, 2=units/package, 3=package, 4=unit/primary/package
8505 J0$="",J0=0,J1=-1
8510 IF J1=0 THEN J0$=STR(F[4]:M4$) ELSE J0$=FNR$(STR(F[4]:M4$))
8512 IF J1=1 THEN J0=18+LEN(J0$)
8515 IF POS(" "<>F$(121,4))<>0 THEN IF F[4]>1 THEN J0$=J0$+FNS$(F$(121,4))+"s of " ELSE J0$=J0$+FNS$(F$(121,4))+" of "
8517 IF J1=2 THEN J0=18+LEN(J0$); IF F[3]<>0 AND POS(" "<>F$(125,4))=0 THEN J0$=J0$+S0$(1,LEN(M3$))+" of "
8520 IF F[2]<>0 THEN IF J1=2 THEN J0$=J0$+STR(F[2]:M3$) ELSE J0$=J0$+FNR$(STR(F[2]:M3$))
8522 IF J1=3 THEN J0=18+LEN(J0$); IF F[3]<>0 AND POS(" "<>F$(125,4))=0 THEN J0$=J0$+"     of "
8525 IF POS(" "<>F$(125,4))<>0 THEN IF F[2]>1 THEN J0$=J0$+FNS$(F$(125,4))+"s of " ELSE J0$=J0$+FNS$(F$(125,4))+" of "
8527 IF J1=4 THEN J0=18+LEN(J0$)
8530 IF J1<>4 AND F[3]<>0 THEN J0$=J0$+FNR$(STR(F[3]:M3$))
8535 IF F$(125,4)="    " THEN Q=F[4]*F[3] ELSE Q=F[4]*F[2]*F[3]
8540 REM "J0$ IS DESC STRING, Q IS TOTAL AMOUNT
8545 RETURN 
8600 REM "ADD TO RECAP TMP FILE
8610 WRITE (Z[8],KEY=U2$(1,30),DOM=9000)U2$(1,30),T[T,1],T[T,2],T[T,3],T[T,4],T[T,5]
8640 RETURN 
8700 REM "Write to temp department sort file
8705 U1=Z[9]
8710 IF POS("R"=V$(59,12))>0 THEN TYPE$="Receiving" ELSE IF POS("D"=V$(59,12))>0 THEN TYPE$="Billing",U5$(41,20)="Billing Department" ELSE TYPE$="Receiving"
8715 X1$=X1$+" by "+TYPE$+" Department"
8720 FIRST$=V2$(1,10)+V2$(21,5)+V2$(31,10)+V2$(51,6)+V2$(63,3)
8725 LAST$=V2$(11,10)+V2$(26,5)+V2$(41,10)+V2$(57,6)+V2$(66,3)
8730 READ (Z[1],KEY=FIRST$,DOM=8731)
8735 FMZ_KEY$=KEY(Z[1],END=8780); READ (Z[1],KEY=FMZ_KEY$)FMZ$
8740 IF FMZ_KEY$>LAST$ THEN GOTO 8780
8745 IF TYPE$="Receiving" THEN DEPT$=FMZ$(365,20) ELSE DEPT$=FMZ$(385,20)
8747 REM "IF X3$(9,3)="449" THEN IF DEPT$=DIM(20) THEN GOTO 08735; REM "SSP#85262,THEY CHANGED THEIR MIND
8750 SORT_KEY$=FMZ$(1,10)+DEPT$+FMZ$(16,10)+FMZ$(26,6)+FMZ$(32,3)+FMZ$(11,5); WRITE (Z[9],KEY=SORT_KEY$); GOTO 8735
8780 REM "Redo V2$ for 1000's to work
8785 V2$=V2$(1,20)+DIM(20)+DIM(20,$7E$)+V2$(31,38)+V2$(21,10)
8790 RETURN 
8900 REM "Function Statements
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8930 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
8940 DEF FNE$(Z9$)=Z9$(3,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8950 DEF FNQ$(Z9,Z9$)=STR(Z9:Z9$(1,POS(".00"=STR(Z9:Z9$)+".00")-1))
8992 DEF FNZ$(Z)=CHR(ASC("B")+SGN(Z))+BIN(INT(ABS(Z)),6)
8994 DEF FNS$(Z$)=Z$(1,POS(S$=Z$+S$)-1)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 IF Y5=68 THEN RETRY ELSE ON C9 GOTO 1140,2040
9750 REM "OPEN FILES ROUTINE
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9790 RETURN 
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9927 ERASE "TMP"+FID(0),ERR=*NEXT ! SSP#222922      
9928 ERASE "TMPS"+FID(0),ERR=*NEXT ! SSP#222922
9930 IF NOT(NUL(Y4$)) THEN CALL Y4$,ERR=*NEXT ELSE RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
56001 REM "166906-Warehouse release report will not pull bundled sell         
