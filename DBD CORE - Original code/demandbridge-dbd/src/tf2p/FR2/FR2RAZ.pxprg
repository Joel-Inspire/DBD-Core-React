0010 REM "Convert F/R Heading Variables <FR2UZA>
0990 REM "F0=FILE FOR MONTH DATA, G0$=FISCAL YEAR, G1$=ACT PERIOD, G2$=PER ENDING  DATES
1000 ENTER X3$,X4$,F0,G1$,G2$,H$
1100 REM 
1105 DIM S$(20)
1110 V0=ASC("a")-1,V1=ASC("z")+1,G1=NUM(G1$),G0$=G2$(9,4)
1120 V0$="01PER/02P#/03PED/04FYE/05ORD/06MTH/07Y#/08CAR/09NAME/"
1200 REM "NEXT STRING
1220 P0=POS("{"=H$); IF P0=0 THEN GOTO 9900 ELSE P1=POS("}"=H$); IF P1=0 THEN GOTO 9900
1240 Q$=H$(P0+1,P1-P0-1)
1250 IF POS("a"<=Q$)=0 THEN GOTO 1300
1260 FOR X=1 TO LEN(Q$); V=ASC(Q$(X,1)); IF V>V0 AND V<V1 THEN Q$(X,1)=CHR(V-32)
1265 NEXT X
1300 REM "LIST OF VALID ENTRIES
1310 P=POS(Q$+"/"=V0$); IF P=0 THEN GOTO 8000 ELSE N=NUM(V0$(P-2,2))
1360 ON N-1 GOSUB 2100,2200,2300,2400,2500,2600,2700,2800,2900
1370 H$=H$(1,P0-1)+X$+H$(P1+1)
1390 GOTO 1200
2100 REM "PER - CARDINAL NUMBER OF PERIOD
2110 X$=G1$; GOSUB 8110; X$=FNS$(F0$(4,20))
2190 RETURN 
2200 REM "P#
2210 X$=G1$; RETURN 
2300 REM "PED - PERIOD ENDING DATE
2305 G=21+(G1-1)*6,X$=G2$(G,6)
2310 CALL "ZZDISP","DX",X$,"",X3$,Q0$,"",0,0,X4$
2315 X$=X$(3,2); GOSUB 8100; X$=FNS$(F0$(52,20))
2320 X$=X$+" "+STR(NUM(G2$(G+4,2)))+", "+Q0$(7,4)
2390 RETURN 
2400 REM "YEAR ENDING DATE
2410 G=15+NUM(G2$(13,2))*6,X$=G2$(G,6)
2420 GOTO 2310
2490 RETURN 
2500 REM "ORDINAL OF PERIOD
2510 X$=G1$; GOSUB 8100; X$=FNS$(F0$(24,20))
2590 RETURN 
2600 REM "MONTH NAME
2605 G=21+(G1-1)*6,X$=G2$(G+2,2)
2610 GOSUB 8100
2630 X$=FNS$(F0$(52,20))
2690 RETURN 
2700 REM "YEAR NUMBER
2710 X$=G0$; RETURN 
2800 REM "CARDINAL NUMBER
2810 X$=G1$; GOSUB 8100; X$=FNS$(F0$(24,20))
2890 RETURN 
2900 REM "Person Responsible name
2990 X$="?????"; RETURN 
8000 REM "UNDEFINED
8010 X$="(Undefined)"
8040 GOTO 1370
8100 REM "READ RECORD IN F0
8110 READ (F0,KEY=X3$(239,1)+X$,DOM=8111)F0$
8190 RETURN 
8910 DEF FNS$(Z$)=Z$(1,POS(S$=Z$+S$)-1)
