0010 REM "<GG2CUA> Update G/L Transaction Gateway
0020 SETESC 9300; SETERR 9000
0035 REM "5.0 - 01/08/02 - 17.379722 - plh - SSP# 142887
0040 REM "Copyright 2002 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="GG2CUA",X1$="Update G/L Transaction Gateway"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0305 GG0_IOLIST:IOLIST GG0$(1)
0310 GL7_IOLIST:IOLIST GL7$(1),GL7[0]
0340 GL8_IOLIST:IOLIST NEXTT,GL8$(1),GL8[0],GL8[1],GL8[2],GL8[3]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01L GG0... 02O GL7... 03O GL8... 07OSGL7... 13O ZZPARM "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0610 GOSUB 6000
0650 GOSUB 8100; GOSUB 8150
0900 DIM GG0$(303),GL7$(166),GL7[1],KK$(20)
0990 READ (Z[1],KEY="",DOM=0991)
0992 PREV=0
1000 REM "Process file
1010 K$=KEY(Z[1],END=5000); READ (Z[1],KEY=K$)IOL=GG0_IOLIST
1020 IF KK$(7,8)=GG0$(7,8) THEN GOTO 1250
1030 IF HEAD$<>"" THEN ESCAPE ; GOSUB 5400
1200 REM "fill GL7 record
1205 DIM GL7$(166),GL7[1]
1210 GL7$(1,157)=GG0$(7,157); REM "All fields except transacion date and allow entry of memo field
1215 IF NUM(GG0$(15,6))<>0 THEN GOTO 1240
1220 EXTRACT (Z[13],KEY=X3$(9,3)+"G/L")P0$
1225 GG0$(15,6)=P0$(244,6); IF P0$(244,6)="999999" THEN P0$(244,6)="000000"
1230 P0$(244,6)=STR(NUM(P0$(244,6))+1:"000000"); WRITE (Z[13],KEY=P0$(1,6))P0$
1240 GL7$(160,7)=GG0$(164,7); REM "Transaction date & allow enrty of memo field
1245 IF A9<>0 THEN NEXTT=0; WRITE (Z[3],IND=A9)IOL=GL8_IOLIST
1250 REM "Add GL8 record
1255 DIM GL8$(82),GL8[3]; GOSUB 5200; GL7[0]=A9
1260 GL8$(1,6)=GG0$(164,6),GL8$(7,12)=GG0$(171,12),GL8$(38,45)=GG0$(202,45); REM "Transaction date, G/L account number, Detail Memo Field
1265 NUM_IN$=GG0$(292,12); GOSUB CONVERT_NUMBER; GL8[3]=NUM_IN; REM "Amount
1270 NEXTT=0; WRITE (Z[3],IND=A9)IOL=GL8_IOLIST
1300 IF KK$(7,8)<>GG0$(7,8) THEN WRITE (Z[7],KEY=GL7$(1,14),DOM=1301)IOL=GL7_IOLIST
1400 REMOVE (Z[1],KEY=K$,DOM=1401)
1420 IF PREV<>0 THEN READ (Z[3],IND=PREV)IOL=GL8_IOLIST
1430 NEXTT=A9
1440 IF PREV<>0 THEN WRITE (Z[3],IND=PREV)IOL=GL8_IOLIST
1450 PREV=A9
1480 HEAD$="*"
1780 KK$=GG0$(1,20)
1790 GOTO 1010
1900 REM "End of file
1901 REM "Close & Rename to .done for archiving. Erase an existing .done file, if same name
1905 Z$="01CU"+F1$+" "
1906 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
1910 F2$=F1$+".done"
1919 REM "Get disk DIRectory of data files
1925 J$=%DATAPATH$
1930 REM "Move the file
1935 ERASE F2$,ERR=*PROCEED; RENAME J$+DLM+F1$ TO J$+DLM+F2$
1995 GOTO 5000
4900 REM "END OF LINES
4910 Y[4]=-1; WRITE (F,IND=0)IOL=0290
4920 CALL "ZZEXPF",X3$,X4$,"X"+STR(F),"",G9; IF G9>0 THEN EXITTO 1300
4925 EXTRACT (F,IND=0)IOL=0290
4930 Z=NUM(FIN(F,"MAXREC")); Y[3]=Z-1
4935 Y[4]=-2; WRITE (F,IND=0)IOL=0290
4940 GOTO 5215
4980 EXITTO 1300
5000 REM "EOJ
5010 PRINT @(0,15),'CE',
5020 REM "IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"PROCESS COMPLETE!!","","",0
5040 GOTO 9900
5200 REM "GET NEXT INDEX TO FILE IN A9
5205 F=Z[3]; DIM Y[4]
5210 EXTRACT (F,IND=0,ERR=5295)IOL=0290
5212 IF Y[4]=-2 THEN READ (F); GOTO 5270
5215 IF Y[0]>=Y[3] OR Y[2]>=Y[3] THEN GOTO 4900 ELSE Y[4]=-2
5220 WRITE (F,IND=0)IOL=0290
5230 IF Y[1]<1 THEN A9=Y[2],Y[2]=Y[2]+1 ELSE A9=Y[1]; READ (F,IND=A9)Y[1]
5240 Y[0]=Y[0]+1,Y[4]=-1
5250 WRITE (F,IND=0)IOL=0290
5255 U1=0
5260 RETURN 
5270 LOCK (F,ERR=5210); Y[4]=-1; GOTO 5215
5295 IF ERR<>0 THEN EXITTO 1300 ELSE RETRY 
5400 REM "WRITE OUT INDEXS BETWEEN SOURCE JOURNALS
5415 IF PREV<>0 THEN READ (Z[3],IND=PREV)IOL=GL8_IOLIST
5420 NEXTT=-1
5430 WRITE (Z[3],IND=PREV)IOL=GL8_IOLIST
5440 DIM GL8$(82),GL8[3]
5450 GOSUB 5200
5460 NEXTT=0; WRITE (Z[3],IND=A9)IOL=GL8_IOLIST
5470 PREV=A9
5490 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(10,5),"Updating Gateway Records to G/L Journal Entry Records"
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7500 CONVERT_YYMMDD:REM "Convert date in YYMMDD format into TopForm format, assume YY <=50 means 20+YY >50 means 19+YY
7501 REM "YYMMDD in date$,result in DATE_RESULT$
7505 DATE_RESULT$=""
7510 IF LEN(DATE$)<6 THEN GOTO CONVERT_YYMMDD_END
7515 IF DATE$(1,2)<="50" THEN DATE$="20"+DATE$ ELSE DATE$="19"+DATE$
7520 DATE_RESULT$=CHR(NUM(DATE$(1,3),ERR=7521)-125)+DATE$(4)
7545 CONVERT_YYMMDD_END:RETURN 
7550 REM "Convert Q$(HH:MM:SS) format into Q0$(HHMM) format
7555 Q0$=Q$(1,2)+Q$(4,2)
7595 RETURN 
7600 CONVERT_NUMBER:REM "Convert number in NUM_IN$ to NUM_IN including translating EBCIDIC to ASCII conversion artifacts
7601 REM "Some programs when converting numberic values from EBCIDIC to ASCII will indicate negative numbers by adding a value to the least significant digit when translated the number is negative if the last char is not 0-9. The value will be as follows: }=0, J=1, K=2, L=3, M=4, N=5, O=6, P=7, Q=8, R=9
7605 NUM_IN=0; TMP=POS(NUM_IN$(LEN(NUM_IN$),1)="}JKLMNOPQR")
7610 IF TMP=0 THEN NUM_IN=NUM(NUM_IN$,ERR=CONVERT_NUMBER_END); GOTO CONVERT_NUMBER_END
7615 REM "Convert last char back to num and convert to negative
7620 NUM_IN$(LEN(NUM_IN$),1)=STR(TMP-1:"0"),NUM_IN=-NUM(NUM_IN$,ERR=CONVERT_NUMBER_END)
7645 CONVERT_NUMBER_END:RETURN 
7700 REM "Read next record
7705 IF LEN(I0$)<130 OR POS($0A$=I0$)=0 THEN READ RECORD (Z[1],IND=I9,END=7750)I1$; I0$=I0$+I1$,I9=I9+1; GOTO 7705
7710 IF POS($00$<>I0$)=0 THEN GOTO 7750 ELSE I1=POS($0A$=I0$); IF I1=1 THEN I0$=I0$(2); GOTO 7710 ELSE I$=I0$(1,I1-1),I0$=I0$(I1+1)
7745 RETURN 
7750 REM "End of file
7795 GOTO 1900
7800 GET_CUST_NUM:REM "Take AS8$ and lookup up customer number CUST$ in AS8 sort file, if not found CUST$ will be ''
7805 CUST$=""
7810 READ (Z[5],KEY=AS8$,DOM=7811)
7815 AS8_KEY$=KEY(Z[5],END=GET_CUST_NUM_END)
7820 IF AS8_KEY$(1,LEN(AS8$))=AS8$ THEN CUST$=AS8_KEY$(21)
7845 GET_CUST_NUM_END:RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F2$
8112 T=T0
8115 REM PRINT @(0,7),"There are "+STR(T)+" records to process"
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8135 T1=0
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,I9
8195 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
