0010 REM "Remote Telemarketing Remote Processing <RT2REM>
0015 REM "Prog Type: IP-3.1.2
0020 SETESC 9300; SETERR 9000
0035 REM "4.0 - 08/19/96 - 19.14 - kmc
0040 REM "Copyright 1996 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0070 REM "R$ are parmeters read, Called from RT2BAT
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="RT2REM",X1$="Remote T/M Remote Processing"
0120 DIM S$(40)
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=*NEXT)
0300 REM "IOLISTS
0310 IOLIST A$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="13O ZZPARM "
0520 GOSUB 9750; ON Z0 GOTO *NEXT,9900
0539 REM "Use R$= '!!!' to skip check of RT0, since it may not exist yet
0540 R$="!!!"; CALL "RT2PRM",X3$,X4$,R$; IF R$="" THEN GOTO 9900
0900 MACHINE$=MID(R$,7,1)
0910 IF MACHINE$="H" THEN GOTO PROCESS_SALESPEOPLE
1000 START_MAIN:REM "Get list of files to process"
1010 SP_NAME$=MID(R$,8,4); REM "Put saleman into SP_NAME$
1030 SP_NAME$=SUB(SP_NAME$," ","X")
1040 SP_LIST$=FN%GET_FILES$(%DATAPATH$,"RT"+SP_NAME$+"*","|","N","N")
1050 FILES_DONE%=0
1060 PROCESS_FILES:
1070 IF LEN(SP_LIST$)<4 THEN GOTO END_PROCESS_FILES
1080 CALL "ZZ2FNC;NEXT_DELIMITED_VALUE",FILE_NAME$,SP_LIST$,"|"
1160 IF LEN(FILE_NAME$)<8 THEN {
1170 GOTO PROCESS_FILES
1180  } ELSE {
1181 IF MACHINE$<>"H" AND MID(FILE_NAME$,7,2)="00" THEN GOTO PROCESS_FILES
1210  }
1230 FILES_DONE%=FILES_DONE%+RATIO_DONE
1240 CALL "RT2PIN",X3$,X4$,FILE_NAME$,R0$,R0
1250 IF R0=0 THEN ERASE FILE_NAME$,ERR=*NEXT
1251 GOTO PROCESS_FILES
1260 END_PROCESS_FILES:
1270 IF FILES_DONE%=0 THEN PRINT @(0,3),'CE',@(20,10),"No Remote Telemarketing Packages to pro"+"cess."
1280 IF MACHINE$="H" THEN GOTO READ_NEXT_RTO_FILE ELSE GOSUB UPDATE_SP_RTO_REC; GOTO END_MAIN
1300 PROCESS_SALESPEOPLE:REM "Get files for each Salesperson
1310 Z$="01O RT0... "; GOSUB 9750; ON Z0 GOTO *NEXT,9900
1320 READ (Z[1],KEY="",DOM=*NEXT)
1330 READ_NEXT_RTO_FILE:
1340 K1$=KEY(Z[1],END=END_MAIN); READ (Z[1],KEY=K1$,DOM=*SAME)IOL=0310; IF K1$="    " THEN GOTO *SAME
1350 L$="",L_LOWER$=""
1351 SP_NAME$=MID(A$,1,4); REM "Put current salesperson into SP_NAME$
1360 SP_NAME$=SUB(SP_NAME$," ","X"); REM "Replaces spaces, if any with an 'X'
1370 HOST_MAIL_ADDR$=MID(A$,152,30)
1390 REM 
1400 MAIL_FILE:
1401 TEMP_DIR$=LWD; CWDIR "/usr/spool/mail"
1405 IF MID(A$,151,1)<>"E" THEN GOTO END_MAIL_FILE
1410 MAILBOXNAME$=MID(HOST_MAIL_ADDR$,1,POS("@"=HOST_MAIL_ADDR$)-1)
1420 CLOSE (14); OPEN (14,ERR=END_MAIL_FILE)MAILBOXNAME$
1430 READ (14,END=END_MAIL_FILE)TMP$; IF POS(MAILBOXNAME$=TMP$)=0 THEN GOTO END_MAIL_FILE
1440 CLOSE (14)
1460 CALL "RT2MOV",X3$,X4$,A$,SP_NAME$
1470 ERASE MAILBOXNAME$,ERR=END_MAIL_FILE
1480 END_MAIL_FILE:CWDIR TEMP_DIR$
1500 SP_LIST$=FN%GET_FILES$(%DATAPATH$+DLM,"HT"+SP_NAME$+"*","|","N","N"); REM "Setup target string
1570 IF LEN(SP_LIST$)=0 THEN GOTO READ_NEXT_RTO_FILE; REM "If no packages for this salesperson, just go on to the next one
1580 A$(67,6)=X3$(21,6),A$(73,4)=STR(INT(TIM*100):"0000")
1590 WRITE (Z[1],KEY=K1$)IOL=0310
1600 GOTO PROCESS_FILES; REM "Process the list of files for this salesperson
5000 END_MAIN:REM "End of Print
5200 REM "
5210 IF Q1$="" THEN CALL "ZZPROM","4",X3$,0,"Process Complete!","","",0
5290 GOTO 9900
7600 UPDATE_SP_RTO_REC:
7601 REM "Update saleperson record in RT0 based R$(8,4), only done at remote computer
7604 Z$="01O RT0... "; GOSUB 9750; ON Z0 GOTO *NEXT,END_MAIN
7605 READ (Z[1],KEY=R$(8,4),DOM=7645)IOL=0310
7630 A$(67,6)=X3$(21,6),A$(73,4)=STR(INT(TIM*100):"0000")
7635 WRITE (Z[1],KEY=R$(8,4))IOL=0310
7645 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9120 ON Y8 GOTO 9900,0990,9920
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1255,2040
9740 RETURN 
9750 REM "FILES
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9790 RETURN 
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
