0010 REM "<RT2UT1> Load RT3 from filelist
0020 SETESC 9300; SETERR 9000
0035 REM "3.9 - 12/07/95 - 16.65 - kmc
0040 REM "Copyright 1995 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="RT2UT1",X1$="Load RT3 from filelist "
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$
0320 RT3_IOL:IOLIST RT3$
0330 ZZE_IOL:IOLIST ZZE$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O filelist 02O RT3...  03O ZZE 13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 CLOSE (Z[1]); OPEN LOCK (Z[1],OPT="TEXT")"filelist"; REM "Reopen as text type
0540 FILELIST_FILE=Z[1],RT3_FILE=Z[2],ZZE_FILE=Z[3],ZZPARM_FILE=Z[13]
0600 REM "
0610 GOSUB 6000
0620 GOSUB 8100
0640 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0710 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Clear command list after command 000100?","","",0; IF Z=0 THEN GOSUB CLEAR_RT3
0800 REM "Get starting number
0810 READ (RT3_FILE,KEY=$FFFFFFFFFFFF$,DOM=0811)
0815 KEY$=KEP(RT3_FILE,END=0816); GOTO 0820
0816 START_NO=10; GOTO 0825
0820 START_NO=(INT(NUM(KEY$)/10)+1)*10
0825 IF START_NO<100 THEN START_NO=100; REM "Save 1st 100 for custom
1000 REM "BEGIN MAIN PROCESS
1010 READ (Z[1],END=5000)FILE_NAME$
1020 C=C+LEN(FILE_NAME$)+2,COUNT=COUNT+1; IF COUNT>10 THEN COUNT=0; GOSUB 8150
1025 DIM RT3$(81)
1100 REM "Determine file type, then make entry
1103 IF FILE_NAME$(1,1)="!" THEN MAKEIT=1,FILE_NAME$=FILE_NAME$(2) ELSE MAKEIT=0
1105 CLOSE (14); IF LEN(FILE_NAME$)=6 AND FILE_NAME$(4,3)="..." THEN OPEN (14,ERR=END_OF_RECORD)FILE_NAME$(1,3)+X3$(9,3) ELSE OPEN (14,ERR=END_OF_RECORD)FILE_NAME$
1110 FID$=FID(14); CLOSE (14); IF FID$(10,1)=$04$ THEN GOTO DO_PROGRAM ELSE GOTO DO_FILE
1200 DO_PROGRAM:REM "Add program to the list
1210 CALL "ZZ2PRG",PROG_NAME$,14,FILE_NAME$,0,RET
1215 RT3$(7,4)="PROG",RT3$(11,30)=FILE_NAME$,RT3$(41,40)=PROG_NAME$
1220 GOSUB WRITE_RT3
1295 GOTO END_OF_RECORD
1300 DO_FILE:REM "add file to the list
1305 IF MAKEIT=1 THEN GOTO DO_MAKE
1310 DIM ZZE$(100); FIND (ZZE_FILE,KEY=FILE_NAME$(1,3)+"   ",DOM=1311)IOL=ZZE_IOL
1320 RT3$(7,4)="DATA",RT3$(11,30)=FILE_NAME$,RT3$(41,40)=ZZE$(7,39)
1330 GOSUB WRITE_RT3
1395 GOTO END_OF_RECORD
1400 DO_MAKE:REM "Add make file command to the list
1410 DIM ZZE$(100); FIND (ZZE_FILE,KEY=FILE_NAME$(1,3)+"   ",DOM=1411)IOL=ZZE_IOL
1420 RT3$(7,4)="MAKE",RT3$(11,30)=FILE_NAME$,RT3$(41,40)=ZZE$(7,39)
1430 GOSUB WRITE_RT3
1495 GOTO END_OF_RECORD
1600 END_OF_RECORD:GOTO 1000
5000 REM "EOJ
5010 C=T; GOSUB 8150
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This TopForm utility program will take a list of file na"+"mes in a text file ",@(0,5),"named 'filelist' and enter them into the"+" RT3 file "+"after the last command that ",@(0,6),"is there now. The "+"sequence number will be"+" increment by 10 for each command",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7500 WRITE_RT3:REM "Write out RT3 record
7505 IF CURRENT_NO=0 THEN CURRENT_NO=START_NO ELSE CURRENT_NO=CURRENT_NO+10
7510 RT3$(1,6)=STR(CURRENT_NO:"000000")
7520 WRITE (RT3_FILE,KEY=RT3$(1,6))IOL=RT3_IOL
7545 RETURN 
7550 CLEAR_RT3:REM "Clear RT3 of all records after 000100. records below t"+      "hat will be perserved.
7554 PRINT @(20,19),"Clearing RT3...",
7555 READ (RT3_FILE,KEY="000099",DOM=7556)
7560 RT3_KEY$=KEY(RT3_FILE,END=7590)
7565 REMOVE (RT3_FILE,KEY=RT3_KEY$,DOM=7560); GOTO 7560
7590 PRINT @(0,19),'CE',
7595 RETURN 
8100 REM 
8110 REM "CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F$
8115 FID$=FN%XFD$(Z[1],0),T=DEC(FID$(38,4))
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar g"+          "raph given total #, T and current #, C, and time counter T1 (T1 i"+          "s init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,C
8195 RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
