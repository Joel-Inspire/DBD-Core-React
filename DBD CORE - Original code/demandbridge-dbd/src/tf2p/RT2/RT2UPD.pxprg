0010 REM "<RT2UPD> Remote T/M Update Remote
0020 SETESC 9300; SETERR 9000
0035 REM "3.9 - 05/23/96 - 9.26 - kmc
0040 REM "Copyright 1995 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="RT2UPD",X1$="Update Remote Data for Remote T/M"
0120 DIM Z0$(80,"-"),NULLS$(512,$00$)
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$
0320 IOLIST B$
0330 IOLIST C$
0340 IOLIST D$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O RT0...  02O AR3... 03O ZZE... 04O RT5... 13O ZZPARM  "; REM "Slots 11 & 12 reserved for temp use by program
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0525 J5$=FID(Z[3]); J5$=J5$(4,6)
0600 REM "
0605 GOSUB 8100
0610 GOSUB 6000
0612 IF LEN(Q1$)>=4 THEN A$=Q1$(1,4); GOTO 0625
0615 DIM A$(4)
0620 CALL "ZZENTR","SZXUX",A{ALL},A$,X4$,X3$,36,10,1,4,C0,"","{1"+X$,"","RT"+"2UPD00","RT0","RT2MAA",""; IF ABS(C0)>4 THEN PRINT @(0,3),'CE',; GOSUB 6000; PRINT @(36,10),A$(1,4),; C0=ABS(C0)-5; ON C0 GOTO 0620,0621
0623 IF ABS(C0)=4 THEN GOTO 9900
0624 IF POS(" "<>A$(1,4))=0 THEN DIM A$(200),B$(200); B$(5,35)="All Salespeople"; GOTO 0632
0625 READ (Z[1],KEY=A$(1,4),DOM=0620)IOL=0310
0630 FIND (Z[2],KEY=A$(1,4),DOM=0631)IOL=0320
0632 PRINT @(36,10),MID(A$,1,4),@(42,10),MID(B$,5,35),
0690 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0691,9900
0900 REM "Initialize
0910 PRINT @(10,17),"Building Initial Package:",
0920 COMMAND_COUNT=0; GOSUB 8150
1000 REM "BEGIN MAIN PROCESS
1003 PRINT @(10,14),"Now Processing: ",
1005 IF POS(" "<>A$(1,4))<>0 THEN IF POS(" "<>A$(5,3))=0 THEN J$="Operator code is blank!"; CALL "ZZPROM",".4",X3$,Z,J$,"","",0; GOTO 9900
1100 GET_COMMAND:READ (Z[4],END=DONE_WITH_COMMANDS)IOL=0340
1102 COMMAND_COUNT=COMMAND_COUNT+1; IF MOD(COMMAND_COUNT,T0)=1 THEN GOSUB 8150
1105 ON (POS(D$(7,4)="MESSPROGDATAMAKE",4)-1)/4 GOTO DO_MESS,DO_PROG,DO_DATA,DO_MAKE,END_OF_COMMAND
1150 DO_MESS:
1155 M$=FNS$(D$(11,30))
1175 GOSUB 7850
1195 GOTO END_OF_COMMAND
1200 DO_PROG:
1205 F9$=FNS$(D$(11,30)); IF LEN(F9$)>8 THEN F9$=F9$(1,8)
1235 GOSUB PACK_PROGRAM
1245 GOTO END_OF_COMMAND
1250 DO_DATA:
1255 TEMP$=FNS$(D$(11,30))
1256 REM "Check for key restrictions, if none the goahead
1257 BAR_POS=POS("|"=TEMP$); IF BAR_POS=0 THEN F9$=TEMP$,T9$=""; GOTO 1285 ELSE F9$=TEMP$(1,BAR_POS-1),T9$=TEMP$(BAR_POS+1); TOKEN$="{O}",REP$=A$(5,3),TARGET$=T9$; GOSUB 7900; T9$=TARGET$; TOKEN$="{S}",REP$=A$(1,4),TARGET$=T9$; GOSUB 7900; TOKEN$="{C}",REP$=X3$(9,3); GOSUB 7900; T9$=TARGET$
1260 IF POS("..."=F9$)<>0 THEN F9$(POS("..."=F9$),3)=X3$(9,3)
1285 GOSUB 2000
1295 GOTO END_OF_COMMAND
1300 DO_MAKE:
1305 F9$=FNS$(D$(11,30))
1310 IF POS("..."=F9$)<>0 THEN F9$(POS("..."=F9$),3)=X3$(9,3)
1335 GOSUB 7800
1345 GOTO END_OF_COMMAND
1850 END_OF_COMMAND:GOTO GET_COMMAND
1900 DONE_WITH_COMMANDS:
1995 GOTO END_OF_PROCESS
2000 REM "Given file F9$, open file and put all records into RT1 file
2001 REM "Must match target T9$, if T9$<>""
2005 GOSUB OPEN_F9_ON_11
2012 GOSUB 7550
2013 DIM C$(70); FIND (Z[3],KEY=F9$(1,3)+"   ",DOM=2014)IOL=0330
2014 IF F9$(1,6)="ZZPARM" THEN C$(7,39)="System Parameters"; PRINT @(26,14),"ZZPARM ",C$(7,39), ELSE PRINT @(26,14),F9$(1,3)+" "+C$(7,39),
2015 GOSUB 7800
2016 EXTRACT (Z[11],KEY=T9$,DOM=2017); REM "Extract, because we may have the full key
2017 K9$=KEY(Z[11],END=2040); IF T9$<>"" AND K9$(1,LEN(T9$))<>T9$ THEN GOTO 2040 ELSE READ RECORD (Z[11],KEY=K9$,DOM=2017)A9$
2025 GOSUB 7500; CALL "RT2WOR",X3$,X4$,A$(1,4),F9$,"U",K9$,A9$,0,A{ALL}; GOSUB 7650; IF POS(F9$(1,3)="ZP4ZP5ZP8",3)<>0 THEN GOSUB 7700 END_IF ; GOTO 2017
2045 RETURN 
2120 PRINT @(10,12),C3:"###,##0","   ",J3$," ",D$(7,39)
2200 PACK_PROGRAM:
2210 PRINT @(26,14),'CL',F9$,
2214 REM "Write record to create a Text Type file to copy into
2215 GOSUB 7800
2219 REM "Write out record to send the program
2220 CALL "RT2WOR",X3$,X4$,A$(1,4),F9$,"B","","",0,A{ALL}
2245 RETURN 
5000 END_OF_PROCESS:
5010 PRINT @(0,12),'CL',@(0,14),'CL',@(0,15),'CL',@(0,16),'CL',
5015 TOTAL_COMMANDS=COMMAND_COUNT; GOSUB 8150
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5025 CALL "ZZINIT",STR(Z[4]:"00"); REM "Clear RT5 file for later use
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(10,10),"Get data for salesperson:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7500 REM "Remove nulls and last seperator from read record string A9$
7505 IF LEN(A9$)=0 THEN GOTO 7545 ELSE A9$=FNN$(A9$); IF A9$(1,LEN(A9$))=$8A$ THEN A9$=A9$(1,LEN(A9$)-1)
7545 RETURN 
7550 J6$=FID(Z[3],ERR=7570)
7551 REM "Uses J5$, set in line 525
7555 IF J6$(4,6)<>J5$ THEN CLOSE (Z[3]); GOTO 7570
7565 GOTO 7595; REM "If here then file is open on ZZE
7570 OPEN (Z[3])J5$; REM "If here the file is closed, so open it up
7595 RETURN 
7600 REM "Initialize for indicator
7605 C9=0,L9=50; PRINT @(10,16),'CL',
7645 RETURN 
7650 REM "Display Activity indicator
7670 C9=C9+1; IF MOD(C9,5)=1 THEN C8=INT(C9/5); IF C8=L9+1 THEN PRINT @(10,16),'CL',; C9=0 ELSE PRINT @(10+C8,16),".",
7695 RETURN 
7700 REM "Handle files ZP8, ZP4, & ZP5
7705 ON INT(POS(F9$(1,3)="ZP4ZP5ZP8",3)+2/3) GOTO 7795,7710,7720,7730,7795
7710 REM "ZP4
7712 J9$=A9$(1,4)+A9$(419,20)+A9$(5,6); CALL "RT2WOR",X3$,X4$,A$(1,4),"ZPA"+F9$(4),"U",J9$,"",0,A{ALL}; REM "ZPA
7713 IF POS(X3$(9,3)="060",3)<>0 THEN J9$=A9$(200,12)+A9$(1,10) ELSE J9$=A9$(200,10)+A9$(1,10)
7714 CALL "RT2WOR",X3$,X4$,A$(1,4),"ZPD"+F9$(4),"U",J9$,"",0,A{ALL}; REM "ZPB
7716 J9$=A9$(203,7)+A9$(200,3)+A9$(1,10); CALL "RT2WOR",X3$,X4$,A$(1,4),"ZPE"+F9$(4),"U",J9$,"",0,A{ALL}; REM "ZPE
7719 GOTO 7795
7720 REM "ZP5
7722 CALL "ZZDATE",X3$,"","G10101",A9$(11,6),D0,1,0,0,0,0,0,"","",""; J9$=A9$(1,10)+STR(100000-D0:"00000")+A9$(11,7)
7724 CALL "RT2WOR",X3$,X4$,A$(1,4),"ZPB"+F9$(4),"U",J9$,"",0,A{ALL}; REM "ZPB
7729 GOTO 7795
7730 REM "ZP8
7739 GOTO 7795
7795 RETURN 
7800 REM "Write out record to create file if not found
7804 IF POS("..."=F9$)<>0 THEN F9$(POS("..."=F9$),3)=X3$(9,3)
7805 DISK_NO$=""; CLOSE (14); OPEN (14,ERR=7810)F9$; FID_14$=FID(14); CLOSE (14); DISK_NO$=STR(DEC(FID_14$(20,1)))
7810 CALL "RT2WOR",X3$,X4$,A$(1,4),F9$,"C",DISK_NO$,"",0,A{ALL}
7845 RETURN 
7850 REM "Write out message record
7855 PRINT @(10,12),M$,
7860 CALL "RT2WOR",X3$,X4$,A$(1,4),"","M",M$,"",0,A{ALL}
7895 RETURN 
7900 REM "If TOKEN$ is in TARGET$, then replace with REP$
7905 TOKEN_POS=POS(TOKEN$=TARGET$); IF TOKEN_POS=0 THEN GOTO 7945
7910 IF TOKEN_POS=1 THEN TARGET$=REP$+TARGET$(LEN(TOKEN$)+1) ELSE IF TOKEN_POS=LEN(TARGET$)-LEN(TOKEN$)+1 THEN TARGET$=TARGET$(1,TOKEN_POS-1)+REP$ ELSE TARGET$=TARGET$(1,TOKEN_POS-1)+REP$+TARGET$(TOKEN_POS+LEN(TOKEN$))
7945 RETURN 
8000 OPEN_F9_ON_11:REM "OPEN file f9$, save name in H9$, so can close later
8005 CLOSE (Z[11],ERR=8006)
8010 FILE_NAME$=F9$,INDEX=11; GOSUB 9750
8020 GOSUB 7600
8045 RETURN 
8100 REM "Setup for bar graph
8110 CALL "ZZINFO",Z[4],T9,X3$,TOTAL_COMMANDS,T0,K,B,D,S0,S1,F$
8115 REM "PRINT @(0,7),"THERE ARE "+STR(T)+"RECORDS TO PROCESS"
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar g"+          "raph given total #, T and current #, C, and time counter T1 (T1 i"+          "s init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,TOTAL_COMMANDS,COMMAND_COUNT
8195 RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
8955 DEF FNN$(Z9$)=Z9$(1,POS(NULLS$=Z9$+NULLS$)-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9750 REM "FILES - Use instead of ZZFLES
9755 Z[INDEX]=UNT
9760 IF LEN(FILE_NAME$)<>3 THEN GOTO 9770
9765 OPEN (Z[INDEX],ERR=9780)FILE_NAME$+X3$(9,3); GOTO 9781
9770 IF POS("..."=FILE_NAME$)<>0 THEN FILE_NAME$(POS("..."=FILE_NAME$),3)=X3$(9,3)
9780 OPEN (Z[INDEX])FILE_NAME$
9790 RETURN 
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
