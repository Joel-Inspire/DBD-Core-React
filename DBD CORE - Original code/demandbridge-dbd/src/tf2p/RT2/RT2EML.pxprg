0005 REM **** RUN IN UNIX ONLY *****
0010 REM "REMOTE TELEMARKETING EMAIL FILE UPDATE <RT2EML>
0020 SETESC 9300; SETERR ERROR_PROCESSING
0035 REM "5.6 - 03/17/08 - 12.000277 - tma - SSP# 142887
0037 REM "142887-ProvideX Edits                                              
0040 REM "Copyright 2008 DemandBridge, Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM Winnt requires SMTP turned on, provided by IIS  
0060 REM "  and uuencode1.exe installed and registered for windows shell
0080 REM PARAMTERS Q0$ = RT0 RECORD, Q1$ = SALES PERSON KEY
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR ERROR_PROCESSING
0110 X0$="RT2EML",X1$="CREATE AN EMAIL FILE UPDATE"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0140 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0150 IF POS("MS"=UCS(SYS)) THEN ISWIN=1 ELSE ISWIN=0
0160 ! 
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=*NEXT)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0296 ! 
0300 REM "IOLISTS
0310 IOLIST A$
0320 ! 
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 GOSUB 7600
0610 REM GOSUB 6000
0620 REM GOSUB GET_RECORD_CNT
0640 IF Q1$="" OR Q0$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Setup Information missing, Proceed?","","",0; ON Z GOTO 0641,9900
0650 PRINT @(0,19),'CL'
0660 ! 
1000 MAIN_PROCESS:
1010 REM GOSUB GENERATE; END ; REM "USE THIS TO GENERATE NEW EMAIL SECTION:
1020 REM **** GET SALES PERSON DATA,4 DIGIT NAME WITH "X"S ****
1030 OUT_EMAIL$=STP(Q0$(121,30),2)
1040 RET_EMAIL$=STP(Q0$(152,30),2)
1050 SP_NAME$=PAD(SUB(Q1$," ","X"),4,1,"X")
1051 IF OUT_EMAIL$="" OR RET_EMAIL$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Setup Information missing, Proceed?","","",0; ON Z GOTO *NEXT,9900
1060 REM **** GET RT(SALESPERSON) FILES TRANSFER LIST ****
1070 PRINT @(0,14),'CL',@(10,14),"Sales Person: ",@(25,14),SP_NAME$
1080 PRINT @(0,16),'CL',@(0,17),'CL',@(10,16),"Files to send:"
1090 PRINT @(0,18),'CL',@(10,18),"File: "
1100 PRINT @(0,20),'CL',@(25,20),"Action: "
1120 H_FILES=FN%GET_FILES_AS_OPEN_FILE(%DATAPATH$,"RT"+SP_NAME$+"*","N")
1130 EMFCNT$=FIN(H_FILES,"NUMREC")
1140 FILE_CNT=NUM(EMFCNT$)
1150 FOR FILE_REC=1 TO FILE_CNT
1160 CH1=UNT
1170 READ RECORD (H_FILES,RNO=FILE_REC)TXTFNAME$
1171 TXTFNAME$=KEC(H_FILES)
1190 SCR_FLIST$=SCR_FLIST$+TXTFNAME$+","
1200 OPEN (CH1,ERR=ERROR_PROCESSING)TXTFNAME$
1210 FILE_LEN$=FIN(CH1)
1220 FILE_LEN=DEC(FILE_LEN$(1,4))
1230 TOTAL_BYTES=TOTAL_BYTES+FILE_LEN
1240 PRINT @(25,16),'CL',@(25,16),SCR_FLIST$
1250 PRINT @(25,18),'CL',@(25,18),TXTFNAME$
1260 PRINT @(25,20),'CL',@(25,20),"Building File list"
1270 CLOSE (CH1)
1280 REM IF FILE IS LARGE, CREATE MULTIPLE FILES OF @ DISKETTE SIZE
1290 IF FILE_LEN>1400000 THEN {
1300 PRINT @(25,20),'CL',@(25,20),"Splitting File"
1301 IF ISWIN THEN CALL "ZZ2FNC;SPLIT","-b1400000","-a1",%DATAPATH$+DLM+TXTFNAME$,%DATAPATH$+DLM+TXTFNAME$; GOTO 1330
1310 CMDTXT$="split -b1400000 -a1 "+%DATAPATH$+DLM+TXTFNAME$+" "+%DATAPATH$+DLM+TXTFNAME$
1320 INVOKE CMDTXT$
1330 REM GOSUB MOVE_FILE
1340 ERASE %DATAPATH$+DLM+TXTFNAME$
1350  }
1360 NEXT FILE_REC
1370 CLOSE (H_FILES)
1380 GOSUB SET_BARGRAPH_RECORD_CNT
1390 REM **** LOOP THROUGH RT(SALESPERSON) FILES AND SEND EMAIL ****, IF FILE IS LARGE, CREATE MULTIPLE EMAILS WITH ATTACHMENTS OF @ DISKETTE SIZE, AT END MOVE FILE TO DONE DIRECTORY
1400 H_FILES=FN%GET_FILES_AS_OPEN_FILE(%DATAPATH$,"RT"+SP_NAME$+"*","N")
1410 EMFCNT$=FIN(H_FILES,"NUMREC")
1420 REM **** START LOOP ****
1421 FILE_CNT=DEC(EMFCNT$)
1430 FOR FILE_REC=1 TO FILE_CNT
1440 READ RECORD (H_FILES,RNO=FILE_REC,END=END_CREATE_EMAIL)TXTFNAME$
1450 TXTFNAME$=KEC(H_FILES)
1470 PRINT @(25,18),'CL',@(25,18),TXTFNAME$
1480 PRINT @(25,20),'CL',@(25,20),"Building Email File"
1490 ! 
1500 CREATE_EMAIL:REM **** CREATE TEXT EMAIL FILE ****
1510 REM OPEN TEMPORARY FILE AND APPEND CURRENT TXTFNAME INTO IT
1522 CH1=UNT
1525 EMAIL_FILE$=TXTFNAME$; EMAIL_FILE$(1,1)="E"
1530 ERASE %DATAPATH$+DLM+EMAIL_FILE$,ERR=*NEXT
1540 SERIAL EMAIL_FILE$,0,0,ERR=*NEXT
1550 OPEN LOCK (CH1,ERR=ERROR_PROCESSING)EMAIL_FILE$
1560 REM **** LOOP THROUGH ARRAY AND BUILD TEMP FILE EMAIL PORTION ****
1570 GOSUB EMAIL_HEADER
1571 LINE_LIMIT=FN%NEA("EMAIL_HDR_LINE$",1)-1
1580 FOR LINECNT=0 TO LINE_LIMIT
1590 WRITE RECORD (CH1,ERR=*NEXT)EMAIL_HDR_LINE$[LINECNT]
1610 END_ARRAY:NEXT LINECNT
1611 ! 
1620 ADD_FILE_TO_EMAIL:
1640 REM **** READ UPDATE FILE AND APPEND TO EMAIL ****
1650 CLOSE (CH1)
1670 PRINT @(0,20),'CL',@(10,20),"Action: ",@(25,20),"Encoding Attachment"
1671 IF ISWIN THEN CMDTEXT$="uuencode1.exe "+%DATAPATH$+DLM+TXTFNAME$+" "+%DATAPATH$+DLM+EMAIL_FILE$; GOTO 1691
1680 REM LET CMDTEXT$="uuencode "+%DATAPATH$+DLM+TXTFNAME$+" "+TXTFNAME$+" > "+%DATAPATH$+DLM+TXTFNAME$+".uue"
1690 CMDTEXT$="uuencode "+%DATAPATH$+DLM+TXTFNAME$+" "+TXTFNAME$+" >> "+%DATAPATH$+DLM+EMAIL_FILE$
1691 INVOKE WAIT CMDTEXT$
1692 ! 
1700 REM LET CMDTEXT$="cat "+%DATAPATH$+DLM+TXTFNAME$+".uue >> "+%DATAPATH$+DLM+EMAIL_FILE$
1710 REM INVOKE WAIT CMDTEXT$
1750 REM **** CLOSE & SAVE FILES ****
1760 CLOSE (CH1)
1770 REM SEND EMAIL
1780 PRINT @(25,20),'CL',@(25,20),"Sending Email"
1781 IF ISWIN THEN CALL "ZZ2FNC;SENDMAIL",%DATAPATH$+DLM+EMAIL_FILE$; GOTO 1810
1790 CMDTXT$="sendmail -t <"+%DATAPATH$+DLM+EMAIL_FILE$
1801 INVOKE WAIT CMDTXT$
1810 GOSUB GET_FILE_SIZE
1890 ERASE_FILE:REM **** ERASE DATA FILE AND ENCODED FILE ****
1900 PRINT @(0,20),'CL',@(10,20),"Action: ",@(25,20),"Erasing temporary work Files"
1910 ERASE %DATAPATH$+DLM+TXTFNAME$,ERR=*NEXT
1930 REM **** DELETE EMAIL FILE ****
1940 ERASE %DATAPATH$+DLM+EMAIL_FILE$,ERR=*NEXT
1945 PRINT @(25,18),'CL',@(25,20),'CL'
1960 NEXT FILE_REC
1970 END_CREATE_EMAIL:
1980 ! 
5000 END_OF_JOB:
5010 PRINT @(0,7),'CL',@(0,14),'CL',@(0,16),'CL',@(0,17),'CL',@(0,18),'CL',@(0,20),'CL'
5011 CLOSE (CH1); CLOSE (CH2); CLOSE (H_FILES)
5020 REM complete bargraph
5040 CUR_BYTES=T; GOSUB UPDATE_BAR_GRAPH
5050 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5060 GOTO 9900
5070 ! 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=*NEXT)'SB',
6010 PRINT @(0,22),'CL'
6165 PRINT (0,ERR=*NEXT)'SF',
6190 RETURN 
6200 ! 
7000 MOVE_FILE:REM **** MOVE FILE TO DONE DIRECTORY ****     110000
7003 REM **** GET DATA DIRECTORY ****
7006 DEST_DIR$=%DATAPATH$+DLM+"done"
7010 REM LET CMDTXT$="mkDIR "+DEST_DIR$
7020 REM INVOKE CMDTXT$
7030 REM LET CMDTXT$="mv "+%DATAPATH$+DLM+TXTFNAME$+" "+DEST_DIR$+DLM+TXTFNAME$
7040 REM INVOKE CMDTXT$
7050 END_MOVE_FILE:RETURN 
7060 ! 
7200 GET_FILE_SIZE:
7210 CH2=UNT
7220 OPEN LOCK (CH2,ERR=ERASE_FILE,OPT="TEXT")TXTFNAME$
7230 FILE_LEN$=FIN(CH2)
7240 CLOSE (CH2)
7250 FILE_LEN=DEC(FILE_LEN$(1,4))
7260 CUR_BYTES=CUR_BYTES+FILE_LEN
7270 GOSUB UPDATE_BAR_GRAPH
7280 END_GET_FILE_SIZE:RETURN 
8000 ! 
8100 SET_BARGRAPH_RECORD_CNT:REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8115 PRINT @(0,7),"There are "+STR(TOTAL_BYTES)+" bytes to process"
8129 REM "Set T0, we make sure T0 is > 1, because later on we MOD and look for avalue of 1. IF T0 is 1, then nothing would get reported. We look for a result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T0'th record to get the first report
8130 T0=INT(TOTAL_BYTES*.02); IF T0<=1 THEN T0=2
8145 END_SET_BARGRAPH_RECORD_CNT:RETURN 
8150 UPDATE_BAR_GRAPH:REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"H",19,10,50,T1,TOTAL_BYTES,CUR_BYTES
8195 END_UPDATE_BAR_GRAPH:RETURN 
8196 ! 
8500 GENERATE:REM "***** LOGIC TO REGENERATE EMAIL * RUN MANUALLY  *****
8510 CLOSE (14); OPEN LOCK (14,OPT="TEXT")"plh500"
8520 CALL "ZZINFO",Z[14],T9,X3$,T,T0,K,B,D,S0,S1,F$
8530 LINECNT=8605,CNT=0
8532 EXECUTE "0"+STR(LINECNT)+" REM **** HARDCODED EMAIL HEADER AND BODY VARIABLES ******"; LINECNT=LINECNT+10
8535 EXECUTE "0"+STR(LINECNT)+" DIM EMAIL_HDR_LINE$[50]"; LINECNT=LINECNT+10
8540 REM START OF EMAIL TEMPLATE LOOP
8550 READ (14,ERR=GENERATE_END)ETEMPLATE$
8560 EXECUTE "0"+STR(LINECNT)+" LET EMAIL_HDR_LINE$["+STR(CNT)+"]="+QUO+ETEMPLATE$+QUO
8570 LINECNT=LINECNT+5,CNT=CNT+1
8580 GOTO 8550
8590 GENERATE_END:RETURN 
8600 ! 
8601 EMAIL_HEADER:REM **** HARDCODED EMAIL HEADER AND BODY VARIABLES ******
8610 DIM EMAIL_HDR_LINE$[8]
8620 EMAIL_HDR_LINE$[0]="Reply-To: <"+RET_EMAIL$+">"; REM +$0A$
8630 EMAIL_HDR_LINE$[1]="From: <"+RET_EMAIL$+">"; REM +$0A$
8640 EMAIL_HDR_LINE$[2]="To: <"+OUT_EMAIL$+">"; REM +$0A$
8650 EMAIL_HDR_LINE$[3]="Subject: TopForm R/T Transfer of file "+TXTFNAME$; REM +$0A$
8660 EMAIL_HDR_LINE$[4]="Importance: Normal"; REM +$0A$
8670 EMAIL_HDR_LINE$[5]=""; REM + $0A$
8680 EMAIL_HDR_LINE$[6]=""; REM +$0A$
8690 EMAIL_HDR_LINE$[7]="Run RT2WIN to update topform remote telemarketing. Keep this message marked as unread. This message will be automatically deleted or stored in the folder specified in the RT2WIN Configuration. TopForm Software, Inc."; REM +$0A$
8700 EMAIL_HDR_LINE$[8]=""; REM $0A$
8745 EMAIL_HEADER_END:RETURN 
8746 ! 
8750 GET_FILE:
8751 LOCAL CH1,FILES_FOUND$
8752 CH1=UNT
8753 OPEN (CH1)"*Memory*"
8754 SELECT FILES_FOUND$ FROM %DATAPATH$ WHERE FILES_FOUND$ LIKE "RT"+SP_NAME$+"*"
8755 WRITE (CH1,KEY=FILES_FOUND$,DOM=*NEXT)
8756 NEXT RECORD 
8757 RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 ERROR_PROCESSING:
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR ERROR_PROCESSING
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR ERROR_PROCESSING; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR ERROR_PROCESSING; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR ERROR_PROCESSING; GOSUB 6400
9520 ON C9 GOTO 1290,1965
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9913 IF H_FILES<>0 THEN CLOSE (H_FILES)
9916 IF CH1<>0 THEN CLOSE (CH1)
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9999 END 
