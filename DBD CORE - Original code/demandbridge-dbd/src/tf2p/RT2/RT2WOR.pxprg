0010 REM "<RT2WOR> Write Remote T/M Output Record
0015 REM "Prog Type: IP-3.1.2
0020 SETESC 9300; SETERR 9000
0035 REM "3.9 - 12/11/95 - 16.79 - kmc
0040 REM "Copyright 1995 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0070 REM "SP_NAME$=saleperson number (or '    ' for all)
0071 REM >>'!'+operator code will"+          " substitute the salesperson, if saleperson not found,
0072 REM >>then the re"+          "cord will not be written out if this is the host
0073 REM "F$ = file name
0074 REM FILE_OP_CDE$= "U"pdate, "D"elete, "C"reate file, "P"rogram file
0076 REM "KEY_VAL$=key value, S1$=string value from record
0079 REM "A=highest array index, A(ALL) array values from record.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,SP_NAME$,F$,FILE_OP_CDE$,KEY_VAL$,S1$,A,A{ALL}
0100 SETERR 9000
0110 X0$="RT2WOR",X1$="Write Remote T/M Output Record"
0120 DIM S$(40)
0145 C$=$7E$
0300 REM "IOLISTS
0310 IOLIST A$
0500 REM "FILES
0510 DIM Z[NUM(X3$(60,3))]
0520 FILE_NAME$="RT1",INDEX=1; GOSUB 9750
0521 DATA_SIZE%=NUM(FIN(Z[1],"RSZ"))-84
0530 FILE_NAME$="RT2",INDEX=2; GOSUB 9750
0540 FILE_NAME$="ZZPARM"; INDEX=13; GOSUB 9750
0585 W9$=""; CALL "RT2PRM",ERR=*NEXT,X3$,X4$,W9$
0600 REM "change operator to salesperson, if no salesperson found, then don"+          "'t write out if this is the host. If remote, then we are sending "+          "mail back to someone
0605 IF LEN(SP_NAME$)<1 THEN GOTO 0650 ELSE IF SP_NAME$(1,1)<>"!" THEN GOTO 0650
0610 IF W9$(7,1)<>"H" THEN SP_NAME$=W9$(8,4); GOTO 0650; REM "if remote, then write out with the salesperson's number, but s"+             "end allrecords, they will be going to people not setup in the "+             "remote t/m file.
0615 READ (Z[2],KEY=SP_NAME$(2),DOM=*NEXT)
0620 K2$=KEY(Z[2],END=0645)
0625 IF K2$(1,3)=SP_NAME$(2) THEN SP_NAME$=K2$(4); GOTO 0650
0645 GOTO 9900
1000 REM "Setup A$ to write out
1010 DIM A$(83)
1020 A$(1,4)=SP_NAME$; REM "Salesperson
1030 CALL "RT2NSQ",X3$,X4$,SP_NAME$,N$
1040 A$(5,5)=N$; REM "Sequence Number
1050 A$(10,1)=FILE_OP_CDE$,A$(11,6)=X3$(21,6); REM "change type, change date
1060 A$(17,4)=STR(INT(TIM*100):"0000"); REM "change time
1070 A$(21,8)=F$; REM "Filename
1080 A$(29,55)=KEY_VAL$+C$; REM "kEY VALUE
1090 GOSUB 7500; REM "Pack A(all) into A0$
1100 DATA_PART$=S1$+C$+A0$
1110 REM "If DATA_PART$ <= DATA_SIZE% bytes then write out
1111 REM one record else write continuation
1120 FIRST_PART$=A$; COMMAND$=A$(10,1)
1130 IF LEN(DATA_PART$)<=DATA_SIZE% THEN {
1140 A$=FIRST_PART$+DATA_PART$,A$(10,1)=COMMAND$
1150 WRITE (Z[1],KEY=A$(1,9))IOL=0310
1160 GOTO 1250
1170  } ELSE {
1180 A$=FIRST_PART$+DATA_PART$(1,DATA_SIZE%)
1190 DATA_PART$=DATA_PART$(DATA_SIZE%+1),A$(10,1)="*"
1200 WRITE (Z[1],KEY=A$(1,9))IOL=0310
1210 CALL "RT2NSQ",X3$,X4$,SP_NAME$,N$
1220 FIRST_PART$(5,5)=N$
1230 GOTO 1130
1240  }
1250 REM "Finished writing out
1995 GOTO 9900
7500 REM "Pack the A(all) array that is A units long into A0$
7505 A0$=""; IF A=0 THEN GOTO 7590
7510 FOR I=0 TO A
7515 A0$=A0$+STR(A[I])+C$
7520 NEXT I
7595 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9120 ON Y8 GOTO 9900,0990,9920
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1210,2040
9740 RETURN 
9750 REM "FILES - Use instead of ZZFLES
9755 Z[INDEX]=UNT
9760 IF LEN(FILE_NAME$)=3 THEN {
9762 OPEN (Z[INDEX],ERR=*NEXT)FILE_NAME$+X3$(9,3)
9770  } ELSE {
9775 OPEN (Z[INDEX],ERR=*NEXT)FILE_NAME$
9780  }
9790 RETURN 
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CLOSE (Z[1]); CLOSE (Z[2]); CLOSE (Z[13])
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
