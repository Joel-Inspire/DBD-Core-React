0010 REM "Order Entry Gateway Journal Printing <OF2REA>
0035 REM "5.7 - 02/15/13 - 16.748611 - crg - SSP# 261369
0037 REM "261369-Program exception using GOE                                 
0040 REM "Copyright 2013 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="OF2REA",X1$="Order Entry Gateway Journal"
0120 DIM OF1$(175),OF1[5],OF2$(198),OF2[7],OF3$(424),OF3[5],OF4$(310),OF4[4],OF5$(95),OF6$(508); REM " SSP# 139226
0140 Z0$="-####,##0",M1$="####0",M2$="######0",M3$="##0.0",M4$="#######0-",Z1$="#,###,##0-",Z2$="#,###,###.00-",Z3$="##,###.00#-"
0160 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0175 DEF FNS$(Z9$)=Z9$(1,POS("  "=Z9$+"  ")-1)
0180 DEF FNY$(Z9$)=Z9$(1,6)+Z9$(9)
0210 T=1,T0=1,T1=1
0220 W3=127
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "I/O lists
0310 IOLIST OF1$(1),OF1[0],OF1[1],OF1[2],OF1[3],OF1[4],OF1[5]
0320 IOLIST OF2$,OF2[0],OF2[1],OF2[2],OF2[3],OF2[4],OF2[5],OF2[6],OF2[7]
0330 IOLIST OF3$,OF3[0],OF3[1],OF3[2],OF3[3],OF3[4],OF3[5]
0340 IOLIST OF4$,OF4[0],OF4[1],OF4[2],OF4[3],OF4[4]
0350 IOLIST OF5$
0360 IOLIST OF6$
0370 IOLIST AR1$,AR1[0],AR1[1],AR1[2],AR1[3],AR1[4],AR1[5],AR1[6],AR1[7],AR1[8],AR1[9],AR1[10],AR1[11],AR1[12],AR1[13],AR1[14],AR1[15]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O OF1...  02O OF2...  03O OF3...  04O OF4...  05O OF5...  06O OF"+"6...  07O AR1...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0700 REM "Open Printer
0720 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0721,9920
0750 REM "Get print pos for customer code
0760 CALL "ZZDISP","AX","XXXXXXXXXX","A/R",X3$,Q$,"",0,0,X4$
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[2]; U=8,U0=1,U[0]=0,U[1]=8,T0=0,U0$="    ",T5$="                "; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0900 REM "Position read"
0905 REM "Set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
0995 IF REMOVE_FLAG=0 THEN GOSUB 6000
1000 REM "Read next record
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])>=V2$(U3,U[U9]) THEN GOTO 1030 ELSE EXTRACT (U1,KEY=U$(1,U2-1)+V2$(U3,U[U9]),DOM=1021)
1025 EXITTO 1000
1030 IF U$(U2,U[U9])<=V2$(U3+U[U9],U[U9]) THEN GOTO 1040 ELSE READ (U1,KEY=U$(1,U2-1)+$FF$,DOM=1032)
1035 EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1180 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1190 REM "**** check if okay to print
1200 REM "If update then process records
1205 IF REMOVE_FLAG<>1 THEN GOTO 1300
1208 IF %GUI THEN PRINT 'MESSAGE'(FN%ZZDISP$(OF1$(9,10),"A/R")+" : "+OF1$(1,8),0),; GOTO 1211
1210 CALL "ZZDISP","A",OF1$(9,10),"A/R",X3$,"","",25,20,X4$; PRINT @(32,20),OF1$(1,8),
1250 IF OF1$(116,1)<>"Y" AND OF1$(97,8)<>"        " THEN GOSUB 8000; REMOVE (U1,KEY=U$,DOM=1251)
1295 GOTO 1000
1300 REM "Read other data
1305 DIM A$(300)
1320 DIM OF3$(599),OF3[5]; READ (Z[3],KEY=OF1$(1,8)+"     ",DOM=1321)IOL=0330
1340 DIM OF5$(95); READ (Z[5],KEY=OF1$(1,8),DOM=1341)IOL=0350
1350 DIM OF6$(508); READ (Z[6],KEY=OF1$(1,8),DOM=1351)IOL=0360
1400 REM "Read Customer Info
1405 IF W+5>W0 THEN GOSUB 6000
1410 DIM AR1$(100),AR1[15]; FIND (Z[7],KEY=OF1$(9,10),DOM=1411)IOL=0370
1411 GOSUB 7300
1415 CALL "ZZDISP","AX",OF1$(9,10),"A/R",X3$,X$,"",0,0,X4$
1420 Y5$(1)="Customer: "+X$,Y5$(23)=AR1$(11,35)
1425 GOSUB 7300
1500 REM "Print Header Info
1505 NOTE$=" **"
1510 IF W+7>W0 THEN GOSUB 6000
1520 Y5$(1)=OF1$(1,8),Y5$(11)=FND$(OF1$(20,6)),Y5$(23)=FND$(OF1$(41,6)),Y5$(48)="Cust PO: "+OF1$(26,15)
1555 IF OF1$(116,1)="Y" THEN VALUE$="** ON HOLD"+NOTE$; GOSUB PRINT_ON_RIGHT; TOT_HOLD=TOT_HOLD+1 ELSE CALL "ZZDISP","AX",OF1$(97,8),"O/P",X3$,X$,"",0,0,X4$; VALUE$="On Order: "+X$; GOSUB PRINT_ON_RIGHT
1560 GOSUB 7300
1570 IF OF1$(117,1)="Y" THEN VALUE$="***** CONTAINS A LOTTED ITEM THAT WAS BACKORDERED "+NOTE$; GOSUB PRINT_ON_RIGHT; GOSUB 7300
1573 IF OF1$(113,3)<>"0" AND OF1$(113,3)<>"   " THEN TOT_ERR=TOT_ERR+1; CALL "OF2SDA",X3$,X4$,OF1$(113,1),D1$; VALUE$="****** "+D1$+DIM(3)+"Header Status: "+OF1$(113,1)+NOTE$; GOSUB PRINT_ON_RIGHT; GOSUB 7300
1579 GOSUB 7500
1580 GOSUB 2000
1590 GOSUB 7300
1900 REM "Accumulate Totals"
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
2000 REM "Print Detail
2005 F0=1
2006 DIM OF2$(198),OF2[7]
2010 READ (Z[2],KEY=OF1$(1,8),DOM=2011)
2015 READ (Z[2],END=2090)IOL=0320
2020 IF OF2$(1,8)<>OF1$(1,8) THEN GOTO 2090
2025 IF W+2>W0 THEN GOSUB 6000; GOSUB 2500; F0=0
2027 IF F0=1 THEN GOSUB 2500; F0=0
2028 DIM OF4$(310),OF4[4]; READ (Z[4],KEY=OF1$(1,8)+OF2$(14,1),DOM=2029)IOL=0340
2030 Y5$(3)=OF2$(9,3),Y5$(8)=OF2$(12,1),Y5$(11)=OF2$(25,10),Y5$(23)=OF2$(45,40)
2035 X$=FNS$(OF2$(117,4))
2040 IF X$<>"M" AND OF2[0]>1 THEN X$=X$+"/"+STP(STR(OF2[0]:Z0$),0)
2045 Y5$(64)=X$
2050 Y5$(75)=OF2$(139,4),Y5$(80)=STR(OF2[2]:Z3$),Y5$(90)=STR(OF2[1]:Z3$)
2065 IF FPT(OF2[3])=0 THEN Y5$(101)=STR(OF2[3]:Z1$) ELSE Y5$(101)=STR(OF2[3]:Z2$)
2068 IF V0$(73,1)="D" THEN Y5$(113)=STP(OF2$(121,12),1)
2070 GOSUB 7300
2075 IF OF2$(144,1)<>" " THEN CALL "OF2SDA",X3$,X4$,OF2$(144,1),D1$; VALUE$="****** "+D1$+DIM(3)+"Line Status: "+OF2$(144,1)+NOTE$; GOSUB PRINT_ON_RIGHT; GOSUB 7300; TOT_ERR=TOT_ERR+1 ! 261369
2080 GOTO 2015
2095 RETURN 
2500 REM "Lines Heading
2505 GOSUB 7300
2510 Y5$(3)="Line",Y5$(8)="Tp",Y5$(11)="Item",Y5$(23)="Description",Y5$(64)="U / M",Y5$(75)="Whse",Y5$(85)="Cost",Y5$(94)="Price",Y5$(102)="Quantity"; IF V0$(73,1)="D" THEN Y5$(112)="Vend Inv"
2520 GOSUB 7300
2545 RETURN 
3000 REM "End of Receiving Document
3010 IF L9$="" THEN GOTO 3080
3070 GOSUB 2900
3080 IF E0$="END" THEN GOTO 3090
3085 L9$=U$(1,9)
3090 RETURN 
3200 REM "End of Page - continued
3210 F5$="Con't",F5=18; GOSUB 8550
3220 GOSUB 2900
3230 L0=1
3235 GOSUB 1500
3240 RETURN 
5000 REM "End of Print
5020 E0$="END"; GOSUB 3000
5025 IF REMOVE_FLAG=0 THEN GOSUB 7200
5045 IF REMOVE_FLAG=0 THEN CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
5205 IF REMOVE_FLAG=0 AND V0$(73,1)="D" THEN GOSUB 5900
5210 IF REMOVE_FLAG=0 THEN GOTO 5300
5290 GOTO 9900
5300 REM "Update orders and then remove them from the req file
5307 IF V3$(2,1)="T" THEN GOTO 9900
5308 IF %GUI THEN MSGBOX "Should these orders be cleared from the files if on order? ",MSG("CONFIRMING"),"?,YESNO",OPT$; IF OPT$="YES" THEN GOTO 5321 ELSE GOTO 5390 ! SSP#237817
5310 CALL "ZZPROM",".Y",X3$,Z,"Should these orders be cleared from the files if on order?","","",0
5320 ON Z GOTO 5321,5390
5330 Y$="07C AR1... 12C ZZP     "
5335 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
5350 REM "Do remove
5354 PRINT @(0,19),'CE',
5355 IF %GUI THEN PRINT 'MESSAGE'("Now clearing DDMS Information",0), ELSE PRINT @(20,19),"Now clearing DDMS Information",
5360 REMOVE_FLAG=1; GOTO 0934
5390 GOTO 9900
5900 REM "If DDMS then reprint the GPC_LOG
5904 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 5905,9920
5910 W=999,W8=0; T0$="",X1$="Product Catalog Log"
5912 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W4$,W5$; IF T0$="END" THEN GOTO 5990 ELSE ON W5 GOTO 5914,5990
5914 F=UNT; OPEN (F,ERR=5995,OPT="TEXT")"GPC_LOG"
5915 READ (F,END=5990)LOG$
5920 IF W>W0 THEN CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W4$,W5$; IF T0$="END" THEN GOTO 5990 ELSE ON W5 GOTO 5921,5990
5925 Y5$(1)=LOG$; GOSUB 7300
5980 GOTO 5915
5990 CLOSE (F)
5992 IF V3$(2,1)<>"T" THEN INVOKE "rm -f GPC_LOG"
5995 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
5999 RETURN 
6000 REM "Page header
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W4$,W5$; IF T0$="END" THEN GOTO 6060 ELSE ON W5 GOTO 6041,5205
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6090 RETURN 
6100 REM "Header record headings
6110 Y5$(1)="GOE #",Y5$(11)="Order Date",Y5$(23)="Needed by"
6115 VALUE$="Important info"; GOSUB PRINT_ON_RIGHT
6120 GOSUB 7300
6199 RETURN 
7000 REM "Totals logic
7001 REM IF T=T0 THEN IF T(T0,0)<2 THEN GOSUB 07300;GOTO 07100
7002 REM IF W+3>W0 THEN GOSUB 06000
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "END
7203 IF W+6>W0 THEN GOSUB 6000
7205 GOSUB 7300; Y5$(1)="Number of items printed: "+STR(T[0,0]); GOSUB 7300
7206 Y5$(1)="Number of GOEs on hold: "+STR(TOT_HOLD); GOSUB 7300
7207 Y5$(1)="Number of errors: "+STR(TOT_ERR); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 IF INT(T2)>0 THEN T4$=STR(INT(T2))+" Hours " ELSE T4$=""
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+STR(T3:"##.##")+" Minutes"
7260 Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 W=W+1
7330 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "Print address /shipping info
7505 ADD_INDEX=0,ADD_POS=11,Y5_MAX=6,COL1_INDEX=0,COL1=57,COL2_INDEX=0,COL2=111; DIM Y5$[Y5_MAX]
7506 FOR YY=1 TO Y5_MAX; DIM Y5$[YY](LEN(Y5$)); NEXT YY
7508 IF OF3$(48,30)<>DIM(30) THEN ADD_INDEX=ADD_INDEX+1,Y5$[ADD_INDEX](ADD_POS-6)="Attn:",Y5$[ADD_INDEX](ADD_POS)=OF3$(48,30)
7510 IF OF3$(234,35)<>DIM(35) THEN ADD_INDEX=ADD_INDEX+1; Y5$[ADD_INDEX](ADD_POS)=OF3$(234,30)
7512 IF OF3$(78,30)<>DIM(30) THEN ADD_INDEX=ADD_INDEX+1; Y5$[ADD_INDEX](ADD_POS)=OF3$(78,30)
7514 IF OF3$(108,30)<>DIM(30) THEN ADD_INDEX=ADD_INDEX+1; Y5$[ADD_INDEX](ADD_POS)=OF3$(108,30)
7516 CSZ$=""; IF OF3$(138,16)<>DIM(16) THEN CSZ$=STP(OF3$(138,16),1)
7518 IF OF3$(154,2)<>DIM(2) THEN CSZ$=CSZ$+", "+OF3$(154,2)
7520 IF OF3$(156,9)<>DIM(9) THEN CALL "ZZDISP","AX",OF3$(156,9),"ZIP",X3$,X$,"",0,0,X4$; CSZ$=CSZ$+" "+X$
7522 IF CSZ$<>"" THEN ADD_INDEX=ADD_INDEX+1; Y5$[ADD_INDEX](ADD_POS)=CSZ$
7529 IF (OF3$(48,30)=DIM(30) AND ADD_INDEX>0) THEN Y5$[1](ADD_POS-6,5)="Addr:" ELSE IF (OF3$(48,30)<>DIM(30) AND ADD_INDEX>1) THEN Y5$[2](ADD_POS-6,5)="Addr:"
7530 REM "Load column 1 fields
7532 TITLE$="Mark for:",VALUE$=OF3$(199,35); GOSUB 7600
7534 TITLE$="Inside del:",VALUE$=OF3$(169,15); GOSUB 7600
7536 TITLE$="Ship with:",VALUE$=OF3$(184,15); GOSUB 7600
7542 TITLE$="Email:",VALUE$=OF3$(278,40); GOSUB 7600
7544 TITLE$="Ordered by:",VALUE$=OF1$(47,20); GOSUB 7600
7550 REM "Load column 2 fields
7555 IF OF1$(131,4)<>DIM(4) THEN TITLE$="Credit card: ",VALUE$=OF1$(131,4)+"- "+STR(OF1[1]:"$###,###.00-"); GOSUB 7620; REM "SSP 145202
7580 FOR I5=1 TO Y5_MAX
7585 IF STP(Y5$[I5],1)<>"" THEN Y5$=Y5$[I5]; GOSUB 7300
7589 NEXT I5
7599 RETURN 
7600 REM "ADD col1 to y5$[], title$ and value$
7605 IF VALUE$<>DIM(LEN(VALUE$)) THEN COL1_INDEX=COL1_INDEX+1,Y5$[COL1_INDEX](COL1-1-LEN(TITLE$))=TITLE$,Y5$[COL1_INDEX](COL1)=VALUE$
7619 RETURN 
7620 REM "ADD col2 to y5$[], title$ and value$
7625 IF VALUE$<>DIM(LEN(VALUE$)) THEN COL2_INDEX=COL2_INDEX+1,Y5$[COL2_INDEX](COL2-1-LEN(TITLE$))=TITLE$,Y5$[COL2_INDEX](COL2)=VALUE$
7649 RETURN 
7650 PRINT_ON_RIGHT:REM "print value$ on far right of line
7655 Y5$(128-LEN(VALUE$))=VALUE$
7659 RETURN 
8000 REM "Remove corresponding files
8020 FOR II=2 TO 6
8022 READ (Z[II],KEY=U$,DOM=8023)
8024 KEY$=KEY(Z[II],END=8026); IF KEY$(1,LEN(U$))<>U$ THEN GOTO 8026 ELSE REMOVE (Z[II],KEY=KEY$,DOM=8024); GOTO 8024
8028 NEXT II
8045 RETURN 
9000 REM "Error Processing
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM "Transfer Control
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End program
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 IF %GUI THEN CALL Y4$,ERR=*NEXT ! SSP 198574
9926 IF %GUI THEN EXIT ! SSP 198574
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 RUN "ZMENU"
9999 END 
56000 ! "198574-test OE gateway                                             
56001 REM "261369-Program exception using GOE                                 
