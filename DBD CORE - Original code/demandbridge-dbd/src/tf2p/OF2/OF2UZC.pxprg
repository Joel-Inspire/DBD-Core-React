0010 REM "<OF2UZC> Validate Item Record for Order Entry Gateway
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 09/23/22 - 16.047896 - jvv - SSP# 000001
0037 REM "000001-Internal call for timekeeping                               
0040 REM "Copyright 2022 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,CUST_CODE$,OF2$,OF2{ALL},ERR_CODE$,HOLD$
0098 REM "Called from OF2UAA
0099 REM "Called from OF2UBA
0100 SETERR 9000
0105 IF LEN(HOLD$)>=6 THEN IF HOLD$(1,6)="[DDMS]" THEN DDMS_MODE=1; IF LEN(HOLD$)=6 THEN HOLD$="" ELSE HOLD$=HOLD$(7); REM "Trigger t o say we are in DDMS mode                                              
0110 X0$="OF2UZC",X1$="Validate Item Record for GOE"
0120 DIM Z0$(80,"-"),FILL_SP$(50)
0135 C9=-1
0200 REM "
0240 REM "SHOULDN'T NEED THIS->CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 09920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "Iolist
0350 IOLIST FM1$,FM1[0],FM1[1],FM1[2],FM1[3],FM1[4],FM1[5],FM1[6],FM1[7],FM1[8],FM1[9],FM1[10],FM1[11],FM1[12],FM1[13],FM1[14],FM1[15],FM1[16],FM1[17],FM1[18],FM1[19],FM1[20],FM1[21],FM1[22],FM1[23],FM1[24],FM1[25],FM1[26],FM1[27],FM1[28],FM1[29],FM1[30]
0360 IOLIST IC0$,IC0[0],IC0[1],IC0[2],IC0[3],IC0[4],IC0[5],IC0[6],IC0[7],IC0[8],IC0[9],IC0[10],IC0[11],IC0[12],IC0[13],IC0[14],IC0[15],IC0[16],IC0[17],IC0[18],IC0[19],IC0[20],IC0[21],IC0[22],IC0[23],IC0[24],IC0[25],IC0[26],IC0[27],IC0[28],IC0[29],IC0[30],IC0[31],IC0[32]
0370 IOLIST AR1$,AR1[0],AR1[1],AR1[2],AR1[3],AR1[4],AR1[5],AR1[6],AR1[7],AR1[8],AR1[9],AR1[10],AR1[11],AR1[12],AR1[13],AR1[14],AR1[15]
0380 IOLIST OF7$
0390 IOLIST AP4$,AP4[0],AP4[1],AP4[2]
0400 IOLIST FM0$
0430 IOLIST ZZPARM_UM$,ZZPARM_UM[0],ZZPARM_UM[1]; REM "ZZPARM-U/M record
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="13O ZZPARM  15O FM1...  16O IC0...  17O AR1...  18O FO5...  19O AP4...  20O FM0...  21O OF7... 22O OF4... "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0550 READ (Z[13],KEY=X3$(9,3)+"F/M")PARMFM$
1000 REM "Validate data
1010 CUST_IN$=CUST_CODE$; GOSUB 7200; CUST_CODE$=CUST_OUT$
1015 GOSUB 7900
1020 GOSUB 1500
1030 GOSUB 1600
1040 GOSUB 1700
1045 GOSUB 1750
1050 GOSUB 1800
1060 IF OF2$(12,1)<>"C" AND OF2$(12,1)<>"I" THEN PO_CODE$=OF2$(14,1),OF2$(14,1)=""; REMOVE (Z[22],KEY=OF2$(1,8)+PO_CODE$,DOM=1061); REM "If not a C type make sure no p/o is created
1090 REM "Set error flag
1095 OF2$(144,1)=ERR_CODE$
1099 GOTO 9900
1500 REM "Item Code
1510 DIM IC0$(188),IC0[33]
1515 DIM FM1$(350),FM1[31]; FM1_FOUND=0; FIND (Z[15],KEY=OF2$(15,20),DOM=1516)IOL=0350; FM1_FOUND=1; GOTO 1520; REM "Look for form master if available to find things like stock item, etc.
1516 FIND (Z[15],KEY=CUST_CODE$+OF2$(25,10),DOM=1517)IOL=0350; FM1_FOUND=1; GOTO 1520
1520 IF FM1_FOUND THEN IC0$(21,40)=FM1$(42,40),IC0$(120,4)=FM1$(100,4),IC0[14]=FM1[4]; REM "Default in case IC0 not found
1521 IF STP(OF2$(15,10),3," ")="" THEN GOSUB 8200 ELSE GOSUB 8300
1530 IF OF2$(12,1)="I" THEN IF OF7$(75,1)<>"Y" AND DDMS_MODE=0 AND %C$<>"100" THEN ERR_CODE$="8",HOLD$="Y"; REM "Parms=No stock requisitioning, ignore if in DDMS_MODE; 266809 - Or if it is DB
1540 IF OF2$(12,1)="I" OR OF2$(12,1)=" " THEN GOTO 1549 ELSE IF OF2$(12,1)="C" OR OF2$(12,1)="X" THEN IF FM1$(311,6)<>"      " THEN GOSUB 8400; GOTO 1549 ELSE GOTO 1549; REM "Check item security
1545 ERR_CODE$="A",HOLD$="Y"; REM "No item master to check for item security
1549 RETURN 
1600 REM "Unit of Measure
1605 DIM ZZPARM_UM$(22),ZZPARM_UM[2]; READ (Z[13],KEY="U/M"+OF2$(117,4),DOM=1606)IOL=0430; REM "Read for U/M info
1610 IF ZZPARM_UM$(20,1)="Y" THEN OF2[0]=ZZPARM_UM[0]; REM "If fixed U/M, use qty per U/M from ZZPARM
1615 IF STP(OF2$(117,4),3," ")="" THEN OF2$(117,4)=IC0$(120,4),OF2[0]=IC0[14]; REM "If U/M blank, get Sell U/M and Sell Qty/Unit from IC0
1649 RETURN 
1700 REM "Validate Vendor code
1705 DIM AP4$(420),AP4[4]; AP4$(1,10)=OF2$(85,10)
1710 READ (Z[19],KEY=OF2$(85,10),DOM=1711)IOL=0390
1720 OF2$(85,10)=AP4$(1,10)
1749 RETURN 
1750 REM "Validate Vendor/plant code
1755 DIM FM0$(522)
1760 READ (Z[20],KEY="P"+OF2$(85,14),DOM=1761)IOL=0400
1770 OF2$(95,4)=FM0$(12,4)
1799 RETURN 
1800 REM "Validate Ship from Warehouse code
1810 DIM FM0$(522)
1820 READ (Z[20],KEY="D"+FILL_SP$(1,10)+OF2$(139,4),DOM=1821)IOL=0400
1830 OF2$(139,4)=FM0$(12,4)
1849 RETURN 
7200 REM "Validate customer number
7205 DIM AR1$(200),AR1[15]
7210 CUST_OUT$=PAD(STP(CUST_IN$,1),10,"0")
7220 READ (Z[17],KEY=CUST_OUT$,DOM=7221)IOL=0370; GOTO 7250
7230 CUST_OUT$=CUST_IN$
7240 READ (Z[17],KEY=CUST_OUT$,DOM=7241)IOL=0370
7250 IF STP(AR1$,1)="" THEN ERR_CODE$="1"
7299 RETURN 
7900 REM "Read customer parameters
7905 DIM OF7$(76)
7910 READ (Z[21],KEY=CUST_CODE$,DOM=7911)IOL=0380
7920 IF STP(OF7$(11,10),3," ")="" THEN ALT_INV=0 ELSE ALT_INV=1; REM "Alt. inv. cust code?
7930 IF OF7$(74,1)="Y" THEN HOLD$="Y"; REM "Review reqs?
7940 LIMIT=0; LIMIT=NUM(OF7$(71,3),ERR=7941); REM "Set % of mthly avg as limit
7945 RETURN 
8200 REM "Find item code when company code blank
8201 REM "1st read IC0 with alternate inventory cust code
8202 REM "2nd read IC0 with cust code
8203 REM "3rd read IC0 with 10 blanks
8204 REM "If found set item description & line type
8205 REM "If not found error/hold
8206 IF DDMS_MODE<>1 THEN OF2$(14,1)=""; REM "If here we will not have  a P/O code                                                             
8210 IF ALT_INV=1 THEN FIND (Z[16],KEY=OF7$(11,10)+OF2$(25,10),DOM=8211)IOL=0360; OF2$(15,10)=OF7$(11,10),OF2$(12,1)="X",OF2$(45,40)=IC0$(21,40); GOTO 8299; REM "Found using alt. inv. cust code
8220 FIND (Z[16],KEY=CUST_CODE$+OF2$(25,10),DOM=8221)IOL=0360; OF2$(15,10)=CUST_CODE$,OF2$(12,1)="C",OF2$(45,40)=IC0$(21,40); GOTO 8299; REM "Found using cust code
8230 FIND (Z[16],KEY=OF2$(15,10)+OF2$(25,10),DOM=8231)IOL=0360; OF2$(12,1)="I",OF2$(45,40)=IC0$(21,40); GOTO 8299; REM "Found using 10 blanks
8240 IF FM1_FOUND THEN OF2$(45,40)=FM1$(42,40); GOTO 8299 ELSE ERR_CODE$="4",HOLD$="Y"; REM "Not found in IC0
8299 RETURN 
8300 REM "Find item code when company code is not blank
8301 REM "1st read IC0 with 10 characters from item code imported
8302 IF UID="jvv" AND %JVV_PO THEN GOTO 8399 ! Ignore checking for stock item if PO coming from EQD conversion of PO's 
8303 REM "If found set item description & line type
8304 REM "If not found error/hold
8305 IF FM1$(105,1)="Y" THEN IC0KEY$=DIM(10)+OF2$(25,10); IF DDMS_MODE<>1 AND %JVV_PO<>1 THEN OF2$(14,1)="" END_IF ELSE IC0KEY$=OF2$(15,20)
8310 FIND (Z[16],KEY=IC0KEY$,DOM=8311)IOL=0360; OF2$(45,40)=IC0$(21,40); GOTO 8315
8311 IC0KEY$(1,10)=""; FIND (Z[16],KEY=IC0KEY$,DOM=8330)IOL=0360; OF2$(12,1)="I",OF2$(15,10)="",OF2$(139,4)=""; IF DDMS_MODE<>1 THEN OF2$(14,1)=" " END_IF ; OF2$(45,40)=IC0$(21,40); GOTO 8200; REM " If not found, then try as stock item and goto 8200's for stock item pr ocessing                                                               
8315 IF FM1$(105,1)="Y" THEN OF2$(15,10)="",OF2$(12,1)="I"; GOTO 8399
8318 IF OF2$(25,10)=DIM(10) AND OF2$(12,1)="I" THEN GOTO 8399; REM "normal stock item
8320 IF OF2$(15,10)=CUST_CODE$ THEN OF2$(12,1)="C"; GOTO 8399 ELSE IF OF2$(15,10)=OF7$(11,10) THEN OF2$(12,1)="X"; GOTO 8399 ELSE ERR_CODE$="6",HOLD$="Y"; GOTO 8399; REM "6=found item but not for alt. inv. cust or the reg cust
8330 IF FM1_FOUND THEN OF2$(45,40)=IC0$(21,40); GOTO 8399 ELSE ERR_CODE$="5",HOLD$="Y"; REM "Not found; error/hold
8399 RETURN 
8400 REM "Item security
8410 Q$=FM1$(311,6); FOR X=1 TO POS("  "=Q$,2) STEP 2
8420 IF POS(Q$(X,2)=OF7$(51,20),2)=0 THEN EXITTO 8430 ELSE NEXT X; RETURN 
8430 ERR_CODE$="9",HOLD$="Y"
8499 RETURN 
8900 REM "Define Functions
8910 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"      ")-1)
9000 REM "Error Processing
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM "Transfer control
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End program
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940
9935 EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
