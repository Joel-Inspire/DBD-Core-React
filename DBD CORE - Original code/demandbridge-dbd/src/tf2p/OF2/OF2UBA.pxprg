0010 REM "<OF2UBA> Import Order Entry Gateway Files
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 07/14/22 - 11.762276 - jvv - SSP# 307409
0037 REM "307409-Changes to GOE for conversion from EQD for Order History and
0040 REM "Copyright 2022 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "IF OPT$="DDMS" then special stuff
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,OPT$,Q1$
0100 SETERR 9000
0110 X0$="OF2UBA",X1$="Import PO Receiving Gateway File"
0120 DIM Z0$(80,"-"),FILL_SP$(100)
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST OF1$(1),OF1[0],OF1[1],OF1[2],OF1[3],OF1[4],OF1[5]
0320 IOLIST OF2$,OF2[0],OF2[1],OF2[2],OF2[3],OF2[4],OF2[5],OF2[6],OF2[7]
0321 IOLIST OF2$,OF2[0],STR(OF2[1]),STR(OF2[2]),OF2[3],OF2[4],OF2[5],OF2[6],OF2[7]
0330 IOLIST OF3$,OF3[0],OF3[1],OF3[2],OF3[3],OF3[4],OF3[5]
0340 IOLIST OF4$,OF4[0],OF4[1],OF4[2],OF4[3],OF4[4]
0350 IOLIST OF5$
0360 IOLIST OF6$
0370 IOLIST AR1$,AR1[0],AR1[1],AR1[2],AR1[3],AR1[4],AR1[5],AR1[6],AR1[7],AR1[8],AR1[9],AR1[10],AR1[11],AR1[12],AR1[13],AR1[14],AR1[15]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X OF1...  02O OF2...  03O OF3...  04O OF4...  05O OF5...  06"+"O"+" OF"+"6...  07O AR1...  08O FM0... 09O OFH... 11O IC0... 13O ZZPARM  14O FMP...  "
0515 Z$=Z$+"15O FM1...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0560 DIM PARMAR$(255); FIND (Z[13],KEY=X3$(9,3)+"A/R",DOM=0561)PARMAR$
0570 DIM PARMOE$(256); FIND (Z[13],KEY=X3$(9,3)+"O/F",DOM=0571)PARMOE$
0590 DIM PARMIC$(101); READ (Z[13],KEY=X3$(9,3)+"I/C")PARMIC$
0595 P8=NUM(PARMIC$(40,2)),P9=NUM(PARMIC$(42,1))
0600 REM "
0610 GOSUB 6000
0640 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0700 REM "Determine division number to use
0710 DIV_NUM$="00"
0720 IF X3$(330,1)="1" THEN DIV_NUM$=X3$(331,2); GOTO 0750
0730 IF PARMAR$(13,1)<>"Y" THEN DIV_NUM$="00"; GOTO 0750
0740 IF X3$(333,2)<>"  " THEN DIV_NUM$=X3$(333,2); GOTO 0750
0900 REM "Setup 2 file masks 1 as given, 1 as all lower, default to D0:OFG*
0905 DIM OFPARM$(256); FIND (Z[13],KEY=X3$(9,3)+"O/F",DOM=0906)OFPARM$
0907 IF OPT$="VOILACHECK" THEN END_FILE$="Y"; GOTO 8600
0910 IF STP(OFPARM$(13,20),3," ")="" THEN MASK$="D0:OFG*" ELSE MASK$=STP(OFPARM$(13,20),1)
0915 IF OPT$="DOCU" THEN XP=POS("OFG"=MASK$); IF XP<>0 THEN MASK$(XP,3)="DCG"
0920 TMP=POS(":"=MASK$); IF TMP=0 THEN LCMASK$=LCS(MASK$) ELSE LCMASK$=MASK$(1,TMP)+LCS(MASK$(TMP+1))
0930 FILE_DISK$="D0"; IF MASK$(1,1)="D" THEN TMP=POS(":"=MASK$); IF TMP=3 THEN FILE_DISK$=MASK$(1,2)
1000 REM "Get list of GOE files to process
1001 IMPORT_PATH$=STP(PARMOE$(37,30),1)
1010 CALL "ZZ2BLS",MASK$+"|"+IMPORT_PATH$,"S",F$
1012 CALL "ZZ2BLS",LCMASK$+"|"+IMPORT_PATH$,"S",F_LOWER$; F$=F$+F_LOWER$
1015 P=POS(":"=F$); IF P=0 THEN GOTO 5000
1020 F1$=F$(P+1); P1=POS(":"=F1$); IF P1>0 THEN F$=F1$(P1-2),F1$=F1$(1,P1-3) ELSE F$=""
1030 IF LEN(F1$)=6 THEN CLOSE (14); OPEN (14)"ZZPARM"; FIND (14,KEY="CMP"+F1$(4,3),DOM=1031); CLOSE (14); GOTO 1015; REM "Weed out OFG files that belong to a company
1040 IF IMPORT_PATH$<>"" THEN PROCESS_FILE$=F1$; F1$=IMPORT_PATH$+F1$
1050 Z[10]=UNT
1060 CLOSE (Z[10]); OPEN (Z[10],OPT="TEXT",ERR=5000)F1$; REM "Open for r"+"eading raw file
1062 VER$=""
1065 CLOSE (13); OPEN (13)"ZZPARM"; REM "Re-open ZZPARM closed in ZZ2BLS
1075 PRINT @(15,15),'CE',"Processing: "+F1$,
1080 GOSUB 8100; GOSUB 8150; FIRST_TIME=1
1090 END_FILE$=""
1100 REM "Process the file
1105 I9=0,I0$="",I1$="",I$="",RCOUNT$=""; DIM RCOUNT[50]
1110 REM "Read record in text file
1120 GOSUB 7000
1130 IF END_FILE$="Y" THEN GOTO 1900
1200 REM "Find header record = Start of new order
1220 IF I$(1,1)<>"H" THEN GOTO 1110
1230 GOSUB 2000
1300 REM "Find other record types
1310 GOSUB 7000
1320 IF END_FILE$="Y" THEN GOSUB 1500; GOTO 1900
1340 IF I$(1,1)="H" THEN GOSUB 1500; GOTO 1200
1350 ON POS(I$(1,1)="LSPCN") GOSUB 7900,2500,3000,3500,4000,4500
1390 GOTO 1300
1500 REM "Complete GOE number/Validate Header & Related
1510 READ (Z[1],KEY=OF1$(1,8))IOL=0310
1520 GOSUB 1600
1530 IF LINE_RECS=0 THEN ERR_CODE$="X",HOLD$="Y"
1535 DIM OF2$(500),OF2[7],OF3$(424),OF3[5],SHIP_REC$(325); REM " SSP# 139226
1540 READ (Z[3],KEY=OF1$(1,8)+FILL_SP$(1,5),DOM=1545)IOL=0330; GOTO 1550
1545 GOSUB 3030
1550 REM "Update order header if HOLD on by now
1555 EXTRACT (Z[1],KEY=OF1$(1,8),DOM=1560)IOL=0310
1556 IF HOLD$=DIM(1) THEN HOLD$="N"; OF1$(113,3)=DIM(3)
1557 OF1$(113,3)=ERR_CODE$,OF1$(116,1)=HOLD$
1558 WRITE (Z[1],KEY=OF1$(1,8))IOL=0310
1599 RETURN 
1600 REM "Validate Lines
1605 LINE_RECS=0
1610 READ (Z[2],KEY=OF1$(1,8),DOM=1611)
1615 DIM OF2$(500),OF2[7]; READ (Z[2],END=1699)IOL=0320
1620 IF OF2$(1,8)<>OF1$(1,8) THEN GOTO 1699
1625 LINE_RECS=LINE_RECS+1
1635 GOSUB 1700
1640 IF SHIP_RECS<>0 THEN OF2$(143,1)="Y"; IF SHIP_QTY<>OF2[3] THEN ERR_CODE$="X",HOLD$="Y" ! SSP 307409 
1650 IF OF2$(14,1)<>" " THEN GOSUB 1800
1659 IF OPT$<>"VOILACHECK" THEN GOTO 1662
1660 IF POS(OF2$(12,1)="IC")<>0 THEN IF OPT$="DDMS" THEN HOLD$="[DDMS]"+HOLD$ END_IF ; NEW_ERR_CODE$=""; CALL "OF2UZC",X3$,X4$,OF1$(9,10),OF2$,OF2{ALL},NEW_ERR_CODE$,HOLD$; IF NEW_ERR_CODE$<>"" AND NEW_ERR_CODE$<>" " THEN ERR_CODE$=NEW_ERR_CODE$; REM "ssp 144184 - send cleared error code into OF2UZC, set err_code only if set in OF2UZC
1670 WRITE (Z[2],KEY=OF2$(1,11))IOL=0321
1680 GOTO 1615
1699 RETURN 
1700 REM "Validate Shipping
1705 SHIP_QTY=0,SHIP_RECS=0
1710 READ (Z[3],KEY=OF1$(1,8)+OF2$(14,1)+OF2$(9,3),DOM=1711)
1715 DIM OF3$(500),OF3[5]; READ (Z[3],END=1799)IOL=0330
1720 IF OF3$(1,12)<>OF1$(1,8)+OF2$(14,1)+OF2$(9,3) THEN GOTO 1799
1725 SHIP_RECS=SHIP_RECS+1
1730 SHIP_QTY=SHIP_QTY+OF3[1]
1780 GOTO 1715
1799 RETURN 
1800 REM "Validate P/O Record if needed
1815 DIM OF4$(310),OF4[4]; READ (Z[4],KEY=OF1$(1,8)+OF2$(14,1),DOM=1816)IOL=0340
1820 IF OF4$(1,9)<>OF1$(1,8)+OF2$(14,1) THEN DIM PO_REC$(270); GOSUB 3530
1899 RETURN 
1900 REM "Next file
1901 REM "Close & Rename to OFH for archiving
1902 REM "Erase an existing OFH file if found
1905 CLOSE (Z[10])
1910 IF PROCESS_FILE$="" THEN GOTO 1915
1911 F1$=PROCESS_FILE$
1915 IF OPT$="DOCU" THEN F2$=F1$; IF F2$ LIKE "[Dd][Cc][Gg]*" THEN F2$(1,3)="DCH"; GOTO 1940 ELSE GOTO 1990
1920 F2$=F1$; IF F2$ LIKE "[Oo][FfEe][Gg]*" THEN F2$(1,3)="OFH" ELSE GOTO 1990
1940 REM "Get disk directory for data files
1944 IF IMPORT_PATH$<>"" THEN J$=IMPORT_PATH$ ELSE IF IMPORT_PATH$="" THEN J$=%DATAPATH$
1950 REM "Move the file
1955 ERASE F2$,ERR=*PROCEED; RENAME J$+DLM+F1$ TO J$+DLM+F2$
1999 GOTO 1015
2000 REM "Header Record - ** Required **
2005 SKIP_SHIP=0; IF OPT$="DDMS" THEN IF LEN(I$)>=114 THEN IF I$(1,24)+I$(31,84)+DDMS_ORDER$=CURR_HEADER$ THEN SKIP_SHIP=1; GOTO 2499 ELSE CURR_HEADER$=I$(1,24)+I$(31,84)+DDMS_ORDER$; REM "If cust+ vend inv# same as last time, don't add new header and shipping info, just the line - for DDMS interface, ssp#137783,REF#196247
2006 REM "SKIP_SHIP=0; IF LEN(I$)>115 THEN IF I$(14,10)+I$(115,10)=CURR_VEND_INV$ THEN SKIP_SHIP=1; GOTO 2499 ELSE CURR_VEND_INV$=I$(14,10)+I$(115,10); REM "If cust+ vend inv# same as last time, don't add new header and shipping info, just the line - for DDMS interface, ssp#137783
2007 DIM DOCU_RC_NUM$(20)
2010 DIM OF1$(198),OF1[5]
2012 DIM HDR_REC$(206); VEND_INV$=""
2020 HDR_REC$(1)=I$
2021 HDR_REC$(115,10)=CVS(HDR_REC$(115,10),4); REM uppercase inv#
2022 COMMENT_LINE=0; REM "Max=10
2024 DETAIL_LINE=1; REM "Max=200
2025 REM "IF PARMOE$(36,1)<>" " THEN PO_CODE$=PARMOE$(36,1),VI_LIST$="",VI_PO$=""; REM " ELSE PO_CODE$="1"; REM "Default to 1 ssp 145202
2026 PO_CODE$=PARMOE$(36,1),VI_LIST$="",VI_PO$=""; REM " ELSE PO_CODE$="1"; REM "Default to 1 ssp 145202
2027 IF OPT$="DOCU" THEN HDR_REC$(24,1)="N",HDR_REC$(96,1)="N",PO_CODE$=PARMOE$(70,1); IF PO_CODE$=" " THEN PO_CODE$="A"
2028 ERR_CODE$=""; REM "Clear error code
2030 HOLD$=""; REM "Clear hold flag
2040 DIM OF2$(500),OF2[7]
2042 DIM OF3$(500),OF3[5]
2044 DIM OF4$(310),OF4[4]
2046 DIM OF5$(95)
2048 DIM OF6$(508)
2050 GOSUB 7100; REM "Get next GOE number
2052 OF1$(1,8)=DIV_NUM$+GOENUM$; REM "GOE number
2100 REM "Header String
2108 CUST_IN$=STP(HDR_REC$(14,10),2); GOSUB 7200; OF1$(9,10)=CUST_OUT$; REM "Customer code - ** Required **
2110 IF OF1$(9,2)<>OF1$(1,2) THEN OF1$(1,2)=OF1$(9,2)
2112 IF HDR_REC$(24,1)<>"Y" THEN OF1$(19,1)="N" ELSE OF1$(19,1)=HDR_REC$(24,1); REM "Update P/O only
2114 IF STP(HDR_REC$(25,8),1)="" THEN OF1$(20,6)=X3$(21,6) ELSE Q$=HDR_REC$(25,8); GOSUB 7500; OF1$(20,6)=Q0$; REM "Order date
2116 OF1$(26,15)=STP(HDR_REC$(33,15),2); REM "Customer P/O
2118 IF STP(HDR_REC$(48,8),1)="" THEN OF1$(41,6)=OF1$(20,6) ELSE Q$=HDR_REC$(48,8); GOSUB 7500; OF1$(41,6)=Q0$; REM "Date requ"+"ested
2120 OF1$(47,20)=HDR_REC$(56,20); REM "Ordered by name
2122 OF1$(67,20)=HDR_REC$(76,20); REM "Ordered by greeting
2124 IF HDR_REC$(96,1)<>"Y" THEN OF1$(87,1)="N" ELSE OF1$(87,1)=HDR_REC$(96,1); REM "Repeat order
2126 OF1$(88,8)=HDR_REC$(97,8); REM "TopForm repeat order number
2128 IF STP(HDR_REC$(8,6),1)<>"" THEN OF1$(105,2)=OF1$(1,2); OF1$(107,6)=HDR_REC$(8,6); REM "Set "+"division SSP 307409
2131 IF OPT$="DDMS" THEN IF HDR_REC$(115,10)=DIM(10) THEN HDR_REC$(115,10)=DDMS_ORDER$; REM "If DDMS and vendor invoice# is blank then setto DDMS order number
2132 OF1$(117,10)=HDR_REC$(115,10),VEND_INV$=HDR_REC$(115,10); REM "Vendor Invoice Number (101) 137783
2133 OF1$(127,4)=MID(HDR_REC$,125,4) ! 272014
2134 OF1$(131,4)=HDR_REC$(129,4),OF1$(135,20)=HDR_REC$(133,20),OF1$(155,4)=HDR_REC$(153,4),OF1$(159,40)=HDR_REC$(157,40); REM "Credit card - type, number, expire date (MMYY), and name on card
2200 REM "Header Numerics
2210 OF1[0]=NUM(HDR_REC$(105,10),ERR=2211); REM "Freight in amount
2212 OF1[1]=NUM(HDR_REC$(197,10),ERR=2213); REM "Credit card amount charged
2400 REM "Write header record
2410 OF1$(113,3)=ERR_CODE$,OF1$(116,1)=HOLD$
2450 WRITE (Z[1],KEY=OF1$(1,8))IOL=0310
2499 RETURN 
2500 REM "Line Record - ** Required **
2510 DIM OF2$(500),OF2[7]
2512 DIM LINE_REC$(181)
2520 LINE_REC$(1)=I$
2525 LINE_REC$(21,20)=CVS(LINE_REC$(21,20),4)
2530 OF2$(1,8)=OF1$(1,8); REM "GOE number
2532 IF STP(LINE_REC$(5,3),1)="" THEN OF2$(9,3)=STR(DETAIL_LINE:"000"),DETAIL_LINE=DETAIL_LINE+1 ELSE OF2$(9,3)=LINE_REC$(5,3); REM "Line number
2534 SEQ_NUM=1
2550 IF OPT$="DOCU" THEN LINE_REC$(11,10)=OF1$(9,10)
2560 IF PARMOE$(131,1)="Y" THEN OF2$(198,20)=STP(DOCU_RC_NUM$(1,20),2)
2600 REM "Line String
2605 GOSUB 7800; REM "Do line mappings
2610 IF POS(LINE_REC$(8,1)="ICMS")=0 THEN OF2$(12,1)="C" ELSE OF2$(12,1)=LINE_REC$(8,1); REM "Line type
2612 IF POS(LINE_REC$(9,1)="NECSB")=0 THEN OF2$(13,1)="N" ELSE OF2$(13,1)=LINE_REC$(9,1); REM "Custom item line type
2613 REM "Blank P/O code means no p/o, 1-9,A-Z, use it, anything else = auto req
2614 IF OPT$="DDMS" OR OPT$="VOILA" OR OPT$="DOCU" THEN GOTO 2615 ELSE OF2$(14,1)=LINE_REC$(10,1); IF OF2$(12,1)="C" OR OF2$(12,1)="I" THEN IF LINE_REC$(10,1)=" " THEN OF2$(14,1)=PO_CODE$; GOSUB 7400; REM "P/O code  - ***** Should be Requir"+"ed  - Flagauto or code, if really an I we will remove later  (ssp 145202)-if DDMS then will do later!SSP#276185
2615 IF OPT$="DDMS" OR POS(OF2$(12,1)="ISM")>0 THEN OF2$(15,10)=""; GOTO 2617; REM " DDMS uses stock items. SSP177553, if it's an I,S,M type line, no customer code check.  
2616 IF OF2$(12,1)="C" AND STP(LINE_REC$(11,10),1)="" THEN OF2$(15,10)=OF1$(9,10) ELSE CUST_IN$=STP(LINE_REC$(11,10),2); GOSUB 7200; OF2$(15,10)=CUST_OUT$; REM "Customer number.  SSP177553, was checking OF2$(13,1)="C", changed to OF2$(12,1)="C"
2618 OF2$(25,20)=LINE_REC$(21,20); GOSUB 7300; REM "Item code - ** Req"+"uired ** (unless Line Type = M)
2620 OF2$(45,40)=LINE_REC$(41,40); REM "Description
2622 OF2$(85,10)=LINE_REC$(81,10); REM "Vendor code
2624 OF2$(95,4)=LINE_REC$(91,4); REM "Plant code
2626 OF2$(99,9)=LINE_REC$(95,9); REM "Starting number
2628 OF2$(108,9)=LINE_REC$(104,9); REM "Ending number
2630 IF STP(LINE_REC$(113,4),1)="" THEN OF2$(117,4)="" ELSE OF2$(117,4)=LINE_REC$(113,4); REM "Selling unit - if blank leave blank, will default later
2632 OF2$(121,12)=LINE_REC$(151,12); REM "Vendor job number
2633 IF OPT$="DDMS" OR OPT$="DOCU" THEN OF2$(121,12)=VEND_INV$; GOSUB 7450; REM "if DDMS loadvend job number w/vendor invoice from the header, Gosub logic to set PO_code
2634 IF LINE_REC$(178,4)=DIM(4) THEN OF2$(139,4)="0001" ELSE OF2$(139,4)=LINE_REC$(178,4); REM "Ship from whse code
2700 REM "Line Numerics
2710 IF STP(LINE_REC$(117,7),1)="" THEN OF2[0]=1 ELSE OF2[0]=NUM(LINE_REC$(117,7),ERR=2711); REM "Selling qty/unit
2711 IF OPT$="DDMS" OR OPT$="DOCU" THEN IF STP(OF2$(117,4),2)="DZ" THEN OF2[0]=12
2712 PRECISION 3; OF2[1]=NUM(LINE_REC$(124,10),ERR=2713); REM "Price per unit,for DDMS get precision in 3 digits
2713 PRECISION 2
2714 OF2[2]=NUM(LINE_REC$(134,10),ERR=2715); REM "Cost per unit
2716 OF2[3]=NUM(LINE_REC$(144,7),ERR=2717); REM "Quantity ordered - **"+" Required **
2718 OF2[4]=NUM(LINE_REC$(171,7),ERR=2719); REM "Carton pack
2900 REM "Write line record
2910 IF POS(OF2$(12,1)="IC")<>0 THEN IF OPT$="DDMS" THEN HOLD$="[DDMS]"+HOLD$ END_IF ; NEW_ERR_CODE$=""; CALL "OF2UZC",X3$,X4$,OF1$(9,10),OF2$,OF2{ALL},NEW_ERR_CODE$,HOLD$; IF NEW_ERR_CODE$<>"" AND NEW_ERR_CODE$<>" " THEN ERR_CODE$=NEW_ERR_CODE$; REM "ssp 144184 - send cleared error code into OF2UZC, set err_code only if set in OF2UZC
2945 IF LINE_REC$(41,40)<>FILL_SP$ THEN OF2$(45,40)=LINE_REC$(41,40)
2946 IF UID="jvv" THEN OF2$(225,1)="N"
2947 IF OPT$="DOCU" AND OF2[0]=0 THEN OF2[0]=1
2950 WRITE (Z[2],KEY=OF2$(1,11))IOL=0321
2999 RETURN 
3000 REM "Shipping Record - ** Required **
3010 DIM OF3$(500),OF3[5]
3012 DIM SHIP_REC$(324); REM " SSP# 139226
3015 IF I$(2,3)="102" THEN I9$=I$; DIM I$(326); I$(1)=I9$; REM " SSP# 139226 - ACCOUNT FOR SHORTER RECORDS COMING FROM EARLIER VERSIONS
3020 SHIP_REC$(1)=I$
3030 OF3$(1,8)=OF1$(1,8); REM "GOE number
3032 OF3$(9,1)=OF2$(14,1); REM "P/O code
3034 IF STP(SHIP_REC$(5,3),1)="G" THEN OF3$(9,4)="" ELSE IF STP(OF2$(9,3),1)<>"" THEN OF3$(10,3)=OF2$(9,3) ELSE IF STP(SHIP_REC$(5,3),1)="" OR STP(SHIP_REC$(5,3),1)="G" THEN OF3$(9,1)="",OF3$(10,3)="" ELSE REM "Line number or blank if general shipping "+"record
3036 IF STP(OF3$(10,3),1)="" THEN OF3$(13,1)="" ELSE IF SEQ_NUM>35 THEN GOTO 3499 ELSE SQNUM$=STR(SEQ_NUM); OF3$(13,1)=FNQ$(SQNUM$); SEQ_NUM=SEQ_NUM+1; REM "Sequence number SSP 307409
3100 REM "Shipping String
3105 IF SKIP_SHIP THEN SKIP_SHIP=0; GOTO 3499; REM "If SKIP_SHIP then don't do anything, but clear it, set in 2005, ssp#137783
3106 IF PARMOE$(131,1)="Y" THEN DOCU_RC_NUM$(1,20)=SHIP_REC$(22,20); REM "SSP# 190052
3110 IF POS(SHIP_REC$(8,1)="CDP")=0 THEN OF3$(14,1)="C" ELSE OF3$(14,1)=SHIP_REC$(8,1); REM "Location type
3111 IF SHIP_REC$(9,4)=DIM(4) THEN OF3$(15,4)="M"; GOTO 3117; REM "Ifblank code then manual ship
3112 OF3$(15,4)=SHIP_REC$(9,4); REM "Ship to code - ** 1st choice **
3114 OF3$(19,9)=SHIP_REC$(13,9); IF STP(OF3$(15,4),1)="" AND STP(OF3$(19,9),1)<>"" THEN CALL "FM2DPC",X3$,X4$,"",OF1$(9,10)+OF3$(19,9),ST_CODE$,"",RET_CODE; IF RET_CODE=0 THEN OF3$(15,4)=ST_CODE$; REM "Location code - ** 2nd choice **
3116 OF3$(28,20)=SHIP_REC$(22,20); IF STP(OF3$(15,4),1)="" AND STP(OF3$(28,20),1)<>"" THEN CALL "FM2DPB",X3$,X4$,OF1$(9,10),OF3$(28,20),ST_CODE$,""; OF3$(15,4)=ST_CODE$; REM "Rec dept - ** 3rd choice **
3117 IF (OPT$="DDMS" OR OPT$="DOCU") AND OF3$(15,4)=DIM(4) THEN OF3$(15,4)="M" ELSE IF STP(SHIP_REC$(72,88),3," ")="" THEN GOSUB 8200; GOTO 3130; REM "If blank get from shipping and skip setting, IF DDMS THEN we may get blank records and we don't want to try to set, just assume manual
3118 OF3$(48,30)=SHIP_REC$(42,30); REM "Attn line
3120 OF3$(78,30)=SHIP_REC$(72,30); REM "Ship addr1
3122 OF3$(108,30)=SHIP_REC$(102,30); REM "Ship addr2
3124 OF3$(138,16)=SHIP_REC$(132,16); REM "Ship city
3126 OF3$(154,2)=SHIP_REC$(148,2); REM "Ship state
3128 ZIP_IN$=SHIP_REC$(150,10); GOSUB 7600; OF3$(156,10)=ZIP_OUT$; REM "Ship zip code
3130 V_TYPE$="F",CODE_IN$=SHIP_REC$(160,1); GOSUB 7350; OF3$(166,1)=CODE_IN$; REM "Ship fob
3132 V_TYPE$="H",CODE_IN$=SHIP_REC$(161,1); GOSUB 7350; OF3$(167,1)=CODE_IN$; REM "How to ship
3134 V_TYPE$="S",CODE_IN$=SHIP_REC$(162,1); GOSUB 7350; OF3$(168,1)=CODE_IN$; REM "Ship via
3136 OF3$(169,15)=SHIP_REC$(163,15); REM "Inside delivery
3138 OF3$(184,15)=SHIP_REC$(178,15); REM "Ship with
3140 OF3$(199,35)=SHIP_REC$(193,35); REM "Mark for
3142 IF STP(SHIP_REC$(228,35),1)<>"" THEN OF3$(234,35)=SHIP_REC$(228,35); REM "Company name!SSP#237660
3144 OF3$(269,4)=SHIP_REC$(263,4); REM "Ship to warehouse
3146 IF STP(SHIP_REC$(267,4),1)="" THEN OF3$(273,4)=OF2$(117,4) ELSE OF3$(273,4)=SHIP_REC$(267,4); REM "Shipping unit
3150 OF3$(278,40)=SHIP_REC$(285,40); REM " EMAIL ADDRESS - SSP# 139226
3200 REM "Shipping Numerics
3210 IF STP(SHIP_REC$(271,7),1)="" THEN OF3[0]=OF2[0] ELSE OF3[0]=NUM(SHIP_REC$(271,7),ERR=3211); REM "Shipping qty/unit
3212 OF3[1]=NUM(SHIP_REC$(278,7),ERR=3213); REM "Quantity to ship - **"+" Required ** (unless G type)
3400 REM "Write Shipping Record
3410 CALL "OF2UZA",X3$,X4$,OF1$(9,10),OF3$,OF3{ALL},ERR_CODE$,HOLD$
3450 WRITE (Z[3],KEY=OF3$(1,13))IOL=0330
3499 RETURN 
3500 REM "Purchase Order Record
3510 DIM OF4$(310),OF4[4]
3512 DIM PO_REC$(270)
3520 PO_REC$(1)=I$
3530 OF4$(1,8)=OF1$(1,8); REM "GOE number
3532 IF PO_REC$(5,1)=" " THEN IF OF2$(14,1)=" " THEN OF4$(9,1)=PO_CODE$; GOSUB 7400 ELSE OF4$(9,1)=OF2$(14,1) END_IF ELSE OF4$(9,1)=PO_REC$(5,1); REM "P/O code
3600 REM "P/O String
3610 OF4$(10,35)=PO_REC$(6,35); REM "Plant name
3612 OF4$(45,30)=PO_REC$(41,30); REM "Plant attn
3614 OF4$(75,30)=PO_REC$(71,30); REM "Plant addr1
3616 OF4$(105,30)=PO_REC$(101,30); REM "Plant addr2
3618 OF4$(135,16)=PO_REC$(131,16); REM "Plant city
3620 OF4$(151,2)=PO_REC$(147,2); REM "Plant state
3622 ZIP_IN$=PO_REC$(149,10); GOSUB 7600; OF4$(153,10)=ZIP_OUT$; REM "Plant zip code
3624 IF PO_REC$(159,1)="Y" THEN OF4$(163,1)=PO_REC$(159,1) ELSE OF4$(163,1)="N"; REM "Print this p/o
3625 IF OPT$="DDMS" AND MID(PARMOE$,68,1)="Y" THEN OF4$(163,1)="Y"; REM " PRINT THE PO
3626 IF POS(PO_REC$(160,1)="NECSB")=0 THEN OF4$(164,1)="N" ELSE OF4$(164,1)=PO_REC$(160,1); REM "New/Repeat
3628 IF PO_REC$(161,1)="Y" THEN OF4$(165,1)=PO_REC$(161,1) ELSE OF4$(165,1)="N"; REM "Confirming
3630 Q$=PO_REC$(162,8); GOSUB 7500; OF4$(166,6)=Q0$; REM "Grouping"+" cutoff date
3632 OF4$(172,12)=PO_REC$(170,12); REM "Plant quote number
3634 Q$=PO_REC$(182,8); GOSUB 7500; OF4$(184,6)=Q0$; REM "Plant qu"+"ote date
3636 OF4$(190,12)=PO_REC$(190,12); REM "Previous job number
3638 Q$=PO_REC$(202,8); GOSUB 7500; OF4$(202,6)=Q0$; REM "Previous"+" job date
3640 OF4$(208,12)=PO_REC$(210,12); REM "This job number
3642 Q$=PO_REC$(222,8); GOSUB 7500; OF4$(220,6)=Q0$; REM "This job"+" date
3644 OF4$(226,12)=PO_REC$(230,12); REM "Job status
3646 Q$=PO_REC$(242,8); GOSUB 7500; OF4$(238,6)=Q0$; REM "Job stat"+"us date
3648 IF POS(PO_REC$(250,1)=" WRCH")<>0 THEN OF4$(244,1)=PO_REC$(250,1); REM "Proof status
3650 Q$=PO_REC$(251,8); GOSUB 7500; OF4$(245,6)=Q0$; REM "Proof st"+"atus date
3652 Q$=PO_REC$(259,8); GOSUB 7500; OF4$(251,6)=Q0$; REM "Schedule"+" shipping date
3654 OF4$(257,4)=PO_REC$(267,4); REM "Customer service rep code
3700 REM "P/O Numerics
3900 REM "Write P/O record
3950 WRITE (Z[4],KEY=OF4$(1,9))IOL=0340
3999 RETURN 
4000 REM "Purchase Order Comments Record
4010 DIM OF5$(95)
4012 DIM POCOM_REC$(73)
4020 POCOM_REC$(1)=I$
4030 OF5$(1,8)=OF1$(1,8); REM "GOE number
4032 IF POCOM_REC$(5,1)=" " THEN OF5$(9,1)=OF2$(14,1) ELSE OF5$(9,1)=POCOM_REC$(5,1); REM "P/O code
4034 OF5$(10,3)=POCOM_REC$(6,3); REM "Line number
4100 REM "P/O Comments String
4110 IF MID(POCOM_REC$,9,1)="Y" THEN OF5$(13,1)=POCOM_REC$(9,1) ELSE OF5$(13,1)="N"; REM "Print comment on P/O !SSP#255022
4112 OF5$(14,64)=POCOM_REC$(10,64); REM "P/O comment
4400 REM "Write P/O Comments Record
4450 WRITE (Z[5],KEY=OF5$(1,12))IOL=0350
4499 RETURN 
4500 REM "Order Notepad Record - Max of 10 lines
4510 IF COMMENT_LINE=10 THEN RETURN 
4512 DIM NOTE_REC$(57)
4520 NOTE_REC$(1)=I$
4530 OF6$(1,8)=OF1$(1,8); REM "GOE number
4600 REM "Order Notepad String
4610 OF6$(COMMENT_LINE*50+9,50)=NOTE_REC$(5,50); REM "Notepad line
4615 COMMENT_LINE=COMMENT_LINE+1
4900 REM "Write Order Notepad Record
4910 WRITE (Z[6],KEY=OF6$(1,8))IOL=0360
4999 RETURN 
5000 REM "End of GOE Processing
5010 PRINT @(0,15),'CE',
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "Background
6005 PRINT (0,ERR=6016)'SB',
6020 IF OPT$="DOCU" THEN PRINT @(10,5),"Importing Docucenter Information" ELSE IF OPT$="DDMS" THEN PRINT @(10,5),"Importing DDMS Information", ELSE PRINT @(10,5),"Importing Factory Order Information"
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7000 REM "Read Import Text File
7005 IF OPT$="VOILA" THEN GOTO VOILA_READ
7010 READ (Z[10],END=7050)I$
7013 IF STP(CVS(I$,16),2)="" THEN GOTO 7000
7015 I9=I9+LEN(I$),COUNT=COUNT+1,ORIG$=I$
7020 IF POS(I$(2,1)=",|")<>0 OR I$(2,1)=$09$ THEN DELIM$=I$(2,1); GOSUB 7700
7025 IF OPT$="DDMS" THEN IF POS(I$(1,1)="HLSPCN")=0 THEN GOSUB 8050; GOTO 7049 ELSE GOSUB 8000
7030 P=POS(I$(2,3)=RCOUNT$,3); IF P=0 THEN RCOUNT$=RCOUNT$+I$(2,3); GOTO 7030; REM "Used to write to /usr/lib/basic/GW_LOG
7040 P1=(P+2)/3; RCOUNT[P1]=RCOUNT[P1]+1
7045 IF MOD(COUNT,15)=1 THEN GOSUB 8150; REM " report every 15 lines
7049 RETURN 
7050 REM "End of Text File
7055 IF LEN(RCOUNT$)<=0 THEN GOTO 7090
7056 CLOSE (14); OPEN LOCK (14,ERR=*NEXT)"GW_LOG"; GOTO 7060
7057 IF ERR=12 THEN SERIAL "GW_LOG"; GOTO 7056 ELSE GOTO 7090
7060 FOR X=1 TO LEN(RCOUNT$)-2 STEP 3
7065 S$="GOE|STAT|"+FN%CDS$+"|"+X3$(40,3)+"|"+X3$(9,3)+"|"+FID(0)+"|"+F1$+"|"+RCOUNT$(X,3)+"|"+STR(RCOUNT[(X+2)/3]:"0000")
7075 PRINT (14)S$
7080 NEXT X
7085 CLOSE (14)
7090 REM "
7095 END_FILE$="Y"
7099 RETURN 
7100 REM "Get next GOE record number
7110 DIM OFPARM$(256)
7115 GOE_NUM=1
7120 EXTRACT (Z[13],KEY=X3$(9,3)+"O/F",DOM=7121)OFPARM$
7130 GOE_NUM=NUM(OFPARM$(7,6),ERR=7131)
7140 GOE_NUM=GOE_NUM+1
7150 IF GOE_NUM>999999 THEN GOE_NUM=1
7160 GOENUM$=STR(GOE_NUM:"000000")
7170 OFPARM$(7,6)=GOENUM$
7180 WRITE (Z[13],KEY=X3$(9,3)+"O/F")OFPARM$
7199 RETURN 
7200 REM "Validate customer number
7205 DIM AR1$(200),AR1[15]
7206 CUST_OUT$=STP(CUST_IN$,1),CUST_IN$=PAD(CUST_IN$,10," ")
7207 IF (PARMAR$(13,1)="Y" AND LEN(CUST_OUT$)<10) OR (PARMAR$(13,1)<>"Y" AND LEN(CUST_OUT$)<8) THEN CUST_OUT$=CUST_OUT$+"0"; GOTO 7207; REM "If rightmost 'hidden' 0's not given, then add
7208 IF LEN(CUST_OUT$)=8 THEN CUST_OUT$="00"+CUST_OUT$
7210 CUST_OUT$=PAD(STP(CUST_OUT$,1),10,"0")
7220 READ (Z[7],KEY=CUST_OUT$,DOM=7221)IOL=0370; GOTO 7250
7230 CUST_OUT$=CUST_IN$
7240 READ (Z[7],KEY=CUST_OUT$,DOM=7241)IOL=0370
7250 IF STP(AR1$,1)="" THEN ERR_CODE$="1",HOLD$="Y"
7299 RETURN 
7300 REM "Validate item code - Check for CIC
7301 REM "Item in  = Customer code + CIC (Len of 30)
7302 REM "Item out = Customer Code + Item Code (Len of 20)
7310 ITEM_IN$=OF2$(25,20)
7320 CALL "FM2DPD",X3$,X4$,"",ITEM_IN$,ITEM_OUT$,"",RETURN_CODE
7330 OF2$(25,20)=ITEM_OUT$
7349 RETURN 
7350 REM "Validate Codes
7360 CALL "OF2UZB",X3$,X4$,V_TYPE$,CODE_IN$,CODE_DESC$,"",""
7399 RETURN 
7400 REM "Increment P/O code *****
7401 IF PARMOE$(36,1)=" " OR PARMOE$(36,1)="" THEN RETURN 
7410 PO_NUM=ASC(PO_CODE$,ERR=7411)
7415 PO_NUM=PO_NUM+1
7420 IF PO_NUM=58 THEN PO_NUM=ASC("A")
7425 IF PO_NUM=91 THEN ERR_CODE$="X"
7426 REM "***** max'd out codes allowed *****
7430 PO_CODE$=CHR(PO_NUM)
7449 RETURN 
7450 REM "if DDMS then if vendor or invoice number different, choose new po_code if vendor+invoice number matches previous one then use it.
7454 IF OF2$(85,10)=DIM(10) THEN OF2$(14,1)=" "; GOTO 7495; REM "IF no vendor then skip
7455 VI$=OF2$(85,10)+OF2$(121,12); REM "TF vendor code + vendor inv #
7460 VIP=POS(VI$=VI_LIST$,22); REM "Is vend+inv# already on list?
7465 IF VIP>0 THEN VIP=(VIP+21)/22,OF2$(14,1)=VI_PO$(VIP,1); GOTO 7490
7470 VI_LIST$=VI_LIST$+VI$,VI_PO$=VI_PO$+PO_CODE$; OF2$(14,1)=PO_CODE$; GOSUB 7400
7499 RETURN 
7500 REM "Convert date Q$ into Q0$
7501 REM "Q$ in YYYYMMDD format or YYMMDD
7505 Q0$=""
7506 Q$=STP(Q$,1)
7507 IF Q$="" THEN GOTO 7549
7510 IF LEN(Q$)=8 THEN GOTO 7520 ELSE IF LEN(Q$)=6 THEN D1$=STR(NUM(Q$(1,2)):"00") ELSE GOTO 7549
7515 IF D1$<"50" THEN Q$="20"+Q$ ELSE Q$="19"+Q$
7520 Q0$=CHR(NUM(Q$(1,3),ERR=7521)-125)+Q$(4,1)+Q$(5,2)+Q$(7,2)
7549 RETURN 
7600 REM "Remove embedded dashes in zip code
7610 P=POS("-"=ZIP_IN$); IF P=LEN(ZIP_IN$) THEN ZIP_IN$=ZIP_IN$(1,P-1); GOTO 7610 ELSE IF P=1 THEN ZIP_IN$=ZIP_IN$(2); GOTO 7610 ELSE IF P<>0 THEN ZIP_IN$=ZIP_IN$(1,P-1)+ZIP_IN$(P+1)
7640 ZIP_OUT$=ZIP_IN$
7649 RETURN 
7650 REM ' determine length from version
7655 IF I$(1,1)="H" THEN IF I$(5,3)="103" OR I$(7,3)="103" THEN VER$="103"
7660 SWITCH VER$
7665 CASE "103"
7666 IF I$(1,1)="H" THEN F_NUM=21,F_LEN$="010303061001081508202001081010040420044010"
7667 IF I$(1,1)="L" THEN F_NUM=22,F_LEN$="01030301010110204010040909040710100712080704"
7668 IF I$(1,1)="S" THEN F_NUM=25,F_LEN$="01030301040920303030160210010101151535350404070740"
7669 IF I$(1,1)="P" THEN F_NUM=26,F_LEN$="0103013530303016021001010108120812081208120801080804"
7670 IF I$(1,1)="C" THEN F_NUM=6,F_LEN$="010301030164"
7671 IF I$(1,1)="N" THEN F_NUM=3,F_LEN$="010350"
7684 BREAK
7690 DEFAULT 
7691 IF I$(1,1)="H" THEN F_NUM=16,F_LEN$="01030306100108150820200108101010"
7692 IF I$(1,1)="L" THEN F_NUM=22,F_LEN$="01030301010110204010040909040710100712080704"
7693 IF I$(1,1)="S" THEN F_NUM=24,F_LEN$="010303010409203030301602100101011515353504040707"
7694 IF I$(1,1)="P" THEN F_NUM=26,F_LEN$="0103013530303016021001010108120812081208120"+"8010"+"808"+"04"
7695 IF I$(1,1)="C" THEN F_NUM=6,F_LEN$="010301030"+"164"
7696 IF I$(1,1)="N" THEN F_NUM=3,F_LEN$="010350"; REM "137783 (101)
7698 END SWITCH 
7699 RETURN 
7700 REM "Parse delimeted strings
7704 REM "For DDMS, redo line a little, save off desc in LINE_DESC$ because it DOES contain ','s which will confuse everything
7705 IF OPT$="DDMS" AND I$(1,1)="L" THEN GOSUB 8300
7710 GOSUB 7650; REM determine version, field length
7720 DIM FIELDS[F_NUM]
7730 FOR F_LOOP=1 TO F_NUM
7740 FIELDS[F_LOOP]=NUM(F_LEN$(2*F_LOOP-1,2))
7750 NEXT F_LOOP
7760 CALL "ZZ2DLM","","",I$,I$(2,1),FIELDS{ALL},"",RETURN$,FIELDS_FOUND
7764 IF OPT$="DDMS" AND RETURN$(1,1)="H" THEN DDMS_ORDER$=STP(RETURN$(125,10),2),RETURN$=RETURN$(1,124)
7765 IF I$(1,1)="S" AND OPT$="DDMS" THEN GOSUB 8250; STMP$=RETURN$(102,30),RETURN$(102,30)=RETURN$(42,30),RETURN$(42,30)=STMP$; REM "reformat for DDMS
7770 I$=RETURN$
7790 RETURN 
7800 REM "Perform mappings on line records I$(1,1)="L"
7805 DIM OFH$(22); FIND (Z[9],KEY=LINE_REC$(81,10),DOM=7806)OFH$; LINE_REC$(81,10)=OFH$(11,10); REM "Map Vendor code on line
7807 IF OFPARM$(33,1)="Y" THEN LINE_REC$(178,4)="00"+OF1$(9,2); REM "If parameter set, default ship from warehouse based on customer division # from order header
7840 IF OPT$="DDMS" OR OPT$="DOCU" OR OFPARM$(132,1)="Y" THEN IF POS(LINE_REC$(8,1)="IC")<>0 THEN GOSUB 7850; REM "See if item exists or can be setup 145202- only check inventory items.  SSP177553, only do this if DDMS or DOCU
7845 RETURN 
7850 REM "Check to see if item is in inventory, if not setup
7852 IF OPT$="DOCU" OR OFPARM$(132,1)="Y" THEN FIND (Z[11],KEY=OF1$(9,10)+LINE_REC$(21,10),DOM=7856); FIND (Z[15],KEY=OF1$(9,10)+LINE_REC$(21,10),DOM=7856); GOTO 7895
7855 FIND (Z[11],KEY=DIM(10)+LINE_REC$(21,10),DOM=7856); GOTO 7895; REM "If found, we're done
7859 REM "Build string & call product catalog gateway
7860 DIM ITEM$(1046) ! SSP279026, WO273055, from 737 to 1046
7862 ITEM$(7,20)=DIM(10)+LINE_REC$(21,20),ITEM$(27,40)=LINE_REC$(41,40),ITEM$(88,14)=LINE_REC$(81,14); REM "item code (full 20), desc, vendor plant code
7863 IF OPT$="DOCU" OR OFPARM$(132,1)="Y" THEN ITEM$(7,10)=OF1$(9,10)
7864 ITEM$(130,4)=LINE_REC$(113,4),ITEM$(344,10)=LINE_REC$(117,7),ITEM$(194,10)=LINE_REC$(124,10),ITEM$(364,10)=LINE_REC$(134,10),ITEM$(374,10)=LINE_REC$(134,10); REM "Sell unit, sell qty per um, price, last cost, avg cost
7875 IF ITEM$(130,4)="DZ  " THEN ITEM$(344,10)="12"; REM "sometimes a problem, if DZ we mean Dozen
7885 IF OPT$="DOCU" OR OFPARM$(132,1)="Y" THEN CALL "PC2UAA",ERR=7886,X3$,X4$,ITEM$,"D" ELSE CALL "PC2UAA",ERR=7886,X3$,X4$,ITEM$,"G"
7895 RETURN 
7900 REM "Invalid record type
7901 REM "***** Flag w/error code?
7999 RETURN 
8000 REM "DDMS integration
8005 IF I$(1,1)<>"L" THEN GOTO 8030; REM "line type checks
8010 ! I$(41,40)=LINE_DESC$; REM "LINE_DESC$ set in 7700's, saved because it has ','s in it
8012 I$(21,20)=I$(21,10); REM "We should only have non customer specific items
8014 IF I$(5,3)="ZZZ" THEN I$(8,1)="S",I$(5,3)=""; REM "145202 if ZZZline # then change into Special charge and blank line number
8015 IF I$(5,3)<>DIM(3) THEN I$(5,3)=DIM(3); REM "We will assign the line number
8017 IF POS(I$(8,1)="IS")<>0 AND STP(VEND_INV$,1)>"" THEN GOTO 8020; REM "If line type not I or S,check to see if special charge, if not assume I
8018 FIND (Z[14],KEY="X"+I$(21,10),DOM=8019); I$(8,1)="S"; GOTO 8020; REM "If found, then set to special charge
8019 I$(8,1)="I"; REM "if not found, set type to inventory
8020 REM "Done processing special charge check
8030 REM "End of line logic
8032 IF LEN(I$)<78 THEN I$=I$+FILL_SP$(1,78-LEN(I$)); REM " SSP# 156082 - PAD OUT SHORT RECORDS
8035 IF I$(1,1)="S" THEN IF I$(42,7)="ATTN:  " THEN I$(42,30)=I$(49,23) ELSE IF I$(42,6)="ATTN: " THEN I$(42,30)=I$(48,24)
8045 RETURN 
8050 REM "Handle DDMS Salesperson line
8052 J1=POS(DELIM$=I$),J2=POS(DELIM$=I$,1,2),SP$="",CC_TYPE$="",CC_AMT=0
8053 IF J1<>0 THEN SP$=I$(1,J1-1); IF J2<>0 THEN CC_TYPE$=I$(J1+1,J2-J1-1); IF J2<>LEN(I$) THEN CC_AMT=NUM(I$(J2+1),ERR=8054)
8054 OF1$(127,4)=I$; IF CC_AMT<>0 THEN OF1$(131,4)=CC_TYPE$,OF1[1]=CC_AMT
8055 DIM DTMP[5]; EXTRACT (Z[1],KEY=OF1$(1,8),DOM=8090)DTMP$,DTMP[0],DTMP[1],DTMP[2],DTMP[3],DTMP[4],DTMP[5]
8060 DTMP$(127,4)=I$; IF CC_AMT<>0 THEN DTMP$(131,4)=CC_TYPE$,DTMP[1]=CC_AMT
8085 WRITE (Z[1],KEY=OF1$(1,8))DTMP$,DTMP[0],DTMP[1],DTMP[2],DTMP[3],DTMP[4],DTMP[5]
8099 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set a"+"nd before the Proceed? question. Set T to total # to do (by calli"+"n"+"g ZZINFO) and T0 to the reporting interval (report every T0 n"+"umb"+"er"+" of records) this is typically 2% of total; This also "+"prints a "+"mes"+"sage (@8115),so adjust accordingly
8105 REM CALL "ZZINFO",Z[10],T9,X3$,T,T0,K,B,D,S0,S1,F2$
8110 XX$=FN%XFD$(Z[10],0); T0=DEC($00$+XX$(38,4))
8112 T=T0
8113 CALL "ZZ2FNC;SERIALRECCNT",Z[10],T
8115 PRINT @(0,7),"There are "+STR(T)+" records to process"
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8135 T1=0
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar g"+"raph given total #, T and current #, C, and time counter T1 (T1 i"+"s"+" init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,I9
8195 RETURN 
8200 REM "If incoming shipping info (SHIP_REC$(72,88)) is all blank then get information for OF3$(48,88) from FM0 based on OF3$(15,4) which should be set by now.
8205 IF STP(SHIP_REC$(72,88),3," ")="" THEN GOTO 8206 ELSE GOTO 8245
8210 FIND (Z[8],KEY=OF3$(14,1)+OF1$(9,10)+OF3$(15,4),DOM=8245)FM0$
8215 OF3$(78,88)=FM0$(51,87); REM "difference is OF3$ has allowance for '-' in zip code
8220 IF STP(OF3$(234,35),1)="" AND STP(FM0$(248,35),1)<>"" THEN OF3$(234,35)=FM0$(248,35) ! SSP#237660
8245 RETURN 
8250 REM "Handle DDMS requested order number
8255 IF RETURN$(163,2)<>"C " THEN GOTO 8260
8256 EXTRACT (Z[2],KEY=OF2$(1,11),DOM=8260)IOL=0320
8257 OF2[3]=-OF2[3]; WRITE (Z[2],KEY=OF2$(1,11))IOL=0321
8270 OF1$(107,6)=RETURN$(165,6)
8275 EXTRACT RECORD (Z[1],KEY=OF1$(1,8),DOM=8290)DTMP$
8280 DTMP$(107,6)=OF1$(107,6)
8285 WRITE RECORD (Z[1],KEY=OF1$(1,8))DTMP$
8295 RETURN 
8300 REM "Redo line for DDMS to fix-> |item code      manfact_code|desc,desc| should be |custcode|item code|manfact_code,desc,desc,desc|, on CREDITS, manfact_code is blank, so strip off
8305 CP6=POS(DELIM$=I$,1,6),CP7=POS(DELIM$=I$,1,7),CP8=POS(DELIM$=I$,1,8)
8310 ITEM_CODE$=I$(CP6+1,CP7-CP6-1-4),MAN_CODE$=I$(CP7-3,3),DESC$=I$(CP7+1,CP8-CP7-1)
8315 IF STP(MAN_CODE$,1)="" THEN NEW_DESC$=DESC$ ELSE NEW_DESC$=STP(MAN_CODE$,1)+","+DESC$
8325 I$=I$(1,CP6)+"|"+ITEM_CODE$+"|"+NEW_DESC$+I$(CP8)
8349 RETURN 
8400 VOILA_READ:! Read VOILA file
8405 IF DO_LINE THEN GOTO SETUP_LINE
8410 DIM VOILA$[7]; READ (Z[10],END=7050)VOILA${ALL}:[STR(",")]
8415 I9+=1,COUNT=COUNT+1,RCOUNT$="VLA",RCOUNT[1]+=1
8416 IF I9=1 THEN GOTO 8410 ! Skip first line of headers
8420 IF MOD(COUNT,15)=1 THEN GOSUB 8150; REM " report every 15 lines
8425 IF FIRST_TIME THEN FIRST_TIME=0; GOTO *NEXT ELSE GOTO SETUP_LINE ! Only do header once
8430 SETUP_HEADER:DIM I$(206); I$(1)="HEXT103",I$(14,10)=VOILA$[0] ! Setup Header record
8431 DO_LINE=1 ! next time use same data to do line info
8432 GOTO VR_DONE
8435 SETUP_LINE: DO_LINE=0; DIM I$(183); I$(1,4)="L103",I$(11,10)=VOILA$[0],I$(21,20)=VOILA$[2],I$(113,4)="PKG" ! customer code, item_code, sell UM
8436 I$(117,7)=VOILA$[4],I$(144,7)=VOILA$[3],I$(178,4)=VOILA$[1] ! Sell Qty per, Qty ordered, Ship from whse
8440 VR_DONE:
8448 RETURN 
8449 ! 
8600 REM "CHECK VALIDITY
8605 DIM OF1$(198),OF1[5]
8610 READ (Z[1],KEY="",DOM=*NEXT)
8620 OF1_KEY$=KEY(Z[1],END=5000)
8630 OF1$(1,8)=OF1_KEY$
8640 READ (Z[1],KEY=OF1$(1,8))IOL=0310
8645 CUST_IN$=OF1$(9,10); GOSUB 7200
8690 GOSUB 1500
8700 GOTO 8620
8900 REM "Define Functions
8910 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"      ")-1)
8920 DEF FNQ$(Z9$)=TBL(NUM(Z9$)<10,CHR(ASC("A")+NUM(Z9$)-10),STR(NUM(Z9$):"0")) ! CHAR(3) SEQ# (1-35) => CHAR(1) SEQ# (1-9,A-Z) SSP 307409
9000 REM "Error Processing
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM "Transfer Control
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End of Program
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 ! Program changes starting 02/15/06
56010 REM "190633-Two WebEC tags, bill_m and shipper_id, data to pass into
56020 REM "194575-Add support for M/1000 pricing on DocuCenter; setup custom item if not present
56021 REM "255022-GOE - PO comments are always being set to N                 
56022 REM "276185-Order Entry Gateway - the special charge lines are printing 
56024 REM "279026-DocuCenter; during import, getting Item Not Found message.  
56025 REM "307409-Changes to GOE for conversion from EQD for Order History and
