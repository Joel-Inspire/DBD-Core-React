0010 REM "Sort payment selections into Check print file <AP2CDB>
0035 REM "5.2 - 12/22/03 - 12.474444 - tma - SSP# 167382
0040 REM "Copyright 2003 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR 
0095 ENTER X3$,F0,F1,F2,F3,F4,F5; REM "F2 & F3 added for sort by vendor lookup (PFG) - F4 AND F5 ADDED FOR SORT BY NAME SSP 160286
0100 SETERR 9000
0120 DIM A[5],C[2],D[5],E[13],D$(28)
0300 REM "I/O lists
0305 REM "IOLIST FOR APH - Check print file
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5]
0320 IOLIST B$
0325 REM "IOLIST FOR AP4 - Vendor Masterfile
0330 IOLIST C$,C[0],C[1],C[2]
0340 IOLIST D$(1),D[0],D[1],D[2],D[3],D[4],D[5]
0350 IOLIST E$,E[0],E[1],E[2],E[3],E[4],E[5],E[6],E[7],E[8],E[9],E[10],E[11],E[12],E[13]
0500 REM 
0510 DIM Z[10]
0520 Z[1]=F0,Z[4]=F1
0521 IF F2<>0 THEN Z[7]=F2,Z[3]=F3,F9=1; REM "F2=Temp Sort File, F3=AP4, F9=1 means we need to write out the alpha sort file
0522 IF F4<>0 THEN Z[10]=F4,Z[3]=F5,F9=2; REM "F2=Sort by Vendor NameTemp File, F3=AP4, F9=2 means we need to write out the Vendor Name sort file
8000 REM "Sort by bank code
8002 PRINT @(12,20),"Now re-sorting data for report:",
8005 DIM D[5],A[5]; Q0=0
8010 CALL "ZZINIT",STR(Z[1]:"00")
8020 READ (Z[4],KEY="",DOM=8021)
8030 READ (Z[4],END=8100)IOL=0340
8035 PRINT @(45,20),D$(1,20),
8040 Q0=Q0+1,A$=X3$(9,3)+D$(22,3)+D$(1,10)+D$(21,1)+D$(11,10)+"      "+D$(25,3),A[0]=D[0],A[1]=D[1],A[2]=D[2],A[3]=D[3]
8050 WRITE (Z[1],KEY=A$(1,27))IOL=0310
8055 IF F9=1 THEN GOSUB 8400
8056 IF F9=2 THEN GOSUB 8600; REM " SSP# 160286
8080 GOTO 8030
8100 REM "
8102 Q=0,Q$=" "
8103 IF Q0>0 THEN WRITE (Z[4],KEY="")"SORTED  "+STR(Q0) ELSE REMOVE (Z[4],KEY="",DOM=8104)
8105 READ (Z[1],KEY="",DOM=8106)
8110 READ (Z[1],END=8200)IOL=0310
8120 IF A$(1,LEN(Q$))<>Q$ THEN GOSUB 8300
8130 Q=Q+A[0]-A[1]
8140 GOTO 8110
8200 REM "
8210 GOSUB 8300
8280 READ (Z[1],KEY="",DOM=8281)
8290 PRINT @(0,20),'CL',; GOTO 9900
8300 REM "CHECK CHANGE
8305 IF Q>=0 THEN GOTO 8385
8310 READ (Z[1],KEY=Q$,DOM=8311)
8315 K$=KEY(Z[1],END=8385); READ (Z[1]); IF K$(1,17)<>Q$ THEN GOTO 8385
8320 REMOVE (Z[1],KEY=K$)
8321 IF F9=1 THEN GOSUB 8500
8325 REMOVE (Z[4],KEY=K$(7,10)+K$(18,10),DOM=8326)
8330 GOTO 8315
8385 Q=0; IF LEN(A$)>=17 THEN Q$=A$(1,17)
8390 RETURN 
8400 REM "Write Temp Sort by Vendor Lookup (PFG)
8410 DIM C$(LEN(C$)); FIND (Z[3],DOM=8411,KEY=D$(1,10))IOL=0330
8415 K1$=A$(1,6)+C$(145,10)+A$(7,10)+A$(17,11)
8416 REM PRINT A$,'LF',K1$,; INPUT *
8420 WRITE (Z[7],KEY=K1$)
8495 RETURN 
8500 REM "
8510 DIM C$(LEN(C$)); FIND (Z[3],DOM=8511,KEY=K$(7,10))IOL=0330
8515 K1$=K$(1,6)+C$(145,10)+K$(7,10)+K$(17,11)
8520 REMOVE (Z[7],KEY=K1$)
8595 RETURN 
8600 REM "Create Sort by Vendor Name - ssp 160286                               
8606 DIM AP4$(577); READ (Z[3],KEY=D$(1,10))AP4$
8608 DIM APZ$(62); APZ$(1,3)=X3$(9,3)
8610 APZ$(4,3)=D$(22,3)
8614 APZ$(7,35)=AP4$(11,35); APZ$(42,10)=D$(1,10); APZ$(52,1)=D$(21,1); APZ$(53,10)=D$(11,10)
8616 WRITE (Z[10],KEY=APZ$)
8699 RETURN 
9000 REM "ERROR PROCESSING
9010 E0=ERR,E1=TCB(5)
9016 SETERR 9000
9040 CALL "ZZERRM",E0$,"AP2CDB",E1$,X3$,E0,E1,E2,E3,0
9050 ON E2 GOTO 9060,9100,9900,9070
9060 RETRY 
9070 SETERR 0000
9075 RETRY 
9080 SETERR 9000; RETRY 
9100 REM " TRANSFER CONTROL
9190 GOTO 9900
9300 RETURN 
9800 REM "
9850 EXIT 
9900 REM "END PROGRAM
9940 EXIT 
9999 END 
