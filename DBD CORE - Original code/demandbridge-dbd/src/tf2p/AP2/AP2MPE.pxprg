0010 REM "Terms Code Setup <AP2MPE>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 07/23/09 - 10.316944 - tma - SSP# 231633
0037 REM "231633-Can add a "  " terms code in Terms Code Masterfile          
0040 REM "Copyright 2009 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="AP2MPE",X1$="Terms Code Setup",K9$="",K9=0
0120 DIM Z0$(80,"-")
0130 K0=2,K1=1
0135 C9=-1,V9=-2
0140 M0$="-##.00"
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1,LEN(A$)),A[0],A[1]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X AP2...  06O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 0521,9900
0530 READ (Z[6],KEY=X3$(9,3)+"A/P")APS$
0585 W9$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,W9$
0600 REM "
0610 X9$="P=Period of time in days, T=This Month, N=Next Month"
0620 GOSUB 6000
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1035 DIM A$(100+K9),A[1]
1045 A$(1,0+K9)=K9$+A1$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1500
1200 REM "Terms Code
1210 CALL "ZZENTR","SZXUX",A{ALL},A$,X4$,X3$,12,3,1+K9,2,C0,"","{1"+X$,"","AP2MPE00","AP2","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1212 IF ABS(C0)<>4 THEN IF X3$(330,1)="1" THEN IF A$(1,2)<="99" THEN CALL "ZZPROM",".4",X3$,0,"You may only setup or change vendor codes A0 through ZZ","","",0; A$(1,2)=""; GOTO 1210
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9900,6970
1220 IF A$(1,2)=DIM(2) THEN GOTO 1210 ! SSP# 231633
1240 GOTO 1120
1500 REM "
1505 Z9=-1
1510 A1$=K9$+A$(LEN(K9$)+1,2),A$(1,LEN(K9$))=K9$
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1525 Z9=0
1530 GOSUB 6600
1590 GOTO 1700
1600 REM "
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630,6975
1630 GOTO 1000
1660 IF V9>=0 THEN GOSUB 6200
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1820 CALL "ZZPROM",STR(6+C9),X3$,Z,"","","V",X0; ON Z GOTO 1850,6500,1630,6700,6970
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310
1865 C9=1
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF C0>3 OR X0<0 THEN GOTO 1800
2035 IF X0*6+11<LEN(V9$) THEN IF V9$(X0*6+11,1)="2" THEN GOTO 2020
2040 ON X0 GOTO 2100,2150,2200,2250,2300,2350,2400,2450,2500,2550,2600,2650,2700,2750,1800
2100 REM "Terms Description
2110 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,33,3,3+K9,30,C0,"","{3"+X$,"","AP2MPE02","","",""
2140 GOTO 2020
2150 REM "P=Period, E=End, N=Next, D=Day
2155 X$=X9$
2160 CALL "ZZENTR","SRUX",A{ALL},A$,X4$,X3$,27,5,33+K9,1,C0,"","{3"+X$,"TNPTNPDPCNT","AP2MPE04","DPEN","",""
2162 IF C0<0 THEN GOTO 2170
2165 IF A$(33+K9,1)="P" THEN A$(37,6)=""; PRINT @(27,6),"  ",@(27,7),"  ",@(27,8),"  ", ELSE A$(34,3)=""; PRINT @(58,5),"   ",
2190 GOTO 2020
2200 REM "Number of days until due
2205 IF A$(33+K9,1)<>"P" THEN GOTO 2240 ELSE X$="Enter the number of days in the period"
2210 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,58,5,34+K9,3,C0,"","{3"+X$,"","AP2MPE06","","",""
2240 GOTO 2020
2250 REM "Day in month or Days in period
2255 IF A$(33+K9,1)="P" THEN GOTO 2240 ELSE X$="Enter the day in the month or E for end of month"
2260 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,27,6,37+K9,2,C0,"","{3"+X$,"","AP2MPE08","","",""
2290 GOTO 2020
2300 REM "cutoff day for current month
2305 IF A$(33+K9,1)="P" THEN GOTO 2240 ELSE X$="Enter the cutoff day after which the second due date will be used"
2310 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,27,7,39+K9,2,C0,"","{3"+X$,"","AP2MPE10","","",""
2340 GOTO 2020
2350 REM "Second Due Date
2355 IF A$(33+K9,1)="P" THEN GOTO 2240 ELSE X$="Enter the day in the month or E for end of month"
2360 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,27,8,41+K9,2,C0,"","{3"+X$,"","AP2MPE12","","",""
2390 GOTO 2020
2400 REM "Discount due date type
2405 X$=X9$
2410 CALL "ZZENTR","SRUX",A{ALL},A$,X4$,X3$,27,11,43+K9,1,C0,"","{3"+X$,"P NDT","AP2MPE14","PEND","",""
2412 IF C0<0 THEN GOTO 2420
2415 IF A$(43+K9,1)="P" THEN A$(47+K9,6)=""; PRINT @(27,12),"  ",@(27,13),"  ",@(27,14),"  ", ELSE A$(44+K9,3)=""; PRINT @(58,11),"   "
2440 GOTO 2020
2450 REM "Number of days until due
2455 IF A$(43+K9,1)<>"P" THEN GOTO 2240 ELSE X$="Enter the number of days in the period"
2460 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,58,11,44+K9,3,C0,"","{3"+X$,"","AP2MPE16","","",""
2490 GOTO 2020
2500 REM "Enter the Due Day
2505 IF POS(A$(K9+43,1)="P ")>0 THEN GOTO 2020
2506 X$="Enter the day in the month or E for end of month"
2510 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,27,12,47+K9,2,C0,"","{3"+X$,"","AP2MPE18","","",""
2540 GOTO 2020
2550 REM "cutoff day fo current month
2555 IF POS(A$(K9+43,1)="P ")>0 THEN GOTO 2020
2556 X$="Enter the cutoff day after which the second due date will be used"
2560 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,27,13,49+K9,2,C0,"","{3"+X$,"","AP2MPE20","","",""
2590 GOTO 2020
2600 REM "Disc Due day or # days period
2605 IF POS(A$(K9+43,1)="P ")>0 THEN GOTO 2020
2606 X$="Enter the day in the month or E for end of month"
2610 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,27,14,51+K9,2,C0,"","{3"+X$,"","AP2MPE22","","",""
2640 GOTO 2020
2650 REM "percentage discount allowed
2655 IF POS(A$(K9+43,1)=" ")>0 THEN GOTO 2020
2660 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,39,16,0,220,C0,M0$,"{3"+X$,"","AP2MPE24","","",""
2690 GOTO 2020
2700 REM "2nd number of days until due, WO131561
2705 IF APS$(80,1)<>"Y" OR A$(43+K9,1)<>"P" OR STP(A$(44+K9,3),3)=" " THEN GOTO 2020 ELSE X$="Enter the number of days in the 2nd period"
2710 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,30,18,53+K9,3,C0,"","{3"+X$,"","AP2MPE26","","",""
2740 GOTO 2020
2750 REM "2nd percentage discount allowed, WO131561
2755 IF APS$(80,1)<>"Y" OR STP(A$(53+K9,3),3)=" " THEN GOTO 2020
2760 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,73,18,1,220,C0,M0$,"{3"+X$,"","AP2MPE28","","",""
2790 GOTO 2020
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(0,3),"Terms Code:",@(20,3),"Description:",@(0,4),Z0$,@(2,5),"Enter the due date type:",@(32,5),"Number of days until due:",@(8,6),"Enter the due day:",@(5,7),"Enter the cutoff day:",@(10,8)," Second due day:",@(0,9),Z0$,@(3,11),"Discount due date type:",@(32,11),"Number of days until due:",@(8,12),"Enter the due day:",@(5,13),"Enter the cutoff day:",@(10,14)," Second due day:",@(0,16),"Enter the percentage discount allowed:",@(0,19),Z0$,
6063 IF APS$(80,1)="Y" THEN PRINT @(0,18),"2nd number of days until due:",@(40,18),"2nd percentage discount allowed:",; REM "WO131561
6065 PRINT (0,ERR=6066)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 PRINT @(33,3),A$(3+K9,30),@(27,5),A$(33+K9,1),@(58,5),A$(34+K9,3),@(27,6),A$(37+K9,2),@(27,7),A$(39+K9,2),@(27,8),A$(41+K9,2),@(27,11),A$(43+K9,1),@(58,11),A$(44+K9,3),@(27,12),A$(47+K9,2),@(27,13),A$(49+K9,2),@(27,14),A$(51+K9,2),@(39,16),A[0]:M0$,
6225 IF APS$(80,1)="Y" THEN PRINT @(30,18),A$(53+K9,3),@(73,18),A[1]:M0$,; REM "WO131561
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6410 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 PRINT @(12,3),A$(1+K9,2),
6490 RETURN 
6500 REM "DELETE
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6515 IF W9$>"" THEN IF W9$(7,1)="H" THEN CALL "RT2WOR",ERR=6516,X3$,X4$,"","AP2...","D",A1$,A$,0,A{ALL}; REM "Record remote T/M change if this is the host
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY
6705 IF Z9=0 THEN GOTO 6790
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6715 IF W9$>"" THEN IF W9$(7,1)="H" THEN CALL "RT2WOR",ERR=6716,X3$,X4$,"","AP2...","U",A1$,A$,1,A{ALL}; REM "Record remote T/M change if this is the host
6775 IF X3$(30,1)="M" THEN IF X3$(330,1)<>"1" THEN IF A$(1,2)<="99" THEN CALL "CM2AAA",X3$,Z[1],1,A1$,"",X9
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=1200)
6815 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 1200
6820 A$(1,LEN(A1$))=A1$
6830 GOSUB 6450
6840 GOTO 1515
6850 REM "PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 BEGIN ; SETESC 9350
9950 RUN "ZMENU"
9999 END 
