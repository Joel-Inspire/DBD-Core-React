0010 REM "Export A/P Invoices <AP2XAA>
0020 SETESC 9300; SETERR 9000
0035 REM "4.0 - 11/14/96 - 15.52 - kmc
0040 REM "Copyright 1996 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0070 REM "PARAM$=EXPORTAP parameters
0071 REM "FILES[] 0=header file slot, 1=detail file slot
0072 REM "INV$,INV[ALL]=APD info
0073 REM "GL$=extra G/L account postings
0074 REM "GL[all]=extra G/L posting amounts
0075 REM "Z_CALLING[] = Z array from calling progam
0076 REM "UPDATE$ = update info, (22,6)=audit control number
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,PARAM$,FILES{ALL},INV$,INV{ALL},GL$,GL{ALL},Z_CALLING{ALL},UPDATE$
0100 SETERR 9000
0110 X0$="AP2XAA",X1$="Export A/P Invoice Information"
0120 IF FN%NEA("FILES",0)=0 THEN DIM FILES[1]
0130 MASK_82$="-00000000000",MASK_72$="-0000000000",MASK_32$="-00000"
0300 REM "IOLISTS
0320 APE_IOLIST:IOLIST LINDEX,LINE$,LINE[0],LINE[1],LINE[2],LINE[3]
0400 REM "Check for files, if not there then create
0401 REM "if array values <>0 then we've already done this
0405 IF FILES[0]<>0 THEN GOTO 0600
0406 HEADER_FILE$=STP(PARAM$(13,8),1),DETAIL_FILE$=STP(PARAM$(21,8),1)
0410 CLOSE (14); OPEN (14,ERR=0411)HEADER_FILE$; CLOSE (14); GOTO 0415
0411 SERIAL HEADER_FILE$,0,0
0415 CLOSE (14); OPEN (14,ERR=0416)DETAIL_FILE$; CLOSE (14); GOTO 0420
0416 SERIAL DETAIL_FILE$,0,0
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O "+HEADER_FILE$+"  02O "+DETAIL_FILE$+" 13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0524 REM "Close and re-open as text files, read to the end in case there are existing entries, then save file slots in FILES[]
0525 CLOSE (Z[1]); OPEN LOCK (Z[1],OPT="TEXT")HEADER_FILE$; FILES[0]=Z[1]; CLOSE (Z[2]); OPEN LOCK (Z[2],OPT="TEXT")DETAIL_FILE$; FILES[1]=Z[2]
0530 READ (FILES[0],END=0531); GOTO 0530
0535 READ (FILES[1],END=0536); GOTO 0535
0600 REM "
1000 REM "Write Header Record
1001 DIM HEADER$(100)
1009 CALL "ZZDISP","AX",INV$(7,10),"A/P",X3$,TEMP$,"",0,0,X4$
1010 HEADER$(1,10)=TEMP$,HEADER$(11,7)=UPDATE$(22,6); REM "Vendor Code (as displayed), audit control number
1015 HEADER$(18,10)=INV$(17,10),HEADER$(28,6)=INV$(1,6); REM "Vendor inv #, FY/AP
1020 HEADER$(34,10)=INV$(62,10); REM "Our P/O number
1025 HEADER$(44,12)=STR(INV[1]*100:MASK_82$); REM " Invoice amount
1030 HEADER$(56,11)=STR(INV[7]*100:MASK_72$); REM "Discount amount
1035 HEADER$(67,12)=STR((INV[1]-INV[7])*100:MASK_82$); REM "Amount after discount
1040 IF INV[6]=0 THEN TEMP=0 ELSE TEMP=INV[7]*100/INV[6] END_IF ; HEADER$(79,6)=STR(INT(TEMP*100):MASK_32$); REM "Discount percentage
1045 HEADER$(85,8)=FND$(X3$(21,6)),HEADER$(93,8)=FND$(INV$(27,6)); REM "Batch date, invoice date
1100 REM "Write record
1105 WRITE RECORD (FILES[0])HEADER$+$0D0A$
1200 REM "Process G/L transactions on invoice by stepping thru the lines
1203 NUM_LINES_DONE=0
1205 LINDEX=INV[0]; DIM LINE[3]
1210 IF LINDEX<=0 THEN GOTO 1300 ELSE READ (Z_CALLING[2],IND=LINDEX)IOL=APE_IOLIST
1215 DIM DETAIL$(61)
1220 DETAIL$(1,33)=HEADER$(1,33); REM "Set Vendor #, batch #, invoice # & G/L accounting period from header
1225 DETAIL$(35,15)=LINE$(7,12); REM "G/L account number
1230 DETAIL$(50,12)=STR(LINE[3]*100:MASK_82$); REM "Amount
1280 WRITE RECORD (FILES[1])DETAIL$+$0D0A$
1285 NUM_LINES_DONE=NUM_LINES_DONE+1
1295 IF PARAM$(29,1)="Y" AND NUM_LINES_DONE=1 THEN GOTO 1300 ELSE GOTO 1210; REM "If params say Y to do only 1 detail line , then leave if this is the one
1300 REM "Process G/L transactions from the header by stepping thru GL$
1305 IF PARAM$(29,1)="Y" OR GL$="" THEN GOTO 1395; REM "If no g/l to do or params say Y to only 1 detail record per invoice
1307 DIM DETAIL$(61); DETAIL$(1,33)=HEADER$(1,33); REM "Initialize for all postings, get header matching stuff from the header
1310 FOR INDEX=1 TO LEN(GL$)-11 STEP 12
1315 DETAIL$(35,15)=GL$(INDEX,12); REM "G/L account number
1320 DETAIL$(50,12)=STR(GL[(INDEX+11)/12]*100:MASK_82$); REM "Amount
1335 WRITE RECORD (FILES[1])DETAIL$+$0D0A$
1340 NEXT INDEX
5000 GOTO 9900
8955 DEF FND$(Z9$)=STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")+Z9$(3,4)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
