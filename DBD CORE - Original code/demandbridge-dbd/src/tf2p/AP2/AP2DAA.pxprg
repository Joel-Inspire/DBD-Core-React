0010 REM "A/P Invoice Entry <AP2DAA>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 11/12/09 - 10.554722 - tma - SSP# 219897
0037 REM "219897-DT2 - PE(028, AP2DAA-7095) in DDMS processing.              
0040 REM "Copyright 2009 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 IF J9<>0 THEN GOTO 8500
0085 IF L0=-1 THEN L0=0,X0$="AP2DAA"; GOTO 1800
0090 ON L0 GOTO 0095,6700,6500,7100
0100 CLEAR ; SETERR 0105; ENTER X3$,X4$,Q0$,Q1$; POR_GATEWAY$="Y"
0105 SETERR 9000; SETESC 9300
0110 X0$="AP2DAA",X1$="A/P Invoice Entry",K9$="",K9=0
0120 DIM Y[4],S$(50)
0125 V8$="000209151899"
0126 F2$="*"
0127 T5=10; REM "Number of elements in T array
0130 K0=26,K1=4
0135 V9=0,Z9=1
0140 M0$="###,###,###.00-",M1$="-#0"
0144 IF POR_GATEWAY$="Y" THEN Q3$=Q1$,F2$=""; GOTO 0146
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0200 REM "
0240 IF POR_GATEWAY$<>"Y" THEN CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1,LEN(A$)),A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14]
0320 IOLIST B$
0330 IOLIST C$,C[0],C[1],C[2]
0340 IOLIST D$,D[0],D[1]
0350 IOLIST E$(1),E[0],E[1],E[2]
0370 IOLIST G$
0390 IOLIST H$,H[0],H[1],H[2],H[3],H[4],H[5],H[6]
0410 IOLIST K$
0420 IOLIST L$
0500 REM "FILES
0505 Z9=NUM(X3$(60,3)); DIM Z[Z9]
0510 Z$="01X APD...  02O APE...  03O AP4...  04ORAP2...  05ORZYB...  07ORAP3...  08ORAPQ...  06O ZZPARM  11ORAPI...  12O AP8...  13O GL1...  "
0516 REM IF POS("J/C"=X4$(7),12)>0 THEN LET Z$=Z$+"09O JC0...  10O JC1...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0525 F=Z[2]
0560 DIM F1$(120); READ (Z[6],KEY=X3$(9,3)+"AP2A",DOM=0562)F1$
0565 REM "F9 IS LIMIT FOR AUTO INVOICE HOLD, REMOTE LOCATION LARAMS
0570 IF X3$(330,1)="1" THEN READ (Z[6],KEY="R/L"+X3$(9,3),DOM=0571)*,F9
0580 READ (Z[6],KEY=X3$(9,3)+"A/P",DOM=9810)F0$
0585 IF F0$(70,1)="Y" THEN Z$="09O AR3...  "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0586,9900
0590 READ (Z[6],KEY=X3$(9,3)+"G/L",DOM=9810)G0$; G8=NUM(G0$(170,2))
0595 DIM SEC$(100); FIND (Z[6],KEY="osec"+X3$(40,3),DOM=0596)SEC$
0600 REM "
0601 GOSUB 8950; IF Q3$<>"" THEN F2$=""
0605 GOSUB 7400
0610 GOSUB 7600
0615 GOSUB 7800
0650 ON Z9 GOTO 7000,0651
0655 C9=-1,V0=0
0658 IF Q3$="" THEN CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,0,0,0
1000 REM "
1005 V0=0,Z9=-1,I1=0,A0=0
1010 IF POR_GATEWAY$<>"Y" THEN CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(1),C$(300),C[2],D$(100),D[1],E$(108),E[2],G$(40),F9$(0)
1035 DIM A$(146+K9),A[14],T[T5]
1045 A$(1,16+K9)=K9$+A1$,A$(104,4)=X3$(9,3)+X3$(330,1)
1050 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0; IF A$(7+K9,2)="  " THEN X0=1
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1250,1300,1350,1500
1200 REM "Fiscal Year Ending
1205 IF LEN(A1$)>0 THEN A$(1,4)=A1$(1,4); GOTO 1212
1207 Q$=X3$(9,3)+"G/LYE"
1210 CALL "ZZENTR","ZXUX",A{ALL},A$,X4$,X3$,13,3+V0,1+K9,4,C0,"","{1"+X$,Q$,"AP2DAA00","GLYE","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1216,6800,9700,6970
1220 FIND (Z[6],KEY=X3$(9,3)+"G/LYE"+A$(1,4),DOM=1210)P1$
1223 IF POS(P1$(99,1)="CR")<>0 THEN CALL "ZZPROM",".4",X3$,Z,"Closed Years must be reopened by System Administrator prior to posting.","","",0; GOTO 1210
1240 GOTO 1120
1250 REM "Accounting Period
1254 IF ABS(C0)=2 THEN A1$=""; GOTO 1120
1255 IF LEN(A1$)>4 THEN A$(5,2)=A1$(5,2); GOTO 1262
1260 CALL "ZZENTR","ZUX",A{ALL},A$,X4$,X3$,31,3+V0,5+K9,2,C0,"","{2"+X$,"","GL2JAA02","","",""
1262 IF ABS(C0)=2 AND LEN(A1$)>0 THEN A1$=A1$(1,0)
1264 IF A$(5+K9,2)<"01" OR A$(5+K9,2)>P1$(13,2) THEN GOTO 1260
1265 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1266,6800,9700,6970
1270 GOSUB 7700
1271 FLAG$=""
1273 IF C0<>0 THEN CALL "AR2DTE",X3$,X4$,A$(1+K9,6),FLAG$
1274 IF FLAG$="*" THEN GOTO 1250
1290 GOTO 1120
1300 REM "A/P Vendor Code
1304 IF F2$="*" THEN F2$="",A1$="",X0=0; GOTO 1140
1305 IF LEN(Q3$)>6 THEN A$(7,10)=Q3$(20,10); GOTO 1312
1308 X$="Press F10 to retrieve by Phone Number"
1310 CALL "ZZENTR","AUX",A{ALL},A$,X4$,X3$,13,4+V0,7+K9,10,C0,"A/P","{2"+X$,"","AP2DAA04","AP4","X",""; IF ABS(C0)=102 THEN J9=1,Q1$="",Z8$="AP2MAB"; RUN "ZZ2STP" ELSE IF C0<>-7 THEN IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1310,1311
1312 IF ABS(C0)=2 THEN IF LEN(A1$)>4 THEN A1$=A1$(1,4)
1313 IF C0=-7 THEN X$="AP",C0=1; CALL "ZZ2PLU",X3$,A$,7,X$; IF X$="" THEN GOTO 1308 ELSE CALL "ZZDISP","A",A$(7,10),"A/P",X3$,"","",13,4+V0,X4$
1315 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1316,6800,9700,6970
1320 FIND (Z[3],KEY=A$(7+K9,10),DOM=1308)IOL=0330
1321 IF POS(X3$(9,3)="144244",3)>0 THEN IF A$(7+K9,2)="00" THEN CALL "ZZPROM",".X0AP2DAA",X3$,Z0,"Division 00 vendors are reserved for franchise operations","","",0; IF Z0=0 THEN GOTO 1308 ELSE GOSUB 8800
1325 IF POR_GATEWAY$<>"Y" THEN PRINT @(31,4+V0),C$(11,35),
1327 IF C$(246,1)="I" THEN CALL "ZZPROM",".4",X3$,0,"This vendor is inactive, please use another vendor","","",0; GOTO 1308
1330 GOSUB 8700
1340 GOTO 1120
1350 REM "Invoice Number
1354 IF Q3$<>"" THEN A$(17+K9,10)=Q3$(10,10); GOSUB 6460
1355 Q$=A$(1,16)
1357 IF POR_GATEWAY$="Y" THEN GOTO 1120; REM "Bypass input
1360 CALL "ZZENTR","SUX",A{ALL},A$,X4$,X3$,13,5+V0,17+K9,10,C0,"","{2"+X$,Q$,"AP2DAA06","APD","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1360,1361
1361 IF POS("PO2MAG"=U1$) AND C0=-3.1 THEN C0=-1
1362 IF ABS(C0)=2 AND LEN(A1$)>6 THEN A1$=A1$(1,6)
1363 IF C0=-4 AND Q3$<>"" THEN GOTO 8930
1364 IF C0=-1 AND POS(" "<>A$(17+K9,10))<>0 THEN GOTO 1366
1365 IF C0=-1 THEN GOTO 1360 ELSE IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1366,6800,9700,6970
1390 GOTO 1120
1500 REM "
1505 Z9=0
1510 A1$=K9$+A$(LEN(K9$)+1,26),A$(1,LEN(K9$))=K9$
1515 IF Q3$<>"" THEN GOTO 1600; REM " Redefaulting distribution will orphan any previous lines distributed for an existing entry, but that's okay since we always process header first.
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1525 Z9=0,A0=A[0],A[11]=A[9],F9$="*"
1530 IF POR_GATEWAY$<>"Y" THEN GOSUB 6080
1540 GOSUB 8100
1590 GOTO 1700
1600 REM "
1610 GOSUB 8200; IF I1=1 THEN GOTO 1650
1615 IF Q3$<>"" THEN GOTO 1650
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630,6975
1630 IF Q3$<>"" THEN GOTO 9700 ELSE GOTO 1000
1650 REM "Set defaults
1655 IF POR_GATEWAY$<>"Y" THEN GOSUB 6080
1660 GOSUB 8000
1690 GOSUB 6200
1695 IF Q3$<>"" THEN GOTO 1800 ELSE GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1805 IF POR_GATEWAY$="Y" THEN GOTO 7000; REM "Bypass prompt
1820 CALL "ZZPROM",STR(6+C9),X3$,Z,"","","V",X0; ON Z+1 GOTO 2040,1830,6500,7000,7000
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310
1865 C9=1
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "Data
2010 L0=1; RUN "AP2DAD"
2040 L0=2; RUN "AP2DAD"
5900 REM "REMOVE LINES
5910 IF A0=0 THEN GOTO 5990
5920 EXTRACT (F,IND=0,ERR=5960)IOL=0290
5925 IF Y[4]=-2 THEN READ (F,IND=0); WAIT 0; GOTO 5920 ELSE Y[4]=-2; WRITE (F,IND=0)IOL=0290
5930 A1=Y[1],Y[1]=A0,Y[0]=Y[0]-1
5935 READ (F,IND=A0)A; IF A>0 THEN Y[0]=Y[0]-1,A0=A; IF POS(BIN(A,3)=Y5$,3)=0 THEN Y5$=Y5$+BIN(A,3); GOTO 5935
5940 WRITE (F,IND=A0)A1
5950 Y[4]=-1; WRITE (F,IND=0)IOL=0290
5955 GOTO 5990
5960 IF ERR=0 THEN RETRY ELSE GOTO 9000
5990 RETURN 
6000 REM "BACKGROUND
6002 IF POR_GATEWAY$="Y" THEN RETURN 
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(0,3+V0),"Fiscal Year:",@(23,3+V0),"Period:",@(0,4+V0),"Vendor Code:",@(25,4+V0),"Name:",@(4,5+V0),"Invoice:",@(25,5+V0),"Date:",@(47,5+V0),"Gross Amount:",@(0,6+V0),Z0$
6070 IF Z9<0 THEN GOTO 6160
6080 PRINT @(0,7+V0),'CE','SB',
6085 PRINT @(1,7+V0),"Terms Code:",@(48,7+V0),"Invoice Due:",@(2,8+V0),"Inv. Type:",@(16,8+V0),"Apply to:",@(40,8+V0),"Discount Applies to:",@(44,9+V0),"Discount Amount:",@(40,10+V0),"Discount Expires on:",@(42,11+V0),"",@(39,12+V0),"Amount to distribute:",
6086 IF F0$(80,1)="Y" THEN PRINT @(1,13+V0),"2nd Disc Amount:",@(40,13+V0),"2nd Disc Expires on:", ELSE PRINT @(0,13+V0),Z0$,; REM "WO131561
6090 PRINT @(10,14+V0),"Invoice Comment:",@(15,15+V0),"P/O Number:",; IF F0$(16,1)="Y" THEN PRINT @(1,17+V0),"Print check on bank code:",
6095 PRINT @(8,18+V0),"G/L Category Code:",@(3,19+V0),"Payment Selection Code:",@(38,19+V0),"Place Invoice on Hold?",@(4,20+V0),"Payment Priority Code:",@(34,20+V0),"# Days to extend due date:",
6120 FOR X=0 TO 3; IF POS(" "<>F1$(X*20+8,16))=0 THEN NEXT X ELSE PRINT @(0,9+X+V0),F1$(8+X*20,16),":",; NEXT X
6130 IF F0$(70,1)="Y" THEN PRINT @(54,14),"Salesperson:"
6135 IF X3$(69,1)="C" THEN PRINT @(40,14),"US$ Equiv Gross:",@(40,15),"US$ Discount:",
6165 PRINT (0,ERR=6066)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6205 IF POR_GATEWAY$="Y" THEN RETURN 
6215 GOSUB 6450
6220 CALL "ZZDISP","D",A$(27+K9,6),"",X3$,"","",31,5+V0,X4$
6222 PRINT @(61,5+V0),A[1]:M0$,@(13,7+V0),A$(33+K9,2),@(17,7+V0),D$(3,30),@(13,8+V0),A$(89+K9,1),@(26,8+V0),A$(90,10),
6224 FOR X=0 TO 3; IF POS(" "<>F1$(8+X*20,16))=0 THEN NEXT X ELSE PRINT @(19,9+X+V0),A[2+X]:M0$,; NEXT X
6226 IF F0$(80,1)="Y" THEN PRINT @(19,13+V0),A[14]:M0$,; CALL "ZZDISP","D",A$(111,6),"",X3$,"","",61,13+V0,X4$; REM "WO131561
6228 CALL "ZZDISP","D",A$(35+K9,6),"",X3$,"","",61,7+V0,X4$
6232 PRINT @(61,8+V0),A[6]:M0$,@(61,9+V0),A[7]:M0$,
6236 CALL "ZZDISP","D",A$(41+K9,6),"",X3$,"","",61,10+V0,X4$
6238 CALL "ZZDISP","A",A$(62+K9,10),"P/O",X3$,"","",27,15+V0,X4$
6240 PRINT @(61,12+V0),A[9]:M0$,@(27,14+V0),A$(47+K9,15),; IF F0$(16,1)="Y" THEN PRINT @(27,17+V0),A$(72+K9,3),@(31,17),E$(4,30),
6250 CALL "ZZDISP","A",A$(75+K9,9),"AP2",X3$,"","",27,18+V0,X4$
6260 PRINT @(38,18),G$(10,30),@(27,19+V0),A$(84+K9,2),@(27,20+V0),A$(86+K9,1),@(61,19+V0),A$(87+K9,1),@(61,20+V0),A[10]:"00",
6270 IF F0$(70,1)="Y" THEN PRINT @(67,14),A$(100,4),
6275 CALL "AP2DAF",X3$,X4$,"D",A$,A{ALL}
6390 RETURN 
6400 REM "WHOLE SCREEN
6401 IF POR_GATEWAY$="Y" THEN RETURN 
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6410 GOSUB 6000
6430 IF Z9>=0 THEN GOSUB 6200 ELSE GOSUB 6450; GOSUB 8700
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6453 IF POR_GATEWAY$="Y" THEN RETURN 
6455 IF A$="" THEN GOTO 6490
6460 IF POR_GATEWAY$="Y" THEN RETURN ELSE PRINT @(13,3+V0),A$(1+K9,4),@(31,3+V0),A$(5+K9,2),
6464 CALL "ZZDISP","A",A$(7+K9,10),"A/P",X3$,"","",13,4+V0,X4$
6466 PRINT @(31,4+V0),C$(11,35),@(13,5+V0),A$(17+K9,10),
6470 GOSUB 7700
6490 RETURN 
6500 REM "DELETE
6505 GOSUB 5900
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6520 Z9=2
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6605 FIND (Z[3],KEY=A$(7+K9,10),DOM=6606)IOL=0330
6610 RETURN 
6630 FIND (Z[4],KEY=A$(33+K9,2),DOM=6631)IOL=0340
6635 FIND (Z[5],KEY=A$(72+K9,3),DOM=6636)IOL=0350
6640 FIND (Z[7],KEY=A$(75+K9,9),DOM=6641)IOL=0370
6690 RETURN 
6700 REM "ADD ENTRY
6705 A[0]=A0
6710 IF X3$(330,1)="1" THEN IF A$(62+K9,10)=S$(1,10) THEN CALL "ZZPROM",".4",X3$,0,"You must specify a P/O number","","",0; X0=16,C0=0; GOTO 2040
6730 WRITE (Z[1],KEY=A1$)IOL=0310
6740 IF POR_GATEWAY$="Y" THEN GOTO 9700
6780 ON Z9 GOTO 7000,6781
6785 C9=-1,V0=0
6788 IF Q3$="" THEN CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,0,0,0
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6815 GOSUB 6600
6820 A$(1,LEN(A1$))=A1$
6825 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM "PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
7000 REM "RUN OVERLAY
7001 IF X3$(330,1)="1" THEN IF A$(62,10)=S$(1,10) THEN CALL "ZZPROM",".4",X3$,0,"You must specify a P/O number","","",0; X0=16,C0=0; GOTO 2040
7005 L0=1,F=Z[2],A0=A[0]
7010 V9=-3,V3=Z[13],V4=Z[9],V5=Z[10],V9$=A$(27,6)
7015 T[0]=-(A[9]-A[11])
7016 T0=T[0]
7018 IF POS(" "<>T9$)=0 THEN T9$="" ELSE IF Q3$<>"" AND Q3$(30,1)<>"D" THEN T[0]=T6+T6+T[0] ELSE T[1]=-T[0],T[0]=T[1]
7019 IF POR_GATEWAY$="Y" AND A[1]<>0 THEN CALL "GL2XAA"; GOTO 6700; REM SSP 212642 !SSP#219897 - Needs to be <> 0 so credits function correctly
7020 IF POR_GATEWAY$="Y" AND A[1]=0 THEN GOTO 6700; REM SSP 212642
7024 IF POR_GATEWAY$<>"Y" THEN RUN "GL2XAA"; REM SSP 212642
7095 RETURN 
7100 REM "RE DO SCREEN
7102 IF POR_GATEWAY$<>"Y" THEN CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,0,0,0
7105 V0=0
7120 IF POR_GATEWAY$<>"Y" THEN PRINT 'SB',@(0,3),"Fiscal Year:",@(23,3),"Period:",@(0,4),"Vendor Code:",@(25,4),"Name:",@(4,5),"Invoice:",@(25,5),"Date:",@(47,5),"Gross Amount:",@(0,6),Z0$,'SF',
7130 GOSUB 6450
7190 GOTO 7020
7200 REM "Get amount if P/O receipt
7205 REM "IF Q3$(54,1)="D" THEN GOTO 07240
7210 IF Q3$<>"" THEN LINE_FREIGHT=0; CALL "AP2DAE",X3$,A$,A{ALL},T9$,T{ALL},Q3$,G8,LINE_FREIGHT
7230 T6=0,A0=0
7235 FOR I=1 TO T5; T6=T6+T[I]; NEXT I
7240 RETURN 
7400 REM "AUDIT
7410 FIND (Z[1],KEY="",DOM=7490)V0$,K0$
7420 IF K0$>"" THEN CALL "ZZPROM","4",X3$,S3,"The update has already been initiated.","","",0; GOTO 9900
7430 EXTRACT (Z[1],KEY="",ERR=9800)
7440 Q$="Audit report "+V0$(22,4)+"."+V0$(26,2)+" has already been printed for this data entry."
7445 CALL "ZZPROM","C",X3$,S3,Q$,"","",0; ON S3 GOTO 9800,7446
7450 REMOVE (Z[1],KEY="")
7490 RETURN 
7605 IF LEN(Q3$)>36 THEN A1$=Q3$(36,17); GOTO 7620
7610 A1$=F0$(7,6)
7620 FIND (Z[6],DOM=8900,KEY=X3$(9,3)+"G/L"+"YE"+A1$(1,4))P1$
7699 RETURN 
7700 REM "PRINT P/E DATE"
7720 IF POR_GATEWAY$<>"Y" THEN PRINT @(34,3+V0),"Ending ",FND$(P1$(NUM(A$(5+K9,2))*6+15,6)),
7749 RETURN 
7750 IF POR_GATEWAY$<>"Y" THEN PRINT @(55,11+V0),"Ending ",FND$(P9$(NUM(A$(19+K9,2))*6+15,6)),; REM "NOT USED??
7799 RETURN 
7800 REM "GET G/L Z MASK
7801 IF POR_GATEWAY$="Y" THEN RETURN 
7810 CALL "ZZDISP","AX","000000000000","G/L",X3$,W2$,"",0,0,X4$
7815 M2=0; FOR X=1 TO LEN(W2$); M2=M2+POS("0"=W2$(X,1)); NEXT X
7820 CALL "ZZDISP","AX","000000000000","J/C",X3$,W3$,"",0,0,X4$
7825 M3=0; FOR X=1 TO LEN(W3$); M3=M3+POS("0"=W3$(X,1)); NEXT X
7830 CALL "ZZDISP","AX","000000000000","JCC",X3$,W4$,"",0,0,X4$
7835 M4=0; FOR X=1 TO LEN(W4$); M4=M4+POS("0"=W4$(X,1)); NEXT X
7840 RETURN 
7850 REM "CALC DISC AMT
7880 A[7]=A[6]*D[0]/100
7882 IF F0$(80,1)="Y" AND D$(43,1)="P" AND D[1]<>0 THEN A[14]=A[6]*D[1]/100; REM "WO131561, 2nd discount amount
7885 IF POR_GATEWAY$<>"Y" THEN PRINT @(61,8+V0),A[6]:M0$,@(61,9+V0),A[7]:M0$,
7887 IF POR_GATEWAY$="Y" AND %DISC_AMT<>0 THEN A[7]=%DISC_AMT
7890 RETURN 
7900 REM "CALCULATE DISCOUNT APPLIES TO
7910 A[6]=A[1]
7915 FOR X=0 TO 3; IF POS(STR(X+1:"0")=C$(294,5))=0 AND F1$(25+20*X,1)<>"Y" THEN A[6]=A[6]-A[2+X]
7918 NEXT X
7920 IF F0$(79,1)="Y" AND POS("1"=C$(294,5))=0 AND F1$(25,1)<>"Y" THEN A[6]=A[6]-LINE_FREIGHT; REM "SSP118375, exclude freight on lines from discount
7935 GOSUB 7850
7950 REM "CALC NET INV AMT
7955 A[9]=A[1]
7960 FOR X=0 TO 3; IF F1$(24+X*20,1)="Y" THEN A[9]=A[9]-A[2+X]; NEXT X ELSE NEXT X
7965 A[9]=A[9]-A[8]
7970 IF F0$(15,1)="L" THEN A[9]=A[9]-A[7]
7971 IF POR_GATEWAY$="Y" AND %AMT_SUB_DISC<>0 THEN A[6]=%AMT_SUB_DIS
7973 IF POR_GATEWAY$="Y" AND %NET<>0 THEN A[9]=%NET
7985 IF POR_GATEWAY$<>"Y" THEN PRINT @(61,12+V0),A[9]:M0$,
7990 RETURN 
8000 REM "DEFAULTS
8001 REM "IF Q3$(54,1)="D" THEN GOTO 08190
8005 A$(89,1)="I"
8010 A$(27,6)=X3$(15,6),A$(33,2)=C$(286,2),A$(72,3)=C$(283,3),A$(47,15)=C$(256,15),A$(75,9)=C$(247,9),A$(84,4)=C$(288,4)
8011 IF LEN(Q3$)>30 THEN A$(27,6)=Q3$(30,6)
8012 T9$=C$(271,12),A$(87,1)="N"
8015 IF I1=0 THEN A[1]=C[0],A[10]=C[1]; GOTO 8100
8025 REM "Existing invoice defaults
8040 A$(27,63)=K$(21,62)+"A"
8100 REM 
8110 IF LEN(Q3$)>54 THEN A$(33,2)=Q3$(54,2)
8160 GOSUB 6630
8162 IF POS(" "<>A$(108,3))=0 THEN A$(108,3)=E$(105,3)
8165 GOSUB 7200
8170 IF F9$<>"*" THEN GOSUB 8300
8185 IF F9$<>"*" THEN GOSUB 7900
8186 IF POR_GATEWAY$="Y" THEN GOSUB 8300; GOSUB 7900
8187 IF F0$(71,1)="Y" AND F9$<>"*" THEN A[1]=A[1]-A[3],A[3]=(A[1]-A[7])*NUM(F0$(72,4))*.0001,A[1]=A[1]+A[3]
8190 RETURN 
8200 REM "
8204 IF POR_GATEWAY$="Y" THEN GOTO 8240; REM "if in gateway , don't ask just assume yes
8205 I1=0; FIND (Z[11],KEY=A1$(7),DOM=8206)IOL=0410; I1=1
8206 IF I1=0 THEN FIND (Z[8],KEY=A1$(7),DOM=8240)IOL=0410; I1=2
8210 IF I1=1 THEN CALL "ZZPROM","Y",X3$,S3,"This invoice "+A$(17,10)+" is currently open. Continue? (Yes, No):","","",0 ! SSP#214981
8211 IF I1=2 THEN CALL "ZZPROM","Y",X3$,S3,"This invoice "+A$(17,10)+" is currently in history. Continue? (Yes, No):","","",0 ! SSP#214981
8215 IF S3=1 THEN EXITTO 1000
8240 RETURN 
8300 REM "CALC DUE DATE
8305 REM "EXTRA DAYS ADDED TO DUE DATE AT PRINT TIME
8310 CALL "ZZDISC",X3$,D$,0,0,A$(27+K9,6),Q4$,Q2$,0,0
8370 A$(35+K9,6)=Q4$,A$(41,6)=Q2$
8372 IF F0$(80,1)="Y" AND D$(43,1)="P" AND D$(53,3)<>"   " THEN ORIGINAL_D$=D$,D$(44,3)=D$(53,3),Q2$=""; CALL "ZZDISC",X3$,D$,0,0,A$(27+K9,6),Q4$,Q2$,0,0; A$(111,6)=Q2$,D$=ORIGINAL_D$; REM "WO131561, 2nd discount expires on date
8375 IF POR_GATEWAY$<>"Y" THEN PRINT @(61,7+V0),FND$(A$(35+K9,6)),@(61,10+V0),FND$(A$(41+K9,6)),
8395 RETURN 
8500 REM "Return from set-up
8510 IF Q1$="" OR J9=0 THEN GOSUB 6400
8520 J9=0; C0=0
8530 IF Q1$>"" THEN A$(7,10)=Q1$; GOSUB 6400; GOTO 1311 ELSE GOTO 1300
8700 REM "Vendor address & message
8701 IF POR_GATEWAY$="Y" THEN RETURN 
8710 PRINT @(0,7),'CE',@(31,7),C$(46,30),@(31,8),C$(76,30),@(31,9),C$(106,16)," ",C$(122,2),
8720 CALL "ZZDISP","A",C$(124,9),"ZIP",X3$,"","",52,9,X4$
8760 IF F0$(67,1)<>"Y" THEN GOTO 8790
8770 FIND (Z[12],KEY=A$(7+K9,10),DOM=8790)IOL=0420
8780 IF POS(" "<>L$(1,120))>0 THEN PRINT @(7,13),'RB',"NOTE: ",@(14),L$(11,60),@(14,14),L$(71,60),
8790 IF C$>"" THEN IF C$(291,1)="Y" THEN PRINT @(7,16),'RB',"WARNING: Vendor is on payment hold!",
8795 RETURN 
8800 REM " Enter source company code for R/Ls and get proper remote location type
8805 DIM J$(3),J1$(50)
8810 PRINT @(0,7+V0),'CE',@(0,10),'SB',@(0,10),"Source company code:",'SF',
8815 CALL "ZZENTR","ZU",A{ALL},J$,X4$,X3$,21,10,1,3,C0,"","","R/L","AP2DAA40","ZZ2CAB","",""; IF ABS(C0)>4 THEN GOSUB 6400; PRINT @(0,7+V0),'CE',@(0,10),'SB',@(0,10),"Source company code:",'SF',@(21,10),J$,@(25,10),J1$(7,40),; ON C0 GOTO 8815,8816
8820 READ (Z[6],KEY="R/L"+J$,DOM=8815)J0$
8825 READ (Z[6],KEY="CMP"+J$,DOM=8815)J1$
8830 PRINT @(25,10),J1$(7,40),
8835 CALL "ZZPROM","10",X3$,Z0,"","","",0; IF Z0=0 THEN GOTO 8815
8840 A$(104,3)=J$,A$(107,1)=J0$(10,1)
8845 RETURN 
8900 REM "
8910 CALL "ZZPROM","4",X3$,S3,"The new fiscal year is not setup (G/L SM T2).","","",0; GOTO 9900
8930 REM "Verify skip of A/P entry from P/O receipt
8935 IF LEN(Q3$)<30 THEN GOTO 9700 ELSE IF Q3$(30,1)="D" THEN GOTO 9700
8940 CALL "ZZPROM",".Y",X3$,S3,"|Verify that you wish to bypass entry of this A/P invoice","","",0
8945 ON S3 GOTO 9700,1360
8950 REM "Program flow logic
8960 DIM U0$(6); U0$(1)=FID(0),U0$=U0$+"PF"
8970 CLOSE (14); OPEN (14)"ZZPARM"; FIND (14,KEY=U0$,DOM=8990)U0$,U1$
8975 IF U1$(1,6)<>X0$ THEN Q3$=""; GOTO 8990
8980 Q1=NUM(U1$(7,2)),Q3$=U1$(9,Q1),U1$=U1$(Q1+9)
8990 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9710 IF U1$="" THEN REMOVE (Z[6],KEY=U0$,DOM=9900); GOTO 9900
9715 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9720 WRITE (Z[6],KEY=U0$)U0$,U1$
9725 L0=0; PRINT @(0,3),'CE',
9730 RUN U1$(1,6)
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "214981-AP-Invoice entry. This prompts you that an invoice is open 
56001 REM "212642-Importing gateway and got an error    
56002 REM "219897-DT2 - PE(028, AP2DAA-7095) in DDMS processing.              
