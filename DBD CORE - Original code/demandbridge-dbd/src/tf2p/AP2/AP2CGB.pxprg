0010 REM "A/P Check printing for Data Products <AP2CGB>
0020 SETESC 9300; SETERR 9000
0035 REM "5.5 - 12/08/06 - 16.828888 - lms - SSP# 197028
0036 REM "10/15/91 ADDED LINE 8057 FOR DOCUTEC
0037 REM "197028-Create A/P check format to match his preprinted form.       
0040 REM "Copyright 2006 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 BEGIN ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0102 CALL "ZZDAPP",X3$,"DZZFLESZZCOMP"
0110 X0$="AP2CGB",X1$="A/P Check Printing"
0120 DIM Z0$(80,"-"),A[2],Z$(10,"0")
0134 REM "O is the shift for second half of invoices
0135 C9=-1,V9=-2,O=41
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0180 DEF FNP(Z0,Z1)=(Z1-1)*W0+Z0
0185 DEF FNS$(Z9$)=Z9$(1,POS("  "=Z9$+"  ")-1)
0190 DEF FNJ$(Z9$)=Z9$(POS(" "<>Z9$))
0200 REM "
0235 X3$=""
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 K9$="",K9=LEN(K9$)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2]
0320 IOLIST B$,B[0],B[1],B[2]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14]
0350 IOLIST E$,E[0],E[1],E[2]
0360 IOLIST Y$,Y[0],Y[1],Y[2],Y[3],Y[4],Y[5],Y[6],Y[7],Y[8],Y[9],Y[10],Y[11],Y[12],Y[13],Y[14],Y[15],Y[16],Y[17],Y[18],Y[19],Y[20],Y[21],Y[22],Y[23],Y[24],Y[25],Y[26],Y[27],Y[28],Y[29],Y[30],Y[31],Y[32],Y[33],Y[34],Y[35],Y[36],Y[37],Y[38]
0370 IOLIST G$
0380 IOLIST H$,H[0],H[1],H[2]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01L APH...  02O AP4...  06O ZZPARM  07O ZYB...  08O APM...  "
0512 Z$=Z$+"03O API...  04O APK...  05L APC...  09L APG...  10O ZY2     "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0575 READ (Z[6],KEY=X3$(9,3)+"A/P")F0$
0580 DIM Y[40]; READ (Z[6],KEY="A/PCKFC",DOM=0581)IOL=0360; GOTO 0590
0585 READ (Z[6],KEY="A/PCKFS")IOL=0360
0590 A$=F0$(7,6); GOSUB 7600
0600 REM "
0620 GOSUB 6000
0700 REM "Resort?
0705 F0=Z[1],F1=Z[9]
0710 READ (Z[9],KEY="",DOM=0711); GOTO 0720
0715 CALL "AP2CDB",X3$,F0,F1,0,0
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(227),B[2]
1035 DIM A$(53); A$(1,6)=P5$
1045 PRINT 'CF',
1100 REM "KEY SEQ
1110 X0=1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1500
1200 REM "
1215 IF C0<0 THEN GOTO 9900
1500 REM "
1505 Z9=-1
1510 A1$=X3$(9,3)+"APC"+X3$(174,4),A$(22,10)=Z$
1520 READ (Z[6],KEY=A1$,DOM=1600)A$,R0$
1522 IF A$(47,6)="      " THEN GOTO 1600
1525 Z9=0,R0=1
1526 IF A$(47,6)="ZZZZZZ" THEN GOTO 9600 ELSE A$(13,6)=STR(NUM(A$(47,6))+1:"000000")
1530 FIND (Z[7],KEY=A$(22+K9,3),DOM=1531)IOL=0370
1535 FIND (Z[2],KEY=A$(25+K9,10),DOM=1536)IOL=0320
1550 GOSUB 6000; GOSUB 6200
1590 GOTO 1700
1600 REM 
1605 A$(1,6)=P5$
1610 GOSUB 6200
1660 GOTO 2000
1700 REM "
1800 REM "
1820 CALL "ZZPROM","X0AP2CGA",X3$,S3,"Ready to proceed with check printing","","",0
1825 ON S3 GOTO 2800,9900,2000,7550
1830 GOTO 2800
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF C0>3 OR X0<0 THEN GOTO 1800
2035 IF X0*6+11<LEN(V9$) THEN IF V9$(X0*6+11,1)="2" THEN GOTO 2020
2040 ON X0 GOTO 2100,2150,2200,2250,2300,2350,2400,2450,1800
2100 REM "Fiscal Year for G/L update
2110 CALL "ZZENTR","ZX",A{ALL},A$,X4$,X3$,38,3+V0,1+K9,4,C0,"",X$,K9$,"AP2XXX00","","",""
2130 FIND (Z[6],DOM=2110,KEY=P0$(1,6)+"YE"+A$(1,4))P1$; GOSUB 7720
2140 GOTO 2020
2150 REM "Acctg Period
2160 CALL "ZZENTR","ZX",A{ALL},A$,X4$,X3$,64,3+V0,5+K9,2,C0,"",X$,"","AP2XXX02","","",""
2165 IF A$(5+K9,2)<"01" OR A$(5+K9,2)>P1$(13,2) THEN GOTO 2150
2170 IF C0>0 THEN GOSUB 7700
2190 GOTO 2020
2200 REM "Check Date
2210 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,38,5+V0,7+K9,6,C0,"",X$,"","AP2XXX04","","",""
2240 GOTO 2020
2250 REM "Starting check number
2260 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,38,7+V0,13+K9,6,C0,"",X$,"","AP2XXX06","","",""
2290 GOTO 2020
2300 REM "Restart in Source Company
2305 IF R0=0 THEN GOTO 2020
2310 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,38,10+V0,19+K9,3,C0,"",X$,"","AP2XXX08","","",""
2340 GOTO 2020
2350 REM "Restart bank code
2355 IF R0=0 THEN GOTO 2020
2360 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,38,12+V0,22+K9,3,C0,"",X$,"","AP2XXX10","ZYB","ZY2BAA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2360,2361
2370 FIND (Z[7],KEY=A$(22+K9,3),DOM=2360)IOL=0370
2375 PRINT @(43+V0+L0,12+V0+L0),G$(4,30),
2390 GOTO 2020
2400 REM "Vendor Code
2405 IF R0=0 THEN GOTO 2020
2410 CALL "ZZENTR","A",A{ALL},A$,X4$,X3$,38,14+V0,25+K9,10,C0,"A/P",X$,"","AP2XXX12","AP4","AP2MAA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2410,2411
2420 FIND (Z[2],KEY=A$(25+K9,10),DOM=2410)IOL=0320
2425 PRINT @(38+V0+L0,16+V0+L0),B$(11,35),
2440 GOTO 2020
2450 REM "Check Code
2455 IF R0=0 THEN GOTO 2020
2460 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,62,14+V0,35+K9,1,C0,"",X$,"","AP2XXX14","","",""
2490 GOTO 2020
2800 REM "Start Check Printing
2805 IF R0$="" THEN GOSUB 7500
2810 CALL "ZZPRIN",X0$,X3$,Y0$,R0$,Z{ALL},H0$,H1$,H0,H1,H2,H3,H4; ON H4 GOTO 2811,9900
2815 REM "IF X3$(9,3)<>"008" THEN PRINT (H1) "          "
2905 V$="Void  Void  Void **",P0=0
2910 C0$=A$
2912 IF R0>0 THEN EXTRACT (Z[1],KEY=C0$(19,17),DOM=2913)
2915 C0=NUM(C0$(13,6)),T$="TEST"
2920 IF G$="" THEN Q$=KEY(Z[1],END=9800); Q$=Q$(1,6); GOSUB 7900
2925 DIM E$(30),E[2]
2930 IF R0$(2,2)="A " THEN PRINT 'EA',
2940 CALL "ZZPROM","YE",X3$,S3,"Do you wish to print a test pattern?","","",0; ON S3 GOTO 2941,3000,9900
2942 IF S3<2 AND R0$(2,2)="A " THEN PRINT 'BA',
2945 DIM B$(300,"X"),C$(140,"X"),C[20]
2946 C$(21,6)="I60000",P0=0,P1=1
2950 M0$="*###,###.00-",M1$="*#,###.00-",M2$="*##,###.00-"
2955 GOSUB 8000
2965 FOR X=1 TO Y[2]*2; I9=X; GOSUB 3200; NEXT X; GOSUB 3900
2970 GOSUB 8700; GOSUB 8800; GOSUB 8900
2975 C0=C0+1
2976 IF FID(H1)<>FID(0) THEN PRINTER$=FID(H1); CLOSE (H1); OPEN (H1)PRINTER$
2990 GOTO 2930
3000 REM "
3002 IF R0$(2,2)="A " THEN PRINT 'BA',
3005 M0$="####,###.00-",M1$="##,###.00-",T$="CHECK",P0=0,P1=1,I9=0,U9$="",M2$="##,###.00-"
3007 GOSUB 6700
3010 DIM T[10]
3040 U$=KEY(Z[1],END=5000); IF G$>"" THEN IF G$(1,3)<>U$(4,3) THEN GOSUB 4000; Q$=U$,C0$(19)=U$,C0$(47,6)=""; GOSUB 8455; GOSUB 7900; GOTO 0800
3042 IF U$(1,LEN(U9$))<>U9$ THEN GOSUB 4000
3045 READ (Z[1],KEY=U$)IOL=0310; U8$=A$(1,17)
3050 IF P0=Y[2] THEN GOSUB 3800 ELSE IF U9$="" THEN GOSUB 8000
3070 DIM C[20]; READ (Z[3],KEY=A$(7,10)+A$(18,10))IOL=0330
3100 REM "Next Invoice
3110 I9=I9+1
3120 GOSUB 3200
3180 GOTO 3040
3200 REM "LOAD LINE VALUES
3210 IF MOD(I9,2)=1 THEN Y5=FNP(0,Y[1]+P0) ELSE Y5=FNP(0+O,Y[1]+P0)
3215 Q$=C$(11,10); IF Y[12]>0 THEN Y3$(Y5+Y[12]+9-LEN(Q$),LEN(Q$))=Q$
3220 IF Y[13]>0 THEN Y3$(Y5+Y[13],10)=FND$(C$(21,6))
3225 IF Y[14]>0 THEN Y3$(Y5+Y[14]+1,LEN(M0$))=STR(A[0]:M2$)
3230 IF Y[15]>0 THEN Y3$(Y5+Y[15],LEN(M1$))=STR(A[1]:M1$)
3235 IF Y[16]>0 THEN Y3$(Y5+Y[16]+1,LEN(M0$))=STR(A[0]-A[1]:M2$)
3240 IF Y[17]>0 THEN Y3$(Y5+Y[17],15)=C$(41,15)
3245 IF Y[18]>0 THEN Y3$(Y5+Y[18],10)=C$(56,10)
3400 REM "Totals
3404 REM "For Data Products, only increment on even invoices
3405 T[0]=T[0]+1; IF MOD(I9,2)=0 THEN P0=P0+1
3410 T[1]=T[1]+A[0],T[2]=T[2]+A[1]
3415 T[3]=T[3]+A[0]-A[1]
3490 RETURN 
3700 REM "
3780 GOTO 3100
3800 REM "CONTINUATION
3815 Q$="PAGE NO. "+STR(P1:"###"); Y3$(FNP(Y[15],Y[3]),LEN(Q$))=Q$
3820 REM LET Y3$(FNP(Y(16),Y(3)),13)="* Continued *"
3825 P1=P1+1
3830 GOSUB 8700; GOSUB 8800
3860 GOSUB 8900
3875 P0=0,Y3$(344)=""
3890 GOSUB 8400
3892 Q$=FNS$(Y$(55,16))+": "+B$(11,35); IF Y[8]>0 THEN IF Y$(38,1)="Y" THEN Y3$(FNP(Y[8],Y[9]),LEN(Q$))=Q$ ELSE Y3$(FNP(Y[8],Y[9]),35)=B$(11,35)
3895 IF POS(X3$(9,3)="020",3)<>0 THEN Y3$(FNP(Y[10],Y[11])-18,10)=""
3899 RETURN 
3900 REM "Set totals
3904 REM "SHIFT OVER BY 40 FOR DATA PRODUCTS
3905 IF Y$(38,1)="Y" THEN Y3$(FNP(Y[12]+O,Y[3]),10)="Totals:"
3910 Q$=STR(T[1]:M2$); IF Y[14]>0 THEN Y3$(FNP(Y[14]+O+1,Y[3]),LEN(Q$))=Q$
3920 Q$=STR(T[2]:M1$); IF Y[15]>0 THEN Y3$(FNP(Y[15]+O,Y[3]),LEN(Q$))=Q$
3930 Q$=STR(T[3]:M2$); IF Y[16]>0 THEN Y3$(FNP(Y[16]+O+1,Y[3]),LEN(Q$))=Q$
3940 IF P1>1 OR T$="TEST" THEN Q$="PAGE NO. "+STR(P1:"###"); Y3$(FNP(Y[15],Y[3]),LEN(Q$))=Q$
3990 RETURN 
4000 REM "PRINT CHECK
4010 P9$=B$(11,35)
4020 IF U9$<>"" THEN GOSUB 3900; GOSUB 8700; GOSUB 8900 ELSE GOTO 4085
4025 GOSUB 8400
4085 U9$="",P1=1,P0=0,I9=0
4095 RETURN 
4100 REM "UK style 'Pay exactly'
4110 DIM Q$(50)
4114 REM "SORRY, but must print checks >=100,000 with digits for 10's of thousands rather than spelled out
4115 IF EPT(T[3])>5 THEN Q$(1,10)=STR(INT(T[3]/10000)),Q2$=STR(INT(T[3])-INT(T[3]/10000)*10000:"00000") ELSE Q2$=STR(INT(T[3]):"00000")
4130 FOR I=1 TO 5
4135 IF I=1 AND EPT(T[3])>5 THEN GOTO 4160
4140 READ (Z[10],KEY=" 0"+Q2$(I,1))Q3$
4150 Q$((I-1)*10+1)=Q3$(4,10)
4160 NEXT I
4165 IF POS(X3$(9,3)="500476")>0 THEN NEW_Q$=Q$(1,10)+DIM(3)+Q$(11,10)+DIM(3)+Q$(21,10)+DIM(3)+Q$(31,10)+DIM(3)+Q$(41,10)+DIM(3); Q$=NEW_Q$; REM SSP 197028
4170 Y3$(FNP(Y[24],Y[34]),LEN(Q$))=Q$
4190 RETURN 
5000 REM "END
5020 GOSUB 4000
5025 IF R0$(2,2)="A " THEN PRINT 'EA',
5030 IF FID(H1)<>FID(0) THEN PRINTER$=FID(H1); CLOSE (H1)
5050 CALL "ZZPROM","Y",X3$,S3,"Did the checks print correctly?","","",0
5060 ON S3 GOTO 5070,0010
5100 REM "WRITE DONE RECORD
5120 C0$(53,1)="3"
5130 WRITE (Z[6],KEY=A1$)C0$
5150 CALL "ZZPROM","5",X3$,S3,"Please load stock paper into printer and print Cash Disbursements Journal","","",0
5240 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(0,3+V0),"Update General Ledger in Fiscal Year:",@(45,3+V0),"Accounting period:",@(26,5+V0),"Check Date:",@(15,7+V0),"Starting check number:",@(0,9+V0),Z0$,
6065 IF R0=0 THEN GOTO 6190
6070 PRINT @(11,10+V0),"Restart in Source Company:",@(16,12+V0),"Restart in bank code:",@(25,14+V0),"Vendor Code:",@(50,14+V0),"Check Code:",@(25,16+V0),"Vendor name:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 PRINT @(38,3+V0),A$(1+K9,4),@(64,3+V0),A$(5+K9,2),
6224 CALL "ZZDISP","D",A$(7+K9,6),"",X3$,"","",38,5+V0,X4$
6228 PRINT @(38,7+V0),A$(13+K9,6),; IF R0=0 THEN GOTO 6290 ELSE PRINT @(38,10+V0),A$(19+K9,3),@(38,12+V0),A$(22+K9,3),@(43+V0+L0,12+V0+L0),G$(4,30),
6232 CALL "ZZDISP","A",A$(25+K9,10),"A/P",X3$,"","",38,14+V0,X4$
6236 PRINT @(38+V0+L0,16+V0+L0),B$(11,35),@(62,14+V0),A$(35+K9,1),
6290 GOSUB 7700
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6490 RETURN 
6500 REM "DELETE
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY
6705 A$(53,1)="2"
6707 IF R0$="" THEN GOSUB 7500
6710 WRITE (Z[6],KEY=A1$)A$,R0$
6790 RETURN 
6800 REM "NEXT ENTRY IN FILE
6810 A1$=KEY(Z[1],END=1140)
6820 A$(1,LEN(A1$))=A1$
6825 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 1140
6830 GOSUB 6600
6835 GOSUB 6450
6890 GOTO 1515
6900 REM "GET DEFAULTS
6902 V9$="1403  3403  1404  3404  6204  "
6910 IF V9>=0 THEN READ (Z[6],IND=V9)V9$,IOL=0310; GOTO 6930 ELSE V9=-1
6920 EXTRACT (Z[6],KEY=X0$+"DFLT",DOM=6921)V9$,IOL=0310; V9=IND(Z[6]); READ (Z[6])
6930 RETURN 
6950 REM "WRITE DEFAULT
6955 WRITE (Z[6],KEY=X0$+"DFLT")V9$,IOL=0310
6960 GOSUB 6920; GOTO 1630
6970 REM "ENTRY DEFAULTS
6975 PRINT 'CF'; V9=-2,Q$=""
6980 FOR X=1 TO LEN(V9$) STEP 6
6982 PRINT @(NUM(V9$(X,2)),NUM(V9$(X+2,2))),; IF V9=-2 THEN PRINT V9$(X+4,1), ELSE READ RECORD (0,SIZ=1)Q$; IF POS(Q$="1234Z")=0 THEN PRINT 'RB'; GOTO 6982
6990 IF Q$="Z" THEN EXITTO 6950 ELSE V9$(X+4,LEN(Q$))=Q$; NEXT X
6995 V9=0; GOTO 6980
7400 REM "Audit already run
7410 FIND (Z[1],KEY="",DOM=7490)V0$,K0$
7420 IF K0$>"" THEN CALL "ZZPROM","4",X3$,S3,"The update has already been initiated. Press |Return>","","",0; GOTO 9900
7430 EXTRACT (Z[1],KEY="",ERR=9800)
7440 Q$="Audit report "+V0$(22,4)+"."+V0$(26,2)+" has already been printed for this data entry."
7445 PRINT @(6,22),Q$,; CALL "ZZPROM","1",X3$,S3,"","","",0; ON S3 GOTO 9800,7446
7450 REMOVE (Z[1],KEY="")
7490 RETURN 
7500 REM "Get printer options
7510 IF LEN(R0$)<30 THEN DIM R0$(30); R0$(1,3)="ILP",R0$(8,1)="N",R0$(8,9)="NNN    66"; GOSUB 6700
7520 CALL "ZZRPTO",X3$,X4$,Y0$,R0$,R{ALL},4,C0,X0
7525 GOSUB 6700
7530 PRINT @(0,3),'CE',; GOSUB 6000; GOSUB 6200
7540 RETURN 
7550 REM "input the output options
7560 GOSUB 7500
7590 GOTO 1800
7610 FIND (Z[6],DOM=7611,KEY=X3$(9,3)+"G/L")P0$
7620 FIND (Z[6],DOM=7621,KEY=P0$(1,6)+"YE"+A$(1,4))P1$
7630 GOTO 7730
7699 RETURN 
7700 REM "PRINT P/E DATE"
7720 PRINT @(69,3+V0),FND$(P1$(NUM(A$(5+K9,2))*6+15,6)),
7730 IF P5$="" THEN P5$=F0$(7,6) ELSE P5$=A$(1,6)
7740 RETURN 
7800 REM "UPDATE MONTHLY DISB W/ VOID CHECK C0
7810 DIM H$(88),H[2]
7820 H$(1,6)=C0$(1,6),H$(7,3)=G$(1,3),H$(10,6)=STR(C0:"000000"),H$(16,2)="00",H$(18,1)="V",H$(19,6)=C0$(7,6),H$(35,35)=V$
7825 H$(70,6)="000000"
7840 WRITE (Z[8],KEY=H$(1,17),DOM=7860)IOL=0380
7890 RETURN 
7900 REM "BANK CODE CHANGE
7910 DIM G0$(36),G$(100); G$(1,3)=Q$(4,3)
7915 Q$=Q$(4,3)
7920 READ (Z[7],KEY=G$(1,3),DOM=7921)IOL=0370
7925 READ (Z[6],KEY="FORM"+G$(38,1),DOM=7926)G0$
7930 Q$=FNS$(G0$(6,30))
7935 IF R0$(2,2)="A " THEN PRINT 'EA',
7940 CALL "ZZPROM","5",X3$,S3,"Please load the "+Q$,"","",0
7945 IF R0$(2,2)="A " THEN PRINT 'BA',
7990 IF S3=1 THEN EXITTO 9800 ELSE RETURN 
8000 REM "BUILD FORM
8005 REM "W0 IS WIDTH OF FORM, W1=LENGTH
8010 W0=85,W1=Y[0],P0=0; REM "W0 WAS 80
8015 IF U$>"" THEN U9$=U$(1,17)
8020 DIM Y3$(W0*W1),T[10]
8025 IF T$<>"TEST" THEN FIND (Z[2],KEY=A$(7+K9,10))IOL=0320
8026 FIND (Z[6],KEY="CMP"+X3$(9,3))F$
8030 REM "IF Y$(38,1)="N" THEN GOTO 08100
8035 REM "SET CONSTANTS HERE
8038 Q$=FNS$(F$(7,40)); IF Y[4]>0 THEN Y3$(FNP(Y[4],Y[5]),LEN(Q$))=Q$; Y3$(FNP(Y[4]+O,Y[5]),LEN(Q$))=Q$
8040 Q$=FNS$(Y$(39,16))+": "; CALL "ZZDISP","AX",B$(1,10),"A/P",X3$,X$,"",0,0,X4$; Q$=Q$+X$
8045 IF Y[6]>=0 THEN IF Y$(38,1)="Y" THEN Y3$(FNP(Y[6],Y[7]),LEN(Q$))=Q$ ELSE IF Y[6]>0 THEN Y3$(FNP(Y[6],Y[7]),LEN(X$))=X$; Y3$(FNP(Y[6]+O,Y[7]),LEN(X$))=X$
8050 Q$=FNS$(Y$(55,16))+": "+B$(11,35); IF Y[8]>0 THEN IF Y$(38,1)="Y" THEN Y3$(FNP(Y[8],Y[9]),LEN(Q$))=Q$ ELSE Y3$(FNP(Y[8],Y[9]),35)=B$(11,35)
8055 IF POS(X3$(9,3)="042047048",3)=0 THEN Q$=FNS$(Y$(71,16))+": "+STR(C0:"000000"); IF Y[10]>0 THEN IF Y$(38,1)="Y" THEN Y3$(FNP(Y[10],Y[11]),LEN(Q$))=Q$ ELSE Y3$(FNP(Y[10],Y[11]),6)=STR(C0:"000000"),Y3$(FNP(Y[10],Y[11])-18,10)=FND$(C0$(7,6))
8060 FOR X=0 TO 6
8065 Q$=FNS$(Y$(87+X*16,16)),Q=-LEN(Q$)
8070 IF X=0 OR X=6 THEN Q=Q+10 ELSE IF X=1 THEN Q=Q+8 ELSE IF X=2 OR X=4 THEN Q=Q+LEN(M0$) ELSE IF X=3 THEN Q=Q+LEN(M1$) ELSE IF X=5 THEN Q=Q+15
8075 IF Y[X+12]>0 THEN Y3$(FNP(Y[X+12],Y[1]-2)+Q-1,LEN(Q$))=Q$
8076 IF Y[X+12]>0 THEN Y3$(FNP(Y[X+12]+O,Y[1]-2)+Q-1,LEN(Q$))=Q$
8080 NEXT X
8085 IF Y$(38,1)="Y" THEN DIM Z5$(W0-5,"-"); Y3$(FNP(1,Y[1]-1),W0-5)=Z5$
8087 Y3$(FNP(59,Y[9]),22)="CHECK DATE: "+FND$(C0$(7,6))
8100 REM "
8390 RETURN 
8400 REM "WRITE CHECK PRINT FILE
8410 DIM E$(58),E[2]
8420 E$(1,17)=U8$,E$(18,6)=STR(C0:"000000"),E$(24,6)=C0$(7,6),E[0]=T[1],E[1]=T[2],E[2]=T[3]
8440 WRITE (Z[5],KEY=E$(1,17))IOL=0350
8450 C0$(47,6)=STR(C0:"000000"),C0$(19,17)=E$(1,17)
8455 WRITE (Z[6],KEY=A1$)C0$
8485 C0=C0+1
8490 RETURN 
8700 REM "Set face of check
8710 Q$=FNS$(F$(7,40)); IF Y[19]>0 THEN Y3$(FNP(Y[19],Y[29]),LEN(Q$))=Q$
8715 IF Y[20]<=0 THEN GOTO 8725 ELSE FOR X=0 TO 2
8720 Q$=FNS$(F$(47+X*40,40)); Y3$(FNP(Y[20],Y[30]+X))=Q$; NEXT X
8725 Q$=STR(C0:"000000"); IF Y[21]>0 THEN Y3$(FNP(Y[21],Y[31]),LEN(Q$))=Q$; Y3$(FNP(Y[21]-2,Y[31]-1),9)="CHECK NO."
8730 Q$=FND$(C0$(7,6)); IF Y[22]>0 THEN Y3$(FNP(Y[22],Y[32]),LEN(Q$))=Q$
8735 Q$=STR(T[3]:"*"+M0$(3)); IF T[3]<=0 THEN Q$="** VOID **"
8740 IF Y[23]>0 THEN Y3$(FNP(Y[23],Y[33]),LEN(Q$))=Q$
8743 IF POS(X3$(9,3)="060338363364365366476",3)>0 THEN GOSUB 4100; GOTO 8746
8745 Q$="*"+M0$(1,POS("."=M0$)-1),Q$=STR(INT(T[3]):Q$)+" Dollars and "+STR(100*FPT(T[3]):"00")+" cents",J9$=Q$; IF Y[24]>0 THEN IF T[3]>0 THEN Y3$(FNP(Y[24],Y[34]),LEN(Q$))=Q$ ELSE Y3$(FNP(Y[24],Y[34]),LEN(V$))=V$
8750 CALL "ZZDISP","AX",B$(1,10),"A/P",X3$,Q$,"",0,0,X4$; IF Y[25]>0 THEN Y3$(FNP(Y[25],Y[35]),LEN(Q$))=Q$
8755 Q$=FNS$(B$(11,35)); IF Y[26]>0 THEN Y3$(FNP(Y[26],Y[36]),LEN(Q$))=Q$
8758 CALL "ZZDISP","AX",B$(124,9),"ZIP",X3$,X$,"",0,0,X4$
8760 IF Y[27]>0 THEN FOR X=0 TO 1; Q$=B$(46+X*30,30); Y3$(FNP(Y[27],Y[37]+X),LEN(Q$))=Q$; NEXT X; Q$=FNS$(B$(106,16))+", "+B$(122,2)+"    "+X$; Y3$(FNP(Y[27],Y[37]+1+SGN(POS(" "<>B$(76,30)))),LEN(Q$))=Q$
8765 IF T[3]<=0 THEN Y3$(FNP(Y[28],Y[38]),LEN(V0$))=V0$
8790 RETURN 
8800 REM "Void face of check
8805 P9$="VOID CHECK"
8810 Y3$(FNP(Y[23],Y[33]),LEN(V$))="**VOID**"
8815 IF POS(X3$(9,3)="060338363364365366476")>0 THEN J9=T[3],T[3]=0; GOSUB 4100; T[3]=J9; GOTO 8821
8820 IF Y[24]>0 THEN Y3$(FNP(Y[24],Y[34]),LEN(J9$))=V$
8825 Y3$(FNP(Y[28],Y[38]),LEN(V$))=V$
8890 RETURN 
8900 REM "PRINT CHECK
8904 REM "Transfer top stub info to bottom stub
8905 IF POS(X3$(9,3)="500482",3)<>0 THEN Y3$(FNP(1,46))=Y3$(FNP(1,Y[1]-2),(Y[2]+8)*W0)
8907 IF X3$(9,3)<>"500" THEN Y3$(FNP(1,46))=Y3$(FNP(1,Y[1]-2),(Y[2]+3)*W0)
8920 FOR X=0 TO W1-1
8930 IF POS(" "<>Y3$(X*W0+1,W0))=0 THEN PRINT (H1)'LF', ELSE PRINT (H1)@(W9),Y3$(X*W0+1,W0)
8950 NEXT X
8980 GOSUB 7800
8990 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9600 REM "
9610 CALL "ZZPROM","4",X3$,S3,"Check printing has already been completed.","","",0; ON S3 GOTO 9800,9611
9620 A$(47,6)=""; GOTO 1525
9800 REM "EXIT PROGRAM
9810 Y4$="ZMENU"
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9950 CLEAR ; CALL "ZZDAPP",X3$,"AALL"; RUN "ZMENU"
9999 END 
56000 REM "SSP 197028 Clearson's Electronic Forms didn't line up properly for field 24...added three blanks to end of each segment
56010 REM "197028-Create A/P check format to match his preprinted form.     
