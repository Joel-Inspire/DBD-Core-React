0010 REM "Suggested Payment Report <AP2CDA>
0035 REM "5.3 - 05/21/04 - 11.896666 - dff - SSP# 133604
0040 REM "Copyright 2004 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="AP2CDA",X1$="Suggested Payment Report"
0120 DIM A$(34),A[2],B$(200),C$(227),C[2],D[4],E$(160),E[13]
0140 Z0$="###,###,###.00-"
0160 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0210 T=1,T0=0,T1=3
0215 NO_BANK_CODE$="N"; REM "SSP135047
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "I/O lists
0305 REM "IOLIST FOR APH - Check print file
0310 IOLIST A$,A[0],A[1],A[2]
0315 REM "IOLIST FOR ZYB - Bank Account Masterfile
0320 IOLIST B$
0325 REM "IOLIST FOR AP4 - Vendor Masterfile
0330 IOLIST C$,C[0],C[1],C[2]
0340 IOLIST D$,D[0],D[1],D[2]
0350 IOLIST E$,E[0],E[1],E[2],E[3],E[4],E[5],E[6],E[7],E[8],E[9],E[10],E[11],E[12],E[13]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O APH...  02O ZYB...  03O AP4...  06O API...  10O APZ...  "
0515 Y$=Y$+"04L APG...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0525 Q$=STR(Z[10]:"00"); CALL "ZZINIT",Q$; PRINT @(0,10),'CL',@(0,12),'CL',
0540 IF POS("A"=V0$(59,12))>0 THEN GOSUB 4100; F9=1; REM "Create Temp Sort by Vendor Lookup (PFG)
0541 IF POS("B"=V0$(59,12))>0 THEN GOSUB 4200; F9=2; REM "Create sort by vendor name - ssp# 160286
0700 REM "Open Printer
0720 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0721,9920
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[5]; U=27,U0=5,U[0]=0,U[1]=3,U[2]=3,U[3]=10,U[4]=1,U[5]=10,T0=3,V1=3,V2=3,V3=11,U0$=" Y  ",T5$="Z   S   AA/P    ",U5$="Company",U6$="Bank code",U7$="Vendor"; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0890 GOSUB 8000
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(U1,END=5000)
1006 IF F9=1 THEN U$=U$(1,6)+U$(17,10)+U$(27,11); READ (U1)
1007 IF F9=2 THEN U$=U$(1,6)+U$(42,21); READ (U1)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN READ (U1,END=5000); EXITTO 1000
1030 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U=1 THEN EXITTO 5000 ELSE READ (U1,END=5000); EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1210 IF A$(4,3)=" " THEN NO_BANK_CODE$="Y"; REM "SSP135047
1300 REM "Read other data
1320 DIM B$(LEN(B$)); FIND (Z[2],DOM=1321,KEY=A$(4,3))IOL=0320
1330 DIM C$(LEN(C$)); FIND (Z[3],DOM=1331,KEY=A$(7,10))IOL=0330
1340 DIM E$(LEN(E$)); FIND (Z[6],DOM=1341,KEY=A$(7,10)+A$(18,10))IOL=0350
1400 REM "Subtotals"
1415 GOSUB 3100
1420 ON T GOTO 1470,1430,1440,1450,1460
1430 GOSUB 6200
1440 GOSUB 6300
1450 GOSUB 6400
1490 T=0
1500 REM "Print Data
1510 IF W+1>W0 THEN GOSUB 6000
1515 IF T[T0,0]=0 THEN CALL "ZZDISP","AX",A$(7,10),"A/P",X3$,X$,"",0,0,X4$; Y5$(1)="Vendor code: "+X$,Y5$(26)="Name: "+C$(11,35); GOSUB 7300
1517 GOSUB 4000
1520 Y5$(53)=A$(17,1),Y5$(55)=STR(E[13]:Z0$),Y5$(74)=STR(A[0]:Z0$),Y5$(90)=STR(A[1]:Z0$)
1525 Y5$(106)=STR(A[0]-A[1]:Z0$,ERR=1526)
1550 REM "Remote Location Invoices
1555 IF E$(121,1)<>"1" THEN GOTO 1600
1560 Y5$(126)=E$(118,3)
1690 GOSUB 7300
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+A[0],T[T0,2]=T[T0,2]+A[1],T[T0,3]=T[T0,3]+A[0]-A[1]
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; GOSUB 7000
3155 IF U0$(1,1)="Y" THEN GOSUB 6000
3160 U1$=U9$(1,V1)
3180 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$,T6$=U2$; GOSUB 3050; GOSUB 7000
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3260 U2$=U9$(V5,V2)
3280 RETURN 
3300 REM "Next to lowest order break"
3310 IF V3=0 THEN RETURN ELSE IF U3$=U9$(V6,V3) THEN GOSUB 3400; RETURN 
3320 GOSUB 3420
3330 IF U3$="" THEN GOTO 3360
3350 T=3,T$=U7$,T6$=U3$; GOSUB 3050; GOSUB 7000
3355 IF U0$(3,1)="Y" THEN GOSUB 6000
3360 U3$=U9$(V6,V3)
3380 RETURN 
3480 RETURN 
4000 REM "Print invoice dates
4003 Y5$(2)=A$(18,10)
4005 IF X3$(69,1)<>"Y" THEN Y5$(13)=FND$(E$(21,6)) ELSE CALL "ZZDISP","DX",E$(21,6),"   ",X3$,X$,"",0,0,X4$; Y5$(13)=X$
4010 IF X3$(69,1)<>"Y" THEN Y5$(25,5)=FND$(E$(29,6)) ELSE CALL "ZZDISP","DX",E$(29,6),"   ",X3$,X$,"",0,0,X4$; Y5$(25)=X$(1,5)
4011 IF E[9]<>0 THEN Y5$(30)="E"
4014 DIM APG$(63); READ (Z[4],KEY=E$(1,20),DOM=4015)APG$
4015 IF POS(" "<>E$(35,6))<>0 THEN IF X3$(69,1)<>"Y" THEN Y5$(32,5)=FND$(E$(35,6)) ELSE CALL "ZZDISP","DX",E$(35,6),"   ",X3$,X$,"",0,0,X4$; Y5$(32)=X$(1,5)
4016 IF POS(" "<>E$(125,6))<>0 AND APG$(28,1)="Y" THEN IF X3$(69,1)<>"Y" THEN Y5$(32,5)=FND$(E$(125,6)),Y5$(38,1)="*" ELSE CALL "ZZDISP","DX",E$(125,6)," ",X3$,X$,"",0,0,X4$; Y5$(32)=X$(1,5),Y5$(38,1)="*"; REM "WO131561, 2nd discount date 
4020 Y5$(40)=E$(27,2)
4040 RETURN 
4100 REM "Create Temp Sort by Vendor Lookup (PFG)
4102 K9$="Payment Selection Data by Vendor Lookup"
4105 CALL "ZZ2SRT",X3$,X4$,Z[13],Z[4],37,K9$,Z,"  "; ON Z GOTO 4106,9900
4107 Y$="07X "+K9$
4108 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
4109 U1=Z[7]
4110 CALL "ZZINFO",Z[4],0,X3$,R,0,0,0,0,0,0,Q$+""
4115 V1$=K9$
4195 RETURN 
4200 REM " SORT BY VENDOR SSP 160286
4202 U1=Z[10]; V1$="APZ..."
4249 RETURN 
5000 REM "End of Print
5001 IF W0=99999999999999 THEN GOTO 5030; REM " SSP# 158440 
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; GOSUB 7000
5200 REM "
5290 GOTO 9900
6000 REM "Page header
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W4$,W5$
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6115 Y5$(41)="Bank account:",Y5$(55)=A$(4,3),Y5$(61)="Description:",Y5$(74)=B$(4,30)
6120 GOSUB 7300
6125 GOSUB 7300
6130 Y5$(50)="Special",Y5$(62)="Invoice",Y5$(83)="Gross",Y5$(96)="Discount",Y5$(109)="Net Payment"
6132 IF X3$(30,1)="M" THEN Y5$(124)="Source"
6135 GOSUB 7300
6139 Y5$(1)="Invoice",Y5$(14)="Date",Y5$(27)="Due",Y5$(33)="Disc  Trms"
6140 Y5$(50)="Ck Code",Y5$(62)="Balance",Y5$(81)="Payment",Y5$(99)="Taken",Y5$(114)="Amount"
6142 IF X3$(30,1)="M" THEN Y5$(123)="Company"
6145 GOSUB 7300
6199 RETURN 
6200 REM "Break #1 header
6299 RETURN 
6300 REM "Break #2 header
6399 RETURN 
6400 REM "Break #3 header
6499 RETURN 
7000 REM "Totals logic
7001 IF T=T0 THEN IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(74)=T1$(1,15),Y5$(90)=T1$(1,15),Y5$(106)=T1$(1,15); GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" Total ("+STR(T[T,0]:"0")+"):"
7020 Y5$(74)=STR(T[T,1]:Z0$),Y5$(90)=STR(T[T,2]:Z0$),Y5$(106)=STR(T[T,3]:Z0$); GOSUB 7300
7065 IF T$="END" THEN Y5$(74)=T2$(1,15),Y5$(90)=T2$(1,15),Y5$(106)=T2$(1,15); GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed: "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 IF INT(T2)>0 THEN T4$=STR(INT(T2))+" Hours " ELSE T4$=""
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+STR(T3:"##.##")+" Minutes"
7260 Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 W=W+1
7330 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7460 V1$="APH   "
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=STP(V0$(19,40),1); REM "cwj 5/22/98 ssp#099101
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
8000 REM "Sort by bank code
8005 F0=Z[1],F1=Z[4]
8006 IF F9=1 THEN F2=Z[7],F3=Z[3],F4=0,F5=0 ELSE F2=0,F3=0
8007 IF F9=2 THEN F4=Z[10],F5=Z[3],F2=0,F3=0 ELSE F4=0,F5=0
8010 CALL "AP2CDB",X3$,F0,F1,F2,F3,F4,F5
8040 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End program
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 IF NO_BANK_CODE$="Y" THEN CALL "ZZPROM","4",X3$,S3,"There is an invoice setup for payment with a blank bank code, you must correct.","","",0; REM "SSP135047 
9930 IF NOT(NUL(Y4$)) THEN IF %GUI THEN CALL Y4$,ERR=9931 ELSE RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
