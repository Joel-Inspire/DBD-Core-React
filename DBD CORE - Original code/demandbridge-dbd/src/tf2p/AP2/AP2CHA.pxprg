0010 REM "A/P Cash Disbursements Journal <AP2CHA>
0035 REM "5.7 - 02/03/17 - 11.9725 - tma - SSP# 280924
0037 REM "280924-Cash Disbursement Report is not page-breaking properly      
0040 REM "Copyright 2017 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="AP2CHA",X1$="A/P Cash Disbursements Journal"
0120 DIM A$(39),A[3],B$(37),B[5],C$(200),D$(227),D[2]
0140 Z0$="###,###,###.00-"
0145 GOSUB 8950
0160 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0210 T=1,T0=0,T1=3
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "I/O lists
0305 REM "IOLIST FOR APC - Check register file
0310 IOLIST A$,A[0],A[1],A[2],A[3]
0315 REM "IOLIST FOR APH - Check print file
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5]
0325 REM "IOLIST FOR ZYB - Bank Account Masterfile
0330 IOLIST C$
0335 REM "IOLIST FOR AP4 - Vendor Masterfile
0340 IOLIST D$,D[0],D[1],D[2]
0350 IOLIST E$,E[0],E[1],E[2],E[3],E[4],E[5],E[6],E[7],E[8],E[9],E[10],E[11],E[12],E[13],E[14],E[15],E[16],E[17],E[18]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O APC...  02O APH...  03O ZYB...  04O AP4...  05O API...  06O APK...  07O GL1...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0530 READ (Z[13],KEY=X3$(9,3)+"APC"+X3$(174,4),DOM=0531)C0$
0535 DIM P2$(105); FIND (Z[13],KEY=X3$(9,3)+"G/LYE"+C0$(1,4),DOM=0536)P2$
0540 D0$=P2$(NUM(C0$(5,2))*6+15,6)
0545 W0$="For Fiscal year "+C0$(1,4)+", period "+C0$(5,2)+" ending "+FND$(D0$)
0605 N0$="AP2CUA"; GOSUB 6900
0700 REM "Open Printer
0720 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0721,9920
0725 IF %HAS_IMAGE_LIB THEN CALL "ILGAUD",ERR=*NEXT,X3$,X4$,"R4" ! 174140, 187474
0750 REM "Check for restart record"
0755 READ (Z[1],KEY="",DOM=0780)
0760 IF K0$>"" THEN X3$(184,1)="1"
0765 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W4,W4$,W5$
0770 CALL "ZZREST",X3$,Z[13],Z[1],W9,W3,W,V0$,K0$,K1$,K0,K1
0775 GOSUB 6000
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[4]; U=17,U0=4,U[0]=0,U[1]=3,U[2]=3,U[3]=10,U[4]=1,T0=2,V1=3,V2=3,U0$="NY  ",T5$="                ",U5$="Source Company",U6$="Bank code"; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN READ (U1,END=5000); EXITTO 1000
1030 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U=1 THEN EXITTO 5000 ELSE READ (U1,END=5000); EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1150 READ (Z[1],KEY=U$(1,17),DOM=1000)IOL=0310
1300 REM "Read other data
1330 DIM C$(LEN(C$)); FIND (Z[3],DOM=1331,KEY=A$(4,3))IOL=0330
1340 DIM D$(LEN(D$)); FIND (Z[4],DOM=1341,KEY=A$(7,10))IOL=0340
1400 REM "Subtotals"
1415 GOSUB 3100
1420 ON T GOTO 1470,1430,1440,1450,1460
1430 GOSUB 6200
1440 GOSUB 6300
1490 T=0
1500 REM "Print Data
1510 IF W+1>W0 THEN GOSUB 6000
1515 Y5$(1)=A$(18,6),Y5$(8)=A$(17,1)
1520 IF X3$(69,1)<>"Y" THEN Y5$(10)=FND$(A$(24,6)) ELSE CALL "ZZDISP","DX",A$(24,6),"   ",X3$,X$,"",0,0,X4$; Y5$(10)=X$
1525 CALL "ZZDISP","AX",A$(7,10),"A/P",X3$,X$,"",0,0,X4$; Y5$(22)=X$
1530 Y5$(34)=D$(11,35),Y5$(84)=STR(A[0]:Z0$),Y5$(101)=STR(A[1]:Z0$),Y5$(117)=STR(A[2]:Z0$)
1810 GOSUB 2000
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+A[0],T[T0,2]=T[T0,2]+A[1],T[T0,3]=T[T0,3]+A[2]
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
2000 REM "Print Keyed Detail
2001 DIM V[3]
2005 T4=0
2010 READ (Z[2],KEY=A$(1,17),DOM=2011,END=2700)
2015 GOSUB 2500
2020 W$=KEY(Z[2],END=2700)
2030 IF W$(1,17)<>A$(1,17) THEN GOTO 2700
2035 READ (Z[2],END=2700)IOL=0320
2036 REM "If this is alternate currency then, substitute alternate amounts in (2) and (3) for normal amounts in (0) and (1) and the rest of the program logic is unchanged.
2037 IF POS(" "<>B$(34,3))<>0 THEN B[0]=B[2],B[1]=B[3]
2038 IF X3$(330,1)="1" THEN IF A$(18,6)<>B$(28,6) THEN GOTO 2700; REM "Scan for checks that don't match on Franchise locations
2040 REM "Read Secondary Information"
2200 REM "Print Each Line
2205 T4=T4+1
2210 IF W+1>W0 THEN GOSUB 6000; GOSUB 2500
2215 Y5$(71)=B$(18,10),Y5$(84)=STR(B[0]:Z0$),Y5$(101)=STR(B[1]:Z0$)
2220 Y5$(117)=STR(B[0]-B[1]:Z0$,ERR=2221)
2225 GOSUB 7300
2400 REM "Accumulate Line Totals"
2410 V[1]=V[1]+B[0],V[2]=V[2]+B[1],V[3]=V[3]+B[0]-B[1]
2440 IF F0$(49,1)="Y" THEN GOSUB 4000
2499 GOTO 2020
2500 REM "Print Line Header
2510 IF W+4>W0 THEN GOSUB 6000
2690 RETURN 
2700 REM "Line Totals"
2701 IF T4<2 THEN GOSUB 7300; GOTO 2790
2702 IF W+2>W0 THEN GOSUB 6000
2705 Y5$(84)=T1$(1,15),Y5$(101)=T1$(1,15),Y5$(117)=T1$(1,15); GOSUB 7300
2720 Y5$(84)=STR(V[1]:Z0$),Y5$(101)=STR(V[2]:Z0$),Y5$(117)=STR(V[3]:Z0$); GOSUB 7300
2765 IF T$="END" THEN Y5$(84)=T2$(1,15),Y5$(101)=T2$(1,15),Y5$(117)=T2$(1,15); GOSUB 7300
2780 GOSUB 7300
2795 RETURN 
3000 REM "Subtotaling Routines"
3033 ESCAPE 
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; GOSUB 7000
3155 IF U0$(1,1)="Y" THEN GOSUB 6000
3160 U1$=U9$(1,V1)
3165 FIND (Z[13],KEY=U1$+"A/P")F0$
3170 F1$=""; FIND (Z[13],KEY=U1$+"AP2A",DOM=3171)F1$
3180 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$,T6$=U2$; GOSUB 3050; GOSUB 7000
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3260 U2$=U9$(V5,V2)
3280 RETURN 
3380 RETURN 
3480 RETURN 
4000 REM "Print G/L Distribution
4002 P=-30
4005 DIM X[4],E[19]
4010 R1=0; READ (Z[5],KEY=B$(7,10)+B$(18,10),DOM=4040)IOL=0350
4015 R2=B[0]; PRECISION 14; IF B[0]=E[13] THEN R0=1,R1=2 ELSE IF E[13]<>0 THEN R0=R2/E[13],R1=2 ELSE IF E[2]=0 THEN R0=1,R1=2 ELSE R0=R2/E[2],R1=1
4020 PRECISION 2
4030 IF F1$>"" THEN GOSUB 4200
4040 I1=E[1],X=0
4045 IF W+2>W0 THEN GOSUB 6000 ! SSP#280924
4050 CALL "GL2XRA",X3$,X4$,W3,W9,W,W0,I1,Z[6],Z[7],X{ALL},X,P,C9,T1$,T2$,Y5$,33,R0,R1,R2
4060 IF C9<>0 THEN GOSUB 6000; GOTO 4050
4095 RETURN 
4200 REM "Special Amounts
4205 GOSUB 7300
4210 FOR X=0 TO 3; IF F1$(X*20+24,1)<>"Y" THEN GOTO 4280
4215 Y5$(63)=F1$(X*20+8,16)
4220 IF R1=2 THEN Q0=R0*(E[3+X]-E[X+14]) ELSE IF R1=1 THEN Q0=R0*E[X+3]
4225 Y5$(84)=STR(Q0:Z0$)
4230 GOSUB 7300
4235 R2=R2-Q0,X[1]=X[1]+Q0
4280 NEXT X
4295 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5220
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; GOSUB 7000
5060 IF X3$(184,1)="1" THEN PRINT (W9)@(INT(W3/2)-39),"** The update was interrupted and will be restarted! See page 1 for details **"; W=W+1
5200 REM "
5220 IF V3$(2,1)<>"T" THEN Y4$="AP2CUA" ELSE Y4$=""
5290 GOTO 9900
6000 REM "Page header
6010 IF POS(" "<>Y5$)>0 THEN GOSUB 7300
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W4$,W5$
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6115 Y5$(41)="Bank code:",Y5$(52)=A$(4,3),Y5$(57)=C$(4,30)
6117 IF POS(" "<>A$(30,9))>0 THEN Y5$(90)="File reference no: "+A$(30,9); REM "WO131772
6120 GOSUB 7300
6125 Y5$(3)="Check",Y5$(121)="Net"
6130 GOSUB 7300
6135 Y5$(2)="Number",Y5$(14)="Date",Y5$(22)="Vendor",Y5$(38)="Payee",Y5$(71)="Invoice",Y5$(86)="Gross Amount",Y5$(105)="Discounts",Y5$(117)="Check Amount"
6140 GOSUB 7300
6199 RETURN 
6200 REM "Break #1 header
6299 RETURN 
6300 REM "Break #2 header
6399 RETURN 
6900 REM 
6905 IF N0$="" OR V3$(1,1)<>"I" THEN GOTO 6990
6910 EXTRACT (Z[1],KEY="",DOM=6990)V0$,K0$
6915 IF K0$>"" THEN GOTO 6990
6930 Q$="+("+V0$(22,4)+"."+V0$(26,2)+")"
6935 CALL "ZZPROM","X0AUDITS",X3$,S3,Q$,"","",0; ON S3 GOTO 6940,5200,9900
6950 REMOVE (Z[1],KEY="")
6990 RETURN 
7000 REM "Totals logic
7001 IF T=T0 THEN IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(84)=T1$(1,15),Y5$(101)=T1$(1,15),Y5$(117)=T1$(1,15); GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" Total ("+STR(T[T,0]:"0")+"):"
7020 Y5$(84)=STR(T[T,1]:Z0$),Y5$(101)=STR(T[T,2]:Z0$),Y5$(117)=STR(T[T,3]:Z0$); GOSUB 7300
7065 IF T$="END" THEN Y5$(84)=T2$(1,15),Y5$(101)=T2$(1,15),Y5$(117)=T2$(1,15); GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed: "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 IF INT(T2)>0 THEN T4$=STR(INT(T2))+" Hours " ELSE T4$=""
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+STR(T3:"##.##")+" Minutes"
7260 Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 W=W+1
7325 IF %IL_PDF THEN PRINT (%IL_PDF,ERR=*NEXT)Y5$ ! 174140
7330 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)); REM ",W3$=V0$(19,POS("   "=V0$(19,40)+"  ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
8950 REM "Program flow logic
8960 DIM U7$(6); U7$(1)=FID(0); U7$=U7$+"PF"
8970 CLOSE (14); OPEN (14)"ZZPARM"; FIND (14,KEY=U7$,DOM=8990)U7$,U8$
8980 Q1=NUM(U8$(7,2)),Q1$=U8$(9,Q1),U8$=U8$(Q1+9)
8990 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9700 REM "PROG FLOW
9705 IF STP(Y4$)<>"" THEN DIM U6$(6); U6$(1)=Y4$,U8$=U6$+STR(LEN(Q1$):"00")+Q1$+U8$; REM "I other progam to run then pass Q1$ to it before resuming the chaing
9710 IF U8$="" THEN REMOVE (Z[13],KEY=U7$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U7$)U7$,U8$
9725 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9730 RUN U8$(1,6)
9900 REM "End program
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9915 IF Q1$<>"" THEN GOTO 9700
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF NOT(NUL(Y4$)) THEN IF %GUI THEN CALL Y4$,ERR=9931 ELSE RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
56001 REM "280924-Cash Disbursement Report is not page-breaking properly      
