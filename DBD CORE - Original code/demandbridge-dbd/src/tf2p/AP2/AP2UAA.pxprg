0010 REM "A/P Period End Processing <AP2UAA>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 06/21/10 - 9.517222 - tma - SSP# 225538
0037 REM "225538-Cash Receipts Journals not attached to invoice history in   
0040 REM "Copyright 2010 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$,Q2$
0100 SETERR 9000
0110 X0$="AP2UAA",X1$="A/P Period End Processing"
0120 DIM Z0$(80,"-")
0125 DIM G[7],D[4],B[15],F[14],H[2]
0130 DIM Y[4]
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0280 IOLIST F0$
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27]
0312 IOLIST A0$,A0[0],A0[1],A0[2],A0[3],A0[4],A0[5],A0[6],A0[7],A0[8],A0[9],A0[10],A0[11],A0[12],A0[13],A0[14],A0[15],A0[16],A0[17],A0[18],A0[19],A0[20],A0[21],A0[22],A0[23],A0[24],A0[25],A0[26],A0[27]
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15]
0330 IOLIST C$
0340 IOLIST D$,D[0],D[1],D[2],D[3]
0360 IOLIST F$,F[0],F[1],F[2],F[3],F[4],F[5],F[6],F[7],F[8],F[9],F[10],F[11],F[12],F[13],F[14]
0370 IOLIST J0,J1$,J[0]
0375 IOLIST J0,J1$,J[0],J[1]
0395 IOLIST APJ$(1),APJ[0] ! SSP#209664
0470 IOLIST G$,G[0],G[1],G[2],G[3],G[4],G[5],G[6],G[7]
0500 REM "FILES
0505 Z=NUM(X3$(60,3)); DIM Z[Z]
0510 Z$="01LRAPI...  02ORAP5...  03ORAP9...  04LRAPM...  05ORAPP...  06ORAPQ...  07ORAPN...  08ORAPO...  09ORAPJ...  10ORAPK...  11ORAP4...  12O APS...  "
0515 Z$=Z$+"13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 0521,9900
0550 FIND (Z[13],KEY=X3$(9,3)+"A/P",DOM=9800)IOL=0280
0560 FIND (Z[13],KEY=X3$(9,3)+"G/L")G0$
0570 FIND (Z[13],KEY=G0$(1,6)+"YE"+F0$(7,4))G1$
0595 SET_PARAM 'XI'; APJ_FID$=FN%FID$(Z[9]); KEY_SZ=ASC(APJ_FID$(11,1)),REC1=DEC(APJ_FID$(12,3)); IF KEY_SZ>0 THEN KEY_SZ=KEY_SZ-4; IF REC1>32767 THEN KEY_SZ=KEY_SZ-2 ! CALL "ZZINFO",Z[9],STATUS,X3$,REC_USED,TOT_REC,KEY_SZ,BYTE,DISC,TYPE,TOT_SEC,FILE_NM$ ! SSP#209664!SSP#231214
0596 SET_PARAM 'XI'=0
0600 REM "
0670 CALL "ZZPROM","P1",X3$,0,U7$,"","",0
0680 CALL "ZZPROM","P2",X3$,0,U8$,"","",0
0690 CALL "ZZPROM","P3",X3$,0,U9$,"","",0
0730 DIM A$(160),A[27]
0800 REM "CHECK FOR ROOM?
0805 CALL "ZZPROM","U1",X3$,0,"","","",0
0810 DIM X[Z,1],X$(0)
0815 FOR X=1 TO Z
0820 CALL "ZZINFO",X,0,X3$,A,B,0,0,0,0,0,""; X[X-1,0]=A,X[X-1,1]=B
0825 NEXT X
0830 PRINT @(0,22),'CL',
0835 IF X[Z[1]-1,0]=0 THEN CALL "ZZPROM","U2",X3$,S3,"","","",0; GOTO 9900
0840 GOTO 0900
0880 REM "PROMPT OUT OF ROOM
0885 X$="There is not enough room to update this data. Press <Return>: "
0890 CALL "ZZPROM","U3",X3$,S3,"","","",0; GOTO 9900
0900 REM "RESTART LOGIC
0901 GOTO 0915 ! SSP#205455 restart logic Not necessary in Month end caused problem with Oracle
0905 V0$=X3$(1,6)+X3$(40,3)+DAY+STR(TIM*100:"0000")+X3$(178,6); WRITE (Z[1],KEY="",DOM=0906)V0$
0910 EXTRACT (Z[1],KEY="")V0$,K0$,K1$,K0,K1; I1=IND(Z[1])
0915 GOSUB 6000
0916 GOTO 0960 ! SSP#205455
0920 IF K0$="" THEN READ (Z[1],KEY=K0$); GOTO 0960
0930 EXTRACT (Z[1],KEY=K0$,DOM=0931)IOL=0310
0935 PRINT @(12,12),"Restarting update with record: ",A$(1,10)+"-"+A$(11,10)
0940 CALL "ZZPROM","U5",X3$,0,"","","",14
0970 CALL "ZZPROM","9",X3$,S3,"","","",0; ON S3 GOTO 0971,9900
0975 PRINT @(0,12),'CL',
0980 CALL "ZZDAPP",X3$,"DALL"
1000 REM "READ NEXT RECORD
1090 CALL "ZZPROM","P0",X3$,0,"","","",12
1100 REM "
1110 P2=5
1200 GOSUB 8000
1220 GOSUB 2500
1230 GOSUB 2000
1240 GOSUB 2200
1260 GOSUB 2800
1280 IF F0$(11,2)>=G1$(13,2) THEN GOSUB 2700
1800 REM "Finish up period end by updating period
1810 IF F0$(11,2)>=G1$(13,2) THEN F0$(7,6)=STR(NUM(F0$(7,4))+1:"0000")+"01" ELSE F0$(11,2)=STR(NUM(F0$(11,2))+1:"00")
1820 WRITE (Z[13],KEY=X3$(9,3)+"A/P")IOL=0280
1950 GOTO 5000
2000 REM "Clear zero balance invoices
2005 P0=X[Z[1]-1,0],P1=0,P4=11,P5=17
2010 PRINT @(0,14),'CL',@(12),U7$," Open Invoice file...",
2020 READ (Z[1],KEY="",DOM=2021)
2040 K$=KEY(Z[1],END=2090); READ (Z[1],KEY=K$)IOL=0310
2045 IF A[13]<>0 THEN GOTO 2075
2050 IF A$(100,6)="      " OR A$(100,6)>F0$(7,6) THEN GOTO 2075
2055 IF KEY_SZ<>0 THEN GOSUB 3300 ! SSP#209664
2060 WRITE (Z[6],KEY=K$,DOM=7600)IOL=0310
2065 REMOVE (Z[1],KEY=K$)
2070 REMOVE (Z[5],KEY=A$(66,3)+A$(1,20),DOM=2071)
2075 PRINT @(12,15),U8$," ",K$,; GOSUB 6800
2080 GOTO 2040
2090 PRINT @(0,14),'CE',
2095 RETURN 
2200 REM "Monthly Disbursements file
2205 P0=X[Z[4]-1,0],P1=0,P4=11,P5=17
2210 PRINT @(12,14),U7$," the Monthly Disbursements file...",
2220 READ (Z[4],KEY="",DOM=2221)
2240 K$=KEY(Z[4],END=2280); READ (Z[4],KEY=K$)IOL=0340
2250 IF K$(1,6)>P0$ THEN GOTO 2280
2260 REMOVE (Z[4],KEY=K$)
2262 GOSUB 2300
2265 PRINT @(12,15),U8$," ",K$,; GOSUB 6800
2270 GOTO 2240
2280 GOSUB 6800
2285 PRINT @(0,14),'CE',
2295 RETURN 
2300 REM "Remove invoice detail for a check
2310 READ (Z[7],KEY=D$(1,17),DOM=2311)
2320 READ (Z[7],END=2380)IOL=0470
2330 IF G$(1,17)<>D$(1,17) THEN GOTO 2380
2340 REMOVE (Z[7],KEY=G$(1,27))
2350 F=Z[8],A0=G[0]
2355 GOSUB 5900
2370 GOTO 2320
2380 RETURN 
2500 REM "Purge Invoice history
2505 P0=X[Z[6]-1,0],P1=0,P4=11,P5=17
2510 PRINT @(12,14),U9$," Invoice History...",
2520 READ (Z[6],KEY="",DOM=2521)
2540 K$=KEY(Z[6],END=2580); READ (Z[6],KEY=K$)IOL=0360
2545 PRINT @(12,15),U8$," ",K$,; GOSUB 6800
2550 IF F$(100,6)>P0$ THEN GOTO 2575
2560 REMOVE (Z[6],KEY=K$)
2565 F=Z[9],A0=F[0]; GOSUB 5900
2570 F=Z[10],A0=F[1]; GOSUB 5900
2575 GOTO 2540
2585 PRINT @(0,14),'CE',
2595 RETURN 
2700 REM "Remove A/P Statistics
2705 P0=X[Z[3]-1,0],P1=0,P4=11,P5=17
2706 P9=POS(" "=F0$(22,10)); IF P9=0 THEN P9=10 ELSE P9=P9-1
2707 P0=P0*P9
2710 P9=NUM(F0$(7,4))
2720 F9=Z[3],F8=15
2740 READ (F9,KEY="",DOM=2741)
2745 K$=KEY(F9,END=2780); READ (F9)APB$
2750 PRINT @(12,15),U8$," ",K$,; GOSUB 6800
2760 X=POS(K$(F8,1)=F0$(22,10)); IF X>0 THEN IF NUM(K$(F8-4,4))>P9-NUM(F0$(31+X,1)) THEN GOTO 2745
2765 REMOVE (F9,KEY=K$)
2770 GOTO 2745
2780 IF F0$(70,1)="Y" AND F9=Z[3] THEN F9=Z[12],F8=19; GOTO 2740
2790 PRINT @(0,14),'CE',
2795 RETURN 
2800 REM "Delete inactive vendors
2810 P0=X[Z[11]-1,0],P1=0,P4=11,P5=17
2813 REM "Inactive vendors for PFG are retained forever, since there could
2814 REM "open items stivv for that vendor.
2815 IF X3$(30,1)="M" THEN GOTO 2890
2820 PRINT @(0,12),'CL',@(0,14),'CL',@(12),U$," Deleting inactive vendors...",
2830 READ (Z[11],KEY="",DOM=2831)
2840 READ (Z[11],END=2890)IOL=0330
2850 IF C$(246,1)<>"I" AND C$(246,1)<>"T" THEN GOTO 2875; REM "WO87609 DXM
2860 Q1$=C$(1,10),Q0$="A/P",Q2$="",Q3$="P"
2870 CALL "AP2UAB",ERR=2871,X3$,X4$,Q0$,Q1$,Q2$,Q3$
2875 GOSUB 6800
2880 GOTO 2840
2890 RETURN 
3300 REM "APJ as direct file needs to be marked as a history record!SSP#209664 
3305 DIM APJ$(80),APJ(1)
3310 READ (Z[9],KEY=A$(1,20),DOM=*NEXT) ! SSP#209664                           
3320 APJ_KEY$=KEY(Z(9),END=3390); READ (Z[9],KEY=APJ_KEY$)IOL=0395 ! SSP#209664
3330 IF APJ$(1,20)<>A$(1,20) THEN GOTO 3390 ! SSP#209664                       
3340 APJ$(58,1)="H" ! SSP#209664                                               
3350 WRITE (Z[9],KEY=APJ_KEY$)IOL=0395 ! SSP#209664                            
3360 GOTO 3320 ! SSP#209664                                                    
3390 RETURN ! SSP#209664                                                       
5000 REM "END OF UPDATE
5002 _APJ=HFN; OPEN (_APJ)"APJ"+%C$
5005 SET_PARAM 'XI'; APJ_FID$=FN%FID$(_APJ); KEY_SZ=ASC(APJ_FID$(11,1)),REC1=DEC(APJ_FID$(12,3)); IF KEY_SZ>0 THEN KEY_SZ=KEY_SZ-4; IF REC1>32767 THEN KEY_SZ=KEY_SZ-2 ! SSP#231214
5006 SET_PARAM 'XI'=0
5010 IF KEY_SZ<>0 THEN CALL "UT2HIS",ERR=*NEXT,X3$,X4$,"","*","AP" ! SSP#225538 !SSP#230159    
5020 CLOSE (_APJ)
5100 REM "
5200 REM "CLEAR FILES
5240 PRINT 'CF'
5400 REM 
5450 GOTO 9900
5900 REM "REMOVE LINES
5910 DIM DET[1]; CALL "AR2XAB",X3$,X4$,F,F$,A0,"",DET{ALL},FLAG$,SEQ$,"DH"
5911 GOTO 5990
5919 IF A0=0 THEN GOTO 5990; REM " OLD LOGIC FOR INDEXED FILES, NO LONGER USED
5920 EXTRACT (F,IND=0,ERR=5960)IOL=0290
5925 IF Y[4]=-2 THEN READ (F,IND=0); WAIT 0; GOTO 5920 ELSE Y[4]=-2; WRITE (F,IND=0)IOL=0290
5930 A1=Y[1],Y[1]=A0,Y[0]=Y[0]-1
5935 READ (F,IND=A0)A; IF A>0 THEN Y[0]=Y[0]-1,A0=A; IF POS(BIN(A,3)=Y5$,3)=0 THEN Y5$=Y5$+BIN(A,3); GOTO 5935
5940 WRITE (F,IND=A0)A1
5950 Y[4]=-1; WRITE (F,IND=0)IOL=0290
5955 GOTO 5990
5960 IF ERR=0 THEN RETRY ELSE GOTO 9000
5990 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(12,4),"This program will perform the period end processing",@(12,5),"for Accounts Payable for period ",F0$(11,2)," FYE ",F0$(7,4),"."
6025 PRINT @(12,7),"This function selectively removes history data based",@(12,8),"on the parameters for this company.  You should verify",@(12,9),"that all reports which may require this data have been",@(12,10),"printed prior to period end processing."
6030 X$=G1$(15+NUM(F0$(11,2))*6,6),X$=FND$(X$)
6040 IF X$(7)>F0$(62,4) THEN PRINT @(12,12),"WARNING: This period ending date of ",X$," is after the",@(12,13),"current 1099 calendar year of ",F0$(62,4),".  Maybe 1099's should be printed."
6165 PRINT (0,ERR=6066)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6225 GOSUB 6450
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6410 GOSUB 6000
6430 IF C9>0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6490 RETURN 
6600 REM "RESTART LOGIG
6610 WRITE (Z[1],IND=I1)V0$,K0$,K1$,K0,K1
6690 RETURN 
6800 REM "% complete & phase printing
6801 REM "P4= xpos on scrn,P5= ypos on scrn,P0=total # of recs,P1=counter(inc. in here),P2=total # of phases,P3=current phase(init to 0, incr. when P1 is = 0)
6810 IF P1=0 THEN PRINT @(P4,P5),"     complete",; IF P2>1 THEN P3=P3+1; PRINT ", phase",P3," of",P2," phases"
6820 P1=P1+1; IF P0<>0 THEN PRINT @(P4,P5),P1*100/P0:"###%",
6840 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7600 REM "If a APQ (Invoice History) record already exists, then just add to the buckets, but totally replace the info.
7605 DIM A0$(LEN(A$)),A0[27]
7610 READ (Z[6],KEY=K$,DOM=2060)IOL=0312
7612 IF A[0]>0 AND KEY_SZ=0 THEN F=Z[9],J0=A0[0]; GOSUB 7800 ! SSP#225164
7614 IF A[1]>0 THEN F=Z[10],J0=A0[1]; GOSUB 7850
7615 FOR I9=2 TO 27; A0[I9]=A0[I9]+A[I9]; NEXT I9
7620 A0$=A$
7625 WRITE (Z[6],KEY=K$)IOL=0312
7645 GOTO 2061; REM "Pickup after write w/DOM
7800 REM "Link up APJ history records for duplicate APQ record
7810 DIM J[1]
7820 J1=J0; READ (F,IND=J0)IOL=0370; IF J0>0 THEN GOTO 7820
7830 J0=A[0]; WRITE (F,IND=J1)IOL=0370
7840 RETURN 
7850 DIM J[1]
7860 J1=J0; READ (F,IND=J0)IOL=0375; IF J0>0 THEN GOTO 7860
7870 J0=A[1]; WRITE (F,IND=J1)IOL=0375
7890 RETURN 
8000 REM "Compute oldest invoice period/year to purge
8010 P0$=F0$(7,6),H[0]=NUM(F0$(42,2)),H[1]=NUM(P0$(5,2)),H[2]=NUM(G1$(13,2))
8020 H[1]=H[1]-H[0]
8025 IF H[1]<1 THEN P0$(1,4)=STR(NUM(P0$(1,4))-1:"0000"),H[1]=H[1]+H[2]; GOTO 8025
8030 P0$(5,2)=STR(H[1]:"00")
8040 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,1
9050 ON Y7 GOTO 9060,9100,9190,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9190 GOSUB 6600; GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9810 CALL "ZZPROM","4",X3$,A,"The A/P parameters have not been set for this company. Press |Return","","",0; GOTO 9900
9900 REM "END PROGRAM
9905 CALL "ZZDAPP",X3$,"AALL"
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 IF %GUI THEN SETESC 9350 ELSE BEGIN ; SETESC 9350
9950 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
56000 REM "225538-Cash Receipts Journals not attached to invoice history in   
