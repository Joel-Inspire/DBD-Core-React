0010 REM "Clear Open Inv Trans Links of no charge invoices <AP2UTI>
0020 SETESC 9300; SETERR 9000
0035 REM "5.2 - 09/06/03 - 7.217777 - tma - SSP# 162848
0040 REM "Copyright 2003 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="AP2UTI",X1$="A/P Clear Open Invoice Transaction Links"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27]
0320 IOLIST B0,B$,B[0],B[1]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01L API...  02O APJ...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0610 GOSUB 6000
0620 GOSUB 8100
0640 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0900 DIM A[27],B[1]
0905 C9=0
0950 PRINT @(20,10),'CE',
0990 READ (Z[1],KEY="",DOM=0991)
1000 K$=KEY(Z[1],END=5000)
1010 READ (Z[1],KEY=K$)IOL=0310
1012 R0=R0+1; IF MOD(R0,T0)=1 THEN C=R0; GOSUB 8150
1055 IF A[0]=0 THEN GOTO 1000
1080 B0=A[0],T=0,B1=0,FLAG$="",SEQ$=""
1100 REM "Add transactions
1120 IF FLAG$="END" THEN GOTO 1400
1130 CALL "AR2XAB",X3$,X4$,Z[2],A$,B0,B$,B{ALL},FLAG$,SEQ$,"R"
1140 IF FLAG$="NONE" THEN GOTO 2600
1160 T=T+B[0]
1180 GOTO 1120
1400 REM 
1410 IF A[10]+T=A[13] OR A[2]+T=A[13] THEN GOTO 1000; REM "Transactions add up ok
1425 IF A[0]<>12366 AND A[0]<>13915 THEN GOTO 1000; REM PRINT @(0,20),'CE',A$,'LF',A(0),A(10),A(13),; INPUT *; GOTO 01000
1449 REM "Since balance not changed, just remove the detail
1450 C9=C9+1,A[0]=0; WRITE (Z[1],KEY=K$)IOL=0310; GOTO 1990
1990 GOTO 1000
5000 REM "EOJ
5010 C=T5; GOSUB 8150
5015 PRINT @(20,15),'CL',STR(C9:"#,##0")," invoices corrected.",
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This CSI utility program will process the Vendor Open Invoice file (API) and set the link to open transactions to 0 whenever the open balance = gross amount and the total of transactions is not 0. IF the number of invoices corrected is 0 then everything was ok.",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[1],T9,X3$,T5,T0,K,B,D,S0,S1,F$
8115 PRINT @(0,9),"There are "+STR(T5)+" records to process"
8129 REM "Set T0, we make sure T0 is > 1, because later on we MOD and look for avalue of 1. IF T0 is 1, then nothing would get reported. We look for a result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T0'th record to get the first report
8130 T0=INT(T5*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T5,C
8195 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
