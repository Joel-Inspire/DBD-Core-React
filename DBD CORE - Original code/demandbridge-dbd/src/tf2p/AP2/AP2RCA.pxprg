0010 REM "A/P Aged Invoice Report <AP2RCA>
0035 REM "5.7 - 11/07/14 - 15.901388 - tma - SSP# 273735
0037 REM "273735-Issue with 542's month end, problem with aged invoice       
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="AP2RCA",X1$="A/P Aged Invoice Report"
0120 DIM A$(160),A[27],B$(0),C$(227),C[2],D(1)
0140 Z0$="###,###,###.00-"
0160 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0210 T=1,T0=0,T1=6
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "I/O lists
0305 REM "IOLIST FOR API - A/P Open Invoice Header
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27]
0325 REM "IOLIST FOR AP4 - Vendor Masterfile
0330 IOLIST C$,C[0],C[1],C[2]
0340 IOLIST D0,D$,D(1),D(2)
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O API...  03O AP4...  04O APJ...  "
0515 Y$=Y$+"13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0530 FIND (Z[13],KEY=X3$(9,3)+"A/P",DOM=9900)P0$
0700 REM "Open Printer
0720 REM 
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[2]; U=20,U0=2,U[0]=0,U[1]=10,U[2]=10,T0=2,V1=2,V2=8,U0$="    ",T5$="Z   AA/P        ",U5$="Division",U6$="Vendor code"; GOTO 0850
0830 DIM U[3]; U=23,U0=3,U[0]=0,U[1]=3,U[2]=10,U[3]=10,T0=2,V1=3,V2=10,U0$="Y   ",T5$="S   AA/P        ",U5$="Bank account",U6$="Vendor"; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])>=V2$(U3,U[U9]) THEN GOTO 1030 ELSE EXTRACT (U1,KEY=U$(1,U2-1)+V2$(U3,U[U9]),DOM=1021)
1025 EXITTO 1000
1030 IF U$(U2,U[U9])<=V2$(U3+U[U9],U[U9]) THEN GOTO 1040 ELSE READ (U1,KEY=U$(1,U2-1)+$FF$,DOM=1032)
1035 EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1180 IF V0$(80,1)="Y" THEN GOSUB 7500
1200 IF A[13]=0 THEN GOTO 1000
1300 REM "Read other data
1330 DIM C$(LEN(C$)); FIND (Z[3],DOM=1331,KEY=A$(1,10))IOL=0330
1340 IF V0$(81,1)="Y" AND POS(" "<>C$(209,12))<>0 THEN GOTO 1000
1400 REM "Subtotals"
1415 GOSUB 3100
1420 ON T GOTO 1470,1430,1440,1450,1460
1430 REM GOSUB 6200
1440 GOSUB 6300
1460 GOSUB 6200
1490 T=0,V9$=A$(1,10)
1500 REM "Print Data
1510 IF W+1>W0 THEN GOSUB 6000
1515 Y5$(1)=A$(11,10)
1520 IF X3$(69,1)<>"Y" THEN Y5$(12)=FND$(A$(21,6)) ELSE CALL "ZZDISP","DX",A$(21,6),"   ",X3$,X$,"",0,0,X4$; Y5$(12)=X$
1525 IF X3$(69,1)<>"Y" THEN Y5$(24,5)=FND$(A$(29,6)) ELSE CALL "ZZDISP","DX",A$(29,6),"   ",X3$,X$,"",0,0,X4$; Y5$(24)=X$(1,5)
1529 DISC_DATE$=A$(35,6),USING_2ND$="N"; IF POS(" "<>A$(125,6))<>0 THEN IF A$(35,6)<X3$(15,6) THEN DISC_DATE$=A$(125,6),USING_2ND$="Y"; REM "WO131561, 2nd discount date/amount
1530 IF POS(" "<>DISC_DATE$)<>0 THEN IF X3$(69,1)<>"Y" THEN Y5$(31,5)=FND$(DISC_DATE$) ELSE CALL "ZZDISP","DX",DISC_DATE$,"   ",X3$,X$,"",0,0,X4$; Y5$(31)=X$(1,5)
1531 IF USING_2ND$="Y" THEN Y5$(37,1)="*"
1532 D0$=A$(21,6); IF V0$(78,1)="D" THEN D0$=A$(29,6)
1535 Q=POS(D0$<=D1$,6); IF Q<>0 THEN Q=4-INT(Q/6)
1555 Y5$(37+Q*(LEN(Z0$)+1))=STR(A[13]:Z0$),Y5$(117)=STR(A[13]:Z0$)+D1$
1590 GOSUB 7300
1900 REM "Accumulate Totals"
1910 T[T0,Q+1]=T[T0,Q+1]+A[13],T[T0,6]=T[T0,6]+A[13]
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,X6$,"",0,0,X4$
3075 T6$=X6$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; GOSUB 7000
3155 IF U0$(1,1)="Y" THEN GOSUB 6000
3160 U1$=U9$(1,V1)
3180 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$,T6$=V9$; GOSUB 3050; GOSUB 7000
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3260 U2$=U9$(V5,V2)
3280 RETURN 
3380 RETURN 
3480 RETURN 
5000 REM "End of Print
5010 IF W0=99999999999999 THEN GOTO 5030
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; GOSUB 7000
5200 REM "
5290 GOTO 9900
6000 REM "Page header
6035 IF T0$="END" THEN W0$=""
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6060 ELSE ON W5 GOTO 6041,9910
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6115 Y5$(14)="Invoice",Y5$(32)="Disc.",Y5$(123)="Vendor"
6120 GOSUB 7300
6125 Y5$(1)="Invoice",Y5$(15)="Date",Y5$(26)="Due",Y5$(33)="Exp",Y5$(45)="Future",Y5$(60)="Current",Y5$(76)="30 Days",Y5$(92)="60 Days",Y5$(108)="90 Days",Y5$(123)="Balance"
6130 GOSUB 7300
6199 RETURN 
6210 IF W+2>W0 THEN GOSUB 6000
6215 GOSUB 7300
6220 Y5$(1)="Vendor Code:"
6225 CALL "ZZDISP","AX",C$(1,10),"A/P",X3$,X$,"",0,0,X4$; Y5$(14)=X$
6230 Y5$(16+LEN(X$))=C$(11,35)
6235 GOSUB 7300; GOSUB 7300
6299 RETURN 
6300 REM "Break #2 header
6399 RETURN 
7000 REM "Totals logic
7001 IF T=T0 THEN IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7002 IF T=1 THEN IF P0$(13,1)<>"Y" THEN GOTO 7100
7004 IF W+3>W0 THEN GOSUB 6000
7005 FOR Q=0 TO 5; Y5$(37+Q*(LEN(Z0$)+1))=T1$(1,15); NEXT Q; GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" Total ("+STR(T[T,0]:"0")+"):"
7020 FOR Q=0 TO 5; Y5$(37+Q*(LEN(Z0$)+1))=STR(T[T,Q+1]:Z0$); NEXT Q
7030 GOSUB 7300
7065 IF T$="END" THEN Y5$(37)=T2$(1,15); GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed: "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 IF INT(T2)>0 THEN T4$=STR(INT(T2))+" Hours " ELSE T4$=""
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+STR(T3:"##.##")+" Minutes"
7260 Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7330 REM 
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7455 GOSUB 7700
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "Get transaction data set Inv Bal to Orig amount then adjust by trans
7505 A[13]=A[10]; IF A$(94,6)>V0$(72,6) THEN A[13]=0
7510 D0=A[0],FLAG$="",SEQ$=""; DIM D[1]; D$="" ! SSP#273735
7520 IF FLAG$="END" THEN GOTO 7570
7530 CALL "AR2XAB",X3$,X4$,Z[4],A$,D0,D$,D{ALL},FLAG$,SEQ$,"R"
7535 IF FLAG$="NONE" THEN GOTO 7570
7540 IF D$(37,6)<=V0$(72,6) THEN A[13]=A[13]+D(0)
7560 GOTO 7520
7590 RETURN 
7700 REM "PRINT P/E DATE"
7710 DIM P1$(105); FIND (Z[13],KEY=X3$(9,3)+"G/LYE"+V0$(72,4),DOM=7740)P1$
7720 D1$=P1$(NUM(V0$(76,2))*6+15,6),D0$=D1$
7725 Q$=D1$; FOR X=1 TO 3; CALL "ZZDATE",X3$,"",Q$,Q1$,-30,0,0,0,0,0,0,"","",""; D1$=Q1$+D1$,Q$=Q1$; NEXT X
7730 W0$="Fiscal year "+V0$(72,4)+", period "+V0$(76,2)+" ending "+FND$(D0$); IF V0$(80,1)="Y" THEN W0$="As of "+W0$
7735 W1$="Aging based on Invoice Date"; IF V0$(78,1)="D" THEN W1$="Aging based on Invoice Due Date"
7740 W1$=W1$+" - As of period end date"
7745 RETURN 
7750 REM "Get months dates
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
