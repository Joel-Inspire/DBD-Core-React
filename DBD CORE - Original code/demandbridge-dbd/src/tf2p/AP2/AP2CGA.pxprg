0010 REM "A/P Check printing <AP2CGA>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 02/09/21 - 14.628374 - jvv - SSP# 307332
0036 REM "10/15/91 ADDED LINE 8057 FOR DOCUTEC
0037 REM "307332-Create combined pdf of checks in a batch                    
0040 REM "Copyright 2021 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 BEGIN ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0102 CALL "ZZDAPP",X3$,"DZZFLESZZCOMP"
0110 X0$="AP2CGA",X1$="A/P Check Printing"
0120 DIM Z1$(80,"-"),A[5],Z$(10,"0")
0135 C9=-1,V9=-2
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0180 DEF FNP(Z0,Z1)=(Z1-1)*W0+Z0
0185 DEF FNS$(Z9$)=Z9$(1,POS("  "=Z9$+"  ")-1)
0190 DEF FNJ$(Z9$)=Z9$(POS(" "<>Z9$))
0200 REM "
0235 X3$=""
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0246 IF POS(X3$(9,3)="096482490",3)<>0 THEN RUN "AP2CGB"
0290 K9$="",K9=LEN(K9$)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5]
0320 IOLIST B$,B[0],B[1],B[2]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20],C[21],C[22],C[23],C[24],C[25],C[26],C[27]
0350 IOLIST E$,E[0],E[1],E[2]
0360 IOLIST Y$,Y[0],Y[1],Y[2],Y[3],Y[4],Y[5],Y[6],Y[7],Y[8],Y[9],Y[10],Y[11],Y[12],Y[13],Y[14],Y[15],Y[16],Y[17],Y[18],Y[19],Y[20],Y[21],Y[22],Y[23],Y[24],Y[25],Y[26],Y[27],Y[28],Y[29],Y[30],Y[31],Y[32],Y[33],Y[34],Y[35],Y[36],Y[37],Y[38],Y[39],Y[40],Y[41],Y[42] ! WO242552
0370 IOLIST G$
0375 IOLIST APZ$; REM "SSP# 160286
0380 IOLIST H$,H[0],H[1],H[2]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01L APH...  02O AP4...  06O ZZPARM  07O ZYB...  08O APM...  "
0512 Z$=Z$+"03O API...  04O APK...  05L APC...  09L APG...  10O ZY2     11O APB...  12O APZ...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0575 READ (Z[6],KEY=X3$(9,3)+"A/P")F0$
0580 DIM Y$(231),Y[42]; READ (Z[6],KEY="A/PCKFA",DOM=0581)IOL=0360; GOTO 0590 ! WO252442
0585 READ (Z[6],KEY="A/PCKFS")IOL=0360
0590 A$=F0$(7,6); GOSUB 7600
0595 IF LEN(Y$)<>231 THEN Y$=Y$+DIM(231-LEN(Y$)); REM "WO131772, some formats coming in short, no easy way to expand for change since in ZZPARM. WO252442 215 changed to 231
0596 READ (Z[12],KEY="",DOM=*NEXT)
0597 K$=KEY(Z[12],END=*NEXT); USE_APZ=1
0600 REM "
0620 GOSUB 6000
0700 REM "Resort?
0705 F0=Z[1],F1=Z[9]
0710 READ (Z[9],KEY="",DOM=0711); GOTO 0720
0712 F4=0,F5=0; APZKEY$=KEY(Z[12],END=*NEXT); F4=Z[12],F5=Z[2]; READ (Z[12],KEY="",DOM=*NEXT)
0715 CALL "AP2CDB",X3$,F0,F1,F2,F3,F4,F5
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1011 DIM B$(426),B[2] ! WO252442
1012 DIM A$(62); A$(1,6)=P5$,FILE$="N"
1037 IF R0=0 THEN READ (Z[1],END=1038)APH$; PRINT @(1,7+V0),"Bank code "+APH$(4,3),; DIM BANK_CODE$(150); READ (Z[7],KEY=APH$(4,3),DOM=1038)BANK_CODE$; A$(13+K9,6)=STR(NUM(BANK_CODE$(93,6))+1:"000000",ERR=1038); REM "Default starting check number
1038 DEF_CHECK$=""; IF R0=0 THEN IF U$>"" THEN DEF_CHECK$=A$(13+K9,6); READ (Z[1],KEY=U$(1,6),DOM=1039) ELSE READ (Z[1],KEY="",DOM=1039); REM "Set pointer back to correct position
1040 IF MID(BANK_CODE$,132,1)<>"" THEN IF BANK_CODE$(132,1)<>" " THEN READ (Z[6],KEY="A/PCKF"+BANK_CODE$(132,1))IOL=0360; IF LEN(Y$)<>231 THEN Y$=Y$+DIM(231-LEN(Y$)) END_IF ; IF X3$(9,3)="377" AND BANK_CODE$(1,3)="SRA" THEN W0=90 ! WO252442, check length of Y$
1045 PRINT 'CF',
1100 REM "KEY SEQ
1110 X0=1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1500
1200 REM "
1215 IF C0<0 THEN GOTO 9900
1500 REM "
1505 Z9=-1
1510 A1$=X3$(9,3)+"APC"+X3$(174,4),A$(22,10)=Z$
1515 READ (Z[6],KEY=X0$,DOM=1516)R0$
1516 READ (Z[6],KEY=A1$,DOM=1600)A$,R0$
1521 READ (Z[6],KEY=X0$,DOM=1522)R0$
1522 IF A$(47,6)="      " THEN GOTO 1600
1525 Z9=0,R0=1
1526 IF A$(47,6)="ZZZZZZ" THEN GOTO 9600 ELSE A$(13,6)=STR(NUM(A$(47,6))+1:"000000")
1530 FIND (Z[7],KEY=A$(22+K9,3),DOM=1531)IOL=0370
1535 FIND (Z[2],KEY=A$(25+K9,10),DOM=1536)IOL=0320
1550 GOSUB 6000; GOSUB 6200
1590 GOTO 1700
1600 REM 
1605 A$(1,6)=P5$
1607 IF DEF_CHECK$>"" THEN A$(13+K9,6)=DEF_CHECK$,DEF_CHECK$=""
1610 GOSUB 6200
1660 GOTO 2000
1700 REM "
1800 REM "
1801 IF A$(7,6)=DIM(6) THEN GOTO 2200; REM "DON'T LET THEM HAVE A BLANK CHECK DATE
1805 CALL "ZZPROM","X0AP2CGA",X3$,S3,"Ready to proceed with check printing","","",0
1885 ON S3 GOTO 2800,9900,2000,7550
1890 GOTO 2800
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF C0>3 OR X0<0 THEN GOTO 1800
2035 IF X0*6+11<LEN(V9$) THEN IF V9$(X0*6+11,1)="2" THEN GOTO 2020
2040 ON X0 GOTO 2100,2150,2200,2250,2300,2350,2400,2450,2500,1800
2100 REM "Fiscal Year for G/L update
2110 CALL "ZZENTR","ZX",A{ALL},A$,X4$,X3$,38,3+V0,1+K9,4,C0,"",X$,K9$,"AP2XXX00","","",""
2130 FIND (Z[6],DOM=2110,KEY=P0$(1,6)+"YE"+A$(1,4))P1$; GOSUB 7720
2140 GOTO 2020
2150 REM "Acctg Period
2160 CALL "ZZENTR","ZX",A{ALL},A$,X4$,X3$,64,3+V0,5+K9,2,C0,"",X$,"","AP2XXX02","","",""
2165 IF A$(5+K9,2)<"01" OR A$(5+K9,2)>P1$(13,2) THEN GOTO 2150
2170 IF C0>0 THEN GOSUB 7700
2171 FLAG$=""
2172 IF ABS(C0)=2 THEN X0=X0-2; GOTO 2020
2173 IF C0<>0 THEN CALL "AR2DTE",X3$,X4$,A$(1+K9,6),FLAG$
2180 IF FLAG$="*" AND ABS(C0)>1 THEN X0=X0-2; GOTO 2020
2182 IF FLAG$="*" AND ABS(C0)=1 THEN X0=X0-1; GOTO 2020
2190 GOTO 2020
2200 REM "Check Date
2210 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,38,5+V0,7+K9,6,C0,"",X$,"","AP2XXX04","","",""
2215 IF A$(7,6)=DIM(6) THEN GOTO 2210; REM " DON'T LET THEM HAVE A BLANK CHECK DATE
2240 GOTO 2020
2250 REM "Starting check number
2255 X$="Defaulted using bank code's last check number used + 1, please verify."
2260 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,38,7+V0,13+K9,6,C0,"",X$,"","AP2XXX06","","",""
2290 GOTO 2020
2300 REM "Restart in Source Company
2305 IF R0=0 THEN GOTO 2020
2310 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,38,10+V0,19+K9,3,C0,"",X$,"","AP2XXX08","","",""
2340 GOTO 2020
2350 REM "Restart bank code
2355 IF R0=0 THEN GOTO 2020
2360 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,38,12+V0,22+K9,3,C0,"",X$,"","AP2XXX10","ZYB","ZY2BAA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2360,2361
2370 FIND (Z[7],KEY=A$(22+K9,3),DOM=2360)IOL=0370
2375 PRINT @(43+V0+L0,12+V0+L0),G$(4,30),
2390 GOTO 2020
2400 REM "Vendor Code
2405 IF R0=0 THEN GOTO 2020
2410 CALL "ZZENTR","A",A{ALL},A$,X4$,X3$,38,14+V0,25+K9,10,C0,"A/P",X$,"","AP2XXX12","AP4","AP2MAA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2410,2411
2420 FIND (Z[2],KEY=A$(25+K9,10),DOM=2410)IOL=0320
2425 PRINT @(38+V0+L0,16+V0+L0),B$(11,35),
2440 GOTO 2020
2450 REM "Check Code
2455 IF R0=0 THEN GOTO 2020
2460 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,62,14+V0,35+K9,1,C0,"",X$,"","AP2XXX14","","",""
2490 GOTO 2020
2500 REM "Reference number, used for file name, b/r summary record, written to payment records
2503 IF MID(BANK_CODE$,131,1)<>"Y" THEN GOTO 2020 ELSE PRINT @(50,7+V0),"Reference:",
2505 X$="B/R summary record, output file name, written to payment records, see E#3025"
2510 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,61,7+V0,54+K9,9,C0,"",X$,"","AP2XXX14","","",""
2540 GOTO 2020
2800 REM "Start Check Printing
2810 FLAG$="",VOID_CHECK=0
2820 CALL "AR2DTE",X3$,X4$,A$(1+K9,6),FLAG$
2830 IF FLAG$="*" THEN GOTO 9900
2870 IF R0$="" THEN GOSUB 7500
2880 CALL "ZZPRIN",X0$,X3$,Y0$,R0$,Z{ALL},H0$,H1$,H0,H1,H2,H3,H4; ON H4 GOTO 2881,9900
2884 IL_P=POS("IL>"=R0$(65,60)); IF IL_P<>0 THEN IL_DEV$="",IL_DEV$=MID(R0$,67+IL_P,2)
2885 JETFORM_MODE=0,X$=FID(H1); DIM F_CODE$(11); F_CODE$(1)="APCHECK",F_CODE$(11,1)=Y$(7,1); CALL "EF2PRM",ERR=2886,X3$,X4$,X$,P6$,F6$,F_CODE$,ITEM_PARM$; IF P6$(144,2)="JF" THEN JETFORM_MODE=1,JETFORM_DEV=H1; GOSUB 4850; FIRST_TIME=1; CALL "EF2JET",X3$,X4$,P6$,"N",JETFORM_DEV,ITEM_PARM$,JOB_MODIFIER$,RET$; ELSE IF P6$(144,2)="UF" THEN UNFORM_MODE=2,UNFORM_DEV=H1; FIRST_TIME=0 ! REM "start new item, set FIRST_TIME so we don't do eject before first item. SSP 237164 SSP254469
2886 IF JETFORM_MODE AND MID(R0$,65,4)="PDF>" THEN GOSUB SETUP_FOR_ARCHIVE
2887 DIM R_CODE$(11); REMIT_FLAG=0,REMIT_PARM$=""; IF UNFORM_MODE THEN R_CODE$(1)="APREMIT",R_CODE$(11,1)=Y$(7,1); CALL "EF2PRM",ERR=*NEXT,X3$,X4$,X$,P6$,F6$,R_CODE$,REMIT_PARM$; IF NOT(NUL(REMIT_PARM$)) THEN REMIT_FLAG=1 ! SSP254469
2890 PRINT (H1)"          " ! Was IF X3$(9,3)<>"008" THEN !SSP#229515  
2905 V$="Void  Void  Void **",P0=0
2910 C0$=A$
2911 GOSUB 7400 ! SSP#266982
2912 IF R0>0 THEN EXTRACT (Z[1],KEY=C0$(19,17),DOM=2913)
2914 C0=NUM(C0$(13,6)),T$="TEST"
2916 IF R0>0 THEN GOSUB 4200; REM " POSITION IN SORT FILE
2920 IF G$="" THEN Q$=KEY(Z[1],END=9800); Q$=Q$(1,6); GOSUB 7900
2925 DIM E$(30),E[2]
2930 IF R0$(2,2)="A " THEN PRINT 'EA',
2940 CALL "ZZPROM","YE",X3$,S3,"Do you wish to print a test pattern?","","",0; ON S3 GOTO 2941,3000,9900
2942 IF S3<2 AND R0$(2,2)="A " THEN PRINT 'BA',
2945 DIM B$(426,"X"),C$(140,"X"),C[20] ! WO252442
2946 C$(21,6)="I60000",P0=0
2950 M0$="*###,###.00-",M1$="*#,###.00-"
2955 GOSUB 8000
2965 FOR X=1 TO Y[2]; GOSUB 3200; NEXT X; GOSUB 3900
2970 VOID_CHECK=1; GOSUB 8700; GOSUB 8900; REM "Not needed since VOID_CHECK added  GOSUB 08800
2975 C0=C0+1
2976 IF FID(H1)<>FID(0) THEN PRINTER$=FID(H1); CLOSE (H1); OPEN (H1)PRINTER$
2980 IF JETFORM_MODE THEN IF FIRST_TIME THEN FIRST_TIME=0 ELSE CALL "EF2JET",X3$,X4$,P6$,"N",JETFORM_DEV,ITEM_PARM$,JOB_MODIFIER$,RET$ ! SSP254469
2985 IF JETFORM_MODE THEN FIRST_TIME=1 ! SSP 237164
2990 GOTO 2930
3000 REM "
3002 IF R0$(2,2)="A " THEN PRINT 'BA',
3005 M0$="####,###.00-",M1$="##,###.00-",M2$="######.00",T$="CHECK",P0=0,P1=1,U9$=""
3006 CONT_IMG$="",EOV$="N" ! SSP 217412
3007 GOSUB 6700
3010 DIM T[10]
3015 COMBINE_PDF$="N",CPKY$=""; IF MID(G$,149,1)="Y" THEN COMBINE_PDF$="Y",CPKY$=COMBINE_PDF$+G$(1,3) ! SSP 307332 Set CPKY$ for combined pdf +BANK CODE$
3019 IF G$(131,1)="Y" THEN GOSUB 8500; REM "Create text file & open, WO131772
3020 IF USE_APZ=0 THEN GOTO 3040
3021 APZ$=KEY(Z[12],END=5000); READ (Z[12])
3022 U$=APZ$(1,6)+APZ$(42,10)+APZ$(52,11); IF G$>"" THEN IF G$(1,3)<>U$(4,3) THEN EOV$="Y"; GOSUB 4000; Q$=U$,C0$(19)=U$,C0$(47,6)=""; GOSUB 8455; GOSUB 7900; GOTO 0800; REM " SSP$ 160286 SSP 217412
3023 GOTO 3042
3040 U$=KEY(Z[1],END=5000); IF G$>"" THEN IF G$(1,3)<>U$(4,3) THEN EOV$="Y"; GOSUB 4000; Q$=U$,C0$(19)=U$,C0$(47,6)=""; GOSUB 8455; GOSUB 7900; GOTO 0800 ! SSP 217412
3042 IF U$(1,LEN(U9$))<>U9$ THEN EOV$="Y"; GOSUB 4000 ! SSP217412
3045 READ (Z[1],KEY=U$,DOM=3020)IOL=0310; U8$=A$(1,17)
3050 IF P0=Y[2] THEN GOSUB 3800 ELSE IF U9$="" THEN GOSUB 8000
3070 DIM C[27]; READ (Z[3],KEY=A$(7,10)+A$(18,10))IOL=0330
3100 REM "Next Invoice
3120 GOSUB 3200
3180 GOTO 3020
3200 REM "LOAD LINE VALUES
3205 REM "If alternate currency the substitute alternate values for regular values and  let the rest of the program logic use the alternate values
3206 IF POS(" "<>A$(34,3))<>0 THEN A[0]=A[2],A[1]=A[3]
3210 Y5=FNP(0,Y[1]+P0)
3215 Q$=C$(11,10); IF Y[12]>0 THEN IF JETFORM_MODE THEN JF_FIELD=12,DATA$=Q$; GOSUB 4800 ELSE Y3$(Y5+Y[12]+10-LEN(Q$),LEN(Q$))=Q$
3220 IF Y[13]>0 THEN IF JETFORM_MODE THEN JF_FIELD=13,DATA$=FND$(C$(21,6)); GOSUB 4800 ELSE Y3$(Y5+Y[13],10)=FND$(C$(21,6))
3225 IF Y[14]>0 THEN IF JETFORM_MODE THEN JF_FIELD=14,DATA$=STR(A[0]:M0$); GOSUB 4800 ELSE Y3$(Y5+Y[14],LEN(M0$))=STR(A[0]:M0$)
3230 IF Y[15]>0 THEN IF JETFORM_MODE THEN JF_FIELD=15,DATA$=STR(A[1]:M1$); GOSUB 4800 ELSE Y3$(Y5+Y[15],LEN(M1$))=STR(A[1]:M1$)
3235 IF Y[16]>0 THEN IF JETFORM_MODE THEN JF_FIELD=16,DATA$=STR(A[0]-A[1]:M0$); GOSUB 4800 ELSE Y3$(Y5+Y[16],LEN(M0$))=STR(A[0]-A[1]:M0$)
3240 IF Y[17]>0 THEN IF JETFORM_MODE THEN JF_FIELD=17,DATA$=C$(41,15); GOSUB 4800 ELSE Y3$(Y5+Y[17],15)=C$(41,15)
3245 IF Y[18]>0 THEN IF JETFORM_MODE THEN JF_FIELD=18,DATA$=C$(56,10); GOSUB 4800 ELSE Y3$(Y5+Y[18],10)=C$(56,10)
3400 REM "Totals
3405 T[0]=T[0]+1,P0=P0+1
3410 T[1]=T[1]+A[0],T[2]=T[2]+A[1]
3415 T[3]=T[3]+A[0]-A[1]
3490 RETURN 
3500 REM "DECIDE HOW LONG THE ALPHA NAME SHOULD BE
3505 ADJUST=0
3510 PAY_EXACTLY_LEN=W0-Y[24]-1
3520 FOR X=29 TO 38
3521 IF X=34 THEN GOTO 3540
3530 IF Y[X]=Y[34] THEN ADJUST=Y[X-10]-Y[24]-1
3540 NEXT X
3550 IF ADJUST<>0 THEN PAY_EXACTLY_LEN=ADJUST
3699 RETURN 
3700 REM "
3780 GOTO 3100
3800 REM "CONTINUATION
3815 Q$="Page: "+STR(P1:"###"); COL=Y[16],LINE=Y[3]+2,DATA$=Q$,JF_FIELD$="PAGE"; GOSUB 8300
3820 IF NOT(REMIT_FLAG) OR (REMIT_FLAG AND NOT(EJECT_REMIT)) THEN COL=Y[16],LINE=Y[3],JF_FIELD$="CONT",DATA$="* Continued *"; GOSUB 8300 ! SSP254469
3825 ! 1=P1+1
3830 VOID_CHECK=1; GOSUB 8700; REM "Not needed since VOID_CHECK added GOSUB 08800
3860 GOSUB 8900
3865 P1=P1+1 ! SSP 217412
3875 P0=0,Y3$((Y[1]-1)*W0)="",Y3$(FNP(Y[28],Y[38]),20)=" "; REM "No jetform, because clearing space not needed
3890 CONT$="Y"; GOSUB 8400; CONT$="N"; REM "SSP137205
3891 IF JETFORM_MODE THEN IF Y[10]>0 THEN IF Y$(38,1)="Y" THEN JF_FIELD=10,DATA$=Q$; GOSUB 4800 ELSE JF_FIELD=10,DATA$=STR(C0:"000000"); GOSUB 4800; JF_FIELD$="DATE",DATA$=FND$(C0$(7,6)); GOSUB 4800 END_IF END_IF ; GOTO 3897; REM "If jetform skip these special case thing, just print once if blank stub (y$(38,1)="Y") or twice otherwise
3892 IF POS(X3$(9,3)="042047048")=0 THEN Q$=FNS$(Y$(71,16))+": "+STR(C0:"000000"); IF Y[10]>0 THEN IF Y$(38,1)="Y" THEN Y3$(FNP(Y[10],Y[11]),LEN(Q$))=Q$ ELSE Y3$(FNP(Y[10],Y[11]),6)=STR(C0:"000000"),Y3$(FNP(Y[10],Y[11])-18,10)=FND$(C0$(7,6)) ! SSP#284854
3893 IF X3$(9,3)="042" THEN Q$=FNS$(Y$(71,16))+": "+STR(C0:"000000"); IF Y[10]>0 THEN IF Y$(38,1)="Y" THEN Y3$(FNP(Y[10],Y[11]),LEN(Q$))=Q$ ELSE Y3$(FNP(Y[10],Y[11]),6)=STR(C0:"000000"),Y3$(FNP(Y[10],Y[11])-12,10)=FND$(C0$(7,6))
3894 IF POS(X3$(9,3)="047048",3)>0 THEN Q$=FNS$(Y$(71,16))+": "+STR(C0:"000000"); IF Y[10]>0 THEN IF Y$(38,1)="Y" THEN Y3$(FNP(Y[10],Y[11]),LEN(Q$))=Q$ ELSE Y3$(FNP(Y[10],Y[11]),6)=STR(C0:"000000"),Y3$(FNP(Y[10],Y[11])-13,10)=FND$(C0$(7,6))
3895 ! IF POS(X3$(9,3)="020376",3)<>0 THEN Y3$(FNP(Y[10],Y[11])-18,10)=""!SSP#285773
3896 Q$=FNS$(Y$(71,16))+": "+STR(C0:"000000"); IF Y[10]>0 THEN IF Y$(38,1)="Y" THEN Y3$(FNP(Y[10],Y[11]),LEN(Q$))=Q$ ! ELSE IF POS(X3$(9,3)="468",3)<>0 THEN Y3$(FNP(Y[10],Y[11]),6)=STR(C0:"000000"),Y3$(FNP(Y[10],Y[11])-W0*3,10)=FND$(C0$(7,6))!SSP#284854       
3899 RETURN 
3900 REM "Set totals
3905 IF Y$(38,1)="Y" THEN JF_FIELD=12,COL=Y[12],LINE=Y[3],JF_FIELD$="TOTAL12",DATA$="Totals:   "; GOSUB 8300
3910 Q$=STR(T[1]:M0$); JF_FIELD=14,COL=Y[14],LINE=Y[3],JF_FIELD$="TOTAL14",DATA$=Q$; GOSUB 8300
3920 Q$=STR(T[2]:M1$); JF_FIELD=15,COL=Y[15],LINE=Y[3],JF_FIELD$="TOTAL15",DATA$=Q$; GOSUB 8300
3930 Q$=STR(T[3]:M0$); JF_FIELD=16,COL=Y[16],LINE=Y[3],JF_FIELD$="TOTAL16",DATA$=Q$; GOSUB 8300
3940 IF P1>1 THEN Q$="Page: "+STR(P1:"###"),COL=Y[16],LINE=Y[3]+2,JF_FIELD$="PAGE",DATA$=Q$; GOSUB 8300
3990 RETURN 
4000 REM "PRINT CHECK
4002 IF REMIT_FLAG THEN IF P1>1 THEN EOV$="N",EJECT_REMIT=1; GOSUB 3900; GOSUB 3800; EOV$="Y",EJECT_REMIT=0; P1=1 ! SSP254469 - Reset page number to 1 for real check here
4005 IF X3$(9,3)="496" THEN WAIT 1
4010 P9$=B$(11,35)
4020 IF U9$<>"" THEN GOSUB 3900; GOSUB 8700; GOSUB 8900 ELSE GOTO 4085
4025 GOSUB 8400
4085 U9$="",P1=1,P0=0
4088 CONT_IMG$="",EOV$="N" ! SSP 217412
4095 RETURN 
4100 REM "UK style 'Pay exactly'
4110 DIM Q$(50)
4114 REM "SORRY, but must print checks >=100,000 with digits for 10's of thousands rather than spelled out
4115 IF EPT(T[3])>5 THEN Q$(1,10)=STR(INT(T[3]/10000)),Q2$=STR(INT(T[3])-INT(T[3]/10000)*10000:"00000") ELSE Q2$=STR(INT(T[3]):"00000")
4130 FOR I=1 TO 5
4135 IF I=1 AND EPT(T[3])>5 THEN GOTO 4160
4140 READ (Z[10],KEY=" 0"+Q2$(I,1))Q3$
4150 Q$((I-1)*10+1)=Q3$(4,10)
4160 NEXT I
4170 JF_FIELD=24,COL=Y[24],LINE=Y[34],DATA$=Q$; GOSUB 8300
4190 RETURN 
4200 REM POSITION IN SORT FILE
4201 IF USE_APZ=0 THEN RETURN 
4205 READ (Z[12],KEY=C0$(19,6),DOM=*NEXT); REM "200390
4210 APZ$=KEY(Z[12],END=5000)
4215 IF APZ$(42,10)<>C0$(25,10) THEN READ (Z[12]); GOTO 4210
4299 RETURN 
4800 JETFORM_PRINT:REM "Add to JF$ for printing later, using JF_FIELD, JF_FIELD$, and DATA
4801 REM "if JF_FIELD$ is set use it, else str JF_FIELD to JF_FIELD$, DATA$ is print data, clear JF_FIELD$ and DATA$ after
4802 REM "If JF_FIELD$ is on repeat list, then print it again with a 2 on the end of the field, Check both numeric and alpha field names
4810 IF JF_FIELD$="" THEN CHECK_FIELD$=STR(JF_FIELD:"0000"),JF_FIELD$=STP(P6$(146,3),1)+STR(JF_FIELD:"000") ELSE CHECK_FIELD$=JF_FIELD$; REM "Set check field for use in checking to repeat field, it is either JF_FIELD$ or 4 character JF_FIELD if it was used
4815 JF$=JF$+"^field "+JF_FIELD$+'LF'+DATA$+'LF'
4820 IF POS(CHECK_FIELD$="TOTAL12TOTAL14TOTAL15TOTAL16",7) OR POS(CHECK_FIELD$="PAGECONTDATE0004000600080012001300140015001600170018",4)<>0 THEN JF$=JF$+"^field "+JF_FIELD$+"2"+'LF'+DATA$+'LF'
4840 JF_FIELD$="",DATA$=""
4845 JETFORM_PRINT_END:RETURN 
4850 SET_JOB_MODIFIER:REM "Set job_modifier$ to the format code used,job_modifier$ will be passed to EF2JET
4855 JOB_MODIFIER$=Y$(7,1)
4895 SET_JOB_MODIFIER_END:RETURN 
5000 REM "END
5010 J$=%DATAPATH$
5015 EOV$="Y" ! SSP217412
5020 GOSUB 4000
5025 IF R0$(2,2)="A " THEN PRINT 'EA',
5030 IF FID(H1)<>FID(0) THEN PRINTER$=FID(H1); CLOSE (H1); IF NOT(%PRINTING_IN_VIEWER) THEN OPEN (H1)PRINTER$
5040 IF TEXT_FILE$>"" THEN CLOSE (32502)
5042 IF TEXT_FILE$>"" THEN ERASE HWD+DLM+"bacs"+DLM+TEXT_FILE$,ERR=5043
5044 IF TEXT_FILE$>"" THEN RENAME J$+DLM+TEXT_FILE$ TO HWD+DLM+"bacs"+DLM+TEXT_FILE$
5050 CALL "ZZPROM","Y",X3$,S3,"Did the checks print correctly?","","",0
5060 ON S3 GOTO 5070,0010
5070 IF MEM_FL<>0 THEN GOSUB CREATE_PDF ! SSP 307332
5100 REM "WRITE DONE RECORD
5120 C0$(53,1)="3"
5130 WRITE (Z[6],KEY=A1$)C0$
5150 CALL "ZZPROM","5",X3$,S3,"Please load stock paper into printer and print Cash Disbursements Journal","","",0
5240 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(0,3+V0),"Update General Ledger in Fiscal Year:",@(45,3+V0),"Accounting period:",@(26,5+V0),"Check Date:",@(15,7+V0),"Starting check number:",@(0,9+V0),Z0$,
6062 REM PRINT @(50,7+V0),"Reference:",
6065 IF R0=0 THEN GOTO 6190
6070 PRINT @(11,10+V0),"Restart in Source Company:",@(16,12+V0),"Restart in bank code:",@(25,14+V0),"Vendor Code:",@(50,14+V0),"Check Code:",@(25,16+V0),"Vendor name:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 PRINT @(38,3+V0),A$(1+K9,4),@(64,3+V0),A$(5+K9,2),
6224 CALL "ZZDISP","D",A$(7+K9,6),"",X3$,"","",38,5+V0,X4$
6226 IF R0=0 AND BANK_CODE$>"" THEN PRINT @(1,7+V0),"Bank code "+BANK_CODE$(1,3), ELSE PRINT @(1,7+V0),DIM(13),
6227 IF POS(" "<>A$(54+K9,9))>0 THEN PRINT @(50,7+V0),"Reference: "+A$(54+K9,9),
6228 PRINT @(38,7+V0),A$(13+K9,6),@(61,7+V0),; IF R0=0 THEN GOTO 6290 ELSE PRINT @(38,10+V0),A$(19+K9,3),@(38,12+V0),A$(22+K9,3),@(43+V0+L0,12+V0+L0),G$(4,30),
6232 CALL "ZZDISP","A",A$(25+K9,10),"A/P",X3$,"","",38,14+V0,X4$
6236 PRINT @(38+V0+L0,16+V0+L0),B$(11,35),@(62,14+V0),A$(35+K9,1),
6290 GOSUB 7700
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6490 RETURN 
6500 REM "DELETE
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY
6705 A$(53,1)="2"
6707 IF R0$="" THEN GOSUB 7500
6710 WRITE (Z[6],KEY=A1$)A$,R0$
6720 WRITE (Z[6],KEY=X0$)R0$
6790 RETURN 
6800 REM "NEXT ENTRY IN FILE
6810 A1$=KEY(Z[1],END=1140)
6820 A$(1,LEN(A1$))=A1$
6825 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 1140
6830 GOSUB 6600
6835 GOSUB 6450
6890 GOTO 1515
6900 REM "GET DEFAULTS
6902 V9$="1403  3403  1404  3404  6204  "
6910 IF V9>=0 THEN READ (Z[6],IND=V9)V9$,IOL=0310; GOTO 6930 ELSE V9=-1
6920 EXTRACT (Z[6],KEY=X0$+"DFLT",DOM=6921)V9$,IOL=0310; V9=IND(Z[6]); READ (Z[6])
6930 RETURN 
6950 REM "WRITE DEFAULT
6955 WRITE (Z[6],KEY=X0$+"DFLT")V9$,IOL=0310
6960 GOSUB 6920; GOTO 1630
6970 REM "ENTRY DEFAULTS
6975 PRINT 'CF'; V9=-2,Q$=""
6980 FOR X=1 TO LEN(V9$) STEP 6
6982 PRINT @(NUM(V9$(X,2)),NUM(V9$(X+2,2))),; IF V9=-2 THEN PRINT V9$(X+4,1), ELSE READ RECORD (0,SIZ=1)Q$; IF POS(Q$="1234Z")=0 THEN PRINT 'RB'; GOTO 6982
6990 IF Q$="Z" THEN EXITTO 6950 ELSE V9$(X+4,LEN(Q$))=Q$; NEXT X
6995 V9=0; GOTO 6980
7400 REM "Audit already run
7410 FIND (Z[5],KEY="",DOM=7490)V0$,K0$ ! SSP#266982
7420 IF K0$>"" THEN CALL "ZZPROM","4",X3$,S3,"The update has already been initiated. Press |Return>","","",0; GOTO 9900
7430 EXTRACT (Z[5],KEY="",ERR=9800) ! SSP#266982
7440 Q$="Audit report "+V0$(22,4)+"."+V0$(26,2)+" has already been printed for this data entry."
7445 CALL "ZZPROM","C",X3$,S3,Q$,"","",0; ON S3 GOTO 9800,7446 ! SSP#266982
7450 REMOVE (Z[5],KEY="") ! SSP#266982
7490 RETURN 
7500 REM "Get printer options
7510 IF LEN(R0$)<30 THEN DIM R0$(30); R0$(1,3)="ILP",R0$(8,1)="N",R0$(8,9)="NNN    66"; GOSUB 6700
7520 CALL "ZZRPTO",X3$,X4$,Y0$,R0$,R{ALL},4,C0,X0
7525 GOSUB 6700
7530 PRINT @(0,3),'CE',; GOSUB 6000; GOSUB 6200
7540 RETURN 
7550 REM "input the output options
7560 GOSUB 7500
7590 GOTO 1800
7610 FIND (Z[6],DOM=7611,KEY=X3$(9,3)+"G/L")P0$
7620 FIND (Z[6],DOM=7621,KEY=P0$(1,6)+"YE"+A$(1,4))P1$; GOTO 7630
7621 CALL "ZZPROM","4",X3$,S3,"The new fiscal year is not setup (G/L SM T2).","","",0; GOTO 9900
7630 GOTO 7730
7699 RETURN 
7700 REM "PRINT P/E DATE"
7720 PRINT @(69,3+V0),FND$(P1$(NUM(A$(5+K9,2))*6+15,6)),
7730 IF P5$="" THEN P5$=F0$(7,6) ELSE P5$=A$(1,6)
7740 RETURN 
7800 REM "UPDATE MONTHLY DISB W/ VOID CHECK C0
7810 DIM H$(88),H[2]
7820 H$(1,6)=C0$(1,6),H$(7,3)=G$(1,3),H$(10,6)=STR(C0:"000000"),H$(16,2)="00",H$(18,1)="V",H$(19,6)=C0$(7,6),H$(35,35)=V$
7825 H$(70,6)="000000"
7840 WRITE (Z[8],KEY=H$(1,17),DOM=7860)IOL=0380
7890 RETURN 
7900 REM "BANK CODE CHANGE
7910 DIM G0$(36),G$(150); G$(1,3)=Q$(4,3)
7915 Q$=Q$(4,3)
7920 READ (Z[7],KEY=G$(1,3),DOM=7921)IOL=0370
7922 IF G$(132,1)<>" " AND G$(132,1)<>Y$(7,1) THEN DIM Y$(231),Y[42]; READ (Z[6],KEY="A/PCKF"+G$(132,1))IOL=0360; IF LEN(Y$)<>231 THEN Y$=Y$+DIM(231-LEN(Y$)); IF X3$(9,3)="377" AND G$(1,3)="SRA" THEN W0=90; REM "Flexible format by bank code, WO131772. WO252442                 
7925 READ (Z[6],KEY="FORM"+G$(38,1),DOM=7926)G0$
7930 Q$=FNS$(G0$(6,30))
7935 IF R0$(2,2)="A " THEN PRINT 'EA',
7938 REM "IF FID(H1)<>FID(0) THEN PRINTER$=FID(H1); CLOSE (H1); OPEN (H1)PRINTER$
7940 CALL "ZZPROM","5",X3$,S3,"Please load the "+Q$,"","",0
7945 IF R0$(2,2)="A " THEN PRINT 'BA',
7990 IF S3=1 THEN EXITTO 9800 ELSE RETURN 
8000 REM "BUILD ITEM
8005 REM "W0 IS WIDTH OF ITEM, W1=LENGTH
8010 W0=85,W1=Y[0],P0=0; REM "W0 WAS 80
8011 IF POS(X3$(9,3)="099")<>0 THEN W0=90
8012 IF POS(X3$(9,3)="021113114448441427014017117270130288",3)<>0 THEN W0=80 ! SSP#302520
8015 IF U$>"" THEN U9$=U$(1,17)
8016 IF F0$(81,1)="Y" THEN GOSUB 3500; REM "FIGURE OUT MAX LENGTH FOR SPELLED OUT PAY EXACTLY AMOUNT SSP# 142931
8020 DIM Y3$(W0*W1),T[10]; IF JETFORM_MODE THEN JF$=""
8025 IF T$<>"TEST" THEN FIND (Z[2],KEY=A$(7+K9,10))IOL=0320
8026 FIND (Z[6],KEY="CMP"+X3$(9,3))F$
8030 REM "IF Y$(38,1)="N" THEN GOTO 08100
8035 REM "SET CONSTANTS HERE
8038 Q$=FNS$(F$(7,40)); JF_FIELD=4,COL=Y[4],LINE=Y[5],DATA$=Q$; GOSUB 8300
8040 X$="",Q$=FNS$(Y$(39,16))+": "; CALL "ZZDISP","AX",B$(1,10),"A/P",X3$,X$,"",0,0,X4$; Q$=Q$+X$ ! WO242552/252753, clear X$
8045 IF Y$(38,1)="Y" THEN JF_FIELD=6,COL=Y[6],LINE=Y[7],DATA$=Q$; GOSUB 8300 ELSE JF_FIELD=6,COL=Y[6],LINE=Y[7],DATA$=X$; GOSUB 8300
8050 Q$=FNS$(Y$(55,16))+": "+B$(11,35); IF Y$(38,1)="Y" THEN JF_FIELD=8,COL=Y[8],LINE=Y[9],DATA$=Q$; GOSUB 8300 ELSE JF_FIELD=8,COL=Y[8],LINE=Y[9],DATA$=B$(11,35); GOSUB 8300
8052 Q$=FNS$(Y$(199,16))+": "+C0$(54,9); IF Y$(38,1)="Y" THEN JF_FIELD=39,COL=Y[39],LINE=Y[40],DATA$=Q$; GOSUB 8300 ELSE JF_FIELD=39,COL=Y[39],LINE=Y[40],DATA$=C0$(54,9); GOSUB 8300; REM "File reference number, WO131772
8053 Q$=FNS$(Y$(215,16))+": "+B$(309,15); IF Y$(38,1)="Y" THEN JF_FIELD=40,COL=Y[41],LINE=Y[42],DATA$=Q$; GOSUB 8300 ELSE JF_FIELD=40,COL=Y[41],LINE=Y[42],DATA$=B$(309,15); GOSUB 8300; REM "Vendor Account Number, WO252442
8055 IF POS(X3$(9,3)="042047048",3)=0 THEN Q$=FNS$(Y$(71,16))+": "+STR(C0:"000000"); IF Y$(38,1)="Y" THEN JF_FIELD=10,COL=Y[10],LINE=Y[11],DATA$=Q$; GOSUB 8300 ELSE JF_FIELD=10,COL=Y[10],LINE=Y[11],DATA$=STR(C0:"000000"); GOSUB 8300; JF_FIELD$="DATE",COL=Y[10]-18,LINE=Y[11],DATA$=FND$(C0$(7,6)); GOSUB 8300 ! SSP#284854
8056 IF X3$(9,3)="042" THEN Q$=FNS$(Y$(71,16))+": "+STR(C0:"000000"); IF Y$(38,1)="Y" THEN JF_FIELD=10,COL=Y[10],LINE=Y[11],DATA$=Q$; GOSUB 8300 ELSE JF_FIELD=10,COL=Y[10],LINE=Y[11],DATA$=STR(C0:"000000"); GOSUB 8300; JF_FIELD$="DATE",COL=Y[10]-12,LINE=Y[11],DATA$=FND$(C0$(7,6)); GOSUB 8300
8057 IF POS(X3$(9,3)="047048",3)>0 THEN Q$=FNS$(Y$(71,16))+": "+STR(C0:"000000"); IF Y$(38,1)="Y" THEN JF_FIELD=10,COL=Y[10],LINE=Y[11],DATA$=Q$; GOSUB 8300 ELSE JF_FIELD=10,COL=Y[10],LINE=Y[11],DATA$=STR(C0:"000000"); GOSUB 8300; JF_FIELD$="DATE",COL=Y[10]-13,LINE=Y[11],DATA$=FND$(C0$(7,6)); GOSUB 8300
8058 ! IF POS(X3$(9,3)="020376",3)<>0 THEN Y3$(FNP(Y[10],Y[11])-18,10)=""!SSP#285773
8059 IF POS(X3$(9,3)="468",3)<>0 AND NOT(UNFORM_MODE) THEN Q$=FNS$(Y$(71,16))+": "+STR(C0:"000000"); IF Y[10]>0 THEN IF Y$(38,1)="Y" THEN Y3$(FNP(Y[10],Y[11]),LEN(Q$))=Q$ ELSE Y3$(FNP(Y[10],Y[11]),6)=STR(C0:"000000"),Y3$(FNP(Y[10],Y[11])-W0*3,10)=FND$(C0$(7,6)) ! ssp284167 temp add 500 for testing
8060 FOR X=0 TO 6
8065 Q$=FNS$(Y$(87+X*16,16)),Q=-LEN(Q$)
8070 IF X=0 OR X=6 THEN Q=Q+10 ELSE IF X=1 THEN Q=Q+8 ELSE IF X=2 OR X=4 THEN Q=Q+LEN(M0$) ELSE IF X=3 THEN Q=Q+LEN(M1$) ELSE IF X=5 THEN Q=Q+15
8075 IF Y[X+12]>0 THEN Y3$(FNP(Y[X+12],Y[1]-2)+Q-1,LEN(Q$))=Q$
8076 IF Y[X+12]>0 THEN Y3$(FNP(Y[X+12]+O,Y[1]-2)+Q-1,LEN(Q$))=Q$
8079 REM "These are headings, don't do -> IF JETFORM_MODE THEN LET JF_FIELD=X+12, COL=Y[X+12]+Q-1, LINE=Y[1]-2, DATA$=Q$; GOSUB 04800
8080 NEXT X
8085 IF Y$(38,1)="Y" THEN Y3$(FNP(1,Y[1]-1),LEN(Z1$))=Z1$
8100 REM "
8295 RETURN 
8300 REM "insert Field into Y3$ or send to JetForm, don't print if col=0
8301 REM "col=column, line = line, data$= data to print, jf_FIELD = Jetform field number, optionally set JF_FIELD$ for jetform to override JF_FIELD setting
8305 IF COL<=0 THEN GOTO 8340
8307 IF VOID_CHECK THEN IF JF_FIELD=23 THEN DATA$="**VOID**" ELSE IF JF_FIELD=24 OR JF_FIELD=28 THEN DATA$=V$
8310 IF JETFORM_MODE THEN GOSUB 4800; GOTO 8340
8320 Y3$(FNP(COL,LINE),LEN(DATA$))=DATA$
8340 RETURN 
8390 RETURN 
8400 REM "WRITE CHECK PRINT FILE
8410 DIM E$(58),E[2]
8420 E$(1,17)=U8$,E$(18,6)=STR(C0:"000000"),E$(24,6)=C0$(7,6),E[0]=T[1],E[1]=T[2],E[2]=T[3]
8425 E$(30,9)=C0$(54,9); REM "Reference number, WO131772
8440 WRITE (Z[5],KEY=E$(1,17))IOL=0350
8450 C0$(47,6)=STR(C0:"000000"),C0$(19,17)=E$(1,17)
8455 WRITE (Z[6],KEY=A1$)C0$
8470 IF FILE$="Y" THEN IF CONT$<>"Y" THEN GOSUB 8550; REM "Create text record, WO131772, SSP137205 if continuation then don't write to file
8485 IF NOT(UNFORM_MODE) OR (UNFORM_MODE AND (REMIT_FLAG=0 OR EOV$="Y")) THEN C0=C0+1 ! SSP254469
8490 RETURN 
8500 REM "Create text file and open, WO131772
8505 IF STP(C0$(54,9),3," ")="" THEN FILE$="N"; GOTO 8545
8510 ERASE C0$(54,9),ERR=8511; GOTO 8510
8515 SERIAL C0$(54,9),0,0,ERR=8516
8520 CLOSE (32502); OPEN LOCK (32502,OPT="TEXT")C0$(54,9)
8540 FILE$="Y"; TEXT_FILE$=C0$(54,9)
8545 RETURN 
8550 REM "Create text records and write out, WO131772
8560 DIM APB$(125); READ (Z[11],KEY=E$(7,10)+"0001",DOM=8561)APB$
8565 RECORD$=QUO+STP(APB$(15,75),1)+QUO+","
8570 DIM APB$(125); READ (Z[11],KEY=E$(7,10)+"0003",DOM=8571)APB$
8575 RECORD$=RECORD$+QUO+STP(APB$(15,75),1)+QUO+","
8580 RECORD$=RECORD$+QUO+"0"+QUO+","+QUO+STP(B$(11,18),1)+QUO+","+QUO+STP(STR(E[2]:M2$),0)+QUO+","+QUO+STP(C0$(54,9),1)+QUO
8590 PRINT (32502)RECORD$
8595 RETURN 
8700 REM "Set face of check
8702 IF EOV$="N" AND REMIT_FLAG THEN VOID_CHECK=0; RETURN ! SSP254469
8705 IF VOID_CHECK THEN P9$="VOID CHECK",V0$=V$
8710 Q$=FNS$(F$(7,40)); JF_FIELD=19,COL=Y[19],LINE=Y[29],DATA$=Q$; GOSUB 8300
8715 IF Y[20]<=0 THEN GOTO 8725 ELSE FOR X=0 TO 2
8720 Q$=FNS$(F$(47+X*40,40)); JF_FIELD=20,COL=Y[20],LINE=Y[30]+X,DATA$=Q$; GOSUB 8300; NEXT X
8725 Q$=STR(C0:"000000"); JF_FIELD=21,COL=Y[21],LINE=Y[31],DATA$=Q$; GOSUB 8300
8730 Q$=FND$(C0$(7,6)); JF_FIELD=22,COL=Y[22],LINE=Y[32],DATA$=Q$; GOSUB 8300
8735 Q$=STR(T[3]:"*"+M0$(2)); IF T[3]<=0 THEN Q$="** VOID **"
8740 JF_FIELD=23,COL=Y[23],LINE=Y[33],DATA$=Q$; GOSUB 8300
8742 IF POS(X3$(9,3)="060338363364365366476520",3)>0 THEN GOSUB 4100; GOTO 8748 ! SSP254469, remove 500 from list of companies
8743 IF F0$(81,1)<>"Y" OR G$(131,1)="Y" THEN GOTO 8746
8744 CALL "AP2CGC",X3$,X4$,T[3],PAY_EXACTLY_LEN,Q$; J9$=Q$; IF T[3]>0 THEN JF_FIELD=24,COL=Y[24],LINE=Y[34],DATA$=Q$; GOSUB 8300 ELSE JF_FIELD=24,COL=Y[24],LINE=Y[34],DATA$=V$; GOSUB 8300; GOTO 8748
8745 IF F0$(81,1)="Y" THEN GOTO 8748
8746 Q$="*"+M0$(1,POS("."=M0$)-1),Q$=STR(INT(T[3]):Q$)+" Dollars and "+STR(100*FPT(T[3]):"00")+" cents",J9$=Q$; IF T[3]>0 THEN JF_FIELD=24,COL=Y[24],LINE=Y[34],DATA$=Q$; GOSUB 8300 ELSE JF_FIELD=24,COL=Y[24],LINE=Y[34],DATA$=V$; GOSUB 8300
8748 IF Y[9]<Y[35] AND P1<2 THEN GOTO 8750
8749 Q$=FNS$(Y$(55,16))+": "+B$(11,35); IF Y$(38,1)="Y" THEN JF_FIELD=8,COL=Y[8],LINE=Y[9],DATA$=Q$; GOSUB 8300 ELSE JF_FIELD=8,COL=Y[8],LINE=Y[9],DATA$=B$(11,35); GOSUB 8300; REM "124545
8750 CALL "ZZDISP","AX",B$(1,10),"A/P",X3$,Q$,"",0,0,X4$; JF_FIELD=25,COL=Y[25],LINE=Y[35],DATA$=Q$; GOSUB 8300
8755 Q$=FNS$(B$(11,35)); JF_FIELD=26,COL=Y[26],LINE=Y[36],DATA$=Q$; GOSUB 8300
8758 CALL "ZZDISP","AX",B$(124,9),"ZIP",X3$,X$,"",0,0,X4$
8760 IF Y[27]>0 THEN FOR X=0 TO 1; Q$=B$(46+X*30,30); IF POS(" "<>Q$)<>0 THEN JF_FIELD=27,COL=Y[27],LINE=Y[37]+X,DATA$=Q$; GOSUB 8300 END_IF ; NEXT X; Q$=FNS$(B$(106,16))+", "+B$(122,2)+"    "+X$; JF_FIELD=27,COL=Y[27],LINE=Y[37]+1+SGN(POS(" "<>B$(76,30))),DATA$=Q$; GOSUB 8300
8765 IF T[3]<=0 OR VOID_CHECK THEN JF_FIELD=28,COL=Y[28],LINE=Y[38],DATA$=V0$; GOSUB 8300
8790 VOID_CHECK=0
8795 RETURN 
8800 REM "Void face of check
8805 P9$="VOID CHECK"
8810 JF_FIELD=23,COL=Y[23],LINE=Y[33],DATA$="**VOID**"; GOSUB 8300
8815 IF POS(X3$(9,3)="060338363364365366476520",3)>0 THEN J9=T[3],T[3]=0; GOSUB 4100; T[3]=J9; GOTO 8821 ! SSP254469, remove 500 from list of companies
8820 JF_FIELD=24,COL=Y[24],LINE=Y[34],DATA$=V$; GOSUB 8300
8825 JF_FIELD=28,COL=Y[28],LINE=Y[38],DATA$=V$; GOSUB 8300
8890 RETURN 
8900 REM "PRINT CHECK
8905 IF JETFORM_MODE THEN TMP$="^field PDF"+'LF'+STP("PDFFILE:"+X3$(9,3)+DLM+"CK"+DLM+X3$(40,3)+DLM+STR(C0:"000000")+"+"+STR(P1),3," ")+'LF'; PRINT (JETFORM_DEV)TMP$,; REM SSP 216896 219849
8910 IF JETFORM_MODE THEN IF FIRST_TIME THEN FIRST_TIME=0 ELSE CALL "EF2JET",X3$,X4$,P6$,"E",JETFORM_DEV,ITEM_PARM$,JOB_MODIFIER$,RET$ END_IF ; PRINT (H1)JF$,; GOSUB IMAGE_LIBRARY_SETUP; IF PDF_ARCHIVE_MODE THEN GOSUB PRINT_ARCHIVE END_IF ; JF$=""; GOTO 8980
8911 IF UNFORM_MODE THEN IF FIRST_TIME THEN FIRST_TIME=0 ELSE GOSUB IMAGE_LIBRARY_SETUP; JF_OPT$="N"; IF PREV_C0<>C0 OR EOV$="Y" THEN PREV_C0=C0,JF_OPT$="N" ELSE JF_OPT$="E" END_IF ; CALL "EF2UNF",X3$,X4$,P6$,JF_OPT$,UNFORM_DEV,TBL(EOV$="Y",TBL(REMIT_FLAG,ITEM_PARM$,REMIT_PARM$),ITEM_PARM$),JOB_MODIFIER$,RET$,MEM_FL,CPKY$; REM SSP 237164, SSP254469-Spin off new output file if different check number or if printing real check (end of voids) SSP 307332
8912 IF UNFORM_MODE THEN IF JF_OPT$="N" THEN TMP$=STP("PDFFILE:"+X3$(9,3)+DLM+"CK"+DLM+X3$(40,3)+DLM+STR(C0:"000000")+TBL(EOV$="Y",TBL(REMIT_FLAG,"","R"),"")+"+"+STR(P1),3," "); PRINT (UNFORM_DEV)TMP$,; REM SSP 237164 SSP255502, was line 8906 move to line 8912 plus check for JF_OPT$="N", SSP254469
8920 FOR X=0 TO W1-1
8930 IF POS(" "<>Y3$(X*W0+1,W0))=0 THEN PRINT (H1)'LF', ELSE PRINT (H1)@(W9),Y3$(X*W0+1,W0)
8950 NEXT X
8960 IF %PRINTING_IN_VIEWER THEN PRINT (H1)'FF', ! 166988 in viewer we must tell it we are done with this page
8980 GOSUB 7800
8990 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9600 REM "
9610 CALL "ZZPROM","4",X3$,S3,"Check printing has already been completed.","","",0; ON S3 GOTO 9800,9611
9620 A$(47,6)=""; GOTO 1525
9800 REM "EXIT PROGRAM
9810 Y4$="ZMENU"
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9915 %IL_ADD_DOC_TYPE$="",%IL_ADD_TF1$="",%IL_ADD_TF2$="",%IL_ADD_IMAGE_PATH$="",%IL_FORMAT$="" ! 186095
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9950 CLEAR ; CALL "ZZDAPP",X3$,"AALL"; RUN "ZMENU"
9999 END 
10000 SETUP_FOR_ARCHIVE:! we will be PDF Archiving so setup for it
10010 A_JETFORM_DEV$=R0$(69,2); A_JETFORM_DEV=HFN; OPEN (A_JETFORM_DEV,ERR=A_SETUP_DONE)A_JETFORM_DEV$
10015 A_ITEM_PARM$=ITEM_PARM$,A_ITEM_PARM$(12,60)=SUB(ITEM_PARM$(12,60),"APCHECK","PDFCHECK")
10080 PDF_ARCHIVE_MODE=1
10090 A_SETUP_DONE:
10095 RETURN 
10099 ! *********************************************************
10100 PRINT_ARCHIVE:! print JF$ to ARCHIVE file as well
10125 CALL "EF2JET",X3$,X4$,P6$,"N",A_JETFORM_DEV,A_ITEM_PARM$,JOB_MODIFIER$,RET$
10130 TMP$="^field PDF"+'LF'+STP("PDFFILE:"+X3$(9,3)+DLM+"CK"+DLM+STR(C0:"000000")+"+"+STR(P1),3," ")+'LF'
10150 PRINT (A_JETFORM_DEV)JF$+TMP$,
10195 RETURN 
10199 ! ************************************************
11000 IMAGE_LIBRARY_SETUP:! If needed, setup parameters to add image library info to Jetform call -- because checks are printed to one file, open our own jetform device and print here, then clear global %IL's so other EF2JET calls don't interfere
11005 IF %HAS_IMAGE_LIB AND (NOT(NUL(IL_DEV$)) OR UNFORM_MODE) THEN {
11010 IF COMBINE_PDF$="Y" THEN IF OPEN_MMF THEN GOTO *NEXT ELSE GOSUB CREATE_MMF; OPEN_MMF=1 ! ssp 307332
11020 %IL_ADD_DOC_TYPE$="A8"
11025 %IL_ADD_TF1$=STR(C0:"000000") ! Check number
11030 %IL_ADD_TF2$=A$(7,10)+STR(C0:"000000") ! Vendor code + check number
11035 DTS$=DTE(0:"%Yz%Mz%Dz_%Hz%mz%s")+"_"+X3$(174,4) ! SSP 244541
11040 %IL_ADD_IMAGE_PATH$=STR(C0:"000000")+TBL(EOV$="Y",TBL(REMIT_FLAG,"","R"),"")+"_"+STR(P1) ! check number_page number, SSP254469
11042 %IL_ADD_IMAGE_PATH$=%IL_ADD_IMAGE_PATH$+"_"+DTS$ ! SSP 216110 244541
11045 %IL_FORMAT$=Y$(7,1) ! 186095
11048 CONT_IMG$+=STR(C0:"000000") ! SSP 217412
11049 IF JETFORM_MODE THEN {
11050 IF NOT(IL_JETFORM_DEV) THEN IL_JETFORM_DEV=HFN; OPEN (IL_JETFORM_DEV,ERR=*NEXT)IL_DEV$
11054 IL_PARM$=ITEM_PARM$,IL_PARM$(72,1)="Y"
11055 CALL "EF2JET",X3$,X4$,P6$,"N",IL_JETFORM_DEV,IL_PARM$,"IL",RET$
11060 PRINT (IL_JETFORM_DEV)JF$,
11061  }
11065 IF EOV$="Y" AND LEN(CONT_IMG$)>6 THEN %IL_ADD_DOC_TYPE$="A8",%IL_ADD_TF1$=STR(C0:"000000"); CALL "ILGFUN;MULTIPLE_PAGE_CHK",CONT_IMG$,A$(7,10) ! SSP 217412
11070 IF NOT(UNFORM_MODE) THEN %IL_ADD_DOC_TYPE$="",%IL_ADD_TF1$="",%IL_ADD_TF2$="",%IL_ADD_IMAGE_PATH$="",%IL_FORMAT$="" ! 186095
11075  } ELSE {
11080 %IL_ADD_DOC_TYPE$="",%IL_ADD_TF1$="",%IL_ADD_TF2$="",%IL_ADD_IMAGE_PATH$="",%IL_FORMAT$="" ! 186095
11085  }
11095 RETURN 
11099 ! ****************************************************************
12000 CREATE_MMF:! SSP307332
12010 IF OPEN_MMF=1 THEN RETURN 
12020 MEM_FL=HFN; OPEN (MEM_FL)"*MEMORY*"
12099 RETURN 
12100 CREATE_PDF:! SSP 307332
12120 CALL "ILGFUN;COMBINE_PDF_SETUP",X3$(174,4),MEM_FL,"A8",3
12299 RETURN 
56000 REM "200390-Issue with restart of A/P checks when using alternate by Vendor lookup
56010 REM "197028-Create A/P check format to match his preprinted form.   
56012 REM "217412-When running checks Image Library only saves the last       
56013 REM "216896-Checks are not going into the PDF, M2 print queue. 
56014 REM "219849-PDF info is displaying on the screen when printing checks.
56015 REM "241023-Modify printing programs to use Unform                      
56016 REM "244541-Still not getting an image of all the pages on the checks.  
56018 REM "252442-Add Vendor Account Number to AP Check Flexible Format and   
56020 REM "254469-Changes to AP Check Printing to print Remittance Advice doc 
56022 ! SSP255502-A/P checks to archive using Unform, timing issue.
56023 REM "266982-A/P C5 Cash Disbursement Jounal gives you the option to skip
56024 REM "284167-Program Exception has occurred (047, AP2CGA-8059).          
56025 REM "285773-Do you have an AP laser check template? We currently run    
56026 REM "302520-Installation: Unform.  Includes one data set.  Standard     ,107 Remove company specific code
56027 REM "307332-Create combined pdf of checks in a batch                    
