0010 REM "A/P Specific Invoice Selection <AP2CBA>
0011 REM 
0020 SETESC 9300; SETERR 9000
0035 REM "4.4 - 04/25/00 - 9.73 - tma - SSP# 125737
0040 REM "Copyright 2000 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="AP2CBA",X1$="A/P Specific Invoice Selection",K9$="",K9=0
0120 DIM Z0$(80,"-"),A0[5],B$(227),B[2],C$(100),C[27],D$(110),E[4],D0$(50)
0130 K0=20,K1=2
0135 C9=-1,V9=-2
0140 M0$="-###,###,###.00"
0160 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1,LEN(A$)),A[0],A[1],A[2],A[3],A[4],A[5]
0315 IOLIST A0$,A0[0],A0[1],A0[2],A0[3],A0[4],A0[5]
0320 IOLIST B$,B[0],B[1],B[2]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20],C[21],C[22],C[23],C[24],C[25],C[26],C[27]
0340 IOLIST D$
0350 IOLIST E$,E[0],E[1],E[2]
0420 IOLIST L$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="06O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0525 READ (Z[6],KEY=X3$(9,3)+"A/P",DOM=9900)F0$
0535 Z$="01O APG...  02O AP4...  03ORAPI...  04O ZYB...  05ORAP5...  12ORAP8...  "
0540 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0541,9900
0600 REM "
0605 GOSUB 7300
0610 CALL "ZZINFO",Z[1],X0,X3$,R0,R1,0,0,0,0,0,""; IF R0>0 THEN GOSUB 8900
0620 GOSUB 6000
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(227),B[2],C$(100),C[27],D$(110),E[4],D0$(50),DISC_DATE$(6)
1035 DIM A$(63+K9),A[5]
1045 A$(1,10+K9)=K9$+A1$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1105 GOSUB 8325
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1250,1500
1200 REM "Vendor Code
1205 IF LEN(A1$)>0 THEN A$(1,10)=A1$(1,10); GOTO 1240
1208 X$="Press F10 to retreive by Phone Number"
1210 CALL "ZZENTR","AXU",A{ALL},A$,X4$,X3$,18,3+V0,1+K9,10,C0,"A/P","{1"+X$,"","AP2CBA00","AP4","AP2MAB",""; IF C0<>-7 THEN IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1212 IF C0=-7 THEN X$="AP",C0=1; CALL "ZZ2PLU",X3$,A$,1,X$; IF X$="" THEN GOTO 1208 ELSE CALL "ZZDISP","A",A$(1,10),"A/P",X3$,"","",18,3+V0,X4$
1213 READ (Z[1],KEY=A$(1,10),DOM=1214)
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1216,6800,9800,6970
1218 IF X3$(330,1)="1" THEN IF A$(1,2)="00" THEN CALL "ZZPROM",".4",X3$,0,"You may not pay this vendor (RL)","","",0; GOTO 1210
1220 FIND (Z[2],KEY=A$(1+K9,10),DOM=1208)IOL=0320
1221 PRINT @(36,3),B$(11,35),
1225 FIND (Z[5],KEY=A$(1+K9,10),DOM=8100)IOL=0350
1230 PRINT @(64,5+V0),E[0]:M0$,
1232 GOSUB 8450
1235 IF B$(291,1)="Y" THEN CALL "ZZPROM",".4",X3$,0,"This vendor is on hold","","",0
1236 GOSUB 8700
1240 GOTO 1120
1250 REM "Invoice number
1258 Q$=A$(1,10),X$="For this Vendor, DELETE - Deletes all invoices;  ALL - Adds all invoices"
1260 CALL "ZZENTR","SUX",A{ALL},A$,X4$,X3$,18,7+V0,11+K9,10,C0,"","{2"+X$,Q$,"AP2CBA02","API","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1260,1261
1262 IF C0=-2 AND LEN(A1$)>0 THEN A1$=A1$(1,0)
1265 IF C0=-1 THEN GOTO 1260 ELSE IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1120,6800,9800,6970
1267 IF A$(11,10)="DELETE    " THEN GOTO 7800 ELSE IF A$(11,10)="ALL       " THEN GOTO 7900
1270 READ (Z[3],KEY=A$(1,20),DOM=8000)IOL=0340
1280 IF C0=2 THEN C0=1
1290 GOTO 1120
1500 REM "
1505 Z9=-1
1510 A1$=K9$+A$(LEN(K9$)+1,20),A$(1,LEN(K9$))=K9$
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1525 Z9=0
1590 GOTO 1700
1600 REM "
1610 READ (Z[3],KEY=A$(1+K9,20),DOM=8000)IOL=0330
1615 USING_2ND$="N"
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630,6975
1630 GOTO 1000
1650 A$(22+K9,3)=C$(66,3),A[0]=C[13],A[3]=C[18]-C[20]
1651 DISC_DATE$=C$(35,6),DISCOUNT=C[7]; IF POS(" "<>C$(125,6))<>0 THEN IF C$(35,6)<X3$(15,6) THEN DISC_DATE$=C$(125,6),DISCOUNT=C[22],USING_2ND$="Y"; REM "WO131561, 2nd discount date/amount
1652 IF DISC_DATE$>=X3$(15,6) OR B$(293,1)="Y" THEN A[1]=DISCOUNT-C[11],A[3]=C[19]-C[21]
1655 GOSUB 7500
1658 READ (Z[4],KEY=A$(22+K9,3),DOM=1711)IOL=0340
1660 GOSUB 6200
1690 GOTO 2000
1700 REM "
1705 GOSUB 6600
1710 GOSUB 7700
1790 GOSUB 6200
1800 REM "
1820 CALL "ZZPROM",STR(6+C9),X3$,Z,"","","V",X0; ON Z GOTO 1850,6500,1630,6700
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310; T1=-1; GOSUB 8300
1865 C9=1
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF C0>3 OR X0<0 THEN GOTO 1800
2035 IF X0*6+11<LEN(V9$) THEN IF V9$(X0*6+11,1)="2" THEN GOTO 2020
2040 ON X0 GOTO 2100,2150,2200,2300,2250,2350,1800
2100 REM "Check code
2105 X$="Check code: If non-blank, this invoice will print on a separate check"
2110 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,18,11+V0,21+K9,1,C0,"","{3"+X$,"","AP2CBA04","","",""
2140 GOTO 2020
2150 REM "Bank account
2155 IF F0$(16,1)<>"Y" THEN GOTO 2020
2160 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,18,12+V0,22+K9,3,C0,"","{3"+X$,"","AP2CBA06","ZYB","ZY2BAA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2160,2161
2170 FIND (Z[4],KEY=A$(22+K9,3),DOM=2160)IOL=0340
2175 PRINT @(23,12),D$(4,30),
2180 GOSUB 8200
2190 GOTO 2020
2200 REM "Gross amount to pay
2210 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,64,14+V0,0,922,C0,M0$,"{3"+X$,"","AP2CBA08","","",""
2215 IF C0>0 THEN GOSUB 7600
2240 GOTO 2020
2250 REM "Discount taken this payment
2260 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,64,16+V0,1,922,C0,M0$,"{3"+X$,"","AP2CBA10","","",""
2285 IF C0>0 THEN GOSUB 7600
2290 GOTO 2020
2300 REM "Gross amount in alternate currency
2305 IF POS(" "<>A$(25,3))=0 THEN GOTO 2020
2310 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,64,15+V0,2,922,C0,M0$,"{3"+X$,"","AP2CBA12","","",""
2315 IF C0>0 THEN GOSUB 8200
2340 GOTO 2020
2350 REM "Discount amount in alternate currency
2355 IF POS(" "<>A$(25,3))=0 THEN GOTO 2020
2360 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,64,17+V0,3,922,C0,M0$,"{3"+X$,"","AP2CBA14","","",""
2365 IF C0>0 THEN GOSUB 8200
2390 GOTO 2020
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',@(71,2),"Ck. Tot",
6060 PRINT @(5,3+V0),"Vendor Code:",@(30,3+V0),"Name:",@(0,4+V0),Z0$,
6070 PRINT @(1,5+V0),"Check run total:",@(38),"Total balance due vendor:",@(0,6+V0),Z0$,
6080 PRINT @(2,7+V0),"Invoice number:",@(39,7+V0),"Original invoice amount:",@(4,8+V0),"Invoice date:",@(1,9+V0),"Invoice on hold?",@(48,9+V0),"Balance to pay:",@(6,11+V0),"Check code:",@(44,11+V0),"Remaining discount:",; IF F0$(16,1)="Y" THEN PRINT @(4,12+V0),"Bank account:",
6085 PRINT @(0,13+V0),Z0$,
6090 PRINT @(0,14+V0),"Invoice due date:",@(43,14+V0),"Gross amount to pay:",@(0,16+V0),"Discount expires:",@(35,16+V0),"Discount taken this payment:",@(21,18+V0),"Net invoice amount paid vendor this check:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6218 GOSUB 8325
6220 PRINT @(18,11+V0),A$(21+K9,1),@(18,12+V0),; IF F0$(16,1)="Y" THEN PRINT A$(22+K9,3),@(23,12),D$(4,30),@(64,16+V0),A[1]:M0$,
6230 PRINT @(64,14+V0),A[0]:M0$,@(64,16+V0),A[1]:M0$,
6280 GOSUB 7600
6285 GOSUB 8200
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF Z9>=0 THEN GOSUB 6200; GOSUB 7500 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6456 IF A1$>"" THEN FIND (Z[2],KEY=A1$(1+K9,10),DOM=6457)IOL=0320
6457 IF A1$>"" THEN FIND (Z[5],KEY=A1$(1+K9,10),DOM=6458)IOL=0350
6460 CALL "ZZDISP","A",A$(1+K9,10),"A/P",X3$,"","",18,3+V0,X4$
6464 PRINT @(36,3),B$(11,35),@(64,5+V0),E[0]:M0$,@(18,7+V0),A$(11+K9,10),
6490 RETURN 
6500 REM "DELETE
6510 REMOVE (Z[1],KEY=A1$,DOM=6511); IF C9=0 THEN T1=-1; GOSUB 8300
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6605 FIND (Z[2],KEY=A$(1+K9,10),DOM=6606)IOL=0320
6610 FIND (Z[3],KEY=A$(1+K9,20),DOM=6611)IOL=0330
6615 FIND (Z[5],KEY=A$(1+K9,10),DOM=6616)IOL=0350
6625 IF A$(28,1)="Y" THEN USING_2ND$="Y",DISC_DATE$=C$(125,6),DISCOUNT=C[22] ELSE DISC_DATE$=C$(35,6),DISCOUNT=C[7]; REM "WO131561, 2nd discount date/amount
6685 GOSUB 7500
6690 RETURN 
6700 REM "ADD ENTRY
6705 IF Z9=0 THEN GOTO 6790
6707 IF USING_2ND$="Y" THEN A$(28,1)="Y"; REM "WO131561, 2nd discount
6709 IF POS(" "<>A$(25,3))=0 THEN A[2]=0,A[3]=0
6710 WRITE (Z[1],KEY=A1$)IOL=0310; T1=1; GOSUB 8300
6790 C9=0; IF Z=6 OR K1=0 THEN GOTO 9900 ELSE IF Z<4 THEN GOTO 1000 ELSE C0=-(3.1+(Z-4)/10); GOTO 6800
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 IF A1$="" THEN GOTO 6800 ELSE IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6825 A$(1,LEN(A1$))=A1$
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM "PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
7300 REM "check printing in progress
7305 FIND (Z[6],KEY=X3$(9,3)+"APC"+X3$(174,4),DOM=7400)
7320 CALL "ZZPROM",".4",X3$,0,"Check printing has already been initiated","","",0; GOTO 9900
7490 RETURN 
7500 REM "Display original invoice data
7520 PRINT @(64,7+V0),C[2]:M0$,@(18,8+V0),FND$(C$(21,6)),@(18,9+V0),C$(81,1),@(64,9+V0),C[13]:M0$,@(64,11+V0),DISCOUNT-C[11]:M0$,
7530 PRINT @(18,14+V0),FND$(C$(29,6)),@(18,16+V0),FND$(DISC_DATE$),
7535 IF USING_2ND$="Y" THEN PRINT @(18,17+V0),"*",
7540 IF C$(81,1)="Y" THEN CALL "ZZPROM",".4",X3$,0,"This invoice is on hold","","",0
7545 IF MID(B$,293,1)="Y" THEN PRINT @(29,16+V0),"*",
7590 RETURN 
7600 REM "
7620 PRINT @(64,18+V0),A[0]-A[1]:M0$,
7640 RETURN 
7700 REM "BANK CODE
7710 FIND (Z[4],KEY=A$(22+K9,3),DOM=7711)IOL=0340
7740 RETURN 
7800 REM "DELETE ALL INVOICES FOR THIS VENDOR"
7820 READ (Z[1],KEY=E$(1,10),DOM=7821)
7830 READ (Z[1],END=7890)IOL=0310
7840 IF E$(1,10)<>A$(1,10) THEN GOTO 7890
7850 REMOVE (Z[1],KEY=A$(1,20)); T1=-1; GOSUB 8300
7860 GOTO 7830
7890 GOTO 1000
7900 REM "ADD ALL INVOICES FOR THIS VENDOR"
7920 Q$=A$(1,10); READ (Z[3],KEY=Q$(1,10),DOM=7921)
7930 READ (Z[3],END=7990)IOL=0330
7940 IF Q$<>C$(1,10) THEN GOTO 7990
7945 IF C[13]=0 THEN GOTO 7930
7947 USIONG_2ND$="N"
7950 A$(1,20)=C$(1,20),A$(21,1)=" ",A$(22,3)=C$(66,3),A[0]=C[13],A[2]=0
7952 DISC_DATE$=C$(35,6),DISCOUNT=C[7]; IF POS(" "<>C$(125,6))<>0 THEN IF C$(35,6)<X3$(15,6) THEN DISC_DATE$=C$(125,6),DISCOUNT=C[22],USING_2ND$="Y"; REM "WO131561, 2nd discount date/amount
7955 IF B$(293,1)="Y" THEN A[1]=DISCOUNT-C[11]; GOTO 7961
7960 IF DISC_DATE$>=X3$(15,6) THEN A[1]=DISCOUNT-C[11] ELSE A[1]=0
7970 GOSUB 7500
7973 IF USING_2ND$="Y" THEN A$(28,1)="Y"; REM "WO131561, 2nd discount 
7975 WRITE (Z[1],DOM=7976,KEY=A$(1,20))IOL=0310; T1=1; GOSUB 8300
7980 GOTO 7930
7990 GOTO 1000
8000 REM 
8010 CALL "ZZPROM",".4",X3$,A,"This invoice is not open. Press <Return>","","",0
8020 GOTO 1260
8100 REM "NO VENDOR STATUS RECORD
8110 CALL "ZZPROM",".4",X3$,A,"Vendor has no activity.","","",0
8120 GOTO 1000
8200 REM "Get & Display alternate currency info
8204 A$(25,3)=D$(105,3)
8205 IF POS(" "<>D$(105,3))=0 THEN PRINT @(39,8+V0),'CL',@(39,10+V0),'CL',@(39,12+V0),'CL',@(20,15+V0),'CL',@(20,17+V0),'CL',@(15,19+V0),'CL',; GOTO 8245
8210 IF D$(105,3)<>D0$(4,3) THEN FIND (Z[6],KEY="CUR"+D$(105,3),DOM=8245)D0$
8215 D1$=FNS$(D0$(7,20))
8217 PRINT @(59-LEN(D1$),8+V0),"in ",D1$,":",@(64,8+V0),C[18]:M0$,
8218 PRINT @(59-LEN(D1$),10+V0),"in ",D1$,":",@(64,10+V0),C[18]-C[20]:M0$,
8219 PRINT @(59-LEN(D1$),12+V0),"in ",D1$,":",@(64,12+V0),C[19]-C[21]:M0$,
8220 PRINT @(46-LEN(D1$),15+V0),"Gross amount in ",D1$,":",@(64,15+V0),A[2]:M0$,
8225 PRINT @(44-LEN(D1$),17+V0),"Discount taken in ",D1$,":",@(64,17+V0),A[3]:M0$,
8230 PRINT @(35-LEN(D1$),19+V0),"Net invoice amount paid in ",D1$,":",@(64,19+V0),A[2]-A[3]:M0$,
8245 RETURN 
8300 REM "Compute Check Run Total
8310 T0=T0+(A[0]-A[1])*T1
8315 A0[0]=A[0],A0[1]=A[1]; GOSUB 8400
8320 RETURN 
8325 REM "Print Check Run Total
8330 PRINT @(18,5),T0:M0$,
8335 GOSUB 8425
8340 RETURN 
8350 REM "Print Check Run Total when re-entering program
8355 DIM A[5]; T0=0
8360 READ (Z[1],KEY="",DOM=8361)
8370 READ (Z[1],END=8390)IOL=0310
8375 T1=1; GOSUB 8300
8380 GOTO 8370
8390 VTOT=0
8395 RETURN 
8400 REM "Compute Vendor Total
8410 VTOT=VTOT+(A0[0]-A0[1])*T1
8420 RETURN 
8425 REM "Print Vendor Total
8430 PRINT @(64,3),VTOT:M0$,
8440 RETURN 
8450 REM "Print Vendor Total when leaving vendor field
8455 DIM A0[5]; VTOT=0
8460 READ (Z[1],KEY=A$(1,10),DOM=8461)
8470 READ (Z[1],END=8490)IOL=0315
8472 IF A0$(1,10)<>A$(1,10) THEN GOTO 8490
8475 T1=1; GOSUB 8400
8480 GOTO 8470
8490 GOSUB 8425
8495 RETURN 
8700 REM " Vendor Message
8710 IF F0$(67,1)<>"Y" THEN GOTO 8790
8720 FIND (Z[12],KEY=A$(1,10),DOM=8790)IOL=0420
8730 IF POS(" "<>L$(131,120))>0 THEN PRINT @(7,19),'RB',"NOTE: ",@(14),L$(131,60),@(14,20),L$(191,60) ELSE GOTO 8741
8740 CALL "ZZPROM",".4",X3$,Z,"","","",0
8760 PRINT @(0,19),'CL',@(0,20),'CL'
8790 RETURN 
8900 REM "CLEAR FILE
8915 PRINT @(12,5),"You have already selected invoices for payment either",@(12,7),"through Automatic Invoice Selection, Specific Invoice Selection,",@(12,9),"or the Manual Check Entry program.",@(12,11),"The Clear file option will remove ALL of the selected invoices",@(12,13),"from the payment file.  The Modify option will allow additional",@(12,15),"selection of invoices for payment."
8920 CALL "ZZPROM","X0AP2CBA",X3$,S3,"","","",0
8930 ON S3 GOTO 8960,8940,9800
8940 PRINT 'CF',@(12,10),"Files now being cleared.",@(12,12),"Do not Interrupt!!",
8950 Q$=STR(Z[1]:"00"); CALL "ZZINIT",Q$; PRINT @(0,10),'CL',@(0,12),'CL',
8960 PRINT @(0,3),'CE',
8965 GOSUB 8350
8970 REMOVE (Z[1],DOM=8971,KEY="")
8980 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
