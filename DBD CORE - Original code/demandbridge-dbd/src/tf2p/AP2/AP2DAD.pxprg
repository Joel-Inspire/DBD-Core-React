0010 REM "A/P Invoice overlay 1 <AP2DAD>
0020 SETESC 9300; SETERR 9000
0035 REM "5.1 - 10/17/02 - 16.770555 - dmm - SSP# 154328
0040 REM "Copyright 2002 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0085 X0$="AP2DAD"
0090 L2=L0; L0=0; ON L2 GOTO 0091,2000,2040
0095 IF J9<>0 THEN GOTO 7500
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0300 REM "IOLISTS
0310 IOLIST A$(1,LEN(A$)),A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11]
0320 IOLIST B$
0330 IOLIST C$,C[0],C[1],C[2]
0340 IOLIST D$,D[0],D[1]
0350 IOLIST E$,E[0],E[1],E[2]
0370 IOLIST G$
0390 IOLIST H$,H[0],H[1],H[2],H[3],H[4],H[5],H[6]
0410 IOLIST K$
0420 IOLIST L$
1800 REM "Return to program
1810 GOTO 9900
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF INT(C0)=3 THEN GOSUB 7300 ELSE IF C0>3 OR X0<0 THEN GOTO 1800
2040 ON X0 GOTO 2100,2150,2200,3150,3200,2250,2300,2350,2400,2450,2500,2550,2600,2650,2700,3400,3450,2750,2800,2850,2900,2950,3000,3300,3350,3050,3100,1800
2100 REM "Invoice Date
2110 CALL "ZZENTR","DX",A{ALL},A$,X4$,X3$,31,5+V0,27+K9,6,C0,"","{3"+X$,"","AP2DAA08","","",""
2120 GOSUB 8300
2140 GOTO 2020
2150 REM "Gross Invoice Amount
2160 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,61,5+V0,1,922,C0,M0$,"{3"+X$,"","AP2DAA10","","",""
2165 IF C0<0 THEN GOTO 2020
2170 IF A[1]<0 THEN A$(89+K9,1)="C"; PRINT @(13,8+V0),A$(89+K9,1),
2175 GOSUB 7800
2180 IF F9>0 THEN IF A[1]>F9 THEN A$(87,1)="Y"; PRINT @(61,19),A$(87,1),
2190 GOTO 2020
2200 REM "Terms Code
2205 IF A$(33+K9,2)<>"  " THEN TERMS$=A$(33+K9,2)
2209 IF SEC$(73,1)="Y" THEN GOTO 2240
2210 CALL "ZZENTR","SZX",A{ALL},A$,X4$,X3$,13,7+V0,33+K9,2,C0,"","{3"+X$,"","AP2DAA12","AP2","X",""; IF ABS(C0)=102 THEN J9=2,Q1$="",Z8$="AP2MPE"; RUN "ZZ2STP" ELSE IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2210,2211
2220 FIND (Z[4],KEY=A$(33+K9,2),DOM=2210)IOL=0340
2225 PRINT @(17,7),D$(3,30),
2227 IF C0<0 THEN GOTO 2020
2228 IF TERMS$>"" THEN IF TERMS$<>A$(33+K9,2) THEN A[14]=0,A$(111,6)=DIM(6); REM "If terms code is changed then blank out 2nd discount fields so they will recalculate correctly based on the new terms code
2230 GOSUB 8300; GOSUB 7850
2240 GOTO 2020
2250 REM "Special Amount 1
2255 IF POS(" "<>F1$((X0-5)*20+8,16))=0 THEN GOTO 2020
2260 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,19,9+V0,2,922,C0,M0$,"{3"+X$,"","AP2DAA14","","",""
2265 IF C0>0 THEN GOSUB 7900
2290 GOTO 2020
2300 REM "Special Amount 2
2305 IF POS(" "<>F1$((X0-5)*20+8,16))=0 THEN GOTO 2020
2307 X=A[3]
2310 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,19,10+V0,3,922,C0,M0$,"{3"+X$,"","AP2DAA16","","",""
2315 IF C0>0 THEN IF F0$(71,1)<>"Y" THEN GOSUB 7900 ELSE F9$="*",A[1]=A[1]-X,A[1]=A[1]+A[3]; GOSUB 7900; GOSUB 7950; GOSUB 6200
2340 GOTO 2020
2350 REM "Special Amount 3
2355 IF POS(" "<>F1$((X0-5)*20+8,16))=0 THEN GOTO 2020
2360 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,19,11+V0,4,922,C0,M0$,"{3"+X$,"","AP2DAA18","","",""
2365 IF C0>0 THEN GOSUB 7900
2390 GOTO 2020
2400 REM "Special Amount 4
2405 IF POS(" "<>F1$((X0-5)*20+8,16))=0 THEN GOTO 2020
2410 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,19,12+V0,5,922,C0,M0$,"{3"+X$,"","AP2DAA20","","",""
2415 IF C0>0 THEN GOSUB 7900
2440 GOTO 2020
2450 REM "Invoice Due Date
2460 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,61,7+V0,35+K9,6,C0,"","{3"+X$,"","AP2DAA22","","",""
2490 GOTO 2020
2500 REM "Discount Applies to
2510 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,61,8+V0,6,922,C0,M0$,"{3"+X$,"","AP2DAA24","","",""
2525 IF C0>0 THEN GOSUB 7935
2540 GOTO 2020
2550 REM "Discount Amount
2560 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,61,9+V0,7,922,C0,M0$,"{3"+X$,"","AP2DAA26","","",""
2570 IF C0>0 THEN GOSUB 7830
2590 GOTO 2020
2600 REM "Discount Expires on
2610 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,61,10+V0,41+K9,6,C0,"","{3"+X$,"","AP2DAA28","","",""
2640 GOTO 2020
2650 REM "Workmans Comp W/H
2655 GOTO 2020
2660 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,61,11+V0,8,922,C0,M0$,"{3"+X$,"","AP2DAA30","","",""
2690 GOTO 2020
2700 REM "Net Invoice Amount
2710 CALL "ZZENTR","NS",A{ALL},A$,X4$,X3$,61,12+V0,9,922,C0,M0$,"{3"+X$,"","AP2DAA32","","",""
2740 GOTO 2020
2750 REM "Invoice Comment
2760 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,27,14+V0,47+K9,15,C0,"","{3"+X$,"","AP2DAA34","","",""
2770 REM "Salesperson - do no disturb X0
2775 IF F0$(70,1)<>"Y" THEN GOTO 2020
2780 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,67,14,100,4,C0,"","{3"+X$,"","AP2DAA35","AR3","AR2MBB",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2780,2781
2785 IF A$(100,4)<>"    " THEN FIND (Z[9],KEY=A$(100,4),DOM=2780)
2790 GOTO 2020
2800 REM "Our Reference Number
2810 CALL "ZZENTR","AU",A{ALL},A$,X4$,X3$,27,15+V0,62+K9,10,C0,"P/O","{3"+X$,"","AP2DAA36","","",""
2840 GOTO 2020
2850 REM "Print Check on Bank Code
2855 IF F0$(16,1)="N" THEN GOTO 2020
2860 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,27,17+V0,72+K9,3,C0,"","{3"+X$,"","AP2DAA38","ZYB","ZY2BAA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2860,2861
2870 FIND (Z[5],KEY=A$(72+K9,3),DOM=2860)IOL=0350
2875 PRINT @(31,17),E$(4,30),
2880 IF A$(108,3)<>E$(105,3) THEN A$(108,3)=E$(105,3),A[12]=0,A[13]=0; CALL "AP2DAF",X3$,X4$,"D",A$,A{ALL}
2890 GOTO 2020
2900 REM "G/L Category Code
2910 CALL "ZZENTR","A",A{ALL},A$,X4$,X3$,27,18+V0,75+K9,9,C0,"AP2","{3"+X$,"","AP2DAA40","AP3","X",""; IF ABS(C0)=102 THEN J9=4,Q1$="",Z8$="AP2MPF"; RUN "ZZ2STP" ELSE IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2910,2911
2920 FIND (Z[7],KEY=A$(75+K9,9),DOM=2910)IOL=0370
2925 PRINT @(38,18),G$(10,30),
2940 GOTO 2020
2950 REM "Payment Selection Code
2960 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,27,19+V0,84+K9,2,C0,"","{3"+X$,"","AP2DAA42","","",""
2990 GOTO 2020
3000 REM "Payment Priority Code
3010 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,27,20+V0,86+K9,1,C0,"","{3"+X$,"","AP2DAA44","","",""
3040 GOTO 2020
3050 REM "Place Invoice on Hold?
3055 IF F9>0 THEN IF A[1]>F9 THEN A$(87,1)="Y"; PRINT @(61,19),A$(87,1),; GOTO 2020
3060 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,61,19+V0,87+K9,1,C0,"","{3"+X$,"","AP2DAA46","","",""
3090 GOTO 2020
3100 REM "# Days to extend due date
3110 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,61,20+V0,10,200,C0,"00","{3"+X$,"","AP2DAA48","","",""
3140 GOTO 2020
3150 REM "Invoice Type
3155 X$="I-Invoice, R-Return, A-Adjustment, D-Debit Memo, C-Credit Memo"
3160 CALL "ZZENTR","SRXU",A{ALL},A$,X4$,X3$,13,8+V0,89+K9,1,C0,"","{3"+X$,"IRADC","AP2DAA12","","",""
3165 IF I1<>0 THEN IF A$(89+K9,1)="I" THEN GOTO 3150
3190 GOTO 2020
3200 REM "APPLIES TO INVOICE NUMBER
3205 IF A$(89+K9,1)="I" THEN A$(90,10)=""; PRINT @(25,8+V0),A$(90,10),; GOTO 2020 ELSE J$=A$(7,10)
3210 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,26,8+V0,90+K9,10,C0,"","{3"+X$,J$,"AP2DAA13","API","X",""; IF ABS(C0)=102 THEN J9=3,Q1$=A$(7,10),Z8$="AP2API"; RUN "ZZ2STP" ELSE IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3210,3211
3240 GOTO 2020
3300 REM "Gross Amount in Alt Currency
3305 IF POS(" "<>A$(108,3))=0 THEN GOTO 2020
3310 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,61,15,12,922,C0,M0$,"{3"+X$,"","AP2DAA33","","",""
3340 GOTO 2020
3350 REM "Discount Amount in Alt Currency
3355 IF POS(" "<>A$(108,3))=0 THEN GOTO 2020
3360 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,61,16,13,922,C0,M0$,"{3"+X$,"","AP2DAA35","","",""
3390 GOTO 2020
3400 REM "2nd discount amount, WO131561
3405 IF F0$(80,1)<>"Y" THEN GOTO 2020
3410 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,19,13+V0,14,922,C0,M0$,"{3"+X$,"","AP2DAA50","","",""
3440 GOTO 2020
3450 REM "2nd discount expires on, WO131561
3455 IF F0$(80,1)<>"Y" THEN GOTO 2020
3460 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,61,13+V0,111,6,C0,"","{3"+X$,"","AP2DAA52","","",""
3490 GOTO 2020
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(0,3+V0),"Fiscal Year:",@(23,3+V0),"Period:",@(0,4+V0),"Vendor Code:",@(25,4+V0),"Name:",@(4,5+V0),"Invoice:",@(25,5+V0),"Date:",@(47,5+V0),"Gross Amount:",@(0,6+V0),Z0$
6070 IF Z9<0 THEN GOTO 6160
6080 PRINT @(0,7+V0),'CE','SB',
6085 PRINT @(1,7+V0),"Terms Code:",@(48,7+V0),"Invoice Due:",@(2,8+V0),"Inv. Type:",@(16,8+V0),"Apply to:",@(40,8+V0),"Discount Applies to:",@(44,9+V0),"Discount Amount:",@(40,10+V0),"Discount Expires on:",@(42,11+V0),"",@(40,12+V0),"Amount to distribute:",@(0,13+V0),Z0$,
6090 PRINT @(10,14+V0),"Invoice Comment:",@(15,15+V0),"P/O Number:",; IF F0$(16,1)="Y" THEN PRINT @(1,17+V0),"Print check on bank code:",
6095 PRINT @(8,18+V0),"G/L Category Code:",@(3,19+V0),"Payment Selection Code:",@(38,19+V0),"Place Invoice on Hold?",@(4,20+V0),"Payment Priority Code:",@(34,20+V0),"# Days to extend due date:",
6120 FOR X=0 TO 3; IF POS(" "<>F1$(X*20+8,16))=0 THEN NEXT X ELSE PRINT @(0,9+X+V0),F1$(8+X*20,16),":",; NEXT X
6130 IF F0$(70,1)="Y" THEN PRINT @(54,14),"Salesperson:"
6165 PRINT (0,ERR=6066)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 CALL "ZZDISP","D",A$(27+K9,6),"",X3$,"","",31,5+V0,X4$
6222 PRINT @(61,5+V0),A[1]:M0$,@(13,7+V0),A$(33+K9,2),@(17,7+V0),D$(3,30),@(13,8+V0),A$(89+K9,1),@(26,8+V0),A$(90,10),
6224 FOR X=0 TO 3; IF POS(" "<>F1$(8+X*20,16))=0 THEN NEXT X ELSE PRINT @(19,9+X+V0),A[2+X]:M0$,; NEXT X
6228 CALL "ZZDISP","D",A$(35+K9,6),"",X3$,"","",61,7+V0,X4$
6232 PRINT @(61,8+V0),A[6]:M0$,@(61,9+V0),A[7]:M0$,
6236 CALL "ZZDISP","D",A$(41+K9,6),"",X3$,"","",61,10+V0,X4$
6238 CALL "ZZDISP","A",A$(62+K9,10),"P/O",X3$,"","",27,15+V0,X4$
6240 PRINT @(61,12+V0),A[9]:M0$,@(27,14+V0),A$(47+K9,15),; IF F0$(16,1)="Y" THEN PRINT @(27,17+V0),A$(72+K9,3),@(31,17),E$(4,30),
6250 CALL "ZZDISP","A",A$(75+K9,9),"AP2",X3$,"","",27,18+V0,X4$
6260 PRINT @(38,18),G$(10,30),@(27,19+V0),A$(84+K9,2),@(27,20+V0),A$(86+K9,1),@(61,19+V0),A$(87+K9,1),@(61,20+V0),A[10]:"00",
6270 IF F0$(70,1)="Y" THEN PRINT @(67,14),A$(100,4),
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6410 GOSUB 6000
6430 IF Z9>=0 THEN GOSUB 6200 ELSE GOSUB 6450; GOSUB 8700
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF A$="" THEN GOTO 6490
6460 PRINT @(13,3+V0),A$(1+K9,4),@(31,3+V0),A$(5+K9,2),
6464 CALL "ZZDISP","A",A$(7+K9,10),"A/P",X3$,"","",13,4+V0,X4$
6468 PRINT @(31,4+V0),C$(11,35),@(13,5+V0),A$(17+K9,10),
6470 GOSUB 7700
6490 RETURN 
7300 REM 
7310 ON FPT(C0)*10 GOTO 7390,7325,7330,7390
7325 IF V8$="" THEN X0=X0+4; GOTO 7390 ELSE X0=NUM(V8$(POS(STR(X0:"00")<=V8$,2),2)); GOTO 7390
7330 IF V8$="" THEN X0=X0-6; GOTO 7335 ELSE X0=POS(STR(X0-1:"00")<=V8$,2)-2; IF X0<0 THEN X0=0; GOTO 7390 ELSE X0=NUM(V8$(X0,2)); GOTO 7390
7335 IF X0<0 THEN X0=0; GOTO 7390
7390 C0=3; RETURN 
7500 REM "Return from setup control
7505 C0=0
7510 J8=J9,J9=0; ON J8 GOTO 7590,7515,7520,7525,7530
7515 IF Q1$<>"" THEN A$(7,10)=Q1$,C0=1; GOSUB 6400; GOTO 1311 ELSE GOSUB 6400; GOTO 1310
7520 IF Q1$<>"" THEN A$(33,2)=Q1$,C0=1; GOSUB 6400; GOTO 2211 ELSE GOSUB 6400; GOTO 2210
7525 IF LEN(Q1$)>10 THEN A$(90,10)=Q1$(11),C0=1; GOSUB 6400; GOTO 3211 ELSE GOSUB 6400; GOTO 3210
7530 IF Q1$>"" THEN A$(75,9)=Q1$,C0=1; GOSUB 6400; GOTO 2911 ELSE GOSUB 6400; GOTO 2910
7590 GOTO 0100
7700 REM "PRINT P/E DATE"
7720 PRINT @(34,3+V0),"Ending ",FND$(P1$(NUM(A$(5+K9,2))*6+15,6)),
7749 RETURN 
7750 PRINT @(55,11+V0),"Ending ",FND$(P9$(NUM(A$(19+K9,2))*6+15,6)),
7799 RETURN 
7800 REM "COMPUTE VAT
7810 IF F0$(71,1)<>"Y" THEN GOSUB 7900; GOSUB 7850; GOTO 7840
7820 A[3]=0; GOSUB 7900
7830 IF F0$(71,1)<>"Y" OR F9$="*" THEN GOTO 7840 ELSE A[1]=A[1]-A[3],A[3]=(A[1]-A[7])*NUM(F0$(72,4))*.0001,A[1]=A[1]+A[3]; GOSUB 7950; GOSUB 6200
7840 RETURN 
7850 REM "CALC DISC AMT
7880 A[7]=A[6]*D[0]/100
7882 IF F0$(80,1)="Y" AND D$(43,1)="P" AND D[1]<>0 THEN A[14]=A[6]*D[1]/100; REM "WO131561, 2nd discount amount
7885 PRINT @(61,8+V0),A[6]:M0$,@(61,9+V0),A[7]:M0$,
7886 IF F0$(80,1)="Y" THEN PRINT @(19,13+V0),A[14]:M0$
7887 GOSUB 7830
7890 RETURN 
7900 REM "CALCULATE DISCOUNT APPLIES TO
7910 A[6]=A[1]
7915 FOR X=0 TO 3; IF POS(STR(X+1:"0")=C$(294,5))=0 AND F1$(25+20*X,1)<>"Y" THEN A[6]=A[6]-A[2+X]
7918 NEXT X
7920 IF F0$(79,1)="Y" AND POS("1"=C$(294,5))=0 AND F1$(25,1)<>"Y" THEN A[6]=A[6]-LINE_FREIGHT; REM "SSP118375, exclude freight on lines from discount
7935 GOSUB 7850
7950 REM "CALC NET INV AMT
7955 A[9]=A[1]
7960 FOR X=0 TO 3; IF F1$(24+X*20,1)="Y" THEN A[9]=A[9]-A[2+X]; NEXT X ELSE NEXT X
7965 A[9]=A[9]-A[8]
7970 IF F0$(15,1)="L" THEN A[9]=A[9]-A[7]
7985 PRINT @(61,12+V0),A[9]:M0$,
7990 RETURN 
8300 REM "CALC DUE DATE
8305 REM "EXTRA DAYS ADDED TO DUE DATE AT PRINT TIME
8310 CALL "ZZDISC",X3$,D$,0,0,A$(27+K9,6),Q4$,Q2$,0,0
8370 A$(35+K9,6)=Q4$,A$(41,6)=Q2$
8372 IF F0$(80,1)="Y" AND D$(43,1)="P" AND D$(53,3)<>"   " THEN ORIGINAL_D$=D$,D$(44,3)=D$(53,3),Q2$=""; CALL "ZZDISC",X3$,D$,0,0,A$(27+K9,6),Q4$,Q2$,0,0; A$(111,6)=Q2$,D$=ORIGINAL_D$; REM "WO131561, 2nd discount expires on date
8375 PRINT @(61,7+V0),FND$(A$(35+K9,6)),@(61,10+V0),FND$(A$(41+K9,6)),
8377 IF F0$(80,1)="Y" THEN PRINT @(61,13+V0),FND$(A$(111,6)),
8395 RETURN 
8700 REM "Place for vendor message not needed here
8710 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9710 IF U1$="" THEN REMOVE (Z[6],KEY=U0$,DOM=9900); GOTO 9900
9715 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9720 WRITE (Z[6],KEY=U0$)U0$,U1$
9725 L0=0; PRINT @(0,3),'CE',
9730 RUN U1$(1,6)
9900 REM "END PROGRAM
9910 L0=-1
9920 RUN "AP2DAA"
9999 END 
