0010 REM "WWW Support Interface <WWWSUP>
0020 SETESC 9300; SETERR 9000
0035 REM "4.1 - 01/21/97 - 17.94 - kmc
0040 REM "Copyright 1997 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER PARAM$,DATA${ALL},NUM_ENTRIES,OUTPUT
0100 SETERR 9000
0110 X0$="WWWSUP",X1$="WWW Support Interface"
0120 EOL$=STP(PARAM$(1,4),1)
0310 IOLIST A$(1),A[0],A[1],A[2]
0500 REM "Open files
0505 DIM Z[20]
0511 Z[1]=UNT; OPEN (Z[1])"ZW2"
0512 Z[2]=UNT; OPEN (Z[2])"ZWD"
0513 Z[3]=UNT; OPEN (Z[3])"SS1"
0517 Z[7]=UNT; OPEN (Z[7])"ZWE"
0518 Z[8]=UNT; OPEN (Z[8])"ZWF"
0521 Z[11]=UNT; OPEN (Z[11])"ZWP"
0522 Z[12]=UNT; OPEN (Z[12])"ZWH"
0524 Z[14]=UNT; OPEN (Z[14])"ZWI"
0525 Z[15]=UNT; OPEN (Z[15])"ZW4"
1000 REM "Process DATA array
1005 GOSUB DEFAULT_RECORD
1006 GOSUB DEFAULT_TAG_LIST
1010 FOR I=1 TO NUM_ENTRIES
1015 TAG$=DATA$[I,0],VALUE$=DATA$[I,1]
1020 TAG_POS=POS(PAD(TAG$,TAG_LENGTH)=TAG_LIST$,TAG_LENGTH); IF TAG_POS>0 THEN ON INT(TAG_POS/TAG_LENGTH) GOSUB 7601,7602,7603,7604,7605,7606,7607,7608,7609,7610,7611,7612,7613,7614,7615,7616,NO_MATCH
1090 NEXT I
1100 REM "Done with record
1120 IF CHANGE$="Y" THEN GOSUB GET_NOTES; GOSUB ADD_NOTES ELSE GOSUB ADD_RECORD
1200 DIM Q$[5,2]
1205 Q$[1,0]="client",Q$[1,1]=A$(240,10),Q$[2,0]="issue",Q$[2,1]=A$(1,6),Q$[3,0]="conf",Q$[3,1]="Y",Q$[4,0]="password",Q$[4,1]=PASSWORD$; IF CHANGE$="Y" THEN Q$[5,0]="change",Q$[5,1]="Y"
1210 CALL "WWWSSI",PARAM$,Q${ALL},FN%NEA("Q$",1),OUTPUT
1995 GOTO 9900
2000 REM "Start html document
2005 PRINT (OUTPUT)"Content-type: text/html",EOL$,EOL$,
2010 FILE_NAME$=PARAM$(5,8); CALL "WWWSND",ERR=2011,PARAM$,FILE_NAME$,OUTPUT; REM "Send file header
2050 PRINT (OUTPUT)A$,EOL$,"<BR>",TAG_LIST$,"<BR>",EOL$,TAG_LENGTH,"<BR>",NUM_ENTRIES,"<BR>","TAG_LIST LENGTH = ",LEN(TAG_LIST$),"<BR>",EOL$,
2090 FILE_NAME$=PARAM$(13,8); CALL "WWWSND",ERR=2091,PARAM$,FILE_NAME$,OUTPUT; REM "Send file trailer
2095 RETURN 
2100 EXIT_WITH_MESSAGE:REM "Send back standard error page with message dependent on MESSAGE_CODE
2105 PRINT (OUTPUT)"Content-type: text/html",EOL$,EOL$,
2110 FILE_NAME$=PARAM$(5,8); CALL "WWWSND",ERR=2111,PARAM$,FILE_NAME$,OUTPUT; REM "Send file header
2115 ON MESSAGE_CODE GOTO 2190,2121,2122,2123,2124,2125,2190
2121 PRINT (OUTPUT)"<BR><BR><H2>","Client code or password is invalid, please correct and re-submit.","</H2><BR><BR>",EOL$,; GOTO 2190
2122 PRINT (OUTPUT)"<BR><BR><H2>","Your support request for client ",VALUE$," cannot be processed at this time.   Please contact the TopForm Accounting Department to resolve this matter.","</H2><BR><BR>",EOL$,; GOTO 2190
2123 PRINT (OUTPUT)"<BR><BR><H2>","The support issue ",ISSUE$," does not exist, please correct and re-submit.","</H2><BR><BR>",EOL$,; GOTO 2190
2124 PRINT (OUTPUT)"<BR><BR><H2>","The support issue ",ISSUE$," is not one of your support issues, please correct and re-submit.","</H2><BR><BR>",EOL$,; GOTO 2190
2125 PRINT (OUTPUT)"<BR><BR><H2>","The following required fields were not supplied, please correct and re-submit: </H2><FONT SIZE=+1><FONT COLOR="+QUO+"Red"+QUO+">"+MESS_1$,"</FONT></FONT><BR><BR>",EOL$,; GOTO 2190
2190 FILE_NAME$=PARAM$(13,8); CALL "WWWSND",ERR=2191,PARAM$,FILE_NAME$,OUTPUT; REM "Send file trailer
2195 EXIT_WITH_MESSAGE_END:GOTO 9900
3000 SETUP_CLIENT:REM "Setup and default client info based on client number input. If client not on file then set MESSAGE_CODE to 1, if on credit hold MESSAGE_CODE is 2
3003 TEMP$=FN%CVT4$(VALUE$); P_DASH=POS("-"=TEMP$)
3004 IF P_DASH>0 THEN IF P_DASH=1 THEN TEMP_2$=TEMP$(P_DASH+1),TEMP$="" ELSE IF P_DASH=LEN(TEMP$) THEN TEMP_2$="",TEMP$=TEMP$(1,P_DASH-1) ELSE TEMP_2$=TEMP$(P_DASH+1),TEMP$=TEMP$(1,P_DASH-1)
3005 A$(240,10)=STR(NUM(TEMP$):"00000000")+STR(NUM(TEMP_2$):"00")
3010 FIND (Z[3],KEY=A$(240,10),DOM=3011)C$; GOTO 3020
3012 MESSAGE_CODE=1; GOTO EXIT_WITH_MESSAGE
3020 IF STP(C$(621,10),1)<>PASSWORD$ THEN MESSAGE_CODE=1; GOTO EXIT_WITH_MESSAGE
3022 A$(39,14)=C$(195,14); REM "set phone number
3025 IF STP(A$(23,16),3," ")="" THEN A$(23,16)=C$(231,30); REM "Set contact if it is blank
3030 IF C$(223,1)="Y" THEN MESSAGE_CODE=2; IF A$(248,2)<>"00" THEN CLIENT$=STR(NUM(A$(240,8)))+"-"+A$(248,2) ELSE CLIENT$=STR(NUM(A$(240,8))) END_IF ; GOTO EXIT_WITH_MESSAGE
3045 SETUP_CLIENT_END:RETURN 
3050 SETUP_HARDWARE:REM "Based on the hardware field set category and maybe program
3055 GOSUB INIT_HARDWARE
3060 FOR HW_INDEX=1 TO NUM_HW_ITEMS; IF VALUE$=HW$[HW_INDEX,0] THEN A$(173,2)=HW$[HW_INDEX,1]; EXITTO 3061 END_IF ; NEXT HW_INDEX
3075 CATEGORY$=A$(173,2); REM "for later use
3095 SETUP_HARDWARE_END:RETURN 
3100 INIT_HARDWARE:REM "Setup hw$ array to be MATCH STRING, and category to set
3105 DIM HW$[20,1]; NUM_HW_ITEMS=0
3111 HW$[1,1]="SI",HW$[1,0]="TopForm Software Issue"
3112 HW$[2,1]="EH",HW$[2,0]="Enhancement Request"
3113 HW$[3,1]="HW",HW$[3,0]="OTHER Hardware?"
3114 HW$[4,1]="HM",HW$[4,0]="Modem"
3115 HW$[5,1]="PE",HW$[5,0]="Program Exception"
3116 HW$[6,1]="3P",HW$[6,0]="VSI*FAX"
3117 HW$[7,1]="3P",HW$[7,0]="Terminal Emulation (TinyTERM, Procom, IceTen...)"
3118 HW$[8,1]="3P",HW$[8,0]="Word Perfect"
3119 HW$[9,1]="3P",HW$[9,0]="Forms Express"
3120 HW$[10,1]="3P",HW$[10,0]="Windows 3.x / WIN95"
3121 HW$[11,1]="3P",HW$[11,0]="OTHER 3rd Party Software?"
3122 HW$[12,1]="HT",HW$[12,0]="Terminal"
3123 HW$[13,1]="HP",HW$[13,0]="Printer"
3124 HW$[14,1]="HW",HW$[14,0]="Tape Backup"
3125 HW$[15,1]="WO",HW$[15,0]="Work Order"
3126 HW$[16,1]="PA",HW$[16,0]="Procedural Assistance"
3127 NUM_HW_ITEMS=16
3145 INIT_HARDWARE_END:RETURN 
5000 REM "End
5095 GOTO 9900
6700 ADD_RECORD:REM "ADD ENTRY
6702 MESS_1$=""
6703 CHK_FLD$=CONTACT_NAME$,CHK_NAME$="Contact Name"; GOSUB CHECK_FIELD; CHK_FLD$=CATEGORY$,CHK_NAME$="Problem Category"; GOSUB CHECK_FIELD; CHK_FIELD$=BRIEF_DESC$,CHK_NAME$="Brief Description"; GOSUB CHECK_FIELD
6705 IF MESS_1$<>"" THEN MESSAGE_CODE=5; GOTO EXIT_WITH_MESSAGE
6708 GOSUB NEXT_SSP_NUM
6710 WRITE (Z[1],KEY=A$(1,6))IOL=0310
6728 WRITE (Z[2],KEY=A$(195,1)+A$(194,1)+A$(7,6)+A$(13,6)+A$(1,6))
6735 WRITE (Z[7],KEY=A$(175,8)+A$(183,8)+A$(1,6))
6745 WRITE (Z[8],KEY=A$(240,10)+A$(1,6))
6755 WRITE (Z[12],KEY=A$(7,10)+A$(1,6))
6765 IF POS(A$(195,1)="CFLZY")>0 THEN REMOVE (Z[14],KEY=A$(1,6),DOM=6770); GOTO 6770
6766 WRITE (Z[14],KEY=A$(1,6))A$(1,6)+A$(195,1)+DIM(22)
6770 IF NOTES$<>"" THEN GOSUB ADD_NOTES
6795 ADD_RECORD_END:RETURN 
6800 ADD_NOTES:REM "Add NOTES$ string to ZW4 file for this issue
6804 REC_SIZE=244,COUNT=0
6805 IF STP(CONTACT_NAME$,3," ")="" THEN TEMP_CONTACT$=STP(A$(23,16),1) ELSE TEMP_CONTACT$=STP(CONTACT_NAME$,1)
6806 NOTES$=OLD_NOTES$+$0A$+"*clientin*WWW*"+DAY+"*"+FNT$(STR(INT(100*TIM):"0000"))+"*"+TEMP_CONTACT$+$0A$+NOTES$
6807 P=POS($0D$=NOTES$); IF P<>0 THEN NOTES$(P,1)=" "; GOTO 6807
6810 COUNT=COUNT+1; KEY_15$=A$(1,6)+STR(COUNT:"00")
6815 IF LEN(NOTES$)<=REC_SIZE THEN OUT$=NOTES$,NOTES$="" ELSE OUT$=NOTES$(1,REC_SIZE),NOTES$=NOTES$(REC_SIZE+1)
6820 WRITE (Z[15],KEY=KEY_15$)KEY_15$,OUT$
6825 IF NOTES$<>"" THEN GOTO 6810
6845 ADD_NOTES_END:RETURN 
6850 GET_NOTES:REM "Retrieve existing notes for call
6853 CLIENT$=A$(240,10); READ RECORD (Z[1],KEY=ISSUE$,DOM=6854)A$; IF A$(240,10)<>CLIENT$ THEN MESSAGE_CODE=4; GOTO EXIT_WITH_MESSAGE ELSE GOTO 6855; REM "Save off formatted client code to check after reading issue number in
6854 MESSAGE_CODE=3; GOTO EXIT_WITH_MESSAGE
6855 OLD_NOTES$=""; READ (Z[15],KEY=A$(1,6),DOM=6856)
6860 READ (Z[15],END=6875)KEY_15$,IN$; IF KEY_15$(1,6)<>A$(1,6) THEN GOTO 6875 ELSE OLD_NOTES$=OLD_NOTES$+IN$; GOTO 6860
6880 IF OLD_NOTES$(LEN(OLD_NOTES$),1)<>$0A$ THEN OLD_NOTES$=OLD_NOTES$+$0A$
6881 TEMP$=FN%NTD$(JUL(0,0,0),"YYYYMMDD"); TEMP$=CHR(NUM(TEMP$(1,3))-125)+TEMP$(4)+STR(100*TIM:"0000")
6882 A$(196,10)=TEMP$
6883 REMOVE (Z[2],KEY=A$(195,1)+A$(194,1)+A$(7,6)+A$(13,6)+A$(1,6),DOM=6884); REM "Remove sort key and re-write with new status
6884 A$(195,1)="!"; REM "IF change set status to '!' to move to do now list
6885 WRITE (Z[2],KEY=A$(195,1)+A$(194,1)+A$(7,6)+A$(13,6)+A$(1,6))
6886 WRITE RECORD (Z[1],KEY=A$(1,6))A$
6895 GET_NOTES_END:RETURN 
6900 CHECK_FIELD:REM "if chk_fld$ is blank, add chk_name to MESS_1$. seperate with <BR>s
6905 IF STP(CHK_FLD$,3," ")="" OR CHK_FLD$="" THEN MESS_1$=MESS_1$+CHK_NAME$+"<BR>"
6945 CHECK_FIELD_END:RETURN 
7500 DEFAULT_RECORD:REM "Set defaults in record
7505 DIM A$(290),A[2]; NOTES$=""
7510 A$(194,1)="1",A$(195,1)="!"; REM "priority 1, status =Look now
7512 A$(196,10)=A$(7,10),A$(206,1)="S"; REM "status date/time, billing code Support
7514 A$(253,3)="WWW"; REM "respsonsible person = www
7516 A[0]=.25; REM "Total time init'ed to .25
7520 TEMP$=FN%NTD$(JUL(0,0,0),"YYYYMMDD"); TEMP$=CHR(NUM(TEMP$(1,3))-125)+TEMP$(4)+STR(100*TIM:"0000")
7522 A$(7,10)=TEMP$,A$(196,10)=TEMP$; REM "Initial date, status date to today's date
7545 DEFAULT_RECORD_END:RETURN 
7550 DEFAULT_TAG_LIST:REM "Setup the Tag list with entries that are TAG_LE"+      "NGTH long: start at 7601 corresponding to tag 1. IF setup ="Y" then s"+      "etup else perform action
7555 TAG_LENGTH=10
7590 TAG_LIST$=""
7595 SETUP$="Y"
7600 REM "TAG LIST for setup or action
7601 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("contact",TAG_LENGTH) ELSE IF STP(A$(23,16),3," ")="" THEN A$(23,16)=VALUE$ END_IF ; CONTACT_NAME$=VALUE$; RETURN 
7602 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("client",TAG_LENGTH) ELSE GOSUB SETUP_CLIENT; RETURN 
7603 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("brief",TAG_LENGTH) ELSE BRIEF_DESC$=VALUE$,P=POS($0D0A$=VALUE$); IF P=0 THEN A$(53,120)=VALUE$ ELSE A$(53,60)=VALUE$(1,P-1); P1=POS($0D0A$=VALUE$(P+2)); IF P1=0 THEN A$(113,60)=VALUE$(P+2) ELSE A$(113,60)=VALUE$(P+2,P1-1) END_IF END_IF ; RETURN 
7604 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("line",TAG_LENGTH) ELSE A$(186,5)=VALUE$; RETURN 
7605 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("menu",TAG_LENGTH) ELSE A$(214,3)=UCS(VALUE$); RETURN 
7606 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("error",TAG_LENGTH) ELSE A$(183,3)=VALUE$; RETURN 
7607 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("program",TAG_LENGTH) ELSE IF STP(A$(175,8),3," ")="" THEN A$(175,8)=UCS(VALUE$) END_IF ; RETURN 
7608 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("option",TAG_LENGTH) ELSE A$(217,2)=UCS(VALUE$); RETURN 
7609 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("category",TAG_LENGTH) ELSE IF STP(A$(173,2),3," ")="" THEN A$(173,2)=VALUE$ END_IF ; RETURN 
7610 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("software",TAG_LENGTH) ELSE A$(173,2)=VALUE$; RETURN 
7611 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("hardware",TAG_LENGTH) ELSE GOSUB SETUP_HARDWARE; RETURN 
7612 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("details",TAG_LENGTH) ELSE NOTES$=VALUE$; RETURN 
7613 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("password",TAG_LENGTH) ELSE PASSWORD$=VALUE$; RETURN 
7614 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("change",TAG_LENGTH) ELSE CHANGE$="Y"; RETURN 
7615 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("issue",TAG_LENGTH) ELSE ISSUE$=STR(NUM(VALUE$):"000000"); RETURN 
7616 IF SETUP$="Y" THEN TAG_LIST$=TAG_LIST$+PAD("email",TAG_LENGTH) ELSE A$(261,30)=VALUE$; RETURN 
7699 DEFAULT_TAG_LIST_END: SETUP$="N"; RETURN 
7700 NO_MATCH:REM "No match from tag list do nothing?
7745 NO_MATCH_END:RETURN 
8800 NEXT_SSP_NUM:REM "Get the next SSP number
8810 EXTRACT (Z[11],KEY="SSP",DOM=8811)Q$,Q0; A$(1,6)=STR(Q0:"000000"),Q0=Q0+1
8815 WRITE (Z[11],KEY="SSP")"SSP",Q0
8845 NEXT_SSP_NUM_END:RETURN 
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
9000 REM "ERROR PROCESSING
9001 J=ERR,K=TCB(5)
9002 SETERR 0000; ENDTRACE 
9004 FILE_NAME$=PARAM$(5,8); CALL "WWWSND",ERR=9005,PARAM$,FILE_NAME$,OUTPUT; REM "Send file header
9005 PRINT (OUTPUT,ERR=9010)"ERR=",STR(J)," LINE = ",STR(K),"<BR>",EOL$,
9010 GOTO 9900
9900 REM "End
9905 FOR I=1 TO 20; IF Z[I]<>0 THEN CLOSE (Z[I]) END_IF ; NEXT I
9950 EXIT 
9999 END 
