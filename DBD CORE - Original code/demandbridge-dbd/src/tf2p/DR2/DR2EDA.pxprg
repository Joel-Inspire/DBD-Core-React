0010 ! <DR2EDA> Data Replication Embedded I/O for PVX file
0020 OPEN_REQUEST:EXIT ! "don't do anything on open right now
0035 REM "5.7 - 12/03/21 - 12.790172 - crg - SSP# 307385
0037 REM "307385-DBD-253 - Data rep change tracking mode                     
0040 REM "Copyright 2021 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0100 ! 
1000 POST_WRITE:! update the database
1005 SETERR 9000
1015 LOCAL ACCESS_MODE,KEY$,INDEX,VALUE$,FILE_NAME$,OUTPUT_PORT,ERR_MSG$,TMP,QUE_KEY$,QUE_CODE$,DR_I,DR1_K$
1020 ENTER ACCESS_MODE,KEY$,INDEX,VALUE$
1030 IF %DR_SUSPEND THEN GOTO JUST_EXIT ! 237003-Suspend datarep if flag is turned on; 307335
1100 ! ^100 Make sure DR2+%C is open, if not open on %DR2_PORT
1110 GOSUB OPEN_REP_FILE
1115 TMP=EVN(FILE_NAME$,ERR=JUST_EXIT) ! 159723, check out file name before updating rep data
1120 GOSUB SET_KEY
1500 ! Write out to DR2
1505 ! Only supports keyed files, if sort, index, or serial then extra work needed
1510 ! DR2 key is Priority code (in %DR2_PRIORITY$ or blank if not there) + YYMMDD + current time to 5 digits + .9999 where 9999 is counter in global var %DR2_COUNT - if DOM then try again
1515 ! DR2 record is 25 char que key+ $8A$ + 1 char Command (W=write/update, D=delete)+$8A$+file_name+$8A$+key+$8A$+data, alt key is file_name + que key
1520 IF NUL(QUE_CODE$) THEN GOTO JUST_EXIT
1530 FOR DR_I=0 TO (LEN(QUE_CODE$)/6)-1
1540 IF QUE_CODE$(DR_I*6+3,1)="C" THEN WRITE (%DRT_PORT,KEY=PAD(FILE_NAME$,10)+KEY$)FILE_NAME$+SEP+KEY$+SEP+"W"+SEP+DTE(0:"%Y%Mz%Dz%Hz%mz%sz")+SEP+MID(%X3$,40,3)+SEP+FID(0)+SEP+WHO+SEP+VALUE$; GOTO *CONTINUE
1550 QUE_KEY$=PAD(QUE_CODE$(DR_I*6+4,3)+DTE(0:"%Yz%Mz%Dz")+STR(TIM:"00.00000")+"."+MID(STR(%DR2_COUNT++:DIM(10,"0")),-7),25); WRITE (OUTPUT_PORT,KEY=QUE_KEY$,DOM=*SAME)QUE_KEY$+SEP+"W"+SEP+PAD(FILE_NAME$,8)+QUE_CODE$(DR_I*6+1,2)+SEP+KEY$+SEP+VALUE$ ! SSP 223989, 282624 ssp276890
1555 IF %DR2_COUNT>9999999 THEN WAIT 1; %DR2_COUNT=0 ! 282624
1560 NEXT DR_I
1990 EXIT 
1999 ! 
2000 POST_REMOVE:
2005 LOCAL ACCESS_MODE,KEY$,INDEX,VALUE$,FILE_NAME$,OUTPUT_PORT,ERR_MSG$,TMP,QUE_KEY$,QUE_CODE$,DR_I,DR1_K$
2008 ENTER ACCESS_MODE,KEY$,INDEX,VALUE$
2009 IF %DR_SUSPEND THEN GOTO JUST_EXIT ! 237003-Suspend datarep if flag is turned on; 307335
2010 GOSUB OPEN_REP_FILE
2015 TMP=EVN(FILE_NAME$,ERR=JUST_EXIT) ! 159723, check out file name before updating rep data
2020 GOSUB SET_KEY
2060 IF NUL(QUE_CODE$) THEN GOTO JUST_EXIT
2070 FOR DR_I=0 TO (LEN(QUE_CODE$)/6)-1
2075 IF QUE_CODE$(DR_I*6+3,1)="C" THEN WRITE (%DRT_PORT,KEY=PAD(FILE_NAME$,10)+KEY$)FILE_NAME$+SEP+KEY$+SEP+"D"+SEP+DTE(0:"%Y%Mz%Dz%Hz%mz%sz")+SEP+MID(%X3$,40,3)+SEP+FID(0)+SEP+WHO+SEP+VALUE$; GOTO *CONTINUE
2080 QUE_KEY$=PAD(QUE_CODE$(DR_I*6+4,3)+DTE(0:"%Yz%Mz%Dz")+STR(TIM:"00.00000")+"."+MID(STR(%DR2_COUNT++:DIM(10,"0")),-7),25); WRITE (OUTPUT_PORT,KEY=QUE_KEY$,DOM=*SAME)QUE_KEY$+SEP+"D"+SEP+PAD(FILE_NAME$,8)+QUE_CODE$(DR_I*6+1,2)+SEP+KEY$+SEP+VALUE$ ! SSP 223989, 282624 SSP276890
2085 IF %DR2_COUNT>9999999 THEN WAIT 1; %DR2_COUNT=0 ! 282624
2090 NEXT DR_I
2095 EXIT 
2099 ! 
2900 JUST_EXIT:EXIT ! Come here to just exit the program   159723
3000 PROGRAM_VERIFY:! Return a string to verify this is a program
3010 ENTER X$
3020 X$="Write to Replication Que"
3095 EXIT 
3099 ! 
4000 OPEN_REP_FILE:! IF  DR2 Port is open, set OUTPUT_PORT to it, else open it and set OUTPUT_PORT, ALSO CAPTURE THE FILE NAME IN FILE_NAME$
4007 REM IF MID(FILE_NAME$,1,3)="FMP" THEN FILE_NAME$="FMP"+MID(VALUE$,1,1)+MID(FILE_NAME$,4) ! If FMP, then add 1 char record type to name
4010 FILE_NAME$=FIN(LFA,"FILENAME")
4015 OPEN_DR2:
4020 IF FN%ISOPENFILE(%DR2_PORT,"DR2") THEN {
4025 OUTPUT_PORT=%DR2_PORT
4045  } ELSE {
4050 %DR2_PORT=GFN; OPEN (%DR2_PORT)"DR2"+%C$
4055 OUTPUT_PORT=%DR2_PORT
4080  }
4095 RETURN 
4099 ! 
4100 SET_KEY:! If que_code not set then get it here
4145 QUE_CODE$=""
4150 IF NOT(FN%ISOPENFILE(%DR1_PORT,"DR1")) THEN %DR1_PORT=GFN; OPEN (%DR1_PORT,ERR=9000)"DR1"+%C$
4151 IF NOT(FN%ISOPENFILE(%DRT_PORT,"DRT")) THEN %DRT_PORT=GFN; OPEN (%DRT_PORT,ERR=9000)"DRT"+%C$ ! 307385
4152 READ (%DR1_PORT,KEY=FILE_NAME$,DOM=*NEXT,ERR=ERR_DR1)
4155 DIM DR1$(500)
4157 DR1_K$=KEY(%DR1_PORT,END=4191); IF FILE_NAME$<>MID(DR1_K$,1,LEN(FILE_NAME$)) THEN GOTO 4191
4160 READ (%DR1_PORT,KEY=DR1_K$,DOM=*NEXT)DR1$
4165 IF DR1$(301,1)<>"Y" OR (NOT(NUL(%DR_TARGET_SPECIFIC$)) AND DR1$(41,20)<>%DR_TARGET_SPECIFIC$) THEN GOTO 4171 ! Flag is set to not replicate
4170 QUE_CODE$+=MID(DR1_K$,9,2)+DR1$(347,1)+DR1$(348,3) ! 307385
4190 GOTO 4155
4195 RETURN 
4199 ! 
9000 ! ^9000 Errors - send to error log and leave
9001 PRINT "ERR=",ERR," LINE=",TCB(5)," DR2EDA"; REM PER TMA 3/29/06
9005 ERROR_NO=ERR,LINE_NO=TCB(5)
9010 ! SETERR 9050
9020 ERR_MSG$="ERR: "+STR(ERROR_NO)+" LINE: "+STR(LINE_NO)+" PGM: "+PGN ! GOSUB LOG_ACTIVITY
9030 GOSUB ERR_LOG ! SSP 276890
9040 IF %IN_WEBEC$="Y" THEN GOTO SEND_EMAIL
9050 MSGBOX ERR_MSG$,MSG("WARNING")+" DATA REPLICATION ERROR","!"; GOTO END_ERROR ! SSP 276890
9100 SEND_EMAIL:
9105 EC=HFN; OPEN (EC,ERR=END_ERROR)"ZZPARM"
9110 DIM EC_PARM$(1275); FIND (EC,KEY=%X3$(9,3)+"E/C",DOM=*NEXT)EC_PARM$(1)
9120 MAIL_TO$=STP(MID(EC_PARM$,596,60),2),MESSAGE$="Data Replication Error in DR2EDA - "+"ERR: "+STR(ERROR_NO)+" LINE: "+STR(LINE_NO),SUBJECT$="Data Replication Error - Please contact DBD Support "
9130 IF NOT(NUL(MAIL_TO$)) THEN EMAILMSG$=FN%SENDMAIL$(MAIL_TO$,"","","",SUBJECT$,MESSAGE$,"","") ! SSP261559, use standard function instead of INVOKE "echo "+QUO+MESSAGE$+QUO+" | mail -s"+QUO+SUBJECT$+QUO+" "+MAIL_TO$ 
9150 LAST_WEBEC_ERR$=MESSAGE$; LAST_WEBEC_ERR$=GBL("LAST_WEBEC_ERR",LAST_WEBEC_ERR$) ! SSP261559, set global last webec err, will be included in response
9160 CLOSE (EC,ERR=*NEXT)
9200 END_ERROR:
9210 ESCAPE ! SSP 276890
9940 EXIT 
9999 END 
10000 ERR_DR1:
10010 IF ERR=14 THEN %DR1_PORT=0; GOTO 4150
10020 GOTO 9000
10050 ! 
10060 ERR_DR2:
10070 %DR2_PORT=0; GOSUB OPEN_DR2 ! SSP239991, move GOTO *RETRY 
10075 RETRY ! SSP23991
10090 ! 
11000 ERR_LOG:! SSP 276890
11010 DIR$=HWD+DLM+"tf2g"+DLM+"ERROR_LOG"
11020 DD=HFN; OPEN (DD,ERR=*NEXT)DIR$; CLOSE (DD); GOTO WRITE_LOG
11030 DIRECTORY DIR$
11090 ! 
11200 WRITE_LOG:
11210 LOG_NAME$="DR2EDA"+"_"+STR(LINE_NO:"0000")+"_"+STR(ERROR_NO:"000")+"_"+DTE(0:"%Yl%Mz%Dz_%Hz%Mz%Sz"),BERROR=ERR
11220 SERIAL HWD+DLM+"tf2g"+DLM+"ERROR_LOG"+DLM+LOG_NAME$,ERR=ERR_LOG_EXIT
11240 FF=HFN; OPEN LOCK (FF)HWD+DLM+"tf2g"+DLM+"ERROR_LOG"+DLM+LOG_NAME$
11250 DUMP (FF)*
11260 ERR_LOG_EXIT:
11270 CLOSE (FF,ERR=*NEXT)
11290 RETURN 
56002 REM "209252-Modify Data Rep to allow output to multiple database
56003 REM "223989-Error 14 because the DR1 channel is disconnected   
56004 REM "229312-Datareplication enhancement for selective replication of    
56005 REM "237003-Need back door process to temporarily turn off data rep     
56006 REM "239991-DR2EDA RETRY issue when user accesses panels with grids in
56007 REM "282624-Timing issue with large number of updates to a single rec
56008 REM "284288-We are having a Data Replication issue with the FS1 and     
56009 REM "276890-Issue with data rep if it receives an error                 
56010 REM "DBSPT-95803 - DR1 and DR2 file channels crosswiring         
56011 REM "307335-DBD-156 - Suppress false error messages                     
56012 REM "307385-DBD-253 - Data rep change tracking mode                     
