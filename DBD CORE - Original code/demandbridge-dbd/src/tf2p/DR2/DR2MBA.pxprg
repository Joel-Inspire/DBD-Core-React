0010 REM "Data Replication Maintenance <DR2MBA>
0015 REM "Prog Type: IP-3.0A
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 11/15/13 - 18.019444 - crg - SSP# 266976
0037 REM "266976-Ability to disable filtering at the table level if target   
0040 REM "Copyright 2013 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0043 ! 
0044 ! ********************************************************************    
0045 ! THIS PROGRAM WILL NO LONGER BE MODIFIED. THE GUI PANEL WILL BE PROCESSED FROM HERE. ALL FUTURE CHANGES NEED TO BE MADE IN DRGMBA PANEL AND PROGRAM. (266976)
0046 ! ********************************************************************
0049 ! 
0050 IF TCB(13)=1 THEN BEGIN 
0052 IF NUL(%WDX$) THEN MSGBOX "You must be using Windx to access this program"; GOTO 0075
0055 IF NOT(%GUI) THEN CHUI_MODE=1; %GUI=1
0060 PROCESS "DRGMBA","../DRG/DR.EN"
0065 IF CHUI_MODE THEN %GUI=0; RUN "ZMENU"
0075 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0089 ! 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000; SETESC 9300
0110 X0$="DR2MBA",X1$="Data Replication Maintenance"
0120 DIM Z0$(80,"-")
0130 K0=3,K1=1
0135 C9=-1
0140 M0$="###,###,###.00",M1$="###.00-"
0150 V1=2
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(65,1)>"2" THEN PRECISION NUM(X3$(65,1),ERR=0246)
0290 K9$="",K9=LEN(K9$)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1)
0320 IOLIST DR0$(1)
0330 IOLIST ZZE$,ZZE[0],ZZE[1]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X DR1... 02O DR0... 03O ZZE...  13O ZZPARM  "
0520 GOSUB 9750; ON Z0 GOTO 0521,9900
0530 DDF=HFN; OPEN (DDF)"providex.ddf"
0590 READ (Z[1],KEY=K9$,DOM=0591)
0600 REM "
0680 GOSUB 6000
1000 REM "
1010 Y8=1; CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM DR0$(185),ZZE$(70),ZZE[1],PREV_A$(502)
1035 DIM A$(502)
1045 A$(1,LEN(A1$))=A1$; IF A1$="" THEN A$(1,LEN(K9$))=K9$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 SETERR 9000; C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1250,1500
1200 REM "
1210 CALL "ZZENTR","SZXUX",A{ALL},A$,X4$,X3$,23,3,1,8,C0,"","{1"+X$,K9$,"DR2MBA00","DR1","",""; IF C0=6 THEN GOSUB 6400; C0=1 ! SSP#231246
1212 IF INT(ABS(C0))=3 THEN C0=-ABS(C0); READ (Z[1],KEY=A$(1,3),DOM=1213)
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9800,6970
1220 DIM ZZE$(LEN(ZZE$)); FIND (Z[3],KEY=A$(1,3)+DIM(3),DOM=*NEXT)IOL=0330; A$(11,30)=ZZE$(7,39); PRINT @(23,4),A$(11,30), ! SSP#231246
1225 GOSUB FILE_EXISTS
1240 GOTO 1120
1250 REM "Sequence number
1260 CALL "ZZENTR","ZX",A{ALL},A$,X4$,X3$,45,3,9,2,C0,"","{1"+X$,A$(1,8),"DR2MBA02","DR1","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1260,1261 ! SSP#231246
1264 IF ABS(C0)=2 THEN IF LEN(A1$)>1 THEN A1$=""
1265 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1266,6800,9700,6970
1290 GOTO 1120
1500 REM "
1505 Z9=-1,C9=0,A2$="",A3$="",A4$="",A5$=""
1510 A1$=A$(1,10)
1520 READ (Z[1],ERR=7200,KEY=A1$,DOM=1600)IOL=0310
1525 Z9=0,V7$=""; GOSUB 5800
1530 PREV_A$=A$
1590 GOTO 1700
1600 REM "
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630
1630 GOTO 1000
1680 GOSUB 7450
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1805 IF C9=0 THEN GOSUB 7500; REM "If no modify done yet, then verify product code is set now, else wait until writing out.
1820 CALL "ZZPROM",STR(6+C9),X3$,Z,"","","V",X0; ON Z GOTO 1850,6500,1630,6700,1821
1825 GOTO 6700
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 GOSUB 7400
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 SETERR 9000; X$="",Q$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF INT(C0)=3 THEN GOSUB 7300 ELSE IF C0>3 OR X0<0 THEN GOTO 1800
2038 IF X0<LEN(V7$) THEN V7$(X0+1,1)=""
2040 X9=FNZ(X0); ON X0 GOTO 2100,2150,2550,2200,2250,2450,2300,2350,2500,2400,2600,2650,1800
2100 ! Description
2105 GOSUB 6400
2110 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,23,4,11,30,C0,"","{3"+X$,"","DR2MBA02","","","" ! SSP#241135
2140 GOTO 2020
2150 ! Data Replication Target Code
2160 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,16,6,41,20,C0,"","{3"+X$,"","DR2MBA04","DR0","DR2MAA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO *SAME,*NEXT
2165 IF STP(A$(41,20))="" THEN DIM DR0$(LEN(DR0$)) ELSE FIND (Z[2],KEY=A$(41,20),DOM=2160)IOL=0320
2166 A$(347,1)=DR0$(181,1); PRINT @(16,7),A$(347,1),@(18,7),'CL',FNDB$(A$(347,1)),
2170 PRINT @(38,6),DR0$(21,30),
2175 GOSUB SETUP_DATAPATH
2180 GOSUB 6400
2190 GOTO 2020
2200 ! Data path
2210 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,16,8,61,60,C0,"","{3"+X$,"","DR2MBA06","","","01V0"
2240 GOTO 2020
2250 ! Options
2260 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,16,11,181,60,C0,"","{3"+X$,"","DR2MBA08","","","01V0"
2290 GOTO 2020
2300 ! Currently Replicating?
2305 CR_VAL$=A$(301,1)
2310 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,26,15,301,1,C0,"","{3"+X$,"","DR2MBA10","","",""
2315 IF A$(301,1)<>CR_VAL$ THEN A$(302,6)=X3$(21,6); PRINT @(44,15),FND$(A$(302,6)) ! If value has changed, then change date
2340 GOTO 2020
2350 ! Status Changed on
2355 IF ABS(C0)<2 THEN GOTO 2020 ! only come here if moving back
2360 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,44,15,302,6,C0,"","{3"+X$,"","DR2MBA12","","",""
2390 GOTO 2020
2400 ! Data Initialized on
2410 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,26,17,308,6,C0,"","{3"+X$,"","DR2MBA14","","",""
2440 GOTO 2020
2450 ! Data Dictionary Name
2455 IF STP(A$(314,32))="" THEN GOSUB SET_DD_NAME
2460 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,26,14,314,32,C0,"","{3"+X$,"","DR2MBA12","","",""
2465 IF STP(A$(314,32))="" THEN GOTO 2490
2470 FIND (DDF,KNO=1,KEY=STP(A$(314,32)),DOM=2460)
2490 GOTO 2020
2500 ! Log Activity
2510 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,26,16,346,1,C0,"","{3"+X$,"","DR2MBA14","","",""
2540 GOTO 2020
2550 ! Database type
2555 X$="O=Oracle, M=MySQL, blank=other"
2560 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,16,7,347,1,C0,"","{3"+X$,"OM ","DR2MAA08","","",""
2565 PRINT @(18,7),'CL',FNDB$(A$(347,1)),
2590 GOTO 2020
2600 ! Que Priority
2610 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,26,18,348,3,C0,"","{3"+X$,"","DR2MAA10","","",""
2640 GOTO 2020
2650 REM "Don't update from Data Dictionary - ssp# 169242
2660 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,36,19,351,1,C0,"","{3"+X$,"","DR2MBA10","","",""
2690 GOTO 2020
5800 REM "Alt sort keys
5810 FIND (Z[2],KEY=A$(41,20),DOM=*NEXT)IOL=0320
5890 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(1,3),"DB Distributor Table:",@(37,3),"Seq.No:",@(10,4),"Description:",@(0,5),Z0$, ! SSP#231246
6065 PRINT @(3,6),"Target code:",@(1,7),"Database type:",@(3,8),"Data Source:",@(7,11),"Options:",@(0,13),Z0$,
6070 PRINT @(3,15),"Currently Replicating?",@(32,15),"Changed on:",@(12,16),"Log activity?",@(1,17),"Replication initialized:",
6075 PRINT @(4,14),"Data Dictionary Name:",
6080 PRINT @(12,18),"    Que Code:",
6082 PRINT @(1,19),"Don't Update from Data Dictionary:",; REM " SSP # 169242
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 PRINT @(23,4),A$(11,30), ! SSP#231246
6225 PRINT @(16,6),A$(41,20),@(38,6),DR0$(21,30),
6230 PRINT @(16,8),A$(61,60),@(16,9),A$(121,60),@(16,11),A$(181,60),@(16,12),A$(241,60),
6232 PRINT @(16,7),A$(347,1),@(18,7),'CL',FNDB$(A$(347,1)),
6235 PRINT @(26,14),A$(314,32),@(26,15),A$(301,1),@(44,15),FND$(A$(302,6)),@(26,16),A$(346,1),@(26,17),FND$(A$(308,6)),
6240 PRINT @(26,18),A$(348,3),
6250 PRINT @(36,19),A$(351,1),; REM " SSP# 167214
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 PRINT @(23,3),A$(1,8),@(45,3),A$(9,2), ! SSP#231246
6490 RETURN 
6500 REM "DELETE
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY
6703 GOSUB 7500
6705 IF Z9=0 THEN GOTO 6790 ELSE IF V7$>"" THEN X0=POS(" "<>V7$); IF X0>0 THEN X0=X0-1; GOTO 2038
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6715 IF A$(301,1)="Y" THEN {
6716 IO_PROG$=STP(DR0$(171,10))
6717 OPT$=A$(351,1); REM " SSP# 169242
6720 CALL "DR2PFX",X3$,X4$,STP(A$(314,32))+"|"+STP(A$(1,10),1),STP(A$(61,120)),IO_PROG$,OPT$
6725  }
6790 C9=0; IF Z=6 OR K1=0 THEN GOTO 9900 ELSE IF Z<4 THEN GOTO 1000 ELSE C0=-(3.1+(Z-4)/10); GOTO 6800
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 IF A1$="" THEN GOTO 6800 ELSE IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6825 A$(1,LEN(A1$))=A1$
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM "PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 PRINT 'RB',; CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6865 GOTO 1100
6870 GOTO 6895
6890 GOTO 1515
6895 IF A1$="" THEN GOTO 6860 ELSE PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=6896)
6898 ON S9 GOTO 6810,1100
7200 REM "Forced read
7210 IF ERR<>0 THEN GOTO 9000
7220 CALL "ZZREAD",Z[1],A1$,A$,A{ALL},S8; ON S8 GOTO 7221,1630
7230 GOSUB 6200
7240 CALL "ZZPROM","E0",X3$,0,"","","",0; GOTO 7290
7250 CALL "ZZPROM","E0A",X3$,0,"","","",0; GOTO 7290
7290 GOTO 1630
7300 REM 
7310 ON FPT(C0)*10 GOTO 7390,7325,7330,7390
7325 IF V8$="" THEN X0=X0+4; GOTO 7390 ELSE X0=NUM(V8$(POS(STR(X0:"00")<=V8$,2),2)); GOTO 7390
7330 IF V8$="" THEN X0=X0-6; GOTO 7335 ELSE X0=POS(STR(X0-1:"00")<=V8$,2)-2; IF X0<0 THEN X0=0; GOTO 7390 ELSE X0=NUM(V8$(X0,2)); GOTO 7390
7335 IF X0<0 THEN X0=0; GOTO 7390
7390 IF X0>0 THEN X0=X0-0
7395 C0=3; RETURN 
7400 REM "EXTRACT
7410 EXTRACT (Z[1],ERR=7200,KEY=A1$,DOM=7440)IOL=0310
7420 C9=1,Z9=1; GOSUB 5800
7440 RETURN 
7450 REM "EXTRACT NEW ENTRY
7455 A$(301,1)="N",A$(346,1)="N",A$(351,1)="N"; REM "Currently replicating, Log activity, Don't update from data dictionary
7460 WRITE (Z[1],KEY=A1$,DOM=7250)IOL=0310; GOTO 7400
7490 RETURN 
7500 FILE_EXISTS:! check to see if file exists, give message if not
7505 CH=HFN; OPEN (CH,ERR=*NEXT)STP(A$(1,8)); GOTO FILE_FOUND
7510 MSGBOX "File "+STP(A$(1,8))+" cannot be found!","Info"
7530 FILE_FOUND:CLOSE (CH)
7545 RETURN 
7549 ! 
7550 SETUP_DATAPATH:! Get datapath from Target and replace tokens
7555 IF A$(41,20)=PREV_A$(41,20) THEN GOTO SD_DONE ELSE PREV_A$(41,20)=A$(41,20)
7560 A$(61,120)=DR0$(51,60) ! if changed then default to model
7565 A$(61,120)=SUB(A$(61,120),"&TF",STP(A$(1,8))) ! &TF replaced with file name
7570 GOSUB SET_DD_NAME; A$(61,120)=SUB(A$(61,120),"&DD",STP(A$(314,32))) ! &DD replaced with data dictionary name
7595 SD_DONE:RETURN 
7599 ! 
7600 SET_DD_NAME:! Set the data dictionary name from the file name
7605 TMP$=A$(1,3)
7615 READ (DDF,KNO=1,KEY=TMP$,DOM=*NEXT)
7620 TMPKEY$=KEY(DDF,KNO=1,END=SDN_DONE); READ (DDF,KEY=TMPKEY$,KNO=1)DDFNAME$; IF MID(TMPKEY$,1,3)<>TMP$ THEN GOTO SDN_DONE ! 212004
7625 A$(314,32)=DDFNAME$; PRINT @(26,14),A$(314,32), ! 212004
7640 SDN_DONE:! done 
7645 RETURN 
7649 ! 
7650 DEF FNDB$(LOCAL TYPE_CODE$)
7655 LOCAL DB_LIST$,P
7660 DB_LIST$="O[Oracle]M[MySQL]"
7665 P=MSK(DB_LIST$,TYPE_CODE$+"\[[^\]]*")
7670 IF P THEN RETURN MID(DB_LIST$,P+2,MSL-2) ELSE RETURN ""
7675 END DEF
7680 ! 
8905 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8920 DEF FNB$(Z9$)=X3$(214-6*NUM(Z9$),1)
8960 DEF FNZ(X)=(X-10)*2+1; REM "133159 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9120 ON Y8 GOTO 9900,0990,9920
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9740 RETURN 
9750 REM "FILES
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9790 RETURN 
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9905 Y8=2
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56002 REM "209252-Modify Data Rep to allow output to multiple database        
56003 REM "212004-Ver 7 Pvx DataRep refers to table names in all caps
56004 REM "241135-The Data Replication Maintenance (D/R S2) description does  
56005 REM "266976-Ability to disable filtering at the table level if target   
