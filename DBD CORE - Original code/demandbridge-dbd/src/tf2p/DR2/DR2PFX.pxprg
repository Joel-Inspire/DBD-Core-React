0010 ! TopForm Build Prefix Entry & Update I/O Program <DR2PFX>
0035 REM "5.7 - 11/07/17 - 10.268055 - crg - SSP# 263584
0037 REM "263584-Providex version upgrade from Pvx 7.1 to PxPlus 2017        
0040 REM "Copyright 2017 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0055 ! TF_FILE$ is TopForm Data dictionary name, DR_FILE$ is outgoing file to open, IO$ is Embedded i/o program, ""= clear it,OPT$ is options
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,TF_FILE$,DR_FILE$,IO$,OPT$
0091 DIM SPACE$(10)
0092 FILE_NAME$=TF_FILE$(POS("|"=TF_FILE$)+1)
0093 TF_FILE$=TF_FILE$(1,POS("|"=TF_FILE$)-1)
0094 IF LEN(FILE_NAME$)<10 THEN FILE_NAME$=FILE_NAME$+SPACE$(1,10-LEN(FILE_NAME$))
0100 SETERR 9000
0110 X0$="DR2PFX",X1$="Build Prefix & Update I/O Program"
0141 %C$=X3$(9,3); PFXNME=HFN; OPEN (PFXNME,ERR=NO_PFXNME)"PFX"+%C$; GOTO 0150
0142 NO_PFXNME:KEYED "PFX"+%C$,12,0,-4096; GOTO 0141
0150 DDE=HFN; OPEN (DDE)"providex.dde"
0170 DDF=HFN; OPEN (DDF)"providex.ddf"
0180 PREFIX FILE "PFX"+%C$ ! Make sure we are using the prefix
0181 DR1=HFN; OPEN (DR1)"DR1"+%C$
0190 READ (DR1,KEY=FILE_NAME$)DR1$
0200 READ_RECORD:
0210 READ (DDF,KNO=1,KEY=TF_FILE$,DOM=9900)IOL=8010
0220 DDF_K$=KEC(DDF,KNO=0) ! Get primary id
0300 ! ^100 - Get File Information
0320 READ (DDE,KEY=DDF_K$,DOM=9900)
0330 PFXNME_K$="DR_"+PATH$(3,3)+%C$+"_"+MID(FILE_NAME$,9,2) ! 209252
0339 ! PFX_FIL$="[oci]wm;"+PFXNME_K$+";"+"USER=webbmason;PSWD=manager"
0340 PFX_FIL$=DR_FILE$ ! set output file name
0350 PFX_KEY$="KEY="
0360 PFX_ELM$="REC="
0365 TFDATE$="",END_OF_STRING=0
0370 DIM KEY_ELEMENT$[100] ! max 100 key elements
0380 PFX_OPT$="" ! 225592
0400 ! ^100 - Get Element Information
0405 GET_ELM:
0410 READ (DDE,END=ADD_PFX)IOL=8020
0420 DDE_K$=KEC(DDE)
0430 IF DDE_K$(1,6)<>DDF_K$ THEN GOTO ADD_PFX
0435 IF NAME$="DESC" THEN NAME$="DESCRIPTION" ! DESC is reserved word for Oracle
0440 IF LEN(PFX_ELM$)=4 THEN GOTO 0460
0448 IF PREV_FORMAT$="L" THEN PFX_ELM$+=", "; GOTO 0460
0449 IF PREV_CLASS$="DATE-KKMMDD" THEN PFX_ELM$+=", "; GOTO 0460 ! IF (SSN>"0750" OR (SSN>"07" AND %C$="101")) AND POS("[OCI]"=UCS(PFX_FIL$))>0 THEN PFX_ELM$+="+ " ELSE PFX_ELM$+=", "; END_IF ; GOTO 0460 ! 209050, 241181
0450 IF FORMAT$="S" THEN PFX_ELM$=PFX_ELM$+"+ "
0451 IF FORMAT$="L" THEN PFX_ELM$=PFX_ELM$+"+ "
0452 IF FORMAT$="D" THEN PFX_ELM$=PFX_ELM$+", "
0460 IF DR1$(347,1)="O" THEN IF CLASS$="DATE-KKMMDD" THEN PFX_ELM$=PFX_ELM$+NAME$+":D9",TFDATE$+=STR(END_OF_STRING+1:"0000"),END_OF_STRING+=10 ELSE PFX_ELM$=PFX_ELM$+NAME$+":"+LEN$,END_OF_STRING+=NUM(LEN$) ! Oracle style dates "DD-MM-YY", 9 long
0461 IF POS(DR1$(347,1)="M ")<>0 THEN IF CLASS$="DATE-KKMMDD" THEN PFX_ELM$=PFX_ELM$+NAME$+":D10",TFDATE$+=STR(END_OF_STRING+1:"0000"),END_OF_STRING+=11 ELSE PFX_ELM$=PFX_ELM$+NAME$+":"+LEN$,END_OF_STRING+=NUM(LEN$) ! MySQL style dates YYYY-MM-DD 10 chars long
0464 PREV_CLASS$=CLASS$,PREV_FORMAT$=FORMAT$
0465 IF CVS(KEYDEF$,128)="" THEN GOTO GET_ELM
0468 IF KEYDEF$(1,2)<>"1:" THEN GOTO GET_ELM
0470 ELEMENT_INDEX=NUM(MID(KEYDEF$,MSK(KEYDEF$,"\:[0-9]*\:")+1,MSL-2),ERR=*NEXT); KEY_ELEMENT$[ELEMENT_INDEX]=NAME$; MAX_KEY_ELEMENT=MAX(MAX_KEY_ELEMENT,ELEMENT_INDEX)
0490 GOTO GET_ELM
0500 ! ^100 - Build Prefix File
0510 ADD_PFX:
0515 IF OPT$="Y" THEN GOTO 0700; REM " SSP 167214
0520 PFX_ELM$=PFX_ELM$
0525 IF MAX_KEY_ELEMENT>0 THEN TMPKEY$=""; FOR E=1 TO MAX_KEY_ELEMENT; TMPKEY$+=","+KEY_ELEMENT$[E]; NEXT E; TMPKEY$=TMPKEY$(2) ! if elements then build TMPKEY$ list, strip off leading ","
0530 PFX_KEY$+=TMPKEY$+"; " ! PFX_KEY$ initialized above
0535 IF LEN(TFDATE$)>0 THEN TFDATE$="TFDATE="+TFDATE$+"; " ! IF (SSN>"0750" OR (SSN>"07" AND %C$="101")) AND POS("[OCI]"=UCS(PFX_FIL$))>0 THEN TFDATE$="DATEFMT=KKMMDD;" ELSE TFDATE$="TFDATE="+TFDATE$+"; " ! 209050, 241181
0540 IF POS("[OCI]"=UCS(PFX_FIL$))>0 THEN PFX_OPT$+="; PREPARE=Y" ! 225592
0570 WRITE (PFXNME,KEY=PFXNME_K$)PFX_FIL$,TFDATE$+PFX_KEY$+PFX_ELM$+PFX_OPT$ ! 225592
0700 ! ^100 Update dictionary & file with I/O procedure
0710 EXTRACT (DDF,KNO=1,KEY=TF_FILE$,DOM=9900)IOL=8010
0715 FILE_PROC$="!"+IO$
0720 WRITE (DDF,KEY=DDF_K$)IOL=8010 ! DDF_K$ set at line 220
0750 CALL "*dict/dd_updt;Update_Physical",NAME$,"","D",ERRMSG$ ! D=update dictionary only
0760 IF STP(ERRMSG$,2)<>"" THEN MSGBOX ERRMSG$
1990 GOTO 9900
8000 ! 8000 - iolists
8010 IOLIST NAME$,PATH$,LST_UPD$,FILE_PROC$,NOT_USED1$,NOT_USED2$,NOT_USED3$,NOT_USED4$,NOT_USED5$,ALT_IOLIST$,SEP$,RFU$
8020 IOLIST IOLOBJ$,NAME$,DESC$,TYPE$,LEN$,OCC$,FORMAT$,VALID$,PRINT$,HELP$,NOTES$,KEYDEF$,QUERY$,CLASS$,FLAGS$,DFLT$,ALT_NAME$,USER_TAG$
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 ! CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9915 CLOSE (PFXNME); CLOSE (DDE); CLOSE (DDF)
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56002 REM "209050-Modify program to handle TopForm date format in PVX 7.1
56003 REM "209252-Modify Data Rep to allow output to multiple database
56004 REM "225592-For Oracle channels, enable use of SQL prepared statements
56005 REM "241181-Oracle and date formats as it relates to WebbMason 
56006 REM "263584-Providex version upgrade from Pvx 7.1 to PxPlus 2017        
