0010 ! UPDDTT - File DTT Maintenance
0035 REM "5.7 - 02/26/11 - 11.3625 - crg - SSP# 240570
0037 REM "240570-GUI Printing - Support for queueing and scheduling reports. 
0040 REM "Copyright 2011 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! FILENO - CHANNEL OF OPEN FILE
0051 ! KEYNO  - KEY NUMBER
0052 ! SEG1-SEG5$ : KEY SEGMENTS . CAN BE NULL
0053 ! A$ - RECORD A$
0054 ! FOUND - IF RECORD FOUND, THEN 1 ELSE 0
0055 ! BUSY - IF RECORD BUSY THEN 1 ELSE 0
0056 ! OK - IF RECORD ADDED OK=1 ELSE 0
0310 IOLIST A$
0500 INIT:
0505 DIM Z[255]; OPENED=0
0507 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
0510 IF FILENO=0 THEN Z$="01S DTT     "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; FILENO=Z[1],OPENED=1 ELSE IF POS("DTT"=PTH(FILENO))=0 THEN Z$="01S DTT     "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; FILENO=Z[1],OPENED=1
0590 RETURN 
0598 ! 
0599 ! 
0600 WRAPUP:
0610 IF OPENED THEN CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0690 RETURN 
0698 ! 
0699 ! 
1000 READBYKEY:
1010 ENTER FILENO,KEYNO,SEG1$,SEG2$,SEG3$,SEG4$,SEG5$,A$,A{ALL},FOUND
1015 FOUND=0
1020 GOSUB INIT
1025 CALL "UPDDTT;CLEAR_FIELDS",A$,A{ALL}
1049 XP=PRM('XI'); SET_PARAM 'XI'
1050 READ (FILENO,KNO=KEYNO,KEY=SEG1$+SEG2$+SEG3$+SEG4$+SEG5$,DOM=*NEXT)IOL=0310; FOUND=1
1051 SET_PARAM 'XI'=XP
1080 GOSUB WRAPUP
1090 EXIT 
1098 ! 
1099 ! 
1100 READNEXT:
1110 ENTER FILENO,A$,A{ALL},FOUND
1115 FOUND=0
1120 GOSUB INIT
1125 CALL "UPDDTT;CLEAR_FIELDS",A$,A{ALL}
1149 XP=PRM('XI'); SET_PARAM 'XI'
1150 READ (FILENO,END=*NEXT)IOL=0310; FOUND=1
1151 SET_PARAM 'XI'=XP
1180 GOSUB WRAPUP
1190 EXIT 
1198 ! 
1199 ! 
1200 READPREV:
1210 ENTER FILENO,A$,A{ALL},FOUND
1215 FOUND=0
1220 GOSUB INIT
1225 CALL "UPDDTT;CLEAR_FIELDS",A$,A{ALL}
1249 XP=PRM('XI'); SET_PARAM 'XI'
1250 PKEY$=KEP(FILENO,ERR=*NEXT); READ (FILENO,KEY=PKEY$)IOL=0310; FOUND=1
1251 SET_PARAM 'XI'=XP
1280 GOSUB WRAPUP
1290 EXIT 
1298 ! 
1299 ! 
1300 READFIRST:
1310 ENTER FILENO,A$,A{ALL},FOUND
1315 FOUND=0
1320 GOSUB INIT
1325 CALL "UPDDTT;CLEAR_FIELDS",A$,A{ALL}
1349 XP=PRM('XI'); SET_PARAM 'XI'
1350 PKEY$=KEF(FILENO,KNO=0,ERR=*NEXT); READ (FILENO,KEY=PKEY$)IOL=0310; FOUND=1
1351 SET_PARAM 'XI'=XP
1380 GOSUB WRAPUP
1390 EXIT 
1398 ! 
1399 ! 
1400 READLAST:
1410 ENTER FILENO,A$,A{ALL},FOUND
1415 FOUND=0
1420 GOSUB INIT
1425 CALL "UPDDTT;CLEAR_FIELDS",A$,A{ALL}
1449 XP=PRM('XI'); SET_PARAM 'XI'
1450 PKEY$=KEL(FILENO,ERR=*NEXT); READ (FILENO,KEY=PKEY$)IOL=0310; FOUND=1
1451 SET_PARAM 'XI'=XP
1480 GOSUB WRAPUP
1490 EXIT 
1498 ! 
1499 ! 
1500 EXTRACTBYKEY:
1510 ENTER FILENO,KEYNO,SEG1$,SEG2$,SEG3$,SEG4$,SEG5$,A$,A{ALL},FOUND,BUSY
1515 FOUND=0,BUSY=0
1525 CALL "UPDDTT;CLEAR_FIELDS",A$,A{ALL}
1549 XP=PRM('XI'); SET_PARAM 'XI'
1550 IF POS("DTT"=PTH(FILENO))=0 THEN BUSY=1 ELSE READ (FILENO,KNO=KEYNO,KEY=SEG1$+SEG2$+SEG3$+SEG4$+SEG5$,DOM=*NEXT)IOL=0310; FOUND=1,BUSY=1,PRIMEKEY$=KEC(FILENO,KNO=0); EXTRACT (FILENO,KEY=PRIMEKEY$,KNO=0,ERR=*NEXT); BUSY=0
1551 SET_PARAM 'XI'=XP
1590 EXIT 
1598 ! 
1599 ! 
1600 INSERT:
1610 ENTER FILENO,A$,A{ALL},OK
1615 OK=0,PRIMEKEY$=A$(1,20)
1620 GOSUB INIT
1650 WRITE (FILENO,KEY=PRIMEKEY$,ERR=*NEXT)IOL=0310; OK=1
1680 GOSUB WRAPUP
1690 EXIT 
1698 ! 
1699 ! 
1700 DELETE:
1710 ENTER FILENO,A$,A{ALL},FOUND,BUSY
1715 FOUND=0,BUSY=1,PRIMEKEY$=A$(1,20)
1720 GOSUB INIT
1750 REMOVE (FILENO,KEY=PRIMEKEY$,DOM=DELETE_END,ERR=*NEXT); BUSY=0
1785 DELETE_END:
1787 GOSUB WRAPUP
1790 EXIT 
1798 ! 
1799 ! 
1800 CLEAR_FIELDS:
1810 ENTER A$,A{ALL}
1820 DIM A$(280),A[13]
1890 EXIT 
1898 ! 
1899 ! 
1900 UPDATE:
1910 ENTER FILENO,A$,A{ALL},BUSY
1915 BUSY=1,PRIMEKEY$=A$(1,20)
1920 GOSUB INIT
1950 WRITE (FILENO,KEY=PRIMEKEY$,ERR=*NEXT)IOL=0310; BUSY=0
1980 GOSUB WRAPUP
1990 EXIT 
1998 ! 
1999 ! 
2000 CLEAR_LOCK:
2010 ENTER FILENO
2020 GOSUB INIT
2050 READ (FILENO,KNO=0,KEY="",ERR=*NEXT)
2080 GOSUB WRAPUP
2090 EXIT 
2098 ! 
2099 ! 
2500 VALIDATE:
2510 ENTER A$,A{ALL},MSG$
2520 A$=PAD(A$,280),MSG$=""
2990 EXIT 
2998 ! 
2999 ! 
9999 END 
10000 GET_VAL_OUT_DROP:
10010 ENTER A$,VAL_OUT_TYPE$
10015 VAL_OUT_TYPE$=""
10020 IF MID(A$,81,1)="Y" THEN VAL_OUT_TYPE$=PAD("PRINTER",10)+"/"
10025 IF MID(A$,82,1)="Y" THEN VAL_OUT_TYPE$=VAL_OUT_TYPE$+PAD("EMAIL",10)+"/"
10030 IF MID(A$,83,1)="Y" THEN VAL_OUT_TYPE$=VAL_OUT_TYPE$+PAD("FTP",10)+"/"
10035 IF MID(A$,84,1)="Y" THEN VAL_OUT_TYPE$=VAL_OUT_TYPE$+PAD("HTTP",10)+"/"
10037 IF MID(A$,85,1)="Y" THEN VAL_OUT_TYPE$=VAL_OUT_TYPE$+PAD("HTTPS",10)+"/"
10040 IF MID(A$,86,1)="Y" THEN VAL_OUT_TYPE$=VAL_OUT_TYPE$+PAD("FAX",10)+"/"
10042 IF MID(A$,87,1)="Y" THEN VAL_OUT_TYPE$=VAL_OUT_TYPE$+PAD("WEBEC",10)+"/"
10045 IF MID(A$,88,1)="Y" THEN VAL_OUT_TYPE$=VAL_OUT_TYPE$+PAD("HOLD",10)+"/"
10050 IF MID(A$,89,1)="Y" THEN VAL_OUT_TYPE$=VAL_OUT_TYPE$+PAD("ARCHIVE",10)+"/" ! [SSP-209370]
10090 EXIT 
10098 ! 
10099 ! 
10100 VALIDATE_OUTPUT_LOCATION:
10110 ENTER TYPE$,LOCATION$,OK
10115 OK=1,VTYPE$=STP(TYPE$,2),VLOCATION$=LOCATION$
10200 SWITCH VTYPE$
10210 CASE "EMAIL"
10215 XP=POS("@"=VLOCATION$),XP1=POS("."=VLOCATION$)
10220 IF XP=0 OR XP1=0 OR XP=LEN(VLOCATION$) OR XP1=LEN(VLOCATION$) THEN OK=0
10227 IF NOT(OK) THEN MSGBOX MSG("INV_EMAIL")
10230 BREAK
10250 CASE "PRINTER"
10252 OK=0; IF LEN(VLOCATION$)>=2 THEN VLOCATION$=PAD(VLOCATION$,2); CALL "UPDZZ9;READBYKEY",0,0,VLOCATION$,SEG2$,SEG3$,SEG4$,SEG5$,ZZ9$,ZZ9{ALL},OK ! 240570
10255 IF NOT(OK) THEN MSGBOX MSG("INV_PTR")
10259 BREAK
10260 CASE "HTTP"
10262 OK=1; XP1=POS("."=VLOCATION$)
10263 IF XP1=0 OR XP1=LEN(VLOCATION$) THEN OK=0
10264 IF POS("@"=VLOCATION$)<>0 THEN OK=0
10265 IF NOT(OK) THEN MSGBOX MSG("INV_HTTP")
10269 BREAK
10270 CASE "FAX"
10271 OK=0 ! [SSP-209368]
10272 IF LEN(STP(VLOCATION$))>0 THEN OK=1 ELSE MSGBOX MSG("INV_FAX") ! [SSP-209368]
10279 BREAK
10300 CASE "FTP"
10310 OK=1; XP1=POS("."=VLOCATION$)
10320 IF XP1=0 OR XP1=LEN(VLOCATION$) THEN OK=0
10327 IF NOT(OK) THEN MSGBOX MSG("INV_FTP")
10345 BREAK
10350 CASE "HTTPS"
10352 OK=1; XP1=POS("."=VLOCATION$)
10353 IF XP1=0 OR XP1=LEN(VLOCATION$) THEN OK=0
10354 IF POS("@"=VLOCATION$)<>0 THEN OK=0
10355 IF NOT(OK) THEN MSGBOX MSG("INV_HTTP")
10359 BREAK
10370 CASE "ARCHIVE" ! [SSP-209370]
10372 OK=1; XP1=POS("."=VLOCATION$) ! [SSP-209370]
10373 IF XP1=0 OR XP1=LEN(VLOCATION$) THEN OK=0 ! [SSP-209370]
10374 IF POS("@"=VLOCATION$)<>0 THEN OK=0 ! [SSP-209370]
10375 IF NOT(OK) THEN MSGBOX MSG("INV_ARC") ! [SSP-209370]
10388 BREAK ! [SSP-209370]
10450 END SWITCH 
10490 EXIT 
10498 ! 
10499 ! 
10500 CVT_OUT_TYPE:
10510 ENTER TYPE_NAME$,TYPE_CODE$
10515 TYPE_NAME$=STP(TYPE_NAME$,2)
10520 SWITCH TYPE_NAME$
10525 CASE "PRINTER"
10527 TYPE_CODE$="1"; BREAK
10530 CASE "EMAIL"
10532 TYPE_CODE$="2"; BREAK
10535 CASE "FTP"
10537 TYPE_CODE$="3"; BREAK
10540 CASE "HTTP"
10542 TYPE_CODE$="4"; BREAK
10545 CASE "HTTPS"
10547 TYPE_CODE$="5"; BREAK
10550 CASE "FAX"
10552 TYPE_CODE$="6"; BREAK
10555 CASE "WEBEC"
10557 TYPE_CODE$="7"; BREAK
10560 CASE "HOLD"
10562 TYPE_CODE$="8"; BREAK
10565 CASE "ARCHIVE"
10567 TYPE_CODE$="9"; BREAK
10580 DEFAULT 
10582 TYPE_CODE$=" "
10585 END SWITCH 
10590 EXIT 
10598 ! 
10599 ! 
10600 CVT_OUT_TYPE_IN:
10610 ENTER TYPE_CODE$,TYPE_NAME$
10615 TYPE_NAME$=DIM(10)
10620 SWITCH TYPE_CODE$
10625 CASE "1"
10627 TYPE_NAME$(1)="PRINTER"; BREAK
10630 CASE "2"
10632 TYPE_NAME$(1)="EMAIL"; BREAK
10635 CASE "3"
10637 TYPE_NAME$(1)="FTP"; BREAK
10640 CASE "4"
10642 TYPE_NAME$(1)="HTTP"; BREAK
10645 CASE "5"
10647 TYPE_NAME$(1)="HTTPS"; BREAK
10650 CASE "6"
10652 TYPE_NAME$(1)="FAX"; BREAK
10655 CASE "7"
10657 TYPE_NAME$(1)="WEBEC"; BREAK
10660 CASE "8"
10662 TYPE_NAME$(1)="HOLD"; BREAK
10665 CASE "9" ! [SSP-209370]
10667 TYPE_NAME$(1)="ARCHIVE"; BREAK ! [SSP-209370]
10680 END SWITCH 
10690 EXIT 
10698 ! 
10699 ! 
56000 REM "198612-TopForm Output director                                     
56002 REM "200524-Ability to send an email to originator of PO that product   
56004 REM "206742-Output Director is occasionally having an                   
56006 REM "203099-Enhancements to Output Director.  Provide ability for       
56007 REM "240570-GUI Printing - Support for queueing and scheduling reports. 
