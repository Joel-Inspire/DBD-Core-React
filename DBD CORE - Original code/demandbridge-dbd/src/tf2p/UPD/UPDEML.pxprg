0010 ! UPDEML - File EML Maintenance
0035 REM "5.6 - 08/07/08 - 17.314166 - jdf - SSP# 220939
0037 REM "220939-Cosmetic changes to Data Transport Setup Customer Shipment  
0040 REM "Copyright 2008 DemandBridge, Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! FILENO - CHANNEL OF OPEN FILE
0051 ! KEYNO  - KEY NUMBER
0052 ! SEG1-SEG5$ : KEY SEGMENTS . CAN BE NULL
0053 ! A$ - RECORD A$
0054 ! FOUND - IF RECORD FOUND, THEN 1 ELSE 0
0055 ! BUSY - IF RECORD BUSY THEN 1 ELSE 0
0056 ! OK - IF RECORD ADDED OK=1 ELSE 0
0310 IOLIST A$
0500 INIT:
0505 DIM Z[255]; OPENED=0
0507 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
0510 IF POS("EML"=PTH(FILENO))=0 THEN Z$="01O EML...  "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; FILENO=Z[1],OPENED=1
0590 RETURN 
0598 ! 
0599 ! 
0600 WRAPUP:
0610 IF OPENED THEN CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0690 RETURN 
0698 ! 
0699 ! 
1000 READBYKEY:
1010 ENTER FILENO,KEYNO,SEG1$,SEG2$,SEG3$,SEG4$,SEG5$,A$,A{ALL},FOUND
1015 FOUND=0
1020 GOSUB INIT
1025 CALL "UPDEML;CLEAR_FIELDS",A$,A{ALL}
1045 IF KEYNO=1 THEN SEG1$=PAD(SEG1$,2),SEG2$=PAD(SEG2$,20),SEG3$=PAD(SEG3$,3),SEG4$="",SEG5$=""
1049 XP=PRM('XI'); SET_PARAM 'XI'
1050 READ (FILENO,KNO=KEYNO,KEY=SEG1$+SEG2$+SEG3$+SEG4$+SEG5$,DOM=*NEXT)IOL=0310; FOUND=1
1051 SET_PARAM 'XI'=XP
1080 GOSUB WRAPUP
1090 EXIT 
1098 ! 
1099 ! 
1100 READNEXT:
1110 ENTER FILENO,A$,A{ALL},FOUND
1115 FOUND=0
1120 GOSUB INIT
1125 CALL "UPDEML;CLEAR_FIELDS",A$,A{ALL}
1149 XP=PRM('XI'); SET_PARAM 'XI'
1150 READ (FILENO,END=*NEXT)IOL=0310; FOUND=1
1151 SET_PARAM 'XI'=XP
1180 GOSUB WRAPUP
1190 EXIT 
1198 ! 
1199 ! 
1200 READPREV:
1210 ENTER FILENO,A$,A{ALL},FOUND
1215 FOUND=0
1220 GOSUB INIT
1225 CALL "UPDEML;CLEAR_FIELDS",A$,A{ALL}
1249 XP=PRM('XI'); SET_PARAM 'XI'
1250 PKEY$=KEP(FILENO,ERR=*NEXT); READ (FILENO,KEY=PKEY$)IOL=0310; FOUND=1
1251 SET_PARAM 'XI'=XP
1280 GOSUB WRAPUP
1290 EXIT 
1298 ! 
1299 ! 
1300 READFIRST:
1310 ENTER FILENO,A$,A{ALL},FOUND
1315 FOUND=0
1320 GOSUB INIT
1325 CALL "UPDEML;CLEAR_FIELDS",A$,A{ALL}
1349 XP=PRM('XI'); SET_PARAM 'XI'
1350 PKEY$=KEF(FILENO,KNO=0,ERR=*NEXT); READ (FILENO,KEY=PKEY$)IOL=0310; FOUND=1
1351 SET_PARAM 'XI'=XP
1380 GOSUB WRAPUP
1390 EXIT 
1398 ! 
1399 ! 
1400 READLAST:
1410 ENTER FILENO,A$,A{ALL},FOUND
1415 FOUND=0
1420 GOSUB INIT
1425 CALL "UPDEML;CLEAR_FIELDS",A$,A{ALL}
1449 XP=PRM('XI'); SET_PARAM 'XI'
1450 PKEY$=KEL(FILENO,ERR=*NEXT); READ (FILENO,KEY=PKEY$)IOL=0310; FOUND=1
1451 SET_PARAM 'XI'=XP
1480 GOSUB WRAPUP
1490 EXIT 
1498 ! 
1499 ! 
1500 EXTRACTBYKEY:
1510 ENTER FILENO,KEYNO,SEG1$,SEG2$,SEG3$,SEG4$,SEG5$,A$,A{ALL},FOUND,BUSY
1515 FOUND=0,BUSY=0
1525 CALL "UPDEML;CLEAR_FIELDS",A$,A{ALL}
1549 XP=PRM('XI'); SET_PARAM 'XI'
1550 IF POS("EML"=PTH(FILENO))=0 THEN BUSY=1 ELSE READ (FILENO,KNO=KEYNO,KEY=SEG1$+SEG2$+SEG3$+SEG4$+SEG5$,DOM=*NEXT)IOL=0310; FOUND=1,BUSY=1,PRIMEKEY$=KEC(FILENO,KNO=0); EXTRACT (FILENO,KEY=PRIMEKEY$,KNO=0,ERR=*NEXT); BUSY=0
1551 SET_PARAM 'XI'=XP
1590 EXIT 
1598 ! 
1599 ! 
1600 INSERT:! Add new record
1610 ENTER FILENO,KEYNO,SEG1$,SEG2$,SEG3$,SEG4$,SEG5$,A$,A{ALL},OK
1615 OK=0
1620 GOSUB INIT
1630 DIM KEY$(5,$00$); KEY$=KEL(FILENO,KNO=0,ERR=*NEXT)
1635 KEY$=FN%NEXT_SEQ$(KEY$,0); A$(1,5)=KEY$; WRITE (FILENO,KEY=KEY$,DOM=*NEXT)IOL=0310; OK=1 ! SSP220939 jdf
1680 GOSUB WRAPUP
1690 EXIT 
1698 ! 
1699 ! 
1700 DELETE:
1710 ENTER FILENO,A$,A{ALL},FOUND,BUSY
1715 FOUND=0,BUSY=1,PRIMEKEY$=A$(1,5)
1720 GOSUB INIT
1750 REMOVE (FILENO,KEY=PRIMEKEY$,DOM=DELETE_END,ERR=*NEXT); BUSY=0
1775 DELETE_END:
1780 GOSUB WRAPUP
1790 EXIT 
1798 ! 
1799 ! 
1800 CLEAR_FIELDS:
1810 ENTER A$,A{ALL}
1820 DIM A$(999),A[1]
1890 EXIT 
1898 ! 
1899 ! 
1900 UPDATE:
1910 ENTER FILENO,A$,A{ALL},BUSY
1915 BUSY=1,PRIMEKEY$=A$(1,5)
1920 GOSUB INIT
1930 REMOVE (FILENO,KEY=PRIMEKEY$,DOM=*NEXT)
1950 WRITE (FILENO,KEY=PRIMEKEY$)IOL=0310; BUSY=0 ! SSP 220924
1980 GOSUB WRAPUP
1990 EXIT 
1998 ! 
1999 ! 
2000 CLEAR_LOCK:
2010 ENTER FILENO
2020 GOSUB INIT
2050 READ (FILENO,KNO=0,KEY="",ERR=*NEXT)
2080 GOSUB WRAPUP
2090 EXIT 
2098 ! 
2099 ! 
2500 VALIDATE:
2510 ENTER A$,A{ALL},MSG$
2520 A$=PAD(A$,999),MSG$=""
2550 SWITCH A$(6,2) ! 2800 END SWITCH
2560 CASE "VN"
2565 IF POS("@"=A$(41,60))=0 OR POS("."=A$(41,60))=0 THEN MSG$=MSG("Inval_Eml")
2570 BREAK
2580 CASE "CS"
2585 IF POS("@"=A$(41,60))=0 OR POS("."=A$(41,60))=0 THEN MSG$=MSG("Inval_Eml")
2590 BREAK
2600 CASE "SH"
2605 IF A$(101,1)="E" THEN IF POS("@"=A$(41,60))=0 OR POS("."=A$(41,60))=0 THEN MSG$=MSG("Inval_Eml")
2606 IF A$(101,1)="F" THEN IF STP(A$(41,60),2)="" THEN MSG$=MSG("Inval_Ftp")
2630 BREAK
2650 CASE "CR"
2660 IF POS(A$(31,10)="FM2CEA    FM2CIA    FM2TBA    ",10)=0 THEN MSG$=MSG("Inval_Rpt")
2665 IF A$(101,1)="E" THEN IF POS("@"=A$(41,60))=0 OR POS("."=A$(41,60))=0 THEN MSG$=MSG("Inval_Eml")
2680 BREAK
2690 CASE "BE"
2695 IF POS("@"=A$(41,60))=0 OR POS("."=A$(41,60))=0 OR STP(A$(41,60),2)="" THEN MSG$=MSG("Inval_Eml")
2697 GOSUB GET_COMPANY; A$(311,60)=PAD(FROM_EML$,60)
2700 IF STP(A$(261,50),2)="" THEN A$(261,50)=PAD("Order Shipment Notification",50)
2702 IF STP(A$(371,12),2)="" THEN A$(371,12)=PAD("EMAIL",12)
2705 IF A$(101,1)<>"E" THEN A$(101,1)="E" ! only email allowed
2710 BREAK
2720 CASE "CE" ! customer ship conf email
2725 IF POS("@"=A$(41,60))=0 OR POS("."=A$(41,60))=0 OR STP(A$(41,60),2)="" THEN MSG$=MSG("Inval_Eml")
2730 GOSUB GET_COMPANY; A$(311,60)=PAD(FROM_EML$,60)
2735 IF STP(A$(261,50),2)="" THEN A$(261,50)=PAD("Order Shipment Notification",50)
2740 IF STP(A$(371,12),2)="" THEN A$(371,12)=PAD("EMAIL",12)
2745 IF A$(101,1)<>"E" THEN A$(101,1)="E" ! only email allowed
2760 BREAK
2800 END SWITCH ! 2550
2990 EXIT 
2998 ! 
2999 ! 
9999 END 
10000 GET_COMPANY:
10010 OPEN (HFN,ERR=*RETURN)"ZZPARM"
10020 _FILENO=LFO
10030 READ (_FILENO,KEY="CMP"+%C$,ERR=*NEXT)CC$; FROM_EML$="sales@"+STP(CC$(7,40),3," ")+".com"
10040 READ (_FILENO,KEY=%C$+"EML",ERR=*NEXT)PARAM$; FROM_EML$=PARAM$(67,60)
10080 CLOSE (_FILENO)
10090 RETURN 
10098 ! 
10099 ! 
18100 CREATE_DIR:
18110 ENTER FILENO,A$,A{ALL},SEND_FROM_DIR$
18115 GOSUB INIT
18120 BASE$=CVS(PTH(FILENO),2); XD=POS("EML"=BASE$); BASE$=BASE$(1,XD-1)
18130 IF POS("\"=BASE$)<>0 THEN SEP$="\" ELSE SEP$="/"
18140 DIR$=BASE$+A$(6,2); DIRECTORY DIR$,ERR=*NEXT
18150 DIR$=DIR$+SEP$+CVS(A$(8,20),2); DIRECTORY DIR$,ERR=*NEXT
18160 DIR$=DIR$+SEP$+A$(28,3); DIRECTORY DIR$,ERR=*NEXT
18170 SEND_FROM_DIR$=PAD(DIR$,60)
18175 A$(137,60)=SEND_FROM_DIR$
18185 GOSUB WRAPUP
18190 EXIT 
18198 ! 
18199 ! 
20400 UPDATE_ALL_CUSTOMER:
20402 SEG2$="",SEG3$="",SEG4$="",SEG5$=""
20405 DIM Z[255],A[1]
20407 ENTER EML,FM0,ZZPARM
20410 Z$="01O EML...  02O FM0... 13O ZZPARM  "; IF POS("EML"=PTH(EML))=0 OR POS("FM0"=PTH(FM0))=0 OR POS("ZZPARM"=PTH(ZZPARM))=0 THEN GOSUB INIT ELSE Z[1]=EML,Z[2]=FM0,Z[13]=ZZPARM,Z$=""
20415 CALL "UPDFM0;READBYKEY",Z[2],0,"C",SEG2$,SEG3$,SEG4$,SEG5$,FM0$,FM0{ALL},FOUND
20420 CALL "UPDFM0;READNEXT",Z[2],FM0$,FM0{ALL},FOUND; IF FOUND THEN IF FM0$(1,1)<>"C" THEN FOUND=0
20430 WHILE FOUND
20440 EMAIL_TYPE$="CE",TOPFORM_REF$=PAD(FM0$(2,14),20),SEQUENCE$="001"
20450 CALL "UPDEML;READBYKEY",Z[1],1,EMAIL_TYPE$,TOPFORM_REF$,SEQUENCE$,SEG4$,SEG5$,A$,A{ALL},FOUND2
20460 IF NOT(FOUND2) THEN IF STP(FM0$(522,40),2)<>"" THEN {
20470 EMAIL_ADDRESS$=PAD(FM0$(522,40),60),XFER_TYPE$="E"
20480 GOSUB GET_COMPANY; EML_FROM$=PAD(FROM_EML$,60)
20485 ADDRESS_TYPE$=PAD("SMGPAA",10)
20490 READ DATA FROM REC(IOL(Z[1])) TO IOL=0310
20495 CALL "UPDEML;CREATE_DIR",Z[1],A$,A{ALL},SEND_FROM_DIR$
20500 CALL "UPDEML;VALIDATE",A$,A{ALL},MSG$; IF MSG$="" THEN CALL "UPDEML;INSERT",Z[1],0,EMAIL_TYPE$,TOPFORM_REF$,SEQUENCE$,SEQ4$,SEQ5$,A$,A{ALL},OK
20510  }
20520 CALL "UPDFM0;READNEXT",Z[2],FM0$,FM0{ALL},FOUND; IF FOUND THEN IF FM0$(1,1)<>"C" THEN FOUND=0
20530 WEND 
20580 IF Z$<>"" THEN GOSUB WRAPUP
20590 EXIT 
20598 ! 
20599 ! 
24400 UPDATE_ALL_BUYER:
24405 DIM Z[255],A[1]
24407 ENTER EML,ECA,ZZPARM
24410 Z$="01O EML...  02O ECA... 13O ZZPARM  "; IF POS("EML"=PTH(EML))=0 OR POS("ECA"=PTH(ECA))=0 OR POS("ZZPARM"=PTH(ZZPARM))=0 THEN GOSUB INIT ELSE Z[1]=EML,Z[2]=ECA,Z[13]=ZZPARM,Z$=""
24420 CALL "UPDECA;READNEXT",Z[2],ECA$,ECA2$,FOUND
24430 WHILE FOUND
24440 EMAIL_TYPE$="BE",TOPFORM_REF$=PAD(ECA$(1,15),20),SEQUENCE$="001"
24450 CALL "UPDEML;READBYKEY",Z[1],1,EMAIL_TYPE$,TOPFORM_REF$,SEQUENCE$,SEG4$,SEG5$,A$,A{ALL},FOUND2
24460 IF NOT(FOUND2) THEN {
24470 EMAIL_ADDRESS$=ECA$(109,60),XFER_TYPE$="E"
24480 GOSUB GET_COMPANY; EML_FROM$=PAD(FROM_EML$,60)
24485 ADDRESS_TYPE$=PAD("SMGPAA",10)
24490 READ DATA FROM REC(IOL(Z[1])) TO IOL=0310
24495 CALL "UPDEML;CREATE_DIR",Z[1],A$,A{ALL},SEND_FROM_DIR$
24500 CALL "UPDEML;VALIDATE",A$,A{ALL},MSG$; IF MSG$="" THEN CALL "UPDEML;INSERT",Z[1],0,EMAIL_TYPE$,TOPFORM_REF$,SEQUENCE$,SEQ4$,SEQ5$,A$,A{ALL},OK
24510  }
24520 CALL "UPDECA;READNEXT",Z[2],ECA$,ECA2$,FOUND
24530 WEND 
24580 IF Z$<>"" THEN GOSUB WRAPUP
24590 EXIT 
24598 ! 
24599 ! 
24600 UPDATE_SINGLE_BUYER:
24605 DIM Z[255],A[1]
24607 ENTER EML,ECA,ZZPARM,BUYER$
24610 Z$="01O EML...  02O ECA... 13O ZZPARM  "; IF POS("EML"=PTH(EML))=0 OR POS("ECA"=PTH(ECA))=0 OR POS("ZZPARM"=PTH(ZZPARM))=0 THEN GOSUB INIT ELSE Z[1]=EML,Z[2]=ECA,Z[13]=ZZPARM,Z$=""
24620 CALL "UPDECA;READBYKEY",Z[2],0,BUYER$,SEG2$,SEG3$,SEG4$,SEG5$,ECA$,ECA2$,FOUND
24630 ! 24750 }
24640 EMAIL_TYPE$="BE",TOPFORM_REF$=PAD(ECA$(1,15),20),SEQUENCE$="001"
24650 CALL "UPDEML;CLEAR_FIELDS",A$,A{ALL}; CALL "UPDEML;READBYKEY",Z[1],1,EMAIL_TYPE$,TOPFORM_REF$,SEQUENCE$,SEG4$,SEG5$,A$,A{ALL},FOUND2
24660 IF NOT(FOUND2) THEN {
24670 EMAIL_ADDRESS$=ECA$(109,60),XFER_TYPE$="E"
24680 GOSUB GET_COMPANY; EML_FROM$=PAD(FROM_EML$,60)
24690 READ DATA FROM REC(IOL(Z[1])) TO IOL=0310
24695 CALL "UPDEML;VALIDATE",A$,A{ALL},MSG$; IF MSG$="" THEN CALL "UPDEML;INSERT",Z[1],0,EMAIL_TYPE$,TOPFORM_REF$,SEQUENCE$,SEQ4$,SEQ5$,A$,A{ALL},OK
24700  } ELSE {
24710 READ DATA FROM REC(IOL=0310) TO IOL=IOL(Z[1])
24715 EMAIL_ADDRESS$=ECA$(109,60)
24730 READ DATA FROM REC(IOL(Z[1])) TO IOL=0310
24735 CALL "UPDEML;VALIDATE",A$,A{ALL},MSG$; IF MSG$="" THEN CALL "UPDEML;UPDATE",Z[1],A$,A{ALL},BUSY
24740  }
24750  }
24780 IF Z$<>"" THEN GOSUB WRAPUP
24790 EXIT 
24798 ! 
24799 ! 
56000 REM "186852-Email Order Shipping Confirmations                          
56002 REM "191846-add call to zzcomp in all upd pgms                          
56003 ! "220924-Cosmetic changes to panel Data Transport Setup              
56004 REM "220939-Cosmetic changes to Data Transport Setup Customer Shipment  
