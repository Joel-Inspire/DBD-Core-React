0010 ! UPDFM1 - File FM1 Maintenance
0035 REM "5.4 - 01/23/06 - 16.722222 - jme - SSP# 188067
0037 REM "188067-New Pricing changes. Standard GP Percentage for a           
0040 REM "Copyright 2006 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! FILENO - CHANNEL OF OPEN FILE
0051 ! KEYNO  - KEY NUMBER
0052 ! SEG1-SEG5$ : KEY SEGMENTS . CAN BE NULL
0053 ! A$ - RECORD A$
0054 ! FOUND - IF RECORD FOUND, THEN 1 ELSE 0
0055 ! BUSY - IF RECORD BUSY THEN 1 ELSE 0
0056 ! OK - IF RECORD ADDED OK=1 ELSE 0
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30],A[31]
0500 INIT:
0505 DIM Z[255]; OPENED=0
0507 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
0510 IF POS("FM1"=PTH(FILENO))=0 THEN Z$="01O FM1...  "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; FILENO=Z[1],OPENED=1
0590 RETURN 
0598 ! 
0599 ! 
0600 WRAPUP:
0610 IF OPENED THEN CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0690 RETURN 
0698 ! 
0699 ! 
1000 READBYKEY:
1010 ENTER FILENO,KEYNO,SEG1$,SEG2$,SEG3$,SEG4$,SEG5$,A$,A{ALL},FOUND
1015 FOUND=0
1020 GOSUB INIT
1025 CALL "UPDFM1;CLEAR_FIELDS",A$,A{ALL}
1049 XP=PRM('XI'); SET_PARAM 'XI'
1050 READ (FILENO,KNO=KEYNO,KEY=SEG1$+SEG2$+SEG3$+SEG4$+SEG5$,DOM=*NEXT)IOL=0310; FOUND=1
1051 SET_PARAM 'XI'=XP
1080 GOSUB WRAPUP
1090 EXIT 
1098 ! 
1099 ! 
1100 READNEXT:
1110 ENTER FILENO,A$,A{ALL},FOUND
1115 FOUND=0
1120 GOSUB INIT
1125 CALL "UPDFM1;CLEAR_FIELDS",A$,A{ALL}
1149 XP=PRM('XI'); SET_PARAM 'XI'
1150 READ (FILENO,END=*NEXT)IOL=0310; FOUND=1
1151 SET_PARAM 'XI'=XP
1180 GOSUB WRAPUP
1190 EXIT 
1198 ! 
1199 ! 
1200 READPREV:
1210 ENTER FILENO,A$,A{ALL},FOUND
1215 FOUND=0
1220 GOSUB INIT
1225 CALL "UPDFM1;CLEAR_FIELDS",A$,A{ALL}
1249 XP=PRM('XI'); SET_PARAM 'XI'
1250 PKEY$=KEP(FILENO,ERR=*NEXT); READ (FILENO,KEY=PKEY$)IOL=0310; FOUND=1
1251 SET_PARAM 'XI'=XP
1280 GOSUB WRAPUP
1290 EXIT 
1298 ! 
1299 ! 
1300 READFIRST:
1310 ENTER FILENO,A$,A{ALL},FOUND
1315 FOUND=0
1320 GOSUB INIT
1325 CALL "UPDFM1;CLEAR_FIELDS",A$,A{ALL}
1349 XP=PRM('XI'); SET_PARAM 'XI'
1350 PKEY$=KEF(FILENO,KNO=0,ERR=*NEXT); READ (FILENO,KEY=PKEY$)IOL=0310; FOUND=1
1351 SET_PARAM 'XI'=XP
1380 GOSUB WRAPUP
1390 EXIT 
1398 ! 
1399 ! 
1400 READLAST:
1410 ENTER FILENO,A$,A{ALL},FOUND
1415 FOUND=0
1420 GOSUB INIT
1425 CALL "UPDFM1;CLEAR_FIELDS",A$,A{ALL}
1449 XP=PRM('XI'); SET_PARAM 'XI'
1450 PKEY$=KEL(FILENO,ERR=*NEXT); READ (FILENO,KEY=PKEY$)IOL=0310; FOUND=1
1451 SET_PARAM 'XI'=XP
1480 GOSUB WRAPUP
1490 EXIT 
1498 ! 
1499 ! 
1500 EXTRACTBYKEY:
1510 ENTER FILENO,KEYNO,SEG1$,SEG2$,SEG3$,SEG4$,SEG5$,A$,A{ALL},FOUND,BUSY
1515 FOUND=0,BUSY=0
1525 CALL "UPDFM1;CLEAR_FIELDS",A$,A{ALL}
1549 XP=PRM('XI'); SET_PARAM 'XI'
1550 IF POS("FM1"=PTH(FILENO))=0 THEN BUSY=1 ELSE READ (FILENO,KNO=KEYNO,KEY=SEG1$+SEG2$+SEG3$+SEG4$+SEG5$,DOM=*NEXT)IOL=0310; FOUND=1,BUSY=1,PRIMEKEY$=KEC(FILENO,KNO=0); EXTRACT (FILENO,KEY=PRIMEKEY$,KNO=0,ERR=*NEXT); BUSY=0
1551 SET_PARAM 'XI'=XP
1590 EXIT 
1598 ! 
1599 ! 
1600 INSERT:
1610 ENTER FILENO,A$,A{ALL},OK
1615 OK=0,PRIMEKEY$=A$(1,20)
1620 GOSUB INIT
1650 WRITE (FILENO,KEY=PRIMEKEY$,ERR=*NEXT)IOL=0310; OK=1
1680 GOSUB WRAPUP
1690 EXIT 
1698 ! 
1699 ! 
1700 DELETE:
1710 ENTER FILENO,A$,A{ALL},FOUND,BUSY
1715 FOUND=0,BUSY=1,PRIMEKEY$=A$(1,20)
1720 GOSUB INIT
1750 REMOVE (FILENO,KEY=PRIMEKEY$,DOM=DELETE_END,ERR=*NEXT); BUSY=0
1780 GOSUB WRAPUP
1785 DELETE_END:
1790 EXIT 
1798 ! 
1799 ! 
1800 CLEAR_FIELDS:
1810 ENTER A$,A{ALL}
1820 DIM A$(449),A[31]
1890 EXIT 
1898 ! 
1899 ! 
1900 UPDATE:
1910 ENTER FILENO,A$,A{ALL},BUSY
1915 BUSY=1,PRIMEKEY$=A$(1,20)
1920 GOSUB INIT
1950 WRITE (FILENO,KEY=PRIMEKEY$,ERR=*NEXT)IOL=0310; BUSY=0
1980 GOSUB WRAPUP
1990 EXIT 
1998 ! 
1999 ! 
2000 CLEAR_LOCK:
2010 ENTER FILENO
2020 GOSUB INIT
2050 READ (FILENO,KNO=0,KEY="",ERR=*NEXT)
2080 GOSUB WRAPUP
2090 EXIT 
2098 ! 
2099 ! 
2500 VALIDATE:
2510 ENTER A$,A{ALL},MSG$
2520 A$=PAD(A$,449),MSG$=""
2990 EXIT 
2998 ! 
2999 ! 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8107 T=0
8113 CALL "ZZINFO",Z[1],0,X3$,A,B,0,0,0,0,0,""; T=A
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8135 T1=0
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,I9
8195 RETURN 
9999 END 
10000 UPD_SP0:
10002 ! Update FM1, price, fms based upon SP0 records
10010 ENTER TYPE$
10015 SEG1$="",SEG2$="",SEG3$="",SEG4$="",SEG5$=""
10020 IF POS(TYPE$="PFB")<>0 THEN {! 10950 }
10025 DIM Z[255]
10030 Z$="01O FM1...  02O SP0...  03O IC0...  13O ZZPARM  "
10050 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
10055 GOSUB 8100; GOSUB 8150
10060 CALL "UPDFM1;READBYKEY",Z[1],0,SEG1$,SEG2$,SEG3$,SEG4$,SEG5$,FM1$,FM1{ALL},FOUND
10090 CALL "UPDFM1;READNEXT",Z[1],FM1$,FM1{ALL},FOUND
10100 WHILE FOUND ! 10390 WEND
10105 OLDPRICE=FM1[3],COST=FM1[1]
10107 COST_QTY=FM1[2],PRICE_QTY=FM1[4]
10110 FOUNDSP0_IGP=0; IF POS(TYPE$="PB")<>0 THEN CALL "UPDSP0;GET_IGP",Z[2],FM1$(1,20),OLDPRICE,COST,NEWPRICE,COST_QTY,PRICE_QTY,FOUNDSP0_IGP
10120 IF FOUNDSP0_IGP THEN {! 10190 }
10140 IF NEWPRICE<>FM1[3] THEN {! 10185
10150 CALL "UPDFM1;EXTRACTBYKEY",Z[1],0,FM1$(1,10),FM1$(11,10),SEG3$,SEG4$,SEG5$,FM1$,FM1{ALL},FOUNDFM1,BUSYFM1
10160 IF FOUNDFM1 AND NOT(BUSYFM1) THEN {
10165 FM1[3]=NEWPRICE; CALL "UPDFM1;UPDATE",Z[1],FM1$,FM1{ALL},BUSY
10170  }
10185  } ! 10140
10190  } ! 10120
10200 ! fms
10210 IF POS(TYPE$="FB")<>0 THEN {! 10290
10215 OLDPRICE=FM1[29],COST=FM1[1],FMSPERCENT=FM1[30],SALEPRICE=FM1[3]
10217 COST_QTY=FM1[2],PRICE_QTY=FM1[4]
10220 CALL "UPDSP0;DETERMINE_FMS",Z[2],FM1$(1,10),FM1$(11,10),OLDPRICE,COST,NEWPRICE,FMSPERCENT,SALEPRICE,COST_QTY,PRICE_QTY
10240 IF NEWPRICE<>FM1[29] THEN {! 10285
10250 CALL "UPDFM1;EXTRACTBYKEY",Z[1],0,FM1$(1,10),FM1$(11,10),SEG3$,SEG4$,SEG5$,FM1$,FM1{ALL},FOUNDFM1,BUSYFM1
10260 IF FOUNDFM1 AND NOT(BUSYFM1) THEN {
10261 FM1[29]=NEWPRICE
10262 IF FM1$(108,1)="N" THEN FM1[29]=0,FM1[30]=0
10265 CALL "UPDFM1;UPDATE",Z[1],FM1$,FM1{ALL},BUSY
10270  }
10285  } ! 10140
10290  } ! 10120
10310 I9=I9+1; IF MOD(I9,T0)=1 THEN GOSUB 8150
10350 CALL "UPDFM1;READNEXT",Z[1],FM1$,FM1{ALL},FOUND
10390 WEND 
10950  } ! 10020
10990 EXIT 
10998 ! 
10999 ! 
56000 REM "188067-New Pricing changes. Standard GP Percentage for a           
