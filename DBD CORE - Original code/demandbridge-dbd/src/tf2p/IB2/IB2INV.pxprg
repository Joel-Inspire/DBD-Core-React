0010 REM "<IB2INV> IBSA Invoice Export
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 11/15/11 - 18.028333 - jvv - SSP# 249775
0037 REM "249775-Changes to sales tax parameters and tax calculations to     
0040 REM "Copyright 2011 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,CUST$,INV$,AUDIT$,CHANNEL,CHANNEL_1
0100 SETERR 9000
0110 X0$="IB2INV",X1$="IBSA Invoice Order Export "
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1,C=1
0135 C9=-1
0140 DIM A[30],C[48],Z1$(15," ") ! SSP 249775
0145 DTT$=FN%NTD$(CDN,"YYYYMMDD")
0150 COMMA$=",",END$="$0D0A$"
0151 LINE=0
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0166 DEF FNF$(Z9$)=Z9$(7,4)+Z9$(1,2)+Z9$(4,2)
0170 IF POS("MS"=UCS(SYS)) THEN ISWIN=1 ELSE ISWIN=0
0290 PARM$=X3$(9,3)+"IBS"
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30]
0330 IOLIST C$,C1$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20],C[21],C[22],C[23],C[24],C[25],C[26],C[27],C[28],C[29],C[30],C[31],C[32],C[33],C[34],C[35],C[36],C[37],C[38],C[39],C[40],C[41],C[42],C[43],C[44],C[45],C[46],C[47],C[48] ! SSP 249775
0370 IOLIST G$
0410 IOLIST Q$
0411 IOLIST S5$,S[0],S[1]
0420 IOLIST K$
0425 IOLIST RECORD_TYPE_H$,COMMA$,INVOICE_NUMBER$,COMMA$,INVOICE_DATE$,COMMA$,CUSTOMER_PO_NUMBER$,COMMA$,INVOICE_SUB_TOTAL,COMMA$,INVOICE_SALES_TAX,COMMA$,INVOICE_FREIGHT,COMMA$,INVOICE_TOTAL,COMMA$,BILL_TO$,COMMA$,BILL_TO_COMPANY_NAME$,COMMA$,BILL_TO_CONTACT$,COMMA$,BILL_TO_ADDRESS_LINE1$,COMMA$,BILL_TO_ADDRESS_LINE2$,COMMA$,BILL_TO_CITY$,COMMA$,BILL_TO_STATE$,COMMA$,BILL_TO_ZIP$,COMMA$,BILL_TO_PHONE$,COMMA$,BILL_TO_FAX$,COMMA$,BILL_TO_EMAIL$,COMMA$,SHIP_TO_CODE$,COMMA$,SHIP_TO_COMPANY_NAME$,COMMA$,SHIP_TO_CONTACT$,COMMA$,SHIP_TO_ADDRESS_LINE1$,COMMA$,SHIP_TO_ADDRESS_LINE2$,COMMA$,SHIP_TO_CITY$,COMMA$,SHIP_TO_STATE$,COMMA$,SHIP_TO_ZIP$,COMMA$,SHIP_TO_PHONE$,COMMA$,SHIP_TO_FAX$,COMMA$,SHIP_TO_EMAIL$,COMMA$,BILLING_ID$+$0D0A$
0450 IOLIST RECORD_TYPE_D$,COMMA$,INVOICE_NUMBER$,COMMA$,LINE_ITEM_REF$,COMMA$,FORM_NUMBER$,COMMA$,ITEM_DESC_LINE1$,COMMA$,ITEM_DESC_LINE2$,COMMA$,UOM$,COMMA$,QUANTITY,COMMA$,UNIT_PRICE,COMMA$,UNIT_COST,COMMA$,SUPPLIER_CODE$,COMMA$,SUPPLIER_NAME$,COMMA$,FACTORY_JOB$,COMMA$,PRODUCT_CATEGORY$+$0D0A$
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O IB0... 02O IB1...  03O IB2...  04O AP4...  05O PO1...  07O AR1...  09O ARB...  10O ART...  11O FS2...  12O FS1...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0560 ! CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0561,9920!SSP#248051
0580 ! DEVICE=W9!SSP#248051
0590 READ (Z[13],KEY=PARM$,DOM=9900)IOL=0410; REM "Get Senders information
1000 REM "Main Process
1010 READ (Z[9],KEY=INV$,DOM=9900)IOL=0310; REM "Initial read from orders file (ARB)
1020 READ (Z[1],KEY=A$(15,10),DOM=*NEXT)IB0$
1030 REM "IF FOUND_TP$="N" THEN GOTO 9900
1055 READ (Z[7],KEY=A$(15,10),DOM=1056)IOL=0370; REM " READ AR1
1060 GOSUB 2100; REM "Header
1070 GOSUB 2300; REM " Lines
1090 REM "IF Q$(128,120)<>DIM(120) THEN GOSUB 3000; REM "Archive file to editmp in case of problems
1600 GOTO 9900
2100 REM HEADER
2101 FIND (Z[12],KEY=A$(92,8),DOM=2105)FS_HEAD$
2110 RECORD_TYPE_H$="H"; REM " H to indicate header
2115 INVOICE_NUMBER$=UCS(A$(7,8))
2117 CALL "ZZDISP","DX",A$(86,6),"",X3$,DATE$,"",0,0,X4$
2120 INVOICE_DATE$=DATE$(1,6)+DATE$(9,2)
2125 IF A$(112,3)=DIM(3) THEN CUSTOMER_PO_NUMBER$=SUB(A$(100,12),","," ") ELSE CUSTOMER_PO_NUMBER$=SUB(A$(103,12),","," "); REM " Per Julie, crop the first three characters to the left
2130 INVOICE_SUB_TOTAL=A[1]
2135 INVOICE_SALES_TAX=A[4]+A[6]+A[8]
2140 INVOICE_FREIGHT=A[2]
2145 INVOICE_TOTAL=A[9]
2150 IF LEN(IB0$)<>0 THEN BILL_TO$=UCS(IB0$(11,7))
2155 BILL_TO_COMPANY_NAME$=SUB(A$(25,35),","," ")
2160 BILL_TO_CONTACT$=SUB(G$(298,20),","," ")
2165 BILL_TO_ADDRESS_LINE1$=SUB(G$(56,30),","," ")
2170 BILL_TO_ADDRESS_LINE2$=SUB(G$(86,30),","," ")
2175 BILL_TO_CITY$=SUB(G$(116,16),","," ")
2180 BILL_TO_STATE$=UCS(G$(132,2))
2185 BILL_TO_ZIP$=SUB(G$(134,9),","," ")
2190 BILL_TO_PHONE$=SUB(G$(205,14),","," ")
2195 BILL_TO_FAX$=SUB(G$(143,12),","," ")
2200 BILL_TO_EMAIL$=""
2205 SHIP_TO_CODE$=BILL_TO$; REM " SSP 181909
2210 SHIP_TO_COMPANY_NAME$=SUB(A$(290,35),","," ")
2215 SHIP_TO_CONTACT$=""
2220 SHIP_TO_ADDRESS_LINE1$=SUB(A$(191,30),","," ")
2225 SHIP_TO_ADDRESS_LINE2$=SUB(A$(221,30),","," ")
2230 SHIP_TO_CITY$=SUB(A$(251,16),","," ")
2235 SHIP_TO_STATE$=UCS(A$(267,2))
2240 SHIP_TO_ZIP$=SUB(A$(269,9),","," ")
2245 SHIP_T0_PHONE$=""
2250 SHIP_TO_FAX$=""
2255 SHIP_TO_EMAIL$=""
2260 BILLING_ID$=""
2275 IF CHANNEL<>0 THEN PRINT (CHANNEL)IOL=0425
2276 IF CHANNEL_1<>0 THEN PRINT (CHANNEL_1)IOL=0425
2290 RETURN 
2300 REM "LINES
2301 KT=A[20]
2302 IF KT<=0 THEN GOTO 2399 ELSE READ (Z[10],IND=KT,END=2399)IOL=0330
2303 IF C$="" THEN EXITTO 1080 ELSE KT=NUM(C$)
2305 IF C1$(6,1)="N" OR C1$(1,1)="M" THEN GOTO 2302
2310 RECORD_TYPE_D$="D"
2315 LINE=LINE+1
2320 LINE_ITEM_REF$=STP(UCS(INVOICE_NUMBER$+STR(LINE)),3," ")
2325 FORM_NUMBER$=UCS(C1$(65,10))
2330 ITEM_DESC_LINE1$=SUB(C1$(7,30),","," ")
2335 ITEM_DESC_LINE2$=""
2340 UOM$=C1$(50,2)
2341 DIM S[2]; FIND (Z[13],KEY="U/M"+UOM$,DOM=2342)IOL=0411
2345 IF C1$(1,1)="S" AND C[2]=0 AND C[4]<>0 AND C[3]=0 AND C[5]<>0 THEN QUANTITY=1 ELSE QUANTITY=C[1]
2346 IF C[6]<>0 AND UOM$<>"M   " THEN QUANTITY=QUANTITY/C[6]
2350 IF C[2]=0 AND C[4]<>0 AND C1$(1,1)="S" THEN UNIT_PRICE=C[4] ELSE UNIT_PRICE=C[2]
2351 IF C[3]=0 AND C[5]<>0 AND C1$(1,1)="S" THEN UNIT_COST=C[5] ELSE UNIT_COST=C[3]
2352 IF S[0]>0 THEN PRECISION 14
2353 IF S[0]>0 THEN UNIT_PRICE=UNIT_PRICE/S[0],UNIT_COST=UNIT_COST/S[0]
2355 READ (Z[11],KEY=A$(92,8)+C1$(47,3),DOM=2371)FS2$
2357 READ (Z[2],KEY=FS2$(94,10),DOM=2360)IB1$
2358 SUPPLIER_CODE$=UCS(IB1$(11,6)); GOTO 2361
2360 SUPPLIER_CODE$=UCS(FS2$(94,6))
2365 READ (Z[5],KEY=FS2$(147,8)+FS2$(9,1),DOM=2370)PO1$
2366 FACTORY_JOB$=SUB(PO1$(248,11),","," "); GOTO 2371
2370 FACTORY_JOB$=""
2371 READ (Z[3],KEY=C1$(3,3),DOM=2375)IB2$
2372 PRODUCT_CATEGORY$=UCS(IB2$(4,3)); GOTO 2376
2375 PRODUCT_CATEGORY$=UCS(C1$(3,3))
2376 READ (Z[4],KEY=MID(FS2$,94,10),DOM=2378)AP4$
2377 SUPPLIER_NAME$=SUB(AP4$(11,30),","," "); GOTO 2379
2378 SUPPLIER_NAME$=""
2380 IF CHANNEL<>0 THEN PRINT (CHANNEL)IOL=0450
2381 IF CHANNEL_1<>0 THEN PRINT (CHANNEL_1)IOL=0450
2382 PRECISION 2
2385 GOTO 2302
2399 RETURN 
3000 REM "Write file to the editmp directory
3030 REM "CLOSE (CHANNEL)
3031 IF Q$(128,120)=DIM(120) THEN GOTO 3090
3035 IF ISWIN THEN S2$="copy "+STP(Q$(8,120),1)+FILE_NAME$+" "+STP(Q$(128,120),1)+DLM; CALL "ZZ2CMD",X3$,X4$,S2$,"","IN",COMM_CODE,COMM_CODE$; GOTO 3050; REM "SSP146619, If NT call command processor so command will happen on server
3040 S2$="cp "+STP(Q$(8,120),1)+FILE_NAME$+" "+STP(Q$(128,120),1)+FILE_NAME$; INVOKE S2$
3090 RETURN 
9000 REM "Error
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM "Transfer Control
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "Ctrl logic
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "End Program
9905 REM "CLOSE (DEV_CHANNEL,ERR=9906)
9906 ERASE "EDILCK",ERR=9907
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "248051-IBSA Customer Code Not Uploading                            
56001 REM "249775-Changes to sales tax parameters and tax calculations to     
