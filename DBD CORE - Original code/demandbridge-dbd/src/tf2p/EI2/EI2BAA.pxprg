0010 REM "Salesperson/Div Summary Ranking <EI2BAA>
0015 REM "Prog Type: I -2.9
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 09/13/13 - 9.022222 - tma - SSP# 265536
0037 REM "265536-Issue with E/I-A1.  Printing blank page/full page/blank page
0040 REM "Copyright 2013 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000; SETESC 9300
0110 X0$="EI2BAA",X1$="Salesperson/Div Summary Ranking",K9$="",K9=0
0120 DIM Z0$(80,"-"),ZL$(80,"="); REM "ssp#091686
0130 K0=4,K1=1
0135 C9=-1,V9=-2
0140 M0$="#,###,##0-",M1$="###.00%-"
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(65,1)>"2" THEN PRECISION NUM(X3$(65,1),ERR=0246)
0270 REM "CALL "ZZRPTO",X3$,X4$,Y0$,F0$,F{ALL},4,C0,X0
0275 PRINT @(0,3),'CE',
0280 REM "IF F0$="" THEN F0$="I"+X3$(172,2)+"   NNN    66 ",F1$="LP" ELSE IF POS(F0$(2,1)="TA")<>0 THEN F1$="LP" ELSE F1$=F0$(2,6)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11]
0400 REM "Get E/I Info - added 400's by cwj 6/22/98 ssp#091696
0405 DIM Z[NUM(X3$(60,3))]
0410 Z$="13O ZZPARM  "
0415 GOSUB 9750; ON Z0 GOTO 0416,9900
0420 FIND (Z[13],KEY=X3$(9,3)+"E/I")E0$
0425 IF E0$(7,1)<>"Y" THEN E1$="..."; GOTO 0500 ELSE E1$=X3$(40,3)
0430 OPEN (1,ERR=0431)"EI1"+E1$; FIND (1,KEY="",ERR=7100)P$; IF P$(29,3)<>X3$(9,3) THEN GOTO 7100
0435 CLOSE (1)
0500 REM "FILES
0509 REM "CHANNEL Z(3) USED for Work file (see 7800)
0510 Z$="01X EI0"+E1$+"  02O EI1"+E1$+"  04O AR3...  05O ARH...  "
0520 GOSUB 9750; ON Z0 GOTO 0521,9900
0530 READ (Z[1],KEY="",DOM=0531)P$
0600 REM "
0610 DIM A$(3),A[1],B[7],S1$(9,"0")
0620 A$(1)="SBY",A[0]=1
0630 I=1,I1=26; DIM H$(13*I1)
0640 H$(I,I1)="Code",I=I+I1
0645 H$(I,I1)="Orders Booked-Sales-MTD",I=I+I1
0650 H$(I,I1)="Orders Booked-Profit-MTD",I=I+I1
0655 H$(I,I1)="Orders Booked-Sales-YTD",I=I+I1
0660 H$(I,I1)="Orders Booked-Profit-YTD",I=I+I1
0665 H$(I,I1)="Invoiced Orders-Sales-MTD",I=I+I1
0670 H$(I,I1)="Invoiced Orders-Profit-MTD",I=I+I1
0680 H$(I,I1)="Invoiced Orders-Sales-YTD",I=I+I1
0685 H$(I,I1)="Invoiced Orders-Profit-YTD",I=I+I1
0690 H$(I,I1)="Orders Booked-MTD-GP%",I=I+I1
0695 H$(I,I1)="Orders Booked-YTD-GP%",I=I+I1
0700 H$(I,I1)="Invoiced Orders-MTD-GP%",I=I+I1
0705 H$(I,I1)="Invoiced Orders-YTD-GP%",I=I+I1
1000 REM "
1020 IF LEN(P$)<50 THEN GOTO 9800
1040 GOSUB 6000; GOSUB 6200
1060 GOTO 2000
1800 REM "
1810 PRNT$="N"; REM "ssp#091686
1820 CALL "ZZPROM","0 EI2BAA",X3$,Z,"Proceed with the display of the ranking","","",X0; ON Z GOTO 2500,9900,2000,7000; REM "ssp#091686 add 7000 selection
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 SETERR 9000; X$="",Q$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF INT(C0)=3 THEN GOSUB 7300 ELSE IF C0>3 OR X0<0 THEN GOTO 1800
2035 IF V9<>-2 THEN IF 6*(X0+K1)+5<LEN(V9$) THEN IF V9$((X0+K1)*6+5,1)="3" THEN GOTO 2020 ELSE IF V9$((X0+K1)*6+5,1)="2" AND C0<>2 THEN GOTO 2020
2038 IF X0<LEN(V7$) THEN V7$(X0+1,1)=""
2040 ON X0 GOTO 2100,2150,2250,2200,1800
2100 REM "Salesperson/Division
2110 CALL "ZZENTR","SUR",A{ALL},A$,X4$,X3$,55,6,1,1,C0,"","{3"+X$,"SD","EI2BAA02","","",""
2140 GOTO 2020
2150 REM "Orders Booked / Invoiced
2160 CALL "ZZENTR","SUR",A{ALL},A$,X4$,X3$,55,7,2,1,C0,M0$,"{3"+X$,"BI","EI2BAA04","","",""
2170 IF A$(2,1)="B" THEN A[0]=1 ELSE A[0]=7
2190 GOTO 2020
2200 REM "Decending field to sort upon
2205 X$="Zero means display unranked but in Salesperson or Division code order"
2210 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,44,18,0,200,C0,"#0","{3"+X$,"","EI2BAA06","","",""
2220 IF A[0]<0 OR A[0]>12 THEN GOTO 2200
2240 GOTO 2020
2250 REM "Eliminate Display if all values zero?
2260 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,55,8,3,1,C0,"","{3"+X$,"SD","EI2BAA04","","",""
2290 GOTO 2020
2500 REM "Compute Ranked Values in Temp File
2510 PRINT @(0,22),'CE',@(10),"...Sorting and Computing Ranking in progress....",
2520 GOSUB 7800
2530 REM "Figure out value to sort on from A(0)
2550 X$=STR(A[0]:"00"); P=POS(X$="0102040507081011",2); IF A[0]=0 THEN P=-1; GOTO 2580
2565 REM " P>0 means P-1 will be index into B(x), otherwise need to use a GP% and get it from index 8-11
2570 IF P>0 THEN P=(P+1)/2,P=P-1 ELSE P=7+A[0]/3
2580 IF A$(1,1)="S" THEN F=Z[1] ELSE F=Z[2]
2590 READ (F,KEY="",DOM=2591)
2600 READ (F,END=3000)IOL=0320
2620 DIM C[11]; C$=B$; FOR I=0 TO 7; B[I]=SGN(B[I])*INT(ABS(B[I])+.5),C[I]=B[I]; NEXT I
2630 GOSUB 7500
2635 IF A[0]=0 THEN I=0; GOTO 2670
2640 I=C[P]; IF P>7 THEN I=I*100; REM "NEED fractional GP% in sort
2660 I=NUM("1"+S1$(1,LEN(S1$)-1))-I
2670 K$=STR(I:S1$)+C$(1,4)
2680 WRITE (Z[3],KEY=K$)IOL=0330
2700 REM "Keep totals at end of file in TOTAL record key
2710 DIM C[11]; C$(1)="TOTAL"; READ (Z[3],KEY=C$(1,5),DOM=2711)IOL=0330
2720 FOR I=0 TO 7; C[I]=C[I]+B[I]; NEXT I
2730 GOSUB 7500
2740 WRITE (Z[3],KEY=C$(1,5))IOL=0330
2750 GOTO 2600
3000 REM DISPLAY RANKINGS
3010 IF PRNT$="Y" THEN GOSUB 7900; REM "ssp#091686
3020 PRINT @(0,5),'CE',@(3),"Ranked in Decending sequence by "+H$((P+1)*I1+1,I1),@(0,6),Z0$,
3025 IF PRNT$="Y" THEN PRINT (W9)@(3),"Ranked in Descending sequence by "+H$((P+1)*I1+1,I1),'LF',ZL$; REM "ssp#091686
3040 IF A$(2,1)="B" THEN P1=0,H1$="Orders Booked" ELSE P1=4,H1$="Invoiced Orders"
3060 IF A$(1,1)="S" THEN H2$="Salesperson" ELSE H2$="Division"
3080 DIM S2$(44,"-"); S2$(22-INT(LEN(H1$)/2),LEN(H1$))=H1$; PRINT @(35,7),S2$,; IF PRNT$="Y" THEN PRINT (W9)@(35),S2$; REM "ssp#091686
3100 DIM S2$(34,"-"); S2$(2,LEN(H2$))=H2$; PRINT @(0,8),S2$,; IF PRNT$="Y" THEN PRINT (W9)@(0),S2$,; REM "ssp#091686
3120 DIM S2$(44,"-"); S2$(5,13)="Month-to-Date",S2$(23,1)="",S2$(27,12)="Year-to-Date"; PRINT @(35,8),S2$,
3140 PRINT @(0,9),"Rank Code Description",@(40),"Sales",@(50),"Profit",@(60),"Sales",@(70),"Profit",@(0,10),Z0$,
3142 IF PRNT$="Y" THEN PRINT (W9)@(0),"Rank Code Description",@(40),"Sales",@(50),"Profit",@(60),"Sales",@(70),"Profit",'LF',@(0),ZL$; REM "ssp#091686
3180 L2=11,L=L2,L1=20,R$="001",LC0=11; IF W0<>0 THEN LC2=W0 ELSE LC2=66
3190 READ (Z[3],KEY="",DOM=3191)
3200 REM 
3220 READ (Z[3],END=3600)IOL=0330
3222 IF A$(3,1)="Y" AND C$(1,5)<>"TOTAL" THEN IF C[P1]=0 AND C[P1+1]=0 AND C[P1+2]=0 AND C[P1+3]=0 THEN GOTO 3220
3225 IF C$(1,5)="TOTAL" THEN R$="",C$(1)="",L=L+1,L1=L1-2; DIM D$(24,"."); D$(1,5)="TOTAL"; GOTO 3250
3230 DIM D$(24); IF A$(1,1)="S" THEN READ (Z[4],KEY=C$(1,4),DOM=3231)D$; D$=D$(5,24) ELSE READ (Z[5],KEY=C$(3,2),DOM=3231)D$; D$=D$(3,24)
3250 IF L>L1 THEN GOSUB 3400
3260 PRINT @(0,L),R$,@(5),C$(1,4),@(10),D$,@(35),C[P1]:M0$,@(46),C[P1+1]:M0$,@(57),C[P1+2]:M0$,@(68),C[P1+3]:M0$,
3261 LC0=LC0+1; IF LC0>LC2 THEN GOSUB 3450; REM "ssp#091686
3262 IF PRNT$="Y" THEN PRINT (W9)@(0),R$,@(5),C$(1,4),@(10),D$,@(35),C[P1]:M0$,@(46),C[P1+1]:M0$,@(57),C[P1+2]:M0$,@(68),C[P1+3]:M0$; REM "ssp#091686
3270 IF D$(1,5)<>"TOTAL" THEN GOTO 3280 ELSE IF A$(2,1)="B" THEN I=8 ELSE I=10
3275 PRINT @(49,L+1),C[I]:M1$,@(71),C[I+1]:M1$,
3278 IF PRNT$="Y" THEN PRINT (W9)@(49),C[I]:M1$,@(71),C[I+1]:M1$; REM "ssp#091686
3280 L=L+1,R$=STR(NUM(R$)+1:"000")
3290 GOTO 3200
3400 REM "End of Page
3420 CALL "ZZPROM",".5",X3$,Z,"End of Page","","",0; IF Z=1 THEN EXITTO 3650
3430 L=L2
3440 PRINT @(0,L),'CE',
3445 RETURN 
3450 REM "End of printed page - add cwj 6/22/98 ssp#091686
3460 LC0=11
3480 IF PRNT$="Y" THEN PRINT (W9)'FF'; GOSUB 7900; REM "ssp#091686
3490 RETURN 
3590 GOTO 3200
3600 REM "End of File
3620 CALL "ZZPROM",".4",X3$,0,"","","",0
3650 Z$="03C TMP"+FID(0)+"   "; GOSUB 9750
3670 IF W9<>0 THEN CLOSE (W9)
3690 GOTO 1000
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6050 GOSUB 7700
6060 PRINT @(5,6),"Display Salespersons (S) or Division (D) values:",@(11,7),"Display Orders Booked (B) or Invoiced (I):",@(5,8),"Eliminate display of lines with all zero values?",
6070 PRINT @(0,9),Z0$,@(22,10),"-----Month-to-Date------",@(52),"------Year-to-Date------",
6080 PRINT @(24,11),"Sales    Profit    GP%        Sales    Profit    GP%",
6090 PRINT @(0,12),Z0$,@(5,13),"ORDERS BOOKED:",@(25),"(1)",@(35),"(2)",@(43),"(3)",@(55),"(4)",@(65),"(5)",@(73),"(6)",
6095 PRINT @(0,14),Z0$,@(3,15),"INVOICED ORDERS:",@(25),"(7)",@(35),"(8)",@(43),"(9)",@(55),"(10)",@(65),"(11)",@(73),"(12)",@(0,16),Z0$,
6110 PRINT @(2,17),"The Ranking will display in Decending Order based upon the",@(2,18),"field (numbered 1-12 above) you select:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 PRINT @(55,6),A$(1,1),@(55,7),A$(2,1),@(55,8),A$(3,1),@(44,18),A[0]:"#0",
6390 RETURN 
6400 REM "WHOLE SCREEN
6402 IF C9=0 THEN IF ABS(C0)=17 THEN GOTO 6970
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6490 RETURN 
7000 REM "Setup Printer - ssp#091686 - create 7000-7099 to open printer
7010 IF W9<>0 THEN CLOSE (W9)
7020 CALL "ZZRPTO",X3$,X4$,Y0$,F0$,F{ALL},4,C0,X0
7030 IF F0$="" THEN F0$="I"+X3$(172,2)+"    NNN    66 ",F1$="LP" ELSE IF POS(F0$(2,1)="TA")<>0 THEN F1$="LP" ELSE F1$=F0$(2,6)
7050 CALL "ZZPRIN",X0$,X3$,Y0$,F0$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 7051,9920
7055 PRNT$="Y"
7060 ! CALL "ZZPRIN",X0$,X3$,Y0$,F0$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 7061,9920 Duplicate line 265536
7070 ! PRNT$="Y"  Duplicate line 265536
7090 GOTO 2500
7100 REM "If company codes do not match - ssp#091686
7110 PRINT @(0,3),'CE',@(15,3),"Current company does not match your data file!",
7120 INPUT (0,ERR=7100,TIM=30)*,
7190 GOTO 9900
7500 REM "Setup C values Compute percentages
7510 FOR I=0 TO 3
7520 IF C[I*2]<>0 THEN C[I+8]=C[I*2+1]*100/C[I*2]
7530 NEXT I
7540 RETURN 
7700 REM "Date upated data
7705 IF LEN(P$)<50 THEN GOTO 7740
7710 PRINT @(0,3),'CE',@(3,3),"Data for Fiscal Year: ",P$(1,4),"  Accounting Period: ",P$(5,2)," ending ",FND$(P$(7,6)),@(3,4),"Last updated: ",FND$(P$(13,6))," at ",P$(19,5)," by ",P$(24,3)," on terminal: ",P$(27,2),@(0,5),Z0$,
7740 RETURN 
7800 REM "Define Work File
7810 Y$="[192]E/I Salesperson/Divison Ranking",K1=0
7820 CALL "ZZ2SRT",X3$,X4$,Z[13],Z[1],13,Y$,K1,"NN"
7830 Z$="03O "+Y$; GOSUB 9750
7840 RETURN 
7900 REM "Do header on paper - ssp#091686
7910 PRINT (W9)'LF',@(2),"Date: ",FND$(X3$(21,6)),@(22),"Salesperson/Div Summary Ranking",@(65),'LF',@(0),ZL$
7920 PRINT (W9)@(3),"Data for Fiscal Year: ",P$(1,4),"  Account Period: ",P$(5,2)," ending ",FND$(P$(7,6)),'LF',@(3),"Last updated: ",FND$(P$(13,6))," at ",P$(19,5)," by ",P$(24,3)," on terminal: ",P$(27,2),'LF',@(0),ZL$
7990 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9120 ON Y8 GOTO 9900,0990,9920
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9740 RETURN 
9750 REM "FILES
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9790 RETURN 
9800 REM "EXIT PROGRAM
9820 CALL "ZZPROM",".4",X3$,0,"There is no data to report.  You must first run the update","","",0
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
