0010 REM "Correct Transaction Dates in Net Proceeds <CM2UT7>
0020 SETESC 9300; SETERR 9000
0035 REM "3.8 - 03/24/95 - 11.32
0040 REM "Copyright 1995 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="CM2UT7",X1$="Set Trans Date in August 1994 CM4"
0120 DIM Z0$(80,"-"),B0[9],C$(100)
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30],A[31],A[32],A[33],A[34],A[35],A[36],A[37],A[38],A[39],A[40]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01L CM4...  13O ZZPARM"
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0610 GOSUB 6000
0620 GOSUB 8100
0640 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0900 DIM A[40]
0905 C0=0
1000 REM 
1010 K$=KEY(Z[1],END=5000); READ (Z[1],KEY=K$,DOM=1010)IOL=0310; C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
1013 IF K$(1,3)=X3$(9,3) THEN GOTO 1010; REM "skip any for this company
1060 REM "Check date if not 8/22/94 then skip processing
1065 IF A$(16,6)<>"J40822" THEN GOTO 1010
1070 C0=C0+1
1100 REM "Process records to be changed
1110 IF A$(39,6)<>"J40822" THEN A$(16,6)=A$(39,6); GOTO 1900; REM "Easy case use the reference date
1200 REM "Both dates are 8/22/94, should be freight conversion (098 seq #)
1201 REM "We are NOT verifing seq # is 098, in all cases we will set date
1202 REM "If we find matching seq 000 record we'll use its transaction date, else use 7/22/94
1210 FIND (Z[1],KEY=A$(1,12)+"000",DOM=1211)A0$; A$(16,6)=A0$(16,6); GOTO 1220
1215 A$(16,6)="J40722"; REM "Missing record case
1900 REM "Write out record then continue
1950 WRITE (Z[1],KEY=K$)IOL=0310
1995 GOTO 1010
5000 REM "EOJ
5005 PRINT @(10,15),STR(C0:M0$)," records changed.",
5010 C=T; GOSUB 8150
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(10,4),"This program will scan the Net Proceeds file (CM4) for records with a transaction date of 8/22/94. These are conversion records. If the reference date is not 8/22/94 the transaction date will be set to the reference date. If the reference date is 8/22/94 then this should be a freight cost conversion record with a sequence # of 098. The date, in any case, for these records willbe set from the matching company,job number, sequence number =000 record if it exists or the date will be set to 7/22/94."
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F$
8115 PRINT @(10,12),"There are "+FNR$(STR(T:M0$))+" records to process.",
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,C
8195 RETURN 
8905 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
