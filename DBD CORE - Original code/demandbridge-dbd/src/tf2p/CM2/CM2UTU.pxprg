0010 ! Convert ProOffice Text Format to R/L PVX Format
0035 REM "5.1 - 04/04/03 - 12.7175 - dmm - SSP# 159455
0040 REM "Copyright 2003 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0095; ENTER X3$
0095 SETERR 9000
0110 DEF FNXMD$(Z9$)=CHR(NUM(Z9$(1,3))-125)+Z9$(4,1)+Z9$(5,2)+Z9$(7,2)
0125 TODAY$=DTE(0:"%Y%Mz%Dz%hz%mz") ! TODAY$=FN%CDS$
0130 DIR$=HWD+DLM+"comm"+DLM+"inbox"+DLM,WORK_DIR$=HWD+DLM+"comm"+DLM+"work"+DLM,PROC_DIR$=HWD+DLM+"comm"+DLM+"processed"+DLM
0600 F2$=FNXMD$(TODAY$(1,8))+TODAY$(9,4)+"pro2.0"
0610 OWNER$="149224270509"
0620 DIV$="76H7Q63M"
0900 PRINT @(10,15),'CL',"Convert ProOffice Text Files to R/L Files for TopForm"
1000 SELECT F$ FROM DIR$ WHERE MID(F$,1,3)="CM0"
1020 CLOSE (100); OPEN (100)DIR$+F$; FTYP$=FID(100); CLOSE (100)
1025 IF FTYP$(10,1)<>$01$ THEN GOTO 1050
1030 PRINT @(10,20),'CL',"Processing File: ",F$
1040 GOSUB 2000
1050 NEXT RECORD 
1090 GOTO 9900
2000 REM "Process TBasic File"
2002 CM2_CNT=0,AR2_CNT=0,AP2_CNT=0,AP4_CNT=0,ARP_CNT=0,ARQ_CNT=0
2005 P=POS(".txt"=F$); IF P>0 THEN WORK_F$=F$(1,P-1)
2007 IF WORK_F$(4,3)="434" THEN WORK_F$(4,3)="509"
2009 ERASE WORK_DIR$+WORK_F$,ERR=2010
2010 DIRECT WORK_DIR$+WORK_F$,60,0,1024,4,0
2020 FPRO=UNT; OPEN (FPRO,OPT="TEXT")DIR$+F$
2030 FWRK=UNT; OPEN (FWRK)WORK_DIR$+WORK_F$
2100 READ (FPRO,END=2160)PRO$
2110 IF PRO$(1,3)="CM2" THEN GOTO 2200
2115 IF PRO$(1,3)="ARD" THEN GOTO 2300
2120 IF PRO$(1,3)="ARC" THEN GOTO 2400
2125 IF PRO$(1,3)="ARI" THEN GOTO 2500
2130 IF PRO$(1,3)="ARQ" THEN GOTO 2600
2135 IF PRO$(1,3)="AR2" THEN GOTO 2700
2140 IF PRO$(1,3)="AP2" THEN GOTO 2800
2145 IF PRO$(1,3)="AP4" THEN GOTO 2900
2150 GOTO 2100
2160 REM "Write Audit Records / Rename Files"
2165 CNTTYPE$="CM2",WRKCNT=CM2_CNT; GOSUB 4000
2170 CNTTYPE$="ARP",WRKCNT=ARP_CNT; GOSUB 4000
2175 CNTTYPE$="AR2",WRKCNT=AR2_CNT; GOSUB 4000
2180 CNTTYPE$="AP2",WRKCNT=AP2_CNT; GOSUB 4000
2185 CNTTYPE$="AP4",WRKCNT=AP4_CNT; GOSUB 4000
2190 GOSUB 4500 ! Close and rename text and work files
2199 RETURN 
2200 REM "CM2 Records"
2205 DIM CM2[5]
2207 IF PRO$(4,3)="434" THEN PRO$(4,3)="509"
2210 F1$="CM2144  "+PRO$(4,27)
2212 SEQ=0
2215 CM2$=PRO$(4,36)+FNXMD$(PRO$(40,8))+PRO$(48,12)
2220 CM2[0]=NUM(PRO$(60,10)),CM2[1]=NUM(PRO$(70,10)),CM2[2]=NUM(PRO$(80,10)),CM2[3]=NUM(PRO$(90,10))
2230 SEQ$=STR(SEQ:"00"); WRITE (FWRK,KEY=F1$+SEQ$,DOM=2231)F1$+SEQ$,F2$,"","",CM2$,CM2{ALL},""; GOTO 2240
2232 SEQ=SEQ+1; GOTO 2230
2240 CM2_CNT=CM2_CNT+1
2250 GOTO 2100
2300 REM "ARP (Deposit Header) ARD Records"
2310 F1$="ARP144  "+PRO$(4,10)
2312 SEQ=0
2315 ARD$=PRO$(4,10)+FNXMD$(PRO$(14,8))+FNXMD$(PRO$(14,8))
2330 SEQ$=STR(SEQ:"00"); WRITE (FWRK,KEY=F1$+SEQ$,DOM=2331)F1$+SEQ$,F2$,"","",ARD$; GOTO 2340
2332 SEQ=SEQ+1; GOTO 2330
2340 ARP_CNT=ARP_CNT+1
2350 GOTO 2100
2400 REM "ARP (Check Header) ARC Records"
2405 DIM ARC[2]
2406 IF PRO$(14,3)="434" THEN PRO$(14,3)="509"
2407 PROCUST$=PRO$(14,9); GOSUB 5000; REM "Get TopForm Customer"
2410 F1$="ARP144  "+PRO$(4,10)+TFCUST$+PRO$(24,6)
2412 SEQ=0
2415 ARC$=PRO$(4,10)+TFCUST$+PRO$(24,6)+FNXMD$(PRO$(30,8))+PRO$(38,35)+PRO$(14,3)
2420 ARC[1]=NUM(PRO$(73,10))
2430 SEQ$=STR(SEQ:"00"); WRITE (FWRK,KEY=F1$+SEQ$,DOM=2431)F1$+SEQ$,F2$,"","",ARC$,ARC{ALL},""; GOTO 2440
2432 SEQ=SEQ+1; GOTO 2430
2440 ARP_CNT=ARP_CNT+1
2450 GOTO 2100
2500 REM "ARP (Invoice Information) ARI Records"
2505 DIM ARI[7]
2506 IF PRO$(14,3)="434" THEN PRO$(14,3)="509"
2507 PROCUST$=PRO$(14,9); GOSUB 5000; REM "Get TopForm Customer"
2510 F1$="ARP144  "+PRO$(4,10)+TFCUST$+PRO$(24,14)
2512 SEQ=0
2515 ARI$=PRO$(4,10)+TFCUST$+PRO$(24,14)
2520 ARI[1]=NUM(PRO$(38,10)),ARI[2]=NUM(PRO$(48,10)),ARI[3]=NUM(PRO$(58,10)),ARI[4]=NUM(PRO$(68,10)),ARI[5]=NUM(PRO$(78,10)),ARI[6]=NUM(PRO$(88,10))
2522 REM IF ARI[2]<>0 THEN ESCAPE ELSE LET ARI[6]=ARI[1]
2530 SEQ$=STR(SEQ:"00"); WRITE (FWRK,KEY=F1$+SEQ$,DOM=2531)F1$+SEQ$,F2$,"","",ARI$,ARI{ALL},""; ARP_CNT=ARP_CNT+1; GOTO 2540
2531 DIM ARIW[7]
2532 EXTRACT (FWRK,KEY=F1$+SEQ$)WK1$,WK2$,WK3$,WK4$,ARIW$,ARIW{ALL}
2533 FOR WIX=0 TO 7; ARIW[WIX]=ARIW[WIX]+ARI[WIX]; NEXT WIX
2534 WRITE (FWRK)WK1$,WK2$,WK3$,WK4$,ARIW$,ARIW{ALL}
2540 GOTO 2100
2700 REM "AR2 Records"
2705 DIM AR2[2]
2710 F1$="AR2     "+PRO$(4,2)
2712 SEQ=0
2715 AR2$=PRO$(4,54)
2720 AR2[0]=NUM(PRO$(58,6)),AR2[1]=NUM(PRO$(64,3)),AR2[2]=NUM(PRO$(67,3))
2730 SEQ$=STR(SEQ:"00"); WRITE (FWRK,KEY=F1$+SEQ$,DOM=2731)F1$+SEQ$,F2$,"","",AR2$,AR2{ALL},""; GOTO 2740
2732 SEQ=SEQ+1; GOTO 2730
2740 AR2_CNT=AR2_CNT+1
2750 GOTO 2100
2800 REM "AP2 Records"
2805 DIM AP2[0]
2810 F1$="AP2     "+PRO$(4,2)
2812 SEQ=0
2815 AP2$=PRO$(4,53)
2820 AP2[0]=NUM(PRO$(57,6))
2830 SEQ$=STR(SEQ:"00"); WRITE (FWRK,KEY=F1$+SEQ$,DOM=2831)F1$+SEQ$,F2$,"","",AP2$,AP2{ALL},""; GOTO 2840
2832 SEQ=SEQ+1; GOTO 2830
2840 AP2_CNT=AP2_CNT+1
2850 GOTO 2100
2900 REM "AP4 Records"
2902 IF LEN(PRO$)<346 THEN PRO$=PRO$+" "; GOTO 2902
2905 DIM AP4[1]
2910 F1$="AP4     "+PRO$(4,10)
2912 SEQ=0
2913 IF PRO$(213,8)="        " THEN PLPSTDT$="      " ELSE PLPSTDT$=FNXMD$(PRO$(213,8))
2914 IF PRO$(305,8)="        " THEN DTADDED$="      " ELSE DTADDED$=FNXMD$(PRO$(305,8))
2915 IF PRO$(313,8)="        " THEN DTMOD$="      " ELSE DTMOD$=FNXMD$(PRO$(313,8))
2918 AP4$=PRO$(4,209)+PLPSTDT$+PRO$(221,84)+DTADDED$+DTMOD$+PRO$(321,13)
2920 AP4[0]=NUM(PRO$(334,10),ERR=*NEXT)
2925 AP4[1]=NUM(PRO$(344,3),ERR=*NEXT)
2930 SEQ$=STR(SEQ:"00"); WRITE (FWRK,KEY=F1$+SEQ$,DOM=2931)F1$+SEQ$,F2$,"","",AP4$,AP4{ALL},""; GOTO 2940
2932 SEQ=SEQ+1; GOTO 2930
2940 AP4_CNT=AP4_CNT+1
2950 GOTO 2100
4000 REM "Write Audit Records"
4010 IF WRKCNT=0 THEN GOTO 4099
4020 AREC$=CNTTYPE$+$FFFFFFFFFFFF$+"AUDIT"+STR(WRKCNT:"00000")
4030 WRITE (FWRK,KEY=AREC$(1,14))AREC$
4099 RETURN 
4500 ! Close and rename text and work files
4510 CLOSE (FPRO); CLOSE (FWRK)
4520 RENAME DIR$+F$,PROC_DIR$+F$,ERR=*NEXT; GOTO 4525
4521 ESCAPE 
4530 RENAME WORK_DIR$+WORK_F$,DIR$+WORK_F$,ERR=*NEXT; GOTO 4535
4531 ESCAPE 
4590 RETURN 
5000 REM "Get TopForm Customer"
5010 P=(POS(PROCUST$(1,3)=OWNER$,3)-1)/3*2+1
5020 TFCUST$=DIV$(P,2)+PROCUST$(4,6)+"00"
5099 RETURN 
9000 REM "ERROR PROCESSING
9005 ! IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9900 ! End Program
9910 ! CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 EXIT 
9999 END 
