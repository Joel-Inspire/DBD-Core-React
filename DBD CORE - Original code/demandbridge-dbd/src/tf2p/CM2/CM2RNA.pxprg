0010 REM <CM2RNA> "Remove Net Proceeds
0035 REM "3.9 - 06/27/96 - 11.27 - dmm
0040 REM "Copyright 1996 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="CM2RNA",X1$="Remove Net Proceeds"
0120 DIM A$(186),A[40],A0$(186),A0[40],D1$(6)
0140 Z0$="##,###.00-",Z1$="##,##0"
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0175 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0210 T=1,T0=1,T1=3
0220 W3=71
0225 W=999
0227 F1=0,T9=0
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "I/O lists
0305 REM "IOLIST FOR CM4
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30],A[31],A[32],A[33],A[34],A[35]
0315 REM "IOLIST FOR CM4 ALSO
0320 IOLIST A0$,A0[0],A0[1],A0[2],A0[3],A0[4],A0[5],A0[6],A0[7],A0[8],A0[9],A0[10],A0[11],A0[12],A0[13],A0[14],A0[15],A0[16],A0[17],A0[18],A0[19],A0[20],A0[21],A0[22],A0[23],A0[24],A0[25],A0[26],A0[27],A0[28],A0[29],A0[30],A0[31],A0[32],A0[33],A0[34],A0[35],A0[36],A0[37],A0[38],A0[39],A0[40]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$,V0[0]
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O CM4...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0700 REM "Open Printer
0720 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0721,9920
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[1]; U=3,U0=1,U[0]=0,U[1]=3,T0=1,V1=3,U0$="    ",T5$="S               ",U5$="Source Company"; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0984 DIM S5$(12); S5$(1,3)=V4$(1,3)
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1002 R0=0
1004 READ (U1,KEY=S5$+$FFFFFF$,DOM=1005)
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])>=V2$(U3,U[U9]) THEN GOTO 1030 ELSE EXTRACT (U1,KEY=U$(1,U2-1)+V2$(U3,U[U9]),DOM=1021)
1025 EXITTO 1000
1030 IF U$(U2,U[U9])<=V2$(U3+U[U9],U[U9]) THEN GOTO 1040 ELSE IF U9=1 THEN EXITTO 5000 ELSE READ (U1,KEY=U$(1,U2-1)+$FF$,DOM=1032)
1035 EXITTO 1005
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1130 READ (Z[1],KEY=U$(5,10),DOM=1000)IOL=0310
1135 READ (U1)
1140 GOTO 1160
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1155 S5$=A$(1,12); REM "Used to make sure file is in correct position for next job, see line 1004
1160 READ (Z[1],KEY=S5$+"999",DOM=1000)IOL=0320; REM "Find the 999 record for this job if there is one
1165 IF A0$(16,6)>V0$(72,6) THEN GOTO 1000; REM "Compare activity recorded date to report selection parameter date
1170 IF A0$(39,6)>V0$(72,6) THEN GOTO 1000; REM "Compare activity date to report selection parameter date
1190 D1$(1,6)=A0$(16,6); REM "We will check this 999 date against the others and print the latest, see line 1230
1195 DIM Q[40]; REM "This array is used to build the data needed to calculate the advance balance for each job
1200 READ (U1,KEY=S5$,DOM=1201); REM "Position the file to the first record for this job
1210 READ (U1,END=1300)IOL=0320
1220 IF A0$(1,12)<>S5$(1,12) THEN GOTO 1300; REM "Are we done with this job?
1230 IF A0$(39,6)>D1$(1,6) THEN D1$(1,6)=A0$(39,6)
1231 IF A0$(16,6)>D1$(1,6) THEN D1$(1,6)=A0$(16,6)
1235 IF A0$(39,6)>V0$(72,6) THEN GOTO 1000; REM "Compare date for each record within this job to report selection parameter date
1240 FOR X=0 TO 35; Q[X]=Q[X]+A0[X]; NEXT X; REM "Building array for advance account balance for this job
1245 IF A0$(13,3)="000" THEN FOR X=36 TO 40; Q[X]=Q[X]+A0[X]; NEXT X
1247 R0=R0+1
1250 GOTO 1210
1300 REM "Calculate advance balance for this job
1305 IF Q[5]<>0 THEN IF Q[11]<>0 THEN Q[11]=Q[11]+Q[5]
1310 Q0=0,Q1=0,Q2=0
1315 IF Q[0]+Q[2]+Q[4]<>0 THEN IF Q[0]<>0 THEN Q0=(Q[6]+Q[7]+Q[8])*100/Q[0]
1320 Q1=Q[0]-Q[4]-Q[2],Q2=Q0*Q1/100
1325 Q[15]=Q[36]*Q1/100
1330 Q[21]=Q[37]*Q1/100*Q0/100-Q[6]
1335 Q[22]=(-Q[36])*(Q[7]+Q[8])/100
1340 Q[24]=Q[9]
1345 Q[20]=-Q[5]
1350 Q[26]=Q[11]
1355 Q[30]=Q[15]+Q[21]+Q[24]+Q[20]+Q[26]+Q[22]
1360 IF ABS(Q[30])>V0[0] THEN GOTO 1000
1380 IF F1=1 THEN GOSUB 7700; GOTO 1000; REM "F1=1 means we are removing records
1400 REM "Subtotals"
1415 GOSUB 3100
1490 T=0
1495 GOSUB 1500
1497 GOTO 1000
1500 REM "Print Data
1510 IF W+5>W0 THEN GOSUB 6000
1520 Y5$(3)=A$(1,3); REM "Source Company
1525 Y5$(14)=A$(4,2)+"-"+A$(6,6); REM "Job Number
1535 Y5$(30)=FND$(D1$(1,6)); REM "Activity Date
1540 Y5$(48)=STR(Q[30]:Z0$); REM "Advance Balance
1545 Y5$(62)=STR(R0:Z1$); REM "Number of records for this job
1690 GOSUB 7300
1750 GOSUB 7300
1900 REM "Accumulate Totals"
1990 T[T0,0]=T[T0,0]+1; T[T0,1]=T[T0,1]+Q[30]; T[T0,2]=T[T0,2]+R0
1995 RETURN 
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; GOSUB 7000
3156 IF U0$(1,1)="Y" THEN GOSUB 6000
3160 U1$=U9$(1,V1)
3165 IF T0$<>"END" THEN GOSUB 7600
3180 RETURN 
3280 RETURN 
3380 RETURN 
3480 RETURN 
5000 REM "End of Print
5010 IF F1=1 THEN R9=C; CALL "ZZBARG",X3$,"HG",19,10,50,T9,C,R9; GOTO 9920; REM "We're done removing records, go to the end
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; GOSUB 7000
5200 REM "
5290 GOTO 9900
6000 REM "Page header
6035 IF T0$="END" THEN W0$=""
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6060 ELSE ON W5 GOTO 6041,9910
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6090 RETURN 
6100 REM "
6110 Y5$(1)=T1$; GOSUB 7300
6115 Y5$(1)="Source",Y5$(18)="Job",Y5$(31)="Activity",Y5$(46)="Advance",Y5$(63)="Number"; GOSUB 7300
6120 Y5$(1)="Company",Y5$(16)="Number",Y5$(33)="Date",Y5$(46)="Balance",Y5$(61)="of Records"; GOSUB 7300
6125 Y5$(1)=T1$; GOSUB 7300
6195 IF FID(W9)=FID(0) THEN PRINT 'SF',
6199 RETURN 
7000 REM "Totals logic
7001 IF T=T0 THEN IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(48)=T1$(1,LEN(Z0$)),Y5$(62)=T1$(1,LEN(Z1$)); GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" Total ("+STR(T[T,0]:"0")+"):"
7020 Y5$(48)=STR(T[T,1]:Z0$),Y5$(62)=STR(T[T,2]:Z1$)
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300; GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed: "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 IF INT(T2)>0 THEN T4$=STR(INT(T2))+" Hours " ELSE T4$=""
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+STR(T3:"##.##")+" Minutes"
7260 Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7330 REM 
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7445 DIM V0[1]
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"  ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
7600 REM "Print next source company name
7610 READ (Z[13],KEY="CMP"+A$(1,3),DOM=7640)S1$
7615 IF W+5>W0 THEN GOSUB 6000
7620 Y5$(1)=S1$(7,38); GOSUB 7300; GOSUB 7300
7640 RETURN 
7700 REM "Remove records
7705 C=T[0,2]
7710 READ (U1,KEY=A$(1,12),DOM=7711)
7720 K$=KEY(U1,END=7750)
7730 IF K$(1,12)<>A$(1,12) THEN GOTO 7750
7740 REMOVE (U1,KEY=K$,DOM=7741); R9=R9+1
7745 GOTO 7720
7750 CALL "ZZBARG",X3$,"HG",19,10,50,T9,C,R9
7795 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9915 IF F1=1 THEN GOTO 9920; REM "We're done removing records, don't ask again
9916 IF V3$(2,1)<>"T" THEN CALL "ZZPROM",".9",X3$,Z,"Do you wish to remove these records?","","",0; IF Z=1 THEN GOTO 9920
9917 IF V3$(2,1)<>"T" THEN F1=1,R9=0; GOTO 0900
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 RUN "ZMENU"
9999 END 
