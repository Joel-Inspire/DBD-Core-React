0010 REM "Update ML payment data at RL <CM2UAB>
0035 REM "3.9 - 10/24/95 - 17.82
0040 REM "Copyright 1995 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0100 BEGIN 
0110 X0$="CM2UAB",X1$="Process ML Check Data"
0120 DIM B$(58),S$(40),B[2]
0140 GOSUB 8950
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0310 IOLIST A$(1),A[0],A[1],A[2]
0320 IOLIST B$,B[0],B[1],B[2]
0325 IOLIST B0$,B0[0],B0[1],B0[2]
0330 IOLIST C$,C[0],C[1],C[2]
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01L CM2... 02L APC...  03O APH... 13O ZZPARM "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; IF Z0=1 THEN GOTO 9900
0580 READ (Z[2],END=0600); RUN "AP2CHS"
0585 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,0,"The check print file (APC) must be empty to proceed","","",0; GOTO 9900
0600 REM 
0620 Y4$="AP2CHS"; READ (Z[1],END=9900)
0640 GOSUB 6000
0990 READ (Z[1],KEY="",DOM=0991)
1000 REM "Get next record from file
1005 DIM A[2],A$(42)
1010 K2$=KEY(Z[1],END=5000)
1015 K3$=K2$
1020 READ (Z[1],KEY=K2$)IOL=0310
1023 REM "For remote locations, they will never get the discount taken by
1024 REM "the master locvation
1025 A[1]=0
1030 IF A$(1,17)+A$(28,6)<>B$(1,23) THEN GOSUB 2000
1040 SETERR 9000
1090 PRINT @(12,10),"Processing: ",K2$
1100 REM 
1110 IF D0$="" THEN D0$=A$(34,6); GOSUB 7700
1200 REM "
1220 WRITE (Z[3],KEY=K2$)IOL=0310
1240 B[0]=B[0]+A[0],B[1]=B[1]+A[1]
1890 REMOVE (Z[1],KEY=K3$)
1950 GOTO 1000
2000 REM "Check change
2005 IF A$(1,17)=B$(1,17) THEN A$(17,1)=CHR(ASC(A$(17,1))+1),K2$(17,1)=A$(17,1); REM "If 1st 17 match, then dup vendor,invoice etc. So update the special check code in both the record and the key, the new B$ will also have the new check code
2010 IF B$(1,23)=S$(1,23) THEN GOTO 2200
2015 B[2]=B[0]-B[1]
2020 WRITE (Z[2],KEY=B$(1,23),DOM=2021)IOL=0320; GOTO 2090
2030 REM "Duplicate found, read the old in to B0$ & B0() then add in current & write back out
2035 DIM B0$(LEN(B$)),B0[3]
2040 READ (Z[2],KEY=B$(1,23),DOM=2020)IOL=0325
2045 FOR I0=0 TO 2; B0[I0]=B0[I0]+B[I0]; NEXT I0
2050 WRITE (Z[2],KEY=B$(1,23))IOL=0325
2200 REM "NEW CHECK
2220 DIM B$(58),B[2]
2230 B$(1,23)=A$(1,23)
2235 B$(18,6)=A$(28,6); IF LEN(A$)>=42 THEN B$(24,6)=A$(37,6)
2240 READ (Z[2],KEY=B$(1,23),DOM=2241)IOL=0320
2290 RETURN 
2950 GOTO 1000
5000 REM "
5010 IF B$(1,23)<>S$(1,23) THEN GOSUB 2000
5020 C1$=X3$(9,3)+"APC"+X3$(174,4); DIM C0$(80); C0$(1)=D0$(7)
5040 WRITE (Z[13],KEY=C1$)C0$
5050 CALL "ZZINIT",STR(Z[1]:"00")
5090 GOTO 9900
6000 REM 
6020 PRINT @(0,3),'CE',"This program will process A/P check data from the master location and update the appropriate data files here.  The check updates will then be performed."
6090 RETURN 
7000 REM "Change file being updated
7020 IF F0$="" THEN GOTO 7050
7030 Z$="02C "+F0$(1,3)+"... "
7031 IF F9$>"" THEN FOR X=1 TO LEN(F9$) STEP 11; F9$(X+2,2)="CU"; NEXT X; Z$=Z$+F9$,F9$=""
7040 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; IF Z0=1 THEN GOTO 9900
7050 REM "Open file
7055 F0$=K0$
7060 Z$="02O "+K2$(1,3)+"... "
7061 IF F0$(1,3)="AP4" THEN F9$="03O AP6... 04O AP7... 05O AP1... "; Z$=Z$+F9$
7070 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; IF Z0=1 THEN GOTO 9900
7090 RETURN 
7700 REM "Default accounting period based on date
7705 D0$=D0$(1,6)+"      "
7706 D0$(1,6)=X3$(21,6)
7710 D1$=X3$(9,3)+"G/LYE",P1$=""
7715 READ (Z[13],KEY=D1$,DOM=7716)
7720 READ (Z[13],END=7770)P1$
7725 IF P1$(1,8)<>D1$(1,8) THEN P1$=""; GOTO 7770
7730 IF P1$(87,6)<D0$(1,6) THEN GOTO 7720
7740 FOR I=1 TO NUM(P1$(13,2))
7745 IF D0$(1,6)<=P1$(15+I*6,6) THEN D0$(7,6)=P1$(9,4)+STR(I:"00"); EXITTO 7755
7750 NEXT I
7770 IF Q1$="" THEN IF P1$="" THEN CALL "ZZPROM",".4",X3$,0,"Unable to determine G/L date.","","",0; GOTO 7700
7795 RETURN 
8910 DEF FNZ$(Z9$)=Z9$(1,POS(Z0$=Z9$+Z0$)-1)
8950 REM "Program flow logic
8960 DIM U0$(6); U0$(1)=FID(0); U0$=U0$+"PF"
8970 CLOSE (14); OPEN (14)"ZZPARM"; FIND (14,KEY=U0$,DOM=8990)U0$,U1$
8980 Q1=NUM(U1$(7,2)),Q1$=U1$(9,Q1),U1$=U1$(Q1+9)
8990 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9705 IF Y4$<>"" THEN DIM U2$(6); U2$(1)=Y4$,U1$=U2$+STR(LEN(Q1$):"00")+Q1$+U1$; REM "If program to run is set, then add it to be run next
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9730 RUN U1$(1,6)
9800 REM 
9810 Q$="+ ("+F0$+")"
9820 CALL "ZZPROM",".4",X3$,0,"Problem accessing files!"+Q$,"","",0
9845 GOTO 9900
9850 REM "Write message file, skip record
9890 GOTO 1500
9900 REM "END PROGRAM
9905 IF Q1$<>"" THEN GOTO 9700
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940
9935 IF Y4$<>"" THEN RUN Y4$
9940 RUN "ZMENU"
9999 END 
