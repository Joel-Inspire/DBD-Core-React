0010 REM "PFG Sales Report <CM2SCA>
0015 REM "Prog Type: O -370032  
0020 SETESC 9300; SETERR 9000
0035 REM "3.9 - 06/26/96 - 10.86 - dmm
0040 REM "Copyright 1996 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0110 X0$="CM2SCA",X1$="Sales Report"
0120 DIM A$(147),A[9],C$(309)
0140 Z0$="###,###.00-",Z1$="###.0%"
0210 T=1,T0=1,T1=1
0220 W3=224
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 REM CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR CMC - Sales Report Data (incl GST/PST)
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9]
0311 IOLIST K$,T[T,0],T[T,1],T[T,2],T[T,3],T[T,4],T[T,5],T[T,6],T[T,7],T[T,8],T[T,9],T[T,10],T[T,11],T[T,12]
0312 IOLIST A0$,A0[0],A0[1],A0[2],A0[3],A0[4],A0[5],A0[6],A0[7],A0[8],A0[9],A0[10],A0[11],A0[12]
0330 IOLIST C$
0420 IOLIST X3$,X4$,V1$,V3$,V2$,IOL=0425
0425 IOLIST V$,V[0],V[1]
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01X CMC...  03O AP4...  "; IF V1$(1,3)<>Y$(5,3) THEN Y$=Y$+"02X "+V1$+"  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0550 IF X3$(9,3)="244" THEN F0=2 ELSE F0=1
0559 REM "Set title from parameter record if it is available
0560 READ (Z[13],KEY=X3$(9,3)+"CMC",DOM=0566)X$
0563 CALL "ZZDISP","DX",X$(7,6),"",X3$,J0$,"",0,0,X4$
0564 CALL "ZZDISP","DX",X$(13,6),"",X3$,J1$,"",0,0,X4$
0565 X1$=X1$+"  -  "+J0$+" Through "+J1$
0590 REM "Set Offset constants O1 and O2
0595 O1=121,O2=147
0800 REM "Alternate Sort Info & total dim"
0810 DIM U[10,2],U0$(10),Y[5,1]
0820 REM CALL "ZZRSEQ",X3$,X0$,V1$,U,U0,U1,U(ALL),Y(ALL),T0,V0,U0$,T5$,U5$,S9$,X9; ON X9 GOTO 00821,09920
0825 U0=3; DIM U[U0,1],U5$((U0+1)*20),Y[U0]; U=32,U1=2,T0=3,T1=12,V0=1,U0$="2332       ",T5$="    S   S   AA/R",U5$(1,20)="REPORT",U5$(21,20)="Franchise",U5$(41,20)="Salesperson",U5$(61,20)="Customer No.",S9$="X",Y[1]=1,Y[2]=4,Y[3]=8,U[0,0]=1,U[0,1]=0,U[1,0]=1,U[1,1]=3,U[2,0]=4,U[2,1]=4,U[3,0]=8,U[3,1]=10
0850 DIM T[T0,T1]; U5$(1,20)=Q4$
0890 REM "Open Printer
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0896 W9$=""
0897 IF V3$(2,1)="T" THEN W0=14
0900 REM " Position read"
0910 IF V$(59,1)="S" THEN L0=1; REM "Turn off printing while doing summary
0950 REM " Position read"
0970 DIM V4$(U); U9=1; FOR X=1 TO U0; V4$(U[X,0],U[X,1])=V2$(U9,U[X,1]); U9=U9+2*U[X,1]; NEXT X
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record and check range
1005 U$=KEY(U1,END=5000)
1010 FOR X=1 TO U0; U2=U[X,0],U3=U[X,0]*2-1,U4=U[X,1]
1015 IF X=3 THEN U2=8,U3=15,U4=35
1020 IF U$(U2,U4)<V2$(U3,U4) THEN READ (U1); EXITTO 1000
1030 IF U$(U2,U4)>V2$(U3+U4,U4) THEN IF X=1 THEN EXITTO 5000 ELSE READ (U1); EXITTO 1000
1040 NEXT X
1100 REM "Get record"
1105 IF U$(4,1)=$00$ THEN GOTO 1000; REM "Skip total records
1110 U9$=U$; IF U1=Z[1] THEN GOTO 1150 ELSE U$="",X=0
1120 IF Y[X,0]>0 THEN U$=U$+U9$(Y[X,0],Y[X,1]),X=X+1; GOTO 1120
1130 READ (U1)
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1155 U9$=U$(1,7)+A$(63,10); REM "Adjust key
1200 REM "Reporting criteria
1400 REM "Subtotals"
1410 IF U2$="" THEN GOTO 1430 ELSE FOR T=1 TO U0; IF U9$(U[T,0],U[T,1])<>U2$(U[T,0],U[T,1]) THEN EXITTO 1420 ELSE NEXT T; GOTO 1480
1425 T9=T; GOSUB 3100; T=T9
1430 ON T GOTO 1490,1435,1440,1445
1435 GOSUB 6200; GOTO 1490
1440 GOSUB 6300; GOTO 1490
1445 GOSUB 6400
1490 T=0,U2$=U9$,U3$=A$(63,10); REM "Substitute customer number for customer name
1510 IF W+2>W0 THEN GOSUB 6000
1515 IF POS(" "<>A$(123,15))<>0 THEN Y5$(1)=A$(123,15) ELSE CALL "ZZDISP","AXS",A$(43,9),"P/O",X3$,Y5$,"",1,0,X4$
1520 Y5$(13)=A$(52,8)
1525 CALL "ZZDISP","DX",A$(138,6),"",X3$,X$,"",0,0,X4$; Y5$(22)=X$(1,6)+X$(9)
1530 Y5$(31)=A$(83,25),Y5$(57)=STR(A[0]:Z0$)
1535 Y5$(70)=STR(A[1]:Z0$),Y5$(84)=STR(A[0]-A[1]:Z0$); IF A[0]<>0 THEN Y5$(98)=STR((A[0]-A[1])*100/A[0]:Z1$); REM "Total Cost, GP, GP%
1550 REM "Different from here depending on whether its 144 or 244, Flag F0 is 1 if its 144 (or 500) and 2 if its 244
1555 IF F0=1 THEN Y5$(108)=STR(A[2]:Z0$),O=O1 ELSE O=O2,Y5$(108)=STR(A[3]:Z0$),Y5$(121)=STR(A[4]:Z0$),Y5$(134)=STR(A[5]:Z0$); REM "Sales tax for F0=1, or GST, PST and GST cost for F0=2
1560 Y5$(O)=STR(A[7]:Z0$),Y5$(O+13)=STR(A[6]:Z0$); REM "Frt Sale,Frt Cost
1565 N0=A[0]-A[6],N1=N0*A[8]/100,N2=N0*A[9]/100,N3=A[0]-A[1]-A[5]-N1-N2; REM "Calculate Net sale (N0), Service Fee(N1), Ad Fund (N2), Net Proceeds (N3)
1570 Y5$(O+26)=STR(N0:Z0$),Y5$(O+39)=STR(N1:Z0$); REM "Net Sale Service Fee
1575 Y5$(O+52)=STR(N2:Z0$),Y5$(O+65)=STR(N3:Z0$); REM "Ad Fund, Net Proceeds
1595 GOSUB 7300
1600 REM "Second line
1605 IF POS(" "<>A$(150,10))=0 THEN GOTO 1620
1610 DIM C$(LEN(C$)); FIND (Z[3],KEY=A$(150,10),DOM=1620)IOL=0330
1615 Y5$(31)=C$(11,35); GOSUB 7300
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+A[0],T[T0,2]=T[T0,2]+A[1],T[T0,3]=T[T0,3]+A[2],T[T0,4]=T[T0,4]+A[3],T[T0,5]=T[T0,5]+A[4],T[T0,6]=T[T0,6]+A[5],T[T0,7]=T[T0,7]+A[6],T[T0,8]=T[T0,8]+A[7]
1920 T[T0,9]=T[T0,9]+N0,T[T0,10]=T[T0,10]+N1,T[T0,11]=T[T0,11]+N2,T[T0,12]=T[T0,12]+N3
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+": "+T6$
3090 RETURN 
3100 REM "Subtotal Logic
3110 FOR T=U0 TO T STEP -1
3130 T$=FNS$(U5$(T*20+1,20)),T6$=U2$(U[T,0],U[T,1]),T7$=T5$(T*4+1,4); GOSUB 3050; IF POS(U0$(T+1,1)="23")>0 THEN GOSUB 7000 ELSE GOSUB 7100
3150 IF POS(U0$(T+1,1)="13")>0 THEN IF T=2 THEN GOSUB 6000 ELSE F8$="Y"; GOSUB 6000; F8$="N"
3180 NEXT T
3195 RETURN 
3280 RETURN 
3380 RETURN 
3480 RETURN 
5000 REM "End of Print
5020 IF L0=0 AND W8=0 THEN GOTO 5290
5030 T0$="END"
5050 T=0; GOSUB 3100
5100 REM "Do Recap
5105 IF S0=1 THEN GOTO 5120; REM "Recap already done so continue on
5110 GOSUB 7500
5200 REM "
5295 GOTO 9900
6000 REM "Page header
6005 IF L0=1 THEN GOTO 6090
6030 IF S0=1 THEN W1$="Franchise/Salesperson Recap" ELSE IF F8$="Y" OR U2$="" THEN W1$="Franchise: "+A$(1,3)+"    Salesperson: "+A$(4,4) ELSE W1$="Franchise: "+U2$(1,3)+"    Salesperson: "+U2$(4,4)
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; ON W5 GOTO 6041,9910
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6110 IF FID(W9)=FID(0) THEN PRINT (0,ERR=6111)'SB',
6115 Y5$(1)="Job No.",Y5$(13)="Inv No.  Inv Date  Item Description",Y5$(60)="Sale Amt",Y5$(71)="Total Cost",Y5$(84)="Gross Prof",Y5$(99)="GP %"
6117 IF F0=1 THEN Y5$(109)="Sales Tax",O=O1 ELSE Y5$(112)="GST",Y5$(125)="PST",Y5$(136)="GST Cost",O=O2
6119 Y5$(O+2)="FRT Sale",Y5$(O+15)="FRT Cost",Y5$(O+28)="Net Sale",Y5$(O+38)="Service Fee",Y5$(O+53)="Ad Fund",Y5$(O+64)="Net Proceeds"
6120 GOSUB 7300
6125 Y5$(1)="=========== =======  ======== ========================  ===========  ===========  ===========  =========  ==========="
6126 IF F0=2 THEN Y5$(121)=T2$(1,LEN(Z0$)),Y5$(134)=T2$(1,LEN(Z0$))
6127 Y5$(O-1)=T2$(1,LEN(Z0$)),Y5$(O+12)=T2$(1,LEN(Z0$)),Y5$(O+25)=T2$(1,LEN(Z0$))
6128 Y5$(O+38)=T2$(1,LEN(Z0$)),Y5$(O+51)=T2$(1,LEN(Z0$)),Y5$(O+64)=T2$(1,LEN(Z0$))
6130 GOSUB 7300
6195 IF FID(W9)=FID(0) THEN PRINT 'SF',
6199 RETURN 
6210 IF W+2>W0 THEN GOSUB 6000
6215 Y5$(1)="Franchise:",Y5$(22)=A$(1,3)
6220 GOSUB 7300
6245 RETURN 
6310 IF W+2>W0 THEN GOSUB 6000
6315 Y5$(1)="Salesperson:",Y5$(22)=A$(4,4)
6320 GOSUB 7300
6345 RETURN 
6410 IF W+3>W0 THEN GOSUB 6000
6415 GOSUB 7300
6417 IF POS(" "<>A$(63,10))=0 THEN Y5$(1)="Cost Only Entries"; GOTO 6435; REM "if Ccustomer number is blank, no sales record was found for these entries
6420 Y5$(1)="Customer:"
6425 CALL "ZZDISP","AXS",A$(63,10),"A/R",X3$,Y5$,"",11,0,X4$
6430 Y5$(22)=A$(8,35)
6435 GOSUB 7300
6445 RETURN 
6900 REM "SORT
6955 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6970 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,S9; ON S9 GOTO 6971,9920
6990 RETURN 
7000 REM "Totals logic
7001 IF T[T,0]<2 AND T<>3 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7003 IF T=3 THEN IF POS(" "<>T$(14))=0 THEN T$="Cost Only Entries:"; REM "Trap for customerless entries
7009 IF T>=3 THEN T8$=T1$ ELSE T8$=T2$; GOSUB 7300
7010 Y5$(57)=T8$(1,LEN(Z0$)),Y5$(70)=T8$(1,LEN(Z0$)),Y5$(84)=T8$(1,LEN(Z0$)),Y5$(98)=T8$(1,LEN(Z1$))
7011 IF F0=1 THEN Y5$(108)=T8$(1,LEN(Z0$)) ELSE Y5$(108)=T8$(1,LEN(Z0$)),Y5$(121)=T8$(1,LEN(Z0$)),Y5$(134)=T8$(1,LEN(Z0$))
7012 Y5$(O)=T8$(1,LEN(Z0$)),Y5$(O+13)=T8$(1,LEN(Z0$)),Y5$(O+26)=T8$(1,LEN(Z0$)),Y5$(O+39)=T8$(1,LEN(Z0$)),Y5$(O+52)=T8$(1,LEN(Z0$)),Y5$(O+65)=T8$(1,LEN(Z0$))
7013 GOSUB 7300
7015 IF T$<>"" THEN Y5$(1)=Q2$+" "+FNS$(T$)+" ("+STR(T[T,0]:"0")+")"
7020 Y5$(57)=STR(T[T,1]:Z0$)
7025 Y5$(70)=STR(T[T,2]:Z0$),Y5$(84)=STR(T[T,1]-T[T,2]:Z0$); IF T[T,1]<>0 THEN Y5$(98)=STR((T[T,1]-T[T,2])*100/T[T,1]:Z1$); REM "Total Cost, GP, GP%
7030 IF F0=1 THEN Y5$(108)=STR(T[T,3]:Z0$) ELSE Y5$(108)=STR(T[T,4]:Z0$),Y5$(121)=STR(T[T,5]:Z0$),Y5$(134)=STR(T[T,6]:Z0$); REM "Sales Tax
7035 Y5$(O)=STR(T[T,8]:Z0$),Y5$(O+13)=STR(T[T,7]:Z0$); REM "Frt Sale, Frt Cost
7040 Y5$(O+26)=STR(T[T,9]:Z0$),Y5$(O+39)=STR(T[T,10]:Z0$); REM "Net Sale, Service Fee
7045 Y5$(O+52)=STR(T[T,11]:Z0$),Y5$(O+65)=STR(T[T,12]:Z0$); REM "Ad Fund, Net Proceeds
7065 IF T$="END" THEN Y5$(57)=T2$(1,11); GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7100
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7115 IF T=1 THEN K$=U2$(1,3)+$00$+"zzzz" ELSE IF T=2 THEN K$=U2$(1,3)+$00$+U2$(4,4) ELSE IF T=0 THEN K$=$FFFFFF$+$00$+$FFFFFFFF$ ELSE GOTO 7120
7116 WRITE (U1,KEY=K$)IOL=0311; READ (U1,KEY=U$,DOM=7117); REM "Write out totals record, then reposition the file.
7119 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of records: "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")
7260 Y5$(1)="Elapsed time"+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 REM IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 07390
7315 IF L0=1 THEN Y5$(1)=""; GOTO 7395
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     13O ZZPARM  "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7435 DIM V[1]
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V$(71,1)),W3$=V$(19,POS("   "=V$(19,40)+"  ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "Print Recap
7505 L0=0,S0=1; DIM K0$(8); REM "Make sure L0 is 0 to start printing now
7510 GOSUB 6000
7515 READ (U1,KEY="",DOM=7516)
7520 K$=KEY(U1,END=5120); IF K$(4,1)<>$00$ THEN READ (U1,KEY=K$(1,3)+$FF$,DOM=7520); GOTO 7520
7523 IF K$(1,3)=$FFFFFF$ THEN GOSUB 7300; Y5$(1)="Report Summary Total:"; READ (U1,KEY=K$)IOL=0312; GOTO 7560 ELSE IF K$(1,3)<V2$(1,3) OR K$(1,3)>V2$(4,3) THEN READ (U1,KEY=K$(1,3)+$FF$,DOM=7520); GOTO 7520
7525 DIM A0$(8),A0[12]; READ (U1,KEY=K$,DOM=7520)IOL=0312
7530 IF W+2>W0 THEN GOSUB 6000
7550 IF A0$(1,3)<>K0$(1,3) THEN GOSUB 7300; Y5$(1)="Franchise:  "+A0$(1,3); GOSUB 7300; K0$(1,3)=A0$(1,3)
7555 IF K$(5,1)="z" THEN GOSUB 7300; Y5$(1)="Franchise "+K$(1,3)+" Total" ELSE Y5$(3)="Salesperson "+K$(5,4)+" Total"
7560 Y5$(49)="("+STR(A0[0]:"0")+")",Y5$(57)=STR(A0[1]:Z0$),Y5$(70)=STR(A0[2]:Z0$),Y5$(84)=STR(A0[1]-A0[2]:Z0$); IF A0[1]<>0 THEN Y5$(98)=STR((A0[1]-A0[2])*100/A0[1]:Z1$)
7562 IF F0=1 THEN Y5$(108)=STR(A0[3]:Z0$) ELSE Y5$(108)=STR(A0[4]:Z0$),Y5$(121)=STR(A0[5]:Z0$),Y5$(134)=STR(A0[6]:Z0$)
7564 Y5$(O)=STR(A0[8]:Z0$),Y5$(O+13)=STR(A0[7]:Z0$)
7566 Y5$(O+26)=STR(A0[9]:Z0$),Y5$(O+39)=STR(A0[10]:Z0$)
7568 Y5$(O+52)=STR(A0[11]:Z0$),Y5$(O+65)=STR(A0[12]:Z0$)
7575 GOSUB 7300
7580 GOTO 7520
8905 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
8910 DEF FND$(Z9$)=Z9$(3,2)+"/"+Z9$(5,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8920 DEF FNB$(Z9$)=X3$(214-6*NUM(Z9$),1)
8940 DEF FNS$(Z$)=Z$(1,POS("   "=Z$+"   ")-1)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 IF Y5=68 THEN RETRY ELSE ON C9 GOTO 1140,2040
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 RUN "ZMENU"
9999 END 
60001 ESCAPE 
