0010 REM "Select franchises to perform nightly transfer <CM2NSL>
0020 SETESC 9300; SETERR 9000
0035 REM "5.0 - 02/19/02 - 11.665277 - plh - SSP# 142887 - DMM
0040 REM "Copyright 2002 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,A$,A{ALL}
0100 SETERR 9000
0110 X0$="CM2NSL",X1$="Franchise Selection for Xfer",K9$="",K9=0
0120 DIM Z0$(80,"-")
0130 K0=10,K1=1
0135 C9=-1
0200 REM "DISPLAY BANNER
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(65,1)>"2" THEN PRECISION NUM(X3$(65,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "DEFINE IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10]
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10]
0500 REM "FILES
0510 DIM Z[NUM(X3$(60,3))]
0520 Z$="13O ZZPARM"
0530 GOSUB 9750; ON Z0 GOTO 0531,9900
0600 REM "Get last scheduled time from temp file
0605 REM GOTO 01000
0610 CLOSE (10); OPEN (10)"XFERTM"
0620 READ RECORD (10)T$; CLOSE (10); T$=T$(1,4)
1000 REM "MAIN PROCESSING
1010 GOSUB 7200
1020 GOSUB 7000
1030 REM "R = Screen Row / C0 = Pointer to C$ / C1 = Entries in C$ table(Offset of 3) / T1 = Total Send="Y"
1040 OPEN (1)"ZZPARM"; C0=0
1050 R=7
1060 GOSUB 7100
1070 GOSUB 7400
1075 IF Z=-1 THEN GOSUB 7600; GOTO 1070
1080 ON Z GOSUB 1050,1200,1600,2000,7300,7700,7800; GOTO 1070
1090 REM 
1200 REM "NEW STARTING POINT
1210 DIM R$(3); PRINT @(0,22),'CE','CL',"Enter your selection:"
1220 CALL "ZZENTR","ZX",A{ALL},R$,X4$,X3$,22,22,1,3,X0,"","","","","","",""
1230 P0=NUM(R$(1,3)); IF P0=0 THEN RETURN 
1240 P1=POS(R$(1,3)=C$,3)
1250 IF P1<>0 THEN P1=P1-1,C0=P1/3,R=7; GOTO 7100
1260 X=0
1270 P2=NUM(C$(X*3+1,3))
1280 IF P2<P0 THEN GOTO 1300
1290 IF P2>P0 THEN C0=X,R=7; GOTO 7100
1300 X=X+1; IF X<C1 THEN GOTO 1270
1310 RETURN 
1600 REM "User pressed EXIT
1610 REM "IF X3$(77,1)="D" THEN S1$="\" ELSE S1$="/"
1620 INVOKE "echo "+T$+" > "+HWD+DLM+"UTILS"+DLM+"XFERTM"
1630 IF T1>0 THEN P$="Do you wish to |CREATE the |CALL file and |SCHEDULE it for the time you specified?" ELSE P$="|No |Companys |Marked! |Currently |scheduled |job |will |be |removed! |Continue?"
1640 CALL "ZZPROM",".Y",X3$,X0,P$,"","",Z0; IF T1>0 THEN ON X0 GOTO 1641,1990 ELSE ON X0 GOTO 1990,1070
1900 REM "Build CALL file, remove 'at' jobs, then add to 'at' queue
1910 CALL "CM2BLD"
1920 T=NUM(T$(3,2)),T2=T/1.645,T$=T$(1,2)+STR(T2:"00")
1930 PRINT @(1,6),"Removing currently scheduled jobs ... "; INVOKE "umask 0;atrm -"; PRINT @(0,7),'CE'
1950 PRINT @(1,8),"Nightly transfer with remote CONFIRMED ..."
1960 REM PRINT @(1,9),"     ",; SYSTEM "at "+T$+" nightly.rmt"
1970 REM PRINT @(1,11),"Please write this job number in your log for future reference!"; PRINT @(1,16)
1980 P$="Complete!"; CALL "ZZPROM","4 ",X3$,Z,P$,"","",Z0
1990 IF T1=0 THEN INVOKE "atrm -"; GOTO 9900 ELSE GOTO 9900
2000 REM "ENTER RANGE
2005 DIM R$(7); PRINT @(40,3),'CL'
2010 CALL "ZZENTR","ZX",A{ALL},R$,X4$,X3$,14,3,1,3,X0,"","","","","","",""
2020 IF R$(1,3)="   " THEN R$="ALL"; PRINT @(14,3),R$; GOTO 2060
2030 CALL "ZZENTR","ZX",A{ALL},R$,X4$,X3$,24,3,4,3,X0,"","","","","","",""
2040 IF R$(4,3)="   " THEN GOTO 2010
2050 IF NUM(R$(1,3))>NUM(R$(4,3)) THEN GOTO 2010
2060 IF R$(1,3)="ALL" THEN R$="001999 "
2070 CALL "ZZENTR","Y",A{ALL},R$,X4$,X3$,35,3,7,1,X0,"","","","","","",""
2080 REM PRINT @(40,3),'BB',"*** Marking Selected Range ***",'EB'
2090 L=NUM(R$(1,3)),H=NUM(R$(4,3))
2100 GOSUB 7500
2110 R1=R,R=7,C0=C0-(R1-7)
2120 GOSUB 7000; GOSUB 7100
2130 R=R1
2140 RETURN 
7000 REM "DISPLAY SCREEN
7010 GOSUB 7030; PRINT @(1,3),'CE','SB',"Range Begin:      End:      Send?",'CL',@(14,3),"___",@(24,3),"___",@(35,3),"_",'SB',@(43,3),"Scheduled For:",@(68,3),"Marked:",'CL',@(76,3),STR(T1:"0000"),'SB',@(0,4),Z0$,@(0,5),"Ln",@(3,5),"Comp#",@(9,5),"Company Name",@(45,5),"Send?",@(54,5),"Last Attempt",@(73,5),"Result",@(0,6),Z0$,'SF'
7015 CALL "ZZDISP","TU",T$,"",X3$,"","",58,3,X4$
7020 RETURN 
7030 REM "COUNT RECORDS MARKED
7040 T1=0; DIM S$(120)
7050 FOR Y=0 TO C1
7060 K$="R/L"+C$(Y*3+1,3); READ (Z[13],KEY=K$)S$(1)
7070 IF LEN(S$)=120 AND S$(103,1)="Y" THEN T1=T1+1
7080 NEXT Y
7090 RETURN 
7100 REM "DISPLAY LINES
7110 DIM B$(120); IF R=7 THEN PRINT @(0,7),'CE'
7120 K$="R/L"+C$(C0*3+1,3); K1$="CMP"+C$(C0*3+1,3)
7130 READ (1,KEY=K$)B$(1); READ (Z[13],KEY=K1$)D$
7140 PRINT @(0,R),STR(R-6:"##"),@(4,R),B$(4,3),@(9,R),D$(7,27),@(47,R),B$(103,1),@(51,R),FND$(B$(11,6))
7145 CALL "ZZDISP","T",B$(105,4),X4$,X3$,"","",62,R,""
7147 IF B$(104,1)="S" THEN PRINT @(73,R),"SUCCESS" ELSE IF B$(104,1)="F" THEN PRINT @(73,R),'BB',"FAILED ",'EB'
7150 C0=C0+1,R=R+1
7160 IF C0<=C1 AND R<21 THEN GOTO 7100
7170 IF C0<C1 THEN M$="RM" ELSE M$="RT"
7180 RETURN 
7200 REM "BUILD C$ FROM ZZPARM WITH KEY="R/L"
7210 OPEN (1)"ZZPARM"
7220 READ (1,KEY="R/L",DOM=7221)
7230 K$=KEY(1); IF K$(1,3)<>"R/L" THEN GOTO 7260
7240 READ (1)A$
7250 IF A$(10,1)="2" THEN C$=C$+A$(4,3); GOTO 7230 ELSE GOTO 7230
7260 CLOSE (1); C1=LEN(C$)/3-1
7270 RETURN 
7300 REM "MODIFY LINES CURRENTLY DISPLAYED
7310 R1=R,R=7,C0=C0-(R1-7),C2=C0; DIM B$(120)
7320 K$="R/L"+C$(C0*3+1,3)
7330 READ (Z[13],KEY=K$)B$(1)
7340 CALL "ZZENTR","Y",A{ALL},B$,X4$,X3$,47,R,103,1,X0,"","","","","","",""
7350 WRITE (Z[13],KEY=K$)B$
7352 IF X0=4 THEN GOTO 7380
7354 IF X0=2 OR X0=3.2 THEN IF R>=8 THEN C0=C0-1,R=R-1; GOTO 7320 ELSE GOTO 7320
7360 C0=C0+1,R=R+1
7370 IF R<R1 THEN GOTO 7320
7380 R=7,C0=C2; GOSUB 7000; GOSUB 7100; R=R1
7390 RETURN 
7400 REM "PROMPT USER FOR ACTION TO TAKE
7405 PRINT @(3,21),'BR',"Next call scheduled for: ",; INVOKE "atq"; PRINT 'ER'
7410 CALL "ZZPROM",M$,X3$,Z,"","","",Z0
7420 RETURN 
7500 REM "MARK RANGE OF RECORDS
7510 OPEN (2)"ZZPARM"; DIM S$(120)
7520 FOR Z=L TO H
7530 K$="R/L"+STR(Z:"000")
7540 READ (2,KEY=K$,DOM=7560)S$(1)
7550 IF S$(10,1)="2" THEN S$(103,1)=R$(7,1); WRITE (2,KEY=K$)S$
7560 NEXT Z
7570 CLOSE (2); RETURN 
7600 REM "SELECT LINE
7605 IF Z0>R-7 THEN RETURN 
7610 R1=R,C2=C0,C0=C0-(R1-7)+Z0,C0=C0-1; DIM R$(7),S$(120)
7620 K$="R/L"+C$(C0*3+1,3)
7625 PRINT @(39,3),'CL',"Company: "+K$(4,3),'SB'; CALL "ZZENTR","Y",A{ALL},R$,X4$,X3$,35,3,7,1,X0,"","","","","","",""
7630 READ (Z[13],KEY=K$)S$(1)
7640 S$(103,1)=R$(7,1)
7650 WRITE (Z[13],KEY=K$)S$
7660 R=7,C0=C2-(R1-7)
7670 GOSUB 7000; GOSUB 7100
7690 RETURN 
7700 REM "ENTER TIME TO SCHEDULE RUN
7710 REM 
7720 CALL "ZZENTR","T",A{ALL},T$,X4$,X3$,58,3,1,4,X0,"","","","","","",""
7730 RETURN 
7800 REM "DELETE CALL
7810 PRINT "Removing scheduled call ... please wait ",
7820 R1=R,R=7,C0=C0-(R1-7)
7840 INVOKE "atrm -"
7850 GOSUB 7000; GOSUB 7100; RETURN 
8000 REM "DEFINITIONS
8010 DEF FND$(Z9$)=Z9$(3,2)+"/"+Z9$(5,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
9000 REM "ERROR PROCESSING
9001 PRINT "ERR: "+STR(ERR)
9002 PRINT "TCB: "+STR(TCB(5))
9003 EXIT 
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9750 REM "FILES
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9770 RETURN 
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
