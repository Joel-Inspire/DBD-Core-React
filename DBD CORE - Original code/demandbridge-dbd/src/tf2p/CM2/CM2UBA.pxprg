0010 REM "Update RL Data at ML <CM2UBA>
0035 REM "5.5 - 03/06/07 - 9.885833 - mhe - SSP# 205703
0037 REM "205703-Oracle - FID and FIB calls, replace with FN%FID$ and FN%FIB$
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0080 REM "*****************************************************************
0081 REM "This program would be run at ML to process newly completed
0082 REM "transmissions from the RLs.
0085 REM "*****************************************************************
0100 BEGIN ; SETERR 9000
0110 X0$="CM2UBA",X1$="Update Data from Remote Location"
0145 DIM R6$(78)
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0260 IF X3$(330,1)="1" THEN CALL "ZZPROM",".4",X3$,0,"Must be run by Master Location company","","",0; GOTO 9900
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="12L CME     13O ZZPARM"; F0$="CMZ"
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; IF Z0=1 THEN GOTO 9900
0590 F5$="APDAPHARBARPCM2CM3"
0600 REM 
0620 REM IF X3$(17,2)<>X3$(23,2) THEN CALL "ZZPROM",".5",X3$,S3,"WARNING: The Accounting month and Machine month are different!","","",0; ON S3 GOTO 00621,09900
0640 GOSUB 6000; CALL "ZZPROM",".Y",X3$,S3,"Proceed?","","",0; ON S3 GOTO 0641,9900
0650 GOTO 7400
0990 PRINT @(20,10),"Now processing:",
1040 SETERR 9000
1050 READ (Z[1],KEY="",DOM=1051)
1080 X9$=$8A$
1085 DIM Z0$(20,$00$)
1500 REM "Get next record from file
1510 K2$=KEY(Z[1],END=5000)
1511 IF K2$(1,3)="CM1" OR K2$(10,5)="AUDIT" THEN REMOVE (Z[1],KEY=K2$,DOM=1510); GOTO 1510
1515 PRINT @(35,10),K2$,
1520 K1$=K2$(9),K0$=K2$(1,8); IF J4$(10,1)="2" THEN K1$=K1$(1,LEN(K1$)-2); REM "remove extra sequence counters we added at 7550-7590
1530 IF K0$<>F0$ THEN GOSUB 7000
1600 REM "Decipher record
1610 READ RECORD (Z[1],KEY=K2$)X$
1615 M9$="Invalid transfer record format "+K0$
1620 P=POS(X9$=X$); IF P=0 THEN GOTO 9850 ELSE K3$=X$(1,P-1),X$=X$(P+LEN(X9$))
1625 P=POS(X9$=X$); IF P=0 THEN GOTO 9850 ELSE U0$=X$(1,P-1),X$=X$(P+LEN(X9$))
1630 P=POS(X9$=X$); IF P=0 THEN GOTO 9850 ELSE U1$=X$(1,P-1),X$=X$(P+LEN(X9$))
1635 P=POS(X9$=X$); IF P=0 THEN GOTO 9850 ELSE U2$=X$(1,P-1),X$=X$(P+LEN(X9$))
1650 A$=FNZ$(X$)
2000 REM "Update the file
2005 IF LEN(U0$)>14 THEN IF U0$(14,3)>="2.0" THEN GOSUB 7800; REM "If version 2 records or greater remove unique id if needed
2010 IF F0$(1,3)="ZR1" THEN GOTO 7600
2020 A0$=""; READ RECORD (Z[2],KEY=K1$,DOM=2021)A0$
2025 IF POS(F0$(1,3)="APDARBCM3",3)>0 THEN A$(1,6)=P0$(1,6),K1$(1,6)=P0$
2035 REM "NO LONGER NEEDED ->IF F0$(1,3)="CM3" THEN READ (Z(2),KEY=K1$,DOM=02036) ; GOTO 02400
2040 WRITE RECORD (Z[2],KEY=K1$,DOM=2041)A$; GOTO 2045
2041 GOSUB 7850
2090 Q=POS(F0$(1,3)="AR1",3); IF Q=0 THEN GOTO 2900 ELSE Q=INT(Q/3); ON Q GOTO 2200
2200 REM "Customer Records - Special
2205 IF A0$="" THEN DIM A0$(LEN(A$))
2210 K5$=A0$(155,10)+A0$(1,10),K6$=A$(155,10)+A$(1,10)
2215 REMOVE (Z[3],KEY=K5$,DOM=2216)
2220 WRITE (Z[3],KEY=K6$)
2225 K5$=A0$(278,4)+A0$(1,10),K6$=A$(278,4)+A$(1,10); REMOVE (Z[4],KEY=K5$,DOM=2226)
2230 WRITE (Z[4],KEY=K6$)
2235 K5$=A0$(205,10)+A0$(1,10),K6$=A$(205,10)+A$(1,10); REMOVE (Z[5],KEY=K5$,DOM=2236)
2240 WRITE (Z[5],KEY=K6$)
2295 GOTO 2900
2400 REM "Updating this record would cause duplicate overwrite
2405 Q$=FN%FID$(Z[2]),Q$=Q$(4,6) ! [205703]-changed FID() to FN%FID$()
2410 Q$="Duplicate key in "+Q$+"="+K1$+", not updated."
2420 CALL "ZZPROM",".4",X3$,0,Q$,"","",0
2480 READ (Z[1],KEY=K2$)
2495 GOTO 1500
2900 REM 
2920 REMOVE (Z[1],KEY=K2$,DOM=2921)
2950 GOTO 1500
5000 REM "END OF COMPANY
5010 READ (Z[13],KEY=C9$(1,6))
5015 ERASE "CMZ"+C9$(4,3),ERR=5016
5020 GOTO 7420
5090 GOTO 0010
6000 REM 
6020 PRINT @(0,3),'CE',"This program will process data from the remote location and update the appropriate data files here.  The updates will then be performed for any data entry functions."
6025 Q$=X3$(21,6); CALL "ZZ2FYP",X3$,Q$,P0$
6030 PRINT @(0,6),Z0$,@(12,7),"Will be updated with a Fiscal year/ Period of: ",P0$(1,4),"/",P0$(5,2),
6090 RETURN 
7000 REM "Change file being updated
7020 IF F0$="" THEN GOTO 7050
7030 Z$="02C "+F0$(1,3)+"... "
7031 IF F9$>"" THEN FOR X=1 TO LEN(F9$) STEP 11; F9$(X+2,2)="CU"; NEXT X; Z$=Z$+F9$,F9$=""
7035 REM PRINT @(0,18),'CL',@(20),"FILE DATA: ",Z$,
7040 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; IF Z0=1 THEN GOTO 9900
7050 REM "Open file
7055 F0$=K0$
7060 Z$="02O "+K2$(1,3)+"... "
7061 IF F0$(1,3)="AR1" THEN F9$="03O ARE... 04O ARU... 05O ARW... ",Z$=Z$+F9$
7070 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; IF Z0=1 THEN ESCAPE ; REM GOTO 09900
7090 RETURN 
7400 REM 
7410 READ (Z[13],KEY="CMP",DOM=7411)
7420 READ (Z[13],END=9900)C9$; IF C9$(1,3)<>"CMP" THEN GOTO 9900 ELSE IF POS(C9$(195,1)="1")=0 THEN GOTO 7420
7440 PRINT 'CF',@(12,8),"Processing for ",C9$(4,3),"  ",C9$(7,40),
7480 Z$="01CU  01O CM1"+C9$(4,3)
7485 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; IF Z0=1 THEN GOTO 9900
7487 DIM J4$(100); FIND (Z[13],KEY="R/L"+C9$(4,3),DOM=7488)J4$
7490 IF J4$(10,1)="2" THEN J9$=F9$; GOSUB 7500; F9$=J9$
7495 GOTO 0990
7500 REM "Accumulate files into the inbox
7510 F9$="CM1"+C9$(4,3),F9=Z[1]; GOSUB 7550
7545 RETURN 
7550 REM "Given TopForm name of YYY### format, get list of files from inbox intoF2$, and for each copy to TMP+FID(0) then load records into YYY### and then erase temp file and corresponding inbox file.
7551 REM "TopForm name is in F9$ and file isopen on F9
7554 J9=0
7555 CALL "CM2TRN",X3$,3,F9$+"*","",F2$,""
7556 IF J9=0 AND F2$="" THEN J9=1; GOTO 7555
7560 P=POS("|"=F2$); IF P=0 THEN GOTO 7595
7565 F1$="TMP"+FID(0),F$=F2$(1,P-1); IF P=LEN(F2$) THEN F2$="" ELSE F2$=F2$(P+1)
7569 ERASE F1$,ERR=7570; WAIT 2
7570 CALL "CM2TRN",X3$,1,F1$,F$,"",""
7573 CLOSE (14,ERR=7574)
7574 OPEN (14,ERR=7575)F1$; CLOSE (14)
7575 Z$="10O "+F1$+" "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; IF Z1<>0 THEN GOTO 7590
7577 GOSUB 7700; IF NUM(O$)=0 THEN GOTO 7578 ELSE GOTO 7585
7579 READ (Z[10],KEY="",DOM=7580); REM REPOSITION TO BOF
7580 K$=KEY(Z[10],END=7585); READ RECORD (Z[10],KEY=K$)R$; K$=K$+"  "; WRITE RECORD (F9,KEY=K$,DOM=7581)R$; GOTO 7580
7581 S0$=K$(LEN(K$)-1,2); IF S0$(2,1)="9" THEN S0$(2,1)="A" ELSE IF S0$(2,1)<>"z" THEN S0$(2,1)=CHR(ASC(S0$(2,1))+1) ELSE S0$(2,1)="0"; IF S0$(1,1)="9" THEN S0$(1,1)="A" ELSE IF S0$(1,1)<"z" THEN S0$(1,1)=CHR(ASC(S0$(1,1))+1) ELSE ESCAPE ; REM "if 9 skip to A if not 'z' increment by 1, else if 'z' set to 0 and add one to 10's digit. If 10's is already a 'z' then give up
7582 K$=K$(1,LEN(K$)-2)+S0$; WRITE RECORD (F9,KEY=K$,DOM=7581)R$; GOTO 7580
7585 Z$="10CU"+F1$+" "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
7586 IF NUM(O$)<2 THEN CALL "CM2TRN",X3$,4,"",F$,"",""
7590 ERASE F1$,ERR=7591
7591 GOTO 7560
7595 RETURN 
7600 REM "Handle Repository records
7604 K3$=K3$(1,LEN(K3$)-2)
7605 I0$=K3$(9)
7610 IF LEN(I0$)=5 THEN GOSUB 7650; GOTO 7645 ELSE IF R6$(1,1)<>"Y" THEN GOTO 7645; REM "If not keep this at ML just skip ti
7615 IF A$(6,4)>R6$(67,4) THEN CALL "ZR2DNP",ERR=7616,X3$,X4$,R6$; GOTO 7615
7620 REM "We want to add a line @ a specific line #, so manipulate R6$ currentl ine # to match the one we want and then call new line routine with just the data
7625 R6$(71,3)=A$(10,3); CALL "ZR2DNL",ERR=7626,X3$,X4$,A$(13),R6$
7645 GOTO 2900
7650 REM "Start new document
7655 IF A$(1,10)<>R6$(10,10) THEN CALL "ZR2DGP",X3$,X4$,A$(1,10),R6$
7660 IF R6$(1,1)="Y" THEN R6$(79,3)=A$(119,3),A5$=A$(11,40),A6$=A$(53,43); CALL "ZR2DND",X3$,X4$,A5$,A6$,R6$; REM " Start a new doc using key values from real ZR1 record, and defaults to preserve the original date/time printed, operator & desc.; R6$(79,3) is set to A$(119,3) so that the source company code is correct
7695 GOTO 2900
7700 REM "VERIFY AUDIT RECORDS
7710 READ (Z[10],KEY="",DOM=7711)
7720 CALL "CM2ATA",X3$,X4$,Z[10],1,O$,C9$,F$(POS("."=F$)+1,3),Z[12]
7730 CALL "CM2RBA",C9$(4,3),F$(POS("."=F$)+1,3)
7740 RETURN 
7800 REM "Remove unique id from keys, IF needed
7810 IF POS(F0$(1,3)=F5$,3)<>0 THEN GOTO 7845; REM "if one of these files, then leave unique part on the key as these are data entry files.
7815 K1$=K1$(1,LEN(K1$)-2)
7845 RETURN 
7850 REM "If dom on write, check to see if in file that needs a unique key. If not, write it out, if it does need unique key, then keep incrementing until unique key.
7855 IF POS(F0$(1,3)=F5$,3)=0 THEN WRITE RECORD (Z[2],KEY=K1$)A$; GOTO 7895
7859 REM "Increment seq id and try again
7860 S0$=K1$(LEN(K1$)-1,2)
7862 IF S0$(2,1)="9" THEN S0$(2,1)="A" ELSE IF S0$(2,1)<>"z" THEN S0$(2,1)=CHR(ASC(S0$(2,1))+1) ELSE S0$(2,1)="0"; IF S0$(1,1)="9" THEN S0$(1,1)="A" ELSE IF S0$(1,1)<"z" THEN S0$(1,1)=CHR(ASC(S0$(1,1))+1) ELSE ESCAPE ; REM "if 9 skip to A if not 'z' increment by 1, else if 'z' set to 0 and add one to 10's digit. If 10's is already a 'z' then give up
7865 K1$(LEN(K1$)-1,2)=S0$
7870 WRITE RECORD (Z[2],KEY=K1$,DOM=7860)A$
7895 RETURN 
8000 REM "Change accounting period to current date
8700 REM "No current data records
8720 PRINT @(0,3),'CE','RB',@(12,6),"Waiting for next completed transmission.",@(12,8),"Press any key to abort...",
8740 F0=14; CLOSE (F0); OPEN (F0)FID(0); READ RECORD (F0,SIZ=1,TIM=20,ERR=8750)X$; IF POS(X$="Mm")>0 THEN GOTO 8800 ELSE GOTO 9900
8750 READ (Z[3],KEY="",DOM=0645)
8800 REM "Manual entry of company
8810 INPUT @(0,3),'CE',@(12,10),"Enter the source company code: ",C9$
8820 IF C9$="" THEN GOTO 8700 ELSE IF LEN(C9$)>3 THEN GOTO 8800 ELSE READ (Z[13],KEY="CMP"+STR(NUM(C9$,ERR=8800):"000"))C8$
8825 IF C8$(195,1)<>"1" THEN CALL "ZZPROM",".4",X3$,0,"This is not a remote location company","","",0; GOTO 8810
8840 WRITE (Z[3],KEY=C8$(4,3))C8$(4,3)
8850 GOTO 0650
8980 DEF FNZ$(Z9$)=Z9$(1,POS(Z0$=Z9$+Z0$)-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9705 IF X9>0 THEN GOTO 9900
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9730 RUN U1$(1,6)
9800 REM 
9810 Q$="+ ("+F0$+")"
9820 CALL "ZZPROM",".4",X3$,0,"Problem accessing files!"+Q$,"","",0
9845 GOTO 9900
9850 REM "Write message file, skip record
9890 GOTO 1500
9900 REM "END PROGRAM
9910 REM CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z(ALL),0,0
9940 RUN "ZMENU"
9999 END 
56000 REM + Modification History
56002 REM "205703-Oracle - FID and FIB calls, replace with FN%FID$ and FN%FIB$
