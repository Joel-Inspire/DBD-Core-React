0010 REM "Generate/Verify CM Audit Records in the XFER files <CM2ATA>
0035 REM "3.8 - 04/26/95 - 11.67 - jtb
0040 REM "Copyright 1995 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0110 ENTER X3$,X4$,F0,Z1,O$,C9$,F0$,F1
0120 SETERR 9000
0150 REM "Z1 IS CONTROL, IF 0 CREATE AUDIT RECORD, IF 1 VERIFY AUDIT RECORD
0160 REM "O$ IS RETURNED Y=MATCH  N=MISMATCH
0170 REM "C9$ IS FOR VERIFYING, COMP#=4,3  IF Z1=1
1000 REM "BEGIN MAIN PROCESS
1010 C=0,C$="",O$="Y"; IF Z1=1 THEN O$="0"; GOSUB 2000; IF NUM(O$)=1 THEN GOTO 4000
1020 READ (F0,KEY="",DOM=1021)
1030 K$=KEY(F0,END=1800)
1040 READ (F0,KEY=K$)A$
1050 IF A$(1,3)="CM1" OR A$(1,3)="CM0" THEN GOTO 1110
1060 IF A$(4,3)=$FFFFFF$ THEN GOTO 1500
1070 IF A$(1,3)=C$ THEN C=C+1; GOTO 1110
1075 IF C$="" THEN C$=A$(1,3); GOTO 1070
1080 IF Z1=1 THEN GOTO 1600
1090 GOSUB 1700
1100 C=0; C$=A$(1,3)
1110 GOTO 1030
1500 REM "RECORD IS AN AUDIT RECORD
1510 IF Z1=0 THEN REMOVE (F0,KEY=K$,DOM=1110); GOTO 1110
1600 REM "READ AUDIT RECORD FOR CURRENT TYPE
1610 IF C$="" THEN C$=A$(1,3); GOTO 1070
1620 GOSUB 1750
1630 GOSUB 3000
1640 K$=KEY(F0,END=1641); C=0,C$=""; GOTO 1110
1650 GOTO 4000
1700 REM "WRITE AUDIT RECORD FOR CURRENT TYPE
1710 GOSUB 1900
1720 WRITE (F0,KEY=D$(1,14))D$
1730 RETURN 
1750 REM "READ AUDIT RECORD FOR CURRENT TYPE
1760 GOSUB 1900
1770 DIM E$(50); READ (F0,KEY=D$(1,14),DOM=1771)E$; GOTO 1780
1775 E$(15,5)="00000"
1780 RETURN 
1800 REM "END OF FILE REACHED, WRITE LAST AUDIT RECORD IF Z1=0, ELSE VERIFY
1810 IF Z1=0 THEN IF C>0 THEN GOSUB 1700; GOTO 4000 ELSE GOTO 4000
1815 IF C=0 THEN GOTO 1840
1820 GOSUB 1750
1830 GOSUB 3000
1840 GOTO 4000
1900 REM "BUILD AUDIT RECORD FOR CURRENT TYPE INTO D$
1910 D$=C$+$FFFFFFFFFFFF$+"AUDIT"+STR(C:"00000")
1940 RETURN 
2000 REM "BUILD AUDIT CONTROL RECORD FOR THIS BATCH FOR CME FILE
2010 IF C9$<>"" THEN F$=C9$(4,3) ELSE F$=X3$(9,3)
2020 F$=F$+F0$+"   00001Y     "
2040 H$=DAY; IF H$(7,1)="9" THEN H$(7,1)="J" ELSE H$(7,1)="K"
2050 H$=H$(7,2)+H$(1,2)+H$(4,2); I$=STR(TIM:"00.00"); I$=I$(1,2)+I$(4,2)
2060 F$=F$+H$+I$+X3$(40,3)
2070 READ (F1,KEY=F$(1,9),DOM=2071,END=2071)J$
2080 IF J$="" THEN GOTO 2100 ELSE IF J$(15,1)="Y" THEN O$="1",F$=J$; GOTO 2110
2090 C=NUM(J$(10,5))
2100 C=C+1,F$(10,5)=STR(C:"00000"),C=0
2110 RETURN 
3000 REM "BUILD TRACKING RECORD FOR CURRENT RECORD TYPE AND WRITE TO CME
3010 G$=F$(1,6)+C$+E$(15,5)+" "+STR(C:"00000")
3020 IF D$(15,5)<>E$(15,5) THEN F$(15,1)="N" ELSE WRITE (F1,KEY=G$(1,9))G$
3050 RETURN 
4000 REM "END OF FILE, TIME TO EXIT
4010 IF Z1=1 THEN WRITE (F1,KEY=F$(1,9))F$; IF F$(15,1)="N" THEN O$="2"
4020 GOTO 9900
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,1
9050 ON Y7 GOTO 9060,9100,9190,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM "TRANSFER CONTROL
9190 GOSUB 6600; GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "END PROGRAM
9999 END 
