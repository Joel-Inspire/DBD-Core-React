0010 REM "Net Proceeds RECAPE <CM2NCB>
0015 REM "Prog Type: O -370032
0020 SETESC 9300; SETERR 9000
0035 REM "3.8 - 07/22/94 - 18.94
0040 REM "Copyright 1994 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0110 X0$="CM2NCB",X1$="Net Proceeds Recap"
0120 DIM A$(186),A[35],S$(60),S[4]
0140 Z0$="####,###.00-"
0210 T=1,T0=3,T1=36
0220 W3=131
0225 W7=1
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0290 IOLIST S[0],S[1],S[2],S[3],S[4]
0300 REM "I/O lists
0305 REM "IOLIST FOR CM5 - Net Proceeds Work File (temp)
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30],A[31],A[32],A[33],A[34],A[35]
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15]
0340 IOLIST D$,D[0],D[1],D[2],D[3],D[4],D[5],D[6],D[7],D[8],D[9],D[10],D[11],D[12],D[13],D[14],D[15],D[16],D[17],D[18],D[19],D[20],D[21],D[22],D[23],D[24],D[25],D[26],D[27],D[28],D[29],D[30],D[31],D[32],D[33],D[34],D[35]
0350 IOLIST E$,E[0],E[1],E[2]
0360 IOLIST F$,F[0],F[1],F[2]
0370 IOLIST G$,G[0],G[1],G[2],G[3],G[4],G[5],G[6],G[7],G[8],G[9],G[10],G[11],G[12],G[13],G[14]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,IOL=0425
0425 IOLIST V$,V[0],V[1]
0432 IOLIST P$,P[0],P[1],P[2],P[3],P[4],P[5],P[6],P[7],P[8],P[9],P[10],P[11],P[12],P[13],P[14],P[15]
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01X CM7... 02O AR1...   04O CM4... 05O CM6... 06O AP4... 07O APD... 08O APE...  09O AR3...  "; IF V1$(1,3)<>Y$(5,3) THEN Y$=Y$+"02X "+V1$+"  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0530 READ (Z[13],KEY="PFGG/L")F3$
0540 Q$=X3$(21,6),Q=-NUM(V$(86,3)); CALL "ZZDATE",X3$,"",Q$,D9$,Q,0,0,0,0,0,0,"","",""
0800 REM "Alternate Sort Info & total dim"
0810 DIM U[10,2],U0$(10),Y[5,1]
0820 CALL "ZZRSEQ",X3$,X0$,V1$,U,U0,U1,U{ALL},Y{ALL},T0,V0,U0$,T5$,U5$,S9$,X9; ON X9 GOTO 0821,9920
0825 IF V0>1 THEN GOSUB 6950
0850 DIM T[T0,T1]; U5$(1,20)=Q4$
0890 REM "Open Printer
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM " Position read"
0950 REM " Position read"
0970 DIM V4$(U); U9=1; FOR X=1 TO U0; V4$(U[X,0],U[X,1])=V2$(U9,U[X,1]); U9=U9+2*U[X,1]; NEXT X
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record and check range
1005 U$=KEY(U1,END=5000)
1010 FOR X=1 TO U0; U2=U[X,0],U3=U[X,0]*2-1,U4=U[X,1]
1020 IF U$(U2,U4)<V2$(U3,U4) THEN READ (U1); EXITTO 1000
1030 IF U$(U2,U4)>V2$(U3+U4,U4) THEN IF X=1 THEN EXITTO 5000 ELSE READ (U1); EXITTO 1000
1040 NEXT X
1100 REM "Get record"
1110 U9$=U$; IF U1=Z[1] THEN GOTO 1150 ELSE U$="",X=0
1120 IF Y[X,0]>0 THEN U$=U$+U9$(Y[X,0],Y[X,1]),X=X+1; GOTO 1120
1130 READ (U1)
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1160 A[31]=A[2],A[2]=A[2]*A[27]/100,A[27]=100
1200 REM "Reporting criteria
1210 GOSUB 8000
1300 REM "Update mode?
1310 IF Q5=1 THEN GOSUB 7500; GOTO 1000
1400 REM "Subtotals"
1410 IF U2$="" THEN GOTO 1490 ELSE FOR T=1 TO U0; IF U9$(U[T,0],U[T,1])<>U2$(U[T,0],U[T,1]) THEN EXITTO 1420 ELSE NEXT T; GOTO 1480
1425 GOSUB 3100
1490 T=0,U2$=U9$
1510 IF W+3>W0 OR W7=1 THEN GOSUB 6000
1515 Y5$(1)=A$(9,2)+"-"+A$(11,6)
1520 Y5$(13)=FND$(A$(39,6))
1525 CALL "ZZDISP","AXS",A$(61,10),"AR6",X3$,Y5$,"",25,0,X4$
1528 IF A$(8,1)="2" THEN Y5$(57)=STR(A[5]-A[12]:Z0$); GOSUB 7300; GOTO 1600
1530 Y5$(33)=STR(A[0]-A[15]:Z0$)+STR(A[6]-A[22]:Z0$)+STR(A[3]-A[16]:Z0$)+STR(A[4]-A[17]:Z0$)+STR(A[27]*(A[2]-A[18])/100:Z0$)+STR(A[7]+A[8]-A[19]-A[20]:Z0$)+STR(A[28]:Z0$)+STR(A[29]:Z0$)
1535 GOSUB 7300
1700 REM "SECOND LINE
1702 CALL "ZZDISP","AXS",A$(51,10),"A/R",X3$,Y5$,"",1,0,X4$
1710 Y5$(13)=B$(11,30)
1715 IF A$(8,1)="2" THEN GOTO 1730
1720 Y5$(33)=STR(A[0]:Z0$)+STR(A[6]:Z0$)+STR(A[3]:Z0$)+STR(A[4]:Z0$)+STR(A[27]*A[2]/100:Z0$)+STR(A[8]+A[7]:Z0$)+STR(A[28]+A[21]:Z0$)+STR(A[29]+A[10]:Z0$)
1730 REM IF A$(8,1)="2" THEN LET Y5$(45)="Item: "+FNS$(A$(137,10))+", "+A$(147,40)
1740 GOSUB 7300
1800 REM 
1820 Y5$(1)=A$(137,10),Y5$(13)=A$(147,40)
1822 IF A$(8,1)="2" THEN GOTO 1900
1825 IF POS("N"=V$(59,12))>0 THEN Y5$(57)=STR(A[5]:Z0$),Y5$(69)=STR(A[4]:Z0$),Y5$(81)=STR(A[31]:Z0$),Y5$(117)=STR(A[30]:Z0$)
1840 GOSUB 7300
1900 REM "Accumulate Totals"
1910 GOSUB 7300
1920 FOR X=1 TO 36; T[T0,X]=T[T0,X]+A[X-1]; NEXT X
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "Subtotal Logic
3110 FOR T=U0 TO T STEP -1
3130 T$=FNS$(U5$(T*20+1,20)),T6$=U2$(U[T,0],U[T,1]),T7$=T5$(T*4+1,4); GOSUB 3050; IF POS(U0$(T+1,1)="23")>0 THEN GOSUB 7000 ELSE GOSUB 7100
3150 IF POS(U0$(T+1,1)="13")>0 THEN W7=1
3180 NEXT T
3195 RETURN 
3280 RETURN 
3380 RETURN 
3480 RETURN 
5000 REM "End of Print
5010 IF Q5=1 THEN GOSUB 7600; GOTO 9900
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END",U$(1)=""
5050 T=0; GOSUB 3100
5200 REM "
5205 IF V$(72,1)<>"Y" THEN GOTO 9900
5210 IF Q5=1 THEN GOTO 9900 ELSE CALL "ZZPROM",".Y",X3$,S3,"Do you wish to update this data?","","",0; IF S3=1 THEN GOTO 9900
5220 Q5=1; GOTO 0990
5295 GOTO 9900
6000 REM "Page header
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; ON W5 GOTO 6041,9910
6045 W7=0
6050 GOSUB 6200
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6110 Y5$(1)=T1$; GOSUB 7300
6115 Y5$(1)="PO Number",Y5$(13)="Invoice",Y5$(25)="Invoice      Sls",Y5$(33)="     Gross     Customer      Total     Freight       Sales     Discount        Net      Royalty"
6120 GOSUB 7300
6125 Y5$(1)="\Cust No.",Y5$(15)="Date",Y5$(26)="Number",Y5$(33)="    Billing    Payments      Costs       Costs      Tax Pd      Allowed        Sale         Fee"
6130 GOSUB 7300
6180 Y5$(1)=T1$; GOSUB 7300
6195 IF FID(W9)=FID(0) THEN PRINT 'SF',
6199 RETURN 
6200 REM "PRINT FRANCHISE HEADER
6205 READ (Z[13],KEY="R/L"+U2$(1,3))F0$
6206 READ (Z[13],KEY="CMP"+F0$(4,3))F1$
6207 I$=""; IF T<>1 THEN READ (Z[9],KEY=U2$(4,4),DOM=6208)I$
6215 Y5$(1)="Prepared for: ",Y5$(15)=FNS$(F1$(7,40))+" ("+F1$(4,3)+")"; GOSUB 7300
6220 IF I$>"" THEN Y5$(2)="Salesperson:",Y5$(16)=FNS$(I$(5,35))+" ("+I$(1,4)+")"; GOSUB 7300 ELSE Y5$(1)="** Franchise Recap **"; GOSUB 7300
6280 GOSUB 7300
6295 RETURN 
6700 REM "Increment Audit Control No.
6705 F9=Z[13]
6710 A2$=X3$(146,6)+"  000100",A0$=X3$(9,3)+"AUDIT",A1$=""; EXTRACT (F9,KEY=A0$,DOM=6716)A0$,A1$
6715 A0=POS(A2$(1,6)=A1$,14); IF A0=0 THEN A1$=A1$+A2$; GOTO 6715 ELSE A2=NUM(A1$(A0+8,4))+1,A2$(9,4)=STR(A2:"0000"),A1$(A0,14)=A2$
6720 WRITE (F9,KEY=A0$)A0$,A1$
6740 RETURN 
6900 REM "SORT
6955 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6970 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,S9; ON S9 GOTO 6971,9920
6990 RETURN 
7000 REM "Totals logic
7001 IF T=0 THEN GOTO 7190 ELSE IF T=1 THEN W7=1
7002 IF W+3>W0 OR W7=1 THEN GOSUB 6000
7010 IF T=3 THEN IF T6$="1" THEN T$="Invoice section" ELSE IF T6$="2" THEN T$="Recovered costs" ELSE IF T6$="3" THEN T$="Reclaimed Costs"
7012 IF T=1 THEN GOTO 7020
7015 Y5$(1)=T$+" totals ("+STR(T[T,0]:"0")+"):"
7016 IF T=3 THEN IF POS(T6$="23")>0 THEN Y5$(57)=T1$(1,LEN(Z0$)); GOSUB 7300; Y5$(57)=STR(T[T,6]-T[T,13]:Z0$); GOSUB 7300; GOTO 7045
7018 FOR Y=0 TO 6; Y5$(45+Y*LEN(Z0$))=T1$(1,LEN(Z0$)-1); NEXT Y; GOSUB 7300
7020 Y5$(6)="Sales tax:    "+STR(T[T,3]-T[T,19]:Z0$),Y5$(33)=STR(T[T,1]-T[T,16]:Z0$)+STR(T[T,7]-T[T,23]:Z0$)+STR(T[T,4]-T[T,17]:Z0$)+STR(T[T,5]-T[T,18]:Z0$)+STR(T[T,3]-T[T,19]:Z0$)+STR(T[T,8]+T[T,9]-T[T,20]-T[T,21]:Z0$)+STR(T[T,29]:Z0$)+STR(T[T,30]:Z0$)
7025 GOSUB 7300
7040 Y5$(6)="Net proceeds: "+STR(T[T,31]-(T[T,3]-T[T,19]):Z0$),Y5$(33)=STR(T[T,1]:Z0$)+STR(T[T,7]:Z0$)+STR(T[T,4]:Z0$)+STR(T[T,5]:Z0$)+STR(T[T,3]:Z0$)+STR(T[T,9]+T[T,8]:Z0$)+STR(T[T,29]+T[T,22]:Z0$)+STR(T[T,30]+T[T,11]:Z0$)
7045 GOSUB 7300; GOSUB 7300
7060 IF U2$(1,7)=U$(1,7) THEN IF U$(8,1)="2" THEN GOSUB 7300; Y5$(1)="Costs Recovered, Invoices over "+STR(NUM(V$(86,3)))+" days"; GOSUB 7300; GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7098 IF T=1 THEN GOSUB 7700
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7180
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     13O ZZPARM  "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7435 DIM V[1]
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V$(71,1)),W3$=V$(19,POS("   "=V$(19,40)+"  ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "'Settle' the amount of this entry
7502 IF A$(8,1)<>"1" THEN GOTO 7590
7505 DIM D$(187),D[35]
7510 D$(1,3)=A$(1,3),D$(4,9)=A$(9,9),D$(13,3)="999",D$(16,6)=X3$(21,6)
7512 D$(37,1)="9",D$(73,40)="Settle net proceeds report"
7520 D[10]=A[29]+A[10],D[11]=A[30]+A[11]-A[5],D[21]=A[21]+A[28]
7525 D[12]=A[5],D[15]=A[0],D[16]=A[3],D[17]=A[4]
7530 D[18]=A[27]*A[2]/100,D[19]=A[8],D[22]=A[6]
7580 WRITE (Z[4],KEY=D$(1,15))IOL=0340
7590 RETURN 
7600 REM "Update A/P Data Entry
7605 Y$=V$(80,6)+S$(1,10)+V$(74,6)
7610 READ (Z[5],KEY="",DOM=7611)
7620 DIM E[2]; READ (Z[5],END=7690)IOL=0350
7625 IF E[0]=0 THEN GOTO 7620
7630 DIM F[2]; READ (Z[6],KEY=E$(1,10))IOL=0360
7635 Y$(7,10)=E$(1,3)+"-"+X3$(178,6)
7640 CALL "AP2ZAA",X3$,Y$,G$,G{ALL},F$,F{ALL},E[0],T9$
7645 G$(17,10)=X3$(178,4)+"."+X3$(182,2)
7650 F=Z[8]; GOSUB 8200; G[0]=A9
7660 WRITE (Z[7],KEY=G$(1,26))IOL=0370
7665 DIM H$(83); H$(7,12)=E$(14,12)
7670 WRITE (Z[8],IND=A9)-1,H$,0,0,0,E[0]
7685 GOTO 7620
7690 GOSUB 6700
7695 RETURN 
7700 REM "Print Sales for the Month
7705 READ (Z[13],KEY="R/L"+U2$(1,3))F0$
7706 DIM P[15]; READ (Z[13],KEY="ROYALTY"+F0$(58,3))IOL=0432
7708 GOSUB 7300; Y5$(1,70)=T1$; GOSUB 7300; Y5$(26)="** NET PROCEEDS RECAP **"; GOSUB 7300; Y5$(1,70)=T1$; GOSUB 7300
7710 GOSUB 7300; Y5$(7)="Gross net proceeds for the period:",Y5$(57)=STR(T[T,31]:Z0$); GOSUB 7300; GOSUB 7300
7715 Q=T[T,6]-T[T,13]; Y5$(8)="LESS: A/P costs recovered by PFG: ",Y5$(57)=STR(Q:Z0$); GOSUB 7300; GOSUB 7300
7720 Q=T[T,14]; Y5$(15)="PLUS: A/P costs reclaimed: ",Y5$(57)=STR(Q:Z0$); GOSUB 7300; GOSUB 7300
7735 Y5$(5)="Total collected sales for the month: ",Y5$(45)=STR(T[T,33]:Z0$); GOSUB 7300
7740 P0=0; FOR X=0 TO 3; IF P[X*2+1]=0 THEN EXITTO 7743 ELSE IF T[T,33]>=P[X*2+1] THEN P0=P[X*2+2]
7742 NEXT X
7745 IF P0=0 THEN GOTO 7750 ELSE Y5$(6)="Applicable Sales Rebate percentage:",Y5$(49)=STR(P0:"###.00")+"%"; GOSUB 7300
7748 Y5$(15)="PLUS: Sales Rebate amount:",Y5$(57)=STR(P0*T[T,33]/100:Z0$); GOSUB 7300
7750 GOSUB 7900; C0=T[T,31]-(T[T,6]-T[T,13])+P0*T[T,33]/100-B[6]
7755 Y5$(57)=T1$(1,LEN(Z0$)); GOSUB 7300; Y5$(12)="EQUALS: Amount to franchisee:",Y5$(57)=STR(C0:Z0$); GOSUB 7300
7760 IF C0<0 THEN Y5$(5)="** This amount will be deducted from your next check **"; GOSUB 7300
7795 RETURN 
7900 REM "Get A/R Invoices for the franchise customer code
7910 READ (Z[2],KEY=F0$(61,10))IOL=0320
7915 IF B[6]=0 THEN GOTO 7990
7920 GOSUB 7300; Y5$(15)="Amounts due PFG from A/R:",Y5$(57)=STR(B[6]:Z0$); GOSUB 7300
7990 RETURN 
8000 REM "Read additional data
8010 DIM B$(300),B[15]; READ (Z[2],KEY=A$(51,10),DOM=8011)IOL=0320
8090 RETURN 
8200 REM "GET NEXT INDEX TO FILE IN A9
8210 EXTRACT (F,IND=0,ERR=8295)IOL=0290
8212 IF S[4]=-2 THEN READ (F); GOTO 8270
8215 IF S[0]>=S[3] OR S[2]>=S[3] THEN GOTO 8300 ELSE S[4]=-2
8220 WRITE (F,IND=0)IOL=0290
8230 IF S[1]<1 THEN A9=S[2],S[2]=S[2]+1 ELSE A9=S[1]; READ (F,IND=A9)S[1]
8240 S[0]=S[0]+1,S[4]=-1
8250 WRITE (F,IND=0)IOL=0290
8255 U1=0
8260 RETURN 
8270 LOCK (F,ERR=8210); S[4]=-1; GOTO 8215
8295 IF ERR<>0 THEN GOTO 9000 ELSE RETRY 
8300 REM "END OF LINES
8310 S[4]=-1; WRITE (F,IND=0)IOL=0290
8320 CALL "ZZEXPF",X3$,X4$,"X"+STR(F),"",G9; IF G9>0 THEN GOTO 9000
8325 EXTRACT (F,IND=0)IOL=0290
8330 Z=NUM(FIN(F,"MAXREC")); S[3]=Z-1
8335 S[4]=-2; WRITE (F,IND=0)IOL=0290
8340 GOTO 8215
8390 CALL "ZZPROM",".4",X3$,0,"CM2NCS-8390: Cannot Expand APE file.  Call Support","","",0; GOTO 8390
8905 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
8910 DEF FND$(Z9$)=Z9$(3,2)+"/"+Z9$(5,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8920 DEF FNB$(Z9$)=X3$(214-6*NUM(Z9$),1)
8940 DEF FNS$(Z$)=Z$(1,POS("   "=Z$+"   ")-1)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 IF Y5=68 THEN RETRY ELSE ON C9 GOTO 1140,2040
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 RUN "ZMENU"
9999 END 
