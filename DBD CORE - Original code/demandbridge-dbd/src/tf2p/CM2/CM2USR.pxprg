0010 REM "Build CMB from CM4 <CM2USR>
0020 SETESC 9300; SETERR 9000
0035 REM "3.8 - 01/19/95 - 10.74
0040 REM "Copyright 1995 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="CM2USR",X1$="Get Sales Report Information"
0120 DIM Z0$(80,"-"),B0[3]
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30],A[31],A[32],A[33],A[34],A[35],A[36],A[37],A[38],A[39],A[40]
0320 IOLIST B$,B[0],B[1],B[2],B[3]
0322 IOLIST B0$,B0[0],B0[1],B0[2],B0[3]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O CM4...  02O CMB...  13O ZZPARM"
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0610 GOSUB 6000
0620 GOSUB 8100
0640 REM IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 00641,09900
0700 CALL "ZZINIT",STR(Z[2]:"00")
0800 REM "Enter dates
0805 DIM D0$(12),D[1]
0807 IF Q1$<>"" THEN D0$(1)=Q1$; GOSUB 6200; GOTO 0890
0810 CALL "ZZENTR","D",D{ALL},D0$,X4$,X3$,25,10,1,6,C0,"","","","CM2USR00","","",""
0830 CALL "ZZENTR","D",D{ALL},D0$,X4$,X3$,25,12,7,6,C0,"","","","CM2USR02","","",""
0831 IF ABS(C0)=2 THEN GOTO 0810
0835 IF D0$(7,6)<D0$(1,6) THEN GOTO 0830
0870 IF Q1$="" THEN CALL "ZZPROM","0",X3$,Z,"Continue to Start Processing.","","",0; ON Z GOTO 0871,9900,0810
0900 DIM A[40]
0905 C0=0
0940 READ (Z[2],KEY="",DOM=0941)
1000 REM 
1010 READ (Z[1],END=5000)IOL=0310; C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
1015 IF POS(A$(37,1)="12")=0 OR A$(16,6)<D0$(1,6) OR A$(16,6)>D0$(7,6) THEN GOTO 1010
1100 REM "Build record
1105 DIM B$(60),B[3]
1107 IF K$<>A$(1,12) THEN K$=A$(1,12),K0$=A$(51,10),K1$=A$(133,4) ELSE A$(51,10)=K0$; IF POS(" "<>A$(133,4))=0 THEN A$(133,4)=K1$ ELSE K1$=A$(133,4)
1110 B$(1)=A$(1,3)+A$(133,4)+A$(51,10)+A$(4,9)+A$(61,12)+A$(16,6)+A$(22,15),B[0]=A[0]-A[2],B[1]=A[2],B[2]=A[3],B[3]=A[0]-A[2]-A[3]
1115 IF B[0]=0 AND B[1]=0 AND B[2]=0 AND B[3]=0 THEN GOTO 1200; REM "Skip if all zeros
1120 WRITE (Z[2],KEY=B$(1,26),DOM=1121)IOL=0320; C0=C0+1; GOTO 1200
1121 REM "If already there, then add to buckets, if activity date> date in record, then update it and the reference number
1125 READ (Z[2],KEY=B$(1,26),DOM=1120)IOL=0322
1130 FOR I=0 TO 3; B0[I]=B0[I]+B[I]; NEXT I
1135 IF B$(39,6)>B0$(39,6) THEN B0$(27,18)=B$(27,18); REM "Change ref # & latest date
1138 IF B0[0]=0 AND B0[1]=0 AND B0[2]=0 AND B0[3]=0 THEN REMOVE (Z[2],KEY=B0$(1,26),DOM=1141); GOTO 1141; REM "If rec has gone to all zeros, then remove it
1140 WRITE (Z[2],KEY=B0$(1,26))IOL=0322
1995 GOTO 1010
5000 REM "EOJ
5005 PRINT @(10,15),FNR$(STR(C0:M0$))+" records created.",
5010 C=T; GOSUB 8150
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(10,4),"This program builds the Sales Report Information",@(10,5),"for the specified dates.",
6030 PRINT @(10,10),"Activity from:",@(21,12),"to:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "Display data
6210 CALL "ZZDISP","D",D0$(1,6),"",X3$,"","",25,10,X4$
6215 CALL "ZZDISP","D",D0$(7,6),"",X3$,"","",25,12,X4$
6245 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F$
8115 PRINT @(10,7),"There are "+FNR$(STR(T:M0$))+" records to process.",
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,C
8195 RETURN 
8905 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
