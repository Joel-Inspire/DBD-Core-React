0010 REM "RFQ Entry <QS2DAA>
0020 SETESC 9300; SETERR 9000
0035 REM "5.5 - 02/26/07 - 16.557222 - crg - SSP# 205460
0037 REM "205460-Oracle - FIN(CHANNEL,"NUMREC") changed to use FN%FIN$()     
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="QS2DAA",X1$="RFQ Entry",K9$="",K9=0
0120 DIM Z0$(80,"-"),A$(391),S0$(40)
0125 V8$="000813161999"
0130 K0=5,K1=1
0135 C9=-1
0145 IF Q1$="" THEN GOSUB 8950
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0200 REM "
0230 IF Q1$<>"" THEN X2=-1; PRINT @(0,6),'CE',
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1)
0320 IOLIST B$
0322 IOLIST B1$
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15]
0340 IOLIST D$,D[0],D[1],D[2],D[3]
0361 IOLIST F0$
0362 IOLIST F1$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X QSA...  02O AR3...  03O AR1...  04O AR2...  05O FEA...  06O FMP...  07O QSX...  08O ZP4... 09O QSE...  11O ZP0...  13O ZZPARM  15O FT3...  16O EML... 17O QS4... "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0550 READ (Z[13],KEY=X3$(9,3)+"F/M")P0$
0560 READ (Z[13],KEY=X3$(9,3)+"A/R")AR_PARMS$; REM " SSP 183590
0575 VALIDATE_QS4=0,VALIDATE_QS4=SGN(NUM(FN%FIN$(Z[17],"NUMREC"),ERR=*NEXT)) ! 189791 - If records in QS4 we will validate action code entry otherwise they can enter anything
0590 READ (Z[1],KEY=K9$,DOM=0591)
0600 REM "
0680 GOSUB 6000
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(244),C$(397),C[15],D$(54),D[3],B1$(244),F0$(20),F1$(20)
1035 DIM A$(480)
1036 %IL_IMAGE_TYPE$="",%IL_IMAGE_KEY$="",COMPLETE_FLAG_PREV$=" "
1045 A$(412)=A1$; IF A1$="" THEN A$(412)=K9$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1500
1200 REM "  RFQ number
1205 IF Q1$>"" THEN A$(412)=Q1$; GOTO 1212
1210 CALL "ZZENTR","AZXUX",A{ALL},A$,X4$,X3$,18,3,412,8,C0,"O/P","{1"+X$,"","QS2DAA00","QSA","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9800,6970
1240 GOTO 1120
1500 REM "
1505 Z9=-1,A2$="",A3$="",A4$="",A5$=""
1510 A1$=A$(412,8)
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1524 IF %HAS_IMAGE_LIB THEN %IL_IMAGE_TYPE$="RFQ",%IL_IMAGE_KEY$=A1$
1525 Z9=0,A2$=A$(6,4)+A$(10,10)+A$(412,8),COMPLETE_FLAG_PREV$=A$(475,1)
1530 FIND (Z[2],KEY=A$(6,4),DOM=1531)IOL=0320
1535 IF A$(391,1)="P" THEN FIND (Z[5],KEY=A$(10,10),DOM=1536)IOL=0330 ELSE IF A$(391,1)="T" THEN FIND (Z[8],KEY=A$(10,10),DOM=1536)IOL=0330 ELSE FIND (Z[3],KEY=A$(10,10),DOM=1536)IOL=0330
1540 FIND (Z[4],KEY=A$(176,2),DOM=1541)IOL=0340
1545 FIND (Z[2],KEY=A$(188,4),DOM=1546)IOL=0322
1550 FIND (Z[6],KEY="F"+A$(185,1),DOM=1551)IOL=0361
1555 FIND (Z[6],KEY="S"+A$(186,1),DOM=1556)IOL=0362
1560 FIND (Z[17],KEY=A$(187,1),DOM=*NEXT)QS4$
1590 GOTO 1700
1600 REM "
1620 IF Q1$="" THEN CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630 ELSE GOSUB 6450; GOTO 1635
1630 IF Q1$>"" THEN GOTO 9700 ELSE GOTO 1000
1635 A$(178,6)=X3$(21,6),A$(184,1)="Y",A$(391,1)="C",A$(373,6)=X3$(21,6)
1640 PRINT @(3,5),'SB',"Customer code:",'SF',
1650 IF POS(" "<>A$(373,6))<>0 THEN CALL "ZZDATE",X3$,"",A$(373,6),J0$,30,0,0,0,0,0,0,"","",""; A$(379,6)=J0$
1660 GOSUB 6240
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1820 CALL "ZZPROM",STR(6+C9),X3$,Z,"","","V",X0; ON Z GOTO 1850,6500,1630,6700
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310
1865 C9=1
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF INT(C0)=3 THEN GOSUB 7300 ELSE IF C0>3 OR X0<0 THEN GOTO 1800
2040 ON X0 GOTO 2100,2350,2150,3500,2200,2250,3150,2300,2500,3300,3350,3550,2550,2600,2650,2700,2750,2800,3100,2900,2950,3000,3050,3200,3400,3250,2850,3600,1800
2100 REM "Salesperson
2105 IF P0$(265,1)="Y" THEN X$="More than 1 RFQ parameter set, will not interface with order"
2110 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,43,3,6,4,C0,"","{3"+X$,"","QS2DAA02","AR3","AR2MBB",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2110,2111
2120 FIND (Z[2],KEY=A$(6,4),DOM=2110)IOL=0320
2125 PRINT @(50,3),B$(5,20),
2140 GOTO 2020
2150 REM "Customer code
2155 J$=A$(391,1)+A$(10,10)
2156 IF A$(391,1)="T" THEN GOTO 2020 ELSE IF A$(391,1)="P" THEN J0$="FEA",J1$="FE2PAA" ELSE J0$="AR1",J1$=""
2160 CALL "ZZENTR","AU",A{ALL},A$,X4$,X3$,18,5,10,10,C0,"A/R","{3"+X$,"","QS2DAA04",J0$,J1$,""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2160,2161
2164 IF POS(" "<>J$)=0 AND ABS(C0)=2 THEN GOTO 2190
2165 IF POS(" "<>J$)<>0 AND J$=A$(391,1)+A$(10,10) AND POS(" "<>A$(10,10))<>0 THEN GOTO 2190
2170 IF A$(391,1)="P" THEN FIND (Z[5],KEY=A$(10,10),DOM=2160)IOL=0330 ELSE FIND (Z[3],KEY=A$(10,10),DOM=2160)IOL=0330
2174 A$(20,35)=C$(11,35),A$(75,87)=C$(56,87),A$(55,20)=C$(165,20),A$(162,14)=C$(205,14),A$(176,2)=C$(359,2),A$(392,20)=C$(185,20),A$(420,14)=C$(143,12)
2175 FIND (Z[4],KEY=A$(176,2),DOM=2176)IOL=0340
2178 GOSUB 7600
2179 IF A$(391,1)="C" THEN CALL "AR2MAG",X3$,X4$,A$(10,10),12,4,X; IF X>0 THEN GOSUB 6400; REM "WO119291, dispaly RFQ entry customer message
2180 GOSUB 6232
2185 IF Z9<0 THEN X0=X0+8
2190 GOTO 2020
2200 REM "Company
2210 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,43,5,20,35,C0,"","{3"+X$,"","QS2DAA06","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2210,2211
2240 GOTO 2020
2250 REM "Contact
2260 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,18,6,55,20,C0,"","{3"+X$,"","QS2DAA08","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2260,2261
2290 GOTO 2020
2300 REM "Address
2305 A9$=A$(75,87)+A$(420,14)
2310 CALL "ZY2AAA",X3$,X4$,0,7,"C","",2,A9$,"",0,C0,A9
2315 A$(75,87)=A9$(1,87),A$(420,14)=A9$(88,14)
2340 GOTO 2020
2350 REM "Prospect or Customer?
2352 J$=A$(391,1)+A$(10,10)
2355 X$="P for prospect, T for Telemarketing, C for customer"
2357 IF A$(391,1)="T" THEN J0=0 ELSE J0=3
2360 CALL "ZZENTR","SUR",A{ALL},A$,X4$,X3$,J0,5,391,1,C0,"","{3"+X$,"PCT","","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2560,2561
2364 IF A$(391,1)=" " THEN A$(391,1)="C"
2365 IF A$(391,1)="T" THEN PRINT @(0,5),'SB',"   T/M List",'SF', ELSE IF A$(391,1)="P" THEN PRINT @(0,5),'SB',"   Prospect",'SF', ELSE PRINT @(0,5),'SB',"   Customer",'SF',
2370 IF A$(391,1)<>J$(1,1) THEN A$(10,10)=""; PRINT @(18,5),S0$(1,14),@(43,5),'CL',
2390 GOTO 2020
2500 REM "Telephone
2510 CALL "ZZENTR","P",A{ALL},A$,X4$,X3$,53,7,162,14,C0,"","{3"+X$,"","QS2DAA18","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2510,2511
2540 GOTO 2020
2550 REM "Terms code
2555 IF P0$(265,1)="Y" THEN X$="More than 1 RFQ parameter set, will not interface with order"
2560 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,18,12,176,2,C0,"","{3"+X$,"","QS2DAA20","AR2","AR2MCA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2560,2561
2570 FIND (Z[4],KEY=A$(176,2),DOM=2560)IOL=0340
2575 PRINT @(22,12),D$(3,20),
2590 GOTO 2020
2600 REM "Initial contact
2610 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,18,13,178,6,C0,"","{3"+X$,"","QS2DAA22","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2610,2611
2640 GOTO 2020
2650 REM "Print   RFQ (Y/N/P)
2655 X$="'Y' outputs the quotation to the Customer - If 'Y' then output type required"
2660 CALL "ZZENTR","SRU",A{ALL},A$,X4$,X3$,18,14,184,1,C0,"","{3"+X$,"YNC","QS2DAA24","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2660,2661
2690 GOTO 2020
2700 REM "F.O.B
2710 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,18,15,185,1,C0,"","{3"+X$,"F","QS2DAA26","FMPF","FM2SBA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2710,2711
2715 IF A$(185,1)=" " THEN GOTO 2740
2720 FIND (Z[6],KEY="F"+A$(185,1),DOM=2710)IOL=0361
2725 PRINT @(21,15),F0$(3,15),
2740 GOTO 2020
2750 REM "Ship Via
2760 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,18,16,186,1,C0,"","{3"+X$,"S","QS2DAA28","FMPS","FM2SCA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2760,2761
2765 IF A$(186,1)=" " THEN GOTO 2790
2770 FIND (Z[6],KEY="S"+A$(186,1),DOM=2760)IOL=0362
2775 PRINT @(21,16),F1$(3,15),
2790 GOTO 2020
2800 REM "Next action
2805 X$="Enter code: 1-9 or A-Z"
2806 IF VALIDATE_QS4 THEN LU$="QS4",MNT$="QS2MAA" ELSE LU$="",MNT$="" ! 189791
2810 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,18,17,187,1,C0,"","{3"+X$,"","QS2DAA30",LU$,MNT$,""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2810,2811
2815 IF VALIDATE_QS4 THEN IF NUL(A$(187,1)) THEN PRINT @(20,17),DIM(19), ELSE FIND (Z[17],KEY=A$(187,1),DOM=2810)QS4$; PRINT @(20,17),MID(QS4$,2,19),
2840 GOTO 2020
2850 REM " Customer Service Rep
2855 IF P0$(265,1)="Y" THEN X$="More than 1 RFQ parameter set, will not interface with order"
2860 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,54,18,188,4,C0,"","{3"+X$,"","QS2DAA32","AR3","AR2MBB",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2860,2861
2870 FIND (Z[2],KEY=A$(188,4),DOM=2871)IOL=0322
2875 PRINT @(59,18),B1$(5,20),
2890 GOTO 2020
2900 REM "Comments
2910 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,18,19,192,60,C0,"","{3"+X$,"","QS2DAA34","","","02V0"; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2910,2911
2940 GOTO 2020
2950 REM "Quote output
2952 IF A$(184,1)<>"Y" THEN GOTO 2020
2955 X$="Customer Quote Output: F=fax, P=printed",J$="FP"
2960 CALL "ZZENTR","SUR",A{ALL},A$,X4$,X3$,60,12,372,1,C0,"","{3"+X$,J$,"QS2DAA36","QSX","",""
2970 IF POS(" "<>A$(423,7))=0 AND A$(372,1)="F" THEN CALL "ZZPROM",".4",X3$,0,"Customer/Prospect must have fax number","","",0; GOTO 2960
2990 GOTO 2020
3000 REM "Estimate date
3010 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,60,13,373,6,C0,"","{3"+X$,"","QS2DAA38","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3010,3011
3025 IF POS(" "<>A$(373,6))<>0 AND POS(" "<>A$(379,6))=0 THEN CALL "ZZDATE",X3$,"",A$(373,6),J0$,30,0,0,0,0,0,0,"","",""; A$(379,6)=J0$; PRINT @(60,15),FND$(A$(379,6)),
3040 GOTO 2020
3050 REM "Quote good until
3060 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,60,14,379,6,C0,"","{3"+X$,"","QS2DAA40","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3060,3061
3090 GOTO 2020
3100 REM "Action date
3110 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,18,18,385,6,C0,"","{3"+X$,"","QS2DAA42","","",""
3140 GOTO 2020
3150 REM "Contact greeting
3160 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,53,6,392,20,C0,"","{3"+X$,"","QS2DAA44","","",""
3190 GOTO 2020
3200 REM "Vendor Reply by Date
3210 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,60,15,434,6,C0,"","{3"+X$,"","QS2DAA66","","",""
3240 GOTO 2020
3250 REM "Customer Reference
3260 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,60,17,440,15,C0,"","{3"+X$,"","QS2DAA68","","",""
3290 GOTO 2020
3300 REM "Ordered on
3305 IF ABS(C0)<>2 THEN X0=X0+1; GOTO 2020
3307 X$="This RFQ last ordered on this order number"
3310 CALL "ZZENTR","A",A{ALL},A$,X4$,X3$,53,8,455,8,C0,"O/P","{3"+X$,"","QS2DAA70","FS1","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3310,3311
3340 GOTO 2020
3350 REM "Ordered on date
3360 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,64,8,463,6,C0,"","{3"+X$,"","QS2DAA72","","",""
3390 GOTO 2020
3400 REM "Product needed by
3405 X$="Advise all vendors of this date"
3410 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,60,16,469,6,C0,"","{3"+X$,"","QS2DAA86","","",""
3440 GOTO 2020
3500 REM "T/M List code
3502 IF A$(391,1)<>"T" THEN GOTO 2020
3510 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,18,5,10,4,C0,"","{3"+X$,"","QS2DAA50","ZP0","ZP2MAA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3510,3211
3515 IF ABS(C0)=2 AND POS(" "<>A$(10,4))=0 THEN GOTO 2020
3520 FIND (Z[11],KEY=A$(10,4),DOM=3510)
3529 REM "Load pick a contact code and load into C$ as a customer
3530 DIM C$(599); X$="*"+A$(10,4); PRINT @(31,5),'SB',"Lookup Seq:",'SF',; CALL "AR2MAS",X3$,X4$,C$,X$; IF X$>"" THEN A$(14,6)=C$(405,6); GOSUB 6400 ELSE PRINT @(31,5),'SB',"   Company:",'SF',
3533 IF POS(" "<>C$)=0 THEN GOTO 3510; REM "If none picked, return to list code
3535 GOSUB 7700
3537 IF Z9<0 THEN X0=X0+5
3540 GOTO 2020
3550 REM "If using repeat order feature, enter repeat order number, find RFQ's that go with that order, show summary screen, decide which ones to repeat
3552 IF P0$(273,1)<>"Y" OR Z9>0 OR A$(391,1)<>"C" THEN GOTO 2020
3555 PRINT @(0,11),"Repeat order no:",'CL',
3557 X$="Enter Repeat Order No, lookup available, or blank for none"
3558 DIM TMP[1],RPT_ORD$(8)
3560 CALL "ZZENTR","AUX",TMP{ALL},RPT_ORD$,X4$,X3$,17,11,1,8,C0,"O/P","{1"+X$,A$(10,10),"QS2DAA52","FT7","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3560,3561
3565 IF STP(RPT_ORD$,3," ")="" THEN PRINT @(0,11),Z0$,; GOTO 2020
3570 DIM FT3$(100); READ (Z[15],KEY=RPT_ORD$,DOM=3558)FT3$; IF FT3$(6,10)<>A$(10,10) THEN CALL "ZZPROM",".4",X3$,0,"Repeat order customer code does not match RFQ customer code","","",0; GOTO 3558
3575 Q0$="R"; CALL "FM2ODT",X3$,X4$,Q0$,RPT_ORD$; REM "Display repeat order summary screen to make sure this is the correct order number
3580 IF Q0$="N" THEN GOSUB 6400; GOTO 3555; REM "Not the correct repeat order
3585 Q0$=A1$; CALL "QS2ODT",X3$,X4$,Q0$,RPT_ORD$; REM "Display RFQ summary which will allow selection of RFQ's to repeat
3586 IF Q0$="None" THEN GOSUB 6400; GOTO 3555; REM "None of the repeat order lines have an RFQ number attached
3587 Q$=STR(LEN(Q0$):"00")+Q0$,U1$="QS2DAZ"+Q$,U0$=X3$(1,6)+"PF"; GOTO 9720
3590 GOTO 2020
3600 ! RFQ Complete?
3610 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,75,12,475,1,C0,"","{3"+X$,"","QS2DAA52","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3610,3611
3640 GOTO 2020
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6050 PRINT @(4,3),"  RFQ number:",@(30,3),"Salesperson:",@(0,4),Z0$,
6055 IF A$(391,1)="T" THEN PRINT @(0,5),"   T/M List code:", ELSE IF A$(391,1)="P" THEN PRINT @(0,5),"   Prospect code:", ELSE PRINT @(0,5),"   Customer code:",
6060 PRINT @(34,5),"Company:",@(9,6),"Contact:",@(43,6),"Greeting:",
6065 CALL "ZY2AAA",X3$,X4$,0,7,"C","",0,"","",0,0,0
6070 PRINT @(43,7),"Phone No:",@(41,8),"Ordered on:",@(0,11),Z0$,@(11,12),"Terms:",@(1,13),"Initial contact:",@(47,12),"Output type:",@(0,14),"Output Quotation:",@(20,14),"(Yes, No, Completed)",@(45,13),"   Quote date:",@(11,15),"F.O.B:",@(45,14),"Quote expires:",@(43,15),"Vendor Reply by:",@(8,16),"Ship Via:",@(41,16),"Product needed by:",@(5,17),"Next action:",@(40,17),"Customer Reference:",@(5,18),"Action date:",@(36,18),"Cust service rep:",@(8,19),"Comments:",
6075 PRINT @(65,12),"Complete?"
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6205 PRINT 'SF',
6215 GOSUB 6450
6218 IF A$(391,1)="T" THEN PRINT @(0,5),'SB',"   T/M List code:",'SF', ELSE IF A$(391,1)="P" THEN PRINT @(0,5),'SB',"   Prospect code:",'SF', ELSE PRINT @(0,5),'SB',"   Customer code:",'SF',
6220 CALL "ZZDISP","A",A$(6,4),"AR2",X3$,"","",43,3,X4$
6224 PRINT @(50,3),B$(5,20),
6228 IF A$(391,1)="T" THEN PRINT @(18,5),A$(10,4),@(24,5),A$(14,6), ELSE CALL "ZZDISP","A",A$(10,10),"A/R",X3$,"","",18,5,X4$
6232 PRINT @(43,5),A$(20,35),@(18,6),A$(55,20),@(53,6),A$(392,20),
6235 CALL "ZY2AAA",X3$,X4$,0,7,"C","",1,A$(75,87)+A$(420,14),"",0,0,0
6237 CALL "ZZDISP","A",A$(455,8),"O/P",X3$,"","",53,8,X4$
6240 CALL "ZZDISP","P",A$(162,14),"",X3$,X$,"",53,7,X4$
6245 PRINT @(64,8),FND$(A$(463,6)),@(18,12),A$(176,2),@(22,12),D$(3,20),@(18,13),FND$(A$(178,6)),@(18,14),A$(184,1),@(18,15),A$(185,1),@(21),F0$(3,15),@(18,16),A$(186,1),@(21),F1$(3,15),@(18,17),A$(187,1),@(20,17),MID(QS4$,2,19),
6248 PRINT @(18,19),A$(192,60),@(18,20),A$(252,60),@(18,21),A$(312,60),@(60,12),A$(372,1),@(60,13),FND$(A$(373,6)),@(60,14),FND$(A$(379,6)),@(60,15),FND$(A$(434,6)),@(60,16),FND$(A$(469,6)),@(60,17),A$(440,15),@(18,18),FND$(A$(385,6)),
6250 CALL "ZZDISP","A",A$(188,4),"AR2",X3$,"","",54,18,X4$
6255 PRINT @(59,18),B1$(5,20),
6260 PRINT @(75,12),A$(475,1),
6331 A$(20,35)=C$(11,35)
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 CALL "ZZDISP","A",A$(412,8),"O/P",X3$,"","",18,3,X4$
6490 RETURN 
6500 REM "DELETE
6520 U1$="QS2DAZ14"+A1$+"DELETE"
6530 IF A2$>"" THEN U1$(7,2)="36",U1$=U1$+A2$
6540 GOTO 9700
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY
6704 %IL_IMAGE_TYPE$="",%IL_IMAGE_KEY$=""
6705 IF Z9=0 THEN GOTO 6790
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6715 REMOVE (Z[9],KEY="f"+A1$,DOM=6716)
6716 REMOVE (Z[9],KEY="p"+A1$,DOM=6717)
6730 IF A$(184,1)<>"Y" OR POS(A$(372,1)="FP")=0 THEN GOTO 6790
6740 X$="f"; IF A$(372,1)="P" THEN X$="p"
6749 PERFORM "QS2DAQ;CHECK_COMPLETE_FLAG",ERR=*NEXT
6750 WRITE (Z[9],KEY=X$+A1$)
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6825 A$(1,LEN(A1$))=A1$
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
7300 REM 
7310 ON FPT(C0)*10 GOTO 7390,7325,7330,7390
7325 IF V8$="" THEN X0=X0+4; GOTO 7390 ELSE X0=NUM(V8$(POS(STR(X0:"00")<=V8$,2),2)); GOTO 7390
7330 IF V8$="" THEN X0=X0-6; GOTO 7335 ELSE X0=POS(STR(X0-1:"00")<=V8$,2)-2; IF X0<0 THEN X0=0; GOTO 7390 ELSE X0=NUM(V8$(X0,2)); GOTO 7390
7335 IF X0<0 THEN X0=0; GOTO 7390
7390 C0=3; RETURN 
7600 REM "Default FOB and Howtoship
7602 IF AR_PARMS$(180,1)="Y" THEN A$(188,4)=X3$(40,3)+DIM(1); A$(188,4)=CVS(A$(188,4),4)
7605 FIND (Z[6],KEY="D"+A$(10,10),DOM=7640)D0$
7610 A$(185,1)=D0$(28,1),A$(186,1)=D0$(45,1)
7615 FIND (Z[6],KEY="F"+A$(185,1),DOM=7616)IOL=0361
7620 FIND (Z[6],KEY="S"+A$(186,1),DOM=7621)IOL=0362
7640 RETURN 
7700 REM " Setup based on T/M
7705 A$(20,35)=C$(11,35),A$(75,87)=C$(56,87),A$(55,20)=C$(165,20),A$(162,14)=C$(143,12),A$(176,2)=C$(359,2),A$(392,20)=C$(185,20),A$(162,14)=C$(205,14),A$(420,14)=C$(143,12)
7710 FIND (Z[4],KEY=A$(176,2),DOM=7711)IOL=0340
7715 GOSUB 7600
7720 GOSUB 6232
7745 RETURN 
8950 REM 
8955 CLOSE (14); OPEN (14)"ZZPARM"
8960 DIM U0$(6); U0$(1)=FID(0),U0$=U0$+"PF"
8965 FIND (14,KEY=U0$,DOM=8990)U0$,U1$
8970 IF U1$>"" THEN Q1=NUM(U1$(7,2)),Q1$=U1$(9,Q1),U1$=U1$(Q1+9)
8990 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9705 %IL_IMAGE_TYPE$="",%IL_IMAGE_KEY$=""
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9725 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9730 RUN U1$(1,6)
9800 REM "EXIT PROGRAM
9810 IF U0$>"" THEN GOTO 9700
9900 REM "END PROGRAM
9905 %IL_IMAGE_TYPE$="",%IL_IMAGE_KEY$=""
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56002 REM "205460-Oracle - FIN(CHANNEL,"NUMREC") changed to use FN%FIN$()     
