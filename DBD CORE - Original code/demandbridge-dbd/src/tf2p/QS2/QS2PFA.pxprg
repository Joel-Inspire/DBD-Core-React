0010 REM "RFQ Output <QS2PFA>
0035 REM "5.7 - 05/08/14 - 11.006388 - jvv - SSP# 270024
0037 REM "270024-840 doesn't output correctly in the field copy change front 
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "Vertical formats suppress blank lines and skip test pattern questions
0051 REM "NOTE: This program run from both QS2PFS and QS2PGS
0052 REM "F,P = Vendors Quotes -  f,p= Customer Quotes (F9$="Y")
0080 REM 
0085 IF J9=-99 THEN J9=0; GOTO 0650
0086 IF %GUI THEN GOTO 0095
0090 BEGIN ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="QS2PFA",X1$="RFQ Output"
0120 DIM A$(0),B$(157),B[24],C$(240),G$(18),G0$(18),H$(315),H0$(315),F$(288),F[35],S0$(80),QSD$,QSD[35] ! WO229740
0140 M1$="##,##0",M2$="##,###,###-",M3$="#,###,###.00-"
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0180 DEF FNS$(Z9$)=Z9$(1,POS(S1$=Z9$+S1$)-1)
0185 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
0195 DEF FNE$(Z9$)=CHR(NUM(Z9$(1,3))-190+65)+Z9$(4,1)
0210 T=1,T0=1,T1=1
0220 W3=120
0230 DIM T3$(50,"*"),Y5$(131),Y6$(131),W[4],S$(100)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR PO1 - RFQ header
0310 IOLIST A$
0315 REM "IOLIST FOR PO2 - RFQ Line
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19],B[20],B[21],B[22],B[23],B[24]
0325 REM "IOLIST FOR AP4 - Vendor Masterfile
0330 IOLIST C$
0335 REM "IOLIST FOR UM CONVERSION
0337 IOLIST UM$,UM[0],UM[1]
0345 REM "IOLIST FOR FM0 - Location Masterfile
0350 IOLIST E$
0360 IOLIST F$,F[0],F[1],F[2],F[3],F[4],F[5],F[6],F[7],F[8],F[9],F[10],F[11],F[12],F[13],F[14],F[15],F[16],F[17],F[18],F[19],F[20],F[21],F[22],F[23],F[24],F[25],F[26],F[27],F[28],F[29],F[30],F[31],F[32],F[33],F[34],F[35] ! WO229740
0365 REM "IOLISTS FOR FMP - FOB(G$), SHIPVIA(G0$)
0370 IOLIST G$
0372 IOLIST G0$
0374 IOLIST G1$
0375 REM "IOLISTS FOR AR3 - Salesperson & Customer Service Rep(H0$)
0380 IOLIST H$
0382 IOLIST H0$
0385 REM "IOLIST FOR FMS - Item Specification II
0390 IOLIST I$
0395 REM "IOLIST FOR FMU - Ply ( line detail )
0400 IOLIST J$,J0$,J[0],J[1],J[2],J[3],J[4],J[5],J[6],J[7],J[8],J[9],J[10],J[11],J[12],J[13],J[14],J[15],J[16],J[17],J[18],J[19],J[20],J[21],J[22],J[23],J[24],J[25],J[26],J[27],J[28],J[29],J[30],J[31],J[32],J[33],J[34],J[35],J[36]
0405 REM "IOLIST FOR FMV - Item spec comments
0410 IOLIST K$
0420 IOLIST L$,L[0],L[1]
0430 IOLIST M$,M[0],M[1],M[2],M[3],M[4],M[5],M[6],M[7],M[8],M[9],M[10],M[11],M[12],M[13],M[14],M[15],M[16],M[17],M[18],M[19],M[20],M[21],M[22],M[23],M[24],M[25],M[26],M[27],M[28],M[29],M[30],M[31]
0440 IOLIST N$
0450 IOLIST O$
0460 IOLIST D4$
0465 IOLIST D42$ ! WO238337
0470 IOLIST QSD$,QSD[0],QSD[1],QSD[2],QSD[3],QSD[4],QSD[5],QSD[6],QSD[7],QSD[8],QSD[9],QSD[10],QSD[11],QSD[12],QSD[13],QSD[14],QSD[15],QSD[16],QSD[17],QSD[18],QSD[19],QSD[20],QSD[21],QSD[22],QSD[23],QSD[24],QSD[25],QSD[26],QSD[27],QSD[28],QSD[29],QSD[30],QSD[31],QSD[32],QSD[33],QSD[34],QSD[35] ! WO22940
0490 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O QSA...  02O QSB...  03O AP4...  06O QSD...  07O FMP...  08O AR3...  09O AR2...  10O QSC...  11O QSV...  12X QSE...  13O ZZPARM 14O EF3... " ! SSP 232848
0520 GOSUB 9750
0600 REM "READ IN FORMATTING, SET W3 ETC.
0605 PMODE=0 ! SSP 241023
0610 DIM X9[169],Y9[169],X9$(169),Y9$(169*40),Y8[10],Y8$(35) ! SSP 244403
0620 RUN "QS2PFX"
0650 REM "RETURN FROM "QS2PFX"
0655 W3=Y8[1]
0660 DIM Y5$(W3),Y6$(W3)
0665 DIM S1$(W3); REM "For use by FNS$
0680 IF X3$(146,6)="QS2PGS" THEN F9$="Y" ELSE F9$=""
0690 READ (Z[13],KEY=X3$(9,3)+"F/M")P9$
0695 READ (Z[13],KEY=X3$(9,3)+"RFQ",DOM=*NEXT)PARM_RFQ$; REM SSP 201419
0700 REM "Open Printer
0735 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0736,9920
0740 W0=60
0745 X$=FID(W9),FORM_CODE$="840"+DIM(7)+V0$(59,1); CALL "EF2PRM",ERR=0746,X3$,X4$,X$,P6$,F6$,FORM_CODE$,FORM_PARM$; IF P6$(144,2)="JF" THEN JETFORM_MODE=1,JETFORM_DEV=W9,PMODE=JETFORM_MODE ELSE IF P6$(144,2)="UF" THEN UNFORM_MODE=2,UNFORM_DEV=W9,PMODE=UNFORM_MODE ! SSP 241023
0750 IF POS("F"=FID(W9))=1 THEN READ (Z[13],KEY=X3$(9,3)+"FX2",DOM=0755)C9$; GOTO 0770 ELSE GOTO 0770
0755 CALL "ZZPROM",".4",X3$,0,"You are not configured for the Fax output option!","","",0; GOTO 9900
0770 IF POS("F"=FID(W9))=1 THEN IF Y8$(40,1)<>"V" THEN CALL "ZZPROM",".4",X3$,0,"Sorry, You have selected fax output but a non-vertical format.","","",0; GOTO 9931 ! %GUI
0780 GOSUB 8100
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 IF F9$="" THEN DIM U[4]; U=22,U0=4,U[0]=0,U[1]=1,U[2]=8,U[3]=3,U[4]=10,T0=0,U0$="    ",T5$="                "; GOTO 0850
0825 IF F9$="Y" THEN DIM U[2]; U=9,U0=2,U[0]=0,U[1]=1,U[2]=8,T0=0,U0$="    ",T5$="            "; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0900 REM " Position read"
0905 REM "set key file to read from"
0910 U1=Z[12]
0920 REM V2$=V2$(1,20)
0924 REM "Based on V0$(72,1), add either FF for fax printing or PP for hardcopy to front of key string
0925 IF F9$="" THEN IF V0$(72,1)="F" THEN V2$="FF"+V2$ ELSE V2$="PP"+V2$
0927 IF F9$="Y" THEN IF V0$(72,1)="F" THEN V2$="ff"+V2$ ELSE V2$="pp"+V2$
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0959 REM "Adjust front of default key based on V0$(72,1)
0960 IF F9$="" THEN IF V0$(72,1)="F" THEN V2$(1,2)="FF" ELSE V2$(1,2)="PP"
0962 IF F9$="Y" THEN IF V0$(72,1)="F" THEN V2$(1,2)="ff" ELSE V2$(1,2)="pp"
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
0995 IF Q5=0 THEN GOSUB 6000
1000 REM "Read next record
1005 U$=KEY(U1,END=5000); READ (U1)
1006 IF F9$="Y" THEN U$=U$+S$(1,15)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN READ (U1,END=5000); EXITTO 1000
1030 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U=1 THEN EXITTO 5000 ELSE READ (U1,END=5000); EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1150 W=0
1160 DIM F$(288),F[35]; F$(1,11)=U$(2,8)+"001"; IF F9$="" THEN READ (Z[6],KEY=U$(2,11)+U$(23,2),DOM=1000)IOL=0360 ! WO229740
1300 REM "Read other data
1310 READ (Z[1],KEY=U$(2,8),DOM=1000)IOL=0310
1315 IF Q5=1 THEN GOSUB 7900; GOTO 1000
1317 IF F9$="" AND F$(67,1)<>"Y" THEN GOTO 1000
1320 DIM B$(158),B[24]; FIND (Z[2],KEY=U$(2,11),DOM=1321)IOL=0320
1330 DIM C$(309); FIND (Z[3],KEY=U$(13,10),DOM=1331)IOL=0330
1350 GOSUB 7550
1365 IF MID(D4$,1,2)<>MID(U$,2,2) THEN GOSUB 7700
1370 FIND (Z[7],KEY="F"+A$(185,1),DOM=1371)IOL=0370
1375 READ (Z[7],KEY="S"+A$(186,1),DOM=1376)IOL=0372
1380 READ (Z[8],KEY=A$(6,4),DOM=1381)IOL=0380
1385 READ (Z[8],KEY=A$(188,4),DOM=1386)IOL=0382
1390 DIM I$(60); READ (Z[9],KEY=A$(176,2),DOM=1391)IOL=0390
1500 REM "Print Data
1501 REM "Clear H7$ after each section
1505 GOSUB 7800; GOSUB SET_JOB_MODIFIER
1507 IF JETFORM_MODE THEN GOSUB IMAGE_LIBRARY_SETUP; CALL "EF2JET",X3$,X4$,P6$,"N",JETFORM_DEV,FORM_PARM$,JOB_MODIFIER$,RET$; REM "If in jetform mode and this is the first page for this rfq start a new file.
1508 IF UNFORM_MODE THEN GOSUB IMAGE_LIBRARY_SETUP; CALL "EF2UNF",X3$,X4$,P6$,"N",UNFORM_DEV,FORM_PARM$,JOB_MODIFIER$,RET$; REM "If in unform mode and this is the first page for this rfq start a new file.; !ssp 241023
1510 REM "Header
1515 IF X9$(11,1)="T" AND Y8$(40,1)="V" THEN GOSUB 7650; GOSUB 2900
1520 O5=0,F5=150; GOSUB 7600
1524 REM "Go ahead and print Fax header
1525 IF H9$<>"" THEN Y5$(INT((W3-LEN(H9$))/2))=H9$; GOSUB 7300; H9$=""
1560 REM "1st Section - Customer/Vendor
1570 F5=151; GOSUB 7600
1571 IF F9$="Y" THEN CALL "ZZDISP","AX",A$(10,10),"A/R",X3$,CUST$,"",0,0,X4$ ELSE CALL "ZZDISP","AX",F$(12,10),"A/P",X3$,VEND$,"",0,0,X4$
1580 GOSUB 2000
1585 H7$=""
1590 REM "2nd Section - RFQ Lines
1595 IF Y8$(40,1)="V" THEN O5=0 ELSE O5=Y8[3]-1
1600 F5=152; GOSUB 7600
1610 GOSUB 3500
1615 H7$=""
1620 REM "2nd Section - RFQ Item Master
1630 GOSUB 4000; IF Y8$(40,1)="V" THEN GOSUB 2900
1635 H7$=""
1640 REM "3rd Section - Plys
1642 IF UNFORM_MODE AND F9$="Y" THEN GOTO 1670 ! SP 241023 Bypass specs
1645 O4=1
1650 F5=153; GOSUB 7600
1655 IF Y8$(40,1)="V" THEN O5=0 ELSE O5=Y8[5]-1 ! JVV
1660 GOSUB 4100; IF O5>0 THEN IF Y8$(40,1)="V" THEN GOSUB 2900
1665 H7$=""
1670 REM "4th Section - Comp
1680 O5=0,F5=154; GOSUB 7600
1690 GOSUB 4050; IF Y8$(40,1)="V" THEN GOSUB 2900
1692 GOSUB 4380; IF Y8$(40,1)="V" THEN GOSUB 2900
1695 H7$=""
1700 REM "5th Section  - Comments
1701 F5=158; F5$=STP("PDFFILE:"+X3$(9,3)+DLM+"FX"+DLM+X3$(40,3)+DLM+FN%ZZDISP$(A$(412,8),"QSA")+"+"+F$(12,10)+"+"+"1",3," "); GOSUB 8300; REM " RFQ PDF FILE NAME
1720 O5=0,F5=155; GOSUB 7600
1730 GOSUB 4500; IF O5>0 AND Y8$(40,1)="V" THEN GOSUB 2900
1735 H7$=""
1750 IF Y8$(40,1)<>"V" THEN GOSUB 2900
1751 REM F5=158; F5$=STP("PDFFILE:"+X3$(9,3)+DLM+"FX"+DLM+X3$(40,3)+DLM+FN%ZZDISP$(A$(412,8),"QSA")+"+"+"1",3," "); GOSUB 8300; REM " RFQ PDF FILE NAME
1752 GOSUB QUOTE_MANAGER_EMAILS
1755 IF P6$="" THEN GOTO 1756 ELSE GOSUB 8060; CALL "EF2FAX",X3$,X4$,P6$,F6$,W9,X$,J6$,PMODE,EM$+"|"+A$(188,4); REM "Setup fax #s and Tag 2s, then  call fax routine ; !!SSP 241023
1800 REM "fix printed flag
1805 IF F9$="Y" THEN GOTO 1850
1810 F$(67,1)="C"
1820 WRITE (Z[6],KEY=U$(2,11)+U$(23,2))IOL=0360
1840 GOTO 1900
1850 REM "Customer quote
1860 EXTRACT RECORD (Z[1],KEY=U$(2,8),DOM=1900)X$
1870 X$(184,1)="C"; WRITE RECORD (Z[1])X$
1900 REM "Accumulate Totals"
1910 IF POS("F"=FID(W9))=1 THEN PRINT (W9)DIM(512); GOSUB 8800
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
2000 REM "Print 1st Section
2002 F5=1; CALL "ZZDISP","AX",A$(412,8),"O/P",X3$,F5$,"",0,0,X4$; GOSUB 8300
2004 F5=2,F5$=A$(6,4); GOSUB 8300
2005 IF MID(X9$,156,1)="E" THEN TYPE$="POX",DATA$=H$(189,10) ELSE TYPE$="PX",DATA$=H$(189,14) END_IF ; IF POS(" "<>DATA$)>0 THEN CALL "ZZDISP",TYPE$,DATA$,"",X3$,F5$,"",0,0,X4$; F5=156; GOSUB 8300; REM "WO115716, Salesperson phone number
2006 IF POS(" "<>H$(127,12))>0 THEN CALL "ZZDISP","POX",H$(127,12),"",X3$,F5$,"",0,0,X4$; F5=157; GOSUB 8300; REM "WO115716, Salesperson fax number
2007 F5=3,F5$=H$(149,20); GOSUB 8300
2008 F5=4; IF A$(391,1)="T" THEN F5$=A$(10,4)+"-"+A$(14,6) ELSE CALL "ZZDISP","AX",A$(10,10),"A/R",X3$,F5$,"",0,0,X4$ END_IF ; GOSUB 8300
2010 F5=5,F5$=A$(20,35); GOSUB 8300
2012 F5=6,F5$=A$(55,20); GOSUB 8300
2014 GOSUB 7750
2024 F5=12; CALL "ZZDISP","POX",A$(162,14),"",X3$,F5$,"",0,0,X4$; GOSUB 8300
2026 F5=13,F5$=FND$(A$(178,6)); GOSUB 8300
2028 F5=14,F5$=G$(3,15); GOSUB 8300
2030 F5=15,F5$=G0$(3,15); GOSUB 8300
2032 F5=16,F5$=A$(188,4); GOSUB 8300
2034 F5=17,F5$=H0$(149,20); GOSUB 8300
2036 IF POS(" "<>A$(373,6))>0 THEN F5=18,F5$=FND$(A$(373,6)); GOSUB 8300
2038 F5=19,F5$=A$(392,20); GOSUB 8300
2040 IF POS(" "<>A$(420,12))>0 THEN F5=20; CALL "ZZDISP","POX",A$(420,12),"",X3$,F5$,"",0,0,X4$; GOSUB 8300
2042 F5=21; CALL "ZZDISP","AX",F$(12,10),"A/P",X3$,X$,"",0,0,X4$; F5$=FNS$(X$); GOSUB 8300
2044 F5=22,F5$=F$(85,4); GOSUB 8300
2046 F5=23,F5$=C$(11,35); GOSUB 8300
2048 GOSUB 7500
2050 IF POS(" "<>C$(133,12))>0 THEN F5=25; CALL "ZZDISP","POX",C$(133,12),"",X3$,F5$,"",0,0,X4$; GOSUB 8300
2052 IF POS(" "<>A$(469,6))>0 THEN F5=26,F5$=FND$(A$(469,6)); GOSUB 8300
2054 IF POS(" "<>A$(434,6))>0 THEN F5=27,F5$=FND$(A$(434,6)); GOSUB 8300
2056 IF POS(" "<>A$(379,6))>0 THEN F5=28,F5$=FND$(A$(379,6)); GOSUB 8300
2058 F5=29,F5$=A$(176,2); GOSUB 8300
2060 F5=30,F5$=I$(3,30); GOSUB 8300
2062 F5=31,F5$=F$(22,10); GOSUB 8300
2064 F5=32,F5$=F$(32,10); GOSUB 8300
2065 REM "Date mishmosh needed for European Format
2066 F5=70; F5$=DAY,F5$=F5$(7,2)+F5$(1,2)+F5$(4,2); IF F5$(1,2)<"50" THEN F5$(1,2)=FNE$("20"+F5$(1,2)) ELSE F5$(1,2)=FNE$("19"+F5$(1,2))
2068 F5$=FND$(F5$); GOSUB 8300; F5=71,F5$=C$(155,20); GOSUB 8300
2070 F5=10,F5$=A$(440,15); GOSUB 8300
2072 IF X9$(11,1)<>"T" OR Y8$(40,1)<>"V" THEN GOSUB 7650
2090 IF Y8$(40,1)="V" THEN GOSUB 2900
2095 RETURN 
2900 REM "Send P$ to printer
2901 REM "O4<>0 means skip all blank lines
2902 IF JETFORM_MODE THEN PRINT (W9)P$,; GOTO 2935
2904 REM "Based on F6 determine last line to do O6,and reset F6
2907 IF F6=0 THEN GOTO 2940
2908 O6=INT(F6/W3),F6=0; IF Y8$(40,1)<>"V" THEN O6=Y8[0]-2
2910 REM "If heading is waiting to be printed, print it and clear H9$
2911 IF H9$<>"" THEN GOSUB 7300; Y5$(INT((W3-LEN(H9$))/2))=H9$; GOSUB 7300; H7$=H9$,H9$=""
2915 FOR I=0 TO O6+1
2920 Y5$=P$(I*W3+1,W3)
2925 IF O4=0 OR Y8$(40,1)<>"V" THEN GOSUB 7300 ELSE IF POS(" "<>Y5$)<>0 THEN GOSUB 7300
2930 NEXT I
2935 IF JETFORM_MODE THEN P$="",F4=0 ELSE DIM P$(Y8[0]*W3); F4=0
2940 RETURN 
3000 REM "If data not blank, then add data to masks and lay masks into item
3001 REM "O5 is offset to add to move line down for printing lines & plys
3005 IF POS(" "<>F5$)=0 THEN GOTO 3095
3010 REM "Lay in mask for first line
3015 O1=O5*W3+X9[F5]+Y9[F5]*W3,O0=(F5-1)*40+1
3019 IF POS(" "<>Y9$(O0,40))=0 THEN Y9$(O0,2)="<>"
3020 GOSUB 3100
3030 REM "Lay in mask for second lne
3035 O1=O5*W3+X9[F5]+(Y9[F5]+1)*W3,O0=(F5-1)*40+21,O3$=""
3040 GOSUB 3100
3095 RETURN 
3100 REM "Insert field into data if <> found and insert mask into from, except for the trailing blanks
3101 REM "F5$ is data, O0 is start of 20 char mask in Y9$, O1 is starting point in the item
3102 REM "Set O3$="*" if SECOND line printed
3105 IF POS(" "<>Y9$(O0,20))=0 THEN GOTO 3195 ELSE O0$=Y9$(O0,20)
3110 O2=POS("<>"=O0$); IF O2=0 THEN IF LEN(F5$)=1 AND F5$<>"Y" THEN GOTO 3195 ELSE GOTO 3150
3114 REM "Insert into string at pos of <>, If first part of line, strip trailing blanks of F5$
3115 IF O2=1 THEN O0$=FNS$(F5$)+O0$(3) ELSE O0$=O0$(1,O2-1)+F5$+O0$(O2+2)
3150 REM "Strip trailing blanks
3155 IF O0$(LEN(O0$))=" " THEN O0$=O0$(1,LEN(O0$)-1); GOTO 3155
3158 IF UNFORM_MODE AND UNF_SPEC THEN IF STP(O0$,2)<>"" THEN QS$="~"+STR(F5:"000")+"~"+O0$+"|"; P$(O1,LEN(QS$))=QS$; GOTO 3165 ! SSP 241023
3160 P$(O1,LEN(O0$))=O0$
3165 O3$="*"
3170 IF O1>F6 THEN F6=O1
3195 RETURN 
3500 REM "2nd Section
3504 DIM UDF$(50)
3505 READ (Z[2],KEY=A$(412,8),DOM=3506)
3510 READ (Z[2],END=3545)IOL=0320
3515 IF B$(150,8)<>A$(412,8) THEN GOTO 3545
3519 DIM J$(20),J[36]
3520 FIND (Z[10],KEY=B$(150,8)+B$(6,3),DOM=3521)IOL=0400
3525 DIM G1$(50); FIND (Z[7],KEY="T"+B$(20,3),DOM=3526)IOL=0374
3526 IF B$(6,3)="001" THEN UDF$=G1$
3530 GOSUB 3600
3535 O5=INT(F6/W3)+1
3540 GOTO 3510
3590 O5=0
3595 RETURN 
3600 REM "Line fields
3605 IF B$(99,1)="Y" THEN GOTO 3660; REM "Custom Forms
3606 IF F9$<>"Y" THEN GOTO 3890
3610 IF B$(9,1)="M" THEN F5=33,F5$=B$(6,3); GOSUB 8300; F5=40,F5$=B$(41,40); GOSUB 8300; GOTO 3890
3614 REM "Add support for other types here
3615 IF B$(9,1)<>"S" THEN GOTO 3890
3619 REM "Specials here
3620 F5=33,F5$=B$(6,3); GOSUB 8300
3622 F5=35,F5$=B$(10,10); GOSUB 8300
3624 F5=36,F5$=B$(20,3); GOSUB 8300
3626 F5=40,F5$=B$(41,40); GOSUB 8300
3628 F5=41,F5$=B$(95,4); GOSUB 8300
3630 F5=58,F5$=STR(B[17]:M3$); GOSUB 8300
3632 F5=59,F5$=STR(B[18]:M3$); GOSUB 8300
3634 F5=63,F5$=STR(J[25]:M3$); GOSUB 8300
3636 F5=64,F5$=STR(J[26]:M3$); GOSUB 8300
3638 F5=46,F5$=STR(B[0]:M1$); GOSUB 8300
3640 F5=61,F5$=J$(9,4); GOSUB 8300
3642 F5=62,F5$=STR(J[0]:M1$); GOSUB 8300
3648 REM "End of specials
3649 GOTO 3890
3660 REM "Do custom forms only
3666 F5=33,F5$=B$(6,3); GOSUB 8300
3668 F5=34,F5$=A$(10,10); GOSUB 8300
3670 F5=35,F5$=B$(10,10); GOSUB 8300
3672 F5=36,F5$=B$(20,3); GOSUB 8300
3674 F5=37,F5$=B$(23,8); GOSUB 8300
3676 F5=38,F5$=B$(31,8); GOSUB 8300
3678 F5=39,F5$=B$(39,2); GOSUB 8300
3680 F5=40,F5$=B$(41,40); GOSUB 8300
3682 F5=41,F5$=B$(95,4); GOSUB 8300
3684 F5=42,F5$=B$(102,12); GOSUB 8300
3686 F5=43,F5$=B$(114,20); GOSUB 8300
3688 F5=44,F5$=FND$(B$(134,6)); GOSUB 8300
3690 F5=45,F5$=G1$(5,15); GOSUB 8300
3692 F5=46,F5$=STR(B[0]:M1$); GOSUB 8300
3694 F5=47,F5$=STR(B[1]:M2$); GOSUB 8300
3696 GOSUB 7000; F5=48,F5$=STR(B[2]/UM0:M2$); GOSUB 8300
3698 GOSUB 7000; F5=49,F5$=STR(B[3]/UM0:M2$); GOSUB 8300
3700 GOSUB 7000; F5=50,F5$=STR(B[4]/UM0:M2$); GOSUB 8300
3702 GOSUB 7000; F5=51,F5$=STR(B[5]/UM0:M2$); GOSUB 8300
3704 GOSUB 7000; F5=52,F5$=STR(B[6]/UM0:M2$); GOSUB 8300
3706 IF B[12]<>0 THEN F5=53,F5$=STR(B[12]:M3$); GOSUB 8300
3708 IF B[13]<>0 THEN F5=54,F5$=STR(B[13]:M3$); GOSUB 8300
3710 IF B[14]<>0 THEN F5=55,F5$=STR(B[14]:M3$); GOSUB 8300
3712 IF B[15]<>0 THEN F5=56,F5$=STR(B[15]:M3$); GOSUB 8300
3714 IF B[16]<>0 THEN F5=57,F5$=STR(B[16]:M3$); GOSUB 8300
3716 IF B[17]<>0 THEN F5=58,F5$=STR(B[17]:M3$); GOSUB 8300
3718 IF B[18]<>0 THEN F5=59,F5$=STR(B[18]:M3$); GOSUB 8300
3720 F5=60,F5$=STR(B[24]:M1$); GOSUB 8300
3722 F5=61,F5$=J$(9,4); GOSUB 8300
3724 F5=62,F5$=STR(J[0]:M1$); GOSUB 8300
3726 F5=63,F5$=STR(J[25]:M3$); GOSUB 8300
3728 F5=64,F5$=STR(J[26]:M3$); GOSUB 8300
3740 IF B[12]<>0 THEN QBREAK=B[2],UNIT$=B$(95,4),QTY_PER=B[0],PRICE=B[12]; GOSUB 8500; IF EXT<>0 THEN F5=159,F5$=STR(EXT:M3$); GOSUB 8300 ! WO229284, selling price extension #1
3741 IF B[13]<>0 THEN QBREAK=B[3],UNIT$=B$(95,4),QTY_PER=B[0],PRICE=B[13]; GOSUB 8500; IF EXT<>0 THEN F5=160,F5$=STR(EXT:M3$); GOSUB 8300 ! WO229284, selling price extension #2
3742 IF B[14]<>0 THEN QBREAK=B[4],UNIT$=B$(95,4),QTY_PER=B[0],PRICE=B[14]; GOSUB 8500; IF EXT<>0 THEN F5=161,F5$=STR(EXT:M3$); GOSUB 8300 ! WO229284, selling price extension #3
3743 IF B[15]<>0 THEN QBREAK=B[5],UNIT$=B$(95,4),QTY_PER=B[0],PRICE=B[15]; GOSUB 8500; IF EXT<>0 THEN F5=162,F5$=STR(EXT:M3$); GOSUB 8300 ! WO229284, selling price extension #4
3744 IF B[16]<>0 THEN QBREAK=B[6],UNIT$=B$(95,4),QTY_PER=B[0],PRICE=B[16]; GOSUB 8500; IF EXT<>0 THEN F5=163,F5$=STR(EXT:M3$); GOSUB 8300 ! WO229284, selling price extension #5
3749 ! WO229740 - Use QSC record to see if each qty break has vendor number and if so then read corresponding QSD (see 8550's) to get number of working days
3750 IF J[32]>0 THEN QTY_BREAK=1,VENDOR_NUM=J[32]; GOSUB 8550; IF WORKING_DAYS<>0 THEN F5=164,F5$=STR(WORKING_DAYS:"####0"); GOSUB 8300 ! WO229740, number of working days break #1
3751 IF J[33]>0 THEN QTY_BREAK=2,VENDOR_NUM=J[33]; GOSUB 8550; IF WORKING_DAYS<>0 THEN F5=165,F5$=STR(WORKING_DAYS:"####0"); GOSUB 8300 ! WO229740, number of working days break #2
3752 IF J[34]>0 THEN QTY_BREAK=3,VENDOR_NUM=J[34]; GOSUB 8550; IF WORKING_DAYS<>0 THEN F5=166,F5$=STR(WORKING_DAYS:"####0"); GOSUB 8300 ! WO229740, number of working days break #3
3753 IF J[35]>0 THEN QTY_BREAK=4,VENDOR_NUM=J[35]; GOSUB 8550; IF WORKING_DAYS<>0 THEN F5=167,F5$=STR(WORKING_DAYS:"####0"); GOSUB 8300 ! WO229740, number of working days break #4
3754 IF J[36]>0 THEN QTY_BREAK=5,VENDOR_NUM=J[36]; GOSUB 8550; IF WORKING_DAYS<>0 THEN F5=168,F5$=STR(WORKING_DAYS:"####0"); GOSUB 8300 ! WO229740, number of working days break #5
3895 RETURN 
4000 REM "Item Master Data - QS1 - Section 2 'cont
4005 Y$="02C QSB...  03C AP4...  09C AR2...  02O QSS...  03O QSU...  09O QS1...  "; GOSUB 9750
4010 DIM M$(387),M[31],N$(159),O$(141)
4015 REM "Read QS1 info into M$ and M()
4020 READ (Z[9],KEY=F$(1,11),DOM=4040)IOL=0430; M$=M$(12)
4030 GOSUB 4400
4040 RETURN 
4050 REM "Read Composition Section - QSS - Section 4
4054 REM "First check to see if user def spec and no std comp screen required
4055 IF P9$(168,1)="Y" THEN IF G1$(33,3)<>"   " AND G1$(21,1)="Y" THEN GOTO 4090
4060 READ (Z[2],KEY=F$(1,11),DOM=4070)IOL=0440; N$=N$(12)
4065 GOSUB 4300
4070 REM 
4080 Y$="02C QSS...  03C QSU...  09C QS1...  02O QSB...  03O AP4...  09O AR2...  "; GOSUB 9750
4090 RETURN 
4100 REM "Read thru Ply Section - QSU - Section 3
4110 I0=1,F4=0; READ (Z[3],KEY=F$(1,11),DOM=4111)
4120 READ (Z[3],END=4170)IOL=0450
4130 IF O$(1,11)<>F$(1,11) THEN GOTO 4170 ELSE O$=O$(12)
4140 GOSUB 4200; I0=I0+F4*W3,O5=O5+F4
4150 GOTO 4120
4195 RETURN 
4200 REM "Print QSU plys - Section (3)
4202 F5=72,F5$=STR(NUM(O$(21,3)):"##"); GOSUB 8350
4204 F5=73,F5$=O$(24,3); GOSUB 8350
4206 F5=74,F5$=O$(27,3); GOSUB 8350
4208 F5=75,F5$=O$(30,6); GOSUB 8350
4210 F5=76,F5$=O$(36,8); GOSUB 8350
4212 F5=77,F5$=O$(44,8); GOSUB 8350
4214 F5=78,F5$=O$(52,6); GOSUB 8350
4216 F5=79,F5$=O$(58,6); GOSUB 8350
4218 F5=80,F5$=O$(64,6); GOSUB 8350
4220 F5=81,F5$=O$(70,6); GOSUB 8350
4222 F5=82,F5$=O$(76,10); GOSUB 8350
4224 F5=83,F5$=O$(86,1); GOSUB 8350
4226 F5=84,F5$=O$(87,2); GOSUB 8350
4227 ! F5=85,F5$=STR(NUM(O$(89,1),ERR=4228):"##"); GOSUB 8350; GOTO 4229; !SSP 270024
4228 F5=85,F5$=O$(89,1); GOSUB 8350
4229 ! F5=86,F5$=STR(NUM(O$(90,1),ERR=4230):"##"); GOSUB 8350; GOTO 4231; !SSP 270024
4230 F5=86,F5$=O$(90,1); GOSUB 8350
4232 F5=87,F5$=O$(91,8); GOSUB 8350
4234 F5=88,F5$=O$(99,3); GOSUB 8350
4236 F5=89,F5$=O$(130,1); GOSUB 8350
4238 F5=90,F5$=O$(102,8); GOSUB 8350
4240 F5=91,F5$=O$(110,20); GOSUB 8350
4242 IF LEN(O$)>133 THEN F5=92,F1$=FNS$(O$(131,1)+"  "+O$(132,1)+"  "+O$(133,1)+"  "+O$(134,1))
4295 RETURN 
4300 REM "Print QSS - Item Comp Section (4)
4305 IF UNFORM_MODE THEN UNF_SPEC=1 ELSE UNF_SPEC=0 ! SSP 241023
4310 FOR I=94 TO 96
4312 F5=I,F5$=N$(I-73,1); GOSUB 8300
4314 NEXT I
4315 FOR I=97 TO 106; F5=I,F5$=N$(I-72,1); GOSUB 8300; NEXT I
4316 F5=107,F5$=STR(NUM(N$(35,3))); GOSUB 8300
4318 F5=108,F5$=N$(48,5); GOSUB 8300
4320 F5=109,F5$=N$(53,2); GOSUB 8300
4322 F5=110,F5$=N$(55,3); GOSUB 8300
4330 FOR I=111 TO 125
4332 F5=I,F5$=N$(I-53,1); GOSUB 8300
4334 NEXT I
4340 F5=126,F5$=N$(73,6); GOSUB 8300
4342 F5=127,F5$=N$(79,8); GOSUB 8300
4344 F5=128,F5$=N$(87,1); GOSUB 8300
4346 F5=129,F5$=N$(88,5); GOSUB 8300
4348 F5=130,F5$=N$(93,8); GOSUB 8300
4350 F5=131,F5$=N$(101,1); GOSUB 8300
4352 F5=132,F5$=N$(102,1); GOSUB 8300
4354 F5=133,F5$=N$(103,4); GOSUB 8300
4356 F5=134,F5$=N$(107,1); GOSUB 8300
4358 F5=135,F5$=N$(108,1); GOSUB 8300
4360 F5=136,F5$=N$(109,1); GOSUB 8300
4362 F5=137,F5$=N$(110,3); GOSUB 8300
4363 F5=138,F5$=N$(113,1); GOSUB 8300
4364 F5=139,F5$=N$(38,4); GOSUB 8300
4366 F5=140,F5$=N$(42,5); GOSUB 8300
4368 F5=141,F5$=N$(114,15); GOSUB 8300
4370 F5=142,F5$=N$(129,15); GOSUB 8300
4372 F5=143,F5$=N$(144,15); GOSUB 8300
4375 UNF_SPEC=0 ! SSP 241023
4379 RETURN 
4380 REM "User defined specs?
4382 Y$="02C QSS...  03C QSU...  09C QS1...  02O QSB...  03O AP4...  09O AR2...  "; GOSUB 9750
4385 IF P9$(168,1)="Y" AND UDF$(33,3)<>"   " THEN CALL "PO2PAC",X3$,X4$,P$,F$(1,11),UDF$,W3,V0$(81,1),V3$,F6,W9,PMODE ! SSP 241023
4386 IF UNFORM_MODE THEN F5=169,F5$=UDF$(33,3)+UDF$(21,1); GOSUB 8300 ! SSP 241023
4390 RETURN 
4400 REM "Print QS1 (Still part of section 2)
4495 RETURN 
4500 REM "3rd Section
4530 O5=0,F5=65,F5$=A$(192,60); GOSUB 8300
4532 O5=O5+1,F5$=A$(252,60); GOSUB 8300
4534 O5=O5+1,F5$=A$(312,60); GOSUB 8300; O5=0
4536 F5=68,F5$=F$(42,20)+F$(70,15); GOSUB 8300
4540 IF Y8$(40,1)="V" THEN GOSUB 2900
4554 REM "Print spec comments
4555 DIM SPEC$[30]; SPEC_COUNT=0; READ (Z[11],KEY=F$(1,11),DOM=4556)
4560 READ (Z[11],END=4580)IOL=0410
4565 IF K$(1,11)<>F$(1,11) THEN GOTO 4580
4567 IF K$(35,1)="N" THEN GOTO 4575 ELSE SPEC_COUNT=SPEC_COUNT+1; SPEC$[SPEC_COUNT]=K$(36,65)
4570 IF X9[69]<>0 THEN F5=69,F5$=K$(36,65); GOSUB 8300; O5=O5+1
4575 GOTO 4560
4580 REM "print specs from spec count
4581 IF SPEC_COUNT=0 THEN GOTO 4590
4582 FOR SPEC_TEMP=1 TO 4
4583 IF SPEC_TEMP<=SPEC_COUNT THEN F5=8,F5$=SPEC$[SPEC_TEMP]; GOSUB 8300
4584 IF SPEC_TEMP+4<=SPEC_COUNT THEN F5=9,F5$=SPEC$[SPEC_TEMP+4]; GOSUB 8300
4585 O5=O5+1
4588 NEXT SPEC_TEMP
4595 RETURN 
4800 JETFORM_PRINT:REM "For jetform mode add field nominated value with F5 as the field name and F5$ as the value
4815 P$=P$+"^field "+STP(P6$(146,3),1)+STR(F5:"000")+'LF'+F5$+'LF'
4845 JETFORM_PRINT_END:RETURN 
4850 SET_JOB_MODIFIER:REM "Set job_modifier$ to '' or the template code if user defined specs will be printed. job_modifier$ will be passed to EF2JET - NOTE:Checks ONLY the first line of the RFQ for a user defined spec request
4855 JOB_MODIFIER$="",TMP$=""
4858 IF P9$(168,1)<>"Y" THEN GOTO 4895
4860 READ (Z[2],KEY=A$(412,8),DOM=4861)
4861 READ (Z[2],END=4865)IOL=0320
4863 TMP$=B$(20,3)
4870 FIND (Z[7],KEY="T"+TMP$,DOM=4871)X$; IF LEN(X$)>34 THEN IF X$(33,3)<>"   " THEN JOB_MODIFIER$=X$(33,3)
4895 SET_JOB_MODIFIER_END:RETURN 
5000 REM "End of Print
5010 IF Q5=1 THEN GOTO 9920
5020 IF W8=0 THEN GOTO 5200
5200 REM "
5291 GOTO 9900
5300 REM "Remove printed P/O's
5301 IF POS(X3$(9,3)="377")<>0 THEN COMMENT$="Did all of the RFQs Email correctly?" ELSE COMMENT$="Did all of the RFQs output correctly?" ! ssp#252222
5305 IF V8$="A" THEN GOTO 5330
5310 CALL "ZZPROM",".Y",X3$,Z,COMMENT$,"","",0
5311 REM "CALL "ZZPROM",".Y",X3$,Z,"Did all of the RFQs print correctly?","","",0
5320 ON Z GOTO 5330,5340
5330 GOSUB 5350; GOTO 5345
5345 RETURN 
5350 REM "Do remove
5354 PRINT @(0,19),'CE',
5355 CALL "ZZPROM","X0PO2PAA",X3$,Z,"","","",19
5359 REM "5360 WAS:LET Q5=1; DIM V2$(20,"~"); LET V2$(1,2)="PP",V2$(3,9)=""; GOTO 0900
5360 Q5=1; GOTO 0990
5390 RETURN 
6000 REM "Print alignment pattern?
6005 F8$=""; IF V8$="A" OR Y8$(40,1)="V" THEN GOTO 6090
6010 IF V3$(2,2)="A " THEN PRINT 'EA',
6015 CALL "ZZPROM","R4",X3$,Z,"","","",X0
6017 IF Z<2 AND V3$(2,2)="A " THEN PRINT 'BA',
6020 ON Z GOTO 6090,6021,9900
6025 F8$="Y"
6030 GOSUB 6100
6040 GOSUB 6150
6050 GOTO 6010
6090 RETURN 
6100 REM "Setup test data"
6105 X$="X",X5$="I6XXXX"
6110 DIM A$(480,X$),B$(118,X$),B[19],C$(200,X$),D$(200,X$),D4$(200,X$),E$(225,X$),F$(200,X$),F[17],G$(20,X$),G0$(20,X$),G1$(20,X$),H$(315,X$),H0$(315,X$),I$(159,X$),J$(130,X$),K$(90,X$),G2$(300,X$),D42$(200,X$),DIV$(200,X$) ! WO238337
6120 A$(204,6)=X5$,A$(192,6)=X5$,C$(16,6)=X5$,A$(224,6)=X5$,J$(21,3)="000",A$(242,6)=X5$,A$(291,6)=X5$,C$(55,6)=X5$,I$(103,4)="0999",A$(326,2)="99",I$(42,5)="99999",A$(190,1)="E"; A$(469,2)="J5",A$(471,4)="0131"
6125 I$(68,5)="YYYYY"
6130 A$(178,2)="J2",A$(373,2)="J2",A$(434,2)="J2",A$(379,2)="J2"
6140 RETURN 
6150 REM " Print align. pattern
6160 GOSUB 7800
6170 GOSUB 2000
6180 GOSUB 2900
6185 IF P6$="" THEN GOTO 6186 ELSE GOSUB 8060; CALL "EF2FAX",X3$,X4$,P6$,F6$,W9,X$,J6$,PMODE,EM$+"|"+A$(188,4); REM "Setup fax #s and Tag 2s, then  call fax routine ; !SSP 241023
6190 RETURN 
6200 REM "COMPANY ADDRESS (FAX ONLY) OR TOP BLANK LINES
6202 Q$=C$(133,12); IF F9$="Y" THEN Q$=A$(420,12)
6205 IF POS("F"=FID(W9))<>1 OR POS(" "<>Q$)=0 THEN GOTO 6290
6210 X$=FID(W9); CLOSE (W9); OPEN (W9)X$
6215 CALL "FX2SBA",X3$,X4$,Q$,C9$,N9$,Z; IF Z=1 THEN EXITTO 9900
6218 PRINT (W9)N9$
6219 IF X9$(11,1)="T" AND Y8$(40,1)="V" THEN IF C9$(23,3)<>"// " THEN GOTO 6290 ELSE X$=FNS$(C9$(23,78)),Y5$(1)=X$; GOSUB 7300; GOTO 6290
6220 FOR X=0 TO 5
6230 X$=FNS$(C9$(23+X*78,78)),Y5$(INT(W3/2-LEN(X$)/2))=X$
6235 GOSUB 7300
6260 NEXT X
6290 RETURN 
6500 REM "Start New Page
6501 REM "Print vertical offset, Fax header and 1st section again. Reset F6
6504 REM "Vertical Offset
6505 IF Y8[2]>0 THEN FOR I=1 TO Y8[2]; GOSUB 7300; NEXT I
6506 F6=0,O4=0
6510 REM "Header
6520 F5=150; GOSUB 7600
6524 REM "Go ahead and print Fax header
6525 IF H9$<>"" THEN Y5$(INT((W3-LEN(H9$))/2))=H9$; GOSUB 7300; H9$=""
6526 H7$=""
6530 REM "1st Section - Customer/Vendor
6535 F5=151; GOSUB 7600
6540 GOSUB 2000
6543 H7$=""
6545 RETURN 
7000 REM " Convert to qty in UM
7010 IF F9$="Y" THEN DIM UM$(21),UM[1]; FIND (Z[13],KEY="U/M"+B$(95,4),DOM=7011)IOL=0337; REM SSP 196643
7011 IF F9$<>"Y" THEN DIM UM$(20),UM[1]; FIND (Z[13],KEY="U/M"+J$(9,4),DOM=7011)IOL=0337; REM SSP 196643
7015 IF UM$(21,1)="Y" THEN UM0=1; GOTO 7090
7020 IF UM$(20,1)="Y" THEN UM0=UM[1] ELSE UM0=B[0]
7025 IF UM$(20,1)="Y" AND UM[0]=0 THEN UM0=UM[1]
7030 IF UM0=0 THEN UM0=1,UM0$=""; GOTO 7890
7090 RETURN 
7300 REM "Output line Y5$ to output device
7310 W=W+1
7315 REM IF W+2>Y8(0) THEN GOSUB 07350
7330 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7345 RETURN 
7350 REM "Handle new page
7355 IF W<Y8[0] THEN PRINT (W9)""; W=W+1; GOTO 7355
7360 W=0
7364 REM "Save the current line to print after head up and the print array and its position info!
7365 Y2$=Y5$,H6$=H7$,I9=I,O8=O6,O3=O4,F4=0; IF JETFORM_MODE THEN P$="",P0$="" ELSE P0$=P$; DIM P$(Y8[0]*W3),Y5$(W3)
7370 GOSUB 6500
7372 IF H6$<>"" THEN GOSUB 7300; Y5$(INT((W3-LEN(H6$))/2))=H6$; GOSUB 7300
7375 Y5$=Y2$,P$=P0$,P0$="",H7$=H6$,I=I9,O6=O8,O4=O3
7395 RETURN 
7400 REM "Read report selection parameters"
7405 CLOSE (14); OPEN (14)"ZZP"
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (14,KEY=X3$(1,8),DOM=7451)IOL=0490
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1)
7482 IF V1$(1,3)="???" THEN V8$="A",V7$=V1$(4,2); IF V0$(71,1)="1" THEN V1$="PO6   "
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7486 CLOSE (14)
7490 RETURN 
7500 REM 'PRINT VENDOR ADDRESS
7505 O5=0,F5=24,F5$=C$(46,30); GOSUB 8300
7510 IF POS(" "<>C$(76,30))>0 THEN F5=24,O5=O5+1,F5$=C$(76,30); GOSUB 8350
7515 J5$=FNS$(C$(106,16))
7520 J5$=J5$+", "+C$(122,2)
7522 CALL "ZZDISP","AX",C$(124,9),"ZIP",X3$,J4$,"",0,0,X4$
7525 J5$=J5$+"  "+J4$
7530 F5=24,O5=O5+1,F5$=J5$; GOSUB 8350; O5=0
7545 RETURN 
7550 REM "Check for plant code, if found load into vendor address field
7552 PLANT_CODE$="",PLANT_FAX$="" ! SSP 232848
7555 IF POS(" "<>F$(85,4))=0 THEN GOTO 7595
7557 Y$="09C AR2...  09O FM0...  "; GOSUB 9750
7560 DIM E$(150); FIND (Z[9],KEY="P"+F$(12,10)+F$(85,4),DOM=7570)IOL=0350
7565 C$(11,163)=E$(16,163)
7568 PLANT_CODE$=MID(F$,85,4),PLANT_FAX$=MID(E$,138,12) ! SSP 232848 !SSP#236879
7570 Y$="09C FM0...  09O AR2...  "; GOSUB 9750
7595 RETURN 
7600 REM "Given a format number F5, get its heading and load into H9$ for printing by 2900's if needed.
7601 IF X9[F5]<>0 AND F5=158 THEN GOTO 7605
7602 IF X9[F5]=0 OR Y8$(40,1)<>"V" THEN RETURN 
7605 H9$=FNS$(Y9$((F5-1)*40+1,40))
7610 H0=INT((W3-LEN(H9$))/2)
7615 IF POS(" "<>H9$)=0 THEN H9$=""
7645 RETURN 
7650 REM "Division Address
7660 O5=0,F5$=DIV$(11,35),F5=11; GOSUB 8300 ! WO238337 - changed to DIV$
7670 F5$=DIV$(46,35),F5=11,O5=O5+1; GOSUB 8350 ! WO238337 - changed to DIV$
7680 F5$=DIV$(81,35),F5=11,O5=O5+1; GOSUB 8350; O5=0 ! WO238337 - changed to DIV$
7690 RETURN 
7700 REM "Read division record
7710 Y$="09C AR2...  09O FSH...  "; GOSUB 9750
7720 DIM D4$(256); FIND (Z[9],KEY=U$(2,2),DOM=7721)IOL=0460
7725 GOSUB AR3_ALT_DIV ! WO238337 - check salesperson record for alternate division for RFQ printing of field 11, either way put division info into DIV$ for this field (7660-7680)
7730 Y$="09C FSH...  09O AR2...  "; GOSUB 9750
7740 RETURN 
7750 REM "Print Cust/Prospect Address
7760 O5=0,F5=7,F5$=A$(75,30); GOSUB 8300
7770 IF POS(" "<>A$(105,30))>0 THEN F5=7,F5$=A$(105,30),O5=O5+1; GOSUB 8300
7775 CALL "ZZDISP","AX",A$(153,9),"ZIP",X3$,F5$,"",0,0,X4$
7780 F5=7,F5$=FNS$(A$(135,16))+", "+A$(151,2)+" "+F5$,O5=O5+1; GOSUB 8300; O5=0
7790 RETURN 
7800 REM "SETUP
7810 IF JETFORM_MODE THEN P$="" ELSE DIM P$(Y8[0]*W3)
7820 IF Y8$(40,1)="V" AND W2<>1 THEN GOSUB 6200
7830 REM "Vertical Offset
7840 IF Y8[2]>0 THEN FOR I=1 TO Y8[2]; GOSUB 7300; NEXT I
7850 F6=0,O4=0
7890 RETURN 
7900 REM "Remove QSE print records
7910 CALL "ZZDISP","A",U$(2,8),"O/P",X3$,"","",34,21,X4$
7920 IF F9$<>"Y" THEN IF F$(67,1)<>"Y" THEN REMOVE (Z[12],KEY=U$,DOM=7921); GOTO 7940
7930 IF A$(184,1)<>"Y" THEN REMOVE (Z[12],KEY=U$(1,9),DOM=7931)
7940 RETURN 
8000 REM "Build desc from H8$
8010 H9$=FNS$(H8$(17,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>H8$(111,8))<>0 THEN H9$=H9$+"x"
8020 H9$=H9$+FNS$(H8$(111,8)); IF H9$<>"" THEN IF POS(" "<>H8$(111,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
8030 IF NUM(H8$(15,2),ERR=8031)<>0 THEN H9$=H9$+H8$(15,2)+" Ply "
8050 RETURN 
8060 REM "Setup Fax String, for Vendor output put in Vendor number ONLY, for customer output (F9$="Y") put out Customer number ONLY
8065 DIM X$(51*3),EM$(6*21); REM " SSP# 163056 SSP 232848
8067 IF F9$="Y" THEN J1$=A$(10,10),J2$=A$(420,12),J3$="A/R",J5$="C",EM$(1)="C"+A$(10,10) ELSE J1$=C$(1,10),J2$=C$(133,12),J3$="A/P",J5$="V",EM$(1)="V"+C$(1,10)
8069 CALL "ZZDISP","AX",J1$,J3$,X3$,J4$,"",0,0,X4$
8070 X$(1)=J5$+J2$,X$(27)=J5$+":"+J4$; REM "Vendor or Customer (f9$="Y")
8072 IF F9$<>"Y" AND MID(PARM_RFQ$,70,1)="Y" THEN GOTO *NEXT ELSE GOTO 8090 ! SSP 232848
8075 DIM XP$(51),EMP$(21) ! SSP 232848
8077 J1$=C$(1,10),J3$="A/P",J5$="P"; IF STP(PLANT_FAX$,2)="" THEN J2$=C$(133,12) ELSE J2$=PLANT_FAX$ ! SSP 232848
8078 CALL "ZZDISP","AX",J1$,J3$,X3$,J4$,"",0,0,X4$
8079 XP$(1)="P"+J2$; IF STP(PLANT_CODE$,2)="" THEN XP$(27)="P:"+J4$,EMP$(1,21)="P"+J1$ ELSE XP$(27)="P:"+J4$+"-"+PLANT_CODE$,EMP$(1,21)="P"+J1$+PLANT_CODE$; REM "Plant - ssp#134376 SSP 232848
8082 FIND (Z[14],KEY=EMP$(1,21),DOM=8090)* ! SSP 232848
8085 X$(1)=XP$(1),EM$(1)=EMP$(1) ! SSP 232848
8090 IF F8$="Y" THEN FOR J7=0 TO 1; X$(2+J7*51,25)=P6$(150,12),EM$(2+J7*21,20)="ALIGNMENTPATTERN"; NEXT J7; REM "If alignment pattern, replace with align pat from parameters
8093 CALL "ZZDISP","AX",A$(412,8),"O/P",X3$,J6$,"",0,0,X4$; J6$="840:"+J6$; REM "Tag 1 for fax
8095 RETURN 
8100 REM "Set D1$=normal, D2$=double high printing
8101 REM "Assumes special mode 5 is double & special mode six is un-double
8105 REM "This program assumes that double high is on until turned off, changes will be needed if double high goes off auto. at end of a line see lines 740 & 9905
8110 D1$="",D2$=""
8111 RETURN 
8114 CLOSE (14); OPEN (14)"ZZPARM"
8115 READ (14,KEY="PRINTERS",DOM=8145)D1$,D2$
8120 P=POS(FID(W9)=D2$,2); IF P=0 THEN GOTO 8145
8125 READ (14,KEY="ptr"+D1$(INT(P/2)*8+9,8),DOM=8130)D3$; GOTO 8135
8130 D1$="",D2$=""; GOTO 8145
8135 D1$=D3$(282,16),D2$=D3$(266,16)
8140 D1$=FNS$(D1$),D2$=FNS$(D2$)
8145 RETURN 
8150 REM "Return to normal
8155 IF D1$="" THEN GOTO *RETURN
8160 PRINT (W9)'BO',D1$,'EO',
8175 RETURN 
8200 REM "Goto double high
8205 IF D2$="" THEN GOTO *RETURN
8210 PRINT (W9)'BO',D2$,'EO',
8225 RETURN 
8300 REM "Place in page, F5 = field #, F5$=field info
8301 REM "O5 is used to position downward
8305 IF X9[F5]=0 THEN GOTO 8345 ELSE IF JETFORM_MODE THEN GOSUB JETFORM_PRINT; GOTO 8345
8320 GOSUB 3000
8345 RETURN 
8350 REM "F5=field#, F5$=field info. line number is used to index off of I0 set to first line in lines section - 1. line 1  on a line = first line.
8351 REM "Set F4 to highest line # printed
8355 IF X9[F5]=0 THEN GOTO 8395 ELSE IF JETFORM_MODE THEN GOSUB JETFORM_PRINT; GOTO 8364
8360 GOSUB 3000
8364 IF O3$="*" THEN O7=Y9[F5]+1 ELSE O7=Y9[F5]
8365 IF O7>F4 THEN F4=O7
8395 RETURN 
8400 REM "Print a ply line based on I0 offset, F5 index, F5$ data
8401 GOSUB 8350
8405 IF X9[F5]=0 THEN GOTO 8445
8445 RETURN 
8500 ! WO229284 - Calculate sell extension
8505 ! QBREAK=qty, UNIT$=u/m, QTY_PER=qty per u/m, PRICE=sell price
8510 EXT=0
8520 CALL "FM2EXT",Z[13],0,UNIT$,QTY_PER,QBREAK,PRICE,EXT,0
8545 RETURN 
8549 ! 
8550 ! WO229740 - Read the vendor QSD record for this qty break for working days field
8555 ! VENDOR_NUM=vendor number on screen for this qty break, QTY_BREAK=which break out of 1-5 are we looking for
8560 WORKING_DAYS=0,VENDOR_NUM$=STR(VENDOR_NUM:"00",ERR=*RETURN)
8565 DIM QSD$(288),QSD[35]; READ (Z[6],KEY=B$(150,8)+B$(6,3)+VENDOR_NUM$,DOM=*RETURN)IOL=0470
8570 WORKING_DAYS=QSD[26+QTY_BREAK]
8590 RETURN 
8599 ! 
8600 AR3_ALT_DIV:! WO238337
8610 DIM DIV$(402); DIV$=D4$
8615 DIM AR32$(319); READ (Z[8],KEY=A$(6,4),DOM=*RETURN)AR32$
8620 IF MID(AR32$,255,2)=DIM(2) THEN GOTO *RETURN
8625 DIM D42$(402); READ (Z[9],KEY=AR32$(255,2),DOM=*RETURN)IOL=0465
8630 DIM DIV$(402); DIV$=D42$
8645 RETURN 
8649 ! 
8800 REM "
8810 CALL "ZZDISP","AX",A$(412,8),"O/P",X3$,F5$,"",0,0,X4$; T0$="RQ "+F5$+"-"+B$(6,3)
8815 T1$=C$(11,14); IF F9$="Y" THEN T1$=A$(20,14),T0$=T0$(1,POS("-"=T0$)-1)
8880 X$=FID(W9); CALL "FX2SBB",X3$,W9,T0$,T1$,"",T2$,"",X9; OPEN (W9)X$
8890 RETURN 
8900 REM "U/M
8910 IF B$(77,4)<>L$(4,4) THEN CLOSE (14); OPEN (14)"ZZPARM"; DIM L$(21),L[1]; READ (14,KEY="U/M"+B$(77,4),DOM=8911)IOL=0420
8920 IF L[1]=0 THEN L[1]=1
8940 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9750 REM FILE OPEN
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
9790 RETURN 
9900 REM "End program
9905 IF FID(0)<>FID(W9) AND JETFORM_MODE=0 THEN W9=-1; REM "TO PREVENT FORMFEED ACTION
9906 IF V3$(2,2)="A " THEN PRINT 'EA',
9909 GOSUB 2900
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9915 IF Y4$<>X0$ AND V3$(2,1)<>"T" THEN GOSUB 5300
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 CALL "ZZDAPP",X3$,"AALL"
9929 IF POS(" "<>V0$(73,8))<>0 THEN Y4$=V0$(73,8)
9930 IF %GUI<>0 AND STP(Y4$)<>"QS2DAZ" AND STP(Y4$)<>"QS2PFS" THEN CALL Y4$,ERR=*NEXT
9931 IF %GUI THEN EXIT 
9935 IF STP(Y4$)<>"" THEN RUN Y4$,ERR=9936
9940 RUN "ZMENU"
9999 END 
11000 IMAGE_LIBRARY_SETUP:! If needed, setup parameters to add image library info to Jetform call
11005 IF %HAS_IMAGE_LIB THEN {
11010 IF F9$="Y" THEN {! Doing Customer quote
11015 %IL_ADD_DOC_TYPE$="A5"
11020 %IL_ADD_TF1$=A$(412,8) ! Quote number
11025 %IL_ADD_TF2$=A$(10,10)+A$(412,8) ! customer code[or list_code + contact code] + quote number
11030 IF A$(391,1)="T" THEN %IL_ADD_IMAGE_PATH$=STP(A$(10,4))+"_"+STP(A$(14,6))+"_"+STP(FN%ZZDISP$(MID(A$,412,8),"O/P")) ELSE %IL_ADD_IMAGE_PATH$=STP(FN%ZZDISP$(MID(A$,10,10),"A/R"))+"_"+STP(FN%ZZDISP$(MID(A$,412,8),"O/P"))
11035  } ELSE {! doing Vendor quote
11040 %IL_ADD_DOC_TYPE$="A4"
11042 %IL_ADD_TF1$=A$(412,8) ! Quote number
11045 %IL_FORMAT$=V0$(59,1) ! 186095
11050 %IL_ADD_TF2$=F$(12,10)+A$(412,8) ! Vendor code + quote number
11055 %IL_ADD_IMAGE_PATH$=STP(FN%ZZDISP$(MID(F$,12,10),"A/P"))+"_"+STP(FN%ZZDISP$(MID(A$,412,8),"O/P"))
11070  }
11075  } ELSE {
11080 %IL_ADD_DOC_TYPE$="",%IL_ADD_TF1$="",%IL_ADD_TF2$="",%IL_ADD_IMAGE_PATH$="",%IL_FORMAT$="" ! 186095
11085  }
11095 RETURN 
11099 ! ****************************************************************
12000 QUOTE_MANAGER_EMAILS:! if quote manager emails turned on, do them here
12005 IF F9$="Y" OR A$(188,4)<>DIM(4) THEN GOTO QME_DONE; REM "SSP# 200528 201419
12010 SEND_TO$=STP(MID(PARM_RFQ$,10,60)); IF STP(SEND_TO$)="" AND X3$(9,3)="288" THEN SEND_TO$="quotemanager@corp.data-source.com" ! SEND_TO$ changed for comp 288, ssp 196782  210419
12015 IF STP(SEND_TO$)="" THEN GOTO QME_DONE
12020 CALL "ZSTRTU",X3$,LOGIN$,TMP$,TMP2$; PRINT (W9)"^field "+STP(P6$(146,3),1)+"EMAIL"+"R"+'LF'+"TFJEMAILR||"+SEND_TO$+"||"+"RFQ:"+FN%ZZDISP$(A$(412,8),"O/P")+"||"+"CSR:"+A$(188,4)+"||"+LOGIN$+"||"+SEND_TO$+"||"
12090 QME_DONE:
12095 RETURN 
12099 ! ***********************************************************
56000 REM "196643-On the RFQ print/emails, it is using the wrong qty per         
56001 REM "200528-in RFQ entry, if the CSR field is  blank, send quote to quotemanager. (FOR 288)
56002 REM "217348-RFQ images are not displaying.  
56004 REM "229284-Create five new RFQ fields for sell extension.  Modify
56006 REM "229740-Create RFQ Working Days field, collect online from vendors
56008 REM "232848-RFQ System parm, use "P" tag for output.  Modify RFQ output 
56010 REM "238337-New Alternate Division for PO/RFQ print in AR3, mod to PO
56012 REM "241023-Modify printing programs to use Unform                      
56013 REM "270024-840 doesn't output correctly in the field copy change front 
