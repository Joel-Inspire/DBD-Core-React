0010 REM "RFQ Vendor Quote Setup <QS2DAK>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 04/09/10 - 12.950555 - jdf - SSP# 237723
0037 REM "237723-Trying to add vendor to RFQ and getting message that the    
0040 REM "Copyright 2010 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0105 IF Q1$(1,1)="!" THEN Q1$=Q1$(2),F9$="Y"
0110 X0$="QS2DAK",X1$="RFQ Vendor Quote Setup"
0115 REM LET Q1$="00000100001"
0120 DIM Z0$(80,"-")
0125 V8$=""
0130 K0=3,K1=1
0135 C9=-1,V9=-2
0140 M0$="###,###,###",M1$="###,###.00",M2$="##",M3$="##.00"
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 K9$="",K9=LEN(K9$)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30],A[31],A[32],A[33],A[34],A[35] ! WO229740
0320 IOLIST B$
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20],C[21],C[22],C[23],C[24]
0340 IOLIST D$
0350 IOLIST E$
0360 IOLIST F$,F0$,F[0],F[1],F[2],F[3],F[4],F[5],F[6],F[7],F[8],F[9],F[10],F[11],F[12],F[13],F[14],F[15],F[16],F[17],F[18],F[19],F[20],F[21],F[22],F[23],F[24],F[25],F[26],F[27],F[28],F[29],F[30],F[31],F[32],F[33],F[34],F[35],F[36]
0380 IOLIST H$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X QSD...  02O QSA...  03O QSB...  04O AR1...  05O AP4...  06O QSC...  07O QSE...  08O FM0...  09O FMP...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 DIM SEC$(255); READ (Z[13],KEY="osec"+X3$(40,3),DOM=0531)SEC$; REM SSP 182590
0531 READ (Z[13],KEY=X3$(9,3)+"RFQ",DOM=0532)RFQ_PARM$; REM SSP 184328
0600 REM "
0620 GOSUB 6000
0700 REM "ARRAY SIZES, L0 is length of strings in L0$, L1 is # of array elements per vendor in L1(),L= max # of vendor quotes
0701 REM "L2 = number of fields maintained for each vendor (vend#,print flag, vndor plant,quote#,delivery info, comment & 5 quantities
0710 L0=288,L1=35 ! WO229740 was L0=88,L1=6
0715 L=8
0720 DIM L1[L1*L]
0725 L0$=""
0730 L2=11
0750 REM "Setup V8$
0755 V8$="05"
0760 FOR I=1 TO L
0765 IF 5+I*L2<99 THEN V8$=V8$+STR(5+I*L2:"00")
0770 NEXT I
0775 V8$=V8$+"99"
0780 REM "V9$ SETUP
0785 REM LET V9$="00101599"
0990 READ (Z[1],KEY=K9$,DOM=0991)
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(420),C$(160),C[24],D$(200),F$(20),F[36],E$(100)
1035 DIM A$(288+K9),A[35] ! WO229740
1036 DIM L1[L1*L]; L0$=""
1045 A$(1,0+K9)=K9$+A1$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1250,1500
1200 REM "RFQ number
1205 IF LEN(Q1$)>8 THEN A$(1,8)=Q1$(1,8); GOSUB 6450; GOTO 1220
1210 CALL "ZZENTR","AUX",A{ALL},A$,X4$,X3$,10,3+V0,1+K9,8,C0,"O/P","{1"+X$,K9$,"QS2DAK00","QSA","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9800,6970
1220 FIND (Z[2],KEY=A$(1,8),DOM=1210)IOL=0320
1225 GOSUB 7500
1240 GOTO 1120
1250 REM "Line number
1255 IF LEN(Q1$)>=11 THEN A$(9,3)=Q1$(9,3); GOSUB 6450; GOTO 1270
1260 CALL "ZZENTR","ZUX",A{ALL},A$,X4$,X3$,28,3+V0,9+K9,3,C0,"","{1"+X$,"","QS2DAK02","QSB","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1260,1261
1265 IF C0=-1 THEN GOTO 1267 ELSE IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1120,6800,9800,6970
1270 EXTRACT (Z[3],KEY=A$(1,11),DOM=1260)IOL=0330
1274 EXTRACT (Z[6],KEY=A$(1,11),DOM=1260)IOL=0360
1275 GOSUB 7550
1276 GOSUB 7850
1277 GOSUB 8000
1280 GOSUB 7600
1290 GOTO 1120
1510 REM "If 1st selling qty (C(2)) is 0 or there are now QSD records for this enquiry line, then goto 2000 to do maintenance, else goto 1800
1515 IF C[2]=0 THEN GOTO 2000
1520 READ (Z[1],KEY=A$(1,11),DOM=1521)
1525 K$=KEY(Z[1],END=2000)
1530 IF K$(1,11)<>A$(1,11) THEN GOTO 2000
1540 READ (Z[1],KEY=K$)IOL=0310
1545 DIM J5$(L0); J5$(1)=A$; L0$=L0$+J5$
1550 L7=(LEN(L0$)/L0-1)*L1
1555 FOR I=0 TO 35; L1[I+L7]=A[I]; NEXT I ! WO229740, was TO 5
1570 K$=KEY(Z[1],END=1700)
1575 IF K$(1,11)<>A$(1,11) THEN GOTO 1700 ELSE GOTO 1540
1600 REM "
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630,6975
1630 IF Q1$<>"" THEN GOTO 9900 ELSE GOTO 1000
1660 IF V9>=0 THEN GOSUB 6200
1690 GOTO 2000
1700 REM "
1720 FOR I=1 TO 5; Q0=I-1,S=F[31+I],E=0; GOSUB 7900; NEXT I
1790 GOSUB 6200
1800 REM "
1820 REM "CALL "ZZPROM","10",X3$,Z,"","","",0; ON Z GOTO 1850,6700
1821 ZSEL=0; CALL "ZZPROM","10QS2DAK",X3$,ZSEL,"","","",0; ON ZSEL GOTO 1850,1850,3700,6700
1830 GOTO 2000
1850 REM "MODIFY
1851 IF ZSEL=1 THEN GOTO 1870
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310
1865 C9=1
1870 GOSUB 6200
1871 IF ZSEL<>1 THEN GOTO 1890
1873 DIM SELECT$(2); X$="Enter Line (1-9) or 0 to exit: "; CALL "ZZENTR","SRU",A{ALL},SELECT$,X4$,X3$,54,22,1,1,C0,"","{3"+X$,"0123456789","FM2SHC08","","",""
1874 IF SELECT$(1,1)="0" THEN ZSEL=0; GOTO 1800
1875 SEL=NUM(SELECT$(1,1)); IF SEL=1 THEN X0=6 ELSE IF SEL=2 THEN X0=17 ELSE IF SEL=3 THEN X0=28 ELSE IF SEL=4 THEN X0=39 ELSE IF SEL=5 THEN X0=50 ELSE IF SEL=6 THEN X0=61 ELSE IF SEL=7 THEN X0=72 ELSE IF SEL=8 THEN X0=83 ELSE IF SEL=9 THEN X0=93 END_IF 
1876 L6=SEL; IF LEN(L0$)/L0<SEL AND X0<>93 THEN GOTO 1873 ELSE GOTO 2045 ! SSP#236282 ! SSP#237729
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF INT(C0)=3 THEN GOSUB 7300 ELSE IF C0>3 OR X0<0 THEN GOTO 1800
2040 IF X0<5 THEN ON X0 GOTO 2100,2100,2100,2100,2100,1800
2045 GOSUB 7700; ON MOD(X0-5,L2) GOTO 2150,2200,2400,2250,2300,2450,2350,2350,2350,2350,2350
2100 REM "Maintain selling breaks (in C() array of QSB!)
2105 J0=20+12*X0,J1=X0+2
2107 IF J1>3 AND C[J1-1]=0 THEN GOTO 2020
2108 DIM Q[1]; Q[1]=C[J1]/U0,X$="Enter quantities in units above"
2110 CALL "ZZENTR","N",Q{ALL},A$,X4$,X3$,J0,6+V0,1,15,C0,M0$,"{3"+X$,"","QS2DAK04","","",""
2115 IF C0>0 THEN C[J1]=ABS(Q[1]*U0),Q0=J1-2; GOSUB 8200
2120 IF C[J1]=0 AND J1<6 THEN GOSUB 7650
2140 GOTO 2020
2150 REM "Vendor Code
2152 READ (Z[9],KEY="T"+C$(20,3),DOM=2153)PROD$
2154 GOSUB 8300
2155 IF MID(PROD$,49,1)="Y" THEN GOTO 2158
2156 IF SEC$(89,1)<>"Y" THEN GOTO 2158; REM SSP 182590
2157 A9$="h"+A$(1,2)+C$(20,3)+DIM(3); IF L0$(12,10)<>DIM(10) THEN GOTO 2165 ELSE GOTO 2163; REM SSP 182590
2158 L0_CHK$=L0$ ! SSP229436 jdf
2159 X$="Enter Vendor Code or Press F10 to enter RFQ Vendor Group Code"
2160 CALL "ZZENTR","AUX",A{ALL},L0$,X4$,X3$,2,6+L6,12+L8,10,C0,"A/P","{1"+X$,"","QS2DAK04","AP4","",""; IF C0<>-7 THEN IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2160,2161
2161 IF C0<>-7 THEN GOTO 2165
2162 DIM A9$(154); X$="Enter RFQ Vendor Group Code:",A9$(1,1)="h"; CALL "ZZENTR","AUX",A{ALL},A9$,X4$,X3$,55,22,2,8,C0,"","{1"+X$,"h","QS2DAK66","FMPH2","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2162,2163
2163 C0=1; CALL "QS2DAL",X3$,X4$,A$,L0$,L8,A9$; GOSUB 6400; GOTO 1800
2165 IF POS(" "<>L0$(12+L8,10))=0 THEN GOSUB 7750; IF ABS(C0)=2 OR ABS(C0)=3.2 THEN GOTO 2020 ELSE GOTO 3000
2166 IF L8>0 AND L0_CHK$<>L0$ THEN IF POS(L0$(12+L8,10)=L0_CHK$)>0 THEN CALL "ZZPROM",".4",X3$,Z,"Vendor already exists on this RFQ!!!","","",0; L0$=L0_CHK$; GOTO 2160 ! SSP229436 jdf
2167 IF L0_CHK$<>L0$ THEN GOSUB VENDOR_CHECK; IF VENDOR_FLG THEN CALL "ZZPROM",".4",X3$,Z,"Vendor already exists on this RFQ!!!","","",0; GOTO 2160 ! SSP229436 jdf
2170 FIND (Z[5],KEY=L0$(12+L8,10),DOM=2160)E$
2180 IF C0>0 THEN L0$(85+L8,4)=""; PRINT @(4,21),L0$(85+L8,4),; L0$(67+L8,1)="Y"; IF POS(" "<>E$(133,12))<>0 THEN L0$(66+L8,1)="F" ELSE L0$(66+L8,1)="P"
2182 CALL "ZZ2PRP","EF",RESULT$,DATE$; IF RESULT$="Y" THEN L0$(66+L8,1)="P"
2185 IF C0>0 THEN GOSUB 8800
2190 GOTO 2020
2200 REM "Print flag
2205 X$="F=fax, P=print,' '=not to print"
2206 IF L0$(12+L8,10)<>E$(1,10) THEN DIM E$(200); READ (Z[5],KEY=L0$(12+L8,10),DOM=2207)E$
2208 X$="Output? Y=yes, N=no, C=output completed"; CALL "ZZENTR","SURX",A{ALL},L0$,X4$,X3$,14,6+L6,67+L8,1,C0,"","{3"+X$,"YNC","QS2DAK05","","",""
2210 X$="P=Print/Electronic Forms, F=Fax Nonelectonic forms, ' '=Blank is neither"
2212 IF L0$(67+L8,1)="Y" THEN X$="F=fax, P=print"; CALL "ZZ2PRP","EF",RESULT$,DATE$; IF RESULT$="Y" AND L0$(67+L8,1)="Y" THEN X$="You have Electronic Forms use the P option to fax" ELSE X$="P=Print (use P for electronic forms to fax) F=Fax non electric forms, ' '=Blank is neither"
2215 CALL "ZZENTR","SURX",A{ALL},L0$,X4$,X3$,16,6+L6,66+L8,1,C0,"","{3"+X$,"FP ","QS2DAK06","","",""; IF L0$(66+L8,1)="F" OR RESULT$="Y" THEN GOSUB 8800; GOSUB 8850
2220 IF L0$(67+L8,1)="N" THEN L0$(66+L8,1)=""; PRINT @(16,6+L6),L0$(66+L8,1),
2240 GOTO 2020
2250 REM "Vendor Quote number
2255 X$="Vendor's quote number"
2260 CALL "ZZENTR","S",A{ALL},L0$,X4$,X3$,13,21,22+L8,10,C0,"","{3"+X$,"","QS2DAK08","","",""
2290 GOTO 2020
2300 REM "Delivery
2305 X$="Delivery"
2310 CALL "ZZENTR","S",A{ALL},L0$,X4$,X3$,28,21,32+L8,10,C0,"","{3"+X$,"","QS2DAK10","","",""
2340 GOTO 2020
2350 REM "Cost
2354 REM "For all 5 numerics, move N0 from 0 to 4
2355 N0=MOD(X0-5,L2)-6
2356 L7=(L6-1)*L1
2358 IF C[N0+2]=0 THEN L1[N0+1+L7]=0; PRINT @(22+12*N0,6+L6),"        ",; GOTO 2020
2360 CALL "ZZENTR","N",L1{ALL},A$,X4$,X3$,22+12*N0,6+L6,N0+1+L7,420,C0,M1$,"{3"+X$,"","QS2DAK12","","",""
2365 IF C0>0 THEN Q0=N0; GOSUB 8200
2390 GOTO 2020
2400 REM "Vendor Plant
2405 J$="P"+L0$(12+L8,10),X$="Alternate vendor plant code"
2410 CALL "ZZENTR","SZUX",A{ALL},L0$,X4$,X3$,4,21,85+L8,4,C0,"","{3"+X$,J$,"QS2DAK14","FM0P","",""; IF ABS(C0)>4 THEN GOSUB 6400; GOSUB 8300; ON C0 GOTO 2410,2411
2415 J$="P"+L0$(12+L8,10)
2420 IF POS(" "<>L0$(85+L8,4))<>0 THEN FIND (Z[8],KEY="P"+J$(2)+L0$(85+L8,4),DOM=2410)
2430 IF C0>0 THEN GOSUB 8800; GOSUB 8850
2440 GOTO 2020
2450 REM "Comments
2455 L5$=L0$(42+L8,20)+L0$(70+L8,15),X$="Comments"
2460 CALL "ZZENTR","S",A{ALL},L5$,X4$,X3$,43,21,1,35,C0,"","{3"+X$,"","QS2DAK16","","",""
2465 L0$(42+L8,20)=L5$(1,20),L0$(70+L8,15)=L5$(21)
2490 GOTO 2020
3000 REM "Do second half of screen
3001 REM "C1=C0, X3=X0
3005 IF ABS(C0)=4 THEN GOTO 1800
3010 C1=0,X3=-1,X1=1
3020 C9=1,X$="",C1=ABS(C1),X3=X3+1; IF C1=2 THEN IF X3>0 THEN X3=X3-2
3025 IF ABS(INT(C1))=3 THEN GOSUB 8400 ELSE IF C1>3 THEN GOTO 1800 ELSE IF X3<0 THEN C0=-2,X3=X3+1; GOTO 2020
3040 ON X3 GOTO 3100,3100,3100,3100,3100,3150,3150,3150,3150,3150,3200,3200,3200,3200,3200,3250,3250,3250,3250,3250,1800
3100 REM "Supplier to use
3105 J1=32+X3,J0=27+12*X3
3107 X$="Enter 9 to select lowest price for each quantity break"
3108 IF C[J1-30]=0 THEN GOTO 3020
3109 N5=F[J1]
3110 CALL "ZZENTR","N",F{ALL},A$,X4$,X3$,J0,15,J1,200,C1,M2$,"{3"+X$,"","QS2DAK20","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3110,3111
3111 IF F[J1]=0 THEN GOSUB 8550; GOTO 3140
3112 IF F[J1]=9 THEN Q0=X3; GOSUB 8700; GOTO 3140
3113 IF F[J1]>L THEN GOTO 3110
3115 IF POS(" "<>L0$((F[J1]-1)*L0+12,10))=0 THEN GOTO 3110
3120 L7=(F[J1]-1)*L1; IF L1[X3+1+L7]=0 THEN CALL "ZZPROM",".4",X3$,Z,"The cost from the vendor cannot be 0!","","",0; F[J1]=0; GOTO 3110
3125 Q0=X3,S=F[J1],E=F[J1-5]
3130 GOSUB 7900
3135 IF X3<4 THEN IF ABS(C1)<>2 THEN IF N5=0 AND F[J1]<>0 AND X3=0 AND F[J1+1]=0 THEN GOSUB 8500
3140 GOTO 3020
3150 REM "Extra cost
3151 IF ABS(C1)=2 OR (ABS(C1)=3 AND C4=2) THEN X3=X3-5; GOTO 3030 ELSE X3=X3+5; GOTO 3030
3155 J1=22+X3,J0=21+12*(X3-5)
3158 IF F[J1+5]=0 THEN GOTO 3020
3160 CALL "ZZENTR","N",F{ALL},A$,X4$,X3$,J0,16,J1,420,C1,M1$,"{3"+X$,"","QS2DAK22","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3160,3161
3170 Q0=X3-5,S=F[J1+5],E=F[J1]
3175 GOSUB 7900
3190 GOTO 3020
3200 REM 'DESIRED GROSS PROFIT
3205 J1=X3-10+7,J0=26+12*(X3-10)
3208 IF F[J1+25]=0 THEN GOTO 3020
3209 N5=C[J1]
3210 CALL "ZZENTR","N.",C{ALL},A$,X4$,X3$,J0,17,J1,220,C1,M3$,"{3"+X$,"","QS2DAK24","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3210,3211
3220 Q0=X3-10; GOSUB 8100
3225 IF ABS(C1)<>2 THEN IF X3=10 AND N5=0 AND C[J1]<>0 THEN FOR I=1 TO 4; IF C[7+I]=0 AND F[32+I]<>0 THEN C[7+I]=C[7],Q0=I; GOSUB 8100; NEXT I ELSE NEXT I
3240 GOTO 3020
3250 REM 'UNIT PRICE
3255 J1=X3-15+12,J0=21+12*(X3-15)
3258 IF F[32+(X3-15)]=0 THEN GOTO 3020
3260 CALL "ZZENTR","N",C{ALL},A$,X4$,X3$,J0,18,J1,520,C1,M1$,"{3"+X$,"","QS2DAK26","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3260,3261
3264 IF C0=0 THEN GOTO 3290
3265 REM "Recalc & display gross profit %
3266 REM "Compare price to cost(IN SELLING UNITS), set & display gp%
3269 Q0=X3-15
3274 IF C[Q0+12]<>0 THEN P0=(C[Q0+12]-C[Q0+19])/C[Q0+12]*100
3275 C[Q0+7]=P0; PRINT @(26+12*Q0,17),C[Q0+7]:M3$,
3278 GOSUB 8350
3290 GOTO 3020
3700 REM "Job Notes SSP 185579
3705 RFQ_NO$=MID(Q1$,1,8)
3710 CALL "ZZGMBA",X3$,X4$,"R",RFQ_NO$
3795 GOTO 1800
3799 ! ******************************************************
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6010 PRINT @(4,3),"RFQ#:",@(21,3),"Line#:",@(32,3),"Customer:",
6020 PRINT @(0,4),"    Item:",
6030 PRINT @(0,5),Z0$,@(56,5)," Quantities ",
6050 PRINT @(2,6),"Vendor",@(13,6),"Output",
6060 PRINT @(2,15),"Price from vendor:",@(10,16),"Unit cost:",@(5,17),"Gross Profit %:",@(2,19),"Total Quote Value:",@(5,20),"Total Quote GP:",
6065 GOSUB 8150
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6224 IF L0$="" THEN GOTO 6251
6225 FOR L9=1 TO LEN(L0$)/L0
6226 REM "L8 is offset into L0$, L7 is offset into L1()
6227 L8=(L9-1)*L0,L7=(L9-1)*L1
6230 PRINT @(0,6+L9),STR(L9:"0"),
6235 CALL "ZZDISP","A",L0$(12+L8,10),"A/P",X3$,"","",2,6+L9,X4$
6240 PRINT @(14,6+L9),L0$(67+L8,1)," ",L0$(66+L8,1),
6241 J$=""
6242 IF POS(" "<>L0$(22+L8,10))<>0 THEN J$=J$+"Q"
6243 IF POS(" "<>L0$(32+L8,10))<>0 THEN J$=J$+"D"
6244 IF POS(" "<>L0$(42+L8,20))<>0 OR POS(" "<>L0$(70+L8,15))<>0 THEN J$=J$+"C"
6247 PRINT @(18,6+L9),J$,
6248 FOR I=1 TO 5; IF C[I+1]=0 THEN PRINT @(10+I*12,6+L9),"          "; L1[I+L7]=0; NEXT I ELSE PRINT @(10+I*12,6+L9),L1[I+L7]:M1$; NEXT I
6250 NEXT L9
6260 FOR I=1 TO 5
6265 IF C[I+1]<>0 AND F[31+I]<>0 THEN PRINT @(15+I*12,15),F[31+I]:M2$,; Q0=I-1; GOSUB 8050
6275 NEXT I
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 CALL "ZZDISP","A",A$(1,8),"O/P",X3$,"","",10,3,X4$
6465 PRINT @(28,3),A$(9,3),
6470 GOSUB 7500
6472 GOSUB 7550
6474 GOSUB 7600
6475 GOSUB 7850; GOSUB 8150
6490 RETURN 
6500 REM "DELETE
6510 GOSUB 7800
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY
6709 REM "Write out the QSB line w/ the selling breaks
6710 WRITE (Z[3],KEY=A$(1,11))IOL=0330
6712 REM "Write out QSC line w/extra costs and costs set
6713 WRITE (Z[6],KEY=A$(1,11))IOL=0360
6715 IF L0$="" THEN GOTO 6795
6719 GOSUB 7800
6720 FOR L9=1 TO LEN(L0$)/L0
6725 L8=(L9-1)*L0,L7=(L9-1)*L1
6730 A$=L0$(1+L8,L0)
6732 A$(68,2)=STR(L9:"00")
6735 DIM A[35] ! WO229740
6740 FOR I=0 TO 35; A[I]=L1[I+L7]; NEXT I ! WO229740, was TO 5
6745 WRITE (Z[1],KEY=A$(1,11)+A$(68,2))IOL=0310
6749 REM "7800 RemoveD any extraneous print flags from QSE, now add
6755 IF A$(67,1)="Y" AND POS(A$(66,1)="FP")<>0 THEN WRITE (Z[7],KEY=A$(66,1)+A$(1,21)+A$(68,2))
6770 NEXT L9
6775 IF F9$<>"Y" THEN GOTO 6795
6780 FOR I=2 TO 6
6782 IF C[I]<>0 AND F[30+I]=0 THEN EXITTO 6790
6784 NEXT I
6786 GOTO 6795
6790 CALL "ZZPROM",".4",X3$,0,"All quantities must have a vendor selected","","",0; GOTO 1820
6795 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 IF A1$="" THEN GOTO 6800 ELSE IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6825 A$(1,LEN(A1$))=A1$
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM "PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
7300 REM 
7310 ON FPT(C0)*10 GOTO 7390,7325,7330,7390
7325 IF V8$="" THEN X0=X0+4; GOTO 7390 ELSE X0=NUM(V8$(POS(STR(X0:"00")<=V8$,2),2)); GOTO 7390
7330 IF V8$="" THEN X0=X0-6; GOTO 7335 ELSE X0=POS(STR(X0-1:"00")<=V8$,2)-2; IF X0<0 THEN X0=0; GOTO 7390 ELSE X0=NUM(V8$(X0,2)); GOTO 7390
7335 IF X0<0 THEN X0=0; GOTO 7390
7390 C0=3; RETURN 
7495 RETURN 
7500 REM "Set & display customer from QSA record
7504 IF B$(10,10)=D$(1,10) THEN GOTO 7515
7505 DIM D$(200)
7510 READ (Z[4],KEY=B$(10,10),DOM=7511)IOL=0340
7515 CALL "ZZDISP","AX",B$(10,10),"A/R",X3$,J$,"",0,0,X4$
7520 J0$=J$+"-"+D$(11,35)
7540 PRINT @(42,3),J0$(1,35),
7545 RETURN 
7550 REM "Display Item info from QSB record
7560 H9$=FNS$(C$(23,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>C$(31,8))<>0 THEN H9$=H9$+" x"
7565 H9$=H9$+FNS$(C$(31,8)); IF H9$<>"" THEN IF POS(" "<>C$(31,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
7570 IF NUM(C$(39,2))<>0 THEN H9$=H9$+C$(39,2)+" Ply "
7575 H9$=H9$+FNS$(C$(41,40))
7580 H9$=FNS$(C$(10,10))+" - "+H9$
7590 IF LEN(H9$)<69 THEN PRINT @(10,4),H9$, ELSE PRINT @(10,4),H9$(1,68),
7595 RETURN 
7600 REM "Display Selling Breaks
7605 PRINT @(40,6),'CL',
7610 FOR I=0 TO 4
7615 IF C[I+2]<>0 THEN PRINT @(20+12*I,6),C[I+2]/U0:M0$,
7620 NEXT I
7645 RETURN 
7650 REM "Move non zero qtys down if zero entered
7655 FOR I=2 TO 5
7660 IF C[I]=0 AND C[I+1]<>0 THEN C[I]=C[I+1],C[I+1]=0
7665 NEXT I
7690 GOSUB 7600
7695 RETURN 
7700 REM "Add new vendor to string
7705 L6=INT((X0-5)/L2)+1,L8=(L6-1)*L0
7706 IF L6>L THEN EXITTO 3000
7708 IF LEN(L0$)>=L6*L0 THEN GOTO 7745
7710 DIM J5$(L0); L0$=L0$+J5$
7715 L7=(L6-1)*L1
7720 FOR L9=0 TO 35; L1[L9+L7]=0; NEXT L9 ! WO229740 was TO 5
7735 L0$(1+(L6-1)*L0,11)=A$(1,11)
7740 PRINT @(0,6+L6),STR(L6:"0"),
7745 RETURN 
7750 REM "Remove unused vendor from string
7751 REM "L6 assumed set by 7700's to vendor info line to remove
7755 IF LEN(L0$)<=L0 THEN L0$="" ELSE IF L6=LEN(L0$)/L0 THEN L0$=L0$(1,LEN(L0$)-L0) ELSE L0$=L0$(1,(L6-1)*L0)+L0$(L6*L0+1)
7760 FOR L5=L6 TO L
7765 L7=(L5-1)*L1
7770 FOR L9=0 TO L1-1
7775 IF L5=L THEN L1[L9+L7]=0 ELSE L1[L9+L7]=L1[L9+L7+L1]
7780 NEXT L9
7785 NEXT L5
7790 FOR I=1 TO L; PRINT @(0,6+I),'CL',; NEXT I
7791 GOSUB 6224
7792 PRINT @(0,21),'CL',
7795 RETURN 
7800 REM "Remove QSD records for the current A$(1,11)
7805 READ (Z[1],KEY=A$(1,11),DOM=7806)
7810 K$=KEY(Z[1],END=7840)
7815 IF K$(1,11)<>A$(1,11) THEN GOTO 7840
7820 REMOVE (Z[1],KEY=K$,DOM=7821)
7824 REM "Remove any print flag records
7825 REMOVE (Z[7],KEY="F"+K$,DOM=7826)
7830 REMOVE (Z[7],KEY="P"+K$,DOM=7831)
7835 GOTO 7810
7845 RETURN 
7850 REM "Display Cost quantity
7853 IF F[0]=0 OR POS(" "<>F$(9,4))=0 THEN GOTO 7895
7855 PRINT (0,ERR=7856)'SB',
7860 J5$=FNS$(F$(9,4))+"/"+FNR$(STR(F[0]:"##,###"))
7865 J5$=" Quantities - "+J5$+" "
7870 PRINT @(0,5),Z0$,@(60-INT(LEN(J5$)/2),5),J5$,
7885 PRINT (0,ERR=7886)'SF',
7895 RETURN 
7900 REM "Given qty column number Q0(0-4), supplier #S, and extra cost E, compute the unit cost,C and display it
7902 IF S=0 THEN RETURN 
7904 REM "Extend Unit cost
7905 CALL "FM2EXT",Z[13],0,F$(9,4),F[0],C[Q0+2],L1[(S-1)*L1+Q0+1],E0,2
7914 REM "Add Extra Cost
7915 REM E0=E0+E
7919 REM "Get unit cost IN SELLING UNITS back to store into QSB record
7920 CALL "FM2EXT",Z[13],1,C$(95,4),C[0],C[Q0+2],C,E0,2
7925 REM "Store unit cost into QSB record
7926 C[Q0+19]=C
7929 REM "NOW PUT INTO COST UNITS FOR DISPLAY
7930 CALL "FM2EXT",Z[13],0,C$(95,4),C[0],C[Q0+2],C[Q0+19],E0,2
7932 CALL "FM2EXT",Z[13],1,F$(9,4),F[0],C[Q0+2],C,E0,2
7940 PRINT @(21+12*Q0,16),C:M1$,
7941 GOSUB 8100
7945 RETURN 
7995 RETURN 
8000 REM "Set U0,U1 based on Cost U/M
8001 REM "U0 is qty / and U1 is ext. /
8003 DIM U$(21),U[1]
8004 READ (Z[13],KEY="U/M"+F$(9,4),DOM=8005)U$,U[0],U[1]
8005 U1=U[0]; IF U$(20,1)<>"Y" THEN U0=F[0] ELSE U0=U[1]
8010 IF U0=0 THEN U0=1
8015 IF U1=0 THEN U1=1
8045 RETURN 
8050 REM "Display info from QSB given column number Q0
8051 REM "Take unit cost IN SELLING UNITS and extend, then divide back by cost unit
8054 CALL "FM2EXT",Z[13],0,C$(95,4),C[0],C[Q0+2],C[Q0+19],E0,2
8055 CALL "FM2EXT",Z[13],1,F$(9,4),F[0],C[Q0+2],C,E0,2
8057 PRINT @(21+12*Q0,16),C:M1$,
8060 PRINT @(26+12*Q0,17),C[Q0+7]:M3$,
8064 REM "Selling price
8065 GOSUB 8600
8070 GOSUB 8350
8095 RETURN 
8100 REM 'COMPUTE PRICE GIVEN COLUMN NUMBER,Q0
8104 REM "Convert unit cost (IN SELLING UNITS) to unit price
8105 PRECISION 6; C=C[Q0+19]/(1-C[Q0+7]*.01); PRECISION 2; C=C+0
8114 REM "Store selling unit price & then display
8115 C[Q0+12]=C
8120 GOSUB 8600
8140 GOSUB 8350
8145 RETURN 
8150 REM 'DISPLAY SELLING UNITS OF MEASURE
8155 IF C$="" THEN GOTO 8195
8160 PRINT (0,ERR=8161)'SB',
8165 J5$=FNS$(C$(95,4))+"/"+STR(C[0])
8170 J5$="Price ("+J5$+"):"
8175 PRINT @(0,18),'CL','SB',@(20-LEN(J5$),18),J5$,
8190 PRINT (0,ERR=8191)'SF',
8195 RETURN 
8200 REM "Given column number Q0 (0-4) recalc & display its matrix
8210 S=F[Q0+32],E=F[Q0+27]
8215 IF S-1<0 THEN GOTO 8245
8220 GOSUB 7900
8245 RETURN 
8300 REM 'PRINT VENDOR INFO @ BOTTOM OF SCREEN,
8301 REM "Assume: L8 is set properly
8306 PRINT (0,ERR=8307)'SB',
8310 PRINT @(0,21),"VP:",@(9),"Qt:",@(24),"Dl:",@(39),"Cm:",
8314 PRINT (0,ERR=8315)'SF',
8320 PRINT @(4,21),L0$(85+L8,4),@(13,21),L0$(22+L8,10),@(28,21),L0$(32+L8,10),@(43,21),L0$(42+L8,20)+L0$(70+L8,15),
8345 RETURN 
8350 REM "Figure & display Total quote value & total Quote GP
8351 REM "Given: column number Q0
8354 REM "Get extended price
8355 CALL "FM2EXT",Z[13],0,C$(95,4),C[0],C[Q0+2],C[Q0+12],E1,2
8359 REM "Display
8360 PRINT @(21+12*Q0,19),E1:M1$,
8364 REM "Get extended cost
8365 CALL "FM2EXT",Z[13],0,C$(95,4),C[0],C[Q0+2],C[Q0+19],E2,2
8369 REM 'DISPLAY EXTENDED GP
8370 PRINT @(21+12*Q0,20),E1-E2:M1$
8374 REM "Recompute gp % and reset desired gp% & display
8375 P0=(E1-E2)*100/E1
8380 C[Q0+7]=P0; PRINT @(26+12*Q0,17),C[Q0+7]:M3$
8395 RETURN 
8400 REM 
8405 C1=ABS(C1)
8407 REM "Save factional part for processing by 3150 logic
8408 C4=FPT(C1)*10
8410 ON FPT(C1)*10 GOTO 8490,8425,8430,8490
8425 IF V9$="" THEN X3=X3+4; GOTO 8490 ELSE X3=NUM(V9$(POS(STR(X3:"00")<=V9$,2),2)); GOTO 8490
8430 IF V9$="" THEN X3=X3-6; GOTO 8435 ELSE X3=POS(STR(X3-1:"00")<=V9$,2)-2; IF X3<0 THEN X3=0; GOTO 8490 ELSE X3=NUM(V9$(X3,2)); GOTO 8490
8435 IF X3<0 THEN X3=0; GOTO 8490
8490 C1=3; RETURN 
8500 REM "If first supplier, then default other fields as applicable
8505 FOR J3=1 TO 4
8510 IF F[J3+32]<>0 THEN GOTO 8530
8512 IF L1[J3+1+(F[32]-1)*L1]=0 THEN GOTO 8530
8515 F[J3+32]=F[32]; PRINT @(27+12*J3,15),F[J3+32]:M2$,
8518 C[J3+7]=C[7]
8520 Q0=J3,S=F[J3+32],E=F[J3+32-5]
8525 GOSUB 7900
8530 NEXT J3
8545 RETURN 
8550 REM "Zero buckets, if supplier set to 0
8551 REM "J0= Xpos on screen, J1(32-36) is F() corresponding to column
8554 REM "Zero GP% and Unit Price
8555 C[J1-32+7]=0,C[J1-32+12]=0
8580 FOR I1=15 TO 20; PRINT @(J0-4,I1),"        ",; NEXT I1
8595 RETURN 
8600 REM "Display selling price C(Q0+12) in M's pricing on line 18
8602 PRINT @(21+12*Q0,18),C[Q0+12]:M1$,; GOTO 8645
8605 IF C$(95,4)="M   " THEN PRINT @(21+12*Q0,18),C[Q0+12]:M1$,; GOTO 8645
8609 REM "Take selling price in selling units, and extend then divide back by M's
8610 CALL "FM2EXT",Z[13],0,C$(95,4),C[0],C[Q0+2],C[Q0+12],E0,2
8615 P5=E0/(C[Q0+2]/1000)
8620 PRINT @(21+12*Q0,18),P5:M1$,
8645 RETURN 
8700 REM "Handle auto pick of prices Q0 is column number 0-4
8705 IF Q0<>0 OR N5<>0 THEN GOSUB 8750; GOTO 8745
8709 REM "First time, do all columns
8710 FOR I=0 TO 4
8715 Q0=I; GOSUB 8750
8720 NEXT I
8745 RETURN 
8750 REM "Change column Q0 (0-4) here
8754 P5=0,P6=0
8755 FOR I0=1 TO L
8759 P4=L1[(I0-1)*L1+(Q0+1)]
8760 IF P4>0 THEN IF P4<P5 OR P5=0 THEN P5=P4,P6=I0
8765 NEXT I0
8770 IF P6=0 THEN GOTO 8795
8775 F[32+Q0]=P6; PRINT @(27+12*Q0,15),F[32+Q0]:M2$,
8780 S=F[Q0+32],E=F[Q0+27]; GOSUB 7900
8795 RETURN 
8800 REM "Plant code, if entered, controls Fax answer
8810 IF POS(" "<>L0$(85+L8,4))<>0 THEN FIND (Z[8],KEY="P"+L0$(12+L8,10)+L0$(85+L8,4),DOM=8811)IOL=0380; GOTO 8820
8812 IF RESULT$="Y" AND L0$(66+L8,1)="P" THEN GOTO 8835
8815 L0$(66+L8,1)="P"; IF FNS$(E$(133,12))>"" THEN L0$(66+L8,1)="F"; GOTO 8830 ELSE GOTO 8830
8820 IF RESULT$="Y" AND L0$(66+L8,1)="P" THEN GOTO 8835
8825 IF FNS$(H$(138,12))>"" THEN L0$(66+L8,1)="F" ELSE L0$(66+L8,1)="P"
8830 REM "CALL "ZZ2PRP","EF",RESULT$,DATE$; IF RESULT$="Y" THEN LET L0$(66+L8,1)="P"
8835 PRINT @(14,6+L6),L0$(67+L8,1)," ",L0$(66+L8,1),
8840 RETURN 
8850 REM "Check Manual entry of Fax request
8860 IF FNS$(L0$(85+L8,4))="" AND FNS$(E$(133,12))>"" THEN GOTO 8890
8870 IF FNS$(L0$(85+L8,4))>"" AND FNS$(H$(138,12))>"" THEN GOTO 8890
8880 CALL "ZZPROM",".4",X3$,0,"No fax number in this Vendor's Record!","","",0; CALL "ZZ2PRP","EF",RESULT$,DATE$; IF RESULT$="Y" THEN GOTO 8890 ELSE EXITTO 2210
8890 RETURN 
8900 REM "Functions
8910 DEF FNS$(Z9$)=Z9$(1,POS("      "=Z9$+"      ")-1)
8915 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9511 IF L2<>0 THEN M9=MOD(X0-5,L2); IF M9>=2 AND M9<=4 THEN GOSUB 8300
9520 ON C9 GOTO 1140,2040
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
20300 VENDOR_CHECK:! SSP229436 jdf
20310 IOLIST QSD$,QSD{ALL} ! SSP229436 jdf
20315 VENDOR_FLG=0 ! SSP229436 jdf
20320 DIM QSD$(289),QSD[36] ! SSP229436 jdf
20330 IF STP(MID(A$,1,11))<>"" AND STP(A9$)="" THEN QSD_KEY_CHECK$=MID(A$,1,11) ELSE IF POS(MID(L0$,12+L8,10)=A9$)>0 THEN CALL "ZZPROM",".4",X3$,Z,"Vendor already exists on this RFQ!!!","","",0; GOTO 2160 ELSE GOTO 2170 ! SSP229436 jdf ! SSP237723 jdf
20340 READ (Z[1],KEY="",DOM=*NEXT) ! SSP229436 jdf
20350 QSD_KEY$=KEY(Z[1],END=20445) ! SSP229436 jdf
20360 READ (Z[1],KEY=QSD_KEY$,DOM=*NEXT)IOL=20310 ! SSP229436 jdf
20370 IF QSD_KEY$(1,11)=QSD_KEY_CHECK$ THEN {! SSP229436 jdf
20380 IF QSD$(12,10)=L0$(12+L8,10) THEN {! SSP229436 jdf
20400 VENDOR_FLG=1 ! SSP229436 jdf
20420  } ! SSP229436 jdf
20430  } ! SSP229436 jdf
20440 GOTO 20350 ! SSP229436 jdf
20450 RETURN ! SSP229436 jdf
56000 REM "201216-PE(047,QS2DAK-2155) when in RFQ                             
56002 REM "229740-Create RFQ Working Days field, collect online from vendors
56004 REM "229436-If a vendor is listed more than once on an RFQ, it sends the
56005 REM "236282-Edit existing RFQ to add a vendor, select a vendor line that
56006 REM "237723-Trying to add vendor to RFQ and getting message that the    
