0010 REM "Delete RFQ <QS2DEL>
0035 REM "3.7 - 12/24/93 - 12.54 - wmz
0040 REM "Copyright 1993 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "IF LEN(X$) > 8 THEN REMOVE ONLY A LINE OTHERWISE THE ENTIRE RFQ
0090 SETERR 9000
0095 ENTER X3$,X4$,A1$
0100 X0$="QS2DEL",X$=A1$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O QS1...  02O QS2...  03O QSS...  04O QSU...  05O QSV...  06O QSD...  07O QSF...  08O QSB...  09O QSC...  10O QSE...  11O QSA...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 IF LEN(X$)>8 THEN GOSUB 1000; GOTO 9900
0620 Q$=X$
0700 REM "    Delete entire RFQ
0701 REM "Remove QSB lines and all associated RFQ item master/spec files
0730 READ (Z[8],KEY=Q$,DOM=0731)
0740 K$=KEY(Z[8],END=0790)
0750 IF Q$<>K$(1,LEN(Q$)) THEN GOTO 0790
0760 X$=K$; GOSUB 1000
0770 REM "Remove of Z(8) done at 1220
0780 GOTO 0740
0800 REM "Remove Quote Header & Customer Quote flags
0810 REMOVE (Z[11],KEY=Q$,DOM=0811)
0820 REMOVE (Z[10],KEY="f"+Q$,DOM=0821)
0830 REMOVE (Z[10],KEY="p"+Q$,DOM=0831)
0990 GOTO 9900
1000 REM "Remove from spec files based on RFQ#, RFQ line# in X$
1001 REM "Remove single records from QS1, QS2 and QSS QSF, multiple from QSU & QSV
1002 REM "Must read QS1 rec to get info to delete QS2
1010 READ (Z[1],KEY=X$,DOM=1020)A$; A$=A$(12)
1012 REMOVE (Z[2],KEY=A$(21,3)+A$(24,16)+A$(40,2)+X$,DOM=1013)
1015 REMOVE (Z[1],KEY=X$,DOM=1016)
1020 REMOVE (Z[3],KEY=X$,DOM=1021)
1030 REMOVE (Z[7],KEY=X$,DOM=1031)
1050 FOR I=4 TO 6
1055 READ (Z[I],KEY=X$,DOM=1056)
1060 K9$=KEY(Z[I],END=1080)
1065 IF K9$(1,LEN(X$))<>X$ THEN GOTO 1080
1070 REMOVE (Z[I],KEY=K9$,DOM=1071)
1075 GOTO 1060
1080 NEXT I
1100 REM "Remove print flags for vendors from QSE
1110 K8$="F"+X$
1120 READ (Z[10],KEY=K8$,DOM=1121)
1130 K7$=KEY(Z[10],END=1170)
1140 IF MID(K7$,1,LEN(K8$))<>K8$ THEN GOTO 1170
1150 REMOVE (Z[10],KEY=K7$)
1160 GOTO 1130
1170 IF K8$(1,1)<>"P" THEN K8$(1,1)="P"; GOTO 1120
1200 REMOVE (Z[9],KEY=X$,DOM=1201)
1220 REMOVE (Z[8],KEY=X$,DOM=1221)
1990 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 RETURN 
9900 REM 
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9940 EXIT 
9999 END 
