0010 REM "<ED2ORR> EDI Order Inquiry Report and Export
0020 SETESC 9300; SETERR 9000
0035 REM "4.4 - 06/27/00 - 17.88 - rms - SSP# 119947
0040 REM "Copyright 2000 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0080 DIM Q0$[50]
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$,Q2${ALL},Q3${ALL},Q4${ALL},Q5${ALL}
0100 SETERR 9000
0110 REM "LET X0$="ED2ORR", X1$="EDI ORDER INQUIRY REPORT AND EXPORT"
0120 DIM Z0$(80,"-"),B[8],H[11]
0130 K0=20,K1=1
0135 C9=-1,C=1
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0200 REM "
0240 REM "CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 09920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 PARM$=X3$(9,3)+"E/M"
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$; REM "FS0
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7]; REM "FS6
0330 IOLIST C$; REM "FS8
0340 IOLIST EM1$; REM "EM1
0360 IOLIST F$; REM "AR1
0370 IOLIST G$; REM "EM8
0380 IOLIST H$,H[0],H[1],H[2],H[3],H[4],H[5],H[6],H[7],H[8],H[9],H[10]; REM "FS2
0410 IOLIST Q$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0509 REM "Slot 1 used for ED0 file opened in 1000's, slot 2 used for text file created in 700's to write to
0510 Z$="01O FSO...  02O FS6...  03O FS8...  04O EM1...  06O AR1...  07O EM8...  08O FS2...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0560 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0561,9920
0565 DEVICE=W9
0590 READ (Z[13],KEY=PARM$,DOM=0591)IOL=0410; REM "Get Senders information
0592 IF STP(Q$(304,2),3," ")="" THEN Q$(304,2)="ZZ"
0600 REM "
0610 REM "GOSUB 06000
0640 REM "IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"PROCEED?","","",0; ON Z GOTO 00641,09900
1000 REM "Start of 870 file
1005 Q=NUM(Q$(143,9))+1; Q$(143,9)=STR(Q:"000000000")
1010 GOSUB 6700; REM "Write new control number back to ZZPARM file
1015 READ (Z[4],KEY=Q0$)IOL=0340
1017 IF STP(EM1$(152,2),3," ")="" THEN EM1$(152,2)="ZZ"
1020 READ (Z[6],KEY=EM1$(51,10),DOM=1021)IOL=0360
1030 GOSUB 3000; REM "Set up print channel
1040 ON INT((POS(Q2$[4]="CAPA",2)+1)/2) GOSUB DONT_NEED,ALL_ORDERS,SELECT_ORDERS
1050 GOSUB 2900
1100 GOTO 9900
1105 C=0,SEGMENT$=""
1900 REM "End of file
2000 DONT_NEED:REM "Don't need this segment, go get another one
2045 DONT_NEED_END:RETURN 
2050 ALL_ORDERS:REM "Find all orders for cust and process
2060 K$=EM1$(51,10)
2065 READ (Z[1],KEY=K$,DOM=2066)
2070 K$=KEY(Z[1],END=2190)
2080 READ (Z[1],KEY=K$)
2090 IF K$(1,10)<>EM1$(51,10) THEN GOTO 2190
2095 GOSUB 2500; GOTO 2070
2190 ALL_ORDERS_END:RETURN 
2200 SELECT_ORDERS:REM "Find select orders for customer and process
2210 K$=EM1$(51,10)
2220 READ (Z[1],KEY=K$,DOM=2221)
2230 K$=KEY(Z[1],END=2390)
2240 READ (Z[1],KEY=K$)
2245 IF STP(K$(11,15),1)<>Q2$[2] THEN GOTO 2230
2250 IF K$(1,10)<>EM1$(51,10) THEN GOTO 2390
2260 GOSUB 2500
2390 SELECT_ORDERS_END:RETURN 
2500 REM "Get information on orders
2510 READ (Z[3],KEY=K$(1,10)+K$(26,8),DOM=2511)
2520 J$=KEY(Z[3],END=2070)
2530 READ (Z[3],KEY=J$)
2535 IF Q2$[2]<>"CA" OR Q2$[2]<>"PA" AND J$(11,8)<>K$(26,8) THEN GOTO 2595
2540 READ (Z[2],KEY=J$(1,10)+J$(19,1)+J$(20,10)+J$(11,8)+J$(30,1),DOM=2595)IOL=0320
2545 IF C>1 THEN GOTO 2546 ELSE GOSUB 3100; REM "Build top part of 870 file
2550 GOSUB 2800; REM "Get unit of measure for EDI standards
2560 GOSUB 2600; REM "Build line item information
2565 GOSUB 2700; REM "See if any part of the order is on backorder
2570 GOTO 2520
2595 RETURN 
2600 REM "Build line item information for the 870 file
2620 PRINT (DEV_CHANNEL)"HL*"+STR(C)+"**O"
2630 PRINT (DEV_CHANNEL)"PO1**"+STR(B[2])+"*"+G$(5,2)+"*"+STR(B[1])+"**VN*"+B$(12,10)
2690 C=C+1,CTT=CTT+2
2695 RETURN ; REM "IF Q2$[4]="CA" OR Q2$[4]="PA" AND THEN THEN GOTO 02575
2700 REM "See if any part of the order is on backorder
2710 READ (Z[8],KEY=B$(22,8),DOM=2711)
2720 L$=KEY(Z[8],END=2721)
2730 IF L$(1,8)<>B$(22,8) THEN GOTO 2790
2740 READ (Z[8],KEY=L$,DOM=2741)IOL=0380
2745 IF H$(19,10)<>B$(12,10) THEN GOTO 2720
2750 PRINT (DEV_CHANNEL)"ISR*BP***"
2755 PRINT (DEV_CHANNEL)"QTY*40*"+STR(H[5])+"*"+G$(5,2)
2760 CTT=CTT+2
2790 RETURN 
2800 REM "Get unit of measure for EDI stds
2805 DIM G$(50)
2810 G$(5,2)="EA"; FIND (Z[7],KEY=B$(101,4),DOM=2811)IOL=0370
2890 RETURN 
2900 REM "Bottom protion of the 870 EDI file
2910 PRINT (DEV_CHANNEL)"CTT*"+STR(CTT:"000")
2920 PRINT (DEV_CHANNEL)"SE*"+STR(CTT+10:"000")+"*"+Q$(143,9)
2930 PRINT (DEV_CHANNEL)"GE*1*"+Q$(143,9)+$0D0A$+"IEA*1*"+Q$(143,9)
2990 RETURN 
3000 REM "Set up channel for printing
3001 CURRENT_PREC=PRC; PRECISION 4
3002 TIME=TIM
3010 FILE_NAME$=FID(0)+STR(TIME*1000:"000000")+".out"
3015 PRECISION CURRENT_PREC
3020 PRINTER_CFG$=STP(Q$(152,30),1)+FILE_NAME$
3030 DEV_CHANNEL=DEVICE,DEVICE_NAME$=FID(DEVICE)
3035 REM "Determine which channel to close
3040 CLOSE (DEV_CHANNEL); OPEN (DEV_CHANNEL,OPT=PRINTER_CFG$)DEVICE_NAME$
3090 RETURN 
3100 REM "Build top part of 870 file
3110 DT$=FND$(B$(34,6)); DT$=DT$(9,2)+DT$(1,2)+DT$(4,2)
3120 PRINT (DEV_CHANNEL)"ISA*00*          *00*          *"+Q$(304,2)+"*"
3130 PRINT (DEV_CHANNEL)STP(Q$(7,15),1)+"*"+EM1$(152,2)+"*"+STP(Q0$,1)+"*"+DT$+"*"+FN%NTD$(JUL(0,0,0),"HHMI")+"*U*00200*"+Q$(143,9)+"*1*P*>"
3140 PRINT (DEV_CHANNEL)"GS*RS*"+STP(Q$(7,15),1)+"*"+STP(Q0$(1,15),1)+"*"+DT$+"*"+FN%NTD$(JUL(0,0,0),"HHMI")+"*"+Q$(143,9)+"*X*"+EM1$(157,10)
3150 PRINT (DEV_CHANNEL)"ST*870*"+Q$(143,9)
3160 PRINT (DEV_CHANNEL)"BSR*1*"+Q2$[4]+"*"+Q$(143,9)+"*"+DT$+"****"+Q2$[2]+"*"+Q2$[3]
3170 PRINT (DEV_CHANNEL)"N1*BY*"+F$(11,35)+$0D0A$+"N3*"+F$(56,30)+$0D0A$+"N4*"+F$(116,16)+"*"+F$(132,2)+"*"+F$(134,9)+$0D0A$+"N1*11*"+EM1$(16,35)+$0D0A$+"N3*"+Q$(52,30)+$0D0A$+"N4*"+Q$(112,16)+"*"+Q$(132,2)+"*"+Q$(134,9)
3180 REM "PRINT (DEV_CHANNEL) "PER*"+Q4$[2]+"*"+Q4$[3]+"*"+Q4$[4]+"*"+Q4$[5]
3195 RETURN 
5000 REM "EOJ
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"Translating Incoming EDI Order Inquiry Records"
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6700 REM "Write new control number out to ZZPARM file
6705 REMOVE (Z[13],KEY=PARM$)
6710 WRITE (Z[13],KEY=PARM$)IOL=0410
6790 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F$
8115 REM "PRINT @(0,7),"THERE ARE "+STR(T)+" RECORDS TO PROCESS"
8129 REM "Set T0, we make sure T0 is > 1, because later on we MOD and look for avalue of 1. IF T0 is 1, then nothing would get reported. We look for a result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T0'th record to get the first report
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,C
8195 RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9945 EXIT 
9999 END 
