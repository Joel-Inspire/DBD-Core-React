0010 REM "<ED2IN9> EDI Invoice Export (Custom 352/DSSI)
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 10/03/12 - 12.265 - jvv - SSP# 257952
0037 REM "257952-Need larger tracking number field to replace 20 character   
0040 REM "Copyright 2012 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! WO231031 - Med-Pass Flat File Invoice for DSSI - copied from ED2IN4
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,CUST$,INV$
0100 SETERR 9000
0110 X0$="ED2IN9",X1$="EDI Invoice Export (352/DSSI)"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1,C=1
0135 C9=-1
0140 DIM A[30],C[20],Z1$(15," ")
0145 DTT$=FN%NTD$(CDN,"YYYYMMDD")
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0166 DEF FNF$(Z9$)=Z9$(7,4)+Z9$(1,2)+Z9$(4,2)
0170 IF POS("MS"=UCS(SYS)) THEN ISWIN=1 ELSE ISWIN=0
0180 M1$="-0000000.00",M2$="-00000.0000"
0290 PARM$=X3$(9,3)+"E/M"
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30]
0330 IOLIST C$,C1$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20]
0370 IOLIST G$
0410 IOLIST Q$
0420 IOLIST K$
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O EM1...  07O AR1...  08O EM2...  09O ARB...  10O ART...  12O FS1...  13O ZZPARM  14O EM8...  15O EM9...  16O FM1...  17O SM4... 30O AR2... 31O AT1...  32O FM0...  33O PO3...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0560 ! CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0561,9920 SSP233089 - if the user doesn't have a default printer in their user record this stops the process and you can't continue.  We always set the EDI system parms with a device to use so this call to ZZPRIN is really not necessary.
0580 DEVICE=HFN ! SSP233089, was setting to W9 but we no longer call ZZPRIN
0590 READ (Z[13],KEY=PARM$,DOM=9900)IOL=0410; REM "Get Senders information
0592 IF STP(Q$(304,2),3," ")="" THEN Q$(304,2)="ZZ"
1000 REM "Main Process
1010 READ (Z[9],KEY=INV$,DOM=9900)IOL=0310 ! Read ARB record
1015 IF A$(179,1)<>"I" THEN GOTO 9900 ! If FOB is not I, then no EDI
1020 GOSUB 1900; REM "Get Customer & Trading Partner Information
1030 IF FOUND_TP$="N" THEN GOTO 9900
1040 GOSUB 2000; REM "Set up channel for printing
1050 GOSUB 6700; REM "Write new control number back to ZZPARM file
1060 GOSUB 2100 ! Header (H)
1070 GOSUB 2200 ! Detail (D)
1080 GOSUB 2300 ! Summary (S)
1090 GOSUB 3000; REM "Archive file to editmp in case of problems
1099 ! 
1600 GOTO 9900
1699 ! 
1900 REM "Get Customer & Trading Partner Info
1910 FOUND_TP$="N"
1920 READ (Z[8],KEY=CUST$,DOM=1921)
1925 DIM EM1$(255),G$(599)
1930 CUST_TRADE_CODE$=KEY(Z[8],END=1995); READ (Z[8],END=1995)
1940 IF CUST_TRADE_CODE$(1,10)<>CUST$ THEN GOTO 1995
1950 REC_ID$=CUST_TRADE_CODE$(11,15)
1960 READ (Z[7],KEY=CUST_TRADE_CODE$(1,10),DOM=1961)IOL=0370
1970 FIND (Z[1],KEY=REC_ID$,DOM=1971)EM1$
1980 IF EM1$(73,1)="Y" THEN FOUND_TP$="Y" ELSE GOTO 1925
1985 IF STP(EM1$(152,2),3," ")="" THEN EM1$(152,2)="ZZ"
1995 RETURN 
1999 ! 
2000 REM "Set the channel up for printing
2001 SERIAL "EDILCK",0,0,ERR=2002; GOTO 2003
2002 WAIT 5; GOTO 2001
2005 PRINTER_CFG$="512,1,3,,"; REM "Line width 512, don't lock others out, build in /tmp then send, no timeout
2010 FILE_NAME$=STR(NUM(A$(7,8)):"00000000")+".out"
2015 PRINTER_CFG$=STP(Q$(152,30),1)+FILE_NAME$
2020 DEV_CHANNEL=DEVICE,DEVICE_NAME$=Q$(212,2) ! Standard is N1
2025 REM "Determine correct channel to close
2030 CLOSE (DEV_CHANNEL); OPEN (DEV_CHANNEL,OPT=PRINTER_CFG$)DEVICE_NAME$
2095 RETURN 
2099 ! 
2100 ! Header record - 185 long
2105 DIM HEADER$(185); HEADER$(1,1)="H"
2110 INV_DT$=FND$(A$(86,6)),INV_DT$=FNF$(INV_DT$),HEADER$(92,8)=INV_DT$ ! Invoice date
2115 DIM FS_HEAD$(350); FIND (Z[12],KEY=A$(92,8),DOM=*NEXT)FS_HEAD$
2120 TRANS_TYPE$=""; IF A$(77,1)="C" OR A[9]<0 THEN TRANS_TYPE$="C" ELSE TRANS_TYPE$="I" END_IF ; HEADER$(40,1)=TRANS_TYPE$ ! Credit or debit invoice
2125 OUT$=DTE(0:"%Hz%mz"); PRECISION 2
2130 IF LEN(FS_HEAD$)>0 THEN ORD_DT$=FND$(FS_HEAD$(16,6)); ORD_DT$=FNF$(ORD_DT$) END_IF ; IF STP(ORD_DT$,3)="" THEN ORD_DT$=INV_DT$ END_IF ; HEADER$(41,8)=ORD_DT$ ! Order date
2140 CUST_PO$=A$(100,15),HEADER$(25,15)=CUST_PO$ ! Customer PO
2145 SHIP_DT$=FND$(A$(180,6)),SHIP_DT$=FNF$(SHIP_DT$); IF STP(SHIP_DT$,3)="" THEN SHIP_DT$=INV_DT$ END_IF ; HEADER$(84,8)=SHIP_DT$ ! Ship date
2150 INV_NUM$=A$(7,8),HEADER$(64,20)=INV_NUM$,HEADER$(166,20)=INV_NUM$; IF TRANS_TYPE$="C" THEN GOSUB GET_INSIDE_DELIVERY ! Invoice number
2155 TAX$=STR(A[4]+A[6]+A[8]:M1$),HEADER$(111,11)=TAX$ ! Sales tax total
2160 FREIGHT$=STR(A[2]:M1$),HEADER$(122,11)=FREIGHT$ ! Freight
2165 FREIGHT_TAX$=STR(0:M1$),HEADER$(133,11)=FREIGHT_TAX$ ! Freight tax
2170 OTHER_TAX$=STR(0:M1$),HEADER$(155,11)=OTHER_TAX$ ! Other tax
2175 GOSUB GET_TOTAL_COST_FIELDS; TOT_PROD_COST$=STR(TOT_PROD_COST:M1$),TOT_OTHER_COST$=STR(TOT_OTHER_COST:M1$),HEADER$(100,11)=TOT_PROD_COST$,HEADER$(144,11)=TOT_OTHER_COST$
2180 SUPPLIER_CODE$="MPSD",PROVIDER_CODE$=G$(446,3),FACILITY_CODE$=G$(246,15); GOSUB GET_FACILITY_FROM_SHIPTO ! Provider code is first three chars of customer master market channel, facility code is first 15 of customer code.  If shipto has receiving dept, use it as the facility code
2185 HEADER$(2,5)=SUPPLIER_CODE$,HEADER$(7,3)=PROVIDER_CODE$,HEADER$(10,15)=FACILITY_CODE$
2190 PRINT (DEV_CHANNEL)HEADER$
2195 RETURN 
2199 ! 
2200 ! Detail - 225 long
2210 KT=A[20],NUM_LINES=0
2215 IF KT<=0 THEN GOTO 2290 ELSE READ (Z[10],IND=KT,END=2290)IOL=0330
2220 IF C$="" THEN GOTO *RETURN ELSE KT=NUM(C$)
2225 IF POS(C1$(1,1)="SM")>0 THEN GOTO 2215 ! Don't want S and M type lines
2227 IF C[1]=0 THEN GOTO 2215 ! SSP238253, if quantity is zero, this is a full backorder line, don't send to DSSI, they can't accept 0 quantity.
2228 IF C1$(6,1)="N" THEN GOTO 2215 ! SSP246307, if invoice line says don't print then we don't want it to go to DSSI
2230 DIM DETAIL$(225); DETAIL$(1,1)="D"
2235 UNIT_PRICE$=STR(C[2]:M2$),DETAIL$(58,11)=UNIT_PRICE$ ! Unit price
2240 UM$=C1$(50,2),DETAIL$(56,2)=UM$ ! U/M
2245 IF C[6]<>0 THEN QTY=C[1]/C[6] ELSE QTY=C[1] END_IF ; QTY$=STR(QTY:M2$),DETAIL$(45,11)=QTY$ ! Quantity
2250 GOSUB 2400; DETAIL$(15,30)=ITEM$ ! Check for CIC, or use item code
2255 DESC$=STP(C1$(7,40),3,"*"),DETAIL$(91,80)=DESC$ ! Description.  SSP238253, strip all asterisks    
2260 LINE_TAX$=STR(0:M1$),TAX_PERCENT$=STR(0:M2$),DETAIL$(69,11)=LINE_TAX$,DETAIL$(80,11)=TAX_PERCENT$ ! Tax and tax percent
2270 PRINT (DEV_CHANNEL)DETAIL$
2275 NUM_LINES+=1
2280 GOTO 2215
2290 RETURN 
2299 ! 
2300 ! Summary - 8 long
2310 DIM SUMMARY$(8)
2315 SUMMARY$(1,1)="S"
2320 SUMMARY$(2,7)=STR(NUM_LINES:"0000000")
2380 PRINT (DEV_CHANNEL)SUMMARY$
2390 RETURN 
2399 ! 
2400 REM "Check for CIC - start using this on WO228481
2410 ITEM$=C1$(65,10)
2420 DIM FM1$(448); READ (Z[16],KEY=CUST$+ITEM$,DOM=2440)FM1$(1)
2430 IF FM1$(350,20)<>DIM(20) THEN ITEM$=STP(FM1$(350,20),1)
2445 RETURN 
2449 ! 
2600 REM "Get Tracking Number
2605 GET_KEY$=A$(92,8)+C1$(135,1)+C1$(47,3)
2610 READ (Z[17],KEY=GET_KEY$,DOM=2620)
2620 READ (Z[17],END=2690)SM4$
2640 IF SM4$(1,12)<>GET_KEY$ THEN GOTO 2690
2660 IF SM4$(101,8)=A$(7,8) THEN TRACK_NO$=MID(SM4$,383,60) ELSE GOTO 2620 ! ssp 257952
2670 CARRIER_NAME$=SM4$(37,12),CARRIER_ID$=SM4$(31,6),TRACKING_NO$=MID(SM4$,383,60) ! SSP 257952
2690 RETURN 
2699 ! 
3000 REM "Write file to the editmp directory
3010 DOC_NUM$=A$(7,8)+Z1$(1,15-LEN(A$(7,8)))
3020 WRITE (Z[15],KEY="810"+DTT$+A$(15,10)+A$(7,8))FILE_NAME$
3030 CLOSE (DEV_CHANNEL)
3035 IF ISWIN THEN S2$="copy "+HWD+DLM+"export"+DLM+FILE_NAME$+" "+HWD+DLM+"editmp"+DLM; CALL "ZZ2CMD",X3$,X4$,S2$,"","IN",COMM_CODE,COMM_CODE$; GOTO 3050; REM "SSP146619, If NT call command processor so command will happen on server
3040 S2$="cp "+HWD+DLM+"export"+DLM+FILE_NAME$+" "+HWD+DLM+"editmp"+DLM; INVOKE S2$
3090 RETURN 
3099 ! 
6700 REM "Write new control number out to ZZPARM file
6710 Q=NUM(Q$(143,9))+1; Q$(143,9)=STR(Q:"000000000")
6720 REMOVE (Z[13],KEY=PARM$)
6730 WRITE (Z[13],KEY=PARM$)IOL=0410
6790 RETURN 
6799 ! 
7000 GET_TOTAL_COST_FIELDS:
7010 I=A[20]; TOT_PROD_COST=0,TOT_OTHER_COST=0
7015 IF I<=0 THEN GOTO *RETURN ELSE DIM C[20]; READ (Z[10],IND=I,END=*RETURN)IOL=0330
7020 IF C$="" THEN GOTO *RETURN ELSE I=NUM(C$)
7025 IF POS(C1$(1,1)="SM")=0 THEN TOT_PROD_COST+=C[4] ELSE IF C1$(1,1)="S" THEN TOT_OTHER_COST+=C[4]
7030 GOTO 7015
7040 RETURN 
7045 ! 
7050 GET_FACILITY_FROM_SHIPTO:
7055 DIM FM0$(618)
7060 FIND (Z[32],KEY="C"+CUST_TRADE_CODE$(1,10)+A$(187,4),DOM=*RETURN)FM0$
7065 IF FM0$(382,15)<>DIM(15) THEN FACILITY_CODE$=FM0$(382,15)
7090 RETURN 
7095 ! 
7100 GET_INSIDE_DELIVERY:
7110 DIM PO3$(429); FIND (Z[33],KEY=A$(92,8)+DIM(5),DOM=*RETURN)PO3$
7115 IF STP(PO3$(135,15),3)>"" THEN HEADER$(166,20)=PO3$(135,15)
7140 RETURN 
7145 ! 
9000 REM "Error
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM "Transfer Control
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "Ctrl logic
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "End Program
9905 CLOSE (DEV_CHANNEL,ERR=9906)
9906 ERASE "EDILCK",ERR=9907
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "231031-EDI Module/810 Document - Custom Outbound 810 program.
56002 REM "233089-Sales Journal Update issue, related to new EDI Invoice
56004 REM "238253-DSSI needs Med-Pass EDI invoice program changed for full
56006 REM "246307-DSSI invoice 1074448 and 1082325 for LCC0151N5445.          
56007 REM "257952-Need larger tracking number field to replace 20 character   
