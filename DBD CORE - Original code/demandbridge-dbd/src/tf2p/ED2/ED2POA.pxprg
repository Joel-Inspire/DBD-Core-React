0010 REM "<ED2POA> EDI Purchase Order Acknowledgment
0020 SETESC 9300; SETERR 9000
0035 REM "5.0 - 03/05/02 - 14.604722 - dmm - SSP# 146819
0040 REM "Copyright 2002 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0080 DIM Q0$[50]
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,SOURCE$,VER$,SENDER$,CUST$,LOC$,ORD_BY$,REQ$,ATTN$,DT_REQ$,DT_ORD$,VIACODE$,VIADESC$,HOWCODE$,INSIDE$,WITH$,ADDR1$,ADDR2$,CITY$,STATE$,ZIP$,MARKFOR$,SALESTAX$,TAXABLE$,HEADWSHE$,SHIPNAME$,FO1_LINE$
0100 SETERR 9000
0110 REM "LET X0$="ED2POA", X1$="EDI PURCHASE ORDER ACKNOWLEDGMENT"
0120 DIM Z0$(80,"-"),B[8],H[11]
0130 K0=20,K1=1
0135 C9=-1,C=1,DT$=DAY
0140 DT$=DT$(7,2)+DT$(1,2)+DT$(4,2)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0200 REM "
0240 REM "CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 09920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 PARM$=X3$(9,3)+"E/M"
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0320 IOLIST B$; REM "ICO
0330 IOLIST C$; REM "EM8
0360 IOLIST F$; REM "AR1
0410 IOLIST Q$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0509 REM "Slot 1 used for ED0 file opened in 1000's, slot 2 used for text file created in 700's to write to
0510 Z$="01O EM1...  02O IC0...  03O EM8...  06O AR1...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0560 REM "CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z[ALL],W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 00561,09920
0565 DEVICE=UNT
0590 DIM Q$(305); READ (Z[13],KEY=PARM$,DOM=0591)IOL=0410; REM "Get Senders information
0592 IF STP(Q$(304,2),3," ")="" THEN Q$(304,2)="ZZ"
0600 REM "Get sender info
0605 DIM EM1$(255); READ (Z[1],KEY=SENDER$,DOM=0606)EM1$
0607 IF STP(EM1$(152,2),3," ")="" THEN EM1$(152,2)="ZZ"
1000 REM "Start of 855 file
1001 SERIAL "EDILCK",0,0,ERR=1002; GOTO 1003
1002 WAIT 5; GOTO 1001
1005 Q=NUM(Q$(143,9))+1; Q$(143,9)=STR(Q:"000000000")
1010 GOSUB 6700; REM "Write new control number back to ZZPARM file
1015 REM "READ (Z[4],KEY=Q0$) IOL=00340
1020 READ (Z[6],KEY=CUST$,DOM=1021)IOL=0360
1030 GOSUB 3000; REM "Set up print channel
1035 GOSUB 3100; REM "Build top of 855 file
1040 GOSUB 2600; REM "Build detail part of 855 file (line item detail)
1050 GOSUB 2900; REM "build bottom part of 855 file
1100 GOTO 9900
1105 C=0,SEGMENT$=""
1900 REM "End of file
2600 REM "Build line item information for the 855 file
2605 IF FO1_LINE$="" THEN RETURN 
2607 VER=41; IF VER$="200" THEN VER=91
2608 VER_1=VER-1
2610 FOR I=1 TO LEN(FO1_LINE$)/VER
2630 PRINT (DEV_CHANNEL)"PO1*"+STR(C:"000")+"*"+STP(FO1_LINE$(I*VER-VER_1+34,7),1)+"*"+STP(FO1_LINE$(I*VER-VER_1+24,4),1)+"**QT***VN*"+FO1_LINE$(I*VER-VER_1+10,10)
2640 C=C+1
2650 NEXT I
2695 RETURN 
2900 REM "Bottom protion of the 855 EDI file
2910 PRINT (DEV_CHANNEL)"CTT*"+STR(C-1:"000")
2920 PRINT (DEV_CHANNEL)"SE*"+STR(C+13:"000")+"*"+Q$(143,9)
2930 PRINT (DEV_CHANNEL)"GE*1*"+Q$(143,9)+$0D0A$+"IEA*1*"+Q$(143,9)
2990 RETURN 
3000 REM "Set up channel for printing
3001 CURRENT_PREC=PRC; PRECISION 4
3002 TIME=TIM
3010 FILE_NAME$=FID(0)+STR(TIME*1000:"000000")+".out"
3015 PRECISION CURRENT_PREC
3020 PRINTER_CFG$=STP(Q$(152,30),1)+FILE_NAME$
3030 DEV_CHANNEL=DEVICE,DEVICE_NAME$=Q$(212,2)
3035 REM "Determine which channel to close
3040 CLOSE (DEV_CHANNEL); OPEN (DEV_CHANNEL,OPT=PRINTER_CFG$)DEVICE_NAME$
3090 RETURN 
3100 REM "Build top part of 855 file
3120 PRINT (DEV_CHANNEL)"ISA*00*          *00*          *"+Q$(304,2)+"*",
3125 IF EM1$(71,1)="N" THEN TEST$="P" ELSE TEST$="T"
3130 PRINT (DEV_CHANNEL)Q$(7,15)+"*"+EM1$(152,2)+"*"+SENDER$+"*"+DT$+"*"+FN%NTD$(JUL(0,0,0),"HHMI")+"*U*00200*"+Q$(143,9)+"*0*"+TEST$+"*>"
3140 PRINT (DEV_CHANNEL)"GS*PR*"+STP(Q$(7,15),1)+"*"+STP(SENDER$,1)+"*"+DT$+"*"+FN%NTD$(JUL(0,0,0),"HHMI")+"*"+Q$(143,9)+"*X*"+EM1$(157,10)
3150 PRINT (DEV_CHANNEL)"ST*855*"+Q$(143,9)
3160 PRINT (DEV_CHANNEL)"BAK*00*AK*"+REQ$+"*"+DT_ORD$+"*****"+DT$
3165 REM "PRINT (DEV_CHANNEL) "REF*CO*"+REQ$
3166 PRINT (DEV_CHANNEL)"DTM*002*"+DT_REQ$
3168 REM "PRINT (DEV_CHANNEL) "TD5*B*2**"+VIADESC$+"*T"
3175 PRINT (DEV_CHANNEL)"N1*VN*"+Q$(22,30)+$0D0A$+"N3*"+STP(Q$(52,30),1)+" "+STP(Q$(82,30),1)+$0D0A$+"N4*"+Q$(112,20)+"*"+Q$(132,2)+"*"+Q$(134,9)+$0D0A$+"N1*BY*"+F$(11,35)+$0D0A$+"N3*"+F$(56,30)+$0D0A$+"N4*"+F$(116,16)+"*"+F$(132,2)+"*"+F$(134,9)+$0D0A$+"N1*ST*"+SHIPNAME$+$0D0A$+"N3*"+ADDR1$+$0D0A$+"N4*"+CITY$+"*"+STATE$+"*"+ZIP$
3195 RETURN 
5000 REM "EOJ
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"Translating Incoming EDI Order Inquiry Records"
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6700 REM "Write new control number out to ZZPARM file
6705 REMOVE (Z[13],KEY=PARM$)
6710 WRITE (Z[13],KEY=PARM$)IOL=0410
6790 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8115 REM "PRINT @(0,7),"THERE ARE "+STR(T)+" RECORDS TO PROCESS"
8129 REM "Set T0, we make sure T0 is > 1, because later on we MOD and look for avalue of 1. IF T0 is 1, then nothing would get reported. We look for a result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T0'th record to get the first report
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,C
8195 RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9905 CLOSE (DEV_CHANNEL)
9906 ERASE "EDILCK",ERR=9007
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9935 EXIT 
9940 SETESC 9350
9999 END 
