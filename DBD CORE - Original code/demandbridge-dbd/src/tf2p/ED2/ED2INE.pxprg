0010 REM "<ED2INE> EDI Invoice Export
0020 SETESC 9300; SETERR 9000
0035 REM "5.2 - 03/02/04 - 10.898888 - lms - SSP# 167214
0040 REM "Copyright 2004 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,CUST$,INV$
0100 SETERR 9000
0110 X0$="ED2INE",X1$="EDI Invoice Order Export"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1,C=1
0135 C9=-1
0140 DIM A[30],C[20],Z1$(15," ")
0145 DTT$=DAY,DTT$=DTT$(7,2)+DTT$(1,2)+DTT$(4,2)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 IF POS("MS"=UCS(SYS)) THEN ISWIN=1 ELSE ISWIN=0
0240 REM "CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 09920
0290 PARM$=X3$(9,3)+"E/M"
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30]
0330 IOLIST C$,C1$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20]
0370 IOLIST G$
0410 IOLIST Q$
0420 IOLIST K$
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O EM1... 07O AR1...  08O EM2... 09O ARB...  10O ART...  11O IC0...  13O ZZPARM  14O EM8...  15O EM9...  16O FM1...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0560 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0561,9920
0580 DEVICE=W9
0590 READ (Z[13],KEY=PARM$,DOM=9900)IOL=0410; REM "Get Senders information
0592 IF STP(Q$(304,2),3," ")="" THEN Q$(304,2)="ZZ"
1000 REM "Main Process
1010 READ (Z[9],KEY=INV$,DOM=9900)IOL=0310; REM "Initial read from orders file (ARB)
1020 GOSUB 1900; REM "Get Customer & Trading Partner Information
1030 IF FOUND_TP$="N" THEN GOTO 9900
1040 GOSUB 2000; REM "Set up channel for printing
1050 GOSUB 6700; REM "Write new control number back to ZZPARM file
1060 GOSUB 2100; REM "Heading portion of EDI file
1070 GOSUB 2200; REM "Write line items out to EDI file
1080 GOSUB 2300; REM "Bottom portion of EDI file
1090 GOSUB 3000; REM "Archive file to editmp in case of problems
1600 GOTO 9900
1900 REM "Get Customer & Trading Partner Info
1910 FOUND_TP$="N"
1920 READ (Z[8],KEY=CUST$,DOM=1921)
1925 DIM EM1$(255),G$(599)
1930 CUST_TRADE_CODE$=KEY(Z[8],END=1995); READ (Z[8],END=1995)
1940 IF CUST_TRADE_CODE$(1,10)<>CUST$ THEN GOTO 1995
1950 REC_ID$=CUST_TRADE_CODE$(11,15)
1960 READ (Z[7],KEY=CUST_TRADE_CODE$(1,10),DOM=1961)IOL=0370
1970 FIND (Z[1],KEY=REC_ID$,DOM=1971)EM1$
1980 IF EM1$(73,1)="Y" THEN FOUND_TP$="Y" ELSE GOTO 1925
1985 IF STP(EM1$(152,2),3," ")="" THEN EM1$(152,2)="ZZ"
1995 RETURN 
2000 REM "Set the channel up for printing
2001 SERIAL "EDILCK",0,0,ERR=2002; GOTO 2003
2002 WAIT 5; GOTO 2001
2010 FILE_NAME$=STR(NUM(A$(7,8)):"00000000")+".out"
2015 PRINTER_CFG$=STP(Q$(152,30),1)+FILE_NAME$
2020 DEV_CHANNEL=DEVICE,DEVICE_NAME$=Q$(212,2)
2025 REM "Determine correct channel to close
2030 CLOSE (DEV_CHANNEL); OPEN (DEV_CHANNEL,OPT=PRINTER_CFG$)DEVICE_NAME$
2095 RETURN 
2100 REM "Build top part of EDI file
2110 DT$=FND$(A$(86,6)); DT$=DT$(9,2)+DT$(1,2)+DT$(4,2)
2120 PRINT (DEV_CHANNEL)"ISA*00*          *00*          *"+Q$(304,2)+"*",
2125 IF EM1$(71,1)="N" THEN TEST$="P" ELSE TEST$="T"
2130 PRINT (DEV_CHANNEL)Q$(7,15)+"*"+EM1$(152,2)+"*"+REC_ID$+"*"+DTT$+"*"+FN%NTD$(JUL(0,0,0),"HHMI")+"*U*00200*"+Q$(143,9)+"*0*"+TEST$+"*>"
2140 PRINT (DEV_CHANNEL)"GS*IN*"+STP(Q$(7,15),1)+"*"+STP(REC_ID$,1)+"*"+DTT$+"*"+FN%NTD$(JUL(0,0,0),"HHMI")+"*"+Q$(143,9)+"*X*"+EM1$(157,10)
2150 PRINT (DEV_CHANNEL)"ST*810*"+Q$(143,9)
2160 PRINT (DEV_CHANNEL)"BIG*"+DT$+"*"+A$(7,8)+"*"+DT$+"*"+A$(100,15)+"*1*1*CA"
2170 PRINT (DEV_CHANNEL)"N1*VN*"+Q$(22,30)+$0D0A$+"N3*"+Q$(52,30)+"*"+Q$(82,30)+$0D0A$+"N4*"+Q$(112,20)+"*"+Q$(132,2)+"*"+Q$(134,9)+$0D0A$+"N1*11*"+G$(11,35)+$0D0A$+"N3*"+G$(56,30)+$0D0A$+"N4*"+G$(116,16)+"*"+G$(132,2)+"*"+G$(134,9)
2180 PRINT (DEV_CHANNEL)"DTM*009*"+DT$
2190 RETURN 
2200 REM "Write line items out to EDI file
2210 KT=A[20]
2220 IF KT<=0 THEN GOTO 2290 ELSE READ (Z[10],IND=KT,END=2290)IOL=0330
2230 IF NUM(C$)=0 THEN EXITTO 1080 ELSE KT=NUM(C$)
2235 FIND (Z[14],KEY=C1$(50,4),DOM=2236)IOL=0420
2240 IF C1$(6,1)="N" THEN GOTO 2220
2250 PRINT (DEV_CHANNEL)"IT1*"+STR(C:"000")+"*1*EA*"+STR(C[4])+"*QT*VX*"+C1$(55,20)
2260 IF C1$(1,1)="C" THEN PRINT (DEV_CHANNEL)"PID*F****"+C1$(65,10); GOTO 2266
2265 PRINT (DEV_CHANNEL)"PID*F****"+C1$(7,40)
2270 C=C+1,CTT=CTT+2
2280 GOTO 2220
2290 RETURN 
2300 REM " Bottom Portion of EDI file
2305 TAX_TOT=A[4]+A[6]+A[8]; FREIGHT=A[2]
2310 PRINT (DEV_CHANNEL)"TDS*"+STP(STR(A[9]:"#######.00"),0)
2315 PRINT (DEV_CHANNEL)"TXI*SU*"+STR(TAX_TOT)
2317 PRINT (DEV_CHANNEL)"ITA*C***06***"+STP(STR(FREIGHT:"######.00"),0)+"*******FCD"
2320 C=C-1,CT=14+CTT
2330 PRINT (DEV_CHANNEL)"CTT*"+STR(C)+$0D0A$+"SE*"+STR(CT)+"*"+Q$(143,9)+$0D0A$+"GE*1*"+Q$(143,9)+$0D0A$+"IEA*1*"+Q$(143,9)
2390 RETURN 
2400 REM "Check for CIC - currently not used
2410 VEND_ITEM$=C1$(55,20)
2420 DIM FM1$(448); READ (Z[16],KEY=CUST$+C1$(65,10),DOM=2440)FM1$
2430 IF STP(FM1$(350,20),1)<>"" THEN VEND_ITEM$=FM1$(350,20)
2440 PRINT (DEV_CHANNEL)"IT1*"+STR(C:"000")+"*1*EA*"+STR(C[4])+"*QT*VX*"+VEND_ITEM$
2450 RETURN 
3000 REM "Write file to the editmp DIRectory
3010 DOC_NUM$=A$(7,8)+Z1$(1,15-LEN(A$(7,8)))
3020 WRITE (Z[15],KEY="810"+DTT$+A$(15,10)+A$(7,8))FILE_NAME$
3030 CLOSE (DEV_CHANNEL)
3035 IF ISWIN THEN S2$="copy "+HWD+DLM+"export"+DLM+FILE_NAME$+" "+HWD+DLM+"editmp"+DLM; CALL "ZZ2CMD",X3$,X4$,S2$,"","IN",COMM_CODE,COMM_CODE$; GOTO 3050; REM "SSP146619, If NT call command processor so command will happen on server
3040 S2$="cp "+HWD+DLM+"export"+DLM+FILE_NAME$+" "+HWD+DLM+"editmp"+DLM; INVOKE S2$
3090 RETURN 
6700 REM "Write new control number out to ZZPARM file
6710 Q=NUM(Q$(143,9))+1; Q$(143,9)=STR(Q:"000000000")
6720 REMOVE (Z[13],KEY=PARM$)
6730 WRITE (Z[13],KEY=PARM$)IOL=0410
6790 RETURN 
9000 REM "Error
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM "Transfer Control
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "Ctrl logic
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "End Program
9905 CLOSE (DEV_CHANNEL,ERR=9906)
9906 ERASE "EDILCK",ERR=9907
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
