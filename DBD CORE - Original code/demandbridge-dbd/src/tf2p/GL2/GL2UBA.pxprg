0010 REM "G/L Period End Processing <GL2UBA>
0020 SETESC 9300; SETERR 9000
0035 REM "5.5 - 03/05/07 - 16.501388 - mhe - SSP# 205703
0037 REM "205703-Oracle - FID and FIB calls, replace with FN%FID$ and FN%FIB$
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="GL2UBA",X1$="G/L Period End Processing"
0120 DIM Z0$(80,"-"),G[2],G1$(105)
0135 C9=-1,V9=-2
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNE$(Z9$)=CHR(NUM(Z9$(1,3))-190+65)+Z9$(4,1)
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 K9$=X3$(9,3)+"G/L",K9=LEN(K9$)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST G0$
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16]
0360 IOLIST G0$
0361 IOLIST G1$(1),G[0],G[1],G[2]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X ZZPARM  02O GL2...  06ORZZPARM  "
0515 Z$=Z$+"05OLGL1...  03O GL3...  04OSGL3...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0570 READ (Z[6],KEY="STATG/L")F9$
0580 READ (Z[6],KEY=X3$(9,3)+"G/L")G0$
0585 READ (Z[6],KEY=X3$(9,3)+"G/LYE"+G0$(34,4))G1$
0600 REM "
0670 CALL "ZZPROM","P1",X3$,0,U7$,"","",0
0700 REM "Check P/E or Y/E
0710 IF G0$(38,2)>=G1$(13,2) THEN F0$="Y"; GOTO 1000 ELSE F0$="P"
0800 REM "Period end Process
0810 GOSUB 6700
0820 CALL "ZZPROM","4",X3$,A,"","","",0
0830 REM " Remove G/L message
0840 GOSUB 7900
0845 GOSUB 8100
0850 CALL "ZZPROM",".Y",X3$,Z,"Do you wish to continue with Period End Processing?","","",0
0855 ON Z GOTO 0856,9900
0856 CALL "GL2DTE",X3$,X4$,G0$(34,6),FLAG$
0857 IF FLAG$="*" THEN GOTO 9900
0858 Z$="06C  06O ZZPARM "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0859,9900
0860 GOSUB 2500
0870 EXTRACT (Z[6],KEY=G0$(1,6))G0$; G0$(38,2)=STR(NUM(G0$(38,2))+1:"00")
0875 WRITE (Z[6],KEY=G0$(1,6))G0$
0880 CALL "ZZPROM",".4",X3$,Z,"Period End Proccessing has been completed","","",0
0890 GOTO 9900
1000 REM "Year end Process
1002 CALL "GL2DTE",X3$,X4$,G0$(34,6),FLAG$
1004 IF FLAG$="*" THEN GOTO 9900
1006 Z$="01C ZZPARM  06C ZZPARM "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 1007,9900
1007 Z$="01O ZZPARM  06O ZZPARM "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 1008,9900
1010 CALL "ZZPROM","Y",X3$,Z,"Do you wish to proceed with G/L Year End Processing?","","",0; ON Z GOTO 1011,9900
1020 REM "Check prior year is close ,this year is open
1025 GOSUB 8000
1100 REM "G/L message
1110 GOSUB 7900
1120 GOSUB 8100
1125 CALL "ZZPROM","4",X3$,Z,"","","",0
1150 PRINT @(0,3),'CE',
1500 REM "
1520 READ (Z[1],KEY=K9$,DOM=9900)IOL=0310
1522 FIND (Z[1],KEY=K9$+"YE"+G0$(34,4))IOL=0361
1523 G9$=G1$(15+NUM(G1$(13,2))*6,6)
1530 GOSUB 6600
1550 G1$=STR(NUM(G0$(34,4))+1:"0000"),G2$=STR(NUM(G0$(34,4))-NUM(G0$(30,2)):"0000")
1555 IF G0$(40,1)="A" THEN G5$=G2$ ELSE G5$=STR(NUM(G0$(34,4))-NUM(G0$(183,2)):"0000")
1560 GOSUB 6000; GOSUB 6200
1590 GOTO 1700
1800 REM "
1820 CALL "ZZPROM","X0GL2UCA",X3$,A,"","","",0; ON A GOTO 1821,9900
1900 REM 
1910 GOSUB 8050
2000 REM "Process Year End Functions
2010 DIM C[18]
2020 READ (Z[3],KEY="",DOM=2021)
2040 K5$=KEY(Z[3],END=2300)
2045 PRINT @(12,18),U7$," data for: ",K5$,
2100 REM 
2110 IF POS(K5$(17,1)="AC")=0 OR K5$(13,4)<>G0$(34,4) THEN GOTO 2280 ELSE GOSUB 7200
2200 REM "
2220 N0=0; FOR X=0 TO 14; N0=N0+C[X]; NEXT X
2230 IF C$(1,12)<G0$(89,12) THEN GOTO 2250
2240 IF C$(17,1)="A" THEN N1=N1+N0 ELSE N2=N2+N0
2245 GOTO 2280
2250 DIM C[18]; C$=C$(1,12)+G1$+C$(17,1)
2260 GOSUB 7215
2265 C[0]=N0
2270 GOSUB 7250
2280 IF K5$(17,1)="A" AND K5$(13,4)<=G5$ THEN REMOVE (Z[3],KEY=K5$,DOM=2290) ELSE IF K5$(17,1)<>"A" AND K5$(13,4)<=G2$ THEN REMOVE (Z[3],KEY=K5$,DOM=2290) ELSE READ (Z[3],KEY=K5$,DOM=2281)REC$
2290 GOTO 2040
2300 REM "PROCESSED YEAR END STUFF; NET= N1
2340 DIM C[18]; C$=G0$(41,12)+G1$+"A"
2350 GOSUB 7215
2360 IF N1<>0 THEN C[0]=C[0]+N1; GOSUB 7250
2370 DIM C[18]; C$=G0$(41,12)+G1$+"C"
2380 GOSUB 7215
2390 IF N2<>0 THEN C[0]=C[0]+N2; GOSUB 7250
2400 REM "
2500 REM "PURGE G/L DETAIL THROUGH PERIOD P0$
2508 PRINT @(0,17),'CE',@(22,17),"Processing transaction detail:",
2515 READ (Z[2],KEY="",DOM=2516)
2520 K$=KEY(Z[2],END=2550); READ (Z[2])
2525 PRINT @(22,19),U8$," ",K$," ",'CL',
2530 IF K$(13,4)<=P0$ THEN GOTO 2535
2531 READ (Z[2],KEY=K$(1,12)+$FFFF$,DOM=2532)
2532 GOTO 2520
2540 REMOVE (Z[2],KEY=K$)
2545 GOTO 2520
2550 PRINT @(0,17),'CE',
2560 IF F0$="P" THEN RETURN 
3000 REM "Remove summary record for years that summary were removed
3010 READ (Z[6],KEY=X3$(9,3)+"G/LYE",DOM=3011)
3020 K$=KEY(Z[6],END=3050)
3030 IF LEN(K$)<13 OR K$(1,8)<>X3$(9,3)+"G/LYE" THEN GOTO 3050
3040 IF K$(9,4)<G2$ THEN REMOVE (Z[6],KEY=K$) ELSE GOTO 3050
3045 GOTO 3020
5000 REM "COMPLETE
5001 DIM G1$(105)
5002 EXTRACT (Z[6],KEY=X3$(9,3)+"G/LYE"+G0$(34,4))IOL=0361
5005 G1$(99,1)="C"; WRITE (Z[6],KEY=G1$(1,12))IOL=0361
5010 G0$(185,4)=G0$(34,4),G0$(34,6)=STR(NUM(G0$(34,4))+1:"0000")+"01"
5020 WRITE (Z[6],KEY=G0$(1,6))IOL=0360
5030 CALL "ZZPROM",".4",X3$,Z,"Year end processing complete.","","",0
5050 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(0,4+V0),"This option will perform the Year End processing for the General Ledger",@(0,5+V0),"Fiscal Year Ending:",@(0,7+V0),"All G/L Accounts starting with and including:",@(0,10+V0),"through the end of the Chart of Accounts will be brought forward with Zero",@(0,11+V0),"balances, and the Net Posting for these accounts will be posted to account:",@(0,15+V0),"Account Balance Summary History will be removed through and including:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6210 PRINT @(20,5+V0),FND$(G9$)
6220 CALL "ZZDISP","A",G0$(89,12),"G/L",X3$,"","",13,8+V0,X4$
6225 PRINT @(29,8+V0),A0$(13,30),
6230 CALL "ZZDISP","A",G0$(41,12),"G/L",X3$,"","",13,13+V0,X4$
6235 PRINT @(29,13+V0),A1$(13,30),
6240 PRINT @(70,15+V0),G2$
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6410 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 PRINT 
6462 GOSUB 7700
6464 CALL "ZZDISP","A",A$(7+K9,10),"AR5",X3$,"","",24,5+V0,X4$
6468 PRINT @(39,5+V0+L0),B$(11,25),
6472 CALL "ZZDISP","A",A$(17+K9,8),"AR6",X3$,"","",24,6+V0,X4$
6476 PRINT @(37,6+V0+L0),C$(36,15),
6490 RETURN 
6600 REM "ALT KEY DATA READS
6610 DIM A0$(50),A1$(50)
6620 FIND (Z[5],KEY=G0$(89,12),DOM=6621)A0$
6630 FIND (Z[5],KEY=G0$(41,12),DOM=6631)A1$
6690 RETURN 
6700 REM "Period end change message
6705 PRINT (0,ERR=6710)'SB',
6710 PRINT @(10,8+V0),"Current Accounting Period:",@(41),"Fiscal Y/E:",@(19,10+V0),"Change to Period:",
6720 PRINT (0,ERR=6721)'SF',
6730 PRINT @(37,8+V0),G0$(38,2),@(54),G0$(34,4),
6740 PRINT @(37,10+V0),STR(NUM(G0$(38,2))+1:"00")
6745 RETURN 
7200 REM "READ STATISTICAL DATA KEY=K5$, goto 7215 if C$ is already set
7205 C$=K5$
7215 X9=POS(K5$(17,1)=F9$(49),17); IF X9<>0 THEN M0$=F9$(X9+49+1,3)
7220 Q0$=C$(LEN(C$),1),Q1$=C$(1,LEN(C$)-1)
7230 CALL "ZZPACK",X3$,"R","","",0,0,C{ALL},Z[4],Q0$,Q1$,F9$
7245 RETURN 
7250 REM "Write stats last read,if q0$&q1$ have not changed
7260 CALL "ZZPACK",X3$,"W","","",0,0,C{ALL},Z[4],Q0$,Q1$,F9$
7290 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts: 
7550 REM "Added stmts: 
7575 REM "Deleted stmts: 
7700 REM "PRINT P/E DATE"
7720 PRINT @(39,4+V0),"Ending ",FND$(P1$(NUM(A$(5+K9,2))*6+15,6)),
7749 RETURN 
7900 REM "> Maxperiods Detail Retained
7905 G4$=G0$(34,4),G5=NUM(G0$(38,2)),P0=NUM(G0$(27,2))
7906 READ (Z[6],KEY=X3$(9,3)+"G/LYE"+G0$(34,4),DOM=9900)G3$
7910 IF G5-P0>0 THEN P0$=FNE$(G4$)+STR(G5-P0:"00"); GOTO 7940
7915 P0=P0-G5,G4$=STR(NUM(G4$)-1:"0000")
7920 FIND (Z[6],KEY=X3$(9,3)+"G/LYE"+G4$,DOM=7921)G3$
7925 G5=NUM(G3$(13,2))
7930 GOTO 7910
7940 P2$=FND$(P0$(1,2)+"0101"),P2$=P2$(7)
7990 RETURN 
8000 REM "Check year not closed and if new year exists"
8010 IF G1$(99,1)<>"O" AND G1$(99,1)<>" " THEN CALL "ZZPROM",".4",X3$,Z,G0$(34,4)+" is already closed.  Check with the System Adminstrator.","","",0; GOTO 9900
8030 REM "Is prev. year closed?"
8033 Q9$=X3$(9,3)+"G/LYE"+STR(NUM(G0$(34,4))-1:"0000")
8035 FIND (Z[6],KEY=Q9$,DOM=8040)Q9$
8038 IF Q9$(99,1)=" " THEN CALL "ZZPROM",".4",X3$,Z,"The year "+Q9$(9,4)+" must be closed before this year can be closed","","",0; GOTO 9900
8040 RETURN 
8050 REM "Check for next year available
8055 Q9$=X3$(9,3)+"G/LYE"+STR(NUM(G0$(34,4))+1:"0000")
8060 FIND (Z[6],KEY=Q9$,DOM=8061); GOTO 8070
8063 CALL "ZZPROM",".4",X3$,Z,"The Period Ending Dates for "+Q9$(9,4)+" need to be setup","","",0
8065 CALL "GL2MQA",X3$,X4$,Q0$,Q9$(9,4); C9=1; GOSUB 6400; GOTO 8060
8090 RETURN 
8100 REM "Yearly Detaill Retention
8110 PRINT @(0,3),'CE',
8120 PRINT @(15,10),'SB',"G/L postings are to be retained for ",'SF',G0$(27,2),'SB'," periods,",@(15,11),"meaning  that  the  G/L postings  prior to and",@(15,12),"including period ",'SF',P0$(3,2),'SB',"  of fiscal year ",'SF',P2$,'SB',"  will",@(15,13),"be removed by this update.",'SF',
8140 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9012 IF ERR=2 AND Y6=7260 THEN F8$="*",F7$=FN%FID$(Z[3]); CLOSE (Z[3]) ! [205703]-changed FID() to FN%FID$()
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 IF F8$="*" THEN F8$=""; OPEN (Z[3])F7$(4,6); READ (Z[3],KEY=K5$,DOM=9046)REC$
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9505 SETERR 9000
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM + Modification History
56002 REM "205703-Oracle - FID and FIB calls, replace with FN%FID$ and FN%FIB$
