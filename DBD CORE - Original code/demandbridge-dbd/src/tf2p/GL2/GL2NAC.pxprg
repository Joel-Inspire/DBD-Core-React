0010 REM "Account number change processing <GL2NAC>
0035 REM "5.7 - 07/10/20 - 12.314444 - crg - DBSPT-93588
0037 REM "DBSPT-93588 - PxPlus 2017 compatible physical updates for data files
0040 REM "Copyright 2020 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 REM SETERR 09000; SETESC 09300
0106 SETERR 0000; SETESC 0000; REM "****** TAKE OUT FOR LIVE RUN *********** SEE 105
0110 X0$="GL2NAC",X1$="Change G/L Account Numbers"
0120 DIM A$(256),A[19],B$(60),C$(227),C[2],D$(214),J$(46),S$(40),D[15],E[15],G[14],G1[14]
0121 DIM S[10,2]
0140 Z0$="-##0",Z1$="-##,###.00#",Z2$="##,##0",Z3$="-######,##0",Z4$="#,###.00",Z5$="####,###.00"
0210 T=1,T0=1,T1=1
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "I/O lists
0330 IOLIST X$,X[0],X[1],X[2],X[3],X[4],X[5],X[6]
0340 IOLIST D$,D[0],D[1],D[2],D[3],D[4],D[5],D[6],D[7],D[8],D[9],D[10],D[11],D[12],D[13],D[14]
0350 IOLIST E$,E[0],E[1],E[2],E[3],E[4],E[5],E[6],E[7],E[8],E[9],E[10],E[11],E[12],E[13],E[14],E[15]
0360 IOLIST G$,G[0],G[1],G[2],G[3],G[4],G[5],G[6],G[7],G[8],G[9],G[10],G[11],G[12],G[13]
0370 IOLIST G1$,G1[0],G1[1],G1[2],G1[3],G1[4],G1[5],G1[6],G1[7],G1[8],G1[9],G1[10],G1[11],G1[12],G1[13]
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]
0510 Y$="01O GL1... 02O GL2...  03O GL3...  04O GL4...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0540 READ (Z[13],KEY=X3$(9,3)+"G/L",DOM=9900)F9$
0550 READ (Z[13],KEY=X3$(9,3)+"G/LYE"+F9$(34,4),DOM=9901)F8$
0555 P9=NUM(F8$(13,2))
0560 REFILE "GLF"+X3$(9,3)
0600 REM 
0610 CALL "ZZPRIN","GL2NAC",X3$,Y0$,F$,Z{ALL},"D",H1$,H0,9,1,128,H
0620 CALL "ZZPROM","YE",X3$,ZZ1,"Are you updating all G/L Account Numbers?","","",0; IF ZZ1=2 THEN GOTO 9900; REM "Changed prompt to allow exit at this point
0700 REM "CHECK IF ALL FILES ARE CLEARED
0710 FLG$=""
0720 CALL "GL2NAB",X3$,X4$,FLG$
0730 IF FLG$="*" THEN GOTO 9900
0740 E1$="GL1GL2GL3GL4"
0750 PRECISION 4
0800 REM CHECK TO SEE IF GL1-GL4 HAVE GOOD ACCOUNT NUMBERS
0805 PRINT @(18,9),'CE',"Checking for bad records before processing",
0810 FOR X=1 TO 4
0820 K$=KEY(Z[X],END=0890)
0830 READ RECORD (Z[X],KEY=K$)D$
0832 XX=XX+1; IF FPT(XX/100)=0 THEN PRINT @(18,11),"Processing ",E1$(X*3-2,3)," record ",K$,"   ",XX:"###,##0",
0835 IF LEN(K$)<12 OR LEN(D$)<12 THEN GOTO 0860
0840 IF K$(1,12)<>D$(1,12) OR NUM(K$(1,12),ERR=0860)<0 OR NUM(D$(1,12),ERR=0860)<0 THEN GOTO 0860
0850 GOTO 0820
0860 DD$=K$(1,12)+" is a bad record in "+E1$(X*3-2,3)+" with a key of "+K$
0870 PRINT (9)DD$
0880 BAD=BAD+1
0885 GOTO 0820
0890 NEXT X
0900 REM "Check if there are bad records
0905 PRINT @(0,9),'CE'
0910 IF BAD=0 THEN GOTO 1000
0920 CALL "ZZPROM","4",X3$,0,"Problems found!! CHECK printout and contact DemandBridge. RETRY AGAIN LATER!!","","",0
0930 GOTO 9900
1500 REM OPEN FILES
1510 Y$="01C GL1...  02C GL2...  03C GL3...  04C GL4...  02O GLN...  12X ZZE...  13O ZZPARM  "
1520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
1600 REM 
1620 GOTO 5000
4050 F0$="TMP"+FID(0)
4100 REM "PROCESS A FILE
4102 F0$="TMP"+FID(0)
4105 Y$="10O "+F1$+"... "
4110 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1; ON Z0 GOTO 4111,4180
4115 Q$=FID(Z[10]); MX=FN%ERASE(F0$,ERR=4116); GOTO 4115 ! SSP#282032    
4116 IF ERR<>12 THEN GOTO 9000
4118 D0=ASC(Q$(20,1)),D9$=STP(Q$(4,6)+Q$(21,2))
4120 Q$(4,6)=F0$,Q$(1,3)=$000000$,Q$(11,1)=CHR(ASC(Q$(11,1))-4); FILE Q$; D0=ASC(Q$(20,1)) ! 247750 - Reduce the key size value, it is inflated by 4
4121 IF SSN<"14" THEN CALL "*conv.tbd/tfdd_updt;Update_Physical",ERR=*NEXT,MID(D9$,1,3),F0$,"DN",EMSG$ ELSE CALL "*dict/dd_updt;Update_Physical",ERR=*NEXT,FN%GETDDFNAME$(MID(D9$,1,3)),F0$,"DN",EMSG$ ! ssp#282032, DBSPT-93588
4122 Y$="11O "+F0$+" "
4125 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1; IF Z0<>0 THEN CALL "ZZPROM","4",X3$,0,"Problem opening "+F0$+" file!!!","","",0; GOTO 4190
4130 PRINT @(18,12),"Currently processing:",
4133 DIM E$(50); FIND (Z[12],KEY=F1$+"   ",DOM=4134)E$
4135 PRINT @(45,12),E$(7,39),
4150 IF F4$="" THEN GOSUB 4200 ELSE GOSUB 7800
4175 PRINT @(0,12),'CE',
4180 Y$="10C "+F1$+"... 11C "+F0$+" "
4182 IF F2$<>"" THEN Y$=Y$+"09C "+F2$+"...  "
4183 GOSUB 9750
4185 MX=FN%RENAME(D9$,F1$+"X") ! SSP#282032
4186 MX=FN%RENAME(F0$,D9$); IF SSN<"14" THEN CALL "*conv.tbd/tfdd_updt;Update_Physical",ERR=*PROCEED,MID(D9$,1,3),D9$,"D",EMSG$ ELSE CALL "*dict/dd_updt;Update_Physical",ERR=*NEXT,FN%GETDDFNAME$(MID(D9$,1,3)),D9$,"D",EMSG$ ! SSP#282032, DBSPT-93588
4187 MX=FN%ERASE(F1$+"X",ERR=*NEXT) ! SSP#282032                                   
4190 REM "CLEAR R() FOR NEXT FILE
4192 DIM R[20],S[4,2]
4193 F2$="",F4$="",GL_SEQ=0
4195 RETURN 
4200 REM "Actual substitution loop
4201 REM "K0$=orig key, K1$=new key, D0$=orig data, D1$=new data, C0$=control data
4205 READ (Z[10],KEY="",DOM=4206)
4210 K0$=KEY(Z[10],END=4290)
4215 IF E$(47,1)="S" THEN D0$=""; READ (Z[10]) ELSE READ RECORD (Z[10],KEY=K0$)D0$
4218 REM PRINT @(18,14),'CE',K0$,
4240 IF GL$<>"GL3" THEN GOSUB 4400
4245 IF GL$="GL3" THEN GOSUB 7000; GOTO 4260
4249 REM "Write with DOM, if GL_SEQ set, change K1$, and D1$ seq number (GL_SEQ alway 2 characters) using FNC funciton
4250 IF D0$>"" THEN WRITE RECORD (Z[11],KEY=K1$,DOM=4256)D1$; GOTO 4260
4255 IF D0$="" THEN WRITE (Z[11],KEY=K1$,DOM=4256); GOTO 4260
4256 IF GL_SEQ THEN SEQ$=FNC$(FNX(K1$(GL_SEQ))+1),K1$(GL_SEQ,2)=SEQ$,D1$(GL_SEQ,2)=SEQ$; GOTO 4250; REM "GL_SEQ is always 2 long, use FNC$ and FNX to inc by 1
4260 IF FPT(COUNT/100)=0 THEN PRINT @(18,16),K0$,
4265 IF F2$>"" THEN GOSUB 4600
4285 GOTO 4210
4290 COUNT=0
4295 RETURN 
4400 REM "Rebuild K0$ to K1$ and D0$ to D1$ if needed based on C0$
4405 K1$=K0$,D1$=D0$
4409 REM "If no key elements to change, go to data change
4410 IF R[1]=0 THEN GOTO 4450
4420 DD$=K1$(R[1],R[0]),P0$=DD$
4425 GOSUB 4500; REM "**** GET NEW G/L NUMBER FOR KEY
4430 K1$(R[1],R[0])=P1$
4440 COUNT=COUNT+1
4450 REM "DATA PORTION
4452 IF D0$="" THEN GOTO 4480
4455 FOR I=2 TO 20; IF R[I]=0 THEN EXITTO 4480
4456 DD$=D0$(R[I],R[0]),P1$=DD$
4460 P0$=D0$(R[I],R[0]); GOSUB 4500; D1$(R[I],R[0])=P1$
4465 NEXT I
4495 RETURN 
4500 REM "Given original G/L number return new G/L # in P1$
4510 P1$=P0$
4515 DIM GL1$(60)
4516 IF STP(DD$,3)="" THEN GOTO 4590
4520 READ (Z[2],KEY=DD$,DOM=4580)GL1$
4530 P1$=GL1$(48,12)
4540 GOTO 4590
4582 FF$=F1$; IF F2$<>"" THEN FF$=F2$(1,3)
4584 IF ZZ1=0 THEN PRINT (9)DD$," is an invalid G/L # in '",FF$,"'; Key is ",K0$
4590 RETURN 
4600 REM "PROCESS INDEXED TRANS FILES FOR EACH RECORD
4601 REM "Y$ IS TO OMIT SELF-LINKING LISTS
4605 DIM X[10]; READ (Z[11],KEY=K1$)IOL=0330
4610 FOR X=1 TO LEN(F2$) STEP 3
4612 L0=INT(X/3),Y$=""
4613 L1=X[S[L0,0]],L2=S[L0,1]; IF L1=0 THEN GOTO 4690
4615 Y$="09O "+F2$(X,3)+"... "; GOSUB 9750; FIL_ID=Z[9]
4620 Y$=""
4650 REM "PROCESS LINKS IN Z(9) STARTING WITH L1
4655 READ RECORD (Z[9],IND=L1)X$
4656 IF POS($00$<>X$)=0 THEN GOTO 4685
4657 COUNT=COUNT+1
4658 IF FPT(COUNT/100)=0 THEN PRINT @(12,18),"Sub-file: ",L0," index: ",L1:"###,##0",
4660 P=POS($8A$=X$),L3=NUM(X$(1,P-1)),Y$=Y$+BIN(L1,3)
4665 P=P+L2,P0$=X$(P,R[0]); DD$=P0$; GOSUB 4500
4670 X$(P,R[0])=P1$
4675 WRITE RECORD (Z[9],IND=L1)X$
4680 IF L3>0 AND POS(BIN(L3,3)=Y$,3)=0 THEN L1=L3; GOTO 4655
4685 Y$="09C "+F2$(X,3)+"... "; GOSUB 9750
4688 COUNT=0
4690 NEXT X
4695 RETURN 
5000 REM "
5010 CALL "ZZPROM",".Y",X3$,Z,"Are you ready to begin this process?","","",0
5020 ON Z GOTO 5021,9900
5250 REM "Set constants for rebuild routine
5290 PRINT @(18,9),"Account Renumbering in Progress",
5300 REM "Begin rebuilding
5301 REM "F1$=FILE, F2$=INDEXED DETAIL FILE
5302 REM "R(0)=LENGTH OF G/L # (12), R(1)=KEY POS, R(2)..R(20)= DATA POSITION  IN STRING FROM READRECORD D0$ & D1$
5303 OR_SETTING=PRM('OR'); SET_PARAM ('OR')=0
5305 DIM R[20]
5306 F1$="GL1",R[0]=12,R[1]=1,R[2]=1; GOSUB 4100
5307 Y$="01O GL1...  "
5308 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
5310 F1$="GL2",R[0]=12,R[1]=1,R[2]=1,GL_SEQ=30; GOSUB 4100
5311 F1$="GL4",R[0]=12,R[1]=1,R[2]=1; GOSUB 4100
5312 F1$="GL6",R[0]=12,R[1]=14,R[2]=14,GL_SQ=26; GOSUB 4100
5313 REM LET F1$="GLF", R[0]=12, R[1]=2, R[2]=2; GOSUB 04100
5314 F1$="PA2",R[0]=12,R[1]=0,R[2]=30; GOSUB 4100
5315 F1$="ZYB",R[0]=12,R[1]=0,R[2]=57,R[3]=69,R[4]=81; GOSUB 4100
5316 F1$="AP4",R[0]=12,R[1]=0,R[2]=209,R[3]=271; GOSUB 4100
5317 REM "**** INDEXED FILE **** LET F1$="API", R[0]=12, R[1]=0, R[2]=94, R[3]=100, F2$="APJAPK", S[0,0]=0, S[0,1]=37, S[1,0]=1, S[1,1]=28; GOSUB 04100
5318 REM "**** STATISTICS FILE **** LET F1$="AP9", F4$="A/P", R[0]=12; GOSUB 04100
5319 F1$="BR0",R[0]=12,R[1]=0,R[2]=67; GOSUB 4100
5320 F1$="CR0",R[0]=12,R[1]=0,R[2]=82; GOSUB 4100
5322 F1$="FR9",R[0]=12,R[1]=1,R[2]=1; GOSUB 4100
5324 F1$="ARI",R[0]=12,R[1]=0,R[2]=10,R[3]=22,R[4]=34,R[5]=46,R[6]=58,R[7]=70,R[8]=82,R[9]=94,R[10]=106,R[11]=118,R[12]=130,R[13]=142,R[14]=154,R[15]=166,R[16]=178,R[17]=190,R[18]=202; GOSUB 4100
5325 F1$="APX",R[0]=12,R[1]=0,R[2]=33,R[3]=45,R[4]=57,R[5]=69,R[6]=81,R[7]=93,R[8]=105,R[9]=117,R[10]=0; GOSUB 4100
5326 F1$="ARV",R[0]=12,R[1]=0,R[2]=94,R[3]=106,R[4]=118,R[5]=130,R[6]=142,R[7]=154,R[8]=166,R[9]=178,R[10]=190,R[11]=202; GOSUB 4100
5327 F1$="API",R[0]=12,R[1]=0,R[2]=0,R[3]=0,F2$="APK",S[0,0]=1,S[0,1]=40; GOSUB 4100
5328 F1$="APN",R[0]=12,R[1]=0,R[2]=0,R[3]=0,F2$="APO",S[0,0]=0,S[0,1]=28; GOSUB 4100
5329 F1$="AR6",R[0]=12,R[1]=0,R[2]=0,R[3]=0,F2$="ARO",S[0,0]=0,S[0,1]=38; GOSUB 4100
5330 F1$="GLT",R[0]=12,R[1]=0,R[2]=16; GOSUB 4100
5335 F1$="AZD",R[0]=12,R[1]=0,R[2]=27; GOSUB 4100 ! SSP247688
5347 R[0]=12,R[1]=1,F1$="GL3",GL$="GL3",F4$="G/L"; GOSUB 4100; REM "DO GL3 WITH DOM ON WRITE - MUST USE STATISTICAL FILES
5350 REM 
5360 CALL "ZZINIT",STR(Z[2]:"00"); REM "Initialize GLN file
5365 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
5380 CALL "ZZPROM","4",X3$,0,"Account number change utility completed!!!","","",0
5398 SET_PARAM ('OR')=OR_SETTING
5399 GOTO 9900
5400 REM "Check for things in progress
5401 REM "Temp lock ZZPARM.  Lock on ZM1 will keep em off (5405)
5402 LOCK (Z[13],ERR=5403); UNLOCK (Z[13]); GOTO 5405
5403 PRINT @(0,21),'CL','BB',"Update will not be initiated at this time.  Try again later",'EB',; CALL "ZZPROM",".4",X3$,0,"Activity detected!  All other terminals must be logged off during update.","","",0; GOTO 9900
5405 Y$="01O ARB...  02O IC3...  03O ICA...  04O FM6...  05O PO7...  09L ZM1    "
5410 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1; ON Z0 GOTO 5411,9900
5415 X5=0
5420 F5=1,F5$="Accounts Receivable Invoicing (ARB)"; GOSUB 5500
5425 F5=2,F5$="Inventory Physical Count (IC3)"; GOSUB 5500
5430 F5=3,F5$="Inventory Transaction Entry (ICA)"; GOSUB 5500
5435 F5=4,F5$="Items Management Physical Count (FM6)"; GOSUB 5500
5440 F5=5,F5$="P/O Receiving (PO7)"; GOSUB 5500
5480 Y$="01C ARB...  02C IC3...  03C ICA...  04C FM6...  05C PO7...  "
5485 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
5490 IF X5<>0 THEN CALL "ZZPROM",".4",X3$,Z,"","","",0; GOTO 9900
5495 RETURN 
5500 REM "See if given file has any records, if yes give message and exit program
5510 READ (Z[F5],KEY="",DOM=5511)
5515 READ (Z[F5],END=5545)
5519 REM "If here then it has records in it
5520 IF X5=0 THEN PRINT @(17,10),"Renumbering cannot proceed until the",@(15,11),"transactions in these files are processed:",; X5=13
5525 PRINT @(18,X5),F5$; X5=X5+1
5545 RETURN 
6000 REM 
6005 PRINT 'CF','SB',
6010 PRINT @(12,3),"Current fiscal year is ",F9$(34,4),
6015 P0=(NUM(F8$(13,2))-1)*6+21,D0$=F8$(P0,6)
6020 PRINT " ending ",FND$(D0$),
6085 PRINT 'SF',
6090 RETURN 
6100 REM "
6110 Y5$(6)="From",Y5$(20)="To"
6120 GOSUB 7300
6199 RETURN 
6900 REM "SORT
6905 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6940 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,X9
6990 RETURN 
7000 REM "CHANGE & UPDATE GL3
7005 DIM G[14],G1[14]
7010 READ (Z[10],KEY=K0$)IOL=0360
7020 P0$=G$(1,12),P1$=P0$
7025 DD$=P0$
7030 GOSUB 4500
7040 G$(1,12)=P1$,K0$(1,12)=P1$
7050 WRITE (Z[11],KEY=K0$,DOM=7060)IOL=0360
7055 GOTO 7090
7060 READ (Z[11],KEY=K0$)IOL=0370
7070 FOR X=0 TO 13
7075 G[X]=G[X]+G1[X]
7080 NEXT X
7085 WRITE (Z[11],KEY=K0$)IOL=0360
7090 RETURN 
7800 REM "PROCESS STATS FILE
7801 REM "F4$=STAT TYPE
7802 R[0]=4,K2$=""
7803 OP2=FFN("ZZPARM")
7804 IF OP2=0 THEN OPEN (Z[13])"ZZPARM" ELSE IF OP2<>0 THEN Z[13]=OP2; REM "Added else on ssp112962
7805 READ (Z[13],KEY="STAT"+F4$,DOM=7900)F4$
7810 READ (Z[10],KEY="",DOM=7811)
7815 K0$=KEY(Z[10],END=7900); READ (Z[10])REC$; K0=LEN(K0$); REM SSP 201900
7816 REM PRINT @(18,14),'CE',K0$,
7820 Q$=K0$(LEN(K0$)); P=POS(Q$=F4$(49),17),Q0$=F4$(49+P-1,17)
7830 CALL "ZZPACK",X3$,"R",Q0$(3,2),"",P9,0,D{ALL},Z[10],Q$,K0$(1,K0-1),F4$
7850 REM "PROCESS PERIODS
7860 FOR X=0 TO P9
7862 PRINT @(22,18),"Period: ",X:"#0",
7863 K1$=K0$
7864 DD$=K0$(1,12)
7865 P0$=K0$(1,17); GOSUB 4500
7870 K1$=K0$,K1$(1,12)=P1$(1,12)
7872 DIM E[14]
7873 READ (Z[11],KEY=K1$,DOM=7876)
7875 CALL "ZZPACK",X3$,"R",Q0$(3,2),"",P9,0,E{ALL},Z[11],Q$,K1$(1,K0-1),F4$
7880 D[X]=D[X]+E[X]
7885 NEXT X
7890 CALL "ZZPACK",X3$,"W",Q0$(3,2),"",P9,0,D{ALL},Z[11],K1$(K0,1),K1$(1,K0-1),F4$; K2$=K1$
7891 REM PRINT @(10,22),D[ALL]
7895 GOTO 7815
7995 RETURN 
8890 XX=0
8900 REM "functions
8905 DEF FNC$(C)=CHR(32+INT(C/95))+CHR(32+MOD(C,95))
8910 DEF FNX(Z9$)=(ASC(Z9$(1,1))-32)*95+(ASC(Z9$(2,1))-32)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9041 OP1=FN%FFN("ZZE") ! [205471]-changed FFN() to FN%FFN()
9042 IF OP1=0 THEN OPEN (Z[12])"ZZE"
9043 OP2=FFN("ZZPARM")
9044 IF OP2=0 THEN OPEN (Z[13])"ZZPARM"
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9750 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
9780 RETURN 
9900 REM "End program
9910 DROP "ZZPACK",ERR=9911
9920 REM CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z(ALL),0,0
9940 RUN "ZMENU"
9999 END 
56000 REM + Modification History
56002 REM "201900-PE(026, GL2NAC-7815) when running GL update.                
56004 REM "205471-Oracle - FFN usage in tf2g progs. to be replaced by FN%FFN  
56006 REM "247688-The AZD file is not included in the GL Number change utility
56008 REM "247750-GL2NAC - GL number change util clobbers file structure
56009 REM "282032-Getting an error 080 when she clicks on the query           
56010 REM "DBSPT-93588 - PxPlus 2017 compatible physical updates for data files
