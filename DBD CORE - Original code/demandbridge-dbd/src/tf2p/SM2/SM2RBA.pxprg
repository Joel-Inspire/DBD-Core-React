0010 REM "<SM2RBA> Customer Invoicing Gateway Plus Journal"
0035 REM "5.7 - 01/22/14 - 16.563611 - dmm - SSP# 266510
0037 REM "266510-Flowpoint; add PO Code, Vendor, Product Code, Starting Num  
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "If V0$(80,1)='Y', the report is printed in order by INDing thru the file:wq
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="SM2RBA",X1$="Customer Invoicing Gateway Plus Journal"
0120 DIM A$(345),A[12],B$(40),C$(54),C[3],D$(37),D[2],S0$(20),M$(20),M[1],LAST_ORDER_NUM$(8),AS5_LAST_ORDER_NUM$(8) ! WO257561
0140 Z0$="#,###.00-",Z1$="#,###,##0-",Z2$="##,###,##0.00-",Z3$="###,###.00-",Z4$="###.00%" ! WO235823
0150 DEF FNQ$(Z8,Z9$)=STR(Z8:Z9$(1,POS(".00"=STR(Z8:Z9$)+".00")-1))
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0175 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0210 T=1,T0=1,T1=1
0220 W3=130
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "I/O lists
0305 REM "IOLIST FOR AR1 - Customer Masterfile
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12]
0320 FS1_IOLIST:IOLIST FS1$
0330 AR1_IOLIST:IOLIST AR1$
0340 FS2_IOLIST:IOLIST FS2$,FS2[0],FS2[1],FS2[2],FS2[3],FS2[4],FS2[5],FS2[6],FS2[7],FS2[8],FS2[9],FS2[10],FS2[11],FS2[12],FS2[13],FS2[14],FS2[15],FS2[16],FS2[18],FS2[19],FS2[20],FS2[21],FS2[22],FS2[23],FS2[24]
0350 PO3_IOLIST:IOLIST PO3$,PO3[0],PO3[1],PO3[2]
0360 FS1S_IOLIST:IOLIST FS1S$ ! SSP266510
0370 AR1S_IOLIST:IOLIST AR1S$ ! SSP266510
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0440 IOLIST FS2$,FS2[0],FS2[1],FS2[2],FS2[3],FS2[4],FS2[5],FS2[6],FS2[7],FS2[8],FS2[9],FS2[10],FS2[11],FS2[12],FS2[13],FS2[14],FS2[15],FS2[16],FS2[17],FS2[18],FS2[19],FS2[20],FS2[21],FS2[22],FS2[23],FS2[24],FS2[25],FS2[26],FS2[27],FS2[28],FS2[29]
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O SM7... 02O FS1... 03O AR1... 04O FS2... 05O PO3... 06O SM4... 07O FMN...  08O AS5...  09O PO2...  10O JT0...  11O AP4...  13O ZZPARM "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0550 DIM M6[1]; CALL "IC2PRE",X3$,Z[13],M6$,M6{ALL},"",0,"",0 ! WO235823
0600 REM "Initialize REMOVE$
0610 REMOVE$=""
0700 REM "Open Printer
0720 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0721,9920
0723 IF %HAS_IMAGE_LIB THEN {! [SSP-200047]-if Image Library is installed.
0725 HLD_X3$=X3$(178,6) ! [SSP-200047]-backup the audit control number
0727 X3$(178,6)=PAD(V0$(81,5),6) ! [SSP-200047]-copy the batch number to audit control number
0730 CALL "ILGAUD",ERR=*NEXT,X3$,X4$,"RG" ! [SSP-200047]-open the pdf file
0735 X3$(178,6)=HLD_X3$ ! [SSP-200047]-restore the audit control number
0737  }
0750 REM "Build 856 file using SMO file
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[4]; V1=9,V2=3,V3=1,V4=1,T0=4,T1=4,U=14,U0=4,U[0]=0,U[1]=9,U[2]=3,U[3]=1,U[4]=1,U0$="     ",T5$="                "; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0895 ! CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920 !SSP#229462 actually calls ZZPRIN twice also on 720
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 IF V0$(80,1)="Y" THEN READ_KNO=2; READ (U1,KEY="",KNO=READ_KNO,DOM=*NEXT) ELSE READ_KNO=0; READ (U1,KEY=V4$,KNO=READ_KNO,DOM=*NEXT,ERR=*NEXT) ! SSP#229776
1000 REM "Read next record
1002 ! IF V0$(80,1)<>"Y" THEN GOTO 1005  SSP#229776
1003 ! A$=""; J=IND(U1,END=5000); READ (U1,IND=I0,END=5000,ERR=1004)IOL=0310 #SSP#229776
1004 ! I0=I0+1; IF A$="" THEN GOTO 1003 ELSE IF A$(1,1)=$00$ THEN GOTO 1003 ELSE U$=A$(1,14),U9$=U$; GOTO 1151 SSP#229776
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])>=V2$(U3,U[U9]) THEN GOTO 1030 ELSE EXTRACT (U1,KEY=U$(1,U2-1)+V2$(U3,U[U9]),DOM=1021)
1025 EXITTO 1000
1030 IF U$(U2,U[U9])<=V2$(U3+U[U9],U[U9]) THEN GOTO 1040 ELSE READ (U1,KEY=U$(1,U2-1)+$FF$,DOM=1032)
1035 EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1130 READ (Z[1],KEY=U$(5,10),DOM=1000)IOL=0310
1135 READ (U1)
1140 GOTO 1160
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1278 C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
1300 REM "Read other data
1310 DIM FS1$(350); FIND (Z[2],KEY=A$(1,8),DOM=1311)IOL=FS1_IOLIST
1320 DIM AR1$(599); FIND (Z[3],KEY=FS1$(6,10),DOM=1321)IOL=AR1_IOLIST
1400 REM "Subtotals"
1415 GOSUB 3100
1490 T=0
1500 REM "Print Data
1502 REM "FIND (Z[2],KEY=A$(1,8),DOM=1503); GOTO 1505
1503 REM "A$(100,1)="O"; REMOVE$=REMOVE$+A$(1,8); REM "If order not here then show as invoiced anyway
1505 IF POS(A$(100,1)="YDCA")=0 AND STP(A$(101,8),3," ")="" THEN GOTO 1506 ELSE REMOVE$=REMOVE$+A$(1,9); REM "If the order has been invoiced, then add it to the list to be removed
1510 IF W+4>W0 THEN GOSUB 6000
1520 CALL "ZZDISP","AX",A$(1,9),"P/O",X3$,X$,"",0,0,X4$; Y5$(1)=X$
1525 Y5$(14)=A$(10,3); REM "P/O line number
1530 Y5$(20)=A$(13,1); REM "Ship to code
1535 REM LET Y5$(23)=A$(14,1); REM "Sequence code
1540 Y5$(25)=STR(A[0]:Z1$); REM "Quantity
1545 Y5$(36)=STR(A[1]:Z0$); REM "Actual Weight
1547 Y5$(46)=STR(A[2]:Z0$); REM "Shipping Weight
1550 Y5$(56)=STR(A[3]:Z0$); REM "Freight Cost
1555 IF A$(97,1)="Y" THEN Y5$(66)="Y" ELSE Y5$(66)="N"; REM "Order updated?
1560 IF A$(98,1)="Y" THEN Y5$(71)="Y" ELSE Y5$(71)="N"; REM "P/O Received?
1563 IF A$(99,1)="Y" THEN Y5$(76)="Y" ELSE Y5$(76)="N"; REM "Don't Bill
1565 Y5$(80)=FND$(A$(15,6)); REM "Shipped on Date
1567 Y5$(91)=A$(31,6); REM "Carrier Id
1571 Y5$(125)=A$(85,8); REM "5 digit zip
1595 GOSUB 7300
1600 REM "line 2
1640 CALL "ZZDISP","AXS",AR1$(1,10),"A/R",X3$,Y5$,"",2,0,X4$
1642 Y5$(15)=AR1$(11,35)
1650 Y5$(56)=STR(A[4]:Z0$); REM "Total cost for this line
1655 Y5$(66)="CSR: "+FS1$(89,4); REM "SSP143548
1660 Y5$(80)=FNT$(A$(21,4)); REM "Shipped on, time
1662 Y5$(91)=A$(37,12); REM "Carrier name
1664 Y5$(104)=A$(69,16); REM "Pickup number
1666 Y5$(125)=A$(94,3); REM "zone
1695 GOSUB 7300
1698 IF STP(MID(A$,383,60),2)<>"" THEN Y5$(1)="Tracking #: "+STP(MID(A$,383,60),2); GOSUB 7300 ! REM "Tracking number SSP257952
1700 REM "Line 3
1703 IF LEN(A$)<111 THEN A$=A$+S0$
1704 IF A$(316,1)="Y" THEN Y5$(4)="**Partial Ship.**" ! SSP260047
1705 IF A$(318,1)="Y" THEN Y5$(21)="**D Type Loc.**" ! SSP260047
1706 GOSUB SPEC_SHIP; IF SPEC_SHIP$>"" THEN Y5$(37)=SPEC_SHIP$ ! SSP260047
1707 IF MID(FS1$,87,2)<>DIM(2) THEN Y5$(53)="Terms: "+MID(FS1$,87,2) ! SSP260047
1709 Y5$(65)="Invoiced:"; REM "invoiced heading
1710 IF A$(100,1)="Y" THEN Y5$(76)="Y" ELSE IF A$(100,1)="O" THEN Y5$(76)="O" ELSE IF A$(100,1)="D" THEN Y5$(76)="D" ELSE IF A$(100,1)="C" THEN Y5$(76)="C" ELSE IF A$(100,1)="A" THEN Y5$(76)="A" ELSE Y5$(76)="N"; REM "Invoiced?
1715 CALL "ZZDISP","AX",A$(101,8),"AR6",X3$,X$,"",0,0,X4$; Y5$(80)=X$; REM "Invoice number invoiced on
1717 IF A$(317,1)="Y" THEN Y5$(80)="STOCK/BAS" ELSE IF A$(100,1)="D" THEN Y5$(80)="DEPOSIT" ELSE IF A$(100,1)="C" THEN Y5$(80)="LINES OPEN" ELSE IF A$(100,1)="A" THEN Y5$(80)="NEVER INV"; REM "Stock or B type order, no invoice number,invoiced? was set to Y so record would clear later. SSP189841, parm set to never create AR invoice
1720 IF POS(" "<>A$(109,3))<>0 THEN Y5$(91)="Packer: "+A$(109,3)
1730 IF STP(A$(266,10),1)<>"" THEN Y5$(104)="Vendor Invoice: "+A$(266,10)
1735 GOSUB 7300
1740 H7$=""
1742 IF POS(" "<>A$(294,12))<>0 THEN H7$=H7$+"Job: "+FNS$(A$(294,12))+"   "+FND$(A$(306,6))+"   "; REM "Job number, job date
1743 IF POS(" "<>A$(147,9))<>0 THEN H7$=H7$+"Ending number: "+A$(147,9)+"   "; REM "Ending number
1745 IF H7$>"" THEN Y5$(2)=H7$; GOSUB 7300
1747 IF V0$(98,1)="Y" AND A$(9,1)<>DIM(1) THEN GOSUB PRINT_SELL_GP_INFO ! WO235823
1750 REM " If error code then get description and print
1755 IF POS(A$(315,1)=" 0")=0 THEN CALL "SM2SDA",X3$,X4$,A$(315,1),DESC$; Y5$(1)="***Error description: "+DESC$; GOSUB 7300
1800 REM "Print order notepad if report parm set
1810 IF V0$(97,1)<>"Y" THEN GOTO 1850
1812 IF A$(1,8)=LAST_ORDER_NUM$ THEN GOTO 1850 ELSE LAST_ORDER_NUM$=A$(1,8)
1815 DIM FMN$(509); FIND (Z[7],KEY=A$(1,8),DOM=1850)FMN$
1820 IF W+10>W0 THEN GOSUB 6000
1823 FIRST_FMN$="Y"
1825 FOR X=9 TO 459 STEP 50
1830 IF FMN$(X,50)=DIM(50) THEN GOTO 1840
1833 IF FIRST_FMN$="Y" THEN Y5$(5)="Order notepad:",FIRST_FMN$="N"
1835 Y5$(20)=FMN$(X,50); GOSUB 7300
1840 NEXT X
1850 ! Print Customer Invoice Entry Message - WO257561
1852 IF MID(V0$,99,1)<>"Y" THEN GOTO 1875
1854 IF A$(1,8)=AS5_LAST_ORDER_NUM$ THEN GOTO 1875 ELSE AS5_LAST_ORDER_NUM$=A$(1,8)
1856 DIM AS5_FS1$(100); FIND (Z[2],KEY=A$(1,8),DOM=1875)AS5_FS1$(1); DIM AS5$(1000); FIND (Z[8],KEY=AS5_FS1$(6,10),DOM=1875)AS5$(1) ! Get customer code from order header, if found then read AS5 for this customer
1858 IF AS5$(11,180)=DIM(180) THEN GOTO 1875 ! this cust doesn't have invoice entry message
1860 IF W+3>W0 THEN GOSUB 6000
1862 FIRST_AS5$="Y"
1864 FOR X=11 TO 131 STEP 60
1866 IF AS5$(X,60)=DIM(60) THEN GOTO 1872
1868 IF FIRST_AS5$="Y" THEN Y5$(5)="Inv Ent Msg:",FIRST_AS5$="N"
1870 Y5$(18)=AS5$(X,60); GOSUB 7300
1872 NEXT X
1875 ! 
1880 ! Print Vendor PO Job Notes - 260047
1882 IF MID(V0$,100,1)<>"Y" THEN GOTO *NEXT ELSE GOSUB VEND_PO_JOB_NOTES
1899 ! 
1900 REM "Accumulate Totals"
1990 T[T0,0]=T[T0,0]+1,T[T0,1]=T[T0,1]+A[0],T[T0,2]=T[T0,2]+A[1]
1991 T[T0,3]=T[T0,3]+A[2],T[T0,4]=T[T0,4]+A[3]
1993 TOTAL_COST=TOTAL_COST+A[4]
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; GOSUB 7000
3155 IF V0$(96,1)="Y" THEN GOSUB PRINT_ORDER_SUMMARY
3160 U1$=U9$(1,V1)
3185 IF U0$(1,1)="Y" THEN GOSUB 6000
3190 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$,T6$=U2$; GOSUB 3050; GOSUB 7000
3260 U2$=U9$(V5,V2)
3285 IF U0$(2,1)="Y" THEN GOSUB 6000
3290 RETURN 
3310 IF V3=0 THEN RETURN ELSE IF U3$=U9$(V6,V3) THEN GOSUB 3400; RETURN 
3320 GOSUB 3420
3330 IF U3$="" THEN GOTO 3360
3350 T=3,T$=U7$,T6$=U3$; GOSUB 3050; GOSUB 7000
3360 U3$=U9$(V6,V3)
3380 RETURN 
3385 IF U0$(3,1)="Y" THEN GOSUB 6000
3390 RETURN 
3410 IF V4=0 THEN RETURN ELSE IF U4$=U9$(V7,V4) THEN RETURN 
3430 IF U4$="" THEN GOTO 3460
3450 T=4,T$=U8$,T6$=U3$; GOSUB 3050; GOSUB 7000
3460 U4$=U9$(V7,V4)
3480 RETURN 
3485 IF U0$(4,1)="Y" THEN GOSUB 6000
3490 RETURN 
4000 PRINT_ORDER_SUMMARY:REM "Print summary of sales order if applicable. SSP266510 - change FS1$ and AR1$ to FS1S$ and AR1S$ to fix timing issue of those vars already used in 1300's when reading data for next record.
4005 ! SSP260698, this line doesn't make sense in here, all are for PO's.  IF U1$(9,1)<>" " THEN GOTO 4095; REM "if P/O THEN SKIP IT
4010 DIM FS1S$(92); FIND (Z[2],KEY=U1$(1,8),DOM=4095)IOL=FS1S_IOLIST ! SSP266510
4015 DIM AR1S$(297); FIND (Z[3],KEY=FS1S$(6,10),DOM=4016)IOL=AR1S_IOLIST ! SSP266510
4020 Y5$(3)="Order Summary:"; GOSUB 7300
4025 CALL "ZZDISP","AX",U1$(1,8),"O/P",X3$,X$,"",0,0,X4$; Y5$(3)=X$ ! SSP260698, was 1,9 and P/O, this is sales order data not PO data
4027 Y5$(15)=FND$(FS1S$(16,6)) ! SSP266510
4028 CALL "ZZDISP","AX",FS1S$(6,10),"A/R",X3$,X$,"",0,0,X4$; Y5$(27)=X$+" "+AR1S$(11,35); IF FS1S$(28,15)<>DIM(15) THEN Y5$(63+LEN(X$))="CUST PO: "+FS1S$(28,15) ! SSP260698, add customer PO to match SM2RAA. SSP266510
4030 GOSUB 7300
4050 GOSUB PRINT_LINE_SUMMARY
4095 PRINT_ORDER_SUMMARY_END:RETURN 
4100 PRINT_LINE_SUMMARY:REM "Print line detail
4105 DIM FS2$(228),FS2[24]; READ (Z[4],KEY=U1$(1,8),DOM=4106)
4110 READ (Z[4],END=4180)IOL=FS2_IOLIST; IF FS2$(147,8)<>U1$(1,8) THEN GOTO 4180
4115 Y5$(6)=FS2$(6,3); IF FS2$(155,1)="M" THEN Y5$(25)=FS2$(50,40); GOSUB 7300; GOTO 4175
4120 IF FS2$(155,1)=" " THEN Y5$(11)=FS2$(19,10) ELSE IF POS(FS2$(155,1)="IC")<>0 THEN CALL "ZZDISP","AX",FS2$(19,10),"ICE",X3$,X$,"",0,0,X4$; Y5$(11)=FS2$(155,1)+"-"+X$ ELSE Y5$(11)=FS2$(155,1)+"-"+FS2$(19,10)
4125 Y5$(25)=FS2$(50,40)
4127 Y5$(66)="PO: "+FS2$(9,1) ! SSP266510, PO Code
4129 Y5$(72)="VN: "; DIM AP4$(426); FIND (Z[11],KEY=FS2$(94,10),DOM=*NEXT)AP4$(1); Y5$(76)=AP4$(11,25) ! SSP266510, first 25 chars of vendor name
4130 Y5$(102)="PC: "+FS2$(29,3) ! SSP266510, Product Code
4132 Y5$(110)="SN: "+FS2$(104,9) ! SSP266510, Starting Number
4135 Y5$(124)="CP: "+FS2$(219,1) ! SSP266510, P/O line complete?
4145 GOSUB 7300
4150 GOSUB PRINT_SHIP_TO
4170 GOSUB 7300
4175 GOTO 4110
4195 PRINT_LINE_SUMMARY_END:RETURN 
4200 PRINT_SHIP_TO:REM "Print shipping info, starting on current line
4204 DIM PO3$(260),PO3[2]
4205 IF FS2$(18,1)<>"Y" THEN GOTO 4265; REM "Goto general shipto. SSP260698/WO259427, was IF FS2$(18,1)="N", changed to <>"Y", can never count on Y/N type being "N", can sometimes be blank
4210 READ (Z[5],KEY=FS2$(147,8)+" "+FS2$(6,3)+" ",DOM=4211)
4215 DIM PO3$(300),PO3[2]; READ (Z[5],END=4260)IOL=PO3_IOLIST; IF PO3$(166,9)+PO3$(7,3)<>FS2$(147,8)+" "+FS2$(6,3) THEN GOTO 4260
4240 GOSUB PRINT_PO3
4255 GOTO 4215
4260 GOTO 4295
4265 REM "Print non-special ships
4270 READ (Z[5],KEY=FS2$(147,8)+DIM(5),DOM=4290)IOL=PO3_IOLIST
4275 GOSUB PRINT_PO3
4295 PRINT_SHIP_TO_END:RETURN 
4300 PRINT_PO3:REM " PRint a po3 record
4305 TO$=""; IF STP(PO3$(12,4),3," ")="" THEN TO$=STP(PO3$(46,30),1) ELSE TO$=STP(PO3$(225,35),1) END_IF ; IF STP(PO3$(106,16),3," ")="" THEN GOTO 4306 ELSE IF LEN(T0$)>0 THEN TO$=TO$+"-" END_IF ; TO$=TO$+STP(PO3$(106,16),1)+","+PO3$(122,2)
4310 Y5$(11)="TO: "+TO$
4315 IF FS2$(124,4)<>M$(4,4) THEN DIM M$(20),M[1]; READ (Z[13],KEY="U/M"+FS2$(124,4),DOM=4316)M$,M[0],M[1]
4320 IF M$(20,1)<>"Y" THEN J=FS2[5],J0=FS2[5] ELSE J=M[1]; IF M[0]<>0 THEN J0=M[0] ELSE J0=M[1]
4335 IF J=0 THEN AMT=0 ELSE IF STP(PO3$(7,3),3," ")="" THEN AMT=FS2[0]/J ELSE AMT=(PO3[0]-PO3[2])/J
4340 Y5$(65)=FNQ$(AMT,Z2$)+" "+STP(FS2$(124,4),1)+"/"+STP(STR(J0:"###,###"),0)
4345 IF PO3$(11,1)="D" THEN Y5$(90)="REC: "+FNQ$(PO3[2],Z2$) ! SSP266510, received to date
4390 GOSUB 7300
4395 PRINT_PO3_END:RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5045 GOSUB 7300
5050 T=0,T$="Report"; GOSUB 7000
5200 REM "
5290 GOTO 9900
6000 REM "Page header
6035 IF T0$="END" THEN W0$=""
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6060 ELSE ON W5 GOTO 6041,9910
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6090 RETURN 
6100 REM "
6105 Y5$(1)="---- Order -----"
6110 Y5$(19)="Shipto"
6115 Y5$(56)="- Updated -"
6120 Y5$(36)="----- Weight ------"
6122 Y5$(56)="Freight"
6125 Y5$(64)="Ord"
6126 Y5$(69)="P/O"
6127 Y5$(74)="Dont"
6128 Y5$(80)="Shipped on"
6130 Y5$(91)="Carrier"
6134 Y5$(122)="Zip code/"
6145 GOSUB 7300
6150 Y5$(1)="Number",Y5$(13)="Line",Y5$(19)=" Code"
6155 Y5$(26)="Quantity"
6157 Y5$(38)="Actual"
6158 Y5$(47)="Shipped"
6160 Y5$(58)="Cost"
6165 Y5$(64)="Updt"
6166 Y5$(69)="Recd"
6167 Y5$(74)="Bill"
6168 Y5$(80)="Date/time"
6170 Y5$(91)="Id/Name"
6172 Y5$(104)="Pickup number"
6174 Y5$(125)="Zone"
6190 GOSUB 7300; GOSUB 7300
6195 RETURN 
6199 RETURN 
7000 REM "Totals logic
7001 IF T>0 THEN GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(36)=T1$(1,LEN(Z0$)),Y5$(46)=T1$(1,LEN(Z0$)),Y5$(56)=T1$(1,LEN(Z0$))
7009 GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" Total ("+STR(T[T,0]:"0")+"):"
7040 Y5$(36)=STR(T[T,2]:Z0$),Y5$(46)=STR(T[T,3]:Z0$),Y5$(56)=STR(T[T,4]:Z0$)
7050 GOSUB 7300
7055 Y5$(56)=STR(TOTAL_COST:Z0$); GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed: "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 IF INT(T2)>0 THEN T4$=STR(INT(T2))+" Hours " ELSE T4$=""
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+STR(T3:"##.##")+" Minutes"
7260 Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1 ! [SSP-200047]
7330 IF POS(" "<>Y5$)=0 THEN {! [SSP-200047]
7332 PRINT (W9)"" ! [SSP-200047]
7334  } ELSE {! [SSP-200047]
7336 PRINT (W9)Y5$ ! [SSP-200047]
7338 IF %IL_PDF THEN PRINT (%IL_PDF,ERR=*NEXT)Y5$ ! [SSP-200047]-print the data to the pdf
7340 Y5$(1)="" ! [SSP-200047]
7342  } ! [SSP-200047]
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7460 W0$="Batch number: "+V0$(81,5); REM " SSP143548
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"  ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
7600 REM "Print final address line
7610 CALL "ZZDISP","AX",A$(134,9),"ZIP",X3$,X$,"",0,0,X4$
7615 Y5$(12)=FNS$(A$(116,16))+", "+A$(132,2)+"   "+X$
7640 RETURN 
7700 REM "PRINT SHIP-TO ADDRESS
7705 F1$="C"+A$(1,10),F2$="Y"
7710 READ (Z[6],KEY=F1$,DOM=7711)
7720 READ (Z[6],END=7790)IOL=0360
7730 IF F1$<>F$(1,11) THEN GOTO 7790
7740 A$(1)=""; A$(1,4)=F$(12,4),A$(11,35)=F$(16,35),A$(56,163)=F$(51,163)
7750 GOSUB 1500
7760 GOTO 7720
7790 F2$=""; RETURN 
7800 CLEAR_FILE:REM "Clear the SM7 file by removing invoiced records.
7801 REM "The string REMOVE$ contains 9 character purchase order numbers. At least one record in SM7 for the indicated order number had an invoice number in the SM7 record indicating that the order had been invoiced, so all records in SM7 beginning with that record will be removed. We are foregoing an additional check whereby we could read the SM7 record and verify the Invoiced? flag was y, because all records of an order should be invoiced at once.
7802 IF Z=2 THEN PRINT @(0,18),'CE',@(25,18),"Removing ALL Records",; CALL "ZZINIT",STR(Z[1]:"00"); GOTO 7845; REM "F9 hidden option to clear all records
7804 PRINT @(0,18),'CE',@(25,18),"Removing Invoiced Records",
7805 T1=0; GOSUB 8100; GOSUB 8150
7810 IF REMOVE$="" THEN GOTO 7830
7812 C=INDEX; GOSUB 8150
7815 READ (Z[1],KEY=REMOVE$(1,9),KNO=0,DOM=7816) ! SSP232022
7820 KEY_1$=KEY(Z[1],END=7825); IF KEY_1$(1,9)<>REMOVE$(1,9) THEN GOTO 7825
7822 REMOVE (Z[1],KEY=KEY_1$,DOM=7820); GOTO 7820
7825 IF LEN(REMOVE$)>9 THEN REMOVE$=REMOVE$(10) ELSE REMOVE$=""
7827 GOTO 7810
7830 C=T; GOSUB 8150
7845 CLEAR_FILE_END:RETURN 
8100 REM "Initialize for Bargraph
8120 T=INT(LEN(REMOVE$)/9); REM "Number of records
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,C
8195 RETURN 
8300 PRINT_SELL_GP_INFO:! WO235823 - Read FS2, try to calculate sell extension based on quantity received, gross profit and GP% to print
8320 EXT_COST=A[4]
8325 QTY_REC=A[0]; IF QTY_REC=0 THEN GOTO *RETURN
8330 DIM FS2$(356),FS2[29]; FIND (Z[4],KEY=A$(1,8)+A$(10,3),DOM=*RETURN)IOL=0440
8335 UNIT_SELL=FS2[4],SELL_UOM$=FS2$(124,4),SELL_QTY_PER_UM=FS2[5]; IF SELL_QTY_PER_UM=0 THEN SELL_QTY_PER_UM=1
8340 EXT_SELL=0; CALL "FM2EXT",Z[13],0,SELL_UOM$,SELL_QTY_PER_UM,QTY_REC,UNIT_SELL,EXT_SELL,M6[1]
8345 GP=EXT_SELL-EXT_COST; IF EXT_SELL=0 THEN GP_PERCENT=0 ELSE GP_PERCENT=GP*100/EXT_SELL
8370 Y5$(2)="Sell extension: "+STR(EXT_SELL:Z3$)
8375 Y5$(30)="Gross profit: "+STR(GP:Z3$)
8380 Y5$(56)="GP%: "+STR(GP_PERCENT:Z4$)
8390 GOSUB 7300
8395 RETURN 
8399 ! 
8400 SPEC_SHIP:! SSP260047, read PO2, see if special shipping
8405 SPEC_SHIP$=""
8410 DIM PO2$(170); FIND (Z[9],KEY=A$(1,12),DOM=*RETURN)PO2$(1)
8420 IF PO2$(14,1)="Y" THEN SPEC_SHIP$="**Spec. Ship.**"
8445 RETURN 
8449 ! 
8450 VEND_PO_JOB_NOTES:! 260047 - Handle display of Vendor PO Job Notes
8452 IF POS(A$(1,9)=JT0_PO_NUM_LIST$,9)=0 THEN JT0_PO_NUM_LIST$+=A$(1,9) ELSE GOTO *RETURN
8455 NOTE_REF$=A$(1,9),REF_TYPE$="P",INVOLVES$="V" ! Get job notes for referenced PO number, restrict results to those involving Vendor only
8460 CALL "ZZGFUN;MAKE_JOB_LIST",REF_TYPE$,NOTE_REF$,INVOLVES$ ! Results are returned in global channel %ZZ_NOTES
8465 NUM_NOTES=0; NUM_NOTES=NUM(FIN(%ZZ_NOTES,"NUMREC",ERR=*NEXT)) ! Get number of job notes entries returned
8470 IF NOT(NUM_NOTES) THEN GOTO *RETURN ! Continue only if at least one job note is found
8475 READ (%ZZ_NOTES,KEY="",DOM=*NEXT)
8480 READ (%ZZ_NOTES,ERR=*NEXT)KJT0$; GOSUB PRINT_JOB_NOTES ! For now print the first/latest job note (%ZZ_NOTES memory file results are in reverse chronological order)
8490 CLOSE (%ZZ_NOTES,ERR=*PROCEED); %ZZ_NOTES=0 ! Clean up
8495 RETURN 
8499 ! 
8500 PRINT_JOB_NOTES:
8510 READ (Z[10],KEY=KJT0$,KNO=0,DOM=*NEXT)JT0$
8515 Y5$(7)="Vendor Job Notes: Entered by "+MID(JT0$,40,3)+" on "+FND$(MID(JT0$,26,6))+" at "+MID(JT0$,32,8); GOSUB 7300
8520 JT0_NOTES$=STP(STP(MID(JT0$,44,1600),2),2,$0A$)
8525 NEXT_JT0_LINE:IF NUL(JT0_NOTES$) THEN GOTO DONE_PRINT_JOB_NOTES ! Finished output of this job notes record
8530 TMP_P1=POS($0A$=JT0_NOTES$); IF TMP_P1>0 THEN NEXT_JT0_LINE$=MID(JT0_NOTES$,1,TMP_P1-1); JT0_NOTES$=MID(JT0_NOTES$,TMP_P1+1) ELSE NEXT_JT0_LINE$=JT0_NOTES$; JT0_NOTES$=""
8535 IF NOT(NUL(NEXT_JT0_LINE$)) THEN Y5$(10)=MID(NEXT_JT0_LINE$,1,110); NEXT_JT0_LINE$=MID(NEXT_JT0_LINE$,111); GOSUB 7300; GOTO *SAME
8540 GOTO NEXT_JT0_LINE
8545 DONE_PRINT_JOB_NOTES:
8546 Y5$(1)=""; GOSUB 7300
8548 RETURN 
8549 ! 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9913 IF W8=0 THEN GOTO 9920
9914 CALL "ZZPROM","Y SM2RBA",X3$,Z,"Clear all invoiced records?","","",0
9915 IF Z=0 THEN IF REMOVE$<>"" THEN GOSUB CLEAR_FILE END_IF ELSE IF Z=2 THEN GOSUB CLEAR_FILE; REM "Z=2 IS F9 hidden option to clear all whether invoiced or not
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF STP(Y4$)<>"" THEN IF %GUI THEN CALL Y4$,ERR=9931 ELSE RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
56000 ! Program changes starting 01/26/06
56010 REM "189841-Add CIG Plus parm to never create AR invoice
56011 REM "229462-When you print the CIG or CIG plus journal, when Adobe pops 
56012 REM "200047-Need to add CIG journal to Image Library.                   
56014 REM "232022-CIG Plus Journal, asks for clear, Yes/No/Clear All
56016 REM "235823-Create a new error for CIG Plus, GP% is >= 20%.  Print on
56018 REM "257952-Need larger tracking number field to replace 20 character   
56020 REM "257561-Output Customer Invoice Entry Message on CIG and CIGP Jrnls.
56022 REM "260698-Working on WO259427, found issue with Print Order Summary   
56024 REM "260047-Flowpoint vendor invoices; New Job Note Type, Purchase Order
56026 REM "266510-Flowpoint; add PO Code, Vendor, Product Code, Starting Num  
