0010 REM "Sales Tax Utility Functions <TW2FUN>
0035 REM "5.7 - 01/07/20 - 13.041111 - tma - SSP# 307249
0037 REM "307249-DBSPT-80353 Update Customer Tax Code Utility not working    
0040 REM "Copyright 2020 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0099 ! 
0100 INIT_LOG:
0110 IF %LOG_LEVEL=%LOG_DEBUG THEN {
0120 LOG_OPTIONS$="A"; CALL "ZZ2LOG;OPEN_LOG",ERR=*NEXT,X3$,X4$,FN%GET_BASENAME$(PGN)+"."+FID(0)+".log",LOG_OPTIONS$,LOG_CHANNEL
0130  }
0145 RETURN 
0149 ! 
0150 WRAPUP_LOG:
0170 IF LOG_CHANNEL THEN CLOSE (LOG_CHANNEL); LOG_CHANNEL=0
0195 RETURN 
0199 ! 
0200 LOG_MSG:
0220 IF %LOG_LEVEL=%LOG_DEBUG THEN {
0230 CALL "ZZ2LOG;LOG_MSG",ERR=*NEXT,X3$,X4$,LOG_CHANNEL,LOG_MSG$,%LOG_DEBUG
0240  }
0245 RETURN 
0249 ! 
1000 LOOKUP_TAX_CODE:
1010 ENTER X3$,X4$,IN_STATE$,IN_ZIP$,IN_CITY$,IN_COUNTY$,T$,T{ALL},ERR=*NEXT
1015 AR5_IOL:IOLIST T$(1),T{ALL}
1020 GOSUB INIT_LOG
1030 CALL "ZZCOMP","","","",X3$,X4$,"",-1,0,0; DIM Z[NUM(X3$(60,3))]
1040 Z$="01O AR5... "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
1050 ! 
1055 LOG_MSG$="BEGIN LOOKUP|"; GOSUB LOG_MSG
1060 ! Validate input
1070 LOG_MSG$="INPUT|CITY|"+IN_CITY$+"|STATE|"+IN_STATE$+"|ZIP|"+IN_ZIP$+"|"; GOSUB LOG_MSG
1080 IF NUL(IN_STATE$) THEN GOTO NOT_FOUND ! 248009
1090 STATE$=UCS(IN_STATE$),ZIP$=MID(STP(STP(IN_ZIP$,3,"-"),3),1,5),CITY$=UCS(IN_CITY$),COUNTY$=UCS(IN_COUNTY$) ! DBSPT-80353 SSP#307249
1100 ! 
1110 IF NUL(CITY$) THEN GOTO LOOKUP_BY_ZIP
1120 ! 
1130 LOOKUP_BY_CITY:
1140 LOC$=PAD(STATE$,2)+PAD(ZIP$,10)+PAD(CITY$,16)
1150 LOG_MSG$="LOOKUP|BY CITY|"+LOC$+"|"; GOSUB LOG_MSG
1160 READ (Z[1],KEY=LOC$,KNO=2,DOM=*NEXT)
1170 TKEY$=KEY(Z[1],END=DONE_BY_CITY)
1180 LOG_MSG$="LOOKUP|BY CITY|FOUND KEY|"+TKEY$+"|"; GOSUB LOG_MSG
1190 IF MID(TKEY$,1,28)=LOC$ THEN TAXCODE$=MID(TKEY$,36,10); GOTO FOUND_CODE
1195 LOG_MSG$="LOOKUP|BY CITY|NO MATCH|"; GOSUB LOG_MSG
1200 DONE_BY_CITY:
1210 ! If here, then could not find taxcode by city, continue to next lookup
1219 ! 
1220 LOOKUP_BY_ZIP:
1230 LOC$=PAD(STATE$,2)+PAD(ZIP$,10)
1240 LOG_MSG$="LOOKUP|BY ZIP|"+LOC$+"|"; GOSUB LOG_MSG
1250 READ (Z[1],KEY=LOC$,KNO=1,DOM=*NEXT)
1260 TKEY$=KEY(Z[1],END=DONE_BY_ZIP)
1270 LOG_MSG$="LOOKUP|BY ZIP|FOUND KEY|"+TKEY$+"|"; GOSUB LOG_MSG
1280 IF MID(TKEY$,1,12)=LOC$ THEN TAXCODE$=MID(TKEY$,20,10); GOTO FOUND_CODE
1285 LOG_MSG$="LOOKUP|BY ZIP|NO MATCH|"; GOSUB LOG_MSG
1290 DONE_BY_ZIP:
1300 ! If here, then could not find taxcode by zip, continue to next lookup
1309 ! 
1310 LOOKUP_BY_STATE:
1320 TAXCODE$=PAD(STATE$,10); GOTO FOUND_CODE
1330 LOG_MSG$="LOOKUP|BY STATE|USE KEY|"+TAXCODE$+"|"; GOSUB LOG_MSG
1335 GOTO FOUND_CODE
1340 ! 
1350 FOUND_CODE:
1360 LOG_MSG$="USING STXCODE|"+TAXCODE$+"|"; GOSUB LOG_MSG
1370 DIM T$(191),T[7]; READ (Z[1],KEY=TAXCODE$,KNO=0,DOM=NOT_FOUND)IOL=AR5_IOL
1380 LOG_MSG$="LOCATED RECORD|TAXCODE|"+T$(1,10)+"|DESC|"+T$(11,25)+"|"; FOR I=0 TO 7; LOG_MSG$+=STR(T[I])+"|"; NEXT I; GOSUB LOG_MSG
1390 GOTO WRAPUP
1400 ! 
1410 NOT_FOUND:
1415 TAXCODE_NOT_FOUND=1
1420 LOG_MSG$="TAXCODE NOT FOUND|"; GOSUB LOG_MSG
1430 GOTO WRAPUP
1440 ! 
1450 WRAPUP:
1460 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
1465 LOG_MSG$="END LOOKUP|"; GOSUB LOG_MSG
1470 GOSUB WRAPUP_LOG
1480 ! 
1485 IF TAXCODE_NOT_FOUND THEN EXIT 96 ! Error condition 96 - Invalid Return Value
1490 ! 
1495 EXIT 
1499 ! 
9999 END 
56000 ! 
56002 REM "245342-Sales tax code lookup - ability to factor City into lookup
56004 REM "248009-Problem in order entry with invalid state                   
56005 REM "307249-DBSPT-80353 Update Customer Tax Code Utility not working    
