0010 REM "Create zip code Crossreference file for ZIPSales Tax download <TW2ZIP>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 08/05/21 - 10.198479 - jvv - SSP# 307371
0037 REM "307371-Sales Tax Update Error 46 in TW2ZIP                         
0040 REM "Copyright 2021 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 IF %GUI THEN PROCESS "TWGZIP","../TWG/TW.EN"; EXIT 
0080 BEGIN 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0095 INIT:
0100 SETERR 9000
0110 X0$="TW2ZIP",X1$="Generate Zip Code Sort File"
0120 DIM Z0$(80,"-"),Z1$(20,"0"),C[15]
0130 K0=20,K1=1
0135 C9=-1
0140 M0$="-###.000",M1$="-#,###.00",M2$="-##.000" ! 233585
0200 REM "
0210 IF %GUI THEN X0=-1
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0250 PRECISION 9
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0297 IF %GUI THEN CALL "ZZ2PRP","TW",TAX_FILE$,""; IF TAX_FILE$<>"Y" THEN MSGBOX "ACCESS DENIED!!"+SEP+SEP+"The Sales Tax File Download System is not an installed Module.",MSG("WARNING"),"!,TIM=5"; CMD_STR$="E"; EXIT ! SSP 219370
0299 CALL "ZZ2PRP","TW",TAX_FILE$,""; IF TAX_FILE$<>"Y" THEN CALL "ZZPROM",".4",X3$,Z,"ACCESS DENIED!! The Sales Tax File Download System is not an installed Module.","","",0; GOTO 9900; REM " SSP # 149046                       
0300 REM "IOLISTS
0310 IOLIST A$
0320 IOLIST AR5$,AR5{ALL}
0325 IOLIST STATEONLY$,STATEONLY[0],STATEONLY[1],STATEONLY[2],STATEONLY[3],STATEONLY[4],STATEONLY[5],STATEONLY[6],STATEONLY[7]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O TW2...  03O ZY0...  04OSTW2...  05O AR5...  06O AX3...  07O AX4...  08O ZY1...  09O TW3...  10O TW4...  11O TW5...   13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 READ (Z[13],KEY=X3$(9,3)+"A/R")P0$
0535 IF %GUI THEN GOTO 0600
0536 Z[1]=HFN,FILE_NAME$="FLAT.txt"
0540 GOSUB 6000; INPUT EDIT (0,ERR=0540)@(10,6),"File Name : ",FILE_NAME$; IF CTL=4 THEN GOTO 9900
0541 OPEN (Z[1],OPT="TEXT",ERR=0540)FILE_NAME$
0600 REM "
0610 GOSUB 6000
0615 IF %GUI THEN RETURN 
0630 GOSUB 8100
0640 CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0650 PROCEED:
0652 IF %GUI THEN Z(1)=HFN
0655 IF %GUI AND NUL(FILE_NAME$) THEN FILE_NAME$="FLAT.txt"
0660 IF %GUI THEN OPEN LOCK (Z[1],OPT="TEXT",ERR=NO_FILE)FILE_NAME$; GOSUB 8100; CALL "$CTL-10;PB_CREATE",0,T,1,7.5; GOTO 0900
0800 CLOSE (Z[1]); OPEN LOCK (Z[1],OPT="TEXT")FILE_NAME$
0900 REM 
0920 H0$=$0A$
1000 REM "Read Data file
1001 A$=""
1100 READ (Z[1],END=5000)R$
1101 IF LEN(R$)<183 THEN GOTO 5000
1102 IF NOT(%GUI) THEN PRINT @(30,10),"Zip Code: ",R$(1,5) ELSE T1+=1; IF MOD(T1,1000)=0 THEN GOSUB 8150 ! SSP 219370
1105 R$=UCS(R$)
1110 REM "IF R$(2,2)="99" THEN GOSUB 8250; GOTO 5000
1111 TF_GEOCODE$=STR(0:"000")
1112 ZIP$=R$(1,5); REM zip
1113 CITY$=R$(6,28),CITY$=CITY$(1,25); REM city SSP 307371
1114 COUNTY$=R$(34,25); REM COUNTY
1115 STATE_ONLY=0; STATE$=R$(59,2); TW3$=""; FIND (Z[9],KEY=STATE$,DOM=*NEXT)TW3$; IF TW3$(33,1)<>"Y" AND ZIP$<>DIM(5) THEN STATE_ONLY=1 ! STATE
1116 CNTYDEF$=R$(61,1); REM COUNTY DEFAULT
1117 DEFAULT$=R$(62,1); REM GENERAL DEFAULT
1118 CNTYFIPS$=R$(63,5); REM SSP COUNTY FIPS CODE
1119 STATETAX=NUM(R$(68,8))*100; REM STATE SALES TAX
1120 CNTSTAX=NUM(R$(76,8))*100; REM COUNTY SALES TAX
1121 CNTLCLSTAX=NUM(R$(84,8))*100; REM COUNTY LOCAL SALES TAX
1122 CTYSTAX=NUM(R$(92,8))*100; REM CITY SALES TAX
1123 CTYLCLSTAX=NUM(R$(100,8))*100; REM CITY LOCAL SALES TAX
1124 COMBSTAX=NUM(R$(108,8))*100; REM COMBINED SALES TAX
1125 STATEUTAX=NUM(R$(116,8))*100; REM STATE USE TAX
1126 CNTUTAX=NUM(R$(124,8))*100; REM COUNTY USE TAX
1127 CNTCLUTAX=NUM(R$(132,8))*100; REM COUNTY LOCAL USE TAX
1128 CTYUTAX=NUM(R$(140,8))*100; REM CITY USE TAX
1129 CTYLCLUTAX=NUM(R$(148,8))*100; REM CITY LOCAL USE TAX
1130 COMBUTAX=NUM(R$(156,8))*100; REM COMBINED USE TAX
1131 EFFDATE$=R$(164,8); REM EFFECTIVE DATE
1132 GEOCODE$=R$(172,10); REM GEOCODE
1133 INOUT$=R$(182,1); REM INSIDE/OUTSIDE CITY FLAG
1140 TAX_PAYABLE_ACC$="",ACCRUED_TAX_ACC$=""
1142 IF TW3$="" THEN GOTO 1000
1145 GOSUB GET_TAX_PARAMS ! Get TW4, TW5 control records for State, County, City
1150 IF P0$(119,1)="Y" AND TW3$(35,12)<>DIM(12) THEN TAX_PAYABLE_ACC$=TW3$(35,12)
1160 IF P0$(119,1)="Y" AND TW3$(47,12)<>DIM(12) THEN ACCRUED_TAX_ACC$=TW3$(47,12)
1290 C1=C1+1
1291 GOSUB 4900
1299 GOSUB 2000
1300 IF MOD(C1,T0)=1 THEN GOSUB 8150; REM "PRINT @(0,14),'LD',@(15,20),A$(1,2)+"-"+A$(3,5)+"-"+A$(8,3)+"  "+A$(11,25),
1900 GOTO 1000
2000 REM "Initialize TopForm Record
2110 IF R$(1,1)=$1A$ THEN GOTO 5000
3000 REM "SETUP AR5 file
3001 DIM AR5$(400),AR5[15],STATEONLY$(400),STATEONLY[15]
3002 READ (Z[5],KEY=STATE$+ZIP$+TF_GEOCODE$,DOM=3003)IOL=0320
3005 DIM ZY0$(100); READ RECORD (Z[3],KEY=STATE$,DOM=*NEXT)ZY0$
3012 AR5$(1,7)=STATE$+ZIP$; REM first part of tax code
3013 AR5$(36,1)=TW3$(59,1),AR5$(154,3)=TW3$(59,1)+TW3$(59,1)+TW3$(59,1); REM TAX FREIGHT DEFAULTS SET TO N
3014 AR5$(11,25)=STATE$+"-"+STP(COUNTY$(1,10),1)+"-"+STP(CITY$(1,10),1); REM DESCRIPITON - NEED TO DOUBLE CHECK
3016 AR5$(37,6)=X3$(21,6); REM DEFAULT TO SYSTEM DATE
3018 AR5$(79,25)=CITY$(1,25); REM CITY NAME
3020 AR5$(157,25)=ZY0$(3,25); REM ROOM FOR SPELLING OUT STATE
3022 AR5$(119,2)=STATE$; REM STATE ABBREVIATION
3024 AR5$(121,25)=COUNTY$; REM COUNTY DESCRIPTION
3026 AR5$(151,3)=DIM(3," "); REM THREE CHARACTER COUNTY CODE - OURS IS FIVE
3027 IF TW3$(34,1)="Y" THEN AR5[3]=STATEUTAX,AR5[2]=CNTUTAX+CNTCLUTAX,AR5$(183,7)=STR(COMBUTAX:M2$),AR5[7]=COMBUTAX,AR5[0]=CTYUTAX+CTYLCLUTA; GOTO 3035 ! SSP 228121, SSP 233585
3028 AR5[2]=CNTLCLSTAX+CNTSTAX; REM COUNTY RATE - NEED TO SEPARATE THIS OUT
3030 AR5[3]=STATETAX; REM STATE RATE
3032 AR5$(183,7)=STR(COMBSTAX:M2$); AR5[7]=COMBSTAX; REM COMBINED RATES, SSP 233585
3034 AR5[0]=CTYSTAX+CTYLCLSTAX; REM CITY RATE
3035 AR5$(8,3)=TF_GEOCODE$
3036 AR5$(109,10)=ZIP$; REM ZIP CODE
3040 AR5$(55,12)=TAX_PAYABLE_ACC$,AR5$(67,12)=ACCRUED_TAX_ACC$,AR5$(190,1)=TW3$(34,1)
3045 ! Default AR5 fields based on TW4, TW5 settings - 249775
3050 AR5$(191,3)=TW3$(61,3) ! State prod code set
3052 AR5$(194,3)=TW4$(29,3),AR5$(197,1)=TW4$(28,1) ! County prod code set, Ignore State's PCS flag
3054 AR5$(198,3)=TW5$(54,3),AR5$(201,1)=TW5$(53,1) ! City/Other prod code set, Ignore County's PCS flag
3056 AR5$(202,1)=TW3$(60,1) ! Tax freight only if at least one time was taxed under this tax code
3100 GOSUB GET_MAX_TAX_AMT; IF NOT(STATE_ONLY) THEN WRITE (Z[5],KEY=AR5$(1,10))IOL=0320 ! SSP 225427
3110 STATEONLY$=AR5$
3112 STATEONLY[3]=AR5[3],STATEONLY[7]=AR5[3],STATEONLY$(183,7)=STR(STATEONLY[7]:M2$) ! SSP 233585
3114 STATEONLY$(3,8)=DIM(8," "),STATEONLY$(79,25)=DIM(25),STATEONLY$(109,10)=DIM(10)
3115 STATEONLY$(121,25)=DIM(25); STATEONLY$(151,3)=DIM(3)
3116 STATEONLY$(11,25)=AR5$(157,25),STATEONLY$(194,8)=DIM(8) ! Blank out pc settings for county and city  SSP 307180
3150 GOSUB GET_MAX_TAX_AMT_STATE; IF NOT(STATE_ONLY) THEN WRITE (Z[5],KEY=STATEONLY$(1,10))IOL=0325 ELSE WRITE (Z[5],KEY=STATEONLY$(1,10),DOM=*NEXT)IOL=0325 ! SSP 225427
3195 IF STATE_ONLY THEN GOTO 4500
3200 WRITE (Z[6],KEY=ZIP$(1,5)+DIM(5," ")+AR5$(1,10)); REM "SORT FILE   AX3
3240 WRITE (Z[7],KEY=AR5$(119,2)+AR5$(121,25)+AR5$(79,25)+AR5$(1,10)); REM "SORT FILE  AX4
4520 DIM ZY1$(255)
4530 ZY1$(1,9)=AR5$(3,5)+"    "
4540 ZY1$(10,16)=AR5$(79,16)
4550 ZY1$(26,2)=AR5$(119,2)
4560 ZY1$(28,12)=AR5$(121,12)
4570 ZY1$(40,3)="   "
4580 ZY1$(43,28)=AR5$(79,25)+"   "
4582 ! ZY1$(71,10)=AR5$(1,10) ! 231068
4585 WRITE RECORD (Z[8],KEY=ZY1$(1,9))ZY1$
4800 RETURN 
4900 REM "Write file(s)
4901 READ (Z[4],KEY=STATE$+ZIP$+TF_GEOCODE$,ERR=4930)TW2$
4902 IF TW2$(11,10)+TW2$(21,25)=GEOCODE$+CITY$(1,25) THEN GOTO 4999
4903 TF_GEOCODE$=STR(NUM(TF_GEOCODE$)+10:"000")
4904 GOTO 4901
4930 WRITE (Z[2],KEY=STATE$+ZIP$+TF_GEOCODE$)STATE$+ZIP$+TF_GEOCODE$+GEOCODE$+CITY$(1,25); C2=C2+1
4999 RETURN 
5000 REM "EOJ
5010 REM "IF T9$<>"Y" THEN X$=STR(C1)+" records done. Did not find a valid trailer record, data is incomplete!!!"; CALL "ZZPROM",".4",X3$,Z,X$,"","",0; GOTO 5021
5015 X$="Process complete - "+STR(C1)+" of "+STR(T)+" records loaded."
5018 IF %GUI THEN MSGBOX X$,"F.Y.I.","!,TIM=5"; CMD_STR$="E"; EXIT ! SSP 219370
5020 CALL "ZZPROM",".4",X3$,Z,X$,"","",0
5040 GOTO 9900
5200 REM "COMPLETE CYCLE
5290 RETURN 
6000 REM "BACKGROUND
6001 IF %GUI THEN RETURN 
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This utility downloads sales tax information from a text file",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8107 T=0
8110 REM "CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F$
8112 REM "T=INT(T0*B/200.0046)
8113 CALL "ZZ2FNC;SERIALRECCNT",Z[1],T
8114 REM 'SUBTRACT 2 , 1 FOR HEADER & 1 FOR TRAILER
8116 IF NOT(%GUI) THEN PRINT @(0,9),"There are "+STR(T)+" records to process"
8129 REM "Set T0, we make sure T0 is > 1, because later on we MOD and look for avalue of 1. IF T0 is 1, then nothing would get reported. We look fora result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T0'th record to get the first report
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8135 T1=0
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 IF NOT(%GUI) THEN CALL "ZZBARG",X3$,"HG",12,10,50,T1,T,C1 ELSE CALL "$CTL-10;PB_VALUE",T1
8195 RETURN 
8250 REM "Check out trailer record, set T9$ to "Y" if it is ok
8260 IF POS("9"<>R$(1,10))<>0 OR POS("VERTEX 'SALESTAX' TAX RATE FILE"=R$)=0 THEN PRINT @(0,15),R$,; CALL "ZZPROM",".4",X3$,0,"This file does not have the proper Vertex Trailer Information!!!","","",0; GOTO 8295
8280 T9$="Y"
8295 RETURN 
8920 DEF FNS$(Z9$)=Z9$(1,POS(S$=Z9$+S$)-1)
8930 DEF FNQ$(Z9$,Z9)=STR(NUM(Z9$):Z1$(1,Z9))
8940 DEF FNE$(Z9)=CHR(ASC("A")+INT(Z9/10))+STR(MOD(Z9,10))
8950 DEF FNS(Z9$)=POS(" "<>Z9$)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9905 END_PROG:
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "AR2UXL"
9999 END 
10000 NO_FILE:
10010 MSGBOX "File : "+FILE_NAME$+" not found",MSG("WARNING"),"!,TIM=10"
10020 NEXT_ID=FILE_NAME.CTL
10030 RETURN 
11000 GET_MAX_TAX_AMT:! SSP 225427
11020 RE=0; IF AR5[3]<>0 THEN CALL "TWGMAA;GET_MAX_TAX_AMT",AR5$(1,2),Z[9],0,AR5[3],RE; END_IF ; AR5[4]=RE
11030 RE=0; IF AR5[2]<>0 THEN CALL "TWGMAA;GET_MAX_TAX_AMT",AR5$(1,2),Z[9],1,AR5[2],RE; END_IF ; AR5[5]=RE
11040 RE=0; IF AR5[0]<>0 THEN CALL "TWGMAA;GET_MAX_TAX_AMT",AR5$(1,2),Z[9],2,AR5[0],RE; END_IF ; AR5[6]=RE
11050 RETURN 
11100 GET_MAX_TAX_AMT_STATE:
11110 RE=0; IF STATEONLY[3]<>0 THEN CALL "TWGMAA;GET_MAX_TAX_AMT",STATEONLY$(1,2),Z[9],0,STATEONLY[3],RE; END_IF ; STATEONLY[4]=RE
11120 RE=0; IF STATEONLY[2]<>0 THEN CALL "TWGMAA;GET_MAX_TAX_AMT",STATEONLY$(1,2),Z[9],1,STATEONLY[2],RE; END_IF ; STATEONLY[5]=RE
11130 RE=0; IF STATEONLY[0]<>0 THEN CALL "TWGMAA;GET_MAX_TAX_AMT",STATEONLY$(1,2),Z[9],2,STATEONLY[0],RE; END_IF ; STATEONLY[6]=RE
11140 RETURN 
13000 GET_TAX_PARAMS:
13005 DIM TW4$(132),TW5$(157)
13010 GET_COUNTY_PARM:! SSP 307180
13018 ALLKEY$="*"+DIM(24)
13020 FIND (Z[10],KEY=STATE$+ALLKEY$,DOM=*NEXT)TW4$ ! SSP 307180
13030 FIND (Z[10],KEY=STATE$+COUNTY$,DOM=*NEXT)TW4$
13050 GET_CITY_PARM:! SSP 307180
13060 FIND (Z[11],KEY=STATE$+COUNTY$+ALLKEY$,DOM=*NEXT)TW5$ ! SSP 307180
13070 FIND (Z[11],KEY=STATE$+COUNTY$+CITY$,DOM=*NEXT)TW5$
13095 RETURN 
13099 ! 
56000 REM "216400-Modify Vertax download to use DSI provided file.        
56002 REM "307371-Sales Tax Update Error 46 in TW2ZIP                         
56003 ! "225427-Florida Sales Tax - each county can set a sales tax percent,
56004 ! "228121-Modify TW2ZIP to support Use Tax flag in TW3. Does not work.
56005 REM "230142-Issue with Sales Tax import: Tax rate for State MO not      
56006 REM "231140-Sales tax data import utility not setting up state level tax
56007 REM "233585-Sales tax issue with override ship to-Order was invoiced at 
56008 REM "249775-Changes to sales tax parameters and tax calculations to     
56009 REM "307180-DBD-45; Modify Sales Tax Parameters to allow for option to  
