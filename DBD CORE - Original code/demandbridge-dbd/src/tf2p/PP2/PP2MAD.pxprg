0010 REM "New EC Promo Product Setup <PP2MAD>
0020 SETESC 9300; SETERR 9000
0035 REM "5.5 - 10/10/06 - 15.681388 - nnm - SSP# 196998
0037 REM "196998-Create a new  Promo Group maintenance program.  Validate    
0040 REM "Copyright 2006 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="PP2MAD",X1$="New EC Promo Product Setup",K9$="",K9=0
0120 DIM Z0$(80,"-")
0130 K0=10,K1=1
0135 C9=-1,V9=-2
0150 IF Q1$="" AND %PP2MAD_CUST$<>"" THEN Q1$=%PP2MAD_CUST$ ! SSP185859, set customer to last one, user is most likely entering many for a particular customer.
0200 REM "
0230 IF Q2$<>"" THEN X2=-1; PRINT @(0,5),'CE',
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1,LEN(A$))
0320 IOLIST B$,B[0],B[1],B[2]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20],C[21],C[22],C[23],C[24],C[25],C[26],C[27],C[28],C[29],C[30],C[31]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O AR1...  02O FMP...  03O FM1...  04O IC0...  13O ZZPARM  06O ED0... 07O PP0...  080 PP1...  09O PP2...  10O PP3...  11O ECR...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0550 DIM SEC$(255); READ (Z[13],KEY="osec"+X3$(40,3),DOM=0551)SEC$
0600 REM "
0620 GOSUB 6000
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(300),B[2],PP0$(190),PP1$(190),PP2$(190),ED0$(500)
1035 DIM A$(20)
1040 IF V9<>-1 THEN GOSUB 6900
1045 A$(1,0+K9)=K9$+A1$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1500
1190 ! 
1200 REM "Customer Code
1203 X$="May be blank for stock item."
1205 IF Q1$>"" THEN A$(1+K9,10)=Q1$; GOSUB 6450; GOTO 1220
1210 CALL "ZZENTR","AXUX",A{ALL},A$,X4$,X3$,18,9+V0,1+K9,10,C0,"A/R","{1"+X$,"","PP2MAD00","AR1","AR2MAB",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1215 IF A$(1+K9,10)=DIM(10) THEN STOCK$="Y"; GOTO 1240 ELSE STOCK$="N"
1220 FIND (Z[1],KEY=A$(1+K9,10),DOM=1210)IOL=0320
1225 PRINT @(44,9),B$(11,35),
1230 FM1_ITEM$="N"; IC0_ITEM$="N"; READ (Z[2],KEY="D"+A$(1,10),DOM=*NEXT)FMP$; IF FMP$(24,1)="Y" THEN FM1_ITEM$="Y"; GOTO 1237
1235 IC0_ITEM$="Y"
1237 CALL "FS2DAK",X3$,X4$,0,A$(1,10),CIC$,"",""
1240 GOTO 1120
1245 ! 
1500 REM "
1510 A1$=K9$+A$(LEN(K9$)+1,20),A$(1,LEN(K9$))=K9$
1700 REM "
1790 GOSUB 6200
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF C0>3 OR X0<0 THEN GOTO 1800
2035 IF X0*6+11<LEN(V9$) THEN IF V9$(X0*6+11,1)="2" THEN GOTO 2020
2040 ON X0 GOTO 2100,2150,2200,2250,6700
2045 ! 
2100 REM "New Item Portion, first four characters
2110 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,10,13,11+K9,4,C0,"","{3"+X$,"","PP2MAD02","EPG","PP2MA0",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2110,2111
2115 IF C0=4 THEN GOTO 9900
2120 IF A$(11+K9,4)=DIM(4) THEN GOTO 2110
2130 READ (Z[6],KEY=A$(11+K9,4),DOM=2110)ED0$
2131 PRINT @(16,13),("("+STP(ED0$(5,60))+")")
2140 GOTO 2020
2145 ! 
2150 REM "Size Portion, two characters
2160 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,21,15,15+K9,2,C0,"","{3"+X$,"","PP2MAD04","PP0","PP2MAA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2160,2161
2162 IF C0=4 THEN GOTO 9900
2165 IF A$(15+K9,2)=DIM(2) THEN GOTO 2190
2170 READ (Z[7],KEY=A$(15+K9,2),DOM=2160)PP0$
2180 PRINT @(15,17),PP0$(3,20),
2190 GOTO 2020
2195 ! 
2200 REM "Color Portion, two characters
2210 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,42,15,17+K9,2,C0,"","{3"+X$,"","PP2MAD06","PP1","PP2MAB",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2210,2211
2212 IF C0=4 THEN GOTO 9900
2215 IF A$(17+K9,2)=DIM(2) THEN GOTO 2240
2220 READ (Z[8],KEY=A$(17+K9,2),DOM=2210)PP1$
2230 PRINT @(35,17),PP1$(3,20),
2240 GOTO 2020
2245 ! 
2250 REM "Other Portion, two characters
2260 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,62,15,19+K9,2,C0,"","{3"+X$,"","PP2MAD08","PP2","PP2MAC",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2260,2261
2262 IF C0=4 THEN GOTO 9900
2265 IF A$(19+K9,2)=DIM(2) THEN GOTO 2290
2270 READ (Z[9],KEY=A$(19+K9,2),DOM=2260)PP2$
2280 PRINT @(55,17),PP2$(3,20),
2290 GOTO 2020
2295 ! 
5200 REM "Transfer from another customer?" Code from FM2OB5
5204 DIM N$(20),N0$(100),C$(448),C[31]
5205 GOSUB 5390
5206 REM "Customer Code
5207 C7=1,N0=1
5210 CALL "ZZENTR","AXU",A{ALL},N$,X4$,X3$,30,12,1,10,C0,"A/R","{3"+X$,"","PP2MAD10","AR1","",""; IF ABS(C0)>4 THEN GOSUB 6400; GOSUB 5390; ON C0 GOTO 5210,5211
5215 IF POS(" "<>N$(1,10))=0 THEN EXITTO 6750
5220 FIND (Z[1],KEY=N$(1,10),DOM=5210)N0$
5225 PRINT @(42,12),N0$(11,35),
5250 REM "Item code
5255 C0$=N$(1,10),C7=2,N0=1
5257 IF POS(" "<>N$(11,10))=0 THEN N$(11,10)=A$(11,10)
5260 CALL "ZZENTR","SUX",A{ALL},N$,X4$,X3$,30,13,11,10,C0,"","{3"+X$,C0$,"PP2MAD12","FM1","",""; IF ABS(C0)>4 THEN GOSUB 6400; GOSUB 5390; ON C0 GOTO 5260,5261
5265 IF C0=-2 THEN GOTO 5206 ELSE IF POS(" "<>N$(11,10))=0 THEN EXITTO 6750
5270 FIND (Z[3],KEY=N$,DOM=5260)IOL=0330
5275 PRINT @(42,13),C$(42,40),
5295 ! 
5300 REM "Convert to new & write out
5305 Q$=C$
5310 C$(1,20)=A$(1,10)+A$(11,10),C$(109)="",C[6]=0,C[7]=0,C[8]=0,C[9]=0,C[10]=0,C[11]=0,C[12]=0,C[13]=0,C[14]=0,C[15]=0,C[16]=0,C[17]=0,C$(254,1)="0",C$(255,6)=X3$(15,6)
5315 C$(238,16)=Q$(238,16),C$(212,3)=Q$(212,3),C[24]=0
5320 WRITE (Z[3],KEY=C$(1,20))IOL=0330
5330 GOSUB 8300
5340 READ RECORD (Z[11],KEY=Q$(1,20),DOM=*NEXT)ECR$; ECR$(1,20)=A$(1,20),ECR$(31,150)=DIM(150); WRITE RECORD (Z[11],KEY=ECR$(1,20),DOM=*NEXT)ECR$ ! SSP184659, also copy ECR record, blank out image file names
5350 GOTO 6750
5380 RETURN 
5385 ! 
5390 PRINT @(0,9),'CE',@(0,12),"Copy this item from customer:",@(24,13),"item:",
5391 CALL "ZZDISP","A",N$(1,10),"A/R",X3$,"","",30,12,X4$
5392 PRINT @(42,12),N0$(11,35),@(24,13),"item:",@(30,13),N$(11,10),@(42,13),C$(42,40),
5395 RETURN 
5399 ! 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(3,9+V0),"Customer Code:",@(0,11),Z0$,
6065 PRINT @(0,3+V0),"Enter New Promotional Products here.  These products may be customer specific   or stock items.  You may not use this entry program for existing items.  When   entry is complete, if this is not an existing item yet, you will be positioned  at the proper area to complete the entry.",@(0,7+V0),Z0$,
6070 PRINT @(3,13),"Group:",@(15,15),"Size:",@(35,15),"Color:",@(55,15),"Other:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6195 ! 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 PRINT @(10,13),A$(11+K9,4),@(21,15),A$(15+K9,2),@(42,15),A$(17+K9,2),@(62,15),A$(19+K9,2),
6230 PRINT @(15,17),PP0$(3,20),@(35,17),PP1$(3,20),@(55,17),PP2$(3,20),
6390 RETURN 
6395 ! 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6449 ! 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 CALL "ZZDISP","A",A$(1+K9,10),"A/R",X3$,"","",18,9+V0,X4$
6464 PRINT @(44,9),B$(11,35),
6490 RETURN 
6495 ! 
6700 REM "Make sure item does not exist, if it does display message asking to try again.  If ok, then we need to advance to proper entry place.
6710 IF STOCK$="Y" OR IC0_ITEM$="Y" THEN FILE$="IC0",FILE_CHAN=Z[4] ELSE FILE$="FM1",FILE_CHAN=Z[3]
6720 READ (FILE_CHAN,KEY=A$(1+K9,20),DOM=*NEXT)ITEM$; CALL "ZZPROM",".4",X3$,Z,"Item already exists, please try again.","","",0; DIM A$(20),B$(300),PP0$(190),PP1$(190),PP2$(190); A1$=""; GOTO 1000
6730 ! Will now advance to either item setup or custom item setup
6732 DIM PP3$(125); PP3$(1,20)=A$(1,20),PP3$(21,10)=A$(11,10); WRITE (Z[10],KEY=PP3$(1,20))PP3$ ! WO171176
6735 %PROMO$="Y",%PP2MAD_CUST$=A1$; L0=0,U1$=""; DIM U0$(6); U0$(1)=FID(0),U0$=U0$+"PF"
6740 IF FILE$="IC0" THEN GOTO 6800
6745 ! Go to custom item setup
6748 IF CIC$="Y" THEN USED$="",CIC_ITEM$="",TF_ITEM$=""; CALL "FS2DAK",X3$,X4$,2,A$(1,10)+A$(11,10),USED$,CIC_ITEM$,TF_ITEM$; IF USED$="Y" THEN CALL "ZZPROM",".4",X3$,0,STP(TF_ITEM$,1)+" is using "+STP(CIC_ITEM$,1)+" as it's CIC, you may not add this item code.","","",0; DIM A$(20),B$(300),PP0$(190),PP1$(190),PP2$(190); A1$=""; GOTO 1000
6749 GOSUB 5200 ! Ask to copy from another item
6750 U1$=X0$+STR(LEN(A1$):"00")+A1$+U1$
6755 U1$="FM2IAZ20"+A$(1,20)+U1$ ! SSP172998
6760 U1$="FM2IAC20"+A$(1,20)+U1$
6770 U1$="FM2IAA20"+A$(1,20)+U1$
6780 GOTO 9700
6800 ! Go to inventory item setup
6810 CALL "IC2MAB",X3$,X4$,"",A$(1,20)
6820 U1$=X0$+STR(LEN(A1$):"00")+A1$+U1$
6830 U1$="IC2MAZ20"+A$(1,20)+U1$
6840 GOTO 9700
6895 ! 
6900 REM "GET DEFAULTS
6902 V9$="1803  1008  1013  "
6910 IF V9>=0 THEN READ (Z[13],IND=V9)V9$,IOL=0310; GOTO 6930 ELSE V9=-1
6920 EXTRACT (Z[13],KEY=X0$+"DFLT",DOM=6921)V9$,IOL=0310; V9=IND(Z[13]); READ (Z[13])
6930 RETURN 
6950 REM "WRITE DEFAULT
6955 WRITE (Z[13],KEY=X0$+"DFLT")V9$,IOL=0310
6960 GOSUB 6920; GOTO 1630
6970 REM "ENTRY DEFAULTS
6975 PRINT 'CF'; V9=-2,Q$=""
6980 FOR X=1 TO LEN(V9$) STEP 6
6982 PRINT @(NUM(V9$(X,2)),NUM(V9$(X+2,2))),; IF V9=-2 THEN PRINT V9$(X+4,1), ELSE READ RECORD (0,SIZ=1)Q$; IF POS(Q$="1234Z")=0 THEN PRINT 'RB'; GOTO 6982
6990 IF Q$="Z" THEN EXITTO 6950 ELSE V9$(X+4,LEN(Q$))=Q$; NEXT X
6995 V9=0; GOTO 6980
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
8300 REM "Copy records for item copy, keep Z(1) open to write out stuff
8310 Z$="02C FMP...  03C FM1...  04C IC0...  02O FMS...  03O FMU...  04O FMV...  05O FM2...  06O FSF...  "
8315 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 8316,9900
8319 REM "Write out to FM2 file
8320 WRITE (Z[5],KEY=C$(21,3)+C$(24,16)+C$(40,2)+C$(11,10)+C$(1,10))
8325 FOR I=2 TO 4
8330 GOSUB 8350
8335 NEXT I
8337 I=6; GOSUB 8350
8340 RETURN 
8345 ! 
8350 REM "Transfer records with key N$ in file Z(I) to key A$(1,10)+A$(11,10)
8355 EXTRACT (Z[I],KEY=N$,DOM=8356)
8357 K$=KEY(Z[I],END=8380)
8358 IF MID(K$,1,LEN(N$))<>N$ THEN GOTO 8380
8360 READ RECORD (Z[I],END=8380)A8$
8365 A8$(1,20)=A$(1,10)+A$(11,10)
8370 WRITE RECORD (Z[I],KEY=A8$(1,LEN(K$)))A8$
8373 READ (Z[I],KEY=K$,END=8380)
8375 GOTO 8357
8390 RETURN 
8395 ! 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9725 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9730 RUN U1$(1,6)
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9905 %PROMO$="N",%PP2MAD_CUST$=""
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56002 REM "196998-Create a new  Promo Group maintenance program.  Validate    
