0010 REM "Kit Component Listing <KT2RAA>
0035 REM "5.7 - 11/13/17 - 13.964166 - tma - SSP# 291708
0037 REM "291708-In the KIT Component setup for special charge, why does     
0040 REM "Copyright 2017 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 SETERR 9000; SETESC 9300
0110 X0$="KT2RAA",X1$="Kit Component Listing"
0120 DIM A[4]
0140 Z0$="###,##0",Z1$="###,##0.00-",Z3$="##,###,##0",Z2$="###,###,###.00-"
0210 T=1,T0=0,T1=2
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4]
0320 IOLIST B$
0330 IOLIST C$,C[0],C[1],C[2]
0400 IOLIST R$,R[0],R[1]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,IOL=0425
0425 IOLIST V$,V[0],V[1]
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O KT0...  02O IC0...  03O FMP...  " ! SSP 208675
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0550 FIND (Z[13],KEY=X3$(9,3)+"I/C",DOM=0551)P0$
0720 REM 
0800 REM "Alternate Sort Info & total dim"
0810 DIM U[10,2],U0$(10),Y[5,1]
0820 DIM U[2,1],U5$(60); U=20,U0=1,U[0,0]=1,U[0,1]=20,U[1,0]=1,U[1,1]=20,U[2,0]=21,U[2,1]=4,T0=2,V1=20,U0$="Y   ",T5$="AI/CS           ",U5$(1,20)="Item number",U6$=""
0825 U1=Z[1]
0850 DIM T[T0,T1]
0890 REM "Open Printer
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0950 REM " Position read"
0970 IF V2$="" THEN GOTO 0990 ELSE DIM V4$(U); U9=1; FOR X=1 TO U0; V4$(U[X,0],U[X,1])=V2$(U9,U[X,1]); U9=U9+2*U[X,1]; NEXT X
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record and check range
1005 U$=KEY(U1,END=5000); IF V2$="" THEN GOTO 1100
1010 FOR X=1 TO U0; U2=U[X,0],U3=U[X,0]*2-1,U4=U[X,1]
1020 IF U$(U2,U4)<V2$(U3,U4) THEN READ (U1); EXITTO 1000
1030 IF U$(U2,U4)>V2$(U3+U4,U4) THEN IF U=1 THEN EXITTO 5000 ELSE READ (U1); EXITTO 1000
1040 NEXT X
1100 REM "Get record"
1110 U9$=U$
1130 READ (U1)
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1200 REM "Reporting criteria
1490 T=0,U2$=U9$
1500 REM "Print record
1502 IF W+3>W0 THEN GOSUB 6000
1600 IF L$<>A$(1,20) THEN A1$=""; GOSUB 6300; L$=A$(1,20)
2000 REM "Print Kit Components
2030 A1$=A$
2100 A$(1,20)=A1$(21,20)
2110 IF A$(50,1)="S" THEN GOSUB SPECIAL_CHARGE; GOTO 2140
2120 GOSUB 6300
2140 Y5$(6)=A$(45,4),Y5$(13)=B$(61,3),Y5$(20)=J$,Y5$(90)=STR(A[1]:Z3$)
2145 Y5$(101)=FNS$(A$(41,4))+"/"+FNR$(STR(A[0]:Z3$))+" = ",Y5$(115)=STR(A[2]:Z3$)
2160 GOSUB 7300
2170 DIM Y5$(W3); Y5$(40)="P/O : "+A$(49,1),Y5$(48)="Type : "+A$(50,1),Y5$(58)="CSR : "+A$(51,4) ! SSP
2175 IF A(3)<>0 OR A(4)<>0 THEN Y5$(70)=" Cost : "+STR(A(3):"###,###.00")+" Ext.: "+STR(A(4):"###,###.00") ! ssp#291708
2180 GOSUB 7300
2290 A$=A1$
2300 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 IF T=1 THEN GOSUB 3500; GOTO 3090
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
5000 REM "End of Print
5290 Y4$=""
5295 GOTO 9900
6000 REM "Page header
6035 IF T0$="END" THEN W0$=""
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6060 ELSE ON W5 GOTO 6041,9910 ! SSP239457, changed W9$ to W3$ for desc from selection screen to output on the report
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6120 Y5$(6)="Whse",Y5$(13)="P/C",Y5$(20)="Component Item No. / Description",Y5$(90)="Quantity Required of this Component"
6195 IF FID(W9)=FID(0) THEN PRINT 'SF',
6199 RETURN 
6300 REM "Break header #2
6305 DIM B$(380)
6310 IF W+3>W0 THEN GOSUB 6000
6315 IF A1$="" THEN GOSUB 7300; GOSUB 7300
6320 CALL "ZZDISP","AX",A$(1,10),"A/R",X3$,X$,"",0,0,X4$; J$=X$
6325 CALL "ZZDISP","AX",A$(11,10),"ICE",X3$,X$,"",0,0,X4$
6330 IF POS(" "<>J$)=0 THEN J$=FNS$(X$) ELSE J$=FNS$(J$)+" - "+FNS$(X$)
6335 FIND (Z[2],KEY=A$(1,20),DOM=6340)IOL=0320
6336 GOSUB 8000
6338 J$=J$+"  "+H9$
6340 IF A1$="" THEN Y5$(1)=J$; GOSUB 7300
6345 RETURN 
7000 REM "Totals logic
7001 IF T>0 THEN IF T[T,0]<2 THEN IF T=2 THEN GOTO 7100 ELSE GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(56)=T1$(1,14),Y5$(118)=T1$(1,14); GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" "+Q2$+" ("+STR(T[T,0]:"0")+"):"
7020 Y5$(56)=STR(T[T,1]:Z0$),Y5$(118)=STR(T[T,2]:Z0$); GOSUB 7300
7065 IF T$="END" THEN Y5$(56)=T2$(1,14),Y5$(118)=T2$(1,14); GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7330 REM 
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     13O ZZPARM  "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7435 DIM V[1]
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V$(71,1)),W3$=V$(19,POS("   "=V$(19,40)+"   ")-1) ! SSP239457
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
7600 REM "Handle V2$<U*2 length
7610 DIM V2$(U*2),V4$(U,"~"); U3=1
7615 FOR U9=1 TO U0; U3=U3+U[U9-1,1]+U[U9-1,1]
7620 V2$(U3+U[U9,1],U[U9,1])=V4$(1,U[U9,1])
7630 NEXT U9
7640 RETURN 
8000 REM "Build desc from B$
8010 H9$=FNS$(B$(64,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>B$(72,8))<>0 THEN H9$=H9$+"x"
8020 H9$=H9$+FNS$(B$(72,8)); IF H9$<>"" THEN IF POS(" "<>B$(72,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
8030 IF NUM(B$(80,2))<>0 THEN H9$=H9$+B$(80,2)+" Ply "
8040 H9$=H9$+FNS$(B$(21,40))
8050 RETURN 
8905 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8940 DEF FNS$(Z$)=Z$(1,POS("   "=Z$+"   ")-1)
8945 DEF FNR$(Z$)=Z$(POS(" "<>Z$))
8950 DEF FNQ$(Z9,Z9$)=STR(Z9:Z9$(1,POS(".00"=STR(Z9:Z9$)+".00")-1))
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 IF Y5=68 THEN RETRY ELSE ON C9 GOTO 1140,2040
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 IF %GUI THEN EXIT 
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 RUN "ZMENU"
9999 END 
10000 SPECIAL_CHARGE:
10005 DIM B$(380)
10020 FIND (Z[3],KEY="X"+A$(31,10),DOM=NO_CHARGE)_FMP$
10030 B$(61,3)=_FMP$(52,3),J$=FNS$(A$(11,10))+"  "+_FMP$(12,40)
10050 RETURN 
10100 NO_CHARGE:RETURN 
56000 ! "208675-Kit Module - Allow processing of items not in inventory     
56002 REM "239457-PE(047, KT2RAA-7480) when in KIT report.
56003 REM "291708-In the KIT Component setup for special charge, why does     
