0010 REM "<CT2UBA> Import Contact Management Gateway
0020 SETESC 9300; SETERR 9000
0035 REM "5.5 - 03/06/07 - 9.693333 - mhe - SSP# 205703
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0095 REM "Called from FO2RAS Q0$="", Q1$="*"
0096 REM "Called from FO2GST Q1$="G"
0097 REM "Adding changes for FO1 version 1.01 10/29/96
0100 SETERR 9000
0110 X0$="CT2UBA",X1$="Import Contact Management Gateway"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0140 L0=299; REM "Record should be 299 long, for version 1.00, will adjust at 1140 if another version
0145 M0$="000"
0150 COUNT=0; REM "Req count to send back to ghost task
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$,A[0]; REM "ZP4
0320 IOLIST B$,B[0]; REM "ZP0
0330 IOLIST C$; REM "ZPA-"S"
0340 IOLIST D$; REM "ZPD-"H"
0350 IOLIST E$; REM "ZPE
0360 IOLIST G$,G[0],G[1],G[2],G[3],G[4],G[5],G[6],G[7],G[8],G[9],G[10],G[11],G[12],G[13],G[14],G[15]; REM "AR1
0370 IOLIST H$,H[0],H[1],H[2],H[3]; REM "AR5
0380 REM "IOLIST L$; REM "FO5
0390 REM "IOLIST N$,N[0],N[1],N[2],N[3],N[4],N[5],N[6],N[7],N[8],N[9],N[10],N[11],N[13],N[14],N[15]; REM "FM4
0500 REM "FILES
0503 ERASE "ZPG"+X3$(9,3),ERR=0504
0504 SERIAL "ZPG"+X3$(9,3),0,0
0505 DIM Z[NUM(X3$(60,3))]
0509 REM "Slot 1 used for import file opened in 1000's
0510 Z$="02O ZP4... 03O ZP0... 04O ZPA... 05O ZPD... 06O ZPE... 07O AR1... 08O AR5... 09O ZPG... 13O ZZPARM "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 FID_9$=FN%FID$(Z[9]); CLOSE (Z[9]); OPEN LOCK (Z[9],OPT="TEXT")FID_9$(4,6) ! [205703]-changed FID() to FN%FID$()
0600 REM "
0610 IF Q1$<>"G" THEN GOSUB 6000
0640 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
1000 REM "Get list of CT1 files to process
1010 CALL "ZZ2BLS","D0:CT1*","S",F$
1012 IF X3$(77,1)="U" THEN CALL "ZZ2BLS","D0:ct1*","S",F_LOWER$; F$=F$+F_LOWER$
1015 P=POS(":"=F$); IF P=0 THEN GOTO 5000
1020 F1$=F$(P+1); P1=POS(":"=F1$); IF P1>0 THEN F$=F1$(P1-2),F1$=F1$(1,P1-3) ELSE F$=""
1050 Z$="01CU 01O "+F1$+" "
1055 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 1056,1995
1060 CLOSE (Z[1]); OPEN LOCK (Z[1],OPT="TEXT")F1$; REM "Open for reading raw file
1065 CLOSE (13); OPEN (13)"ZZPARM"; REM "Re-open ZZPARM closed in ZZ2BLS
1075 IF Q1$<>"G" THEN PRINT @(15,15),'CE',"Processing: "+F1$,
1080 IF Q1$<>"G" THEN GOSUB 8100; GOSUB 8150
1100 REM "Process the file
1105 I9=0,I0$="",I1$="",F8=0,I$="",RCOUNT$=""; DIM RCOUNT[50]
1110 READ (Z[1],END=7750)I$; I9=I9+1,COUNT=COUNT+1,L2=0,S1$="0",F3=0; REM "S1$ is header status code, F3 is 1 if cust parm has alt. inv. cust code
1115 P=POS(I$(1,3)=RCOUNT$,3); IF P=0 THEN RCOUNT$=RCOUNT$+I$(1,3); GOTO 1115; REM "Used to write to "+hwd+dlm+"GW_LOG
1120 P1=(P+2)/3; RCOUNT[P1]=RCOUNT[P1]+1
1125 IF Q1$<>"G" THEN IF MOD(I9,T0)=1 THEN GOSUB 8150
1130 REM PRINT @(0,3),'CE',@(0,4),I$; INPUT *
1135 DIM AT$(494),A$(494),A[1],B$(122),B[2]; F8=0
1140 REM "IF I$(4,3)="101" THEN LET L0=310; REM "HEADER OF VERSION 1.01 FILE IS 310 LONG
1145 IF LEN(I$)<L0 THEN I9$=I$; DIM I$(L0); I$(1)=I9$; REM "Correct length if short
1150 AT$(1,4)=I$(7,4); GOSUB 7800; REM "LIST TYPE
1155 AT$(5,6)=I$(11,6); REM "CONTACT CODE
1160 AT$(11,40)=STP(I$(17,30),1); REM "CONTACT NAME
1180 AT$(51,30)=I$(47,30); REM "ADDRESS LINE 1
1190 AT$(81,30)=I$(77,30); REM "ADDRESS LINE 2
1195 AT$(391,28)=I$(107,16); REM "CITY
1200 AT$(127,2)=I$(123,2); REM "STATE
1205 AT$(129,9)=STP(I$(126,9),0); REM "ZIP CODE
1210 AT$(138,12)=STP(I$(148,12),0); REM "FAX NUMBER
1215 AT$(200,14)=STP(STP(I$(135,14),0),3); REM "PHONE NUMBER
1220 AT$(419,20)=UCS(I$(17,20)); REM "LOOKUP CODE
1225 AT$(160,20)=I$(216,20); REM "CONTACT PERSON
1230 AT$(223,3)=I$(160,3); REM "CONTACT CATEGORY
1235 AT$(231,33)=I$(163,33); REM " SELECTION CODES
1240 AT$(363,10)=I$(254,10); REM "CUSTOMER CODE
1245 AT$(373,18)=I$(236,18); REM "CONTACT TITLE
1250 AT$(452,12)=I$(264,12); REM "DUNS NUMBER
1255 Q2$=DAY,Q$=Q2$(7,2)+Q2$(1,2)+Q2$(4,2); GOSUB 7500; AT$(214,6)=D0$; REM "SYSTEM DATE
1500 REM "DO CHECKING ON THE FILE B4 WE WRITE TO ZP4 AND SORT FILES
1502 Z$="Old Value: ",Z1$="New Value: "
1505 K$=AT$(1,4)+AT$(419,20)
1506 READ (Z[4],KEY=K$,DOM=1507)
1510 K$=KEY(Z[4],END=1600)
1515 READ (Z[4])
1517 IF K$(25,6)="000000" THEN GOTO 1700
1520 IF K$(1,24)<>AT$(1,4)+AT$(419,20) THEN GOTO 1700; REM "BUILD A$ USING DEFAULT RECORD
1525 READ (Z[2],KEY=K$(1,4)+K$(25,6))IOL=0310; TMP$=A$
1530 IF A$(51,30)<>AT$(51,30) THEN VAR$=A$(51,30),VAR1$=AT$(51,30); A$(51,30)=AT$(51,30); GOSUB 7850; REM "UPDATE ADDRESS LINE 1
1535 IF A$(81,30)<>AT$(81,30) THEN VAR$=A$(81,30),VAR1$=AT$(81,30); A$(81,30)=AT$(81,30); GOSUB 7850; REM "UPDATE ADDRESS LINE 2
1540 IF A$(111,16)<>AT$(111,16) THEN VAR$=A$(111,16),VAR1$=AT$(111,16); A$(111,16)=AT$(111,16); GOSUB 7850; REM "UPDATE CITY
1545 IF A$(127,2)<>AT$(127,2) THEN VAR$=A$(127,2),VAR1$=AT$(127,2); A$(127,2)=AT$(127,2); GOSUB 7850; REM "UPDATE STATE
1550 IF A$(129,9)<>AT$(129,9) THEN VAR$=A$(129,9),VAR1$=AT$(129,9); A$(129,9)=AT$(129,9); GOSUB 7850; REM "UPDATE ZIP CODE
1555 IF A$(138,12)<>AT$(138,12) THEN VAR$=A$(138,12),VAR1$=AT$(138,12); A$(138,12)=AT$(138,12); GOSUB 7850; REM "UPDATE FAX NUMBER
1560 IF A$(160,20)<>AT$(160,20) THEN VAR$=A$(160,20),VAR1$=AT$(160,20); A$(160,20)=AT$(160,20); GOSUB 7850; REM "UPDATE CONTACT PERSON
1565 IF A$(200,14)<>AT$(200,14) THEN VAR$=A$(200,14),VAR1$=AT$(200,14); A$(200,14)=AT$(200,14); GOSUB 7850; REM "UPDATE PHONE NUMBER
1575 IF A$(223,3)<>AT$(223,3) THEN VAR$=A$(223,3),VAR1$=AT$(223,3); A$(223,3)=AT$(223,3); GOSUB 7850; REM "UPDATE CONTACT CATEGORY
1580 IF A$(226,5)<>AT$(226,5) THEN VAR$=A$(226,5),VAR1$=AT$(226,5); A$(226,5)=AT$(226,5); GOSUB 7850; REM "UPDATE STD INDUSTRY CLASSIFICATION CODE
1585 IF A$(363,10)<>AT$(363,10) THEN VAR$=A$(363,10),VAR1$=AT$(363,10); A$(363,10)=AT$(363,10); GOSUB 7850; REM "UPDATE CUSTOMER CODE
1590 IF A$(373,18)<>AT$(373,18) THEN VAR$=A$(373,18),VAR1$=AT$(373,18); A$(373,18)=AT$(373,18); GOSUB 7850; REM "UPDATE CONTACTS TITLE
1592 IF A$(452,12)<>AT$(452,12) THEN VAR$=A$(452,12),VAR1$=AT$(452,12); A$(452,12)=AT$(452,12); GOSUB 7850; REM "UPDATE DUNS NUMBER - W/O 108317
1595 GOSUB 7600
1600 GOTO 1110
1700 REM "BUILD RECORD USING DEFAULT INFORMATION
1705 A$=""
1710 READ (Z[2],KEY=AT$(1,4)+"000000",DOM=8010)TMP$; A$=TMP$
1715 IF AT$(5,6)<>"      " THEN A$(5,6)=AT$(5,6) ELSE FIND (Z[3],KEY=AT$(1,4))IOL=0320; A$(5,6)=STR(B[0]+1:"000000"),B[0]=B[0]+1; WRITE (Z[3],KEY=A$(1,4))IOL=0320
1720 IF STP(AT$(11,40),3," ")="" THEN GOTO 1721 ELSE A$(11,40)=AT$(11,40)
1725 IF STP(AT$(51,30),3," ")="" THEN GOTO 1726 ELSE A$(51,30)=AT$(51,30)
1730 IF STP(AT$(81,30),3," ")="" THEN GOTO 1731 ELSE A$(81,30)=AT$(81,30)
1735 IF STP(AT$(391,28),3," ")="" THEN GOTO 1736 ELSE A$(391,28)=AT$(391,28)
1740 IF STP(AT$(127,2),3," ")="" THEN GOTO 1741 ELSE A$(127,2)=AT$(127,2)
1745 IF STP(AT$(129,9),3," ")="" THEN GOTO 1746 ELSE A$(129,9)=STP(AT$(129,9),3,"-")
1750 IF STP(AT$(138,12),3," ")="" THEN GOTO 1751 ELSE A$(138,12)=AT$(148,12)
1755 IF STP(AT$(419,20),3," ")="" THEN GOSUB 6600 ELSE A$(419,20)=AT$(419,20)
1760 IF STP(AT$(160,20),3," ")="" THEN GOTO 1761 ELSE A$(160,20)=AT$(160,20)
1765 IF STP(AT$(200,14),3," ")="" THEN GOTO 1766 ELSE A$(200,14)=AT$(200,14)
1770 IF STP(AT$(223,3),3," ")="" THEN GOTO 1771 ELSE A$(223,3)=AT$(223,3)
1775 IF STP(AT$(231,33),3," ")="" THEN GOTO 1776 ELSE A$(231,33)=AT$(231,33)
1780 IF STP(AT$(363,10),3," ")="" THEN GOTO 1781 ELSE A$(363,10)=AT$(363,10)
1785 IF STP(AT$(373,18),3," ")="" THEN GOTO 1786 ELSE A$(373,18)=AT$(373,18)
1787 IF STP(AT$(452,12),3," ")="" THEN GOTO 1788 ELSE A$(452,12)=AT$(452,12); REM "W/O 108317
1790 REM "IF AT$(5,6)= ALL " " THEN FIND (Z[3],KEY=K$(1,4)) IOL=00320,let a$(5,6)=str(b[0]+1:"000000"),b[0]=b[0]+1 ELSE LET A$(5,6)=AT$(5,6)
1795 GOSUB 7600
1800 GOTO 1110
1900 REM "End of file
1901 REM "Close & Rename to CT4 for archiving. Erase an existing CT4 file, if same name
1905 Z$="01CU"+F1$+" "
1906 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
1910 F2$=F1$; IF F2$ LIKE "[Cc][Tt]1*" THEN F2$(1,3)="CT4" ELSE GOTO 1995
1919 REM "Get disk DIRectory of data files
1925 J$=%DATAPATH$
1930 REM "Move the file
1935 ERASE F2$,ERR=*PROCEED; RENAME J$+DLM+F1$ TO J$+DLM+F2$
1995 GOTO 1015
5000 REM "EOJ
5010 IF Q1$<>"G" THEN PRINT @(0,15),'CE',
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(10,5),"Importing Contact Management Gateway Records"
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6500 REM "Delete Record from Sort Files
6520 REMOVE (Z[4],KEY=TMP$(1,4)+TMP$(419,20)+TMP$(5,6),DOM=6521)
6530 REMOVE (Z[5],KEY=TMP$(200,10)+TMP$(1,10),DOM=6531)
6540 REMOVE (Z[6],KEY=TMP$(203,7)+TMP$(200,3)+TMP$(1,10),DOM=6541)
6545 RETURN 
6600 REM "Lookup Code
6610 FOR I=1 TO 20
6620 IF AT$(I,1)>"Z" THEN AT$(I,1)=CHR(ASC(AT$(419+I,1))-32); NEXT I ELSE NEXT I
6690 RETURN 
7500 REM "Convert date Q$ into D0$
7501 REM "Q$ in YYMMDD format
7505 D0$=""
7506 IF STP(Q$,3," ")="" THEN GOTO 7545
7510 IF LEN(Q$)<6 THEN GOTO 7545 ELSE D1$=Q$(1,2)
7515 IF D1$<"50" THEN D1$="20"+D1$ ELSE D1$="19"+D1$
7520 D0$=CHR(NUM(D1$(1,3),ERR=7521)-125)+D1$(4,1)+Q$(3,2)+Q$(5,2)
7545 RETURN 
7600 REM "Write out record to ZP4
7605 Z$="Reject Record",Z1$="",VAR$="",VAR1$=""
7607 GOSUB 6500
7610 WRITE (Z[2],KEY=A$(1,10))IOL=0310
7615 REM "WRITE (Z[3],KEY=A$(1,4)) IOL=00320
7625 WRITE (Z[4],KEY=A$(1,4)+A$(419,20)+A$(5,6))
7635 WRITE (Z[5],KEY=A$(200,10)+A$(1,10))
7645 WRITE (Z[6],KEY=A$(203,7)+A$(200,3)+A$(1,10))
7695 RETURN 
7750 REM "End of file
7754 IF LEN(RCOUNT$)<=0 THEN GOTO 7795
7755 FOR X=1 TO LEN(RCOUNT$)-2 STEP 3
7756 S$="GCM|STAT|"+FN%CDS$+"|"+X3$(40,3)+"|"+X3$(9,3)+"|"+FID(0)+"|"+F1$+"|"+RCOUNT$(X,3)+"|"+STR(RCOUNT[(X+2)/3]:"0000")
7757 S2$="echo "+QUO+S$+QUO+" >> "+HWD+DLM+"GW_LOG"
7758 INVOKE S2$
7759 NEXT X
7795 GOTO 1900
7800 REM "READ TO SEE IF LIST CODE IS SETUP
7810 FIND (Z[3],KEY=AT$(1,4),DOM=7811); GOTO 7845
7811 GOTO 8020
7845 RETURN 
7850 REM "WRITE TO LOG FILE
7860 WRITE (Z[9])A$(1,4)+A$(5,6)+AT$(11,40)+Z$+VAR$+Z1$+VAR1$
7880 VAR$="",VAR1$=""
7885 IF Z$="Reject Record" THEN GOTO 7695
7890 RETURN 
7900 REM "Read customer parameters
7910 DIM L$(76); READ (Z[9],KEY=A$(1,10),DOM=7911)IOL=0380
7920 IF STP(L$(11,10),3," ")="" THEN F3=0 ELSE F3=1; REM "Alt. inv. cust code?
7930 IF L$(74,1)="Y" THEN A$(149,1)="Y"; REM "Review reqs?
7940 LIMIT=0; LIMIT=NUM(L$(71,3),ERR=7941); REM "Set % of mthly avg as limit
7945 RETURN 
7950 REM "Blank location, manual ship-to?
7955 DIM FMP_D$(242); READ (Z[4],KEY="D"+A$(1,10),DOM=7956)FMP_D$
7960 IF FMP_D$(24,1)="Y" THEN IF S1$="1" THEN S1$="D"; GOTO 7995 ELSE S1$="C",A$(149,1)="Y"; GOTO 7995; REM "FMS customer, manual ship-to not allowed, set status & put on hold
7965 IF STP(I$(139,87),3," ")="" THEN GOTO 7966 ELSE A$(204,87)=I$(139,87),A$(341,35)=I$(276,35); GOTO 7995; REM "If manual ship-to info fields are blank we'll see if there is a default ship-to to use, else set manual ship-to fields
7970 IF FMP_D$(125,4)<>"    " THEN A$(11,4)=FMP_D$(125,4); GOTO 7995; REM "Found a default ship-to so let's use it
7975 IF STP(AR1$(56,87),3," ")="" THEN IF S1$="1" THEN S1$="F"; GOTO 7995 ELSE S1$="E",A$(149,1)="Y"; GOTO 7995; REM "Didn't find a default ship-to and address info from AR1 blank, set status & put on hold
7980 A$(204,87)=AR1$(56,87); REM "Using address info from AR1 as manual ship-to info
7995 RETURN 
8000 REM "SYSTEM PROMPTS
8010 CALL "ZZPROM",".4",X3$,Z,"Process Not Complete - Need to Set Up a Default Record in Contact Setup","","",0; GOTO 8090
8020 CALL "ZZPROM",".4",X3$,Z,"List Type Not Defined in List Type Setup","","",0; GOTO 8090
8090 GOTO 5040
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8105 REM "CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F2$
8106 REM "LET T=T0
8107 T=0
8111 DIR_PATH$=%DATAPATH$
8112 FULL_PATH$=DIR_PATH$+DLM+F1$
8113 CALL "ZZ2FNC;SerialRecCnt",Z[1],T ! CLOSE (14); OPEN (14)"< cat "+FULL_PATH$+" | wc -l"; READ (14)NUM_REC$
8114 T=NUM(NUM_REC$,ERR=8130)
8115 PRINT @(0,7),"There are "+STR(T)+" records to process"
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8135 T1=0
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,I9
8195 RETURN 
8200 REM "Try to read IC0 1st with alt. inv. cust code, 2nd with cust code, 3rd with 10 blanks
8203 REM "If found set item description & line type, if not set status & put on hold
8210 IF F3=1 THEN FIND (Z[6],KEY=L$(11,10)+B$(45,10),DOM=8211)IOL=0350; B$(35,10)=L$(11,10),B$(34,1)="X",B$(73,40)=E$(21,40); GOTO 8215; REM "Found using alt. inv. cust code
8211 FIND (Z[6],KEY=B$(1,10)+B$(45,10),DOM=8212)IOL=0350; B$(35,10)=B$(1,10),B$(34,1)="C",B$(73,40)=E$(21,40); GOTO 8215; REM "Found using cust code
8212 FIND (Z[6],KEY=B$(35,20),DOM=8213)IOL=0350; B$(34,1)="I",B$(73,40)=E$(21,40); GOTO 8215; REM "Found using 10 blanks
8213 B$(121,1)="4",A$(149,1)="Y"; WRITE (Z[2],KEY=A$(1,30))IOL=0310; REM "Did not find item in IC0, put req on hold
8290 RETURN 
8300 REM "Read IC0 with 1st 10 of item code from imported data
8303 REM "If found set item description & line type, if not set status & put on hold
8310 FIND (Z[6],KEY=B$(35,20),DOM=8312)IOL=0350; B$(73,40)=E$(21,40)
8311 IF B$(35,10)=B$(1,10) THEN B$(34,1)="C"; GOTO 8315 ELSE IF B$(35,10)=L$(11,10) THEN B$(34,1)="X"; GOTO 8315 ELSE B$(121,1)="6",A$(149,1)="Y"; WRITE (Z[2],KEY=A$(1,30))IOL=0310; GOTO 8315; REM "6=found item but it's not the alt. inv. customer or the regular customer code
8312 B$(121,1)="5",A$(149,1)="Y"; WRITE (Z[2],KEY=A$(1,30))IOL=0310; REM "Did not find, put req on hold
8390 RETURN 
8400 REM "Item security
8410 Q$=G$(311,6); FOR X=1 TO POS("  "=Q$,2) STEP 2
8420 IF POS(Q$(X,2)=L$(51,20),2)=0 THEN EXITTO 8430 ELSE NEXT X; RETURN 
8430 B$(121,1)="9",A$(149,1)="Y"; WRITE (Z[2],KEY=A$(1,30))IOL=0310
8490 RETURN 
8500 REM "Read FM4 for monthly avg usage for limit
8510 DIM N$(100),N[16]; FIND (Z[10],KEY=B$(1,10)+B$(45,10)+B$(11,4),DOM=8511)IOL=0390
8520 IF N[0]>0 THEN IF B[0]>LIMIT*N[0]/100 THEN B$(121,1)="B",A$(149,1)="Y"; WRITE (Z[2],KEY=A$(1,30))IOL=0310
8595 RETURN 
8910 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"      ")-1)
9000 REM "ERROR PROCESSING
9001 IF Q1$<>"G" THEN PRINT @(4,15),ERR,"  ",TCB(5); INPUT *
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1150,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; IF Q1$="G" THEN Q0$=STR(COUNT) ELSE Q1$=A1$
9935 EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM + Modification History
56002 REM "205703-Oracle - FID and FIB calls, replace with FN%FID$ and FN%FIB$
