0010 REM "Check for multiple P/O's before invoicing <AR2EAS>
0020 SETESC 9300; SETERR 9000
0035 REM "5.0 - 11/29/01 - 10.891944 - lms - SSP# 142887
0040 REM "Copyright 2001 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 BEGIN 
0100 SETERR 9000
0110 X0$="AR2EAS",X1$="Check for Muliple Purchase Orders",K9$="",K9=0
0140 M0$="#######.00-"
0145 GOSUB 8950
0230 X2=-1
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "Iolists
0310 IOLIST A$
0320 IOLIST B$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O PO1...  02O PO7...  03O FS1...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0550 READ (Z[13],KEY=X3$(9,3)+"F/M")P1$; REM " SSP# 129955 - CHECK PARAMETER
0600 REM 
0610 IF LEN(Q1$)<8 THEN GOTO 9700
1000 REM 
1005 L=0,L0=4,F0=0
1010 READ (Z[1],KEY=Q1$(1,8),DOM=1011)
1020 K$=KEY(Z[1],END=1090)
1030 IF K$(1,8)<>Q1$(1,8) THEN GOTO 1090
1035 READ (Z[1],KEY=K$,DOM=1020)IOL=0310
1036 CST_FLAG$=""; KEY$=Q1$; REM " SSP# 129955
1037 IF P1$(297,1)<>"Y" THEN GOTO 1045; REM " SSP# 129955 - CHECK FLAG
1038 CALL "PO2CST",A$,X3$,X4$,J$,KEY$,CST_FLAG$,"FLAG_CHECK",X0$,A{ALL},J{ALL},EXTRA_NUM2; REM " SSP# 129955 - CHECK TO SEE IF THE FLAG IS SET
1039 IF POS(CST_FLAG$="Y")<>0 THEN CALL "ZZPROM",".4",X3$,S3,"Unbilled Cost Update Parameter is set-MUST Update PO Recvg BEFORE A/R Billing ","","",0; GOTO 9700; REM " SSP# 129955 - IF THE FLAG IS SET TO DISPLAY MESSAGE - USER CAN'T ENTER AR INVOICE
1040 IF POS(CST_FLAG$="P")<>0 THEN CALL "ZZPROM",".4",X3$,S3,"Cost Update Parameter is Set-You MUST receive AP Invoice BEFORE A/R Billing ","","",0; GOTO 9700; REM " SSP# 129955 - IF THE FLAG IS SET TO DISPLAY MESSAGE - USER CAN'T ENTER AR INVOICE
1045 REM "IF first one, don't do anything,if second, display 1st one first, else just display
1050 ON L GOTO 1060,1070,1080
1060 A1$=A$,L=1; GOTO 1085
1070 GOSUB 3000; GOSUB 2000; L=2; GOTO 1085
1080 GOSUB 2000
1085 GOTO 1020
1100 REM "What to do next
1102 FIND (Z[3],DOM=1400,KEY=Q1$(1,8)); GOTO 1103
1105 REM "if no muliples then go on
1107 IF L=1 THEN GOSUB 2300; IF F0<>0 THEN GOTO 1300
1110 IF L<2 THEN GOTO 9700
1120 CALL "ZZPROM","Y",X3$,Z,"Do you wish to invoice the customer for this order now?","","",0
1130 ON Z GOTO 1300,1200
1200 REM "Remove AR2EAA record and continue
1210 IF LEN(U1$)<6 THEN GOTO 9700
1215 IF U1$(1,6)<>"AR2EAA" THEN GOTO 9700
1220 Q1=NUM(U1$(7,2)),Q1$=U1$(9,Q1),U1$=U1$(Q1+9)
1230 PRINT @(0,3),'CE',
1240 GOTO 9700
1300 REM "Update freight amount in U1$ and continue
1305 IF LEN(U1$)<38 THEN GOTO 1340 ELSE IF U1$(1,6)<>"AR2EAA" THEN GOTO 1340
1310 U1$(28,11)=STR(F0:M0$)
1340 GOTO 9700
1400 REM "No S/O - can't invoice"
1420 CALL "ZZPROM",".4",X3$,0,"Sales Order no longer open","","",0
1440 GOTO 1200
2000 REM "Print on screen
2010 IF L0>20 THEN CALL "ZZPROM",".4",X3$,0,"","","",0; PRINT @(0,4),'CE',; L0=4
2020 GOSUB 2100
2030 CALL "ZZDISP","A",A$(297,9),"P/O",X3$,"","",7,L0,X4$
2035 CALL "ZZDISP","AX",A$(7,10),"A/P",X3$,X$,"",0,0,X4$
2040 IF POS(" "<>A$(17,4))<>0 THEN X$=X$+" - "+A$(17,4) ELSE X$=X$+"   "+A$(17,4)
2045 PRINT @(24,L0),X$,"  "+A$(21,35),
2085 L0=L0+1
2090 RETURN 
2100 REM "Check PO7 to see if recv'd
2105 R$=" "
2110 READ (Z[2],KEY=A$(297,9),DOM=2111)
2115 K0$=KEY(Z[2],END=2130); READ (Z[2])
2120 IF K0$(1,9)<>A$(297,9) THEN GOTO 2130
2123 GOSUB 2200
2125 R$="Yes"
2130 PRINT @(2,L0),R$,
2140 RETURN 
2200 REM "Figure freight for this P/O and add to F0
2201 REM "Assumes K0$ pointing to first PO7 record and Z(2) is pointing to next one
2205 F5$=""
2209 REM "Only process 'header' PO7 records
2210 IF K0$(23,1)<>" " THEN GOTO 2230
2215 READ (Z[2],KEY=K0$)IOL=0320
2220 F1=0,F1=NUM(B$(51,11),ERR=2230)
2225 IF B$(10,10)<>F5$ THEN F0=F0+F1,F5$=B$(10,10)
2230 K0$=KEY(Z[2],END=2240); READ (Z[2])
2235 IF K0$(1,9)=A$(297,9) THEN GOTO 2210
2240 RETURN 
2300 REM "Get freight if only one P/O
2310 READ (Z[2],KEY=A$(297,9),DOM=2311)
2315 K0$=KEY(Z[2],END=2330); READ (Z[2])
2320 IF K0$(1,9)<>A$(297,9) THEN GOTO 2330
2325 GOSUB 2200
2340 RETURN 
3000 REM "Clear screen & display first P/O now
3010 PRINT @(0,3),'CE',
3020 GOSUB 6000
3030 A2$=A$,A$=A1$
3040 GOSUB 2000
3050 A$=A2$
3090 RETURN 
6000 REM "Background
6010 PRINT 'SB',
6060 PRINT @(0,3),"Recv'd",@(7),"P/O number",@(24),"Vendor",
6165 PRINT 'SF',
6190 RETURN 
8950 REM "Program flow logic
8960 DIM U0$(6); U0$(1)=FID(0),U0$=U0$+"PF"; CLOSE (14); OPEN (14)"ZZPARM"
8970 FIND (14,KEY=U0$,DOM=8990)U0$,U1$
8975 IF U1$="" THEN RETURN 
8980 Q1=NUM(U1$(7,2)),Q1$=U1$(9,Q1),U1$=U1$(Q1+9)
8990 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9705 L0=0,L1=0
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9725 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9730 RUN U1$(1,6)
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9950 RUN "ZMENU"
9999 END 
