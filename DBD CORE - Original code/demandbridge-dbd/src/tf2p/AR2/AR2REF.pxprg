0010 REM "<AR2REF> Customer Summary Bill--Tully-Wihr-Depts
0035 REM "5.7 - 07/29/24 - 16.6403 - crg - SSP# 307474
0037 REM "307474-DBD-441: Changes related to Cust PO for Summary Bill invoice
0040 REM "Copyright 2024 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0092 REM " Breaks by either receiving dept(R) or billing dept(P)
0093 REM "83171, Tully-Wihr changes all work by entering Z in options field
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="AR2REF",X1$="Summary Bill"
0120 DIM A$(523),AR6[15],AR6_TOT[15],A[13],C$(400),C[15],W[4],E$(247),S$(20),F$(180),F[3],G[2] ! WO212145, changed DIM for FMY from 265 to 512
0140 Z0$="-##,###,###.00",Z1$="####,###.00-",Z2$="#,###,###.00-",Z3$="###,###.00-",Z4$="#####.00-",Z5$="$"+Z2$
0160 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0185 DEF FNS$(Z9$)=Z9$(1,POS(S$=Z9$+S$)-1)
0190 DEF FNE$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+NUM(Z9$(2,1))+330*POS("  "=Z9$(1,2)):"##")
0210 T=1,T0=0,T1=5
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0310 IOLIST A$(1),A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13] ! WO212145, added (1) to A$ so record will always match DIM
0325 REM "IOLIST FOR AR1 - Customer Masterfile
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15]
0340 IOLIST AR6$,AR6[0],AR6[1],AR6[2],AR6[3],AR6[4],AR6[5],AR6[6],AR6[7],AR6[8],AR6[9],AR6[10],AR6[11],AR6[12],AR6[13],AR6[14],AR6[15]
0350 IOLIST E$
0360 IOLIST F$,F[0],F[1],F[2]
0370 IOLIST FM1$,FM1[0],FM1[1],FM1[2],FM1[3],FM1[4],FM1[5],FM1[6],FM1[7],FM1[8],FM1[9],FM1[10],FM1[11],FM1[12],FM1[13],FM1[14],FM1[15],FM1[16],FM1[17],FM1[18],FM1[19],FM1[20],FM1[21],FM1[22],FM1[23],FM1[24],FM1[25],FM1[26],FM1[27],FM1[28],FM1[29],FM1[30],FM1[31]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$,W[0],W[1],W[2],W[3]
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O FMY...  03O AR1...  04O AR6...  05O FM0... 07O FM1...  08O AR2...  13O ZZPARM "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0530 IF V0$(72,1)="S" THEN GOSUB 7550
0535 IF %HAS_IMAGE_LIB THEN GOSUB 15100 ! SSP 213924 Create temp file for Image Library entries
0550 READ (Z[13],KEY="CMP"+X3$(9,3),DOM=9900)C0$
0555 EXPORT_ON$="N"; READ (Z[13],KEY=X3$(9,3)+"EXPORTAR",DOM=0556)EXPORT_PARAM$; IF EXPORT_PARAM$(12,1)="Y" THEN EXPORT_ON$="Y"
0585 RT_PARM$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,RT_PARM$
0620 FIND (Z[13],KEY=X3$(9,3)+"A/R",DOM=9800)P0$
0640 IF X3$(56,3)="DMY" THEN Z5$(1,1)="#"
0650 REM "Check Repository parameters
0660 R6$="N"; IF V3$(2,1)<>"T" THEN CALL "ZR2DGP",ERR=0661,X3$,X4$,"SUMBIL_RPT",R6$
0665 IF R6$(1,1)="Y" THEN T9$=X3$(21,6)+X3$(178,6); CALL "ZR2DND",ERR=0666,X3$,X4$,T9$,"",R6$; REM "Signal start of new document, key is ...
0700 REM "Open Printer
0720 REM 
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[2]; U=18,U0=2,U[0]=0,U[1]=10,U[2]=8,T0=1,V1=10,U0$="    ",T5$="AA/R            ",U5$="Customer"; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0880 IF POS(V0$(72,1)="DB")>0 THEN GOTO 8000
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0931 V9$=V2$
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0960 V2$(1,20)=V9$(1,20)
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])>=V2$(U3,U[U9]) THEN GOTO 1030 ELSE EXTRACT (U1,KEY=U$(1,U2-1)+V2$(U3,U[U9]),DOM=1021)
1025 EXITTO 1000
1030 IF U$(U2,U[U9])<=V2$(U3+U[U9],U[U9]) THEN GOTO 1040 ELSE READ (U1,KEY=U$(1,U2-1)+$FF$,DOM=1032)
1035 EXITTO 1000
1040 NEXT U9
1100 REM "Get Record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1130 READ (U1); READ (Z[1],KEY=U$(1,10)+U$(39,12),DOM=1000)IOL=0310
1135 IF A$(29,1)="N" THEN GOTO 1000; REM "dxm, 85686
1140 IF (A$(192,1)="P" AND Q5<>1 AND POS("X"=V0$(59,12))=0) OR (A$(192,1)<>"P" AND Q5<>1 AND POS("X"=V0$(59,12))>0) THEN GOTO 1000 ELSE GOTO 4000
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1152 IF ((V0$(305,6)<>"      " AND A$(23,6)<V0$(305,6)) OR (V0$(311,6)<>"      " AND A$(23,6)>V0$(311,6))) THEN GOTO 1000; REM "ssp #111944 Date Range selection, SSP128181, open up for all companies
1153 IF (A$(192,1)="P" AND Q5<>1 AND POS("X"=V0$(59,12))=0) OR (A$(192,1)<>"P" AND Q5<>1 AND POS("X"=V0$(59,12))>0) THEN GOTO 1000
1155 IF A$(72,1)=" " THEN A$(72,1)="I"
1156 IF V0$(292,4)<>"    " THEN IF POS(A$(72,1)=V0$(292,4))=0 THEN GOTO 1000
1157 IF TYPE$="Receiving" THEN DEPT$=A$(226,20) ELSE DEPT$=A$(246,20)
1158 IF V0$(318,1)="1" OR V0$(318,1)=" " THEN GOTO *NEXT ELSE IF V0$(318,1)="2" AND NUL(A$(34,15)) THEN GOTO *NEXT ELSE IF V0$(318,1)="3" AND MSK(STP(A$(34,15),2),STP(MID(V0$,319,60),2))=1 THEN GOTO *NEXT ELSE IF V0$(318,1)="4" AND MSK(STP(A$(34,15),2),STP(MID(V0$,319,60),2))<>1 THEN GOTO *NEXT ELSE GOTO 1000 ! 307474-Filter invoices: 1=All, 2=Blank only, 3=Matches a pattern, 4=Does not match pattern
1160 IF Q5=2 THEN PRINT @(30),U$(1,10),"-",U$(11,8),"-",U$(19,3),; IF POS("I"=V0$(59,12))<>0 THEN WRITE (Z[2],KEY=A$(1,10)+DEPT$+A$(23,6)+"  "+A$(11,12)); GOTO 1000 ELSE WRITE (Z[2],KEY=A$(1,10)+DEPT$+A$(49,8)+A$(11,12)); GOTO 1000
1162 IF Q5=1 AND POS("M"=V0$(59,12))<>0 THEN PRINT @(25,21),U$, ELSE GOTO 1165
1163 IF U$(1,18)=C1$ THEN GOTO 1000 ELSE GOSUB 5500; GOSUB 5600; GOTO 1000
1165 IF POS("X"=V0$(59,12))>0 AND Q5=1 THEN IF A$(192,1)="P" THEN PRINT @(25,21),U$,; REMOVE (Z[1],KEY=U$,DOM=1000); GOTO 1000 ELSE GOTO 1000
1166 IF Q5=1 THEN PRINT @(25,21),U$,; REMOVE (Z[1],KEY=U$,DOM=1000); IF U$(1,18)=C1$ THEN GOTO 1000 ELSE GOSUB 5500; GOSUB 5600; GOTO 1000
1170 IF Q5=3 THEN A$(192,1)="P"; WRITE (Z[1],KEY=U$)IOL=0310; GOTO 1000
1180 IF A$(29,1)<>"Y" AND V0$(73,1)<>"Y" THEN GOTO 1000
1200 REM "check range
1210 IF C1$<>A$(1,10) THEN GOSUB 2000
1220 IF C2$<>A$(11,8) THEN GOSUB 2100
1300 REM "Read other data
1400 REM "Subtotals"
1415 REM GOSUB 03100
1420 ON T GOTO 1470,1430,1440,1450,1460
1450 GOSUB 6200
1490 T=0
1500 REM "Lines
1510 IF W+4>W0 THEN GOSUB 6000
1520 Q=A[1]; IF A$(73,1)<>"M" AND Q<>0 THEN CALL "FM2EXT",Z[13],2,A$(138,4),A[0],Q,0,0,0
1530 CALL "ZZDISP","AX",A$(74,20),"I/C",X3$,X$,"",0,0,X4$
1534 REM "Look for a CIC with full item code, if not found try using cust+item, this may be a stock item
1535 DIM FM1$(448),FM1[31]; FIND (Z[7],KEY=A$(74,20),DOM=1536)IOL=0370; GOTO 1540
1536 FIND (Z[7],KEY=A$(1,10)+A$(84,10),DOM=1537)IOL=0370
1540 IF POS(" "<>FM1$(350,20))>0 THEN X$=FM1$(350,20)
1550 IF Q<>0 THEN Y5$(3)=FNQ$(Q,Z0$)
1555 IF A$(73,1)<>"M" THEN Y5$(18)=A$(226,20)
1560 Y5$(44)=X$,Y5$(65)=A$(94,30)
1565 IF A[2]<>0 AND A[3]<>0 AND A$(138,4)="T   " AND A[0]=1000 THEN GOSUB 7700; GOTO 1585
1570 IF A[2]<>0 AND A[3]<>0 THEN Y5$(95)=STR(A[2]:Z2$)
1580 Y5$(109,4)=A$(138,4)
1585 IF A$(73,1)="C" AND A$(134,1)="9" THEN Y5$(105)="(Req)"; IF A[3]<>0 THEN Y5$(98)="<*BILLED CUST OWNED*"; FLAG=1; GOTO 1592 ! SSP#234752
1590 IF A[3]<>0 THEN Y5$(113)=STR(A[3]:Z2$)
1592 CONTRACT$=""; IF A$(386,1)="Y" THEN CONTRACT$="C",Y5$(114,1)=CONTRACT$ ! WO212145, contract price (DDMS)
1595 IF A$(152,1)="Y" THEN Y5$(113+LEN(Z2$))="*"
1600 A2[0]=A2[0]+A[5],A2[1]=A2[1]+A[6],A2[2]=A2[2]+A[3]
1620 GOSUB 7300
1625 IF A$(365,20)<>DIM(20) AND A$(365,20)<>"DIDN'T FIND RC      " AND A$(73,1)<>"M" THEN Y5$(65)="RC: "+A$(365,20); GOSUB 7300; REM SSP 184043
1640 X$=""; IF POS(" "<>A$(153,9))>0 AND A$(73,1)<>"M" THEN X$="Starting#: "+FNR$(A$(153,9))+"  "
1650 IF POS(" "<>A$(162,9))>0 THEN X$=X$+"Ending#: "+FNR$(A$(162,9))
1660 IF POS(" "<>X$)>0 THEN Y5$(40)=X$; GOSUB 7300
1900 REM "Accumulate Totals"
1990 T[T0,0]=T[T0,0]+1
1992 T[T0,1]=T[T0,1]+A[1],T[T0,2]=T[T0,2]+A[3],T[T0,3]=T[T0,3]+A[4]
1995 GOTO 1000
2000 REM "Customer Change
2005 ITYPE$="AC" ! SSP 213924 sET I/L Type for Summary Bill
2010 IF C1$="" THEN GOTO 2050
2020 T1$="END"; GOSUB 2100; T1$=""
2030 IF W+3>W0 THEN GOSUB 6000
2032 X$=V0$(73,8); IF V0$(73,8)="AUTO    " THEN GOSUB 7500
2035 GOSUB 7300; Y5$(6)="When paying, please reference Summary Bill#: "+X$,Y5$(92)="SUMMARY BILL TOTAL:",Y5$(113)=STR(A3[0]+A3[1]+A3[2]:Z2$); GOSUB 7300
2040 IF V0$(81,1)="Y" THEN GOSUB 7600
2045 IF T0$="END" THEN RETURN 
2050 C1$=A$(1,10),SUMM_CUST$=A$(1,10) ! SSP#258564
2055 IF P0$(15,1)="Y" THEN READ (Z[3],KEY=A$(1,10))IOL=0330; IF POS(" "<>C$(46,10))<>0 THEN SUMM_CUST$=C$(46,10); GOTO 2060 ! SSP#258564
2060 DIM C$(LEN(C$)); FIND (Z[3],DOM=2061,KEY=C1$)IOL=0330
2070 W0$=FNS$(C$(11,35))
2075 GOSUB PUT_IN_IMAGE_LIBRARY ! ssp 213924
2080 GOSUB 6000; GOSUB 6300
2082 IF %HAS_IMAGE_LIB THEN MNEMONIC (%IL_PDF)'F_'="" ! SSP 213924
2085 DIM A3[2]
2090 RETURN 
2100 REM "Invoice Number Change
2110 IF C2$="" THEN GOTO 2180
2115 REM "IF POS("L"=V0$(59,12))<>0 THEN LET Y5$(20)=E$(214,9)
2130 IF A2[0]<>0 THEN Y5$(103)="Freight:",Y5$(113)=STR(A2[0]:Z2$); GOSUB 7300
2140 IF A2[1]<>0 THEN Y5$(101)="Sales Tax:",Y5$(113)=STR(A2[1]:Z2$); GOSUB 7300
2150 Y5$(97)="Invoice Total:",Y5$(113)=STR(A2[0]+A2[1]+A2[2]:Z2$); GOSUB 7300; GOSUB 7300
2152 FOR I=0 TO 2; A3[I]=A3[I]+A2[I]; NEXT I
2155 IF T0$="END" THEN RETURN 
2160 IF T1$<>"END" THEN GOSUB 6300
2180 C2$=A$(11,8); DIM A2[2]
2190 RETURN 
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3101 RETURN 
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; GOSUB 7000
3155 IF U0$(1,1)="Y" THEN GOSUB 6000
3160 U1$=U9$(1,V1)
3180 RETURN 
3280 RETURN 
3380 RETURN 
3480 RETURN 
4000 REM "Shipment Detail by Location Version
4010 IF A$(73,1)="M" THEN GOTO 1000
4020 IF C1$<>A$(1,10) THEN GOSUB 4800
4025 IF TYPE$="Receiving" THEN DEPT$=A$(226,20) ELSE DEPT$=A$(246,20)
4030 IF C2$<>A$(1,10)+DEPT$ THEN GOSUB 4900
4035 IF POS("S"=V0$(59,12))<>0 THEN GOTO 4170; REM "Doing summarized version
4040 IF POS("Z"=V0$(59,12))<>0 THEN IF W+5>W0 THEN GOSUB 6000 END_IF ELSE IF W+4>W0 THEN GOSUB 6000
4045 IF POS("I"=V0$(59,12))<>0 THEN Y5$(1)=FNE$(A$(23,6)) ELSE CALL "ZZDISP","AX",A$(49,8),"O/P",X3$,X$,"",0,0,X4$; Y5$(1)=X$
4050 CALL "ZZDISP","AX",A$(11,8),"AR6",X3$,X$,"",0,0,X4$; Y5$(11)=X$
4060 Q=A[1]; IF Q<>0 THEN CALL "FM2EXT",Z[13],2,A$(138,4),A[0],Q,0,0,0
4070 IF Q<>0 THEN Y5$(21)=FNQ$(Q,Z0$)
4080 CALL "ZZDISP","AX",A$(74,20),"I/C",X3$,X$,"",0,0,X4$
4084 REM "Look for a CIC with full item code, if not found try using cust+item, this may be stock item
4085 DIM FM1$(448),FM1[31]; FIND (Z[7],KEY=A$(74,20),DOM=4086)IOL=0370; GOTO 4090
4086 FIND (Z[7],KEY=A$(1,10)+A$(84,10),DOM=4087)IOL=0370
4090 IF POS(" "<>FM1$(350,20))>0 THEN X$=FM1$(350,20)
4100 Y5$(35)=X$,Y5$(56)=A$(94,30)
4104 REM "Req value is in A(4)
4105 IF A$(73,1)="C" AND A$(134,1)="9" THEN Y5$(109)="(Req)"; GOTO 4120
4110 IF A[2]<>0 THEN Y5$(86)=STR(A[2]:Z3$)
4115 Y5$(102)=STR(A[3]:Z3$); IF A$(152,1)="Y" THEN Y5$(113)="*"
4120 Y5$(97,4)=A$(138,4)
4140 Y5$(114)=STR(A[5]:Z4$),Y5$(123)=STR(A[6]:Z4$)
4145 CONTRACT$=""; IF A$(386,1)="Y" THEN CONTRACT$="C",Y5$(102,1)=CONTRACT$ ! WO212145, contract price (DDMS)
4150 IF A$(73,1)="C" AND A$(134,1)="9" AND A[3]<>0 THEN Y5$(102)="<*BILLED CUST OWNED "; FLAG=1 ! SSP#234752
4165 GOSUB 7300
4170 A2[0]=A2[0]+A[5],A2[1]=A2[1]+A[6],A2[2]=A2[2]+A[3]
4180 IF POS("Z"=V0$(59,12))<>0 THEN GOSUB 4700; REM "Print second line stuff for Tully-Wihr, ssp#83171
4190 GOTO 1000
4700 REM "Second line stuff--Tully-Wihr
4705 DIM DEPT_2ND$(522); LOC$=""
4710 IF TYPE$="Billing" THEN IF POS(" "<>A$(226,20))>0 THEN Y5$(3)=A$(226,20); CALL "FM2DPB",X3$,X4$,A$(1,10),A$(226,20),LOC$,LOC_DEPT${ALL}; FIND (Z[5],KEY="C"+A$(1,10)+LOC$,DOM=4711)DEPT_2ND$ END_IF ELSE IF POS(" "<>A$(246,20))>0 THEN Y5$(3)=A$(246,20); CALL "FM2DPB",X3$,X4$,A$(1,10),A$(246,20),LOC$,LOC_DEPT${ALL}; FIND (Z[5],KEY="C"+A$(1,10)+LOC$,DOM=4711)DEPT_2ND$
4715 Y5$(25)=DEPT_2ND$(16,35)
4720 Y5$(64)="Shipped: "+FND$(A$(171,6))
4730 Y5$(87)="PO: "+A$(34,15)
4790 GOSUB 7300
4795 RETURN 
4800 REM "Customer Change
4805 ITYPE$="AB" ! SSP 213924 Image library type for Detail Shipment by Loc
4810 IF C1$="" THEN GOTO 4850
4820 T1$="END"; GOSUB 4900; T1$=""
4830 IF W+3>W0 THEN GOSUB 6000
4835 GOSUB 7300; IF POS("S"=V0$(59,12))<>0 THEN Y5$(1)="Customer Total:",Y5$(19)=STR(A3[0]+A3[1]+A3[2]:Z5$) ELSE Y5$(40)="Customer Totals:",Y5$(83)=STR(A3[0]+A3[1]+A3[2]:Z5$),Y5$(102)=STR(A3[2]:Z3$),Y5$(114)=STR(A3[0]:Z4$),Y5$(123)=STR(A3[1]:Z4$)
4840 GOSUB 7300
4845 IF T0$="END" THEN RETURN 
4850 C1$=A$(1,10),SUMM_CUST$=A$(1,10) ! SSP#258564
4860 DIM C$(LEN(C$)); FIND (Z[3],DOM=4861,KEY=C1$)IOL=0330; IF P0$(15,1)="Y" AND POS(" "<>C$(46,10))<>0 THEN SUMM_CUST$=C$(46,10) ! SSP#258564
4870 W0$=FNS$(C$(11,35))
4875 GOSUB PUT_IN_IMAGE_LIBRARY ! ssp 213924
4880 GOSUB 6000
4882 IF %HAS_IMAGE_LIB THEN MNEMONIC (%IL_PDF)'F_'="" ! SSP 213924
4885 DIM A3[2]
4890 RETURN 
4900 REM "Department Change
4910 IF C2$="" THEN GOTO 4960
4915 IF POS("S"=V0$(59,12))=0 AND POS("Z"=V0$(59,12))=0 THEN GOSUB 7300; Y5$(20)="Total for "+E1$ ELSE IF POS("Z"=V0$(59,12))>0 THEN GOSUB 7300; Y5$(4)="Total for "+E1$
4920 IF POS("S"=V0$(59,12))<>0 THEN Y5$(1)=J5$,Y5$(19)=STR(A2[0]+A2[1]+A2[2]:Z5$),Y5$(35)=J6$ ELSE Y5$(83)=STR(A2[0]+A2[1]+A2[2]:Z5$),Y5$(102)=STR(A2[2]:Z3$),Y5$(114)=STR(A2[0]:Z4$),Y5$(123)=STR(A2[1]:Z4$)
4925 IF POS("S"=V0$(59,12))<>0 THEN GOSUB 7300 ELSE GOSUB 7300; GOSUB 7300
4930 FOR I=0 TO 2; A3[I]=A3[I]+A2[I]; NEXT I
4932 IF POS("Z"=V0$(59,12))>0 THEN DEPT_SUMM$=DEPT_SUMM$+C2$(11,20)+E$(16,35)+STR(A2[0]+A2[1]+A2[2]:Z2$); REM "print dept summ in 8400's, ssp#83171
4935 IF T1$="END" THEN RETURN 
4950 IF V0$(296,1)="Y" THEN GOSUB 6000
4960 C2$=A$(1,10)+DEPT$; DIM A2[2]
4965 IF POS("Z"=V0$(59,12))=0 THEN IF TYPE$="Billing" THEN DIM E$(LEN(E$)); GOTO 4980
4970 DIM X$(15); X$(1,1)=A$(147,1),X$(12,4)=A$(148,4); IF X$(1,1)="C" THEN X$(2,10)=A$(1,10)
4972 IF X$(1,1)="C" THEN IF POS("Z"=V0$(59,12))>0 THEN IF TYPE$="Billing" THEN LOC$=""; CALL "FM2DPB",X3$,X4$,A$(1,10),DEPT$,LOC$,LOC_DEPT${ALL}; X$(12,4)=LOC$
4975 DIM E$(LEN(E$)); FIND (Z[5],DOM=4976,KEY=X$)IOL=0350; GOTO 4980
4976 IF POS(" "<>X$(12,4))=0 THEN E$(16,132)=C$(11,35)+C$(56)
4980 GOSUB 6400
4990 RETURN 
5000 REM "End of Print
5005 IF Q5=2 THEN GOTO 8100
5010 IF Q5=1 AND EXPORT_ON$="Y" THEN GOSUB 5715; GOTO 9920 ELSE IF Q5=1 THEN GOTO 9920
5015 IF Q5=3 THEN GOTO 5318
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5032 IF V0=2 THEN GOSUB 4800; GOTO 8300
5035 GOSUB 2000; GOTO 5200
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; GOSUB 7000
5060 IF POS("M"=V0$(59,12))<>0 THEN CALL "ZZPROM",".Y",X3$,Z,"Do you wish to archive this information?","","",0; ON Z GOTO 5061,9920 ELSE GOTO 5071
5065 Q5=3; T9=1
5070 PRINT @(24),"Setting the Print Archive Flag"; GOTO 0900
5200 REM "
5210 T9=1
5290 GOTO 9900
5300 REM "Purge records
5302 IF FLAG THEN X$="Billing problem detected, no update allowed"; CALL "ZZPROM",".4",X3$,Z,X$,"","",0; GOTO 9920 ! ssp#234752
5310 CALL "ZZPROM","X0AR2RHA",X3$,Z,"","","",0; ON Z GOTO 5311,9920
5317 PRINT @(0,20),'CE',@(23,20),"Now clearing:",
5320 AR6_TOT$=""
5360 Q5=1,C1$=""; GOTO 0900
5500 REM "Flag Open Invoice Record
5505 IF A$(29,1)<>"Y" THEN GOTO 5580
5506 D$=A$(1,10)
5510 IF P0$(15,1)="Y" THEN READ (Z[3],KEY=A$(1,10))IOL=0330; IF POS(" "<>C$(46,10))<>0 THEN A$(1,10)=C$(46,10)
5515 DIM AR6[15]
5520 EXTRACT (Z[4],KEY=A$(1,18),DOM=5580)IOL=0340
5521 IF AR6$(160,8)<>"PENDING " THEN GOTO 5580; REM "If summary bill no already assigned, don't write over it
5525 Q$=V0$(73,8); IF Q$="AUTO    " THEN FIND (Z[6],KEY=D$(1,10))*,Q$
5530 AR6$(160,8)=Q$
5535 IF EXPORT_ON$="Y" THEN GOSUB EXPORT_LOGIC
5540 WRITE (Z[4])IOL=0340
5541 IF RT_PARM$<>"" THEN CUST$=AR6$(1,10); CALL "RT2WOC",ERR=5542,X3$,X4$,CUST$,"U",AR6$(1,18)
5550 IF %HAS_IMAGE_LIB THEN IMG_INV$=Q$,C1$=A$(1,10); GOSUB UPD_IL_INV_NUM ! SSP 213924 Update I/L with Summary bill ref number   !SSP#254728
5580 C1$=U$(1,18)
5590 RETURN 
5600 REM "If this is an R/L, need to send summary bill no to M/L
5610 IF X3$(330,1)<>"1" THEN GOTO 5695
5620 K0$="CMH"+X3$(9,3)+"  "+"|"+X$(1,18),K1$=X$(1,18)+X$(160,8)
5630 CALL "CM2AAA",X3$,0,4,K0$,K1$,0
5695 RETURN 
5700 EXPORT_LOGIC:REM "Total into AR6_TOT until customer # changes, then create pseudo ARB data to pass to export routine
5705 IF AR6_TOT$="" THEN AR6_TOT$=AR6$; DIM AR6_TOT[15]; FOR TOT_I=0 TO 15; AR6_TOT[TOT_I]=AR6[TOT_I]; NEXT TOT_I; GOTO 5795; REM "FIRST TIME THRU, SO INITIALIZE EVERTHING AND CAPTURE 1ST ONE.
5710 IF AR6_TOT$(1,10)=AR6$(1,10) THEN FOR TOT_I=1 TO 15; AR6_TOT[TOT_I]=AR6_TOT[TOT_I]+AR6[TOT_I]; NEXT TOT_I; GOTO 5795; REM "if on the same customer then add numerics and leave
5714 REM "If here then we are not on first one, and the customer number has changed so we need to update the old record and start on the new one.
5715 DIM ARB$(689),ARB[30]
5720 ARB$(15,10)=AR6_TOT$(1,10),ARB$(7,8)=AR6_TOT$(160,8); REM "customer number, invoice number from summary bill number
5722 ARB$(86,6)=X3$(21,6),ARB$(92,8)=AR6_TOT$(66,8); REM "invoice date from accounting date, order number from 1st invoice
5724 ARB$(100,15)=AR6_TOT$(36,15),ARB$(133,4)=AR6_TOT$(74,4); REM "Customer P/O and 1st salesperson
5726 ARB$(180,6)=AR6_TOT$(139,6); REM "Date shipped
5728 ARB[1]=AR6_TOT[9],ARB[2]=AR6_TOT[8],ARB[3]=ARB[1]+ARB[2]; REM "net sale, freight, taxable amount
5730 ARB[4]=AR6_TOT[7],ARB[9]=AR6_TOT[6],ARB[11]=AR6_TOT[9]-AR6_TOT[10]; REM "Sales tax billed, invoice total, cost (net sale - gross profit)
5735 DIM Z_FAKE[4]; Z_FAKE[4]=Z[3]; REM "used in call to access customer master
5740 CALL "AR2XIA",X3$,X4$,EXPORT_PARAM$,FILES{ALL},ARB$,ARB{ALL},Z_FAKE{ALL},UPDATE$
5745 AR6_TOT$=""; GOTO 5705; REM "Clear totals and go to start new totals
5795 EXPORT_LOGIC_END:RETURN 
6000 REM "Page header
6030 IF POS("Z"=V0$(59,12))>0 THEN W8$=W8$+"sP030"; REM "Suppress run date and time, move page number over to left,ssp#83171
6035 IF POS("Z"=V0$(59,12))=0 THEN IF T0$="END" THEN W0$=""
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$+"R",R6$; IF T0$="END" THEN GOTO 6060 ELSE ON W5 GOTO 6041,9910
6045 ON W5 GOTO 6046,9900
6060 IF PRINT_SUMM$<>"Y" THEN GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6101 IF V0=2 AND POS("S"=V0$(59,12))<>0 THEN Y5$(1)="Department",Y5$(25)="Total",Y5$(45)="Description",Y5$(71)="Address"; GOTO 6130 ELSE IF V0=2 THEN IF POS("I"=V0$(59,12))<>0 THEN Y5$(1)="Invoice"
6102 IF V0=2 THEN Y5$(91)="Unit",Y5$(116)="Freight",Y5$(126)="Sales"; GOSUB 7300
6103 IF MFLAG$="Y" THEN Y5$(6)="ITEM NUMBER",Y5$(19)="ORDER NUMBER",Y5$(43)="DESCRIPTION",Y5$(75)="UNIT PRICE",Y5$(92)="QTY SHIPPED",Y5$(109)="AMOUNT"; GOSUB 7300
6105 IF V0=2 THEN Y5$(2)="Order",Y5$(11)="Invoice",Y5$(23)="Quantity",Y5$(35)="Item No.",Y5$(56)="Description",Y5$(91)="Price",Y5$(106)="Amount",Y5$(127)="Tax"; IF POS("I"=V0$(59,12))<>0 THEN Y5$(2,5)=" Date"; GOTO 6130 ELSE GOTO 6130
6110 Y5$(6)="QUANTITY",Y5$(18)="RECEIVING DEPT",Y5$(45)="ITEM NUMBER",Y5$(68)="DESCRIPTION",Y5$(97)="UNIT PRICE",Y5$(109)="UNIT",Y5$(119)="AMOUNT"
6130 GOSUB 7300
6199 RETURN 
6200 REM "Break #1 header
6202 IF C1$<>A$(1,10) THEN GOSUB 2000
6210 IF W+4>W0 THEN GOSUB 6000
6215 GOSUB 7300
6299 RETURN 
6300 REM 
6310 Y5$(1)="Invoice No.    Date     Order No    Customer P/O No.   Date Shipped   Shipped Via"
6315 GOSUB 7300
6320 CALL "ZZDISP","AX",A$(11,8),"AR6",X3$,X$,"",0,0,X4$
6330 Y5$(2)=X$,Y5$(13)=FND$(A$(23,6))
6340 CALL "ZZDISP","AX",A$(49,8),"O/P",X3$,X$,"",0,0,X4$
6350 Y5$(25)=X$,Y5$(37)=A$(34,15),Y5$(56)=FND$(A$(171,6)),Y5$(71)=A$(57,15)
6360 GOSUB 7300; GOSUB 7300
6390 RETURN 
6400 REM "Detail shipments
6405 X$=FNS$(C2$(11,20))
6410 E1$="Department: "+X$
6415 IF POS("Z"=V0$(59,12))=0 THEN IF TYPE$="Billing" THEN X$=""; GOTO 6430
6420 CALL "ZZDISP","AX",E$(129,9),"ZIP",X3$,X$,"",0,0,X4$
6425 IF POS("S"=V0$(59,12))<>0 THEN X$=E$(51,30); GOTO 6450 ELSE H$=FNS$(E$(111,16))+", "+E$(127,2)+"   "; IF POS("G"=V0$(59,12))>0 THEN X$=H$+E$(160,20) ELSE X$=H$+X$
6430 IF POS("Z"=V0$(59,12))=0 THEN E1$=E1$+" "+X$; Y5$(1)=E1$ ELSE E1$=E1$+" "+FNS$(E$(16,35)); Y5$(1)=E1$
6435 GOSUB 7300
6436 IF POS("Z"=V0$(59,12))=0 THEN Y5$(11)=E$(16,35); GOSUB 7300 ELSE GOSUB 7300
6440 GOSUB 7300
6445 RETURN 
6450 REM "Location heading for Summarized Shipment detail, don't gosub 7300, that will be done after filling in the totals
6455 J5$=E1$(11); REM "Pull off location code
6460 J6$=E$(16,35)+" "+X$; REM "Desc + city,state,zip
6495 RETURN 
7000 REM "Totals logic
7002 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(66)=T1$(1,14),Y5$(80)=T1$(1,12),Y5$(92)=T1$(1,12),Y5$(104)=T1$(1,14),Y5$(119)=T1$(1,14); GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7012 IF T=2 AND POS("T"=V0$(59,12))<>0 AND T$<>"" THEN Y5$(T0+2-T)="****LOCATION"+" "+E$(214,9)+" "+STR(T[T,0]:"0"); GOTO 7016
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" Total ("+STR(T[T,0]:"0")+"):"
7020 Y5$(66)=STR(T[T,1]:Z0$),Y5$(80)=STR(T[T,2]:Z1$),Y5$(92)=STR(T[T,3]:Z1$),Y5$(104)=STR(T[T,4]:Z0$),Y5$(119)=STR(T[T,5]:Z0$); GOSUB 7300
7065 IF T$="END" THEN Y5$(66)=T2$(1,14),Y5$(80)=T2$(1,12),Y5$(92)=T2$(1,12),Y5$(104)=T2$(1,14),Y5$(119)=T2$(1,14); GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed: "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 IF INT(T2)>0 THEN T4$=STR(INT(T2))+" Hours " ELSE T4$=""
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+STR(T3:"##.##")+" Minutes"
7260 Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7305 IF R6$(1,1)="Y" THEN CALL "ZR2DNL",ERR=7306,X3$,X4$,Y5$,R6$; REM "Send line to repository if needed.
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1 ! SSP 213924
7325 IF %IL_PDF THEN IF POS(" "<>Y5$)=0 THEN PRINT (%IL_PDF)"" ELSE PRINT (%IL_PDF)Y5$ ! SSP 213924
7330 REM 
7340 IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7505 REM "Next invoice number
7510 EXTRACT RECORD (Z[13],KEY=X3$(9,3)+"A/R")P9$
7515 X$=P9$(89,7)
7520 P0=NUM(P9$(89,7)),P0=P0+1; IF P0>9999999 THEN P0=1
7525 P9$(89,7)=STR(P0:"0000000"); WRITE RECORD (Z[13],KEY=X3$(9,3)+"A/R")P9$
7530 WRITE (Z[6],KEY=C1$)C1$,X$,A3[0],A3[1],A3[2]
7545 RETURN 
7550 REM "CREATE TEMP FILE FOR SUMMARY BILL NUMBERS
7560 Q$="TMP"+FID(0)
7565 ERASE Q$,ERR=7566; GOTO 7565
7566 IF ERR<>12 THEN GOTO 9000
7570 DIRECT Q$,10,200,64,0,0
7575 Y$="06O "+Q$; CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
7595 RETURN 
7600 REM "Print Invoice Cover Sheet
7610 IF POS("Z"=V0$(59,12))=0 THEN GOTO 7640
7615 DIM TERMS$(54); FIND (Z[8],KEY=C$(359,2),DOM=7640)TERMS$
7620 X$=PAD(X$,8)+TERMS$(3,30)
7640 CALL "AR2REZ",X3$,X4$,W9$,W,W9,W0,W2,C0$,C$,X$,V0$,A3[0],A3[1],A3[2]
7690 RETURN 
7700 REM "T type unit of measure
7710 PRECISION 5; Y5$(80)=STR(A[2]*.001:"###,###.00000"),Y5$(94)="EACH"; PRECISION 2
7740 RETURN 
7800 REM "Create M0$ unit of measure string set M0
7810 DIM M$(20); FIND (Z[13],KEY="U/M"+A$(133,4),DOM=7811)IOL=0390
7815 IF M$(20,1)="Y" THEN M0=M[0] ELSE M0=A[3]
7820 IF M0=0 THEN M0=1,M0$=""; GOTO 7840
7830 IF M$(20,1)<>"Y" THEN M0$=FNS$(A$(133,4))+"/"+FNR$(STR(M0:Z3$)) ELSE IF M[0]=0 THEN M0$=FNS$(A$(133,4)) ELSE M0$="",M0=1
7840 RETURN 
8000 REM "Sort by Department
8005 IF POS("R"=V0$(59,12))>0 THEN TYPE$="Receiving" ELSE IF POS("P"=V0$(59,12))>0 THEN TYPE$="Billing" ELSE TYPE$="Receiving"
8010 Y$="Monthly Shipment Detail by "+TYPE$+" Department"
8020 CALL "ZZ2SRT",X3$,X4$,Z[13],Z[1],50,Y$,Z,"YY"
8025 IF Z=2 THEN GOTO 9920
8030 Y$="02O "+Y$,Y9$=Y$
8040 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
8050 IF Z=0 THEN Q5=2,C1$=""; GOTO 0900
8100 REM "Set ups for Detail Shipment version
8110 Q5=0,V0=2,V1$="TMP"+FID(0)
8120 IF POS("S"=V0$(59,12))<>0 THEN X1$="Shipment Summary by "+TYPE$+" Department" ELSE X1$="Shipment Detail by "+TYPE$+" Department - * indicates prorated amount"
8140 GOTO 0881
8300 REM "EOJ for Ship to detail
8305 IF POS("Z"=V0$(59,12))>0 AND LEN(DEPT_SUMM$)>=68 THEN GOSUB 8400; REM "print dept summary page, ssp#83171
8310 IF V0$(72,1)<>"B" THEN GOTO 9900
8315 IF POS("V"=W8$)>0 THEN GOSUB 6000
8320 X1$="Summary Bill"
8330 Q5=0,V0=0,V1$="FMY..."
8340 T0$="",T1$="",C1$="",C2$=""
8350 DIM A2[2],A3[2]
8360 IF Y9$>"" THEN Y9$(3,1)="C"; CALL "ZZFLES",X3$,Y1$,Y0$,Y9$,Z{ALL},Z0,Z1
8365 GOSUB 7550
8390 GOTO 0900
8400 REM "Print department summary page, ssp#83171, using DEPT_SUMM$
8405 X1$=TYPE$+" Department Summary",PRINT_SUMM$="Y"
8410 GOSUB 6000
8415 DIM TERMS$(54); FIND (Z[8],KEY=C$(359,2),DOM=8425)TERMS$
8420 Y5$(20)="Terms: "+TERMS$(3,30); GOSUB 7300
8425 GOSUB 7300; Y5$(20)="Department Number",Y5$(50)="Department Name",Y5$(85)="Total Amount"; GOSUB 7300; GOSUB 7300; GOSUB 7300
8430 IF W+4>W0 THEN GOSUB 6000
8435 Y5$(20)=DEPT_SUMM$(1,20),Y5$(50)=DEPT_SUMM$(21,35),Y5$(85)=DEPT_SUMM$(56,13); GOSUB 7300
8440 IF LEN(DEPT_SUMM$)>=136 THEN DEPT_SUMM$=DEPT_SUMM$(69); GOTO 8430
8445 Y5$(85)="-------------"; GOSUB 7300; GOSUB 7300
8450 Y5$(50)="Total",Y5$(84)=STR(A3[2]+A3[0]+A3[1]:Z5$); GOSUB 7300
8455 Y5$(84)=T2$(1,14); GOSUB 7300
8485 PRINT_SUMM$="N"
8490 RETURN 
8915 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$)+POS("         "=Z9$))
8950 DEF FNQ$(Z9,Z9$)=STR(Z9:Z9$(1,POS(".00"=STR(Z9:Z9$)+".00")-1))
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9912 IF POS("M"=V0$(59,12))<>0 AND V3$(2,1)<>"T" THEN GOTO 5060; REM "Mark records with P and write summary bill no to AR6 if PENDING
9915 IF V0$(72,1)="B" AND T9=1 AND V3$(2,1)<>"T" AND POS("M"=V0$(59,12))=0 THEN GOTO 5300
9920 IF %HAS_IMAGE_LIB THEN GOSUB ADD_IL1 ! SSP 213924 Create Image Library entries
9925 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9927 ERASE "TMP"+FID(0),ERR=*NEXT ! SSP#222785          
9930 IF NOT(NUL(Y4$)) THEN CALL Y4$,ERR=*NEXT ELSE RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
15000 PUT_IN_IMAGE_LIBRARY:! If globals are set, then get correct directory and add to image_library, then clear the globals
15005 IF %HAS_IMAGE_LIB THEN {
15006 IMGKEY$=SUMM_CUST$+C1$ ! SSP 265203
15010 IMG_TYPE$=ITYPE$,IMG_REF$="",IMG_RPT$=IMGKEY$,IL2_CHAN=0; CALL "ILGAUD",ERR=*NEXT,X3$,X4$,IMG_TYPE$,IMG_RPT$ ! SSP 213924!SSP#258564
15015 RNAME$=%IL_FILE_NAME$,%IL_FILE_NAME$="",%PRINT_OPT$="" ! SSP 213924
15020 R1NAME$="",R2NAME$=""; FIND (Z[10],KEY=IMGKEY$,DOM=*NEXT)SUMM_CUST$,R1NAME$,R2NAME$,RINV$ ! SSP#258564 SSP265203
15025 IF ITYPE$="AB" THEN R1NAME$=RNAME$ ELSE R2NAME$=RNAME$
15030 ! F STP(IMG_INV$,2)<>"" THEN RINV$=IMG_INV$ ELSE RINV$="PENDING "
15040 WRITE (Z[10],KEY=IMGKEY$)SUMM_CUST$,R1NAME$,R2NAME$,RINV$ ! SSP#258564!SSP#258564  SSP 265203
15045 MNEMONIC (%IL_PDF)'F_'="Y"
15050  }
15095 RETURN 
15099 ! *************************************************************
15100 REM "CREATE TEMP FILE FOR IMAGE LIBRARY FILE NAMES BY CUSTOMER
15120 Q$="TMI"+FID(0)
15125 ERASE Q$,ERR=*NEXT; GOTO *SAME
15130 IF ERR<>12 THEN GOTO 9000
15140 DIRECT Q$,20,200,200,0,0 ! SSP 265203
15150 Y$="11O IL1...  10O "+Q$; CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
15190 RETURN 
15199 ! 
15200 ADD_IL1:! ssp 213924
15210 READ (Z[10],KEY="",DOM=*NEXT)
15220 K$=KEY(Z[10],END=15280); READ (Z[10],KEY=K$)SUMM_CUST$,R1NAME$,R2NAME$,RINV$
15225 IF STP(RINV$,2)<>"" THEN TF_REF_1$="",TF_REF_2$=SUMM_CUST$+RINV$ ! SSP 265203
15240 IF R1NAME$<>"" THEN IF STP(RINV$,2)="" THEN ERASE R1NAME$,ERR=*NEXT ELSE CALL "ILGFUN;ADD_IMAGE",ERR=*NEXT,Z[11],IMG_KEY$,"AB",TF_REF_1$,TF_REF_2$,R1NAME$ ! SSP 213924
15245 IF R2NAME$<>"" THEN IF STP(RINV$,2)="" THEN ERASE R2NAME$,ERR=*NEXT ELSE CALL "ILGFUN;ADD_IMAGE",ERR=*NEXT,Z[11],IMG_KEY$,"AC",TF_REF_1$,TF_REF_2$,R2NAME$ ! SSP 213924
15260 GOTO 15220
15290 RETURN 
15299 ! 
15300 UPD_IL_INV_NUM:! SSP 213924 Update IL temp file with summary bill number
15305 IF %HAS_IMAGE_LIB THEN {
15310 SUMM_CUST$=AR6$(1,10),ICUST$=AR6$(1,10); IF STP(MID(AR6$,26,10),2)<>"" THEN ICUST$=AR6$(26,10) ! SSP 265203
15315 IKEY$=SUMM_CUST$+ICUST$
15320 FIND (Z[10],KEY=IKEY$,DOM=*NEXT)SUMM_CUST$,R1NAME$,R2NAME$,RINV$ ! 265203
15330 IF STP(IMG_INV$,2)<>"" THEN RINV$=IMG_INV$ ELSE RINV$="" ! "Pending"
15340 WRITE (Z[10],KEY=IKEY$)SUMM_CUST$,R1NAME$,R2NAME$,RINV$ ! SSP#258564 SSP265203
15350  }
15390 RETURN 
56000 ! changes since 03/01/2006
56005 REM "190474-Additional Services - Enhance TF to provide for tax codes   
56008 REM "212145-Modify DDMS interface programs to recognize contract number
56009 REM "222785-Summary bill report leaves orphaned TMP files               
56010 REM "213924-Summary bill in Image Library - isn't automatic archive     
56011 REM "254728-They are getting an error when they answer the prompt to    
56013 REM "258520-Getting an error in summary bill. Clears the report and     
56015 REM "265203-Summary Bill images are not there anymore.                  
56016 REM "307474-DBD-441: Changes related to Cust PO for Summary Bill invoice
