0010 REM "Invoicing - Build invoice lines from order <AR2EAT>
0020 SETESC 9300; SETERR 9000
0035 REM "5.2 - 03/02/04 - 10.686666 - lms - SSP# 167214
0036 REM "MOD TO CORRECT V TYPE CUST REQ CHG
0037 REM "MOD LINE 1538, 1540 FOR UNDESIRED W CHARGE ON SALES ORDERS
0038 REM "Mod 5/25/93 for req charges cost codes in art(113,9)
0040 REM "Copyright 2004 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0085 SETERR 0090; ENTER X3$,X4$,Q0$,Q1$; L$="Y"; IF LEN(Q1$)=20 THEN Q1$=Q1$(1,14); GOTO 0100 ELSE GOTO 0100
0090 BEGIN 
0100 SETERR 9000
0110 X0$="AR2EAT",X1$="A/R Invoice Data Entry",K9$="",K9=0
0120 DIM H$(31),H[1],Y[4],A[20]
0140 Z6$="##,###,###.00-"
0145 IF Q1$="" THEN GOSUB 8950
0230 X2=-1
0235 IF L$="Y" THEN GOTO 0241
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20]
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19],B[20],B[21]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9]
0331 IOLIST C$,C[0],STR(C[1]),C[2],C[3],C[4],C[5],STR(C[6]),C[7],C[8],C[9]
0340 IOLIST H$,H[0],H[1],H[2],H[3],H[4],H[5],H[6],H[7],H[8],H[9],H[10],H[11],H[12],H[13],H[14],H[15]
0341 IOLIST D$
0350 IOLIST A9,E$,E[0],E[1],STR(E[2]),STR(E[3]),E[4],E[5],E[6],E[7],E[8],E[9],E[10],E[11],E[12],E[13],E[14],E[15],E[16],E[17],E[18],E[19],E[20]
0360 IOLIST F$
0365 IOLIST F$,F[0],F[1],F[2],F[3],F[4],F[5],F[6],F[7],F[8],F[9],F[10]
0370 IOLIST S$(1),S[0],S[1],S[2],S[3]
0390 IOLIST O$,O0
0400 IOLIST J$
0401 IOLIST J$,J[0],J[1],J[2],J[3],J[4],J[5],J[6],J[7],J[8],J[9],J[10],J[11],J[12],J[13],J[14],J[15],J[16],J[17],J[18],J[19],J[20]
0410 IOLIST I$,I[0],I[1],I[2],I[3],I[4],I[5],I[6],I[7],I[8],I[9],I[10],I[11],I[12],I[13],I[14],I[15],I[16],I[17],I[18],I[19],I[20]
0500 REM "FILES
0501 IF L$<>"Y" THEN PRINT @(25,22),"Defaulting invoice information",
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O ARB...  02O FS2...  03O FS3...  04O AR4...  05O ART...  06O FMP...  07O PO3...  08O ZE0... 09O FS1...  11O IC1...  12O IC0...  13O ZZPARM  "
0515 DIM P9[5]; CLOSE (14); OPEN (14)"ZZPARM"; FIND (14,KEY=X3$(9,3)+"F/M")P9$,*,*,P9[0],P9[1],P9[2],P9[3]; CLOSE (14); IF P9$(22,1)="Y" THEN Z$=Z$+"10O FW0...  " ELSE Z$=Z$+"10O FM1...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 F=Z[5],Y9=2
0550 DIM M6[1]; CALL "IC2PRE",X3$,Z[13],"",M6{ALL},"",0,"",0
0560 FIND (Z[13],KEY=X3$(9,3)+"F/M")P0$
0570 FIND (Z[13],KEY=X3$(9,3)+"AR1")P8$
0900 REM 
0910 EXTRACT (Z[1],KEY=Q1$,DOM=9900)IOL=0310
0920 IF A[20]>0 THEN GOSUB 8800
0960 DIM O$(168); FIND (Z[9],KEY=A$(92,8),DOM=0961)IOL=0390
1000 REM "Look for lines to default
1001 GOSUB 3700
1004 E0$=""; REM "Used to determine if multiple special shipping lines
1005 READ (Z[3],KEY=A$(7,8),DOM=1006)
1010 K$=KEY(Z[3],END=1100); READ (Z[3])
1015 IF K$(1,8)<>A$(7,8) THEN GOTO 1100
1018 DIM B$(204),B[21]; FIND (Z[2],KEY=K$(9,11),DOM=1019)IOL=0320
1020 GOSUB 1800
1040 IF K$(20,1)<>" " THEN A$(177,1)="Y"; GOSUB 2500
1090 GOTO 1010
1100 REM "Finish up
1104 GOSUB 7000
1106 GOSUB 1500
1107 GOSUB 3500
1108 GOSUB 3000
1110 A[20]=A0
1120 WRITE (Z[1],KEY=Q1$)IOL=0310
1125 EXTRACT (Z[1],KEY=Q1$,DOM=1126)
1190 GOTO 9700
1500 REM "CHECK FOR AUTO REQ CHARGES
1502 IF P9$(22,1)="Y" THEN RETURN 
1510 CALL "AR2SHC",X3$,X4$,"I"+A$,O$,R,R$,R{ALL}
1515 IF R=0 THEN GOTO 1595
1520 FOR I0=1 TO R
1523 R0$=R$((I0-1)*100+1,100)
1525 GOSUB 2100; DIM E$(289),E[20]; E$(6,1)="Y",E$(125,10)=A$(67,10); GOSUB 2900
1530 FOR I1=0 TO 10
1531 E[I1]=R[(I0-1)*11+I1]
1532 NEXT I1
1550 DIM H$(31),H[15]; FIND (Z[4],KEY=A$(128,5),DOM=1551)IOL=0340; IF H$(32,1)="I" THEN E$(84,5)=A$(128,5); GOTO 1560
1555 IF H$(1,5)<>E$(84,5) THEN DIM H$(31),H[15]; FIND (Z[4],KEY=E$(84,5),DOM=1565)IOL=0340
1560 GOSUB 8600
1565 IF E$(6,1)="N" THEN IF E[4]<>0 THEN E$(6,1)="Y"
1570 IF R0$(85,1)="Y" AND E[4]=0 THEN E$(6,1)="N"
1580 NEXT I0
1595 RETURN 
1800 REM "Default line
1810 DIM C[9],B[21],B$(204)
1820 EXTRACT (Z[3],KEY=K$)IOL=0330
1830 FIND (Z[2],KEY=K$(9,11),DOM=2090)IOL=0320
1835 IF B$(18,1)="Y" AND K$(20,1)=" " AND C[3]=0 THEN GOTO 2090
1840 GOSUB 2100
1845 IF K$(20,1)=" " THEN E0=B[0] ELSE IF E0$=B$ THEN E0=0 ELSE E0=B[0],E0$=B$
1850 IF B$(9,1)<>" " THEN IF POS(B$(5,1)="NECB")>0 THEN A$(354,40)=B$(19,10)+B$(50,40)
1860 DIM E$(289),E[20]; E$(6,1)="Y"
1870 E$(1,1)=B$(155,1),E$(3,3)=B$(29,3),E$(7,40)=B$(50,40),E$(47,3)=B$(6,3),E$(50,4)=B$(124,4),E$(54,1)=B$(136,1),E$(84,5)=B$(128,5),E$(89,4)=B$(10,4),E$(93,18)=B$(32,18),E$(125,10)=A$(67,10)
1880 DIM H$(32),H[15]; FIND (Z[4],KEY=A$(128,5),DOM=1881)IOL=0340; IF H$(32,1)="I" THEN E$(84,5)=A$(128,5)
1890 DIM F$(50); FIND (Z[6],KEY="T"+E$(3,3),DOM=1891)IOL=0360; IF LEN(F$)>36 AND F$(36,2)<>"  " THEN IF A$(342,1)=" " THEN A$(65,2)=F$(36,2)
1900 E$(75,9)=F$(24,9)
1910 IF B$(155,1)="C" THEN E$(55,20)=B$(161,10)+B$(19,10) ELSE IF B$(14,1)="Y" THEN E$(55,20)=A$(15,10)+B$(19,10) ELSE E$(65,10)=B$(19,10)
1920 E[0]=E0,E[1]=C[0],E[2]=C[1],E[4]=C[2],E[6]=B[5],E[5]=C[4],E[3]=C[6]; IF K$(20,1)<>" " THEN E0=0 ELSE IF B$(18,1)="Y" AND C[3]<>0 THEN E[0]=0,E[1]=0,E[4]=0,E[5]=0
1930 IF E[4]=0 AND POS("I"=B$(156,5))=0 OR (E$(7,1)="/" AND E$(1,1)="M") THEN E$(6,1)="N",E9$="N" ELSE E$(6,1)="Y",E9$="Y"
1940 IF B$(14,1)="Y" THEN E$(1,1)="F"
1945 IF B$(18,1)="Y" THEN GOSUB 7950; E$(125,10)=S$(269,10),E$(54,1)=S$(279,1),E$(135,1)=S$(10,1)
1950 IF B$(9,1)=" " THEN IF O$(43,1)="Y" AND B$(155,1)="N" OR (POS(B$(155,1)="IC")>0 AND B$(10,1)<>"9") THEN IF B$(2,1)<>"Y" THEN GOSUB 7600 ELSE GOSUB 8100
1955 IF P0$(219,1)="Y" AND B$(9,1)<>" " AND POS(B$(155,1)="IC")>0 THEN GOSUB 8100
1960 IF B$(155,1)="S" THEN GOSUB 5000; IF POS(B$(6,3)=Q7$(11),13)>0 THEN GOSUB 3750
1970 IF H$(1,5)<>E$(84,5) THEN DIM H$(31),H[15]; FIND (Z[4],KEY=E$(84,5),DOM=1971)IOL=0340
1980 REM "IF POS(X3$(9,3)="020",3)>0 AND B$(9,1)<>" " THEN CALL "AR2EAX",X3$,B$,E[5],D0; E[8]=E[8]+D0
1990 IF P9$(147,1)="Y" AND B$(135,1)<>" " AND B[11]<>0 THEN GOSUB 7800
2000 IF POS(E$(1,1)="IC")>0 AND B$(9,1)=" " THEN E$(112,4)="0000"; IF B[12]<>0 THEN I=B[0]/B[12]+SGN(FPT(B[0]/B[12])); E$(112,4)=STR(I:"0000"); REM "used by house charge within AR2COM - Blank means not coming out of whse
2010 IF P0$(171,1)="Y" AND POS(E$(1,1)="C")>0 AND E$(55,10)<>P0$(172,10) THEN E[8]=E[8]+E[5]; GOSUB 8150
2020 GOSUB 8600
2030 B6=E[6]
2040 IF E$(1,1)<>"M" THEN IF B$(155,1)=" " THEN IF B$(5,1)="N" THEN E$(111,1)="N" ELSE E$(111,1)="R"
2050 IF E[4]<>0 THEN E$(6,1)="Y"
2060 IF POS(" "<>A$(325,12))=0 THEN A$(325,12)=B$(171,12) ELSE IF POS(" "<>B$(171,12))>0 THEN GOSUB 4500
2070 IF C[3]<>0 THEN GOSUB 4400
2080 IF O$(43,1)="Y" THEN GOSUB 7500
2090 C$(40,6)=A$(180,6); WRITE (Z[3])IOL=0331
2095 RETURN 
2100 REM "Write out prev line
2110 GOSUB 5200
2115 GOSUB 3000
2120 A=A9,A9=0
2140 RETURN 
2500 REM "Set up message line for special shipping
2502 IF P9$(154,1)="S" THEN GOTO 2590
2510 GOSUB 2100
2512 READ (Z[3],KEY=K$)IOL=0330
2515 GOSUB 8000
2519 CALL "ZZDISP","AX",S$(124,9),"ZIP",X3$,Q9$,"",0,0,X4$
2520 D9$=FNS$(S$(106,16))+", "+S$(122,2)
2525 IF S$(11,1)="D" THEN D9$="At our warehouse in "+D9$ ELSE IF S$(12,4)="    " THEN D9$="To: "+D9$+" "+FNS$(Q9$) ELSE D9$="To: "+D9$+" "+FNS$(Q9$)+" ("+S$(12,4)+") "
2527 IF P9$(154,1)="Y" AND S$(11,1)="C" THEN IF S$(12,4)="    " THEN D9$="Shipped to Sold To Address above" ELSE GOSUB 7900; GOTO 2590
2530 REM "B6 leftover from doing line above
2535 E$(7,40)=D9$,E[1]=0,E[6]=0,E$(47,3)="   "
2590 RETURN 
2900 REM "LOAD E$ FROM SPECIAL CHG CODE
2910 READ (Z[6],KEY="X"+R0$(12,10),DOM=2980)J9$
2915 E$(7,40)=R0$(22,40),E$(50,4)=J9$(55,4),E$(54,1)="N"
2920 E$(3,3)=J9$(52,3),E$(2,1)=J9$(60,1)
2921 IF POS(" "<>R0$(70,5))>0 THEN E$(84,5)=R0$(70,5) ELSE E$(84,5)=A$(128,5)
2922 E$(54,1)=O$(86,1); IF E$(84,5)="     " THEN E$(84,5)=P8$(57,5)
2923 IF LEN(R0$)>75 THEN E$(116,9)=R0$(75,9)
2925 E$(1,1)="S",E$(65,10)=R0$(12,10),E$(6,1)="Y"; IF R0$(85,1)="Y" THEN E$(6,1)="N"
2930 DIM J9$(50); READ (Z[6],DOM=2931,KEY="T"+E$(3,3))J9$
2935 E$(75,9)=J9$(24,9); IF J9$(23,1)="N" THEN E$(54,1)="N"
2985 RETURN 
3000 REM "Write out line to index A, A9 is assumed to point to next line
3005 IF A<=0 THEN GOTO 3090
3008 IF POS(" "<>E$(125,10))=0 THEN E$(125,10)=A$(67,10); IF A$(115,1)="Y" THEN E$(54,1)="N" ELSE E$(54,1)="Y"
3010 WRITE (F,IND=A)IOL=0350
3090 RETURN 
3500 REM "ADD EXTERNAL CHGS NOT ALREADY ON ORDER LINES
3505 DIM F[10]
3530 IF Q7$="" THEN GOTO 3680
3539 REM "LINE SHOULD BE THERE, BUT ERROR 11 IS NO BIG DEAL...
3540 READ (Z[8],KEY=A$(92,8)+Q7$(1,13))IOL=0365
3549 REM "ALREADY BILLED COMPLETELY?
3550 IF F[1]=F[3] AND F[2]=F[4] THEN GOTO 3680
3555 GOSUB 2100
3560 GOSUB 3570; Q7$=Q7$(14); GOTO 3530
3570 REM "SETUP FILE "ART" LINE
3580 DIM E$(289),R0$(84),E[20],R[3]; E$(6,1)="Y",E$(111,1)="E"
3585 R0$(12,10)=F$(9,10),R0$(22,40)=F$(65,40)
3590 GOSUB 2900
3594 E$(47,3)=Q7$(11,3)
3595 E[0]=1,E[1]=1,E[2]=F[2]-F[4],E[4]=E[2]
3596 E[3]=F[1]-F[3],E[5]=E[3]
3598 IF X3$(9,3)="080" THEN IF E[4]=0 THEN E$(6,1)="N"
3600 IF H$(1,5)<>E$(84,5) THEN DIM H$(31),H[15]; FIND (Z[4],KEY=E$(84,5),DOM=1750)IOL=0340
3615 GOSUB 8600
3670 RETURN 
3680 REM "END OF CHARGES
3695 RETURN 
3700 REM "GET LIST OF EXTERNAL CHG LINES
3710 Q7=0,Q7$=""; READ (Z[8],KEY=A$(92,8),DOM=3711)
3715 READ (Z[8],END=3720)F$; IF F$(1,8)=A$(92,8) THEN Q7=Q7+1; Q7$=Q7$+F$(9,13); GOTO 3715
3745 RETURN 
3750 REM "GET COST/BILL AMT FROM EXTERNAL FILE FOR PARTICULAR LINE
3760 DIM F[10]; READ (Z[8],KEY=A$(92,8)+B$(19,10)+B$(6,3),DOM=3790)IOL=0365; IF F[1]=F[3] AND F[2]=F[4] THEN GOTO 3790
3770 GOSUB 3570
3775 E$(47,3)=B$(6,3)
3780 Q=POS(B$(6,3)=Q7$(11),13); Q7$=Q7$(1,Q-1)+Q7$(Q+13)
3795 RETURN 
4400 REM "Setup memo line if backordered amount
4405 IF P9$(158,1)="Y" THEN GOTO 4440
4410 GOSUB 2100
4415 DIM E$(289),E[20]; E$(6,1)="Y",E$(1,1)="M"
4420 E$(7,40)="Backordered",E[1]=C[3],E[6]=B6,E$(47,3)=""
4440 RETURN 
4500 REM "Job no 2+ line - print on stub"
4510 GOSUB 2100
4515 DIM E$(289),E[20]; E$(6,1)="N",E$(1,1)="M"
4520 E$(7,40)="Job no:"+B$(171,12),E$(47,3)=""
4540 RETURN 
4900 REM "END OF LINES
4910 Y[4]=-1; WRITE (F,IND=0)IOL=0290
4920 CALL "ZZEXPF",X3$,X4$,"X"+STR(F),"",G9; IF G9>0 THEN GOTO 5230
4925 EXTRACT (F,IND=0)IOL=0290
4930 Z=NUM(FIN(F,"MAXREC")); Y[3]=Z-1
4935 Y[4]=-2; WRITE (F,IND=0)IOL=0290
4980 GOTO 5230
5000 REM "Check special to see if costed freight
5010 READ (Z[6],KEY="X"+B$(19,10),DOM=5040)J9$
5015 IF J9$(60,1)="Y" THEN E$(2,1)="Y"; IF F$(23,1)="N" THEN E$(54,1)="N" ELSE E$(54,1)="F"
5040 RETURN 
5200 REM "GET NEXT INDEX TO FILE IN A9
5210 EXTRACT (F,IND=0,ERR=5295)IOL=0290
5215 IF Y[4]=-2 THEN READ (F); WAIT 0; GOTO 5210
5220 Y[4]=-2; WRITE (F,IND=0)IOL=0290
5230 IF Y[1]<1 THEN A9=Y[2],Y[2]=Y[2]+1; GOTO 5260
5255 A9=Y[1]; READ (F,IND=A9)Y[1]
5260 IF Y[0]<Y[3]-1 THEN Y[0]=Y[0]+1,Y[4]=-1 ELSE Y[4]=-1; GOTO 4900
5280 WRITE (F,IND=0)IOL=0290
5285 IF A0=0 THEN A0=A9
5290 RETURN 
5295 IF ERR<>0 THEN GOTO 9000 ELSE RETRY 
5300 REM "REMOVE INDEX Z
5310 EXTRACT (F,IND=0)IOL=0290
5315 IF Y[4]=-2 THEN READ (F); WAIT 0; GOTO 5310 ELSE Y[4]=-2
5320 WRITE (F,IND=0)IOL=0290
5330 WRITE (F,IND=Z)Y[1]
5340 Y[1]=Z,Y[4]=-1,Y[0]=Y[0]-1
5350 WRITE (F,IND=0)IOL=0290
5390 RETURN 
5395 ON ERR GOTO 5310,5311,9000
7000 REM "Check for deposit amount
7010 IF POS(" "<>O$(64,6))=0 THEN GOTO 7090
7020 GOSUB 2100
7025 DIM E$(289),E[20]; E$(6,1)="Y",E$(1,1)="M"
7030 CALL "ZZDISP","DX",O$(70,6),"",X3$,Q9$,"",0,0,X4$
7035 Q8$=STR(O0:"$###,###.00")
7040 Q8$=Q8$(POS(" "<>Q8$))
7050 E$(7,40)="When paying, deduct your deposit of",E$(47,3)="   "; IF POS(X3$(9,3)="099",3)>0 THEN E$(7,40)="Thank you for your payment of"
7055 GOSUB 2100
7060 E$(7,40)=Q8$+"  check #: "+FNS$(O$(64,6))+"  "+Q9$,E$(47,3)="   "
7090 RETURN 
7500 REM "ENDING#
7505 IF POS(B$(155,1)="INC")=0 THEN RETURN 
7510 FIND (Z[10],KEY=A$(15,10)+B$(161,10)+B$(19,10)+B$(147,8),DOM=7599)IOL=0400
7515 IF POS(" "<>J$(135,9))=0 THEN GOTO 7599
7530 PRINT @(0,6),'CE',@(0,10),"Item: ",B$(19,10),'LF',"Desc: ",B$(50,40),'LF',"Starting number: ",J$(135,9)
7540 EXTRACT (Z[2],KEY=K$(9,11))IOL=0320
7550 PRINT @(0,13),"Ending number: ",
7560 CALL "ZZENTR","RZ",A{ALL},B$,X4$,X3$,17,13,189,9,C0,"","","","AR2EAT42","","",""
7565 X$=""; IF J$(135,9)>B$(189,9) THEN X$="Caution - Ending number less than Starting number"
7570 CALL "ZZPROM","A",X3$,Z,X$,"","",0; ON Z GOTO 7560,7571
7575 WRITE (Z[2])IOL=0320
7580 GOSUB 2100
7585 DIM E$(289),E[20]; E$(6,1)="Y",E$(1,1)="M",E$(7,40)="Starting# "+FNR$(J$(135,9))+"  Ending# "+FNR$(B$(189,9))
7599 RETURN 
7600 REM "Revise average cost at time of shipment
7605 I0=0,I1=0,I2=0; DIM I[20]
7610 IF O$(43,1)="Y" AND POS(B$(155,1)="N")>0 THEN I1=C[4],C[5]=B[15]*C[0]/1000; GOTO 7770
7620 IF POS(B$(155,1)="IC")=0 OR B$(10,1)="9" OR C$(39,1)="Y" THEN GOTO 7790
7630 DIM C0$(10); IF B$(155,1)="C" THEN C0$=B$(161,10)
7635 FIND (Z[12],DOM=7700,KEY=C0$+B$(19,10))IOL=0410; I0=I[18]
7640 FIND (Z[11],DOM=7641,KEY=C0$+B$(19,10)+B$(10,4))*,*,*,I[18]
7645 IF I[18]<>0 THEN I0=I[18]
7650 IF I0=0 THEN I0=B[1]; CALL "FM2EXT",Z[13],0,B$(120,4),B[2],C[0],I0,I1,0 ELSE CALL "FM2EXT",Z[13],0,I$(124,4),I[15],C[0],I0,I1,0
7700 REM "W/O Adjustment
7710 IF O$(43,1)<>"Y" THEN GOTO 7770
7715 CALL "FW2BOM",X3$,X4$,O$(118,8)+B$(6,3),O$(6,10)+C0$+B$(19,10),C[0],I1
7720 CALL "FM2EXT",Z[13],1,"M   ",1000,C[0],I0,I1,2
7730 IF O$(44,1)="Y" THEN C[5]=1.05*C[0]*I0/1000; GOTO 7770
7740 P9[4]=P9[0],P9[5]=P9[1]; IF ABS(C[0])<P9[3] THEN P9[4]=P9[4]+P9[2],P9[5]=0
7750 P9[5]=P9[5]+I0
7755 I1=1.05*C[0]*P9[5]/1000+P9[4],C[5]=1.05*C[0]*I0/1000
7770 CALL "FM2EXT",Z[13],1,B$(124,4),B[5],C[0],I0,I1,M6[1]
7780 C[4]=I1,C[6]=I0
7785 E[3]=I0,E[5]=I1
7787 I3=0; IF POS(" "<>I$(168,20))>0 THEN GOSUB 7850 ELSE I3=I[20]
7788 IF I3<>0 THEN CALL "FM2EXT",Z[13],0,I$(124,4),I[15],C[0],I3,I2,0; E[8]=E[8]+I1-I2
7790 RETURN 
7800 REM "FINDERS FEE
7810 ON POS(B$(135,1)="SCGF") GOTO 7845,7820,7830,7840,7842
7820 E[8]=E[8]-E[4]*B[11]*.01; GOTO 7845
7830 E[8]=E[8]-E[5]*B[11]*.01; GOTO 7845
7840 E[8]=E[8]-(E[4]-E[5])*.01; GOTO 7845
7842 IF POS(B$(6,3)=L9$)=0 THEN E[8]=E[8]-B[11],L9$=L9$+B$(6,3); GOTO 7845
7845 RETURN 
7850 REM "FEATURE DRIVEN PRICING - INVENTORY
7860 X$=I$(82,10)+I$(168,20)
7870 CALL "FM2OB9",X3$,X$,B$(124,4),B[5],B$(124,4),B[5],C[0],I3,0,M6[1]
7880 IF I3=0 THEN I3=I[20]
7895 RETURN 
7900 REM "Use Full Special Ship-to address"
7910 E$(7,40)="To: "+S$(225,35),E[1]=0,E[6]=0; GOSUB 2100; GOSUB 8000
7920 IF POS(" "<>S$(16,30))>0 THEN E$(11)="Attn: "+S$(16,30); GOSUB 2100; GOSUB 8000
7930 IF POS(" "<>S$(46,30))>0 THEN E$(11)=S$(46,30); GOSUB 2100; GOSUB 8000
7940 E$(11,36)=D9$(5)
7945 RETURN 
7950 REM "Get PO3 data for special shipping
7960 DIM S$(299),S[3]
7970 READ (Z[7],KEY=K$(9,8)+" "+K$(17,4),DOM=7971)IOL=0370
7990 RETURN 
8000 REM 
8010 DIM E$(289),E[20]; E$(1,1)="M"
8020 E$(6,1)=E9$
8040 RETURN 
8100 REM "Lot - Salesperson Cost
8110 I0=0,I1=0,I2=0; DIM I[20]
8120 DIM C0$(10); IF B$(155,1)="C" THEN C0$=B$(161,10)
8130 FIND (Z[12],DOM=8140,KEY=C0$+B$(19,10))IOL=0410; I3=I[20],I1=E[5]
8135 GOSUB 7788
8140 RETURN 
8150 REM "Exculde whse from setting GP adjustment to cost
8155 CLOSE (14)
8160 OPEN (14,ERR=8190)"FM0"+X3$(9,3)
8165 DIM X$(15); X$(1,1)="D",X$(12,4)=E$(89,4)
8170 READ (14,KEY=X$,DOM=8190)X$
8175 IF X$(235,1)="Y" THEN E[8]=E[8]-E[5]
8190 CLOSE (14)
8195 RETURN 
8600 REM "Calculate commission
8605 IF H$(32,1)<>"L" THEN E[7]=0; GOTO 8640
8607 IF H$(31,1)="T" AND B$(10,1)<>"9" THEN IF POS(E$(1,1)="IC")>0 THEN GOSUB 8650 ELSE GOSUB 8680
8609 IF E$(1,1)<>"M" THEN GOSUB 8700
8611 X$=" "+E$(112,4)+"[X3$B]"+X3$+"[X3$E]"+"[A$]"+A$+"[B$]"+E$
8613 CALL "AR2COM",ERR=8614,X$,H$,H{ALL},Q,E[4],E[5]-E[8]
8615 E[7]=Q
8640 RETURN 
8650 REM "T-Type Comm"
8655 E9=I[0]; Q=E[1]; IF E[0]>Q THEN Q=E[0]
8660 FOR I=1 TO 11 STEP 2
8665 IF I[I]=0 OR Q<I[I] THEN I=99; GOTO 8675
8670 E9=I[I+1]
8675 NEXT I
8680 Q=0; IF POS(E$(1,1)="IC")>0 THEN CALL "FM2EXT",Z[13],0,I$(120,4),I[14],E[1],E9,Q,2 ELSE Q=(E[5]-E[8])/.6
8690 RETURN 
8700 REM "If sub mask # in AR1 parameter do substitution
8710 IF B$(9,1)<>" " THEN I=1 ELSE I=POS(B$(155,1)="ICNS")+1; X$=P8$(32+I*5,5)
8712 IF POS(X3$(9,3)="136148",3)>0 THEN IF B$(9,1)<>" " AND POS(B$(155,1)="I")>0 THEN I=1
8715 X$=P8$(32+I*5,5),F9$=E$(84,5); IF POS("#"=X$)=0 THEN GOTO 8740
8720 FOR I=1 TO 5
8725 IF X$(I,1)<>"#" THEN F9$(I,1)=X$(I,1)
8730 NEXT I
8735 FIND (Z[4],KEY=F9$,DOM=8736)IOL=0340; E$(84,5)=F9$
8740 RETURN 
8800 REM "DELETE CURRENT LIST
8805 Z=A[20]
8815 IF Z<=0 THEN GOTO 8840 ELSE READ (F,IND=Z)Z9
8820 GOSUB 5300
8830 Z=Z9; GOTO 8815
8840 A[20]=0; RETURN 
8900 REM "Functions
8910 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
8915 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$)+POS("         "=Z9$))
8950 REM "Program flow logic
8960 DIM U0$(6); U0$(1)=FID(0),U0$=U0$+"PF"; CLOSE (14); OPEN (14)"ZZPARM"
8970 FIND (14,KEY=U0$,DOM=8990)U0$,U1$
8975 IF U1$="" THEN RETURN 
8980 Q1=NUM(U1$(7,2)),Q1$=U1$(9,Q1),U1$=U1$(Q1+9)
8984 IF LEN(Q1$)=20 THEN Q1$=Q1$(1,14)
8990 RETURN 
9000 REM "ERROR PROCESSING
9001 ESCAPE 
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9700 REM "PROG FLOW
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9725 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9730 RUN U1$(1,6)
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 IF L$="Y" THEN SETERR 9921; EXIT 
9950 RUN "AR2EAA"
9999 END 
