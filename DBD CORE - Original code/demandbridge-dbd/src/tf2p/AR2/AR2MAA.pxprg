0010 REM "Customer Setup/Inquiry <AR2MAA>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 11/13/16 - 20.043055 - crg - SSP# 287873
0036 REM "MOD TO LINE 2065 FOR ASSISTANT FEATURE 10/14/91 JSC
0037 REM "287873-Customer search by order zip code. Display order number/date
0040 REM "Copyright 2016 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 SETERR 0060; ENTER X3$,X4$,Q0$,A1$; X9=1; GOTO 0105
0090 ON L0 GOTO 0100,1900
0100 BEGIN 
0105 SETERR 9000; SETESC 9300
0110 X0$="AR2MAA",X1$="Customer Setup/Inquiry",K9$="",K9=0
0115 IF X9=1 THEN X1$=X1$+" (ASSISTANT)",F9$="Y"
0120 DIM Z0$(80,"-")
0130 K0=10,K1=1
0135 C9=-1,V9=-2
0145 GOSUB 8950
0200 REM "
0230 IF Q1$<>"" THEN X2=-1; PRINT @(0,6),'CE',
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1,LEN(A$)),A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15]
0320 IOLIST B$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X AR1...  02O ARE...  03O ARH...  04O ARU...  05O AS5...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0525 IF RETURN$="Y" THEN RETURN ; REM "Coming back from AR2UAB, SSP122424
0530 DIM SEC$(255); READ (Z[13],KEY="osec"+X3$(40,3),DOM=0531)SEC$
0585 W9$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,W9$
0600 REM "
0620 REM 
0700 REM "Read params"
0710 FIND (Z[13],KEY=X3$(9,3)+"A/R",DOM=9800)P0$
0720 DIM P1$(22); FIND (Z[13],KEY=X3$(9,3)+"F/M",DOM=0721)P1$
0900 REM 
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1035 DIM A$(599),B$(100),A[15]
1040 IF V9<>-1 THEN GOSUB 6900
1045 A$(1,0+K9)=K9$+A1$
1050 REM IF Q1$<>"" THEN LET A$(1)=Q1$; GOTO 1500
1060 GOSUB 6600
1070 REM 
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9700
1140 ON X0 GOTO 1200,1500
1200 REM "Customer code
1202 REM LET K$=""; CALL "AR2KEY",X3$,"",6,9,12,10,Z(2),"",K$,18,3,65,Z(1); IF K$>"" THEN LET A$(1,10)=K$(11); GOSUB 06450; LET C0=1; GOTO 01211
1205 IF Q1$>"" THEN A$(1,10)=Q1$; GOTO 1211
1208 X$="Lookup Cust: F7=Order F8=Invoice F9=Cust ZipCode F10=Phone No. F6=Order ZipCode"
1210 CALL "ZZENTR","AXUF",A{ALL},A$,X4$,X3$,18,3+V0,1+K9,10,C0,"A/R","{1"+X$,"","AR2MAA00","AR1","",""; X$=""; IF C0<>-7 AND C0<>-16 AND C0<>-17 AND C0<>-18 AND C0<>-15 THEN IF C0<-10 THEN GOTO 1210 ELSE IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1211 IF C0=-17 THEN C0=1; CALL "AR2INV",X3$,X4$,A$,1,X$; IF X$="" THEN GOTO 1200
1212 IF C0=-7 THEN C0=1; CALL "ZZ2PLU",X3$,A$,1,X$; IF X$="" THEN GOTO 1200
1213 IF C0=-18 THEN C0=1; CALL "AR2LST",X3$,X4$,A$,1,X$; IF X$="" THEN GOSUB 6400; GOTO 1200
1214 IF C0=-16 THEN C0=1; CALL "AR2OR0",X3$,X4$,A$,1,X$; IF X$="" THEN GOTO 1200
1215 IF C0=-15 THEN C0=1; GOSUB LOOKUP_F6; IF NUL(RETVAL$) THEN GOTO 1200
1219 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9700,6970
1220 IF A$(1,2)<>"00" THEN READ (Z[3],KEY=A$(1,2),DOM=1225); GOTO 1230 ELSE GOTO 1230
1225 CALL "ZZPROM",".4",X3$,S3,"Division "+A$(1,2)+" is not on file.","","",0; GOTO 1200
1240 GOTO 1120
1500 REM "
1505 Z9=-1
1510 A1$=K9$+A$(LEN(K9$)+1,10),A$(1,LEN(K9$))=K9$,NEW_FLAG$=FID(0)+X0$+"NEW"+A1$
1515 A2$="",A3$=""
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1522 GOSUB VALIDATE_REP; IF NOT(VAL_REP) THEN CALL "ZZPROM",".4",X3$,0,"Access denied! You may not view this customer","","",0; GOTO 1000
1525 Z9=0
1530 FIND (Z[1],KEY=A$(46+K9,10),DOM=1531)IOL=0320; READ (Z[1],KEY=A1$)
1545 A2$=A$(155+K9,10)+A$(1+K9,10)
1550 A3$=A$(278+K9,4)+A$(1+K9,10)
1580 GOTO 1870
1600 REM "
1610 IF X9=1 THEN CALL "ZZPROM",".4",X3$,0,"Set up not allowed using the ASSISTANT feature!","","",0; GOTO 1000
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630,6975
1625 IF LEN(U1$)>6 THEN IF U1$(1,6)="FM2ODB" THEN Q1$=""; GOTO 1000
1630 IF Q1$>"" AND U1$>"" THEN GOTO 9700 ELSE Q1$=""; GOTO 1000
1650 IF SEC$(44,1)="Y" OR SEC$(57,1)="Y" THEN CALL "ZZPROM",".4",X3$,0,"Access denied! You may not set up a new customer","","",0; GOTO 1000
1655 FIND (Z[13],KEY="DFLT"+X0$,DOM=1656)IOL=0310; GOSUB 6200
1660 IF V9>=0 THEN GOSUB 6200
1665 IF X3$(9,3)="566" THEN CALL "RECVER_566",A1$,H566$; IF H566$="4" THEN GOTO 1000 ! SSP227115
1670 WRITE (Z[13],KEY=NEW_FLAG$(1,11))NEW_FLAG$
1680 GOTO 1900
1690 GOTO 2240
1699 GOTO 2240
1700 REM "
1790 GOSUB 6200
1870 GOSUB 6600; GOSUB 6200
1880 IF F9$="Y" THEN GOSUB 7700
1890 GOTO 2000
1900 REM "Do first time stuff"
1910 U0$=X3$(1,6)+"PF",Q$=STR(LEN(A1$)+1:"00")+A1$+"1"; IF POS(" "<>P1$)=0 THEN U1$="AR2MAB"+Q$+"AR2MAC"+Q$+X0$+Q$+U1$ ELSE U1$="AR2MAB"+Q$+"AR2MAC"+Q$+"FM2SHA"+Q$+X0$+Q$+U1$
1920 WRITE (Z[13],KEY=U0$)U0$,U1$
1930 SETERR 1931; RUN "AR2MAB"
1935 SETERR 9000
2000 REM "
2010 P0=21,P1=6,P=0
2020 PRINT @(0,P1),'CE',
2022 CALL "ZZVDTP",X3$,X0$,X1$,"",0; PRINT 'SF',
2025 IF P1$(22,1)="Y" THEN P1=P1-1
2030 W8$="Y",Q$="Customer Constant Data",P$="CAR2MAB"; GOSUB 3000
2035 W8$="Y",Q$="Credit Analysis",P$="CAR2MAC"; GOSUB 3000
2040 W8$="Y",Q$="Open Invoice Inquiry",P$="RAR2MAD"; IF X9=0 THEN GOSUB 3000 ELSE P$(1,1)="C"; GOSUB 3000
2045 W8$="Y",Q$="Invoice History Inquiry",P$="RAR2MAH"; IF X9=0 THEN GOSUB 3000 ELSE P$(1,1)="C"; GOSUB 3000
2050 W8$="Y",Q$="Statistical Data Inquiry",P$="CZZ2MDA"; GOSUB 3000
2055 IF POS(" "<>P1$)=0 THEN GOTO 2095
2060 W8$="Y",Q$="Custom Item Setup/Inquiry"; IF X9=0 THEN P$="RFM2IAZ"; GOSUB 3000 ELSE P$=" NA    "; GOSUB 3000
2065 W8$="N",Q$="Open Order Inquiry"; IF X9=0 THEN P$="RFM2ODI"; GOSUB 3000 ELSE P$="CFM2ODI"; GOSUB 3000
2070 W8$="Y",Q$="Order History by Order No."; IF X9=0 THEN P$="RAR2MAO"; GOSUB 3000 ELSE P$="CAR2MAO"; GOSUB 3000
2076 W8$="Y",Q$="Order History by Item",P$="RFM2IAH"; IF X9=1 THEN P$(1,1)="C"; GOSUB 3000 ELSE GOSUB 3000
2078 IF X9=1 THEN GOTO 2100
2079 W8$="N",Q$="Customer Default Data",P$="RFM2SHA"; GOSUB 3000
2080 W8$="N",Q$="Ship-To Location Setup",P$="RZZ2STP"; GOSUB 3000
2081 W8$="N",Q$="Item Mgt Period Ending Dates",P$="RFM2FYE"; GOSUB 3000
2082 W8$="N",Q$="Customer Message Setup",P$="CAR2MAE"; GOSUB 3000
2083 W8$="Y",Q$="Invoiced Order History by Date",P$="RFM2IAL"; GOSUB 3000
2084 IF P0$(114,1)="Y" THEN W8$="N",Q$="Comment Printing Setup",P$="CAR2MAU"; GOSUB 3000
2092 IF P1$(22,1)<>"Y" THEN GOTO 2095
2093 W8$="N",Q$="Production Work Order Setup",P$="RFW2MAA"; GOSUB 3000
2094 W8$="N",Q$="Production Order Tracking",P$="RFW2MBA"; GOSUB 3000
2095 W8$="N",Q$="Requisition/Release Entry (O/P-D1)",P$="RFS2DAX"; GOSUB 3000; W8$="N",Q$="Order Entry (O/P-O1)",P$="RFM2ODX"; GOSUB 3000; W8$="N",Q$="Contact Management Contact Setup ",P$="RZP2MEA"; GOSUB 3000; REM "Mod cwj 3/5/98 SSP# 087750 chg to DAX from DAA, ODX from ODM.!SSP#230604
2096 W8$="Y",Q$="Credit Card Information Setup",P$="CAR2MAV"; IF SEC$(61,1)="Y" THEN GOSUB 3000 ELSE P$=" NA    "; GOSUB 3000; REM " 96622 - DXM
2097 W8$="N",Q$="Special Pricing",P$="CSPGSP0"; GOSUB 3000
2099 W8$="N",Q$="Change Customer Status/Delete",P$="PDELETE"; GOSUB 3000 ! ssp#240337
2100 REM "
2120 CALL "ZZPROM","M0",X3$,Z,"","","",22
2122 DIM Q$(2)
2125 CALL "ZZENTR","ZX",A{ALL},Q$,X4$,X3$,60,22,1,2,C0,"",X$,"","AR2MAA10","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2125,2126
2126 F9$="Y"
2127 Q0=NUM(Q$,ERR=2125); PRINT @(18,22),'CL',
2130 REM IF C0<0 THEN LET Q1$=""; GOTO 1000
2131 IF C0<0 THEN READ (Z[13],KEY=NEW_FLAG$(1,11),DOM=1625)NEW_FLAG$; IF NEW_FLAG$(12,10)<>A1$ THEN GOTO 1625 ELSE GOTO 4000
2132 IF Q0=77 AND X3$(43,2)="SU" THEN CALL "AR2CHG",X3$,X4$,A1$,Q$; A1$=Q$,A$(1,10)=Q$; GOTO 1500
2134 IF Q0<1 OR Q0>P THEN GOTO 2120
2135 IF Q0<1 THEN GOTO 2120
2165 Q$=P8$((Q0-1)*6+1,6)
2170 IF POS("z"=A$(1,10))>0 THEN IF Q$<>"ZZ2MDA" THEN Q$="NA"
2215 IF Q$(1,2)="NA" THEN CALL "ZZPROM","4",X3$,Q1,"Option "+STR(Q0)+" is unavailable","","",0; GOTO 2120
2245 IF Q$="DELETE" THEN GOTO 6500
2250 IF Q$="ZZ2MDA" THEN CALL "ZZ2MDA",X3$,X4$,"A/R",A1$,Y0$; CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,-1; PRINT @(0,3),'CE',; GOSUB 6600; GOSUB 6200; GOTO 2000
2265 IF P9$(Q0,1)="R" THEN GOTO 3100
2275 IF Q$="SPGSP0" THEN CALL Q$,X3$,X4$,A1$,"","","C" ELSE CALL Q$,ERR=2280,X3$,X4$,Q0$,A1$
2276 A1$=A1$(1,10); GOSUB 6400; REM "SSP#196865 !SSP#230573
2285 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,-1
2287 IF POS(Q$="AR2MABAR2MAEAR2MAU",6)>0 THEN GOSUB 6400
2290 GOTO 2000
3000 REM "print menu line
3005 P9=11,P=P+1; REM "96622 - DXM
3006 W8=0; IF W9$>"" THEN IF W9$(7,1)<>"H" THEN IF W8$<>"Y" THEN W8=1; REM "if remote T/M & not host, then if feature is not marked "Y",  set w8 to 1 to suppress option
3010 P0=2; IF P>=P9 THEN P0=39
3020 IF W8=1 THEN P8$=P8$+"NA    ",P9$=P9$+"R" ELSE P8$=P8$+P$(2),P9$=P9$+P$(1,1)
3040 IF W8=1 THEN PRINT @(P0,P1+MOD(P,P9)+INT(P0/39)),'SB',P:"##.#","Not Available",'SF', ELSE PRINT @(P0,P1+MOD(P,P9)+INT(P0/39)),P:"##.#",Q$
3090 RETURN 
3100 REM "SETUP RUN SEQUENCE
3105 P$=Q$
3106 CLOSE (14); OPEN (14,ERR=3195)P$; CLOSE (14)
3110 U0$=X3$(1,6)+"PF"
3120 Q$=STR(LEN(A1$):"00")+A1$
3123 IF LEN(U1$)<5 THEN U1$="" ELSE IF U1$(1,6)=X0$ THEN U1$=""
3125 U1$=P$+Q$+X0$+Q$+U1$; IF U1$(1,6)="ZP2MEA" THEN U1$(9,10)=A$(401,10)
3130 WRITE (Z[13],KEY=U0$)U0$,U1$
3135 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
3140 IF P$="ZZ2STP" THEN Q1$="C"+A$(1,10),Z8$="FM2LAA"
3190 SETERR 3195; RUN P$
3195 SETERR 9000
3196 Q$="NA"; GOTO 2215
4000 REM "Set up a T/M record?
4010 IF P0$(115,1)<>"Y" THEN Z=1; GOTO 4040
4020 CALL "ZZPROM",".Y",X3$,Z,"Set up a Contact Management record for this new customer?","","",0 ! ssp#239088
4040 REMOVE (Z[13],KEY=NEW_FLAG$(1,11),DOM=4041)
4060 IF Z=1 THEN GOTO 1625
4080 Q$="ZP2MEA"
4090 GOTO 3100
6000 REM "BACKGROUND
6010 GOSUB 6600
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 PRINT @(44,3+V0),A$(11+K9,35),
6223 IF P0$(15,1)<>"Y" THEN GOTO 6230
6224 CALL "ZZDISP","A",A$(46+K9,10),"A/R",X3$,"","",18,4+V0,X4$
6228 PRINT @(44,4),B$(11,35),
6230 REM 
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6600
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6460 CALL "ZZDISP","A",A$(1+K9,10),"A/R",X3$,"","",18,3+V0,X4$
6490 RETURN 
6500 REM "DELETE
6501 IF POS(A$(219,1)=" A")<>0 THEN STATUS$="Active" ELSE IF A$(219,1)="S" THEN STATUS$="Retain Data/DELETE" ELSE IF A$(219,1)="I" THEN STATUS$="Clear Data/DELETE" ! SSP#240337
6502 PRINT @(20,21),"Current Customer DELETE Status is: ",STATUS$
6505 IF SEC$(44,1)="Y" OR SEC$(70,1)="Y" THEN CALL "ZZPROM",".4",X3$,0,"Access denied! You may not change a Customer DELETE Status","","",0; GOTO 1800; REM "WO129971
6515 CALL "ZZPROM","6 AR2MAA",X3$,Z,"","","V",X0
6516 IF Z=0 THEN GOTO 1800
6517 IF Z=1 THEN A8$="Set to Active"
6518 IF Z=2 THEN IF A$(219,1)<>"S" THEN A8$="Set to Retain Data/DELETE" ! SSP#240337
6519 IF Z=3 THEN A8$="Set to Clear Data/DELETE" ! SSP#240337
6520 CALL "ZZPROM",".Y",X3$,Z1,"|Verify you wish to "+A8$,"","",0
6521 IF Z1=1 THEN GOTO 6500
6525 IF Z=1 THEN A$(219,1)="A"; WRITE (Z[1],KEY=A1$)IOL=0310; GOTO 1800
6526 IF Z=2 THEN A$(219,1)="S"; WRITE (Z[1],KEY=A1$)IOL=0310; GOTO 1800
6530 Q2$="",Q0$="A/R",Q1$=A1$,Q3$="M"
6540 CALL "AR2UAB",ERR=6541,X3$,X4$,Q0$,Q1$,Q2$,Q3$
6545 Q0$=""
6550 IF Q2$="Y" THEN A8$="Record deleted!",A9$="4",RETURN$="Y",Y0$=""; GOSUB 0500; RETURN$="N"; GOTO 6580
6555 PRINT @(0,21),'CL',"Activity detected. "+Q2$, ! SSP#047384   
6560 A8$="Record set to inactive for later removal.",A9$=".4" ! SSP#047384 
6570 A$(219,1)="I"; WRITE (Z[1],KEY=A1$)IOL=0310; GOTO 6580
6580 CALL "ZZPROM",A9$,X3$,Z,A8$,"","",0; GOTO 6590
6590 IF Q2$="Y" THEN GOTO 1630 ELSE GOTO 1800
6600 REM "ALT KEY DATA READS
6605 PRINT @(0,3),'CE','SB',
6620 PRINT @(3,3+V0),"Customer code:",@(38,3+V0),"Name:",; IF P0$(15,1)="Y" THEN PRINT @(0,4+V0),"Bill-to customer:",@(38,4+V0),"Name:",
6625 PRINT @(0,5+V0),Z0$,
6630 PRINT 
6685 PRINT 'SF',
6690 RETURN 
6700 REM "ADD ENTRY
6701 GOTO 1630
6705 IF Z9=0 THEN GOTO 6790
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6720 IF A2$=A$(155+K9,10)+A$(1+K9,10) THEN GOTO 6726
6724 REMOVE (Z[2],KEY=A2$,DOM=6725)
6728 WRITE (Z[2],KEY=A$(155+K9,10)+A$(1+K9,10))
6730 REMOVE (Z[4],KEY=A3$,DOM=6731)
6735 WRITE (Z[4],KEY=A$(278+K9,4)+A$(1+K9,10))
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 A$(1,LEN(A1$))=A1$
6825 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 1140
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1510
6850 REM PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
6900 REM "GET DEFAULTS
6902 V9$="1803  4403  1804  "
6910 IF V9>=0 THEN READ (Z[6],IND=V9)V9$,IOL=0310; GOTO 6930 ELSE V9=-1
6920 EXTRACT (Z[13],KEY=X0$+"DFLT",DOM=6921)V9$,IOL=0310; V9=IND(Z[6]); READ (Z[6])
6930 RETURN 
6950 REM "WRITE DEFAULT
6955 WRITE (Z[6],KEY=X0$+"DFLT")V9$,IOL=0310
6960 GOSUB 6920; GOTO 1630
6970 REM "ENTRY DEFAULTS
6975 PRINT 'CF'; V9=-2,Q$=""
6980 FOR X=1 TO LEN(V9$) STEP 6
6982 PRINT @(NUM(V9$(X,2)),NUM(V9$(X+2,2))),; IF V9=-2 THEN PRINT V9$(X+4,1), ELSE READ RECORD (0,SIZ=1)Q$; IF POS(Q$="1234Z")=0 THEN PRINT 'RB'; GOTO 6982
6990 IF Q$="Z" THEN EXITTO 6950 ELSE V9$(X+4,LEN(Q$))=Q$; NEXT X
6995 V9=0; GOTO 6980
7085 CALL "ZZ2MDA",X3$,X4$,"A/R",A1$,Y0$; PRINT @(0,3),'CE',; GOSUB 6600; GOSUB 6200; GOTO 7500
7500 REM "return from call"
7505 X2=-1
7510 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2
7540 GOTO 1900
7600 REM "PHONE LOOKUP
7690 RETURN 
7700 REM "Check for A/R Inquiry Message
7710 FIND (Z[5],KEY=A1$,DOM=7780)X$
7720 IF POS(" "<>X$(551,180))=0 THEN GOTO 7780
7730 PRINT @(10,10),X$(551,60),@(10,11),X$(611,60),@(10,12),X$(671,60),
7740 CALL "ZZPROM",".4",X3$,0,"NOTE: Important message from screen option #13","","",0
7780 X$=""
7790 RETURN 
8950 REM "Program flow logic
8960 DIM U0$(6); U0$(1)=FID(0); U0$=U0$+"PF"
8970 CLOSE (14); OPEN (14)"ZZPARM"; FIND (14,KEY=U0$,DOM=8995)U0$,U1$
8973 IF MID(U1$,1,6)="ZZ2STP" THEN U1$=U1$(NUM(U1$(7,2))+9),Q1$=""
8975 IF U1$="" THEN RETURN ELSE IF U1$(1,6)<>X0$ THEN RETURN 
8980 Q1=NUM(U1$(7,2)),Q1$=U1$(9,Q1),U1$=U1$(Q1+9)
8985 IF Q1<10 THEN Q1$=""; GOTO 8975
8987 IF POS(U1$(1,6)="ZP2MEA",6,ERR=8988)>0 THEN GOTO 8995
8990 RETURN 
8995 F9$="Y"; RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9705 IF X9>0 THEN GOTO 9900
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9730 RUN U1$(1,6)
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
10000 VALIDATE_REP:
10010 VAL_REP=0,CUST$=A$
10020 IF CUST$(278,4)<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,X3$,CUST$(278,4),OK; IF OK THEN VAL_REP=1; GOTO *RETURN
10030 IF CUST$(282,4)<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,X3$,CUST$(282,4),OK; IF OK THEN VAL_REP=1; GOTO *RETURN
10040 IF CUST$(286,4)<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,X3$,CUST$(286,4),OK; IF OK THEN VAL_REP=1; GOTO *RETURN
10050 IF CUST$(290,4)<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,X3$,CUST$(290,4),OK; IF OK THEN VAL_REP=1; GOTO *RETURN
10060 IF CUST$(294,4)<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,X3$,CUST$(294,4),OK; IF OK THEN VAL_REP=0; GOTO *RETURN
10080 IF CUST$(278,20)=DIM(20) THEN VAL_REP=1
10090 RETURN 
10098 ! 
10099 ! 
10100 REM "F6 - Lookup by order ship-to zip code - 287873
10105 LOOKUP_F6:
10106 IF TCB(88)=0 THEN MSGBOX "You must be using Windx to access this feature!",MSG("WARNING"); GOTO LOOKUP_F6_DONE
10110 RETVAL$=""
10120 DIM INPUTVAL$(9); PRINT @(33,20),'CE',"Zip Code:",; CALL "ZZENTR","AUX",A{ALL},INPUTVAL$,X4$,X3$,44,20,1,8,C0,"ZIP","{1Enter Order Ship-to Zip Code -or- Blank for all, then hit Return.","AR2MAA02","FS1","",""; IF ABS(C0)=4 THEN GOSUB 6400; RETURN 
10130 RETVAL$=STP(INPUTVAL$,2); PROCESS "QRY_FT5.1","../OPG/OP.EN",RETVAL$; IF CTL=4 OR CTL=-1999 OR LEN(RETVAL$)<10 THEN RETVAL$=""; GOSUB 6400; RETURN 
10140 A$(1,10)=STP(RETVAL$); C0=1
10145 LOOKUP_F6_DONE:GOSUB 6400
10190 RETURN 
10199 ! 
56000 REM "188067-New Pricing changes. Standard GP Percentage for a           
56002 REM "191822-Work order to allow sales reps to only see their own        
56004 REM "193458-Customers not printing ONACCTS on the open invoice          
56006 REM "227115-Request mod to AR2MAA for custom code.
56007 REM "047384-Show where the activity is when a delete is not allowed.    
56008 REM "287873-Customer search by order zip code. Display order number/date
