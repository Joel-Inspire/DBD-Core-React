0010 REM "Release 3.8 Sales Tax Computation <AR2EAY>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 06/20/13 - 14.201388 - jvv - SSP# 262668
0037 REM "262668-Has a special charge and needs to know how tax is determine 
0040 REM "Copyright 2013 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "A$=ARB string, A0=index to ART
0051 REM "T(I,0)=Taxable Sales, T(I,1)=Non-Taxable Sale, T(I,2)=Tax as Freight
0052 REM "T(I,3)=State Taxable Sale, T(I,4)=State Sales Tax, T(I,5)=County Taxable Amount, T(I,6)=County Sales Tax, T(I,7)=Local Taxable Amount, T(I,8)=Local Sales Tax, T(I,9)=Total Sales Tax   ,T(I,10)=Additonal Freight, T(I,11)=Total of Special Charges to subtract if not taxing special charges, T(I,12)=NonTaxed special charges (fuel surcharge for 584 for now)
0053 REM "Don't worry about deletion, since we will always go thru ARB and thisroutine always removes ASD records before computing tax.
0054 REM "S0=when coming from taxable amount override entry.  4=state, 6=county, 8=local
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,A$,A{ALL},S0
0100 SETERR 9000
0110 X0$="AR2EAY",X1$="Compute Sales Tax"
0120 DIM S$(40),D$(190),D[4]
0125 DIM AR5[7]; REM " SSP# 149046
0135 C9=-1
0140 M2$="###,###.00-"
0200 REM "
0300 REM "IOLISTS
0310 IOLIST A,B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19],B[20],B[21],B[22],B[23],B[24],B[25],B[26],B[27],B[28],B[29],B[30],B[31],B[32],B[33],B[34],B[35],B[36],B[37],B[38],B[39],B[40],B[41],B[42],B[43],B[44],B[45],B[46],B[47],B[48] ! SSP 249775
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16]
0340 IOLIST D$(1),D[0],D[1],D[2],D[3],D[4]
0350 IOLIST AR5$,AR5[0],AR5[1],AR5[2],AR5[3],AR5[4],AR5[5],AR5[6],AR5[7]
0380 IOLIST ICF$,ICF[0],ICF[1],ICF[2],ICF[3],ICF[4],ICF[5],ICF[6],ICF[7],ICF[8]
0390 IOLIST FM1$,FM1[0],FM1[1],FM1[2],FM1[3],FM1[4],FM1[5],FM1[6],FM1[7],FM1[8],FM1[9],FM1[10],FM1[11],FM1[12],FM1[13],FM1[14],FM1[15],FM1[16],FM1[17],FM1[18],FM1[19],FM1[20],FM1[21]
0430 IOLIST C1$,C1[0],C1[1],C1[2],C1[3],C1[4],C1[5],C1[6],C1[7],C1[8],C1[9],C1[10],C1[11],C1[12],C1[13],C1[14],C1[15],C1[16]
0440 IOLIST C2$,C2[0],C2[1],C2[2],C2[3],C2[4],C2[5],C2[6],C2[7],C2[8],C2[9],C2[10],C2[11],C2[12],C2[13],C2[14],C2[15],C2[16]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O ART...  03O ASD...  04O AR5...  05O FMP...  06O ICF...  10O FM1...  13O ZZPARM  250 AR1...  26O PO3...  "
0511 IF %ACTION$="C" THEN Z$="02O GRT...  03O GSD...  04O AR5...  05O FMP...  06O ICF...  10O FM1...  13O ZZPARM  25O AR1...  26O PO3...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 READ (Z[13],KEY=X3$(9,3)+"A/R")AR_PARMS$; REM " SSP# 149046
0900 DIM B[48],C[16] ! SSP 249775
0910 A0=A[20]
1000 REM 
1010 GOSUB 3000
1020 GOSUB 1100
1025 IF AR_PARMS$(134,1)="Y" THEN GOSUB 4000; REM " CALCULATE TAX IF FROM DOWNLOADED FILE
1030 IF AR_PARMS$(134,1)="Y" THEN GOTO 1060; REM " CALCULATE TAX IF FROM DOWNLOADED FILE
1040 GOSUB 2000
1060 GOSUB 1440
1080 CALL "AR2EA1",X3$,X4$,A$,A{ALL},S0 ! SSP 249775
1090 GOTO 9900
1100 REM "Compute sales tax total T(X,0)=Taxable Sale, T(X,1)=Non-taxable sale, T(X,2)=Tax as Freight
1102 FLAG$=""
1110 T9$=A$(67,10); REM " TAX CODE
1120 IF A0=0 THEN GOTO 3100 ELSE GOSUB 1300; A=A0
1140 IF A<=0 THEN GOTO 1280
1160 READ (Z[2],IND=A)IOL=0310
1162 IF POS(MID(AR_PARMS$,176,1)="Y")=0 THEN GOTO 1170; REM " SSP# 158548
1163 READ (Z[5],KEY="D"+A$(15,10),DOM=1170)CUST_PARMS$; REM " SSP# 158548
1164 IF POS(MID(CUST_PARMS$,244,1)="Y")=0 THEN GOTO 1170
1165 IF B$(135,1)<>"" THEN GOSUB 8500
1166 REM "IF B$(89,1)<>"9" OR PO3$(12,1)<>"9" THEN GOTO 1170
1168 IF B$(1,1)="S" THEN GOTO 1170
1169 GOSUB 8400; GOTO 1174
1170 EXTENSION=B[4]
1173 REM "IF B$(89,1)="9" THEN GOTO 1260 ELSE X=(POS(B$(125,10)=T9$)-1)/10
1174 IF EXTENSION=0 THEN GOTO 1260 ELSE X=(POS(B$(125,10)=T9$)-1)/10
1177 IF B$(89,1)="9" THEN GOSUB 1686
1180 IF B$(54,1)="Y" OR (B$(54,1)="C" AND B$(125,2)<>"IL") THEN T[X,0]=T[X,0]+EXTENSION
1200 IF B$(54,1)="C" AND B$(125,2)="IL" THEN T[X,0]=T[X,0]+EXTENSION*.5,T[X,1]=T[X,1]+EXTENSION-EXTENSION*.5
1220 IF B$(54,1)="F" THEN T[X,2]=T[X,2]+EXTENSION
1240 IF POS(B$(54,1)="YCF")=0 THEN T[X,1]=T[X,1]+EXTENSION
1245 IF B$(1,1)="S" THEN T[X,11]+=EXTENSION
1250 ! F POS(X3$(9,3)="584500",3) AND B$(1,1)="S" THEN IF MID(B$,65,10)=PAD("ENERGY",10) THEN T[X,12]+=EXTENSION ;!SSP 262668
1260 GOTO 1140
1280 RETURN 
1300 REM "Multiple Sales Tax Codes 3.8
1320 A=A0
1340 IF A<=0 THEN GOTO 1400 ELSE A9=A; READ (Z[2],IND=A)IOL=0310; IF POS(" "<>B$(125,10))=0 THEN B$(125,10)=A$(67,10); WRITE (Z[2],IND=A9)IOL=0310
1360 IF POS(B$(125,10)=T9$)=0 THEN T9$=T9$+B$(125,10)
1380 GOTO 1340
1400 DIM T[LEN(T9$)/10,12]
1410 IF T9$<>A$(67,10) THEN A$(178,1)="Y" ELSE A$(178,1)="N"
1415 T[0,10]=A[2]; REM "Header STC is always in T(0,), must put additional freight here"
1420 RETURN 
1440 REM "Write results to ASD record
1460 REM PRINT 'CS'
1470 IF S0>0 AND A0>0 AND A$(178,1)<>"Y" THEN GOSUB 3200
1480 FOR X=1 TO LEN(T9$) STEP 10
1500 I=(X-1)/10
1540 DIM C$(44),C[16]
1541 DIM C1$(44),C1[16],C2$(44),C2[16]
1560 C$(1,14)=A$(1,14),C$(15,10)=T9$(X,10)
1640 FOR J=0 TO 10; C[J]=T[I,J]; NEXT J
1650 C[9]=T[I,4]+T[I,6]+T[I,8]
1656 IF POS(MID(CUST_PARMS$,244,1)="Y")>0 THEN C[11]=C[3],C[12]=C[5],C[13]=C[7],C[14]=C[1],C[15]=C[1],C[16]=C[1]
1657 READ (Z[3],KEY=C$(1,24),DOM=1659)IOL=0430
1658 EXTRACT (Z[3],KEY=C$(1,24),DOM=1659)IOL=0430
1659 FOR I=0 TO 16; C1[I]=C1[I]+C[I]; NEXT I
1660 C1$=C$; WRITE (Z[3],KEY=C$(1,24))IOL=0430
1670 REM PRINT C$,"  ",C(0):M2$,C(1):M2$,C(2):M2$,C(3):M2$,C(4):M2$,C(5):M2$,C(6):M2$,C(7):M2$,C(8):M2$,C(9):M2$
1672 REM "IF POS(C$(15,10)=TAXABLE$)>0 THEN GOSUB 1686
1675 FOR J=3 TO 8; IF A$(178,1)="Y" THEN A[J]=A[J]+C[J] ELSE A[J]=C[J]
1676 NEXT J
1680 NEXT X
1681 RETURN 
1685 REM INPUT *,
1687 DIM C1$(44)
1688 FIND (Z[25],KEY=A$(15,10),DOM=1720)AR_CUST$
1689 C1$(1,14)=A$(1,14),C1$(15,10)=AR_CUST$(235,10)
1690 DIM C1[16],C2[16]
1691 FOR I=14 TO 16; C1[I]=EXTENSION*(-1); NEXT I
1692 READ (Z[3],KEY=C1$(1,24),DOM=1694)IOL=0440
1693 EXTRACT (Z[3],KEY=C1$(1,24))IOL=0440
1694 FOR I=0 TO 16; C2[I]=C2[I]+C1[I]; NEXT I
1695 C2$=C1$
1700 WRITE (Z[3],KEY=C2$(1,24))IOL=0440
1701 REM "DIM C1$(44),C1[16]
1702 REM "C1$(1,14)=A$(1,14),C1$(15,10)=B$(125,10)
1703 REM "FOR I=11 TO 13; C1[I]=C1[I]+EXTENSION; NEXT I
1704 REM "EXTRACT (Z[3],KEY=C1$(1,24),DOM=1706)IOL=0440
1705 REM "FOR I=0 TO 16; C1[I]=C1[I]+C2[I]; NEXT I
1706 REM "WRITE (Z[3],KEY=C1$(1,24),DOM=1710)IOL=0430
1720 RETURN 
2000 REM "Compute tax
2010 IF A$(178,1)="Y" THEN FOR X=3 TO 8; A[X]=0; NEXT X
2020 FOR X=1 TO LEN(T9$) STEP 10
2040 I=(X-1)/10,Q$=T9$(X,10)
2080 IF Q$(1,2)<>"  " AND (S0=0 OR S0=3) THEN READ (Z[4],KEY=Q$(1,2)+S$(1,8),DOM=2160)IOL=0340; J=3; GOSUB 2500
2100 IF Q$(3,4)<>S$(1,4) AND (S0=0 OR S0=5) THEN READ (Z[4],KEY=Q$(1,6)+S$(1,4),DOM=2160)IOL=0340; J=5; GOSUB 2500
2120 IF Q$(7,4)<>S$(1,4) AND (S0=0 OR S0=7) THEN READ (Z[4],KEY=Q$,DOM=2160)IOL=0340; J=7; GOSUB 2500
2160 NEXT X
2190 RETURN 
2500 REM "Calculate Tax
2509 IF A$(178,1)="Y" AND (S0>0 OR A0=0) THEN PRINT @(0,22),'CL',"A$(178,1)=",A$(178,1),"  S0=",S0,"  A0=",A0,; INPUT *,
2510 IF S0=0 THEN K=T[I,0] ELSE K=A[J]; GOTO 2540
2512 IF D$(106,1)="Y" THEN {! If Canadian Tax Method
2513 IF D$(104,1)<>"Y" THEN IF K<T[I,11] THEN SP=K ELSE SP=T[I,11] END_IF ; K-=SP,T[I,0]-=SP,T[I,1]+=SP ! if not taxing special charges, subtract off the amount of special charges
2514 IF J=5 THEN IF D$(104,1)="Y" THEN IF ABS(K)<ABS(T[I,12]) THEN SP=K ELSE SP=T[I,12] END_IF ; K-=SP,T[I,0]-=SP,T[I,1]+=SP ! If taxing special charges, subtract off the amount of special charges that should be non-taxed ONLY IF PST
2515 IF D$(105,1)="Y" THEN K+=T[I,J-1],T[I,0]+=T[I,J-1] ! If taxing the other tax then add into total and taxable amounts
2519  }
2520 IF D$(36,1)="Y" AND A$(115,1)<>"Y" THEN K=K+T[I,2]+T[I,10]; REM "Freight on lines/total screen  - additional freight can only be in (0,10)-SSP#108423 only add in if invoice is taxable 
2535 IF A$(178,1)<>"Y" AND A$(115,1)="Y" THEN K=0
2540 T[I,J+1]=K*D[0]/100
2560 IF D[1]>0 THEN IF ABS(T[I,J+1])>D[1] THEN T[I,J+1]=D[1]*SGN(T[I,J+1]); REM "Maximum tax
2580 T[I,J]=K
2590 RETURN 
3000 REM "Remove existing records
3020 READ (Z[3],KEY=A$(1,14),DOM=3021)
3040 K$=KEY(Z[3],END=3090)
3060 IF K$(1,14)<>A$(1,14) THEN GOTO 3090
3080 REMOVE (Z[3],KEY=K$); GOTO 3040
3090 RETURN 
3100 REM "no lines
3110 GOSUB 1400
3120 T[0,0]=A[1],T[0,1]=0,T[0,2]=0
3130 FOR I=3 TO 8; T[0,I]=A[I]; NEXT I
3190 RETURN 
3200 REM "Sales Tax override with line items and 1 tax code case
3210 FOR I=3 TO 8 STEP 2; IF S0<>I THEN T[0,I]=A[I],T[0,I+1]=A[I+1]
3220 NEXT I
3240 RETURN 
4000 REM "CALCULATE TAX IF DOWNLOADED TAX FILE
4010 IF A$(178,1)="Y" THEN FOR X=3 TO 8; A[X]=0; NEXT X
4020 FOR X=1 TO LEN(T9$) STEP 10
4040 I=(X-1)/10,Q$=T9$(X,10)
4045 IF Q$(1,10)=DIM(10) THEN GOTO 4160; REM SSP 187704
4050 DIM AR5$(190),AR5[7]; READ (Z[4],KNO=0,KEY=Q$(1,10),DOM=4060)IOL=0350
4055 DIM D$(190)
4060 PRECISION 3; D[0]=AR5[3]; D[1]=AR5[4]; PRECISION 2; D$(36,1)=AR5$(154,1); J=3; GOSUB 2500; REM " STATE!SSP#233395
4070 PRECISION 3; D[0]=AR5[2]; D[1]=AR5[5]; PRECISION 2; D$(36,1)=AR5$(155,1); J=5; GOSUB 2500; REM " COUNTY  !SSP#233395
4080 PRECISION 3; D[0]=AR5[0]; D[1]=AR5[6]; PRECISION 2; D$(36,1)=AR5$(156,1); J=7; GOSUB 2500; REM " CITY !SSP#233395  
4160 NEXT X
4190 RETURN 
8400 REM " SEARCH FOR PRICE IN LOT RECORD
8401 IF MID(PO3$,12,1)="9" THEN EXTENSION=B[4]; RETURN 
8403 EXTENSION=0
8405 DIM ICF$(214),ICF[8]
8407 Q=0,ICF_K$=B$(55,20)+B$(89,4),Q1=0
8410 READ (Z[6],KEY=ICF_K$,DOM=8411)
8415 READ (Z[6],END=8435)IOL=0380
8420 IF ICF$(1,LEN(ICF_K$))<>ICF_K$ THEN GOTO 8445 ELSE IF ICF$(110,11)<>A$(92,8)+B$(47,3) THEN GOTO 8415
8430 PRICE=ICF(5),UOM$=ICF$(129,4),PER_UNIT=ICF(6)
8435 IF PER_UNIT=0 THEN PER_UNIT=1
8440 EXTENSION=(PRICE*B[1])/PER_UNIT
8441 FLAG$="T"
8445 IF PRICE<>0 THEN GOTO 8499
8450 FM1$=""; DIM FM1[21]; READ (Z[10],KEY=B$(55,20),DOM=8495)IOL=0390
8460 PRICE=FM1(3),UOM$=FM1$(100,4),PER_UNIT=FM1(4)
8470 EXTENSION=(PRICE*B[1])/PER_UNIT
8495 IF EXTENSION=0 THEN EXTENSION=B[4]
8499 RETURN 
8500 REM "CHECK PO3 FOR SPECIAL SHIPPING
8510 READ (Z[26],KEY=A$(92,9)+B$(47,3)+B$(135,1),DOM=8599)PO3$
8599 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 ! Program changes since 03/01/2006
56005 REM "190474-Additional Services - Enhance TF to provide for tax codes   
56006 REM "233395-Issue with rounding of tax rate of 0.04225                  
56007 REM "262668-Has a special charge and needs to know how tax is determine 
