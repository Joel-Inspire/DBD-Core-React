0010 REM "Past Due Letter Printing  <AR2RFA>
0015 REM "Data Products Version
0035 REM "5.2 - 08/15/03 - 13.324166 - tma - SSP# 155810
0040 REM "Copyright 2003 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER C8$
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 SETERR 9000; SETESC 9300
0110 X0$="AR2RFA",X1$="Past Due Letter Printing"
0120 DIM A$(204),A[1],I[19],S$(40),C[1],I$(180),DET[1]
0140 Z0$="###,###.00-"
0170 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0175 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0180 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
0185 DEF FNF$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR(MOD(ASC(Z9$(1,1))-65,10)*10+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"00")
0190 DEF FNE$(Z9$)=STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0210 T=1,T0=1,T1=1
0225 W3=79
0228 REM "Margin 'M' is the starting pos for text; ie the left margin+1
0229 M=11
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR AR1 - Customer Masterfile
0310 IOLIST A$,L0
0320 IOLIST I$,I[0],I[1],I[2],I[3],I[4],I[5],I[6],I[7],I[8],I[9],I[10],I[11],I[12],I[13],I[14]
0330 IOLIST C$,C[0],C[1]
0340 IOLIST I1,I2$,DET[0],DET[1]
0370 IOLIST G$
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O ARE...  02O AR1...  03O ZY3...  04O ZY4...  05O AR6...  06O ZY2...  07O FS6...  08O FS8...  09O IC0...  10O FM1...  11O AR7...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0560 DIM O[1]; O[0]=NUM(V0$(59,2),ERR=0561),O[1]=NUM(V0$(61,2),ERR=0561)
0565 IF O[0]>0 THEN O[0]=O[0]-1 ELSE O[0]=7
0700 REM "Open Printer
0720 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0721,9920
0750 IF POS("F"=FID(W9))>0 THEN READ (Z[13],KEY=X3$(9,3)+"FX2",DOM=0755)C9$; GOTO 0770 ELSE GOTO 0770
0755 CALL "ZZPROM",".4",X3$,0,"You are not configured for the Fax output option!","","",0; GOTO 9900
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[1]; U=10,U0=1,U[0]=0,U[1]=10,T0=0,U0$="    ",T5$="                "; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(U1,END=5000); READ (U1)
1010 U3=1,U2=1
1020 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1030 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN READ (U1,END=5000); EXITTO 1000
1040 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U=1 THEN EXITTO 5000 ELSE READ (U1,END=5000); EXITTO 1000
1050 NEXT U9
1100 REM "Get record
1110 U9$=U$
1130 READ (Z[2],KEY=U$(11,10),DOM=1000)IOL=0310
1200 REM "CHECK FOR INVOICES NEEDING PRINTING
1205 READ (Z[5],KEY=A$(1,10),DOM=1206)
1210 K$=KEY(Z[5],END=1250)
1215 IF K$(1,10)<>A$(1,10) THEN GOTO 1250
1216 READ (Z[5],KEY=K$)IOL=0320
1218 IF I[3]<=0 THEN GOTO 1245; REM "Skip 0 balanceand credit invoices
1220 GOSUB 4500; REM "Age the invoice
1225 IF A2<=1 THEN GOTO 1245; REM "SKIP IT
1240 GOSUB 1400
1245 GOTO 1210
1250 GOTO 1000
1410 DIM D0$(40); READ (Z[6],KEY=V0$(75,2),DOM=1411)D0$
1420 P0=POS(" "=D0$(3,15))
1425 T=NUM(V0$(77,2))
1430 D0$=D0$(3,P0-1)+" "+STR(T)+", "+FNE$(V0$(73,2))
1490 T=0
1500 REM "Print Data
1503 GOSUB 6000
1600 REM "PRINT LETTER
1603 FOR I=1 TO 8; GOSUB 7300; NEXT I
1605 Y5$(M)=D0$; IF V0$(85,1)="Y" THEN GOSUB 7700 ELSE GOSUB 7300; GOSUB 7300
1610 Y5$(M)="Accounts Payable"; GOSUB 7300
1615 IF A$(11,40)<>S$(1,40) THEN Y5$(M)=A$(11,40); GOSUB 7300
1625 IF A$(56,30)<>S$(1,30) THEN Y5$(M)=A$(56,30); GOSUB 7300
1630 IF POS(" "<>A$(86,30))>0 THEN Y5$(M)=A$(86,30); GOSUB 7300
1635 CALL "ZZDISP","AX",A$(134,9),"ZIP",X3$,X$,"",0,0,X4$
1640 Y5$(M)=FNS$(A$(116,16))+",  "+A$(132,2)+"    "+X$
1645 IF A$(132,2)="  " THEN Y5$(M)=FNS$(A$(116,16))+"  "+X$
1650 FOR I=1 TO 4; GOSUB 7300; NEXT I
1660 S1$="1"; GOSUB 7500
1700 REM "Print invoice info
1705 GOSUB 7800
1710 CALL "ZZDISP","AX",I$(11,8),"AR6",X3$,X$,"",0,0,X4$; Y5$(M)=X$
1715 Y5$(M+9)=FNF$(I$(20,6))
1720 Y5$(M+18)=I$(36,15)
1725 Y5$(M+42)=STR(I[3]:Z0$)
1730 CALL "ZZDATE",X3$,"",I$(20,6),V0$(73,6),N5,1,0,0,0,0,0,"","",""
1735 Y5$(M+57)=STR(N5:"###")
1736 GOSUB 7300
1737 GOSUB 8100; GOSUB 8400
1739 REM "Calc & display late charges
1740 L8=(I[2]+L9)*.015
1745 GOSUB 7300; Y5$(M+18)="Late Charge this period:",Y5$(M+42)=STR(L8:Z0$); GOSUB 7300
1750 GOSUB 7900
1755 Y5$(M+23)="Prior Late Charges:",Y5$(M+42)=STR(L7:Z0$); GOSUB 7300
1760 GOSUB 7300; Y5$(M+27)="TOTAL PAST DUE:",Y5$(M+42)=STR(I[3]+L7+L8:Z0$); GOSUB 7300
1790 GOSUB 7300; GOSUB 7300
1800 REM "FINISH LETTER
1805 S1$="2"; GOSUB 7500
1900 REM "Accumulate Totals"
1910 IF POS("F"=FID(W9))>0 THEN T0$="TM "+A$(5,4)+"-"+A$(9,6),T1$=A$(16,14),X$=FID(W9); CALL "FX2SBB",X3$,W9,T0$,T1$,"",T2$,"",X9; OPEN (W9)X$
1990 T[T0,0]=T[T0,0]+1
1995 RETURN 
2000 GOTO 1000
3100 REM "Subtotal logic goes here, see -ZREPS
3180 RETURN 
4500 REM "Age invoice and set A1= xpos to print, A2= total to update"
4501 REM "<30 A2=1, >30 A2=2, >60 A2=3, >90 A2=4,>120 A2=5
4507 IF I$(11,7)="ONACCNT" OR (I$(19,1)="C" AND P$(96,1)<>"Y") THEN A2=1; GOTO 4790
4508 A3=0
4510 CALL "ZZDATE",X3$,"",I$(20,6),V0$(73,6),N1,1,0,0,0,0,0,"","",""
4511 CALL "ZZDATE",X3$,"",I$(20,6),V0$(79,6),N2,1,0,0,0,0,0,"","",""
4600 A6=30
4610 FOR X=1 TO 19; IF N1<A6 THEN A2=X; EXITTO 4650
4620 A6=A6+30; NEXT X
4650 A6=30
4660 FOR X=1 TO 19; IF N2<A6 THEN A3=X; EXITTO 4700
4670 A6=A6+30; NEXT X
4700 IF A2=A3 THEN A2=1
4710 IF N1>540 THEN A2=1
4720 IF A2=1 THEN L$=" " ELSE IF A2=2 THEN L$="A" ELSE IF A2=3 THEN L$="B" ELSE IF A2=4 THEN L$="C" ELSE IF A2>=5 THEN L$="D"
4790 RETURN 
5000 REM "End of Print
5010 GOTO 9900
6000 REM "Page header
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,"X",W5$
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6090 RETURN 
6100 REM "
6199 RETURN 
6900 REM "SORT
6905 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6940 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,X9
6990 RETURN 
7000 REM "Totals logic
7003 GOTO 7190
7005 IF T=T0 THEN IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7007 IF W+3>W0 THEN GOSUB 6000
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" "+Q2$+" ("+STR(T[T,0]:"0")+"):"
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600),T4=MOD(T3,60),T3=INT(T3/60)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")+":"+STR(T4:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 W=W+1
7330 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP  13O ZZPARM    "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7460 READ (Z[13],KEY="CMP"+X3$(9,3))C9$
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"  ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "Build letter text
7505 REM "L$ is letter type (A,B,C, or D)
7506 REM "S1$ is section to print
7560 FIND (Z[3],KEY="PASTDUE     "+L$+S1$,DOM=7561)L0$,L0; F=Z[4]
7570 W3=NUM(L0$(56,2))+10
7600 REM "Index line detail
7610 L1=0
7620 READ (F,IND=L0)L0,L7$
7640 Y5$(M)=L7$; GOSUB 7300
7660 IF L0=-1 THEN GOTO 7690
7670 L1=L1+1
7680 GOTO 7610
7690 L1=0
7692 IF V3$(2,1)="T" THEN W3=79
7695 RETURN 
7700 REM "Print file copy info
7704 REM "If a credit contact, use it, else use the sales contact & number
7705 IF POS(" "<>A$(298,20))=0 THEN GOTO 7720
7710 Y5$(40)="AR: "+A$(298,20); GOSUB 7300
7715 CALL "ZZDISP","PX",A$(338,14),"PHONE",X3$,X$,"",0,0,X4$; Y5$(44)=X$; GOSUB 7300
7719 GOTO 7745
7720 REM "Print using sales contact
7725 Y5$(40)="SALES: "+A$(165,20); GOSUB 7300
7730 CALL "ZZDISP","PX",A$(205,14),"PHONE",X3$,X$,"",0,0,X4$; Y5$(47)=X$; GOSUB 7300
7745 RETURN 
7800 REM "Print invoice info header
7805 GOSUB 7300; GOSUB 7300
7810 Y5$(M)="Invoice Invoice",Y5$(M+18)="Client P.O. No./",Y5$(M+46)="Amount",Y5$(M+56)="No. Days"; GOSUB 7300
7820 Y5$(M)="Number",Y5$(M+9)="Date",Y5$(M+18)="Item Description",Y5$(M+47)="Due",Y5$(M+54)="Outstanding"; GOSUB 7300
7830 Y5$(M)="-------",Y5$(M+8)="-------",Y5$(M+18)="----------------",Y5$(M+46)="------",Y5$(M+54)="-----------"; GOSUB 7300
7840 GOSUB 7300
7845 RETURN 
7900 REM "Prior late charges - Interiem version
7904 L7=0
7905 IF A2<=2 THEN GOTO 7995
7910 GOSUB 8200
7919 L7=0
7920 FOR I=2 TO A2-1
7925 L7=L7+.015*I0[I]
7930 NEXT I
7995 RETURN 
8000 REM "Build desc from H8$
8010 H9$=FNS$(H8$(1,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>H8$(9,8))<>0 THEN H9$=H9$+"x"
8020 H9$=H9$+FNS$(H8$(9,8)); IF H9$<>"" THEN IF POS(" "<>H8$(9,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
8030 IF NUM(H8$(17,2))<>0 THEN H9$=H9$+H8$(17,2)+" Ply "
8045 H9$=H9$+FNS$(H8$(19,40)); H8$=""
8050 RETURN 
8100 REM "Disply first item from order, if available
8105 READ (Z[8],KEY=I$(1,10)+I$(66,8),DOM=8106)
8110 K0$=KEY(Z[8],END=8190)
8115 IF K0$(1,18)<>I$(1,10)+I$(66,8) THEN GOTO 8190
8116 IF POS(K0$(19,1)="BDF")=0 THEN READ (Z[8],END=8190); GOTO 8110
8120 READ (Z[7],KEY=K0$(1,10)+K0$(19,11)+K0$(11,8)+K0$(30,1),DOM=8190)J9$
8125 IF K0$(19,1)="F" THEN READ (Z[9],KEY="          "+K0$(20,10),DOM=8190)J8$ ELSE IF K0$(19,1)="D" THEN READ (Z[9],KEY=K0$(1,10)+K0$(20,10),DOM=8190)J8$ ELSE READ (Z[10],KEY=K0$(1,10)+K0$(20,10),DOM=8190)J8$
8130 IF K0$(19,1)="B" THEN H8$=J8$(24,8)+J8$(32,8)+J8$(40,2)+J8$(42,40) ELSE H8$=J8$(64,8)+J8$(72,8)+J8$(80,2)+J8$(21,40)
8135 GOSUB 8000
8136 IF K0$(19,1)="B" THEN H9$=FNS$(J8$(42,40)); GOTO 8138
8137 H9$=FNS$(J8$(21,40))
8140 Y5$(M+18)=H9$
8190 GOSUB 7300
8195 RETURN 
8200 REM "Setup I0 array based on transactions from AR7
8201 REM "I0 will contain a bucket for each period (1="<30", 2="<60".. 5="=>120") which will be defaulted with the original invoice amount (I(2)). The transactions from AR7 for this invoice will be retrieved. Each transaction will be "AGED" the same as the invoice was, and the appropiated bucket and the buckets greater will be adjusted by this amount. The end result should be that each bucket will contain the amount due in each aging period.
8205 DIM I0[19],DET[1]; FLAG$="",SEQ$=""
8210 FOR I0=1 TO 19; I0[I0]=I[2]; NEXT I0
8215 I1=I[1]
8220 IF FLAG$="END" THEN GOTO 8290
8221 CALL "AR2XAB",X3$,X4$,Z[11],I$,I1,I2$,DET{ALL},FLAG$,SEQ$,"R"
8222 IF FLAG$="NONE" THEN GOTO 8290
8223 IF I2$(20,6)>V0$(73,6) THEN GOTO 8220
8225 CALL "ZZDATE",X3$,"",I2$(20,6),I$(20,6),N1,1,0,0,0,0,0,"","",""
8226 N1=-N1,A6=30
8230 FOR X=1 TO 19; IF N1<A6 THEN I4=X; EXITTO 8240
8235 A6=A6+30; NEXT X
8240 FOR I0=I4+1 TO 19; I0[I0]=I0[I0]+DET[0]; NEXT I0
8250 GOTO 8220
8295 RETURN 
8300 V0$(73,6)="J20610"; GOSUB 1220
8301 V0$(73,6)="J20710"; GOSUB 1220
8302 V0$(73,6)="J20810"; GOSUB 1220
8303 V0$(73,6)="J20910"; GOSUB 1220
8304 V0$(73,6)="J21010"; GOSUB 1220
8305 V0$(73,6)="J21110"; GOSUB 1220
8306 V0$(73,6)="J21210"; GOSUB 1220
8307 ESCAPE 
8400 REM "IF INVOICE PAID PRIOR TO LETTERS CHECK DATES ON PAYMENT IS               SHOULD LATE CHARGE INCLUDE THIS PAYMENT?
8410 I1=I[1],L9=0,FLAG$="",SEQ$=""; DIM DET[1]
8420 IF FLAG$="END" THEN GOTO 8490
8450 CALL "AR2XAB",X3$,X4$,Z[11],I$,I1,I2$,DET{ALL},FLAG$,SEQ$,"R"
8455 IF FLAG$="NONE" THEN GOTO 8490
8460 IF I2$(20,6)>V0$(73,6) THEN GOTO 8490
8470 L9=L9+DET[0]
8480 GOTO 8420
8490 RETURN 
8950 DEF FNQ$(Z9,Z9$)=STR(Z9:Z9$)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "End program
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9940 RUN "ZMENU"
9999 END 
