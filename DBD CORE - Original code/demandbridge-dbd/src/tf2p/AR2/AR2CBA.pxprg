0010 REM "Cash Receipts Journal <AR2CBA>
0035 REM "5.7 - 05/03/19 - 9.853333 - tma - SSP# 307158
0037 REM "307158-DBD-20 & DBSPT-61590 don't allow cash receipt update if     
0040 REM "Copyright 2019 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="AR2CBA",X1$="Cash Receipts Journal"
0120 DIM A$(23),A[2],B$(35),B[7],C$(200),D$(68),D[2],E$(159),E[12],P1$(105),F$(32),K$(90),K[15]
0140 Z0$="##,###,###.00-",Z1$="#,###,###.00-",Z2$=Z0$(1,LEN(Z0$)-1)+"DR"
0160 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0175 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0210 T=1,T0=0,T1=4
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "I/O lists
0305 REM "IOLIST FOR ARP0 - Cash Receipts Deposit Header
0310 IOLIST A$,A[0],A[1],A[2]
0315 REM "IOLIST FOR ARP - Cash Receipts Invoice Informat
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7]
0325 REM "IOLIST FOR ZYB - Bank Account Masterfile
0330 IOLIST C$
0335 REM "IOLIST FOR ARP1 - Cash Receipts Check Header
0340 IOLIST D$,D[0],D[1],D[2]
0345 REM "IOLIST FOR AR6 - Open Invoice Header File
0350 IOLIST E$,E[0],E[1],E[2],E[3],E[4],E[5],E[6],E[7],E[8],E[9],E[10],E[11],E[12]
0355 REM "IOLIST FOR ARH - Division Masterfile
0360 IOLIST F$
0370 IOLIST F$
0400 IOLIST K$,K[0],K[1],K[2],K[3],K[4],K[5],K[6],K[7],K[8],K[9],K[10],K[11],K[12],K[13],K[14],K[15],K[16],K[17],K[18],K[19],K[20]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O ARP...  02OSARP...  03O ZYB...  04O ARP...  05O AR6...  06O ARQ...  07O GL1...  08O ARH...  09O ARJ...  10O ARK...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0530 CALL "ZZINIT",STR(Z[9]:"00")
0540 GOSUB 8500
0590 READ (Z[13],KEY=X3$(9,3)+"A/R",DOM=9800)P0$
0595 READ (Z[13],KEY=X3$(9,3)+"AR1",DOM=9800)P2$
0605 N0$="AR2CBA"; GOSUB 6900
0620 REM " Repository stuff
0630 R6$="N"; IF V3$(2,1)<>"T" THEN CALL "ZR2DGP",ERR=0631,X3$,X4$,"CR_JOURNAL",R6$
0635 IF R6$(1,1)="Y" THEN T9$=X3$(21,6)+X3$(178,6); CALL "ZR2DND",ERR=0636,X3$,X4$,T9$,"",R6$; REM "Signal start of a new document, the key is system date+audit control #
0700 REM "Open Printer
0720 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0721,9920
0724 ! IF %HAS_IMAGE_LIB THEN CALL "ILGAUD",ERR=*NEXT,X3$,X4$,"R2" ! 174140
0725 IF %HAS_IMAGE_LIB THEN IMG_TYPE$="R2",IMG_REF$="",IL2_CHAN=0; CALL "ILGAUD",ERR=*NEXT,X3$,X4$,IMG_TYPE$; IF LEN(IMG_TYPE$)>2 THEN IMG_REF$=IMG_TYPE$(3),IMG_TYPE$=IMG_TYPE$(1,2) ! 174140 SSP 208706
0750 REM "Check for restart record"
0755 READ (Z[1],KEY="",DOM=0780)
0760 IF K0$>"" THEN X3$(184,1)="1"
0765 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W4,W4$,W5$
0770 CALL "ZZREST",X3$,Z[6],Z[1],W9,W3,W
0775 GOSUB 6000
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[4]; U=10,U0=4,U[0]=0,U[1]=4,U[2]=2,U[3]=3,U[4]=1,T0=3,V1=6,V2=3,V3=1,U0$="Y Y ",T5$="S   S   S       ",U5$="Fiscal year",U6$="Bank code",U7$="Deposit number"; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 GOTO 1100 ! SSP#298502 !U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN READ (U1,END=5000); EXITTO 1000
1030 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U=1 THEN EXITTO 5000 ELSE READ (U1,END=5000); EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$,U8$=""
1120 ON V0 GOTO 1150,1150,1130,1140
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0340
1155 IF X3$(330,1)="1" AND (LEN(U$)=12 OR LEN(U$)=28 OR LEN(U$)=36) THEN U8$=U$(1,2),U$=U$(3),U9$=U9$(3) ! WO162043, Only if keys indicate coming from PFG transmission file
1160 IF LEN(U9$)>=10 THEN GOTO 1300
1170 P9=10-LEN(U9$); FOR X=1 TO P9; U9$=U9$+" "; NEXT X
1300 REM "Read other data
1330 DIM C$(LEN(C$)); FIND (Z[3],DOM=1331,KEY=D$(7,3))IOL=0330
1400 REM "Subtotals"
1415 GOSUB 3100
1420 ON T GOTO 1470,1430,1440,1450,1460
1430 GOSUB 6200
1440 GOSUB 6300
1450 GOSUB 6400
1490 T=0
1500 REM "Print Data
1501 IF LEN(U$)<>26 THEN GOTO 1000
1510 IF W+1>W0 THEN GOSUB 6000
1515 IF POS(" "<>D$(11,10))=0 THEN Y5$(1)="Journal entry" ELSE CALL "ZZDISP","AX",D$(11,10),"A/R",X3$,X$,"",0,0,X4$; Y5$(1)=X$
1520 Y5$(16)=D$(33,35)
1525 GOTO 1550; REM IF POS(" "<>D$(11,10))<>0 THEN GOTO 1550
1530 Y5$(49)="Reference no:",Y5$(63)=D$(21,6)
1535 IF X3$(69,1)<>"Y" THEN Y5$(72)=FND$(D$(27,6)) ELSE CALL "ZZDISP","DX",D$(27,6),"   ",X3$,X$,"",0,0,X4$; Y5$(72)=X$
1536 %WINDATE_FMT$="YYYYMMDD"; BADDATE$=""; CALL "ZZWDTE;VALIDATE",D$(27,6),BADDATE$; IF BADDATE$<>"" THEN FLAG1=1; Y5$(8,18)="** INVALID DATE **" ! SSP#299637
1550 GOSUB 7300
1700 REM "Set up for recap totaling
1705 IF P0$(13,1)<>"Y" THEN GOTO 1731
1710 IF D$(11,2)<>F$(1,2) THEN F$(1)=D$(11,2)+"*** NOT ON FILE ***"; READ (Z[8],KEY=D$(11,2),DOM=1711)IOL=0360
1720 O1$=D$(11,2),O2$=FNS$(F$(3,30))
1730 GOSUB 8600
1800 REM "Print invoices
1810 GOSUB 2000
1820 REM "Print lines
1830 X3=D[0],X5=0,JE_TYPE=1; GOSUB 7600 ! SSP#218042
1835 IF D[0]<>0 AND X[1]<>0 THEN Y5$(33)="Non A/R cash:"; Y5$(85)=STR(-X[1]:Z0$); GOSUB 7300; O[O4+1]=O[O4+1]-X[1],T[T0,2]=T[T0,2]-X[1],S0=S0+X[1]
1840 REM IF POS(" "<>D$(11,10))=0 THEN GOTO 1895
1850 Y5$(67)=T1$(1,15),Y5$(85)=T1$(1,14),Y5$(102)=T1$(1,14); GOSUB 7300
1870 Y5$(33)="Check no:",Y5$(43)=D$(21,6)
1875 IF X3$(69,1)<>"Y" THEN Y5$(52)=FND$(D$(27,6)) ELSE CALL "ZZDISP","DX",D$(27,6),"   ",X3$,X$,"",0,0,X4$; Y5$(52)=X$
1878 %WINDATE_FMT$="YYYYMMDD"; BADDATE$=""; CALL "ZZWDTE;VALIDATE",D$(27,6),BADDATE$; IF BADDATE$<>"" THEN FLAG1=1; Y5$(8,18)="** INVALID DATE **" ! SSP#299637
1880 Y5$(67)=STR(S0:Z2$),Y5$(85)=STR(D[1]:Z0$),Y5$(102)=STR(S1:Z0$)
1885 GOSUB 7300
1888 IF S0+D(1)<>S1 THEN FLAG=1; Y5$(105)="*** OUT OF BALANCE ***" ! SSP#218042
1890 GOSUB 7300
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+X[1]
1920 O[O4]=O[O4]+X[1]
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
2000 REM "Print Keyed Detail
2005 T4=0,S0=0,S1=0
2007 IF X3$(330,1)="1" THEN D9$=U8$+D$(1,26) ELSE D9$=D$(1,26)
2010 READ (Z[2],KEY=D9$,DOM=2011,END=2700)
2015 GOSUB 2500
2020 W$=KEY(Z[2],END=2700)
2030 IF LEN(W$)<LEN(D9$) THEN GOTO 2700 ELSE IF W$(1,LEN(D9$))<>D9$ THEN GOTO 2700
2035 READ (Z[2],END=2700)IOL=0320
2040 REM "Read Secondary Information"
2100 DIM E$(LEN(E$)),E[12]; FIND (Z[5],DOM=2101,KEY=B$(11,10)+B$(27,8))IOL=0350
2120 IF B[0]=0 AND B[1]=0 AND B[2]=0 AND B[3]=0 AND B[4]=0 AND B[5]=0 AND B[6]=0 THEN GOTO 2020
2140 IF %HAS_IMAGE_LIB THEN CALL "ILGAUD;ADD_AUDIT",ERR=*NEXT,IL2_CHAN,X3$,IMG_TYPE$,IMG_REF$,B$(11,10)+B$(27,8),"","" ! SSP 208706
2200 REM "Print Each Line
2205 T4=T4+1
2210 IF W+3>W0 THEN GOSUB 6000; GOSUB 2500
2212 IF E$(11,7)="ONACCNT" THEN Y5$(2)="ON ACCNT",J0=12; GOTO 2220 ELSE J0=12
2215 CALL "ZZDISP","AX",B$(27,8),"AR6",X3$,X$,"",0,0,X4$; Y5$(2)=X$
2220 IF X3$(69,1)<>"Y" THEN Y5$(J0)=FND$(E$(54,6)) ELSE CALL "ZZDISP","DX",E$(54,6),"   ",X3$,X$,"",0,0,X4$; Y5$(J0)=X$
2221 IF E$(11,7)="ONACCNT" THEN GOTO 2237
2225 Y5$(24)=STR(E[3]:Z1$)
2230 IF POS(" "<>E$(60,6))<>0 THEN IF X3$(69,1)<>"Y" THEN Y5$(38)=FND$(E$(60,6)) ELSE CALL "ZZDISP","DX",E$(60,6),"   ",X3$,X$,"",0,0,X4$; Y5$(38)=X$
2235 Y5$(50)=STR(E[4]:Z1$),Y5$(67)=STR(B[2]:Z0$)
2237 Y5$(85)=STR(B[6]:Z0$),Y5$(102)=STR(B[1]:Z0$)
2240 Y5$(118)=STR(E[3]-B[1]:Z0$,ERR=2241)
2245 GOSUB 7300
2247 F6=-1; IF P2$(32,1)="Y" AND (P2$(33,1)<>"Y" OR (P2$(33,1)="Y" AND E[3]-E[5]=0)) THEN GOSUB 8000
2250 IF B[3]<>0 THEN F8=1; GOSUB 8050; Y5$(49)="Freight allowed:",Y5$(67)=STR(B[3]:Z0$); GOSUB 7300
2260 IF B[4]<>0 THEN F8=1; GOSUB 8050; Y5$(47)="Sales tax allowed:",Y5$(67)=STR(B[4]:Z0$); GOSUB 7300
2275 FOR I6=F6 TO 4; GOSUB 8050; NEXT I6
2300 REM "Print lines
2310 X3=B[0],X5=0,JE_TYPE=2; GOSUB 7600 ! SSP#218042
2400 REM "Accumulate Line Totals"
2405 S2=B[2]+B[3]+B[4]+B[5]
2410 S0=S0+S2,O[O4]=O[O4]+S2,T[T0,1]=T[T0,1]+S2,T[T0,2]=T[T0,2]+B[6],O[O4+1]=O[O4+1]+B[6],T[T0,3]=T[T0,3]+B[1],S1=S1+B[1],O[O4+2]=O[O4+2]+B[1]
2499 GOTO 2020
2500 REM "Print Line Header
2510 IF W+4>W0 THEN GOSUB 6000
2690 RETURN 
2700 REM "Line Totals"
2702 IF W+2>W0 THEN GOSUB 6000
2795 RETURN 
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$(1,4)+" Period "+U1$(5,2); GOSUB 3050; GOSUB 7000
3160 U1$=U9$(1,V1)
3165 IF U0$(1,1)="Y" AND T0$<>"END" THEN GOSUB 6000
3180 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$,T6$=U2$; GOSUB 3050; GOSUB 7000
3255 IF U0$(2,1)="Y" THEN GOSUB 6000 ELSE GOSUB 7300; GOSUB 7300
3260 U2$=U9$(V5,V2)
3280 RETURN 
3300 REM "Next to lowest order break"
3310 IF V3=0 THEN RETURN ELSE IF U3$=U9$(V6,V3) THEN GOSUB 3400; RETURN 
3320 GOSUB 3420
3330 IF U3$="" THEN GOTO 3360
3350 T=3,T$=U7$,T6$=U3$; GOSUB 3050; GOSUB 7000
3355 IF U0$(3,1)="Y" THEN GOSUB 6000 ELSE GOSUB 7300; GOSUB 7300
3360 U3$=U9$(V6,V3)
3380 RETURN 
3480 RETURN 
5000 REM "End of Print
5010 IF P0$(13,1)="Y" THEN GOSUB 8650
5011 IF W0=99999999999999 THEN GOTO 5030; REM "ssp# 157497 
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; GOSUB 7000
5060 IF X3$(184,1)="1" THEN PRINT (W9)@(INT(W3/2)-39),"** The update was interrupted and will be restarted! See page 1 for details **"; W=W+1
5070 Q$=FN%FID$(Z[9]),Q$=FNS$(Q$(4,6)+Q$(21,2)); IF LEN(Q$)=6 THEN GOTO 5100 ! [205703]-changed FID() to FN%FID$()
5080 REM "ERASE BATCH RECAP FILE
5085 Y$="09C ARJ... "
5090 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1; ON Z0 GOTO 5091,9900
5095 ERASE Q$
5200 REM "
5205 X$="G/L Postings are out of balance OR totals do not balance" ! SS P#218042                                                                   
5210 IF FLAG THEN CALL "ZZPROM",".4",X3$,Z,X$,"","",0; GOTO 9900 ! SSP#218042   
5215 IF X8$="*" THEN Y4$=""; GOTO 5300
5290 IF V3$(1,1)="I" AND V3$(2,1)<>"T" THEN Y4$="AR2CUA" ELSE Y4$=""
5295 GOTO 9900
5300 REM "Deposits not balanced
5310 IF %GUI=0 THEN PRINT @(0,3),'CE',@(10,10),"C/R Journal indicates a deposit has not been fully updated",@(12,12),"Cannot proceed with the update until this is corrected.", ! SSP#230714
5315 IF %GUI THEN X$="The C/R Journal indicates a deposit has not been fully updated.  Cannot proceed with the update until this is corrected." ! SSP#230714
5320 IF %GUI THEN CALL "ZZPROM",".4",X3$,Z,X$,"","",0 ELSE CALL "ZZPROM",".4",X3$,Z,"","","",0 ! SSP#230714
5390 GOTO 9900
6000 REM "Page header
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W4$+"R",R6$
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6115 GOSUB 7700
6125 Y5$(1)="- Invoice -------------------------- - Discount --------------------------------"
6130 GOSUB 7300
6135 Y5$(3)="Number",Y5$(13)="Due date",Y5$(28)="Amount",Y5$(40)="date",Y5$(52)="Available",Y5$(72)="Taken",Y5$(87)="Cash posted",Y5$(102)="Total applied",Y5$(119)="Inv. balance"
6140 GOSUB 7300
6199 RETURN 
6200 REM "Break #1 header
6299 RETURN 
6300 REM "Break #2 header
6310 IF W+1>W0 THEN GOSUB 6000
6315 Y5$(1)="Bank code:",Y5$(12)=D$(7,3),Y5$(17)=C$(4,30)
6320 GOSUB 7300
6325 GOSUB 7300
6399 RETURN 
6400 REM "Break #3 header
6410 IF W+1>W0 THEN GOSUB 6000
6415 Y5$(1)="Deposit number:",Y5$(17)=D$(10,1),Y5$(22)="Date:"
6420 IF X3$(69,1)<>"Y" THEN Y5$(28)=FND$(D$(11,6)) ELSE CALL "ZZDISP","DX",D$(11,6),"   ",X3$,X$,"",0,0,X4$; Y5$(28)=X$
6425 Y5$(43)="Discounts past due:"
6430 IF X3$(69,1)<>"Y" THEN Y5$(63)=FND$(D$(17,6)) ELSE CALL "ZZDISP","DX",D$(17,6),"   ",X3$,X$,"",0,0,X4$; Y5$(63)=X$
6431 %WINDATE_FMT$="YYYYMMDD"; BADDATE$=""; CALL "ZZWDTE;VALIDATE",D$(11,6),BADDATE$; IF BADDATE$<>"" THEN FLAG1=1; Y5$(85)="** INVALID DATE **" ! SSP#299637
6432 GOSUB 7300
6435 IF D[1]<>0 THEN X8$="*",X9$=STR(D[1]:Z0$)+" remains to be distributed on this deposit ",X9$=X9$(POS(" "<>X9$)-1),Y5$=T3$,Y5$(INT(W3/2)-INT(LEN(X9$)/2),LEN(X9$))=X9$; GOSUB 7300
6445 GOSUB 7300
6499 RETURN 
6900 REM 
6905 IF N0$="" OR V3$(1,1)<>"I" THEN GOTO 6990
6910 EXTRACT (Z[1],KEY="",DOM=6990)V0$,K0$
6915 IF K0$>"" THEN GOTO 6990
6930 Q$="+("+V0$(22,4)+"."+V0$(26,2)+")"
6935 CALL "ZZPROM","X0AUDITS",X3$,S3,Q$,"","",0; ON S3 GOTO 6940,5200,9900
6950 REMOVE (Z[1],KEY="")
6990 RETURN 
7000 REM "Totals logic
7001 IF T=T0 THEN IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(67)=T1$(1,15),Y5$(85)=T1$(1,14),Y5$(102)=T1$(1,14); GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" Total ("+STR(T[T,0]:"0")+"):"
7018 Y5$(42)="Comm: "+STR(T[T,4]:Z0$)
7020 Y5$(67)=STR(T[T,1]:Z2$),Y5$(85)=STR(T[T,2]:Z0$),Y5$(102)=STR(T[T,3]:Z0$); GOSUB 7300
7065 IF T$="END" THEN Y5$(67)=T2$(1,14),Y5$(85)=T2$(1,14),Y5$(102)=T2$(1,14); GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 8700; GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed: "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 IF INT(T2)>0 THEN T4$=STR(INT(T2))+" Hours " ELSE T4$=""
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+STR(T3:"##.##")+" Minutes"
7260 Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 W=W+1
7320 IF R6$(1,1)="Y" THEN CALL "ZR2DNL",ERR=7321,X3$,X4$,Y5$,R6$; REM "Send line to repository
7325 IF %IL_PDF THEN PRINT (%IL_PDF,ERR=*NEXT)Y5$ ! 174140
7330 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
7600 REM "Print lines, leave total in X(1)
7601 REM "X3=index to lines, X5=x position, assumes both have been set
7605 DIM X[3]
7610 IF X3<=0 THEN GOTO 7690 ! SSP#218042
7615 IF W+3>W0 THEN GOSUB 6000
7620 X=0
7630 P=X5-48
7640 CALL "GL2XRA",X3$,X4$,W3,W9,W,W0,X3,Z[6],Z[7],X{ALL},X,P,C9,T1$,T2$,Y5$,0,0,0,B[5]; IF C9>0 THEN GOSUB 6000; GOTO 7640; REM " EXITTO 02320
7645 REM "IF (JE_TYPE=1 AND S1=D(1)+X(1)+T(T0,1)) OR (JE_TYPE=1 AND ABS(X(1))=ABS(D(1))) OR (JE_TYPE=1 AND S1+X(1)=B(5)) OR (JE_TYPE=2 AND ABS(X(1))=ABS(B(5))) THEN GOTO 7690 ! SSP#218042
7650 REM "Y5$(105)="*** OUT OF BALANCE ***",FLAG=1; GOSUB 7300 ! SSP#218042
7690 RETURN ! SSP#218042
7700 REM "PRINT P/E DATE"
7710 IF P1$(9,4)<>U1$(1,4) THEN DIM P1$(105); FIND (Z[13],KEY=X3$(9,3)+"G/LYE"+U1$(1,4),DOM=7740)P1$
7720 D1$=P1$(NUM(U1$(5,2))*6+15,6),D0$=D1$
7725 J5$="For Fiscal year "+U1$(1,4)+", period "+U1$(5,2)+" ending "+FND$(D0$)
7730 Y5$(INT(W3/2)-INT(LEN(J5$)/2))=J5$
7735 GOSUB 7300
7740 RETURN 
8000 REM "Print Commissions
8005 DIM L[6],M[35],K$(90),K[15]; F6=0,F7=0,F8=0
8010 DIM K[20]; READ (Z[10],KEY=E$(1,18),DOM=8045)IOL=0400
8015 L[0]=K[0],L[1]=K[1],L[2]=K[2],L[3]=K[3],L[4]=K[4],L[6]=E[11]; IF P2$(33,1)="Y" THEN L[5]=K[0] ELSE L[5]=B[1]
8020 IF POS(X3$(9,3)="020500",3)<>0 AND P1$(33,1)="Y" AND (B[3]<>0 OR B[5]<>0) THEN L[5]=L[5]-B[2]-B[3]-B[5],L[4]=L[4]*(L[3]-B[2]-B[3]-B[5])/L[3]
8021 CALL "AR2XAA",X3$,L{ALL},0,6,M{ALL},K{ALL},11,15
8022 IF K[16]+K[17]+K[18]+K[19]+K[20]=0 THEN GOTO 8027 ! SSP#266846
8023 M[4]=K[16],M[6]=K[16],M[11]=K[17],M[13]=K[17]
8024 M[18]=K[18],M[20]=K[18],M[25]=K[19],M[27]=K[19],M[32]=K[20],M[34]=K[20]
8027 IF E[3]-B[1]=0 THEN GOTO 8045
8029 IF E[3]<>0 THEN R2=2; PRECISION 14; C8=B[1]/E[3]; PRECISION 2 ELSE IF E[2]<>0 THEN R2=1; PRECISION 14; C8=B[1]/E[2]; PRECISION 2 ELSE R2=1,C8=1
8030 FOR I0=0 TO 4
8035 M[I0*7+4]=M[I0*7+4]*C8
8040 NEXT I0
8045 RETURN 
8050 REM "Print commission info if available
8051 REM "F6 is which comm to print
8052 REM "F6 Set in line 2247 and in 8000's, F7 in 8000's
8055 IF F6=-1 THEN GOTO 8095
8060 IF POS(" "<>K$(46+F6*4,4))=0 THEN GOTO 8090
8065 IF F7=0 THEN Y5$(J0)="Comm:",F7=1
8070 Y5$(J0+6)=K$(46+F6*4,4),Y5$(24)=STR(M[F6*7+4]:Z1$),T[T0,4]=T[T0,4]+M[F6*7+4]
8075 IF F8=0 THEN GOSUB 7300
8085 F6=F6+1,F8=0
8095 RETURN 
8500 REM "Initialize Recap stuff
8510 REM "O0=max # to hold in core,O1=max size of key,O2=# of totals to keep,O3=device # of recap file,O4=starting index of totals in O(),O$=string of keys,O0$=blank string used in 8600 routine,O1$=key to add,IOL is at 8550,O2$=desc, O4$=string to hold desc, O7=max size of desc.
8520 O0=20,O1=2,O2=3,O3=Z[9],O7=30
8530 DIM O0$(O1),O1$(O1)
8534 REM "Set iolist at 8550
8535 O$=""; FOR O4=1 TO O2; O$=O$+",O("+STR(O4)+")"; NEXT O4
8540 O$="8550 IOLIST O5$,"+O$(2)
8545 EXECUTE O$
8550 IOLIST O5$,O(1),O(2),O(3)
8570 REM "Don't put anything between 8580 and 8590, because a gosub 8580 is used in 8650 routine
8580 DIM O[(O0+1)*O2],O$(O0*O1),O4$(O0*O7)
8590 RETURN 
8600 REM "Set O1$ to key, then gosub here to get O4 set to position of the 1st total in the O array
8601 REM "exit from routine through 8620
8610 O4=POS(O1$=O$,O1)
8619 REM "if found / index by O1 and return
8620 IF O4>0 THEN O8=INT(O4/O1),O4=(O8+1)*O2+1,O4$(O8*O7+1,O7)=O2$; RETURN 
8629 REM "if not found check for room in string
8630 O4=POS(O0$=O$,O1)
8639 REM "If no room,use clear rountine & set O4 to 1st position
8640 IF O4=0 THEN GOSUB 8650; O4=1
8644 REM "use 8620 exit to finish up, now that index is found
8645 O$(O4,O1)=O1$; GOTO 8620
8650 REM "Write totals out to file & clear O$,O()
8660 FOR O4=1 TO LEN(O$) STEP O1
8661 IF O$(O4,O1)=O0$ THEN GOTO 8680
8665 O5=(INT(O4/O1)+1)*O2; REM "One less than index set in 8620
8668 FOR O6=1 TO O2; O[O6]=0; NEXT O6
8670 READ (O3,KEY=O$(O4,O1),DOM=8671)IOL=8550
8672 FOR O6=1 TO O2; O[O6]=O[O5+O6]; NEXT O6
8673 O5$=O4$(1+INT(O4/O1)*O7,O7)
8675 WRITE (O3,KEY=O$(O4,O1))IOL=8550
8680 NEXT O4
8684 REM "Use last part of init logic to clear O$ & O()
8685 GOSUB 8580
8690 RETURN 
8700 REM "Print out Recap totals"
8704 IF P0$(13,1)<>"Y" THEN RETURN 
8705 GOSUB 7300; Y5$(3)="Divisional Recap Totals:"; GOSUB 7300; GOSUB 7300
8710 READ (O3,KEY="",DOM=8711)
8720 IF W+4>W0 THEN GOSUB 6000
8730 READ (O3,END=8780)IOL=8550
8735 REM "Add logic to check for different types & title printing here"
8740 Y5$(8)=O5$,Y5$(67)=STR(O[1]:Z2$),Y5$(85)=STR(O[2]:Z0$),Y5$(102)=STR(O[3]:Z0$); GOSUB 7300; GOSUB 7300
8750 GOTO 8730
8790 RETURN 
8950 REM "Program flow logic
8960 DIM Q8$(6); Q8$(1)=FID(0); Q8$=Q8$+"PF"
8970 CLOSE (14); OPEN (14)"ZZPARM"; FIND (14,KEY=Q8$,DOM=8990)Q8$,Q9$
8980 Q1=NUM(Q9$(7,2)),Q1$=Q9$(9,Q1),Q9$=Q9$(Q1+9)
8990 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9700 REM "PROG FLOW
9705 IF POS(" "<>Y4$)<>0 THEN DIM Q2$(6); Q2$(1)=Y4$,Q9$=Q2$+STR(LEN(Q1$):"00")+Q9$; REM "If need to run update add to chain before proceeding
9710 IF Q9$="" THEN REMOVE (Z[13],KEY=Q8$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=Q8$)Q8$,Q9$
9725 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9730 IF %GUI THEN CALL Q9$(1,6) ELSE RUN Q9$(1,6)
9900 REM "End program
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9915 IF Q1$<>"" THEN GOTO 9700
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9923 CLOSE (IL2_CHAN,ERR=*NEXT) ! SSP 208706
9926 IF FLAG1=1 THEN X$="Invalid date detected, no update allowed"; CALL "ZZPROM",".4",X3$,Z,X$,"","",0; GOTO 9940 ! ssp#299637                         
9928 IF X3$(15,6)=DIM(6) THEN X$="Accounting date is blank.  No update allowed"; CALL "ZZPROM",".4",X3$,Z,X$,"","",0; GOTO 9940 ! SSP#307158-DBD-20
9930 IF Y4$<>"" THEN IF %GUI THEN CALL Y4$,ERR=9931 ELSE RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
56000 REM + Modification History
56002 REM "205703-Oracle - FID and FIB calls, replace with FN%FID$ and FN%FIB$
56003 REM "218042-Cash receipt batch allowed a out of balance JE and  posted a
56004 REM "208706-Audit control numbers are duplicating from last year        
56005 REM "266846-Information is showing incorrectly on the commission pending
56006 REM "298502-Co 589 - Cash Receipts out of balance.                      
56007 REM "299637-Co. 589 GL out of balance.  See notes for details.          
56008 REM "307158-DBD-20 & DBSPT-61590 don't allow cash receipt update if     
