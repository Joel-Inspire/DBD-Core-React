0010 REM "Export A/R Invoices  - Bradley Group <AR2XIC>
0020 SETESC 9300; SETERR 9000
0035 REM "5.1 - 11/27/02 - 9.711944 - lms - SSP# 155866
0040 REM "Copyright 2002 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0070 REM "FILES[] 0=EXP ASCII file
0072 REM "OPTION$ = Option routine to perform
0073 REM "SUMBIL$ = Summary bill number
0074 REM "FMYREC$ = FMY Record
0075 REM "SHIPTO$ = Ship to Record
0076 REM "EXAMT[] = Export amounts passed for ASCII record;REM "0,1,2
0077 REM "0=Freight, 1=Tax, 2=Extended value of goods
0078 REM "FMY[]   = FMY Numeric Array
0079 REM "EXPTOT[]= Totals for EXP
0080 REM "0=Total Freight, 1=Total Tax, 2=Total Sum Bill, 3=Count
0081 REM "4=       , 5=
0082 REM "CH_ZZPARM =Channel number for ZZPARM file
0083 REM "DETREC$ = Detail record built from Item & Special Charges
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,FILES{ALL},OPTION$,SUMBIL$,FMYREC$,SHIPTO$,EXAMT{ALL},FMY{ALL},EXPTOT{ALL},CH_ZZPARM,DETREC$
0100 SETERR 9000
0105 PRECISION 4
0110 X0$="AR2XIC",X1$="Export A/R Invoice Information"
0120 IF FN%NEA("FILES",0)=0 THEN DIM FILES[9]
0140 IF FN%NEA("EXPTOT",0)=0 THEN DIM EXPTOT[5]
0200 REM "Set data paths & file names
0210 DATA_PATH$=%DATAPATH$
0220 EXP_PATH$="/usr/lib/basic/pvx/chase/"
0221 REM "EXP_PATH$="/usr/lib/pvx/chase/"
0230 DATENO=FN%DTN(FND$(X3$(15,6)),"MMDDYY")
0250 REM "Get current sequence number for EXP file
0260 EXP_FILE$="CH",FILE_NUM=0
0270 READ (CH_ZZPARM,KEY="543EXPORTFILE",DOM=0271)FILE_NUM
0280 EXP_FILE$=EXP_FILE$+STR(FILE_NUM:"000")
0300 REM "Iolists
0400 REM "Check for files, if not there then create
0401 REM "if array values <>0 then we've already done this
0410 IF FILES[0]<>0 THEN GOTO 0600
0440 CLOSE (14); OPEN (14,ERR=0441)EXP_FILE$; CLOSE (14); GOTO 0460
0450 SERIAL EXP_FILE$,0,0
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O "+EXP_FILE$
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0524 REM "Close and re-open as text files then save file slots in FILES[]
0530 CLOSE (Z[2]); OPEN LOCK (Z[2],OPT="TEXT")EXP_FILE$; FILES[0]=Z[2]
0700 REM "Determine if EXP data
0710 DIM FILLER$(391)
0720 IF OPTION$(1,3)<>"EXP" THEN GOTO 0799
0740 IF OPTION$(4,1)="H" THEN GOSUB 2000
0741 IF OPTION$(4,1)="D" THEN IF FMYREC$(73,1)="M" THEN GOTO 0742 ELSE IF FMYREC$(73,1)<>"S" THEN GOSUB 2100 ELSE IF FMYREC$(73,1)="S" THEN GOSUB 2200
0742 IF OPTION$(4,1)="I" THEN GOSUB 2300
0743 IF OPTION$(4,1)="T" THEN GOSUB 2400; GOSUB 3000
0799 GOTO 9900
2000 REM "Export Header Record - Chase Interface - 543
2010 DIM REC_H$(1),SUM_BILL$(6),SUM_DATE$(8),PURCH_VEND$(6),IDA_QUAL$(1),DISC_PC$(3),FILLER$(150)
2020 REC_H$(1)="A",SUM_BILL$(1)=SUMBIL$,DATENO=FN%DTN(FND$(X3$(21,6)),"MMDDYY"),SUM_DATE$(1)=FN%NTD$(DATENO,"MMDDYYYY"),PURCH_VEND$(1)="101554",IDA_QUAL$(1)=" ",DISC_PC$(1)="002"
2080 DIM EXPREC$(25)
2090 EXPREC$(1)=REC_H$+SUM_BILL$+SUM_DATE$+PURCH_VEND$+FILLER$(1,4); REM " SSP# 148029
2095 GOSUB 2800
2099 RETURN 
2100 REM "Export Detail Record - Item Record
2110 DIM REC_D1$(1),INV_DATE$(8),ACTION_CD$(1),INV_NO$(14),ITEM_NUM$(20),COST_CTR$(7),PO_NUM$(10),UNIT_COST$(10),QTY$(9),MERCH_COST$(10),FRT_CHG$(9),OTHER_CHG$(10),TAX_RATE$(6),TAX_AMT$(10),INV_TTL$(10),REQ_NUM$(14),UM$(11),GL_PFX$(3),GL_EXP$(7),SRC$(7),DET_DESC$(25),EMPEE_ID$(25),UNSPC$(8); REM " SSP# 148029
2115 DIM PROC_CODE$(2),CARRIER$(15),POD$(25),POD_SIG$(15),PROJ_NUM$(20),DEAL_NUM$(20),DEAL_CLIENT$(30),CONTROLLER$(25),ZIP$(9),TAX_EXEMPT$(1)
2116 ZIP$=SHIPTO$(129,9); REM "SSP# 148029
2117 IF POS(SHIPTO$(12,1)="ISFNC")<>0 THEN TAX_EXEMPT$(1,1)=SHIPTO$(12,1); REM " SSP# 148029
2118 PROC_CODE$="SU"
2120 REC_D1$(1)="D",DATENO=FN%DTN(FND$(FMYREC$(23,6)),"MMDDYY"),INV_DATE$(1)=FN%NTD$(DATENO,"MMDDYYYY"),INV_NO$(1)=FMYREC$(11,8),ITEM_NUM$(1)=FMYREC$(84,10),PO_NUM$(1)=FMYREC$(34,15),QTY$(1)=STR(FMY[1]:"#########"),MERCH_COST$(1)=STR(FMY[3]:"#######.00"),FRT_CHG$(1)=STR(FMY[5]:"######.00"),OTHER_CHG$(1)=STR(0:"######.00"),TAX_RATE$(1)="",TAX_AMT$(1)=STR(FMY[6]:"#######.00"),INV_TTL$(1)=STR(FMY[3]+FMY[5]+FMY[6]:"#######.00"),UM$(1)=FMYREC$(138,4),SRC$(1)="",DET_DESC$(1)=FMYREC$(94,40),UNSPC$(1)="14111517"
2125 IF FMY[1]<>0 THEN UNIT_COST$(1)=STR(FMY[3]/FMY[1]:"#####.0000")
2130 IF FMY[3]<0 THEN ACTION_CD$="C" ELSE ACTION_CD$=" "
2140 EXPTOT[0]=EXPTOT[0]+FMY[5]
2150 EXPTOT[1]=EXPTOT[1]+FMY[6]
2160 EXPTOT[2]=EXPTOT[2]+FMY[3]+FMY[5]+FMY[6]
2170 EXPTOT[3]=EXPTOT[3]+1
2180 DIM DETREC$(588)
2190 DETREC$(1)=REC_D1$+PROC_CODE$+INV_DATE$+FILLER$(1,8)+ACTION_CD$+REQ_NUM$+PO_NUM$+INV_NO$+COST_CTR$+FILLER$(1,7)+ITEM_NUM$+FILLER$(1,20)+DET_DESC$+UM$+UNSPC$+UNIT_COST$+QTY$+FILLER$(1,7)+MERCH_COST$+FILLER$(1,10)+FILLER$(1,6)+FILLER$(1,10)+FRT_CHG$+OTHER_CHG$+TAX_RATE$+TAX_AMT$+INV_TTL$+FILLER$(1,3)+GL_EXP$+FILLER$(1,7)+EMPEE_ID$+FILLER$(1,7)+CARRIER$+POD$+POD_SIG$+PROJ_NUM$+DEAL_NUM$+DEAL_CLIENT$+FILLER$(1,25)+CONTROLLER$+FILLER$(1,25)+FILLER$(1,25)+FILLER$(1,15)+FILLER$(1,2)+ZIP$+FILLER$(1,10)+FILLER$(1,10)+FILLER$(1,1)+FILLER$(1,1)+FILLER$(1,1)+FILLER$(1,1)+TAX_EXEMPT$; REM " SSP# 148029
2199 RETURN 
2200 REM "Export Detail Record - Special Charge Record
2210 IF STP(FMYREC$(84,10),1)="CC" THEN DETREC$(59,7)=STP(FMYREC$(94,40),1) ELSE IF STP(FMYREC$(84,10),1)="REQ" THEN DETREC$(21,14)=STP(FMYREC$(94,40),1) ELSE IF STP(FMYREC$(84,10),1)="GL" THEN DETREC$(267,7)=STP(FMYREC$(94,40),1) ELSE IF STP(FMYREC$(84,10),1)="ID" THEN DETREC$(281,25)=STP(FMYREC$(94,40),1); REM " SSP# 148029
2220 DETREC$(228,10)=STR(NUM(DETREC$(228,10))+FMY[3]:"#######.00"),DETREC$(244,10)=STR(NUM(DETREC$(244,10))+FMY[6]:"#######.00"),DETREC$(254,10)=STR(NUM(DETREC$(254,10))+FMY[3]+FMY[6]:"#######.00")
2230 EXPTOT[1]=EXPTOT[1]+FMY[6]
2240 EXPTOT[2]=EXPTOT[2]+FMY[3]+FMY[6]
2250 EXPTOT[4]=EXPTOT[4]+FMY[3]
2299 RETURN 
2300 REM "Write Out Detail Record
2310 DIM EXPREC$(588)
2320 EXPREC$(1)=DETREC$
2395 GOSUB 2800
2399 RETURN 
2400 REM "Export Trailer Record
2410 DIM REC_T$(1),SUM_BILL$(6),SUM_DATE$(8),PURCH_VEND$(6),GROSS_AMT$(13),GROSS_FRT$(13),GROSS_OTHER$(13),GROSS_TAX$(13),TOT_AMT$(15),TOT_REC$(6)
2420 REC_T$(1)="T",SUM_BILL$(1)=SUMBIL$,DATENO=FN%DTN(FND$(X3$(21,6)),"MMDDYY"),SUM_DATE$(1)=FN%NTD$(DATENO,"MMDDYYYY"),PURCH_VEND$(1)="101554",GROSS_AMT$(1)=STR(EXPTOT[2]-EXPTOT[0]-EXPTOT[1]-EXPTOT[4]:"##########.00"),GROSS_FRT$(1)=STR(EXPTOT[0]:"##########.00"),GROSS_OTHER$(1)=STR(EXPTOT[4]:"##########.00"),GROSS_TAX$(1)=STR(EXPTOT[1]:"##########.00"),TOT_AMT$(1)=STR(EXPTOT[2]:"############.00"),TOT_REC$(1)=STR(EXPTOT[3]:"######")
2480 DIM EXPREC$(94)
2490 EXPREC$(1)=REC_T$+SUM_BILL$+SUM_DATE$+PURCH_VEND$+GROSS_AMT$+GROSS_FRT$+GROSS_OTHER$+GROSS_TAX$+TOT_AMT$+TOT_REC$
2495 GOSUB 2800
2499 RETURN 
2800 REM "Write EXP record
2820 PRINT (FILES[0])EXPREC$+$0D$; REM " SSP# 149206
2840 RETURN 
3000 REM "End Routines - Copy files to export path
3040 CLOSE (FILES[0])
3050 RENAME DATA_PATH$+DLM+EXP_FILE$,EXP_PATH$+EXP_FILE$+".txt"
3100 REM "Update Export File Nubmer
3120 IF FILE_NUM=999 THEN FILE_NUM=1 ELSE FILE_NUM=FILE_NUM+1
3140 WRITE (CH_ZZPARM,KEY="543EXPORTFILE")FILE_NUM
3190 RETURN 
8000 REM "Define functions
8955 DEF FND$(Z9$)=Z9$(3,4)+STR(MOD((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)),100):"0#")
8960 DEF FNE$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR(MOD(ASC(Z9$(1,1))-65,10)*10+NUM(Z9$(2,1)):"00")
9000 REM "Error Processing
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM "Transfer Control
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "Ctrl Logic
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "End Program
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
