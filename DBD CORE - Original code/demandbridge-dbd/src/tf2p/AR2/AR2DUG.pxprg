0010 REM "A/R Order Update Update <AR2DUG>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 12/14/20 - 12.236111 - dmm - SSP# 307319
0037 REM "307319-DBSPT-87098-DBD-147; Change timing of ShipNotice Request to 
0040 REM "Copyright 2020 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "Delete lines and/or Orders - Clear files
0090 IF %GUI THEN CLEAR ELSE BEGIN 
0100 SETERR 9000
0110 X0$="AR2DUG",X1$="Order Update - Phase IV"
0120 DIM A[13],B[29],C[9],F[7],I[10],L[15]
0125 DIM PO3[4] ! [206626]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13]
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19],B[20],B[21],B[22],B[23],B[24],B[25],B[26],B[27],B[28],B[29]
0321 IOLIST B$,B[0],STR(B[1]),B[2],B[3],STR(B[4]),B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],STR(B[15]),B[16],B[17],STR(B[18]),B[19],B[20],B[21],STR(B[22]),B[23],B[24],B[25],B[26],B[27],B[28],B[29]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9]
0340 IOLIST D$
0350 IOLIST E$
0360 IOLIST F$,F[0],F[1],F[2],F[3],F[4],F[5],F[6],F[7]
0390 IOLIST I$,I[0],I[1],I[2],I[3],I[4],I[5],I[6],I[7],I[8],I[9],I[10]
0410 IOLIST L$,L[0],L[1],L[2],L[3],L[4],L[5],L[6],L[7],L[8],L[9],L[10],L[11],L[12],L[13],L[14],L[15]
0430 IOLIST S$,S[0],S[1]
0480 IOLIST FMPS$ ! [206626]-IOLIST for FMPS
0485 IOLIST PO3$,PO3[0],PO3[1],PO3[2],PO3[3] ! [206626]-IOLIST for PO3
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O FS1...  02O FS2...  03L FS3...  04O FS4...  05O FMV...  06O PO1...  07O PO2...  08O PO3...  09O FMN...  10O PO6...  11O AR1...  12O FSV...  14O SH8...  15O ICF...  16O SH9...  17O FT6...  "
0515 Z$=Z$+"13O ZZPARM  25O FMP...  " ! [206626]-added FMP
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 0521,8810
0550 FIND (Z[13],KEY=X3$(9,3)+"F/M")P0$
0552 MFF_PARM=0; IF MID(P0$,337,1)="Y" THEN MFF_PARM=1 ! SSP 276772
0555 FIND (Z[13],KEY=X3$(9,3)+"AR3")AR3PARM$ ! SSP 298391
0562 PO_ACK$="N"; IF P0$(323,1)="Y" THEN PO_ACK$="Y" ! SSP 233493
0570 DIM ECS$(1275); READ (Z[13],KEY=X3$(9,3)+"E/C",DOM=*NEXT)ECS$ ! SSP307139-DBSPT-87098-DBD-147
0695 PRINT @(29,9),"Invoice      Order    Line"
1000 REM "READ NEXT RECORD
1050 READ (Z[3],KEY="",DOM=1051)
1100 READ (Z[3],END=5000)IOL=0330
1110 IF C$(20,1)<>" " THEN GOTO 1100
1120 IF K0$<>C$(9,8) THEN GOSUB 4000; EXTRACT (Z[1],KEY=K0$,DOM=1900)IOL=0310
1150 PRINT @(29,10),C$(1,8),"   ",C$(9,8),"    ",C$(17,3),
1200 K2$=K0$+C$(17,3)
1300 EXTRACT (Z[2],KEY=K2$,DOM=1900)IOL=0320
1400 IF C[3]=0 THEN GOSUB 2000; GOTO 1900
1500 GOSUB 3000
1900 REM "Next record
1990 GOTO 1100
2000 REM "Remove a sales order line
2010 C8=-1; GOSUB 2500
2020 REM "Remove PO3 shipping records provided no P/O
2030 IF B$(9,1)<>" " THEN FIND (Z[7],KEY=B$(147,8)+B$(9,1)+B$(6,3),DOM=2031); GOTO 2050
2040 K9$=B$(147,8)+" "+B$(6,3); GOSUB 3500
2050 REMOVE (Z[2],KEY=K2$,DOM=2056)
2052 IF PO_ACK$="Y" THEN CALL "FM2ODW;PO_ACK_INV_LINE",X3$,K2$,"I" ! !SSP 245709
2053 IF MFF_PARM THEN CALL "FMGFV5;DELETE_FINDER_FEE_LINES",K2$ ! SSP 276772
2055 IF B$(155,1)=" " THEN GOSUB 2100
2060 RETURN 
2100 REM "Remove Custom Item Temporary Comments
2110 K3$=A$(6,10)+B$(19,10)
2120 READ (Z[5],KEY=K3$,DOM=2121)
2130 READ (Z[5],END=2190)IOL=0350
2140 IF E$(1,20)<>K3$ THEN GOTO 2190
2150 IF MID(E$,24,1)="T" THEN REMOVE (Z[5],KEY=E$(1,23)) ! 186258 Don't get error 47
2160 GOTO 2130
2190 RETURN 
2200 REM "Remove a sales order
2205 IF LEN(A$)<125 THEN GOTO 2220
2210 REMOVE (Z[4],KEY=A$(6,10)+K0$,DOM=2211)
2215 CLOSE (14); OPEN (14,ERR=2216)"FSO"+X3$(9,3); REMOVE (14,KEY=A$(6,10)+A$(28,15)+A$(118,8),DOM=2216)
2220 REM "If no P/O's exist for this order then remove orphaned PO3 records
2230 DIM K6$(9); READ (Z[6],KEY=K0$,DOM=2231)
2240 K6$=KEY(Z[6],END=2241)
2250 IF K6$(1,8)<>K0$ THEN K9$=K0$; GOSUB 3500
2260 DIM FS1$(606); FIND (Z[1],KEY=K0$,DOM=*NEXT)FS1$(1); IF POS(FS1$(184,1)="YO")>0 THEN FIND (Z[9],KEY=K0$,DOM=*NEXT)FMN$; WRITE (Z[17],KEY=K0$)FMN$ ! SSP272336, if repeat order and order notepad exists then write to FT6 before removals
2270 REMOVE (Z[1],KEY=K0$,DOM=2271)
2275 GOSUB 8400; REM "Remove SH8 records
2280 REMOVE (Z[9],KEY=K0$,DOM=2281)
2285 REMOVE (Z[12],KEY=K0$,DOM=2286)
2290 RETURN 
2500 REM "Adjust customer open order amount"
2510 EXTRACT (Z[11],KEY=A$(6,10),DOM=2550)IOL=0410
2520 L[13]=L[13]+C8*B[6]
2530 IF L[13]<0 THEN L[13]=0
2540 WRITE (Z[11],KEY=A$(6,10))IOL=0410
2550 RETURN 
3000 REM "Adjust Order Line and re-set-up
3020 B[16]=B[16]+C[0],B[21]=B[21]+C[2],B[20]=B[20]+C[4]; IF B$(9,1)=" " THEN B[8]=C[3] ELSE B[8]=0
3025 B[7]=B[7]-C[0]; IF B[7]<0 OR B$(9,1)=" " THEN B[7]=0
3030 IF B$(2,1)="Y" THEN GOSUB CHECK_LOT_AVAIL
3035 B[19]=B[19]-C[4]; IF B[19]<0 THEN B[19]=0
3070 IF C$(39,1)="Y" THEN GOSUB 3200; GOTO 3100
3080 REM "Put backordered amount in B(0), recalc cost & price ext
3090 B[0]=C[3]; CALL "FM2EXT",Z[13],0,B$(120,4),B[2],B[0],B[1],Z,2; B[3]=Z
3120 IF C$(38,1)="Y" THEN GOSUB 3250; GOTO 3140
3130 CALL "FM2EXT",Z[13],0,B$(124,4),B[5],B[0],B[4],Z,2; B[6]=Z
3140 WRITE (Z[2],KEY=K2$)IOL=0321
3141 IF PO_ACK$="Y" THEN CALL "FM2ODW;PO_ACK_INV_LINE",X3$,K2$,"P" ! !SSP 245709
3142 GOSUB GET_PICK_PRIORITY ! [206626]-get the pick priority
3145 IF P0$(152,1)="Y" THEN IF POS("P"=B$(156,5))>0 THEN A$(128,1)=""; IF B$(9,1)>" " THEN WRITE (Z[10],KEY="L"+B$(215,4)+PICK_PRIORITY$+B$(147,8)+B$(6,3)) ELSE WRITE (Z[10],KEY="L"+B$(10,4)+PICK_PRIORITY$+B$(147,8)+B$(6,3)) ! [206626]
3146 IF B$(9,1)=" " AND A$(43,1)="Y" THEN WRITE (Z[10],KEY="W"+B$(147,8)); A$(45,1)=""
3150 RETURN 
3200 REM "Lot Cost U/M
3220 B[0]=C[3],B[1]=0,B[3]=B[3]-C[4]; IF B[3]<0 THEN B[3]=0
3240 RETURN 
3250 REM "Lot Selling Price U/M
3260 B[4]=0,B[6]=B[6]-C[2]; IF B[6]<0 THEN B[6]=0
3290 RETURN 
3500 REM "Remove PO3 records starting with K9$
3510 READ (Z[8],KEY=K9$,DOM=3511)
3520 K8$=KEY(Z[8],END=3560)
3530 IF K8$(1,LEN(K9$))<>K9$ THEN GOTO 3560
3540 REMOVE (Z[8],KEY=K8$,DOM=3541)
3550 GOTO 3520
3560 RETURN 
3700 REM "Update total order amount (A(6)) and cost (A(7))
3710 A[6]=A[6]+B[6],A[7]=A[7]+B[3],A9=A9+1
3740 RETURN 
4000 REM "Order number changed - Are there any lines other than messages?
4005 IF C$="" THEN GOTO 4190
4010 IF K0$="" THEN GOTO 4160
4020 FOR A9=6 TO 13; A[A9]=0; NEXT A9; A9=0
4030 READ (Z[2],KEY=K0$,DOM=4031)
4040 K2$=KEY(Z[2],END=4120); READ (Z[2],KEY=K2$)IOL=0320
4060 IF K2$(1,8)<>K0$ THEN GOTO 4120
4080 IF B$(155,1)<>"M" THEN GOSUB 3700
4100 GOTO 4040
4120 IF A9=0 THEN GOSUB 2200; GOSUB 4300; GOTO 4150 ! SSP 233493
4140 A$(63,1)="",A$(48,1)="Y",A$(284,1)="U"; WRITE (Z[1],KEY=K0$)IOL=0310 ! SSP172662, set CC inv status flag to U for updated
4150 IF PO_ACK$<>"Y" THEN GOTO 4160 ! SSP 233493
4152 IF A9=0 THEN INV_TYPE$="F" ELSE INV_TYPE$="P" ! Check DSSI order as final invoice or not SSP 233493
4155 CALL "FM2ODW;PO_ACK_INVOICE",X3$,X4$,K0$,INV_TYPE$ ! !SSP 233493
4160 K0$=C$(9,8)
4190 RETURN 
4300 REM "Remove orphaned sales order message lines
4310 READ (Z[2],KEY=K0$,DOM=4311)
4320 K2$=KEY(Z[2],END=4390)
4330 IF K2$(1,8)<>K0$ THEN GOTO 4390
4340 READ (Z[2],KEY=K2$)IOL=0320
4350 GOSUB 2000
4360 GOTO 4320
4390 RETURN 
5000 REM "END OF UPDATE
5005 GOSUB 4000
5006 IF ECS$(1179,1)="Y" THEN CALL "EC3SN0;PROCESS",ERR=*NEXT,X3$,X4$,RETURN$ ! SSP307319-DBSPT-87098-DBD-147 CREATE SHIPNOTICES FOR THIS BATCH FROM EES TRIGGERED IN AR2DUM
5008 IF MID(AR3PARM$,13,1)="Y" THEN CALL "EFGMAQ;SEND_EMAILS",X3$,X4$,1,"IN",X3$(174,4)+X3$(85,1),1,1 ! SSP 298391 SSP 305069
5010 PRINT @(0,10),'CE',
5020 CALL "ZZPROM","U7",X3$,0,"","","",10
5030 Z$="09C FMN...  09L ARB...  04C FS4...  04O ART...  12C FSV...  20O GLW...  12O ASD...  26O SM7...  " ! SSP279907 add SM7
5040 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 5041,8820
5045 GOSUB CHECK_ARB_NO_FS3 ! SSP255325
5050 Q$=STR(Z[3]:"00")+STR(Z[9]:"00")+STR(Z[4]:"00")+STR(Z[12]:"00")+STR(Z[20]:"00")+STR(Z[26]); CALL "ZZINTZ",X3$,"B",0,Q$ ! SSP279907 add SM7
5060 GOSUB 6700
5070 Y4$="ZMENU"
5090 GOTO 9900
6700 REM "Increment Audit Control No.
6705 F9=Z[13]
6710 A2$=X3$(146,6)+"  000100",A0$=X3$(9,3)+"AUDIT",A1$=""; EXTRACT (F9,KEY=A0$,DOM=6716)A0$,A1$
6715 A0=POS(A2$(1,6)=A1$,14); IF A0=0 THEN A1$=A1$+A2$; GOTO 6715 ELSE A2=NUM(A1$(A0+8,4))+1,A2$(9,4)=STR(A2:"0000"),A1$(A0,14)=A2$
6720 WRITE (F9,KEY=A0$)A0$,A1$
6740 RETURN 
7500 CHECK_ARB_NO_FS3:! SSP255325, if invoice for order but no lines, meaning no FS3, need to remove invoicing in progress flag from order and clear order header freight amounts
7510 READ (Z[9],KEY="",DOM=*NEXT)
7515 NEXT_ARB: K$=KEY(Z[9],END=*RETURN)
7520 DIM ARB$(690); READ (Z[9],KEY=K$)ARB$(1)
7525 IF ARB$(92,8)=DIM(8) THEN GOTO NEXT_ARB
7530 READ (Z[3],KEY=ARB$(7,8)+ARB$(92,8),DOM=*NEXT)
7535 K1$=KEY(Z[3],END=NO_FS3)
7540 IF K1$(9,8)=ARB$(92,8) THEN GOTO NEXT_ARB ! have FS3 for this invoice
7545 NO_FS3:! ARB record with order number but no FS3 records
7550 DIM A[13]; EXTRACT (Z[1],KEY=ARB$(92,8),DOM=NEXT_ARB)IOL=0310
7555 A$(63,1)=DIM(1),A[8]=0,A[10]=0 ! Inv in prog, freight, freight from lots
7560 WRITE (Z[1],KEY=ARB$(92,8))IOL=0310
7565 GOTO NEXT_ARB
7590 RETURN 
7595 ! 
8400 REM "Remove SH8 records if FS1 removed - WO124784
8405 CALL "UPDSH9;DELETE_ORDER_ENTRIES",ERR=*NEXT,Z[16],K0$
8410 READ (Z[14],KEY=K0$,DOM=8411)
8415 SH8_KEY$=KEY(Z[14],END=8445)
8420 IF SH8_KEY$(1,8)<>K0$ THEN GOTO 8445
8425 REMOVE (Z[14],KEY=SH8_KEY$,DOM=8415)
8430 GOTO 8415
8445 RETURN 
8800 REM "Problem in ZZFILES
8810 GOSUB 8850; GOTO 0520
8820 GOSUB 8850; GOTO 5040
8850 REM 
8860 X$="Update "+PGN+" in progress, you may not exit. Contact SW Support for assistance"
8870 CALL "ZZPROM",".4",X3$,0,X$,"","",0
8890 RETURN 
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,1
9050 ON Y7 GOTO 9060,9100,9190,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9190 GOSUB 6600; GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9800 REM 
9830 Y4$="ZMENU"
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 0000
9950 IF NOT(NUL(Y4$)) THEN IF %GUI THEN IF Y4$<>"ZMENU" THEN CALL Y4$ ELSE EXIT ELSE RUN Y4$
9960 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
10000 CHECK_LOT_AVAIL:
10002 SEG4$="",SEG5$=""
10010 CALL "UPDICF;READBYKEY",Z[15],0,B$(161,10),B$(19,10),B$(10,4),SEG4$,SEG5$,ICF$,ICF{ALL},FOUNDICF
10020 CALL "UPDICF;READNEXT",Z[15],ICF$,ICF{ALL},FOUNDICF; IF FOUNDICF THEN IF ICF$(1,24)<>B$(161,10)+B$(19,10)+B$(10,4) THEN FOUNDICF=0
10050 IF NOT(FOUNDICF) THEN B[7]=0,B[8]=C[3]
10090 RETURN 
10997 ! 
10998 ! ***************************************************
10999 ! 
11000 GET_PICK_PRIORITY:! [206626]-Added routine
11002 FMP_LFN=Z[25] ! set the FMP LFN
11004 PO3_LFN=Z[8] ! set the PO3 LFN
11010 ORDER_DIV$=B$(147,2) ! set the Order Division from FS2
11012 ORDER_NUM$=B$(149,6) ! set the Order Number from FS2
11014 ORDER_LINE_NUM$=B$(6,3) ! set the Order Line Number from FS2
11016 PO_CODE$=B$(9,1) ! set the PO code from FS2
11018 PRINT_FLAGS$=B$(156,5) ! set the print flag from FS2
11020 SPECIAL_SHIPPING_FLAG$=B$(18,1) ! set the special shipping flag from FS2
11022 WHSE_CODE_PKLIST$=B$(215,4) ! set the warehouse code packing list from FS2
11030 GOSUB PROCESS_PO6_RECORDS ! generate the PO6 records and write them
11095 RETURN 
11097 ! 
11098 ! ***************************************************
11099 ! 
11200 READ_SHIP_VIA:! [206626]-Added routine
11201 ! This routine reads the FMPS file to get the picking 
11202 ! priority for the specified ship via code
11210 PICK_PRIORITY$="~~" ! clear the field
11220 FMPS_KEY$="S"+SHIP_VIA_CODE$ ! set the FMPS key
11230 READ (FMP_LFN,KEY=FMPS_KEY$,DOM=*RETURN)IOL=0480 ! read the FMPS record
11240 PICK_PRIORITY$=MID(FMPS$,278,2); PICK_PRIORITY$=PAD(PICK_PRIORITY$,2) ! set the pick priority
11295 RETURN 
11297 ! 
11298 ! ***************************************************
11299 ! 
11300 PROCESS_PO6_RECORDS:! [206626]-Added routine
11310 IF (SPECIAL_SHIPPING_FLAG$<>"Y") THEN {! no special shipping for the line
11320 TMP_PO3_KEY$=ORDER_DIV$+ORDER_NUM$+"     " ! set the PO3 key
11325  } ELSE {! special shipping 
11330 TMP_PO3_KEY$=ORDER_DIV$+ORDER_NUM$+" "+ORDER_LINE_NUM$+" ",PICK_PRIORITY$=DIM(2) ! set the PO3 key SSP#207082
11335  }
11340 READ (PO3_LFN,KEY=TMP_PO3_KEY$,DOM=*NEXT,ERR=9000)IOL=0485; GOTO 11344 ! read the PO3 record
11342 READ (PO3_LFN,END=*RETURN)IOL=0485 ! read the PO3 record 
11344 IF MID(PO3$,166,9)+MID(PO3$,7,3)<>TMP_PO3_KEY$(1,12) THEN GOTO 11695 ! if no match is found, return
11345 SHIP_VIA_CODE$="" ! clear the field
11350 SHIP_VIA_CODE$=PO3$(150,1) ! set the ship via code
11355 GOSUB READ_SHIP_VIA ! read the FMPS file to get the pick priority
11695 RETURN 
55997 ! 
55998 ! ***************************************************
55999 ! 
56000 REM + Modification History
56002 REM "194798-Customer 10-541001, Order 10-478511                         
56004 REM "206626-B/O releases no longer print after billing other lines      
56006 REM "233493-Retrieve/process DSSI PO files, create/send DSSI PO Acks.   
56008 REM "245709-DSSI; When orders are partially invoiced, need to mark the  
56010 REM "255325-Order 588305 says it is in invoicing but I have updated my  
56012 REM "272336-272235; Need to save open order notepad to repeat order note
56013 REM "276772-Multiple Finder's Fees per Customer/Item/Order Line.        
56014 REM "279907-SM7???? empty files being created, not being removed, 270121
56015 REM "298391-Ability to send E/F emails of Invoices after SJ Update      
56016 REM "305069-Change to Defer Email in SJ process                         
56018 REM "307319-DBSPT-87098-DBD-147; Change timing of ShipNotice Request to 
