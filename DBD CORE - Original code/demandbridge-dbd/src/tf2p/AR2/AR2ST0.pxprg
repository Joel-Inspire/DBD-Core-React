0010 REM "Shipto Setup/Update from Text File <AR2ST0>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 10/07/20 - 10.233333 - dmm - SSP# 307308
0037 REM "307308-DBSPT-99148-DBD-137;AR2ST0 format 05 add Country field      
0040 REM "Copyright 2020 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0080 ! Copied from IM2ST0, WO152738, will use file format field to determine which record layout the text file will be in, first one will be 01, will setup FM0 record and corresponding sort records.  Will read customer defaults for fields not in text file.
0085 ! 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="AR2ST0",X1$="Shipto Setup/Update from Text File"
0120 DIM Z0$(80,"-")
0195 ! 
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0299 ! 
0310 IOLIST FM0$
0320 IOLIST AR1$
0330 IOLIST AR5$
0395 ! 
0500 REM "Open files
0505 DIM Z[NUM(X3$(60,3))]
0509 ! Slot 10 is used for ST1 file opened in 1000's
0510 Z$="01O FM0... 02O AR1... 03O AR5... 04O FMP... 05O FMM... 06O ASR... 07O ASH... 08O AR5...  09O FTD...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0525 READ (Z[13],KEY=X3$(9,3)+"F/M")OP_PARM$
0530 GOSUB 7900; REM "Create if necessary, open ST_LOG file
0595 ! 
0900 ! Display screen, prompt for file format
0915 GOSUB 6000
0950 DIM FILE_FMT$(2); FILE_FMT$="01",X$="Valid formats: 01, 02, 03, 04 and 05" ! WO222321, WO232434, SSP307308-DBSPT-99148-DBD-137
0955 CALL "ZZENTR","SZ",TMP{ALL},FILE_FMT$,X4$,X3$,34,13,1,2,C0,"","{3"+X$,"","FM2UPA00","","",""; IF ABS(C0)=4 THEN GOTO 9900
0960 IF POS(FILE_FMT$="0102030405",2)=0 THEN CALL "ZZPROM",".4",X3$,S3,"This is not a valid file format, please try again.","","",0; GOTO 0955 ! WO222321, WO232434, SSP307308-DBSPT-99148-DBD-137
0965 DIM CUST$(10); X$="Customer code for shiptos"
0970 CALL "ZZENTR","AXUF",TMP{ALL},CUST$,X4$,X3$,34,14,1,10,C0,"A/R","{1"+X$,"","AR2MAA00","AR1","",""; IF ABS(C0)=4 THEN GOTO 9900 ELSE IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 0970,0971
0975 DIM AR1$(600); READ (Z[2],KEY=CUST$,DOM=0970)AR1$
0977 DIM FMP$(243); READ (Z[4],KEY="D"+CUST$,DOM=*NEXT)FMP$; REM "Customer default record
0980 PRINT @(46,14),AR1$(11,30),
0990 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0991,9900
0995 ! 
1000 REM "Get list of ST1 files to process
1005 ! Logic copied from FO2UBA
1010 CALL "ZZ2BLS","D0:ST1*","S",F$
1012 IF X3$(77,1)="U" THEN CALL "ZZ2BLS","D0:st1*","S",F_LOWER$; F$=F$+F_LOWER$
1015 P=POS(":"=F$); IF P=0 THEN GOTO 5000
1020 F1$=F$(P+1); P1=POS(":"=F1$); IF P1>0 THEN F$=F1$(P1-2),F1$=F1$(1,P1-3) ELSE F$=""
1050 Z$="10CU 10O "+F1$+" "
1055 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 1056,1995
1060 CLOSE (Z[10]); OPEN LOCK (Z[10],OPT="TEXT")F1$; REM "Open for reading raw file
1065 CLOSE (13); OPEN (13)"ZZPARM"; REM "Re-open ZZPARM closed in ZZ2BLS
1075 IF Q1$<>"G" THEN PRINT @(15,15),'CE',"Processing: "+F1$,
1080 IF Q1$<>"G" THEN GOSUB 8100; GOSUB 8150
1085 L0=227; IF FILE_FMT$="02" THEN L0=274 ELSE IF FILE_FMT$="03" THEN L0=338 ELSE IF FILE_FMT$="04" THEN L0=373 ELSE IF FILE_FMT$="05" THEN L0=375 ! Length of file format 01 records, change accordingly as file formats are added.  WO166044, format 02, WO222321, format 03, WO232434, format 04, SSP307308-DBSPT-99148-DBD-137, format 05 
1090 DIM FL(16); IF FILE_FMT$="03" THEN DIM FL(20) END_IF ; IF FILE_FMT$="04" THEN DIM FL(23) END_IF ; IF FILE_FMT$="05" THEN DIM FL(24) END_IF ; FL(1)=4,FL(2)=35,FL(3)=30,FL(4)=30,FL(5)=16,FL(6)=2,FL(7)=9,FL(8)=40,FL(9)=10,FL(10)=10,FL(11)=40,FL(12)=10,FL(13)=4,FL(14)=20,FL(15)=4,FL(16)=9; IF FILE_FMT$="03" OR FILE_FMT$="04" OR FILE_FMT$="05" THEN FL(17)=1,FL(18)=20,FL(19)=35,FL(20)=8 END_IF ; IF FILE_FMT$="04" OR FILE_FMT$="05" THEN FL(21)=1,FL(22)=20,FL(23)=15 END_IF ; IF FILE_FMT$="05" THEN FL(24)=2 ! WO222321, moved line 150 here to line 1090, need to know which format. WO232434, format 04, SSP307308-DBSPT-99148-DBD-137
1095 ! 
1100 REM "Process the file
1105 I9=0,RECORD$="",RCOUNT$=""; DIM RCOUNT[50]
1110 READ (Z[10],END=7750)RECORD$; GOSUB 7500
1111 IF STP(RECORD$,3)="" THEN GOTO 1110 END_IF ; CHECK_RECORD$=STP(RECORD$,3),CHECK_RECORD$=STP(CHECK_RECORD$,3,$09$); IF CHECK_RECORD$="" THEN GOTO 1110 ! WO232434,if all blanks don't process.  SSP256282, add additional check for all blanks and tabs
1112 IF FILE_FMT$="01" THEN IF STP(CVS(RECORD$,16),3)="" OR FN%NMV(RECORD$(1,4))<>1 OR STP(RECORD$,3)="" OR STP(RECORD$(1,4),3)="" THEN GOTO 1110 ! If first four characters (shipto code) are not numeric then read next record, may be file layout info at top of file
1113 I9=I9+1
1115 P=POS("ST1"=RCOUNT$,3); IF P=0 THEN RCOUNT$=RCOUNT$+"ST1"; GOTO 1115; REM "Used to write to /usr/lib/pvx/ST_LOG
1120 P1=(P+2)/3; RCOUNT[P1]=RCOUNT[P1]+1
1125 IF Q1$<>"G" THEN IF MOD(I9,T0)=1 THEN GOSUB 8150
1130 REM PRINT @(0,3),'CE',@(0,4),RECORD$; INPUT *
1145 IF LEN(RECORD$)<L0 THEN I9$=RECORD$; DIM RECORD$(L0); RECORD$(1)=I9$; REM "Correct length if short
1195 ! 
1500 REM "Process RECORD$
1510 DIM FM0$(618); RET_CODE=0,MESSAGE$="",FM0$(1,11)="C"+CUST$; REM "preset type & customer code
1520 ON POS(FILE_FMT$="0102030405",2)/2+.5 GOTO TYPE_NOT_FOUND,PROCESS_FILE_01,PROCESS_FILE_02,PROCESS_FILE_03,PROCESS_FILE_04,PROCESS_FILE_05 ! WO222321 SSP 225083, WO232434, SSP307308-DBSPT-99148-DBD-137
1530 GOTO 9900
1595 ! 
1900 REM "End of file
1901 REM "Close & Rename to ST4 for archiving. Erase an existing ST4 file, if same name
1905 Z$="10CU"+F1$+" "
1906 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
1910 F2$=F1$; IF F2$ LIKE "[Ss][Tt]1*" THEN F2$(1,3)="ST4" ELSE GOTO 1995
1915 ERASE F2$,ERR=1916
1919 REM "Get disk DIRectory of data files
1925 J$=%DATAPATH$
1930 REM "Move the file
1936 ORIGINAL$=J$+DLM+F1$,ARCHIVE$=J$+DLM+F2$; RENAME ORIGINAL$,ARCHIVE$,ERR=*NEXT
1995 GOTO 1015
1999 ! 
2000 TYPE_NOT_FOUND:REM "Record type sent in not found
2010 MESSAGE$="File layout format "+FILE_FMT$+" not found, can't process."
2015 GOSUB PRINT_MSG
2090 TYPE_NOT_FOUND_END:GOTO 9900
2099 ! 
2100 PROCESS_FILE_01:REM "Process file format 01 record (WO152738/546)
2110 KEY$="C"+CUST$+RECORD$(1,4)
2115 READ (Z[1],KEY=KEY$,DOM=*NEXT)FM0$
2125 IF FM0$(1,15)=KEY$ THEN GOSUB FILE_01_UPDATE ELSE GOSUB FILE_01_CREATE
2150 FILE_01_PROCESSED:
2190 PROCESS_FILE_01_END:GOTO 1110
2199 ! 
2200 FILE_01_UPDATE:REM "Existing record, extract FM0 and update with current info
2205 DIM FM0$(618); EXTRACT (Z[1],KEY=KEY$,DOM=*NEXT)IOL=0310; GOTO UPDATE_FM0
2210 GOSUB FILE_01_CREATE; GOTO FILE_01_PROCESSED; REM "Was set to update existing record but got DOM when trying EXTRACT, need to create new record instead
2215 UPDATE_FM0:REM "Have FM0 record, update fields
2220 REMOVE (Z[6],KEY=FM0$(200,10)+FM0$(1,15),DOM=*NEXT); REM "Remove phone number sort record
2225 REMOVE (Z[7],KEY=FM0$(129,9)+FM0$(2,10)+FM0$(12,4),DOM=*NEXT); REM "Remove zip code sort record
2230 GOSUB FILE_01_FIELDS
2235 GOSUB WRITE_RECORDS
2240 FILE_01_UPDATE_END:RETURN 
2245 ! 
2250 FILE_01_FIELDS:REM "Come here for both update and create, these are the fields provided in the incoming record
2255 FM0$(248,35)=RECORD$(5,35),FM0$(16,35)=RECORD$(5,35); REM "Description, name
2260 FM0$(51,30)=RECORD$(40,30),FM0$(81,30)=RECORD$(70,30); REM "Address lines 1 and 2
2265 PTMP$=RECORD$(167,10); GOSUB PHONE_NUM; FM0$(200,14)=PTMP$; REM "Phone number, strip out all but numbers
2270 PTMP$=RECORD$(177,10); GOSUB PHONE_NUM; FM0$(138,12)=PTMP$; REM "Fax number, strip out all but numbers
2275 FM0$(111,16)=RECORD$(100,16),FM0$(127,2)=RECORD$(116,2); REM "City,state
2280 FM0$(129,9)=STP(RECORD$(118,9),3,"-"); REM "Zip code
2285 FM0$(522,40)=RECORD$(187,40); REM "Email address
2290 FM0$(160,20)=RECORD$(127,20); REM "Attention
2295 FILE_01_FIELDS_END:RETURN 
2299 ! 
2300 FILE_01_CREATE:REM "Create new FM0 record
2305 FM0$(12,4)=RECORD$(1,4)
2310 WRITE (Z[1],KEY=FM0$(1,15),DOM=FILE_01_UPDATE)IOL=0310
2315 EXTRACT (Z[1],KEY=FM0$(1,15))IOL=0310
2320 GOSUB FILE_01_FIELDS
2325 FM0$(214,9)=FM0$(12,4); REM "Customers location code
2330 FM0$(288,18)=FMP$(28,18); REM "FOB, How to ship, Inside delivery, Ship via 
2335 IF FM0$(305,1)<>" " THEN READ (Z[4],KEY="S"+FM0$(305,1),DOM=*NEXT)SVIA$; FM0$(306,15)=SVIA$(3,15); REM "Ship via description
2340 IF FMP$(24,1)="Y" THEN FM0$(223,1)="I"; REM "FMS Method
2345 IF STP(FM0$(16,35),3)="" THEN FM0$(16,35)=AR1$(11,35); REM "Description
2350 FM0$(224,10)=FM0$(127,2),FM0$(283,1)=AR1$(245,1); REM "Sales tax code and tax exempt status, set sales tax code to state
2355 FM0$(180,20)=AR1$(185,20); REM "Greeting
2360 IF STP(FM0$(160,20),3)="" THEN FM0$(160,20)=AR1$(165,20); REM "Attention/purchasing agent
2365 FM0$(150,10)=AR1$(155,10); REM "Lookup code
2370 FM0$(284,4)=FMP$(238,4); REM "Default supply warehouse
2375 GOSUB WRITE_RECORDS
2390 FILE_01_CREATE_END:RETURN 
2399 ! 
2500 PROCESS_FILE_02:! Process file format 02 record (WO166044/487)
2505 GOSUB 7800; REM verify cust location; SSP176242
2510 KEY$="C"+CUST$+RECORD$(1,4); IF STP(RECORD$(1,4),3)="" THEN GOSUB FILE_02_CREATE; GOTO FILE_02_PROCESSED
2515 READ (Z[1],KEY=KEY$,DOM=*NEXT)FM0$
2525 IF FM0$(1,15)=KEY$ THEN GOSUB FILE_02_UPDATE ELSE GOSUB FILE_02_CREATE
2550 FILE_02_PROCESSED:
2590 PROCESS_FILE_02_END:GOTO 1110
2599 ! 
2600 FILE_02_UPDATE:
2605 DIM FM0$(618); EXTRACT (Z[1],KEY=KEY$,DOM=*NEXT)IOL=0310; GOTO UPDATE_FM0_02
2610 GOSUB FILE_02_CREATE; GOTO FILE_02_PROCESSED ! Was set to update existing record but got DOM when trying EXTRACT, need to create new record instead
2615 UPDATE_FM0_02:REM "Have FM0 record, update fields
2620 REMOVE (Z[6],KEY=FM0$(200,10)+FM0$(1,15),DOM=*NEXT) ! ASR
2625 REMOVE (Z[7],KEY=FM0$(129,9)+FM0$(2,10)+FM0$(12,4),DOM=*NEXT) ! ASH
2627 REMOVE (Z[5],KEY=FM0$(1,11)+FM0$(214,9)+FM0$(12,4),DOM=*NEXT) ! FMM
2628 REMOVE (Z[9],KEY=FM0$(1,11)+FM0$(382,20)+FM0$(12,4),DOM=*NEXT) ! FTD
2630 GOSUB FILE_01_FIELDS
2635 GOSUB FILE_02_FIELDS
2640 GOSUB WRITE_RECORDS
2645 FILE_02_UPDATE_END:RETURN 
2646 ! 
2650 FILE_02_FIELDS:! Come here for both update and create, these are the fields provided in the incoming record
2655 IF STP(RECORD$(227,10),3)<>"" THEN READ (Z[8],KEY=RECORD$(227,10),DOM=*NEXT); FM0$(224,10)=RECORD$(227,10) ! Sales tax code
2660 IF STP(RECORD$(237,4),3)<>"" THEN READ (Z[1],KEY="D"+DIM(10)+RECORD$(237,4),DOM=*NEXT); FM0$(284,4)=RECORD$(237,4) ! Default supply warehouse
2665 IF STP(RECORD$(241,20),3)="" THEN GOTO 2670 ELSE IF POS(OP_PARM$(279,1)="YF")=0 THEN FM0$(382,20)=RECORD$(241,20); GOTO 2670 ELSE READ (Z[9],KEY="C"+CUST$+RECORD$(241,20),DOM=*NEXT) ! Receiving department
2666 K$=KEY(Z[9],END=2669); IF K$(1,31)<>"C"+CUST$+RECORD$(241,20) THEN GOTO 2669 ELSE IF K$(32,4)=FM0$(1,4) THEN GOTO 2669 ELSE GOTO 2670 ! If parm set to Y or F then must be unique for this customer
2669 FM0$(382,20)=RECORD$(241,20)
2670 IF STP(RECORD$(261,4),3)="" THEN GOTO 2675 ELSE FM0$(236,4)=RECORD$(261,4); READ (Z[4],KEY="c"+CUST$+FM0$(236,9),DOM=*NEXT); GOTO 2675 ! Cost center
2671 DIM FMC$(51); FMC$(1,20)="c"+CUST$+FM0$(236,9),FMC$(21,30)=RECORD$(127,30); IF STP(FMC$(21,30),3)="" THEN FMC$(21,30)="WD Sales Location" END_IF ; WRITE (Z[4],KEY=FMC$(1,20))FMC$ ! If new cost center, create FMP record
2675 IF STP(RECORD$(265,9),3)="" THEN GOTO 2680 ELSE READ (Z[5],KEY="C"+CUST$+RECORD$(265,9),DOM=*NEXT)
2676 K$=KEY(Z[5],END=2679); IF K$(1,20)<>"C"+CUST$+RECORD$(265,9) THEN GOTO 2679 ELSE IF K$(21,4)=FM0$(1,4) THEN GOTO 2679 ELSE GOTO 2680 ! Must be unique for this customer
2679 FM0$(214,9)=RECORD$(265,9) ! Customers location code
2695 FILE_02_FIELDS_END:RETURN 
2699 ! 
2700 FILE_02_CREATE:! Create new FM0 record
2705 IF STP(RECORD$(1,4),3)<>"" THEN FM0$(12,4)=RECORD$(1,4) ELSE GOSUB GET_SHIPTO_CODE_02
2710 WRITE (Z[1],KEY=FM0$(1,15),DOM=FILE_02_UPDATE)IOL=0310
2715 EXTRACT (Z[1],KEY=FM0$(1,15))IOL=0310
2720 GOSUB FILE_01_FIELDS
2725 FM0$(214,9)=FM0$(12,4) ! Default location code
2730 FM0$(288,18)=FMP$(28,18) ! FOB, how to ship, inside delivery, ship via from customer default record
2735 IF FM0$(305,1)<>" " THEN READ (Z[4],KEY="S"+FM0$(305,1),DOM=*NEXT)SVIA$; FM0$(306,15)=SVIA$(3,15) ! Ship via description
2740 IF FMP$(24,1)="Y" THEN FM0$(223,1)="I" ! FMS Method
2745 IF STP(FM0$(16,35),3)="" THEN FM0$(16,35)=AR1$(11,35) ! Description
2750 FM0$(224,10)=AR1$(235,10),FM0$(283,1)=AR1$(245,1) ! Sales tax code and tax exempt status
2751 FM0$(235,1)="N"; REM SSP
2755 FM0$(180,20)=AR1$(185,20) ! Greeting
2760 ! IF STP(FM0$(160,20),3)="" THEN FM0$(160,20)=AR1$(165,20) ! Attention, leave blank if blank in text file
2765 FM0$(150,10)=AR1$(155,10) ! Lookup code
2770 FM0$(284,4)=FMP$(238,4) ! Default supply warehouse
2775 GOSUB FILE_02_FIELDS
2780 GOSUB WRITE_RECORDS
2790 FILE_02_CREATE_END:RETURN 
2799 ! 
3500 PROCESS_FILE_03:! Process file format 03 record - WO222321
3505 GOSUB 7800 ! Verify cust location
3510 KEY$="C"+CUST$+RECORD$(1,4); IF STP(RECORD$(1,4),3)="" THEN GOSUB FILE_03_CREATE; GOTO FILE_03_PROCESSED
3515 READ (Z[1],KEY=KEY$,DOM=*NEXT)FM0$
3525 IF FM0$(1,15)=KEY$ THEN GOSUB FILE_03_UPDATE ELSE GOSUB FILE_03_CREATE
3550 FILE_03_PROCESSED:
3590 PROCESS_FILE_03_END:GOTO 1110
3599 ! 
3600 FILE_03_UPDATE:! WO222321
3605 DIM FM0$(618); EXTRACT (Z[1],KEY=KEY$,DOM=*NEXT)IOL=0310; GOTO UPDATE_FM0_03
3610 GOSUB FILE_03_CREATE; GOTO FILE_03_PROCESSED ! Was set to update existingrecord but got DOM when trying EXTRACT, need to create new record instead
3615 UPDATE_FM0_03:! Have FM0 record, update fields
3620 REMOVE (Z[6],KEY=FM0$(200,10)+FM0$(1,15),DOM=*NEXT) ! ASR
3625 REMOVE (Z[7],KEY=FM0$(129,9)+FM0$(2,10)+FM0$(12,4),DOM=*NEXT) ! ASH
3627 REMOVE (Z[5],KEY=FM0$(1,11)+FM0$(214,9)+FM0$(12,4),DOM=*NEXT) ! FMM
3628 REMOVE (Z[9],KEY=FM0$(1,11)+FM0$(382,20)+FM0$(12,4),DOM=*NEXT) ! FTD
3630 GOSUB FILE_01_FIELDS
3635 GOSUB FILE_02_FIELDS
3637 GOSUB FILE_03_FIELDS
3640 GOSUB WRITE_RECORDS
3645 FILE_03_UPDATE_END:RETURN 
3649 ! 
3650 FILE_03_FIELDS:! WO222321
3655 IF RECORD$(274,1)<>DIM(1) THEN FM0$(375,1)=UCS(RECORD$(274,1)) ! CE ID
3660 IF STP(RECORD$(275,20),3)<>"" THEN FM0$(347,20)=UCS(RECORD$(275,20)) ! Secure items allowed
3665 IF STP(RECORD$(295,35),3)<>"" THEN FM0$(16,35)=RECORD$(295,35) ! Description, set same as name above, but if in file here, use it.
3670 IF STP(RECORD$(330,8),3)<>"" THEN FM0$(367,8)=UCS(RECORD$(330,8)) ! Location options
3695 FILE_03_FIELDS_END:RETURN 
3699 ! 
3700 FILE_03_CREATE:! WO222321
3705 IF STP(RECORD$(1,4),3)<>"" THEN FM0$(12,4)=RECORD$(1,4) ELSE GOSUB GET_SHIPTO_CODE_02
3710 WRITE (Z[1],KEY=FM0$(1,15),DOM=FILE_03_UPDATE)IOL=0310
3715 EXTRACT (Z[1],KEY=FM0$(1,15))IOL=0310
3720 GOSUB FILE_01_FIELDS
3725 FM0$(214,9)=FM0$(12,4) ! Default location code
3730 FM0$(288,18)=FMP$(28,18) ! FOB, how to ship, inside delivery, ship via from customer default record
3735 IF FM0$(305,1)<>" " THEN READ (Z[4],KEY="S"+FM0$(305,1),DOM=*NEXT)SVIA$; FM0$(306,15)=SVIA$(3,15) ! Ship via description 
3740 IF FMP$(24,1)="Y" THEN FM0$(223,1)="I" ! FMS Method
3745 IF STP(FM0$(16,35),3)="" THEN FM0$(16,35)=AR1$(11,35) ! Description
3750 FM0$(224,10)=AR1$(235,10),FM0$(283,1)=AR1$(245,1) ! Sales tax code and tax exempt status
3751 FM0$(235,1)="N" ! Don't show in WebEC?
3755 FM0$(180,20)=AR1$(185,20) ! Greeting
3765 FM0$(150,10)=AR1$(155,10) ! Lookup code
3770 FM0$(284,4)=FMP$(238,4) ! Default supply warehouse
3775 GOSUB FILE_02_FIELDS
3777 GOSUB FILE_03_FIELDS
3780 GOSUB WRITE_RECORDS
3790 FILE_03_CREATE_END:RETURN 
3799 ! 
3800 PROCESS_FILE_04:! Process file format 04 - WO232434
3805 GOSUB 7800 ! Verify cust location
3810 KEY$="C"+CUST$+RECORD$(1,4); IF STP(RECORD$(1,4),3)="" THEN GOSUB FILE_04_CREATE; GOTO FILE_04_PROCESSED
3815 READ (Z[1],KEY=KEY$,DOM=*NEXT)FM0$
3825 IF FM0$(1,15)=KEY$ THEN GOSUB FILE_04_UPDATE ELSE GOSUB FILE_04_CREATE
3850 FILE_04_PROCESSED:
3890 PROCESS_FILE_04_END:GOTO 1110
3899 ! 
3900 FILE_04_UPDATE:! WO232434
3905 DIM FM0$(618); EXTRACT (Z[1],KEY=KEY$,DOM=*NEXT)IOL=0310; GOTO UPDATE_FM0_04
3910 GOSUB FILE_04_CREATE; GOTO FILE_04_PROCESSED ! Was set to update existing record but got DOM when trying EXTRACT, need to create new record instead 
3915 UPDATE_FM0_04:! Have FM0 record, update fields
3920 REMOVE (Z[6],KEY=FM0$(200,10)+FM0$(1,15),DOM=*NEXT) ! ASR
3925 REMOVE (Z[7],KEY=FM0$(129,9)+FM0$(2,10)+FM0$(12,4),DOM=*NEXT) ! ASH
3927 REMOVE (Z[5],KEY=FM0$(1,11)+FM0$(214,9)+FM0$(12,4),DOM=*NEXT) ! FMM
3928 REMOVE (Z[9],KEY=FM0$(1,11)+FM0$(382,20)+FM0$(12,4),DOM=*NEXT) ! FTD
3930 GOSUB FILE_01_FIELDS
3935 GOSUB FILE_02_FIELDS
3937 GOSUB FILE_03_FIELDS
3938 GOSUB FILE_04_FIELDS
3940 GOSUB WRITE_RECORDS
3945 FILE_04_UPDATE_END:RETURN 
3949 ! 
3950 FILE_04_FIELDS:! WO232434
3955 IF RECORD$(338,1)<>DIM(1) THEN TAX_EXEMPT$=UCS(RECORD$(338,1)); IF POS(TAX_EXEMPT$="YNS")>0 THEN FM0$(283,1)=TAX_EXEMPT$ ! is location tax exempt?
3960 IF STP(RECORD$(339,20),3)<>"" THEN FM0$(180,20)=RECORD$(339,20) ! Greeting
3965 IF STP(RECORD$(359,15),3)<>"" THEN FM0$(290,15)=RECORD$(359,15) ! Inside delivery
3995 FILE_04_FIELDS_END:RETURN 
3999 ! 
4000 FILE_04_CREATE:! WO232434
4005 IF STP(RECORD$(1,4),3)<>"" THEN FM0$(12,4)=RECORD$(1,4) ELSE GOSUB GET_SHIPTO_CODE_02
4010 WRITE (Z[1],KEY=FM0$(1,15),DOM=FILE_04_UPDATE)IOL=0310
4015 EXTRACT (Z[1],KEY=FM0$(1,15))IOL=0310
4020 GOSUB FILE_01_FIELDS
4025 FM0$(214,9)=FM0$(12,4) ! Default location code
4030 FM0$(288,18)=FMP$(28,18) ! FOB, how to ship, inside delivery, ship via from customer default record
4035 IF FM0$(305,1)<>" " THEN READ (Z[4],KEY="S"+FM0$(305,1),DOM=*NEXT)SVIA$; FM0$(306,15)=SVIA$(3,15) ! Ship via description
4040 IF FMP$(24,1)="Y" THEN FM0$(223,1)="I" ! FMS Method
4045 IF STP(FM0$(16,35),3)="" THEN FM0$(16,35)=AR1$(11,35) ! Description
4050 FM0$(224,10)=AR1$(235,10),FM0$(283,1)=AR1$(245,1) ! Sales tax code and tax exempt status
4051 FM0$(235,1)="N" ! Don't show in WebEC?
4055 FM0$(180,20)=AR1$(185,20) ! Greeting
4065 FM0$(150,10)=AR1$(155,10) ! Lookup code
4070 FM0$(284,4)=FMP$(238,4) ! Default supply warehouse
4075 GOSUB FILE_02_FIELDS
4077 GOSUB FILE_03_FIELDS
4078 GOSUB FILE_04_FIELDS
4080 GOSUB WRITE_RECORDS
4090 FILE_04_CREATE_END:RETURN 
4099 ! 
4100 WRITE_RECORDS:
4102 IF POS(X3$(9,3)="288")<>0 THEN FM0$(214,9)=MID(RECORD$,265,9) ! Customers location code WO222321, this said 500288, don't know why this line was added, went to release with SSP205524
4110 WRITE (Z[1],KEY=FM0$(1,15))IOL=0310
4115 WRITE (Z[5],KEY=FM0$(1,11)+FM0$(214,9)+FM0$(12,4)); REM "FMM
4120 WRITE (Z[6],KEY=FM0$(200,10)+FM0$(1,15)); REM "ASR
4125 WRITE (Z[7],KEY=FM0$(129,9)+FM0$(2,10)+FM0$(12,4)); REM "ASH
4130 IF POS(OP_PARM$(279,1)="YF")<>0 THEN WRITE (Z[9],KEY=FM0$(1,11)+FM0$(382,20)+FM0$(12,4)) ! FTD
4145 WRITE_RECORDS_END:RETURN 
4199 ! 
4200 PROCESS_FILE_05:! Process file format 05 - SSP307308-DBSPT-99148-DBD-137
4205 GOSUB 7800 ! Verify cust location
4210 KEY$="C"+CUST$+RECORD$(1,4); IF STP(RECORD$(1,4),3)="" THEN GOSUB FILE_05_CREATE; GOTO FILE_05_PROCESSED
4215 READ (Z[1],KEY=KEY$,DOM=*NEXT)FM0$
4225 IF FM0$(1,15)=KEY$ THEN GOSUB FILE_05_UPDATE ELSE GOSUB FILE_05_CREATE
4250 FILE_05_PROCESSED:
4290 PROCESS_FILE_05_END:GOTO 1110
4299 ! 
4300 FILE_05_UPDATE:! SSP307308-DBSPT-99148-DBD-137
4305 DIM FM0$(618); EXTRACT (Z[1],KEY=KEY$,DOM=*NEXT)IOL=0310; GOTO UPDATE_FM0_05
4310 GOSUB FILE_05_CREATE; GOTO FILE_05_PROCESSED ! Was set to update existing record but got DOM when trying EXTRACT, need to create new record instead 
4315 UPDATE_FM0_05:! Have FM0 record, update fields
4320 REMOVE (Z[6],KEY=FM0$(200,10)+FM0$(1,15),DOM=*NEXT) ! ASR
4325 REMOVE (Z[7],KEY=FM0$(129,9)+FM0$(2,10)+FM0$(12,4),DOM=*NEXT) ! ASH
4327 REMOVE (Z[5],KEY=FM0$(1,11)+FM0$(214,9)+FM0$(12,4),DOM=*NEXT) ! FMM
4328 REMOVE (Z[9],KEY=FM0$(1,11)+FM0$(382,20)+FM0$(12,4),DOM=*NEXT) ! FTD
4330 GOSUB FILE_01_FIELDS
4335 GOSUB FILE_02_FIELDS
4337 GOSUB FILE_03_FIELDS
4338 GOSUB FILE_04_FIELDS
4339 GOSUB FILE_05_FIELDS
4340 GOSUB WRITE_RECORDS
4345 FILE_05_UPDATE_END:RETURN 
4349 ! 
4350 FILE_05_FIELDS:! SSP307308-DBSPT-99148-DBD-137
4355 COUNTRY_IN$=STP(UCS(RECORD$(374,2)),2); GOSUB GET_COUNTRY; FM0$(380,2)=COUNTRY_OUT$
4395 FILE_05_FIELDS_END:RETURN 
4399 ! 
4400 FILE_05_CREATE:! SSP307308-DBSPT-99148-DBD-137
4405 IF STP(RECORD$(1,4),3)<>"" THEN FM0$(12,4)=RECORD$(1,4) ELSE GOSUB GET_SHIPTO_CODE_02
4410 WRITE (Z[1],KEY=FM0$(1,15),DOM=FILE_05_UPDATE)IOL=0310
4415 EXTRACT (Z[1],KEY=FM0$(1,15))IOL=0310
4420 GOSUB FILE_01_FIELDS
4425 FM0$(214,9)=FM0$(12,4) ! Default location code
4430 FM0$(288,18)=FMP$(28,18) ! FOB, how to ship, inside delivery, ship via from customer default record
4435 IF FM0$(305,1)<>" " THEN READ (Z[4],KEY="S"+FM0$(305,1),DOM=*NEXT)SVIA$; FM0$(306,15)=SVIA$(3,15) ! Ship via description
4440 IF FMP$(24,1)="Y" THEN FM0$(223,1)="I" ! FMS Method
4445 IF STP(FM0$(16,35),3)="" THEN FM0$(16,35)=AR1$(11,35) ! Description
4450 FM0$(224,10)=AR1$(235,10),FM0$(283,1)=AR1$(245,1) ! Sales tax code and tax exempt status
4451 FM0$(235,1)="N" ! Don't show in WebEC?
4455 FM0$(180,20)=AR1$(185,20) ! Greeting
4465 FM0$(150,10)=AR1$(155,10) ! Lookup code
4470 FM0$(284,4)=FMP$(238,4) ! Default supply warehouse
4475 GOSUB FILE_02_FIELDS
4477 GOSUB FILE_03_FIELDS
4478 GOSUB FILE_04_FIELDS
4479 GOSUB FILE_05_FIELDS
4480 GOSUB WRITE_RECORDS
4490 FILE_05_CREATE_END:RETURN 
4499 ! 
5000 REM "EOJ 
5010 IF Q1$<>"G" THEN PRINT @(0,15),'CE',
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
5099 ! 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(12,4),"This program will read the ST1 text file in the format",@(12,5),"entered below, then for each record read,",
6025 PRINT @(12,6),"if an existing shipto record is found it will be",@(12,7),"updated, otherwise a new shipto record will be",@(12,8),"created.  All corresponding sort records will also be",@(12,9),"created or updated."
6050 PRINT @(12,13),"ST1 text file format:",
6060 PRINT @(19,14),"Customer Code:",
6165 PRINT (0,ERR=6066)'SF',
6190 RETURN 
6195 ! 
6200 REM "Display data
6210 GOSUB 6450
6295 RETURN 
6299 ! 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6458 PRINT @(34,13),FILE_FMT$(1,2),
6460 CALL "ZZDISP","A",CUST$(1,10),"A/R",X3$,"","",34,14,X4$
6490 RETURN 
6499 ! 
7500 REM remove tabs
7502 TMPRECORD$="",CNT=1
7505 IF POS($09$=RECORD$)=0 THEN GOTO *RETURN
7510 TB=POS($09$=RECORD$); IF TB=0 THEN TMPRECORD$=TMPRECORD$+RECORD$; GOTO 7590
7520 TMPRECORD$=TMPRECORD$+PAD(RECORD$(1,TB-1),FL(CNT))
7540 IF TB=LEN(RECORD$) THEN GOTO 7590 ELSE RECORD$=RECORD$(TB+1)
7545 CNT=CNT+1
7550 GOTO 7510
7590 RECORD$=TMPRECORD$
7592 IF FILE_FMT$="02" THEN RECORD$=PAD(RECORD$,273) ELSE IF FILE_FMT$="03" THEN RECORD$=PAD(RECORD$,337) ELSE IF FILE_FMT$="04" THEN RECORD$=PAD(RECORD$,373) ELSE IF FILE_FMT$="05" THEN RECORD$=PAD(RECORD$,375) ! WO222321, WO232434, SSP307308-DBSPT-99148-DBD-137
7593 IF RECORD$(1,4)="xxxx" THEN RECORD$(1,4)="    "
7594 RECORD$=STP(RECORD$,3,$0D$); REM SSP 205524
7595 RETURN 
7600 GET_SHIPTO_CODE:REM "Get next higher shipto code for this customer
7610 REM "if here, blank code, find next higher ship to code
7612 READ (Z[1],KEY=FM0$(1,11)+$FF$,DOM=*NEXT)
7614 PREV_KEY_1$=KEP(Z[1],END=*NEXT); GOTO 7616
7615 FM0$(12,4)="0001"; GOTO GET_SHIPTO_CODE_END; REM "if here, we found beginning of file so assign first number
7616 IF PREV_KEY_1$(1,11)<>FM0$(1,11) THEN FM0$(12,4)="0001"; GOTO GET_SHIPTO_CODE_END; REM "if here there are no shiptos for this customer so use the first one
7620 REM "if here we have the last one used in prev_key_1$(12,4), increment it
7622 IF PREV_KEY_1$(12,4)="ZZZZ" THEN RET_CODE=4,MESSAGE$="No higher shipto code can be generated."; GOTO GET_SHIPTO_CODE_END ELSE FM0$(12,4)=PREV_KEY_1$(12,4)
7623 ST_NUM=NUM(FM0$(12,4),ERR=*NEXT); IF ST_NUM>0 AND ST_NUM<9999 THEN FM0$(12,4)=STR(ST_NUM+1:"0000"); GOTO GET_SHIPTO_CODE_END
7625 FOR K_INDEX=15 TO 12 STEP -1
7627 IF FM0$(K_INDEX,1)="Z" THEN FM0$(K_INDEX,1)="0" ELSE IF FM0$(K_INDEX,1)="9" THEN FM0$(K_INDEX,1)="A"; EXITTO GET_SHIPTO_CODE_END ELSE FM0$(K_INDEX,1)=CHR(ASC(FM0$(K_INDEX,1))+1); EXITTO GET_SHIPTO_CODE_END
7629 NEXT K_INDEX
7645 GET_SHIPTO_CODE_END:RETURN 
7649 ! 
7650 GET_SHIPTO_CODE_02:
7655 KEY_CHECK$="C"+CUST$+"1001"; IF STP(LAST_USED$,3)>"" THEN KEY_CHECK$="C"+CUST$+LAST_USED$
7660 READ (Z[1],KEY=KEY_CHECK$,DOM=NOT_USED)
7665 ST_NUM=NUM(KEY_CHECK$(12,4),ERR=*NEXT); IF ST_NUM>0 AND ST_NUM<9999 THEN KEY_CHECK$(12,4)=STR(ST_NUM+1:"0000"); GOTO 7660
7670 FOR K_INDEX=15 TO 12 STEP -1
7671 IF KEY_CHECK$(K_INDEX,1)="Z" THEN KEY_CHECK$(K_INDEX,1)="0" ELSE IF KEY_CHECK$(K_INDEX,1)="9" THEN KEY_CHECK$(K_INDEX,1)="A"; EXITTO 7660 ELSE KEY_CHECK$(K_INDEX,1)=CHR(ASC(KEY_CHECK$(K_INDEX,1))+1); EXITTO 7660
7672 NEXT K_INDEX
7690 NOT_USED: FM0$(12,4)=KEY_CHECK$(12,4),LAST_USED$=FM0$(12,4)
7695 GET_SHIPTO_CODE_02_END:RETURN 
7699 ! 
7700 PHONE_NUM:REM "Given PTMP$, remove all but numbers
7705 PTMP$=STP(PTMP$,2); IF PTMP$="" THEN GOTO 7745 ELSE MY_TMP$=PTMP$,LEN_MY_TMP=LEN(MY_TMP$),PTMP$=""
7710 FOR MY_INDEX=1 TO LEN_MY_TMP; IF POS(MY_TMP$(MY_INDEX,1)="0123456789")<>0 THEN PTMP$=PTMP$+MY_TMP$(MY_INDEX,1) END_IF ; NEXT MY_INDEX
7745 PHONE_NUM_END:RETURN 
7749 ! 
7750 REM "End of file
7752 IF LEN(RCOUNT$)<=0 THEN GOTO 7795
7755 FOR X=1 TO LEN(RCOUNT$)-2 STEP 3
7756 S$="ST1|STAT|"+FN%CDS$+"|"+X3$(40,3)+"|"+X3$(9,3)+"|"+FID(0)+"|"+F1$+"|"+RCOUNT$(X,3)+"|"+STR(RCOUNT[(X+2)/3]:"0000")
7758 PRINT (14)S$
7759 NEXT X
7795 GOTO 1900
7799 ! 
7800 REM ' compare cust location
7802 IF RECORD$(1,4)<>DIM(4) THEN GOTO *RETURN; REM SSP 176242 - found code in call...missing from program
7805 READ (Z[1],KEY="C"+CUST$,ERR=*NEXT)
7810 KYZ1$=KEY(Z[1],END=7890); IF KYZ1$(1,11)<>"C"+CUST$ THEN GOTO *RETURN
7820 READ (Z[1],KEY=KYZ1$)TMPFM0$
7830 IF RECORD$(265,9)=TMPFM0$(214,9) THEN RECORD$(1,4)=TMPFM0$(12,4); GOTO *RETURN
7850 GOTO 7810
7890 RETURN 
7900 REM "Open ST_LOG file, create if not there
7910 CLOSE (14); OPEN LOCK (14,ERR=*NEXT)"ST_LOG"; GOTO 7940
7915 IF ERR=12 THEN SERIAL "ST_LOG"; GOTO 7910
7920 CALL "ZZPROM",".4",X3$,Z,"Log file could not be opened, please contact TopForm Support.","","",0; EXITTO 9900
7940 RETURN 
7949 ! 
7950 PRINT_MSG:
7960 PRINT (14)MESSAGE$
7990 PRINT_MSG_END:RETURN 
7995 ! 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8107 T=0
8113 CALL "ZZ2FNC;SERIALRECCNT",Z[10],T
8115 PRINT @(0,11),"There are "+STR(T)+" records to process"
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8135 T1=0
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,I9
8195 RETURN 
8199 ! 
8910 DEF FND$(Z9$)=Z9$(1*2+1,2)+"/"+Z9$(7-1*2,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "End
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9915 CLOSE (14)
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 BEGIN ; SETESC 9350
9950 RUN "ZMENU"
9999 END 
13150 GET_COUNTRY:! SSP307308-DBSPT-99148-DBD-137 copied from EC3WS1
13153 IF STP(COUNTRY_IN$,2)="" THEN COUNTRY_OUT$=DIM(2); RETURN 
13155 DEFAULT_COUNTRY$=OP_PARM$(344,2); DIM ZY8$(450),ZY8[10]
13160 CALL "ZYGZY8;LOOKUP_COUNTRY",ERR=*NEXT,X3$,X4$,COUNTRY_IN$,ZY8$,ZY8{ALL}; GOTO 13165
13161 IF ERR=96 THEN DIM ZY8$(450); GOTO 13165 ELSE GOTO 9000 ! error 96 is country not found
13165 IF MID(ZY8$,1,2)=DIM(2) THEN COUNTRY_OUT$=DEFAULT_COUNTRY$ ELSE COUNTRY_OUT$=MID(ZY8$,1,2)
13195 RETURN 
13199 ! 
56000 REM "204631-Import shiptos from text file function not working
56001 REM "205524-Needs assistance with UAR M2         
56002 REM "208642-I need 190+ ship to addresses and codes transported to         
56004 REM "222321-Add format 03 to Shipto Import program (AR2ST0)
56005 REM "225083-There is a new program to create a new or update existing 
56008 REM "232434-Format 04 for Import Shiptos from Text File program AR2ST0
56010 REM "256282-AR2ST0, need to check for all blanks and tabs, if after     
56012 REM "307308-DBSPT-99148-DBD-137;AR2ST0 format 05 add Country field      
