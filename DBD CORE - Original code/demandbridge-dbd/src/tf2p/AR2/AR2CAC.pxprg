0010 REM "Cash Receipts Check Entry <AR2CAC>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 05/11/23 - 11.358978 - jvv - SSP# 307442
0037 REM "307442-DBD-369: Enabole access to Customer PO for prepayments      
0040 REM "Copyright 2023 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0085 IF L0=16 THEN L0=0
0090 ON L0 GOTO 0100,8300,8300,8100,1800,7100
0100 SETERR 9000
0110 X0$="AR2CAC",X1$="Cash Receipts Check Entry",K9$="",K9=0
0135 C9=-1,V9=-2
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0200 REM "
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1,LEN(A$)),A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7]
0320 IOLIST B$
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15]
0340 IOLIST D$,D[0],D[1],D[2],D[3],D[4],D[5],D[6],D[7],D[8],D[9],D[10],D[11],D[12],D[13],D[14]
0350 IOLIST N$,N[0],N[1],N[2]
0550 READ (Z[6],KEY=X3$(9,3)+"G/L")G0$
0600 REM "
0620 DIM D$(180),D[14]
0630 A1$=N1$+"        "
0700 REM "
0704 DIM A$(35+K9),A[7]
0705 PRINT @(0,12+V0),'CE',
0710 CALL "ZZPROM","X1AR2CAC",X3$,Z,"","","",0
0720 ON Z GOTO 9900,0950,0780,0750,0760,0770,7900
0750 REM "List invoices
0752 J0$=""
0755 CALL "AR2CAD",X3$,X4$,Q0$,N1$(11,10),"",J0$,0,0,0,"","",0,""; IF J0$="" THEN GOTO 0700 ELSE B0=1; GOSUB 6000; A$(1,26+K9)=K9$+A1$,A$(27+K9,8)=J0$; GOSUB 6472; X0=1,C0=0; GOTO 1460
0760 REM "Put on acct
0762 IF N[2]=0 THEN GOTO 0700
0765 CALL "AR2CAH",X3$,X4$,N$,N[2],"",0
0769 N[2]=0; PRINT @(65,10),0:M0$,; WRITE (Z[1],KEY=N1$)IOL=0350; GOTO 0700
0770 REM "Use on acct
0775 CALL "AR2CAH",X3$,X4$,N$,N0,"",1
0779 N[2]=N[2]+N0; PRINT @(65,10),N[2]:M0$,; WRITE (Z[1],KEY=N1$)IOL=0350; GOTO 0700
0780 REM "Auto distribute
0783 N0=N[2]
0785 CALL "AR2CAH",X3$,X4$,N$,N0,M$(17,6),2
0789 N[2]=N0; PRINT @(65,10),N[2]:M0$,; WRITE (Z[1],KEY=N1$)IOL=0350; GOTO 0700
0950 B0=1; GOSUB 6000
1000 REM "
1010 GOSUB 7000; REM CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1035 DIM A$(35+K9),A[7]
1040 IF V9<>-1 THEN GOSUB 6900
1045 A$(1,26+K9)=K9$+A1$,A6$=""
1050 GOSUB 6472
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1440,1500
1440 REM "Invoice number
1445 J$=A$(11+K9,10),X$="To set up pre-payment, enter the order# & press F10. F8=lookup by Order#"
1450 CALL "ZZENTR","AUXF",A{ALL},A$,X4$,X3$,25,12+V0,27+K9,8,C0,"C/R","{3"+X$,J$,"AR2CAC12","/AR2CAD","",""; IF C0=-17 THEN GOTO 1451 ELSE IF ABS(C0)=7 THEN GOTO 8450 ELSE IF ABS(C0)>4 THEN B0=1; GOSUB 6000; GOSUB 6472; J$=A$(11+K9,10); ON C0 GOTO 1450,1451 ! SSP#220872
1452 IF C0=-17 THEN C0=1,CUST$=A$(11+K9,10),LIST$=""; DIM INV$(8); CALL "AR2ORD",X3$,X4$,CUST$,INV$,LIST$; IF LIST$="" THEN GOTO 1440 ELSE A$(27+K9,8)=INV$; PRINT @(25,12+V0),A$(27+K9,8); REM "WO111625, Lookup by order#
1455 IF C0=-2 OR C0=-4 THEN GOTO 0700
1460 EXTRACT (Z[4],KEY=A$(11+K9,10)+A$(27+K9,8),DOM=1450)IOL=0340
1462 IF ABS(C0)=2 AND LEN(A1$)>20 THEN A1$=A1$(1,20)
1465 GOSUB 7300
1480 GOTO 1120
1500 REM "
1505 Z9=-1
1510 A1$=K9$+A$(LEN(K9$)+1,34),A$(1,LEN(K9$))=K9$
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1525 Z9=0
1530 REM "Adjust Cash in progress
1535 D[5]=D[5]-A[1]
1540 PRINT @(65,12+V0),D[3]-D[5]:M0$,
1590 GOTO 1700
1600 REM "
1620 IF C0<>-1 THEN GOTO 1650 ELSE CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630,6975
1630 REM "Clear z(4) extract
1631 READ (Z[4],END=1632)
1635 GOTO 1000
1660 IF V9>=0 THEN GOSUB 6200
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1820 CALL "ZZPROM",".6",X3$,Z,"","","V",X0; ON Z GOTO 1850,6500,6700
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310
1865 C9=1
1870 GOSUB 6200
1890 REM IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF C0>3 OR X0<0 THEN GOTO 1800
2035 REM IF X0*6+11<LEN(V9$) THEN IF V9$(X0*6+11,1)="2" THEN GOTO 2020
2040 ON X0 GOTO 2050,2100,2150,2350,2200,2250,2300,2400,1800
2050 REM "Customer PO
2052 IF MID(D$,19,1)="P" THEN GOTO *NEXT ELSE GOTO 2020
2055 DIM CUST_PO$(15); CUST_PO$=D$(36,15) ! SSP 307442
2060 CALL "ZZENTR","S",A{ALL},CUST_PO$,X4$,X3$,25,19+V0,1,15,C0,"","{3"+X$,"","AR2EAB26","","",""; IF NUL(CUST_PO$) THEN CUST_PO$="Prepayment" ! SSP 307442
2062 D$(36,15)=CUST_PO$ ! SSP307442
2065 GOTO 2020 ! SSP 307442
2100 REM "Action to take
2105 A2$="0",X$=""; PRINT @(47,13+V0),"0",
2106 GOSUB 8400
2110 CALL "ZZENTR","ZX",A{ALL},A2$,X4$,X3$,47,13+V0,1,1,C0,"","{8"+X$,"","AR2CAC14","","",""
2111 IF C0=-1 THEN C0=1,A2$="9"
2112 IF C0=-2 THEN GOTO 2020 ! SSP 307442
2114 PRINT @(47,13+V0)," ",
2115 IF C0<0 THEN IF POS(A6$="0123456789")=0 THEN GOTO 2110 ELSE PRINT @(0,22),'CE',; GOTO 6700
2120 GOSUB 7600; IF A2$="8" THEN PRINT @(0,22),'CE',; GOTO 2020 ELSE GOTO 2110
2135 PRINT @(0,22),'CE',
2140 GOTO 2020
2150 REM "Total applied
2160 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,65,13+V0,1,822,C0,M0$,X$,"","AR2CAC16","","",""
2170 IF C0>=0 THEN PRINT @(65,21+V0),D[3]-D[5]-A[1]:M0$,
2190 GOTO 2020
2200 REM "Discount allowed
2210 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,65,16+V0,2,822,C0,M0$,X$,"","AR2CAC18","","",""
2240 GOTO 2020
2250 REM "Freight allowed
2255 IF G0$(40,1)="C" THEN GOTO 2020
2260 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,65,17+V0,3,822,C0,M0$,X$,"","AR2CAC20","","",""
2290 GOTO 2020
2300 REM "Sales tax allowed
2305 IF G0$(40,1)="C" THEN GOTO 2020
2310 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,65,18+V0,4,822,C0,M0$,X$,"","AR2CAC22","","",""
2340 GOTO 2020
2350 REM "G/L adjustment
2355 GOTO 2020
2360 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,65,15+V0,5,822,C0,M0$,X$,"","AR2CAC24","","",""
2390 GOTO 2020
2400 REM "Cash posted
2405 N[2]=N[2]+A[6]
2410 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,65,19+V0,6,822,C0,M0$,X$,"","AR2CAC26","","",""
2415 IF N[2]<0 AND A[6]<N[2] OR (N[2]>=0 AND A[6]>N[2]) THEN GOSUB 7800; GOTO 2410
2420 N[2]=N[2]-A[6]; PRINT @(65,10+V0),N[2]:M0$,
2440 GOTO 2020
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6010 IF B0=1 THEN B0=0; GOTO 6090
6060 PRINT @(5,3+V0),"Fiscal year:",@(35,3+V0),"Accounting period:",@(7,4+V0),"Bank code:",@(0,5+V0),Z0$,
6070 PRINT @(0,6+V0),"Deposit no",@(12,6+V0),"Deposit date",@(27,6+V0),"Discounts past",@(48,6+V0),"Deposit amount",@(65,6+V0),"To distribute",@(27,7+V0),"due:",@(0,8+V0),Z0$,
6080 PRINT @(0,9+V0),"Customer:",@(22,9+V0),"Reference:",@(40,9+V0),"Date:",@(57,9+V0),"Amount:",@(50,10+V0),"To distribute:",@(0,11+V0),Z0$,
6085 IF Q0$="*" THEN GOTO 6165
6090 PRINT @(9,12+V0),"Invoice number:",@(52,12+V0),"Balance due:",@(11,13+V0),"Invoice date:",@(39,13+V0),"Action:",@(50,13+V0),"Total applied:",@(7,14+V0),"Invoice due date:",@(6,15+V0),"Discount due date:",@(66,14+V0),"------------- ",@(49,15+V0),"G/L adjustment:",@(0,16+V0),"Cash discount remaining:",@(47,16+V0),"Discount allowed:",@(9,17+V0),"Freight billed:",@(7,18+V0),"Sales tax billed:",@(4,19+V0),"Customer P/O number:",@(0,20+V0),"Original invoice amount:",@(52,19+V0),"Cash posted:",@(66,20+V0),"-------------",@(52,21+V0),"New balance:",
6095 IF G0$(40,1)<>"C" THEN PRINT @(48,17+V0),"Freight allowed:",@(46,18+V0),"Sales tax allowed:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6472
6220 PRINT @(65,13+V0),A[1]:M0$,@(65,15+V0),A[5]:M0$,@(65,16+V0),A[2]:M0$,@(65,19+V0),A[6]:M0$,
6225 IF G0$(40,1)<>"C" THEN PRINT @(65,17+V0),A[3]:M0$,@(65,18+V0),A[4]:M0$,
6230 GOSUB 7420
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6450; GOSUB 7300; GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6460 GOSUB 7200
6472 CALL "ZZDISP","A",A$(27+K9,8),"AR6",X3$,"","",25,12+V0,X4$
6490 RETURN 
6500 REM "DELETE
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6515 N[2]=N[2]+A[6]; PRINT @(65,10+V0),N[2]:M0$,
6520 WRITE (Z[4],KEY=A$(11+K9,10)+A$(27+K9,8))IOL=0340
6530 WRITE (Z[1],KEY=N1$)IOL=0350
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6610 REM FIND (Z(3),KEY=A$(11+K9,10),DOM=6611) IOL=0330
6690 RETURN 
6700 REM "ADD ENTRY
6705 IF A[0]=0 AND A[1]=0 AND A[2]=0 AND A[3]=0 AND A[4]=0 AND A[5]=0 AND A[6]=0 THEN REMOVE (Z[1],KEY=A1$,DOM=6790); GOTO 6720
6708 IF A[2]+A[3]+A[4]+A[5]+A[6]<>A[1] THEN GOTO 8200
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6715 D[5]=D[5]+A[1]
6720 WRITE (Z[4],KEY=A$(11+K9,10)+A$(27+K9,8))IOL=0340
6730 WRITE (Z[1],KEY=N1$)IOL=0350
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6810 A1$=KEY(Z[1],END=6895)
6815 GOSUB 6600
6819 IF LEN(A$)<LEN(A1$) THEN DIM A$(35+K9),A[7]
6820 A$(1,LEN(A1$))=A1$
6825 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 1140
6830 GOSUB 6600
6835 GOSUB 6450
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
6900 REM "GET DEFAULTS
6930 RETURN 
6950 REM "WRITE DEFAULT
6955 WRITE (Z[6],KEY=X0$+"DFLT")V9$,IOL=0310
6960 GOSUB 6920; GOTO 1630
6970 REM "ENTRY DEFAULTS
6975 PRINT 'CF'; V9=-2,Q$=""
6980 FOR X=1 TO LEN(V9$) STEP 6
6982 PRINT @(NUM(V9$(X,2)),NUM(V9$(X+2,2))),; IF V9=-2 THEN PRINT V9$(X+4,1), ELSE READ RECORD (0,SIZ=1)Q$; IF POS(Q$="1234Z")=0 THEN PRINT 'RB'; GOTO 6982
6990 IF Q$="Z" THEN EXITTO 6950 ELSE V9$(X+4,LEN(Q$))=Q$; NEXT X
6995 V9=0; GOTO 6980
7000 REM "Clear input fields"
7010 PRINT @(25,12+V0),Z1$,@(65),Z1$,@(25,13+V0),Z1$,@(65),Z1$,@(25,14+V0),Z1$,@(25,15+V0),Z1$,@(65),Z1$,@(25,16+V0),Z1$,@(65),Z1$,@(25,17+V0),Z1$,@(65),Z1$,@(25,18+V0),Z1$,@(65),Z1$,@(25,19+V0),Z1$,@(65),Z1$,@(25,20+V0),Z1$,@(65,21+V0),Z1$,
7040 RETURN 
7200 REM "Display data @ top of screen
7230 PRINT @(18,3+V0),M$(1+K9,4),@(54,3+V0),M$(5+K9,2),
7240 PRINT @(59,3+V0),"Ending ",FND$(P1$(NUM(N$(5+K9,2))*6+15,6)),
7250 PRINT @(18,4+V0),M$(7+K9,3),@(35,4+V0),B$(4,30),@(4,7+V0),M$(10+K9,1),
7260 CALL "ZZDISP","D",M$(11+K9,6),"",X3$,"","",13,7+V0,X4$
7265 CALL "ZZDISP","D",M$(17+K9,6),"",X3$,"","",32,7+V0,X4$
7270 PRINT @(48,7+V0),M[0]:M0$,@(65,7+V0),M[1]:M0$,
7273 IF POS(" "<>N$(11+K9,10))=0 THEN PRINT @(10,9+V0),"Not on file", ELSE CALL "ZZDISP","A",N$(11+K9,10),"A/R",X3$,"","",10,9+V0,X4$
7274 PRINT @(33,9+V0),N$(21+K9,6),
7275 CALL "ZZDISP","D",N$(27+K9,6),"",X3$,"","",46,9+V0,X4$
7280 PRINT @(65,9+V0),N[1]:M0$,@(65,10+V0),N[2]:M0$,@(10,10+V0),N$(33+K9,35),
7290 RETURN 
7300 REM "Display an invoice
7305 IF POS(" "<>A$(27+K9,8))=0 THEN GOTO 7390
7310 PRINT @(65,12+V0),D[3]-D[5]:M0$,@(25,13+V0),FND$(D$(20,6)),@(25,14+V0),FND$(D$(54,6)),@(25,15+V0),FND$(D$(60,6)),@(25,16+V0),D[4]:M0$,@(25,17+V0),D[8]:M0$,@(25,18+V0),D[7]:M0$,@(25,19+V0),D$(36,15),@(25,20+V0),D[6]:M0$,
7390 RETURN 
7400 REM "Compute & display cash posted & new balance
7410 N[2]=N[2]+A[6],A[6]=A[1]-A[2]-A[3]-A[4]-A[5],N2=N[2],N[2]=N[2]-A[6]
7415 IF N2<0 AND N[2]>0 OR (N2>=0 AND N[2]<0) THEN GOSUB 7800; GOSUB 7470; GOSUB 8400; GOTO 7440
7420 PRINT @(65,10+V0),N[2]:M0$,@(65,19+V0),A[6]:M0$,@(65,21+V0),D[3]-D[5]-A[1]:M0$,
7440 RETURN 
7450 REM "Save values
7460 DIM Q[5]; FOR I=2 TO 5; Q[I]=A[I]; NEXT I
7465 RETURN 
7470 REM "Restore values
7474 N[2]=N[2]+A[6]
7475 FOR I=1 TO 5; A[I]=Q[I]; NEXT I
7476 A[6]=A[1]-A[2]-A[3]-A[4]-A[5],N[2]=N[2]-A[6]
7480 GOSUB 6220
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7600 REM "Take action based on action flag A2$
7610 GOSUB 7450
7670 REM "Clear buckets for new amounts unless manual entry(8) or exit (9)
7671 REM "Don't clear A(6), cash posted so that distribution can be balance properly, A(6) will be set properly by exiting through 7799
7675 A6$=A2$
7680 IF POS(A2$="89")=0 THEN A[1]=0,A[2]=0,A[3]=0,A[4]=0
7690 ON POS(A2$="123456789") GOTO 7700,7710,7720,7730,7740,7750,7760,7770,7780,7790
7700 REM "Full credit; allow available discount
7705 A[1]=D[3]-D[5]; GOSUB 7950
7709 GOTO 7799
7710 REM "Full credit; allow remaining discount
7715 A[1]=D[3]-D[5],A[2]=D[4]
7719 GOTO 7799
7720 REM "Full credit; disallow discount
7725 A[1]=D[3]-D[5]
7729 GOTO 7799
7730 REM "Full credit; allow sales tax
7735 A[1]=D[3]-D[5]; GOSUB 7950; IF G0$(40,1)<>"C" THEN A[4]=D[7]
7739 GOTO 7799
7740 REM "Full credit; allow freight
7745 A[1]=D[3]-D[5]; GOSUB 7950; IF G0$(40,1)<>"C" THEN A[3]=D[8]
7749 GOTO 7799
7750 REM "Partial credit; leave sales tax open
7755 A[1]=D[3]-D[5]-D[7]
7759 GOTO 7799
7760 REM "Partial credit; leave freight open
7765 A[1]=D[3]-D[5]-D[8]
7769 GOTO 7799
7770 REM "Apply distribution amount
7775 A[1]=N[2]
7779 GOTO 7799
7780 REM "Manual entry
7785 RETURN 
7789 GOTO 7799
7790 REM "Exit
7794 PRINT @(0,22),'CE',; EXITTO 6700
7795 RETURN 
7799 GOSUB 6220; GOSUB 7400; RETURN 
7800 REM "Not enough cash
7820 CALL "ZZPROM",".4",X3$,Z,"Action requires too much cash!","","",0
7845 RETURN 
7900 REM "Do G/L distribution for checks
7910 Q9=1,V9=2,A0=N[0],T[0]=N[2]
7920 GOTO 8010
7950 REM "Take available discount"
7960 IF D$(60,6)>M$(17,6) THEN A[2]=D[4]
7970 RETURN 
8000 REM "Run G/L overlay
8005 Q9=0,V9=4,A0=A[0],T[0]=-(A[1]-A[6]-A[5]-A[4]-A[3]-A[2]),A[5]=A[5]-T[0]
8010 PRINT (0,ERR=8011)@(0,14+V0),'CE','SB',
8020 X0$="AR2CAC",X3$(15,6)=M$(11,6),L0=1,F=Z[5],V1=0,V3=Z[7]
8050 RUN "GL2XAA"
8100 REM "Re-do Screen after G/L overlay
8105 V0=0
8110 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
8115 Q1$=Q0$,Q0$="*"; GOSUB 6000; Q0$=Q1$
8117 PRINT @(9,12+V0),"Invoice number:",
8120 GOSUB 6450
8190 GOTO 8010
8200 REM "Total applied <> Total of applied values
8210 CALL "ZZPROM","X0AR2CAC",X3$,Z,"","","",0
8220 ON Z GOTO 8000,2000
8300 REM "Return from lines
8305 V0=0
8306 IF Q9=1 AND L0=2 THEN PRINT @(0,12+V0),'CE',; RUN "AR2CAB"
8310 IF Q9=1 THEN N[0]=A0,N[2]=0; PRINT @(65,10+V0),N[2]:M0$,; WRITE (Z[1],KEY=N1$)IOL=0350; GOTO 0700 ELSE A[0]=A0
8330 PRINT @(0,12+V0),'CE',
8335 B0=1; GOSUB 6000; IF L0=2 THEN GOTO 6500 ELSE GOSUB 6470; GOSUB 7300; GOSUB 6200
8340 GOTO 1820
8400 REM "Action prompt"
8410 PRINT @(0,22),'CE',@(0,22),"0=Normal",@(20),"1=Allow discount;",@(40),"2=No discount;",; IF G0$(40,1)<>"C" THEN PRINT @(57,22),"3=W/O tax;",@(68),"4=W/O frt;",
8420 PRINT @(0,23),"5=Leave tax open;",@(20),"6=Leave frt open;",@(40),"7=Use Dist amt;",@(57),"8=Manual;",@(68),"9/RET=Exit;",
8440 RETURN 
8450 REM "Set up zero invoice
8451 IF POS(" "<>A$(27,8))=0 THEN GOTO 1440
8452 CALL "ZZDISP","A",A$(27,8),"AR6",X3$,"","",25,12+V0,X4$
8455 DIM D$(180),D[14]
8460 D$(1,18)=A$(11,10)+A$(27,8)
8470 FIND (Z[4],KEY=D$(1,18),DOM=8480)
8475 CALL "ZZPROM",".4",X3$,0,"Sorry, cannot set up since an invoice already exists with this number!","","",0; GOTO 1440
8480 CALL "ZZPROM",".Y",X3$,Z,"Verify the pre-payment invoice number you entered is correct","","",0; ON Z GOTO 8481,1440
8485 D$(19,1)="P",D$(20,6)=N$(27,6),D$(36,15)="Prepayment",D$(51,2)=C$(359,2),D$(54,6)=D$(20,6),D$(66,8)=C$(1,2)+D$(12,6),D$(74,4)=C$(278,4),D$(108,9)=C$(220,9),D$(117,10)=C$(235,10),D$(127,6)=N$(1,6),D$(133,6)=N$(1,6),D$(102,6)="CASHRC"
8490 WRITE (Z[4],KEY=D$(1,18))IOL=0340
8492 WRITE (Z[8],KEY=D$(1,10)+"B"+D$(20,6)+D$(11,8))
8493 WRITE (Z[9],KEY=D$(11,8)+D$(1,10))
8495 GOTO 1460
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9905 L0=6
9910 PRINT @(0,12+V0),'CE',
9950 RUN "AR2CAB"
9999 END 
56000 REM "220872-DBE - Alpha numeric order numbers will not allow you to     
56002 REM "307442-DBD-369: Enabole access to Customer PO for prepayments      
