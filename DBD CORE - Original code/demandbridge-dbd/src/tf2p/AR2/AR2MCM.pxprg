0010 REM "A/R Commission calculation program <AR2MCM>- MULTIPLE COMMISSIONS
0035 REM "5.7 - 01/26/10 - 15.583888 - jvv - SSP# 235855
0037 REM "235855-House Charge not being calculated correctly in Multi Comm   
0040 REM "Copyright 2010 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0070 REM "C0$=control,C$ & C(ALL)=commission stuff, C=calc comm amt,
0071 REM "S=sales amount, C0=cost, R=Rate
0072 REM "stmt 3080 modified 10/31/91 for 0 sell
0073 REM "House charge table logic added 8/10/92
0074 REM "Modified to do defaulting of comm code as well
0075 REM " C0='DEFAULT' means default comm code & return as C$
0076 REM "C$ input is PO Code (1,1)['*' means ignore P/O code], line type(2,1)('FICNS')+Company code (3,3)+Comm code from header or customer(6,6)
0077 REM "C is file slot of ZZPARM
0078 REM "3/14/96 -Invoice Header and Invoice Line passed in C0$ then split into A$ & B$  -LINk to storage charge, ALSO, X3$ is passed independently
0100 SETERR 9000
0110 ENTER C0$,C$,C{ALL},C,S,C0,MCKEY$,CMSUB,EXTRA$,HEADER{ALL},LINE{ALL},EXTRA{ALL},ERR=0111
0112 FLAG=0,P0=POS("[X3$B]"=C0$),P1=POS("[X3$E]"=C0$); IF P0>0 THEN X3$=C0$(P0+6,P1-P0-6),C0$=C0$(1,P0-1)+C0$(P1+6)
0115 P0=POS("[A$]"=C0$),P1=POS("[B$]"=C0$); IF P0>0 THEN A$=C0$(P0+4,P1-P0-4),B$=C0$(P1+4),C0$=C0$(1,P0-1)
0118 VCM$="" ! SSP 217566
0119 IGHC=0 ! SSP 304758
0120 HOLD_C$=C$
0150 ! PROCESS TO READ EACH COMMISSION CODE FROM AXS.
0170 DIM AC$(315),AS$[5](4," "),ASC$[10](5," "),AC[10],ASPLT[5],CADJ[5],ACMP[5]
0175 TOT_AC=0,SCD$="",TOT_GPADJ=0 ! SSP 235855
0180 INV_ONLY$=""
0300 REM 
0310 IOLIST Q$,Q[0],Q[1],Q[2],Q[3],Q[4],Q[5],Q[6],Q[7],Q[8],Q[9],Q[10],Q[11],Q[12],Q[13],Q[14],Q[15],Q[16]
0315 IOLIST AC$,AS$[1],AS$[2],AS$[3],AS$[4],AS$[5],ASC$[1],ASC$[2],ASC$[3],ASC$[4],ASC$[5],ASC$[6],ASC$[7],ASC$[8],ASC$[9],ASC$[10],AC[1],AC[2],AC[3],AC[4],AC[5],AC[6],AC[7],AC[8],AC[9],AC[10],ASPLT[1],ASPLT[2],ASPLT[3],ASPLT[4],ASPLT[5],ACMP[1],ACMP[2],ACMP[3],ACMP[4],ACMP[5],CADJ[1],CADJ[2],CADJ[3],CADJ[4],CADJ[5],ACMSELL,ACMCOST,ACMLADJ
0320 IOLIST ARA$,ARA[0],ARA[1],ARA[2],ARA[3],ARA[4],ARA[5],ARA[6],ARA[7],ARA[8],ARA[9],ARA[10],ARA[11],ARA[12],ARA[13]
0380 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15]
0500 REM "FILES                                                                 
0505 DIM Z[35],T[21]
0510 ZZ$="01O ARA... 02O AXS... 04O AR4... 13O ZZPARM... "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,ZZ$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0521 FIND (Z[13],KEY=X3$(9,3)+"AR1")PP8$; IF MID(PP8$,76,1)="Y" THEN IGHC=1 ! SSP 304758
0522 NO_READ=0
0523 PRECISION 3
0524 IF MCKEY$="NOREAD" THEN NO_READ=1,SCD$=EXTRA$; ASPLT[CMSUB]=EXTRA[15+CMSUB]; GOTO 0535
0525 FIND (Z[2],KEY=MCKEY$,DOM=9900)IOL=0315
0528 IF MCKEY$(20,3)="000" THEN INV_ONLY$="Y"
0530 FOR XX=1 TO 5; SCD$=SCD$+ASC$[XX]; NEXT XX
0535 IF STP(SCD$,2)="" THEN GOTO 9900
0540 FOR SLP=1 TO 5
0541 IF CMSUB>0 THEN IF CMSUB<>SLP THEN CONTINUE
0542 IF STP(SCD$(SLP*5-4,5),2)="" THEN CONTINUE
0544 C$=HOLD_C$,MCOM$=SCD$(SLP*5-4,5)
0545 IF C0$="DEFAULT" THEN F0$="*"; GOSUB 7500; GOTO 7600
0550 IF C0$="VENDSUB" THEN F0$="*"; GOSUB 7500; GOTO 7700 ! ssp 217566
0555 IF C0$="VENDCOM" THEN F0$="*"; GOTO 10000 ! SSP 217566
0556 IF S=0 AND C0=0 THEN AC[SLP]=0,AC[SLP+5]=0,LINE[11+(SLP-1)]=0; GOTO 0900 ! test if non line comm from AR2EAM
0558 CCD$=SCD$(SLP*5-4,5); DIM C$(31),C[15]; FIND (Z[4],KEY=CCD$,DOM=0900)IOL=0380
0560 IF LEN(C0$)<6 THEN C0$=C0$+" "; GOTO 0560
0565 IF LEN(C$)<44 THEN C$=C$+" "; GOTO 0565
0570 IF C0$(1,2)="TX" THEN A$=C0$(3),FLAG=1; REM " SSP# 166575
0575 GOSUB 7500
0580 IF C0$(1,2)="HC" OR C0$(1,2)="CC" THEN GOSUB 8200; CADJ[SLP]=Q2,TOT_GPADJ=TOT_GPADJ+Q2; GOTO 0900 ! SSP 235855
0590 S1=C
0595 C=0
0600 REM "Calculate commission
0605 REM "calculate amount(A)and rate R
0610 ON POS(C$(31,1)="GSPNICTQA") GOSUB 9900,1000,2000,3000,4000,5000,1200,1900,1250,3500,9900
0615 IF C$(31,1)="A" THEN ACMP[CMSUB]=LAST_RATE; GOTO 0800; REM " SSP# 166575
0620 IF POS(C$(6,1)="-!&+")>0 AND A<0 THEN IF C$(6,1)="-" THEN R=50 ELSE IF C$(6,1)="+" THEN R=25 ELSE R=100; REM "1st char of comm code desc = - means commission rate is 50%, !=100%. WO221484, if "+" rate is 25%.
0640 IF C$(6,1)="!" AND A<15 AND A>=0 THEN R=0
0650 IF C$(6,1)="<" AND ABS(A)<100 THEN R=30
0700 REM "Run through table
0710 C=A*R*.01; IF POS(C$(33,1)="123456789")>0 AND Q$(32,1)="Y" THEN C=C-Q3
0760 IF C<>0 AND A<>0 THEN IF SGN(C)<>SGN(A) THEN C=0
0770 ACMP[CMSUB]=R
0800 REM "Exit
0810 IF C$(31,1)<>"A" THEN %BRACKET$="",%HIGH_BRACKET$=DIM(5); REM " SSP# 166575
0812 IF C$(31,1)="A" THEN %LAST_RATE=LAST_RATE ELSE %LAST_RATE=R; REM " SSO# 166575
0813 IF C$(31,1)="A" AND C$(32,1)="L" THEN %LINE$="YES" ELSE IF C$(31,1)="A" AND C$(32,1)="I" THEN %LINE$="NO"; REM " SSP# 166575
0820 IF C>0 AND NUM(C$(35,10))>0 AND C<NUM(C$(35,10)) THEN C=0; REM "*** IF DOLLAR AMT < MIN DOLLAR AMT
0830 IF C$(31,1)="A" THEN GOTO 0900
0840 AC[SLP+5]=C ! COMM AMT FROM FROM COMM CODE
0845 AC[SLP]=AC[SLP+5]*ASPLT[SLP]*.01
0860 IF C$(32,1)="L" THEN LINE[11+(SLP-1)]=AC[SLP]
0865 IF C$(32,1)="I" THEN HEADER[21+(SLP-1)]=AC[SLP]
0900 NEXT SLP
0920 TOT_AC=0; FOR Y=1 TO 5; TOT_AC=TOT_AC+AC[Y]; NEXT Y ! SSP 235855
0930 IF NO_READ=0 THEN GOTO 0950
0935 EXTRA[CMSUB]=AC[CMSUB]; EXTRA[CMSUB+5]=AC[CMSUB+5]
0938 EXTRA[CMSUB+10]=CADJ[CMSUB]
0939 EXTRA[CMSUB+20]=ACMP[CMSUB]
0940 GOTO 0955
0950 IF F0$="*" THEN GOTO 0952 ELSE ACMSELL=LINE[4],ACMCOST=LINE[5],ACMLADJ=LINE[8]
0952 WRITE (Z[2],KEY=MCKEY$)IOL=0315
0955 C=TOT_AC
0958 IF C0$(1,2)="HC" OR C0$(1,2)="CC" THEN C=TOT_GPADJ ! SSP 235855
0960 GOTO 9900
1000 REM "By gross profit
1010 A=S-C0
1020 GOSUB 4100
1079 REM "Get rate R
1080 T=A; GOSUB 8000
1090 RETURN 
1200 REM "By cost - Fleet 4/29/90
1210 A=C0
1220 GOTO 1020
1250 REM "Q=Buckeye Special Type / uses House charge if entered!
1255 A=C0; GOSUB 4100; A=C0+(C0-A); REM "Must ADD to cost
1260 IF A<250 THEN L9=.55; GOTO 1300
1270 IF A<500 THEN L9=.6; GOTO 1300
1280 IF A<750 THEN L9=.65 ELSE L9=.7
1300 L9=A/L9; REM "List price now in L9
1320 IF L9<>0 THEN T=(L9-S)*100/L9 ELSE T=0
1330 IF T<1 THEN T=0
1340 GOSUB 8000
1345 A=S
1350 RETURN 
1900 REM "T= Lancer
1904 IF S=0 THEN A=-C0,R=50; GOTO 1934
1906 IF S1=0 THEN IF C0=0 THEN A=S,R=C[1]; GOTO 1934 ELSE S1=C0/.6
1910 PRECISION 4
1920 IF C0/S>.6 THEN S2=(100-C0*100/S)/2 ELSE IF ABS(S1)>ABS(S) THEN S2=C[1]-(100-S*100/S1)/2 ELSE S2=C[1]
1930 PRECISION 2; A=S+0,R=S2+0
1940 RETURN 
2000 REM "By sales
2010 A=S
2020 IF C$(33,1)=" " THEN C$(33,1)="S"
2030 GOSUB 4100
2079 REM "Get rate R
2080 T=A; GOSUB 8000
2090 RETURN 
3000 REM "Gross profit percentage based
3010 A=S-C0
3079 REM "Get rate R
3080 REM "IF S<> THEN LET T=A*100/S: GOSUB 8000 ELSE LET R=C[1]; THis line of code causes the Basic Commission rate to be used if the Sale Price is zero. Removed by SSP 117692 and line 3081 added to use the rate in the table based on the abs of the gp% 
3081 IF S<>0 THEN T=A*100/S; GOSUB 8000 ELSE T=A*100/1; GOSUB 8000
3084 REM "Take out house charge after figuring rate
3085 GOSUB 4100
3090 RETURN 
3500 REM " Calculate A type
3501 IF MID(A$,7,8)=DIM(8) OR LEN(A$)=0 THEN GOTO 3635
3505 %BRACKET$="0",LAST_RATE=0,TOTAL=0,%LAST_RATE=0,C=0
3506 A=S-C0
3507 GOSUB 4100
3508 TOTAL_TO_SPLIT=A
3509 FOR SPSL=SLP-1 TO SLP-1 ! !!
3510 SPSL$=A$(133+4*SPSL,4); SPSL_PERCENT=HEADER[SPSL+12]
3511 IF SPSL$=DIM(4) THEN EXITTO 3601
3512 A=SPSL_PERCENT*TOTAL_TO_SPLIT/100,AC[SPSL+6]=A
3513 IF A<0 THEN HOLD_A=A; A=ABS(A)
3525 READ (Z[13],KEY="STATAR3")PARMS$
3530 REM "READ SALESPERSON STATISTICS FILE
3531 IF C$(45,1)="N" THEN ARA_TOT=0; GOTO 3555
3532 HIGH_RATE=0,R=0,LAST_RATE=0,LOW_RATE=0,C=0,COMM_BASIS=0,TOTAL_GP=0
3535 Q0$="G"
3540 DIM K[14]
3541 Q=POS(Q0$=PARMS$(49),17); IF Q=0 THEN GOTO 3555 ELSE M0$=PARMS$(Q+48,17)
3542 CALL "ZZPACK",X3$,"R",M0$(3,2),"",0,0,K{ALL},Z[1],"G",SPSL$+A$(1,4),PARMS$
3550 ARA_TOT=0; I9=NUM(A$(5,2)); FOR I=1 TO I9 STEP 1; ARA_TOT=ARA_TOT+K[I]; NEXT I
3555 TOTAL_GP=A+ARA_TOT
3560 FOR I0=1 TO 15
3562 REM "IF C[I0]=0 OR C[I0]>ABS(TOTAL_GP) THEN R=C[I0-1]
3565 T[I0]=C[I0]; REM "T[I0-1]=C[I0-1]; R=0
3566 IF A>0 AND MOD(I0,2) THEN IF T[I0]<>0 THEN HIGH_RATE=T[I0]; %HIGH_BRACKET$=STR(INT(I0/2)+1)
3568 NEXT I0
3569 C=0,TOTAL=0
3570 IF A>0 THEN LOW_RATE=T[1]
3571 IF A<0 THEN INVOICE=ABS(A); REM "HIGH_RATE=T[1]
3572 IF A>0 AND ARA_TOT<T[2] THEN COMM_BASIS=T[2]-ARA_TOT END_IF ; IF A>0 AND A<COMM_BASIS THEN COMM_BASIS=A END_IF ; IF A>0 THEN C=COMM_BASIS*T[1]/100; %BRACKET$="1",LAST_RATE=T[1]; IF A>0 THEN TOTAL=COMM_BASIS
3573 IF A<0 THEN FIRST=14,LAST=2,STEP_TO_TAKE=-2 ELSE FIRST=2,LAST=14,STEP_TO_TAKE=2
3574 IF A=TOTAL THEN GOTO 3594
3575 FOR I0=FIRST TO LAST STEP STEP_TO_TAKE
3576 IF A>0 AND I0=14 AND T[16]=0 THEN EXITTO 3594
3577 IF A>0 AND T[I0+2]>0 AND TOTAL_GP>T[I0] AND TOTAL_GP<T[I0+2]-.01 THEN HIGH_RATE=T[I0+1]; REM "EXITTO 3594
3578 IF A>0 AND T[I0+2]>0 AND ARA_TOT>=T[I0] AND TOTAL_GP>=T[I0] AND TOTAL_GP<T[I0+2]-.01 THEN C=C+A*T[I0+1]/100; TOTAL=TOTAL+A; %BRACKET$=STR((I0/2)+1); LAST_RATE=T[I0+1]
3579 IF A>0 AND T[I0+2]>0 AND ARA_TOT<=T[I0] AND TOTAL_GP>=T[I0] AND TOTAL_GP>=T[I0+2]-.01 THEN C=C+((T[I0+2])-T[I0])*T[I0+1]/100,LAST_RATE=T[I0+1],TOTAL=TOTAL+(T[I0+2])-T[I0]; %BRACKET$=STR((I0/2)+1)
3580 IF A<0 AND T[I0]>0 AND ARA_TOT>T[I0] AND T[I0+2]=0 THEN C=C+INVOICE*T[I0+1]/100,LAST_RATE=T[I0+1]; GOTO 3582
3581 IF A<0 AND T[I0]>0 AND ARA_TOT>=T[I0] AND ARA_TOT<T[I0+2] THEN BASIS=T[I0]-ARA_TOT; IF BASIS>=INVOICE THEN BASIS=INVOICE END_IF ; C=C+(BASIS*-1)*T[I0+1]/100,LAST_RATE=T[I0+1],%BRACKET$=STR((I0/2)+1)
3582 IF A<0 THEN ARA_TOT=ARA_TOT+BASIS
3585 INVOICE=INVOICE-ABS(BASIS)
3590 IF A<0 AND INVOICE=0 THEN EXITTO 3594
3592 NEXT I0
3594 IF A>0 AND A<>TOTAL THEN C=C+(A-TOTAL)*HIGH_RATE/100,LAST_RATE=HIGH_RATE,TOTAL=TOTAL+(A-TOTAL)
3595 IF A<0 AND INVOICE>0 THEN C=C+INVOICE*HIGH_RATE/100,LAST_RATE=HIGH_RATE,%BRACKET$=STR(INT(NUM(%HIGH_BRACKET$)))
3596 IF A>0 AND A<>TOTAL THEN C=C+(A-TOTAL)*T[1]/100,LAST_RATE=T[1]; IF %BRACKET$="" THEN %BRACKET$="1",LAST_RATE=T[1]
3597 IF HOLD_A<0 THEN A=HOLD_A,C=C*-1
3598 %ALL_BRACKET$=%ALL_BRACKET$+%BRACKET$; IF POS(C$(33,1)="123456789")>0 AND Q$(32,1)="Y" THEN C=C-Q3 ! SSP 235227
3599 IF C$(32,1)="L" THEN LINE[SPSL+11]=C,LINE[SPSL+16]=LAST_RATE ELSE IF C$(32,1)="I" THEN HEADER[SPSL+21]=C,HEADER[SPSL+26]=LAST_RATE END_IF ; LAST_RATE=0
3600 AC[SPSL+1]=C
3601 NEXT SPSL
3602 C=0
3616 IF C$(32,1)="L" THEN FOR COUNT=11 TO 15; C=C+LINE[COUNT]; NEXT COUNT
3617 IF C$(32,1)="I" THEN FOR COUNT=21 TO 25; C=C+HEADER[COUNT]; NEXT COUNT
3635 RETURN 
3636 Q=POS(Q0$=PARMS$(49),17); IF Q=0 THEN ESCAPE ELSE M0$=PARMS$(Q+48,17)
4000 REM "% of sales determined by gross profit table
4010 A=S-C0
4020 GOSUB 4100
4030 REM " GET RATE R
4079 REM "Get rate R
4080 IF S<>0 THEN T=A*100/S; GOSUB 8000 ELSE R=0
4085 IF POS(C$(6,1)="-!+")<>0 AND S-C0<0 THEN A=S-C0 ELSE A=S; REM "If using special 50% or 100% of gp on negative GP sales, then set amount to the gp amount so that the check comparing the sign of A to the sign of the commission will pass. Otherwise set to the sale amount. WO221484, "+" rate is 25%.
4086 IF POS(C$(6,1)="@")<>0 THEN IF S-C0<0 THEN A=0
4088 IF POS(C$(7,1)="#")>0 AND I0=2 AND S-C0>=0 THEN A=S-C0; REM "# In second position reverts 1st table entry to a GP calculation w/o house charge"
4090 RETURN 
4100 REM "CALC House Charge
4102 ORIG_A=A
4105 IF POS(C$(33,1)="123456789")>0 THEN GOSUB 8200; GOTO 4190
4110 IF C[0]=0 THEN GOTO 4190
4120 IF C$(33,1)="S" THEN A=A-S*C[0]/100; GOTO 4190
4130 IF C$(33,1)="F" THEN IF IGHC=1 THEN GOSUB 4300; GOTO 4190 ELSE A=A-C[0]; GOTO 4190 ! SSP 30475
4180 IF POS(C$(33,1)=" C")>0 THEN IF POS(MID(X3$,9,3)="603",3)>0 THEN A=A-(C0*100/(100-C[0])-C0) ELSE A=A-C0*C[0]/100
4190 REM "Calc Storage Charge
4210 IF C$(34,1)=" " OR FLAG=1 THEN GOTO 4290
4215 REM PRINT 'CS',"X3$=",X3$,'LF',"A$=",A$,'LF',"B$=",B$,; INPUT *
4220 CALL "AR2EAZ",X3$,A$,B$,P0,C$
4240 A=A-P0
4290 CADJ[CMSUB]=ORIG_A-A
4295 RETURN 
4300 REM "Determine if house charge on comm code is applicable if a/r parameter IGHC=1 (76,1)is set.
4305 IF STP(MID(B$,135,1),2)="" THEN GOTO 4330
4310 SSEQ=NUM(B$(135,1),ERR=*RETURN)
4320 IF SSEQ>1 THEN RETURN 
4330 A=A-C[0]
4350 RETURN 
4359 ! 
4845 AC[SLP]=AC[SLP+5]*ASPLT[SLP]*.01 ! COMM AFTER SALESMAN SPLIT
5000 REM "I type means Gp% rounded to .x times GP
5010 A=S-C0
5020 IF S<>0 THEN T=A*100/S
5079 REM "Get rate R
5080 R=T/10*10
5085 IF ABS(R)<30 THEN R=SGN(R)*30 ELSE IF ABS(R)>40 THEN R=SGN(R)*40
5086 IF SGN(R)<0 THEN C=A*ABS(R)*.01; EXITTO 9900
5090 RETURN 
7500 REM "Get AR4 file device and read new numerics
7501 REM "Replaced the following with FN%FFN to get last opened AR4 channel
7505 ! IF POS("7."=SYS)=0 THEN J9=100 ELSE J9=14
7510 ! FOR F0=1 TO J9
7520 ! Q$=FID(F0,ERR=7540); IF LEN(Q$)=2 THEN GOTO 7540 ELSE Q$=Q$(4,6)+Q$(21,2)
7530 ! IF POS("AR4"=Q$)=1 THEN EXITTO 7580
7540 ! NEXT F0
7545 F0=FN%FFN("AR4"+%C$); IF F0<>-1 THEN GOTO 7580
7550 X$="AR2COM cannot find the file slot on which AR4 is open"
7560 PRINT @(0,22),X$,; INPUT 'RB',*
7570 GOTO 9900
7580 IF F0$<>"*" THEN DIM D[6]; X$="Missing Commission Code "+C$(1,5); READ (F0,KEY=C$(1,5),DOM=7570)*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,D[0],D[1],D[2],D[3],D[4],D[5],D[6]
7590 RETURN 
7600 REM "Default Commission code
7601 REM "Mod cwj 4/27/98 SSP# 096774
7602 REM "Break C$ into P0$-P/O code, T$-filetype and D$-default comm coder from header or customer,C9$-Company code
7605 P0$=C$(1,1),T$=C$(2,1),C9$=C$(3,3); IF LEN(C$)>5 THEN D$=MCOM$
7607 IF P0$<>"*" THEN IF P0$<>" " AND POS(C9$="136148",3)=0 THEN T$="F"; REM "P0$='*'means came from line, don't mess with type, a non blank p/o code means use DIRect ship, unless it is company 136 which uses I substition even if DIRect ship.
7610 DIM C$(5); REM " return value in c$, default to blanks"
7612 FIND (F0,KEY=D$,DOM=7613); C$=D$; REM "SET COMM TO VALID DEFAULT"
7614 REM "Read A/R commission params
7615 READ (Z[13],KEY=C9$+"AR1")P$; REM "Find appropriate param based on line type"
7619 IF P0$="!" THEN X$=P$(70,5); GOTO 7621; REM "If ! then use DIRect ship comm code
7620 I=POS(T$="FICNS"); IF I=0 THEN X$="" ELSE X$=P$(32+I*5,5)
7629 REM "IF C$ is blanks, no default supplied so get from params IF: 1) line type gets a param comm code, 2) the comm code is not blank nor a sub type param (with '#'s), 3) the param comm code is in the comm code file
7630 IF X$>"" AND POS(" "<>X$)>0 AND POS("#"=X$)=0 THEN FIND (F0,KEY=X$,DOM=7631); C$=X$; GOTO 7695; REM "We can leave, we're done
7639 REM "Do substitution if needed
7640 IF X$>"" AND POS("#"=X$)<>0 THEN C1$=""; FOR I0=1 TO 5; IF X$(I0,1)<>"#" THEN C1$=C1$+X$(I0,1); NEXT I0 ELSE C1$=C1$+C$(I0,1); NEXT I0
7644 REM "Check newly created C1$, if ok use it, else keep c$ the same
7645 FIND (F0,KEY=C1$,DOM=7646); C$=C1$
7695 ASC$[SLP]=C$
7698 IF VCM$="Y" THEN RETURN ELSE GOTO 0900
7700 REM "Vendor code Substitution based on position in comm code(from A/R parm) and Substitution code (from AP Vendor Constant data) SSP 217566 (incl 7700-7750)
7710 DIM VC$(5)
7715 VC$=MCOM$
7720 VP=NUM(EXTRA$(1,1),ERR=7750),SUBP$=EXTRA$(2,1) ! VP -POSITION, SUBP$=SUB CODE
7730 VC$(VP,1)=SUBP$
7740 FIND (F0,KEY=VC$,DOM=7750); MCOM$=VC$
7750 ASC$[SLP]=MCOM$; GOTO 0900
8000 REM "Run value T though table to get R
8005 R=0
8010 FOR I0=2 TO 14 STEP 2
8015 IF C[I0]=0 OR C[I0]>ABS(T) THEN R=C[I0-1]; IF I0>2 THEN R=R+INT(ABS(T)-C[I0-2])*D[I0/2-2]; EXITTO 8030 ELSE EXITTO 8030
8020 NEXT I0
8025 R=C[15]+INT(ABS(T)-C[14])*D[6]
8040 RETURN 
8200 REM "Compute house charge
8210 Q2=0,Q3=0
8250 REM "Get House Charge Table
8260 IF LEN(C$)>=33 AND POS(C$(33,1)="1234567890")>0 THEN DIM Q[16]; X$="Missing House Charge Code "+C$(33,1); READ (F0,KEY="@"+C$(33,1)+"   ",DOM=7560)IOL=0310 ELSE GOTO 8290
8265 ON NUM(Q$(31,1),ERR=8235)-1 GOTO 8270,8350,8400,8450,8500,8550 ! SSP 234793
8270 REM "Cost Table
8275 Q1=C0; GOSUB 8300; Q2=Q2*C0*.01
8277 REM "Don't apply min/max by line if comm code is Inv Total Type
8278 IF C0$(1,2)="HC" AND C$(32,1)="I" THEN GOTO 8284
8280 IF Q[0]>0 AND Q2<Q[0] THEN Q2=Q[0] ELSE IF Q[16]>0 AND Q2>Q[16] THEN Q2=Q[16]
8284 IF C0$(1,2)="CC" THEN IF Q$(32,2)="YY" THEN GOTO 8290 ELSE Q2=0; GOTO 8290
8285 IF Q$(32,1)<>"Y" THEN A=A-Q2 ELSE IF Q$(33,1)<>"Y" THEN Q3=Q2,Q2=0 ELSE Q2=0
8290 RETURN 
8300 REM "Run value Q1 though table to get Q2
8305 Q2=0
8310 FOR I0=2 TO 14 STEP 2
8315 IF Q[I0]=0 OR Q[I0]>ABS(Q1) THEN Q2=Q[I0-1]; EXITTO 8330
8320 NEXT I0
8325 Q2=Q[15]
8340 RETURN 
8350 REM "House chg based on number of cartons
8360 Q1=0,Q1=NUM(C0$(3,4),ERR=8361)
8370 GOSUB 8300; Q2=Q1*Q2
8390 GOTO 8280
8400 REM "Based on line items
8410 IF POS(" "<>C0$(3,4))>0 THEN Q1=1
8420 GOSUB 8300; Q2=Q1*Q2
8440 GOTO 8280
8450 REM "Based on sales
8460 Q1=S; GOSUB 8300; Q2=Q2*S*.01
8490 GOTO 8280
8500 REM "Based on gross profit % table then charge % of sale amount
8505 A=S-C0
8507 IF S<>0 THEN Q1=A*100/S ELSE Q1=0; REM "was A/S*100 Caused rounding prob
8510 GOSUB 8300
8515 Q2=Q2*S*.01
8520 GOTO 8280
8550 REM "(7)-based on gross profit % table then charge % of Adj GP" ;!SSP 234793
8555 A=S-C0,Q1=A ! SSP 234793
8556 IF C$(32,1)="I" THEN GOTO 8560 ! SSP 234793 - bypass this storage charge calculation if by Invoice (See AR2EAL -does call to AR2EAO for storage at inv level)
8558 IF C$(34,1)<>" " THEN CALL "AR2EAZ",X3$,A$,B$,P0,C$; Q1=Q1-P0 ! SSP 234793
8560 GOSUB 8300; Q2=Q2*Q1*.01 ! SSP 234793
8570 GOTO 8280 ! SSP 234793
9000 REM "errors
9005 PRINT @(0,20),'CL',"ERR=",ERR," LINE=",TCB(5),; INPUT *
9010 EXIT ERR
9900 REM "End
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Y{ALL},0,0
9920 PRECISION 2
9930 EXIT 
9999 END 
10000 VENDOR_COMM_SUB:! SSP 217566 Vendor comm code substitution
10005 ! This routine is performed from order enter and/or change of vendor in PO entry if the AR parameter "Vendor Commission code Substitution" is set. Default commission is recalculated and if the Vendor has a "Position for Vendor Designator in Comm Code " set, the modified commission code will be returned.
10010 CFS2$=EXTRA$
10015 ORDER_DIV$=CFS2$(147,2),ORDER_NUM$=CFS2$(149,6),PO_CODE$=CFS2$(9,1),PRODUCT_CODE$=CFS2$(38,5),VENDOR$=CFS2$(94,10),COMM_CODE$=CFS2$(128,5),PROD_CODE$=CFS2$(29,3) ! SSP 217566
10016 LINE_TYPE$=CFS2$(155,1)
10018 DIM Y[35]
10020 YY$="01O FS1... 02O AP4... 03O ASQ... 04O AR4... 05O FMP... 13O ZZPARM... "
10025 CALL "ZZFLES",X3$,Y1$,Y0$,YY$,Y{ALL},Z0,Z1; ON Z0 GOTO 10030,9900
10035 FIND (Y[13],KEY=%C$+"AR1")CAR1$
10038 FIND (Y[13],KEY=%C$+"A/R")CAR2$ ! SSP 229014
10040 FIND (Y[13],KEY=%C$+"F/M")CFM$
10060 CORDER$=ORDER_DIV$+ORDER_NUM$
10120 FIND (Y[1],KEY=CORDER$,ERR=*NEXT,DOM=*NEXT,REC=FSC1$)
10130 IF NOT(NUL(FSC1.COMM_CODE$)) THEN COMM_CODE$=FSC1.COMM_CODE$
10135 COMM_CODE$=MCOM$
10140 COMM_PARM$=PAD(PO_CODE$,1,1)+PAD(LINE_TYPE$,1,1)+X3$(9,3)+PAD(COMM_CODE$,5,1)
10142 GOSUB 7500
10145 VCM$="Y"; C$=COMM_PARM$,C=Y[13]; GOSUB 7600; VCM$="",COMM_CODE$=C$
10150 DIM PRODUCT_CODE$(100); FIND (Y[5],KEY="T"+PROD_CODE$,DOM=*NEXT)PRODUCT_CODE$
10160 IF MID(CFM$,186,1)="Y" THEN IF NOT(NUL(MID(PRODUCT_CODE$,38,5))) THEN COMM_CODE$=MID(PRODUCT_CODE$,38,5)
10170 IF MID(CAR2$,122,1)="Y" THEN FIND (Y[3],KEY=FSC1.SPER_CODE$+PROD_CODE$,DOM=*NEXT)ASQ$; COMM_CODE$=MID(ASQ$,8,5) ! SSP 229014
10175 IF STP(PO_CODE$,2)="" THEN GOTO 10290
10180 CALL "ZZWLKU;PARSE_VEND",VENDOR$,VEND_DIV$,VEND_CODE$ ! SSP 217566
10190 DIM APC4$:IOL(Y[2]); FIND (Y[2],KEY=VEND_DIV$+VEND_CODE$,REC=APC4$,DOM=10290)
10200 IF STP(APC4.COMM_SUB_CODE$,2)="" THEN GOTO 10290 ! SSP 217566 
10210 DIM VC$(5)
10215 VC$=COMM_CODE$
10220 VP=NUM(CAR1$(75,1),ERR=10290),SUBP$=APC4.COMM_SUB_CODE$ ! VP -POSITION, SUBP$=SUB CODE
10225 IF VP>0 THEN VC$(VP,1)=SUBP$
10230 FIND (Y[4],KEY=VC$,DOM=10290); COMM_CODE$=VC$
10350 C$=COMM_CODE$ ! SSP 217566
10355 ASC$[SLP]=C$
10360 GOTO 0900
56002 REM "205455-Oracle - FFN usage in ZZFLES to be replaced with FN%FFN     
56004 REM "217566-Commission substitution by vendor to allow certain house    
56006 REM "221484-Commission option for negative, "+" in first character of
56007 REM "225752-Calculation for GP Adj (Addon) not working as DSI wants     
56009 REM "229014-Omitting the Add on from Postage Product code               
56010 REM "234793-New House Charge Method, like method 5, but apply to GP amt 
56011 REM "235855-House Charge not being calculated correctly in Multi Comm   
