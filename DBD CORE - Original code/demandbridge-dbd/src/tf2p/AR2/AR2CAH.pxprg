0010 REM "Put and use on acct and auto distribute <AR2CAH>"
0035 REM "5.7 - 07/07/10 - 8.410277 - tma - SSP# 239247
0037 REM "239247-May Trial Balance is showing a lower balance. Unavailable   
0040 REM "Copyright 2010 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,N$,N,D0$,Q0
0091 REM "Q0=0, put on acct, =1 get off acct, =2 auto distribute
0100 SETERR 9000
0110 X0$="AR2CAH",Q0$=""
0140 M0$="##,###,###.00-",M1$="-##,###,###.00"
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNS$(Z9$)=Z9$(1,POS("  "=Z9$+"  ")-1)
0300 REM "FILES
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14]
0320 IOLIST B$
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6]
0340 IOLIST D$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X ARP...  02O AR6...  03O AR1...  04O ARG...  05O ARN...  06O ASU...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0585 W9$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,W9$
0725 READ (Z[13],KEY=X3$(9,3)+"A/R",DOM=9800)P0$
0726 READ (Z[13],KEY=X3$(9,3)+"AR3",DOM=*NEXT)P3$ ! 232358
0900 REM 
0910 DIM A[14]
0920 K$=N$(11,10)+"ONACCNT "
0950 ON Q0 GOTO 1000,4000,5000,9900
1000 REM "Is there an on-acct invoice already?
1005 Q1$="ONACCNT ",D=0
1010 EXTRACT (Z[2],KEY=K$,DOM=2000)IOL=0310
1030 A[5]=A[5]+N,A$(20,6)=N$(27,6); IF A$(133,6)<N$(1,6) THEN A$(133,6)=N$(1,6) ! SSP#239247
1035 GOSUB 2100
1040 GOSUB 3000
1070 WRITE (Z[2],KEY=K$)IOL=0310
1075 IF W9$<>"" THEN TMP$=K$(1,10); CALL "RT2WOC",ERR=1076,X3$,X4$,TMP$,"AR6...","U",K$
1090 GOTO 9900
2000 REM "Create onacct invoice"
2010 DIM A$(159),A[14],B$(230)
2020 FIND (Z[3],KEY=N$(11,10),DOM=2021)IOL=0320
2030 A$(1,10)=N$(11,10),A$(11,8)="ONACCNT ",A$(19,1)="A",A$(20,6)=N$(27,6),A$(54,6)=A$(20,6),A$(127,6)=N$(1,6),A$(108,9)=B$(220,9)
2040 WRITE (Z[5],KEY=A$(1,10)+"B"+A$(20,6)+A$(11,8))
2090 GOTO 1030
2100 REM "Check customer category if necessary"
2102 RETURN 
2103 REM "Use customer's default for now
2105 IF P0$(19,1)<>"Y" THEN GOTO 2190
2110 PRINT @(12,20),'CE',"Customer category to use for G/L distribution:",
2115 CALL "ZZDISP","A",A$(108,9),"ARG",X3$,"","",12,21,X4$; GOSUB 2200
2120 CALL "ZZENTR","AU",A{ALL},A$,X4$,X3$,12,21,108,9,C0,"ARG","{3"+X$,"","AR2CAH02","","",""
2130 GOSUB 2200
2180 PRINT @(0,20),'CE',
2190 RETURN 
2200 REM "Display category 
2210 DIM D$(40)
2220 FIND (Z[4],KEY=A$(108,9),DOM=2240)IOL=0340
2230 PRINT @(22,21),D$(10,30),
2235 RETURN 
2240 PRINT @(22,21),D$(10,30),
2245 EXITTO 2120
3000 REM "Create cash receipt for transaction"
3005 REM "Q1$=invoice #, N=total amount applied, D=discount amount
3010 DIM C$(35),C[6]
3020 C$(1,26)=N$,C$(27,8)=Q1$
3030 READ (Z[1],KEY=C$(1,34),DOM=3031)IOL=0330
3040 C[1]=C[1]+N,C[6]=C[6]+N-D,C[2]=C[2]+D
3045 IF Q0=1 THEN IF C[1]=0 AND C[6]=0 THEN REMOVE (Z[1],KEY=C$(1,34),DOM=3090); GOTO 3090
3050 WRITE (Z[1],KEY=C$(1,34))IOL=0330
3090 RETURN 
4000 REM "Use on-acct,check to see if there then get amount & make c/r trans
4005 N=0,Q1$="ONACCNT ",D=0
4010 READ (Z[2],KEY=K$,DOM=4090)IOL=0310
4020 IF A[3]-A[5]=0 THEN GOTO 4090
4030 N0=-(A[3]-A[5]),N=-N0,A$(20,6)=N$(27,6); IF A$(133,6)<N$(1,6) THEN A$(133,6)=N$(1,6) ! SSP#239247
4040 GOSUB 3000
4050 N=N0,A[5]=A[5]-N0
4070 WRITE (Z[2],KEY=K$)IOL=0310
4075 IF W9$<>"" THEN TMP$=K$(1,10); CALL "RT2WOC",ERR=4076,X3$,X4$,TMP$,"AR6...","U",K$
4090 GOTO 9900
5000 REM "Auto distribute amount
5004 GOSUB 7000
5005 DIM INV_START$(8),INV_END$(8,"~"),ORD_START$(8),ORD_END$(8,"~"); RANGE_PICKED=0; IF MID(P0$,179,1)="Y" OR MID(P3$,7,1)="Y" THEN GOSUB 5150 ! 232358
5007 IF NOT(RANGE_PICKED) THEN GOSUB 5100
5008 GOSUB 5129 ! Always ask credit memo question
5009 N0=N,L=13,Q8$="",F1$=""; IF RANGE_PICKED=2 THEN ZINV=Z[6],ZKEY$=N$(11,10) ELSE ZINV=Z[5],ZKEY$=N$(11,10)+"B"
5010 READ (ZINV,KEY=ZKEY$,DOM=5011)
5020 K$=KEY(ZINV,END=5080); READ (ZINV); IF K$(1,10)<>N$(11,10) THEN GOTO 5080
5021 ON RANGE_PICKED GOTO 5022,5022,5023
5022 IF MID(K$,18,8)<INV_START$ OR MID(K$,18,8)>INV_END$ THEN GOTO 5020 ELSE GOTO 5024 ! Skip based on invoice number range
5023 IF MID(K$,11,8)<ORD_START$ OR MID(K$,11,8)>ORD_END$ THEN GOTO 5020 ELSE GOTO 5024 ! Skip based on order number range
5024 IF RANGE_PICKED=2 THEN AR6KEY$=K$(1,10)+K$(19,8) ELSE AR6KEY$=K$(1,10)+K$(18,8)
5025 DIM A$(285),A[15]; READ (Z[2],KEY=AR6KEY$,DOM=5020)IOL=0310
5027 IF A$(11,7)="ONACCNT" OR (POS(" "<>J$)>0 AND J$<>A$(160,8)) THEN GOTO 5020
5030 GOSUB 6000; C=A[3]-A[5]
5035 IF C=0 OR (C<0 AND F0$="N") THEN N=0,D=0; GOSUB 6050; GOTO 5020; REM "If 0 balance, or credit memo and we're not doing credit memos, just get next one
5040 IF A$(60,6)>=D0$ AND C>0 THEN D=A[4] ELSE D=0; REM "Don't take discounts on credit memos
5045 IF C-D<=N0 THEN N=C,N0=N0-N+D ELSE IF F1$<>"" OR F0$<>"Y" THEN N=N0,N0=0,D=0 ELSE GOSUB 5200; GOTO 5030; REM "pay off if able to, if not able to then if we've already checked for c/m's just partial pay, else check for c/m's and retry
5050 GOSUB 6050
5055 Q1$=A$(11,8); GOSUB 3000
5060 A[5]=A[5]+N; WRITE (Z[2],KEY=AR6KEY$)IOL=0310
5061 IF W9$<>"" THEN TMP$=K$(1,10); CALL "RT2WOC",ERR=5062,X3$,X4$,TMP$,"AR6...","U",AR6KEY$
5065 IF N0=0 AND (F0$="N" OR F1$<>"") THEN GOTO 5080 ELSE GOTO 5020
5080 N=N0
5085 CALL "ZZPROM",Q8$+"4",X3$,Z,"","","",0
5090 GOTO 9900
5100 REM "Summary bill feature
5110 DIM J$(8),J[1]
5115 PRINT @(0,22),'SB',"Enter summary bill ref to match or leave blank to apply to oldest 1st:",'SF',
5120 CALL "ZZENTR","SU",J{ALL},J$,X4$,X3$,70,22,1,8,C0,"",X$,"","AR2CAH02","","",""
5125 RETURN 
5129 REM "Set F0$ to indicate if we include Credit memos. Skip ? and set to yes if summary bill being done.
5130 IF POS(" "<>J$)<>0 THEN F0$="Y"; GOTO 5136 ELSE CALL "ZZPROM",".Y",X3$,Z0,"Add all credit memos to the distributed amount?","","",0
5135 IF Z0=1 THEN F0$="N" ELSE F0$="Y"
5140 RETURN 
5150 ! Pay based on invoice number range; WO 232358, pay based on order range
5151 IF TCB(88)=0 THEN MSGBOX "You must be using Windx to access this feature"; EXITTO 9900
5155 IF MID(P0$,179,1)<>"Y" AND MID(P3$,7,1)<>"Y" THEN RANGE_PICKED=0; RETURN 
5160 DISPLAY_INV_RANGE$=MID(P0$,179,1),DISPLAY_ORD_RANGE$=MID(P3$,7,1),SELECTED_RANGE$="",%CUSTOMER$=N$(11,10)
5165 PROCESS "ARGCAA.7","../ARG/AR.EN",INV_START$,INV_END$,ORD_START$,ORD_END$,DISPLAY_INV_RANGE$,DISPLAY_ORD_RANGE$,SELECTED_RANGE$
5170 IF NUL(INV_END$) THEN INV_END$=DIM(8,"~")
5175 IF NUL(ORD_END$) THEN ORD_END$=DIM(8,"~")
5180 IF NUL(INV_START$) AND INV_END$=DIM(8,"~") AND NUL(ORD_START$) AND ORD_END$=DIM(8,"~") THEN RANGE_PICKED=0; GOTO 5195
5185 IF SELECTED_RANGE$="O" THEN IF (NOT(NUL(ORD_START$)) OR ORD_END$<>DIM(8,"~")) THEN RANGE_PICKED=2 ELSE RANGE_PICKED=0 END_IF ; GOTO 5195
5187 IF SELECTED_RANGE$="I" THEN IF (NOT(NUL(INV_START$)) OR INV_END$<>DIM(8,"~")) THEN RANGE_PICKED=1 ELSE RANGE_PICKED=0 END_IF ; GOTO 5195
5195 RETURN 
5199 ! ********************************************************
5200 REM "Add credit memos into distributed amount
5205 F1$=K$,C9=0,C9$="-\|/"; PRINT @(0,L),'CL',@(20,20),"Checking for unapplied credit memos"
5210 K$=KEY(ZINV,END=5230); READ (ZINV); IF K$(1,10)<>N$(11,10) THEN GOTO 5230
5211 IF RANGE_PICKED=2 THEN AR6KEY$=K$(1,10)+K$(19,8) ELSE AR6KEY$=K$(1,10)+K$(18,8)
5212 DIM A$(285),A[15]; READ (Z[2],KEY=AR6KEY$,DOM=5210)IOL=0310; IF A$(11,7)="ONACCNT" OR (POS(" "<>J$)>0 AND J$<>A$(160,8)) THEN GOTO 5210
5214 C9=C9+1; PRINT @(56,20),C9$(MOD(C9,3)+1,1),
5215 C=A[3]-A[5]; IF C>=0 THEN GOTO 5210 ELSE N=C,D=0,N0=N0-N,Q1$=A$(11,8); GOSUB 3000; A[5]=A[5]+N; WRITE (Z[2],KEY=AR6KEY$)IOL=0310; GOSUB 6000; GOSUB 6050; IF W9$<>"" THEN TMP$=K$(1,10); CALL "RT2WOC",ERR=5216,X3$,X4$,TMP$,"U","AR6...",AR6KEY$
5220 GOTO 5210
5230 REM "Reset pos in file & continue
5231 IF RANGE_PICKED=2 THEN AR6KEY$=F1$(1,10)+F1$(19,8) ELSE AR6KEY$=F1$(1,10)+F1$(18,8)
5235 DIM A$(285),A[15]; READ (Z[2],KEY=AR6KEY$)IOL=0310; C=A[3]-A[5],K$=F1$; READ (ZINV,KEY=F1$,DOM=5236)
5240 PRINT @(65,10),N0:M1$,@(20,20),'CL',
5245 RETURN 
6000 REM "Display invoice
6010 IF L>18 THEN PRINT @(0,13),'CE',; L=13
6015 CALL "ZZDISP","A",A$(11,8),"AR6",X3$,"","",1,L,X4$
6020 PRINT @(19,L),FND$(A$(54,6)),@(35,L),FND$(A$(60,6)),@(50,L),A[3]:M0$,@(65,L),A[4]:M0$,
6040 RETURN 
6050 REM "Display action taken, inc. L
6055 IF A[3]-N-D<>0 THEN Q8$="."; PRINT @(5,L+1),"Left open: ",A[3]-N-D:M0$,
6060 PRINT @(65,10),N0:M1$,@(41,L+1),"Applied:",@(50,L+1),N:M0$,@(65,L+1),D:M0$,
6070 L=L+2
6090 RETURN 
7000 REM "Auto dis. background
7005 PRINT (0,ERR=7006)@(0,12),'CE','SB',
7020 PRINT @(1,12),"Invoice no.",@(20),"Due date",@(35),"Disc. date",@(50),"Inv. balance",@(65),"Disc. amount"
7035 PRINT (0,ERR=7036)'SF',
7040 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9800 REM "EXIT PROGRAM
9999 END 
56002 REM "232358-Ability to enter an order number range in cash receipts.    
56003 REM "239247-May Trial Balance is showing a lower balance.               
