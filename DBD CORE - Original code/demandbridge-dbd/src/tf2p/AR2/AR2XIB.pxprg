0010 REM "Export A/R Invoices  - Carwin <AR2XIB>
0020 SETESC 9300; SETERR 9000
0035 REM "5.1 - 06/04/02 - 11.562777 - lms - SSP# 149624
0040 REM "Copyright 2002 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "FILES[] 0=SUN ASCII format,
0051 REM "        1=SAP ASCII format (1st part), 2=SAP (2nd part)
0052 REM "        3=ZZZ Non-Sap/Sun
0060 REM "OPTION$ = Option routine to perfrom
0062 REM "SUMBIL$ =Summary bill number
0064 REM "FMYREC$ = FMY Record
0066 REM "SHIPTO$ = Ship to Record
0068 REM "EXAMT[] = Export amounts passed for ASCII record;REM "0,1,2
0069 REM "0=Freight, 1=Tax, 2=Extended value of goods
0070 REM "SUNTOT[]= Totals for SUN (only using 0-3)
0072 REM "SAPTOT[]= Totals for SAP
0074 REM "ZZZTOT[]=Totals for Non-Sap/Sun
0080 REM "0=Count, 1=Total Sum Bill, 2=Hash Total, 3=Total Vat
0081 REM "4=Total Sum Bill V5 records , 5=Total Sum Bill V0 records
0082 REM "CH_ZZPARM =Channel number for ZZPARM file
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,FILES{ALL},OPTION$,SUMBIL$,FMYREC$,SHIPTO$,EXAMT{ALL},SUNTOT{ALL},SAPTOT{ALL},CH_ZZPARM,ZZZTOT{ALL}
0100 SETERR 9000
0101 REM "SETESC 00000; ESCAPE
0110 X0$="AR2XIB",X1$="Export A/R Invoice Information"
0120 IF FN%NEA("FILES",0)=0 THEN DIM FILES[9]
0130 IF FN%NEA("SUNTOT",0)=0 THEN DIM SUNTOT[5]
0140 IF FN%NEA("SAPTOT",0)=0 THEN DIM SAPTOT[5]
0150 IF FN%NEA("ZZZTOT",0)=0 THEN DIM ZZZTOT[5]
0200 REM "Set data paths & file names
0210 DATA_PATH$=%DATAPATH$
0220 NW_PATH$=HWD+DLM+"nwbank"+DLM
0230 DATENO=FN%DTN(FND$(X3$(15,6)),"MMDDYY")
0240 SUN_FILE$="N"+FN%NTD$(DATENO,"YYMMDD")+"1"
0245 ZZZ_FILE$="Z"+FN%NTD$(DATENO,"YYMMDD")+"1"
0250 REM "Get current sequence number for SAP file
0260 SAP_FILE$="T39",FILE_NUM=0
0270 READ (CH_ZZPARM,KEY="338EXPORTFILE",DOM=0271)FILE_NUM
0280 SAP_FILE$=SAP_FILE$+STR(FILE_NUM:"000")
0290 SAP_FILE2$=SAP_FILE$+"2"
0300 REM "Iolists
0400 REM "Check for files, if not there then create
0401 REM "if array values <>0 then we've already done this
0410 IF FILES[0]<>0 THEN GOTO 0600
0420 CLOSE (14); OPEN (14,ERR=0421)SUN_FILE$; CLOSE (14); GOTO 0440
0430 SERIAL SUN_FILE$,0,0
0440 CLOSE (14); OPEN (14,ERR=0441)SAP_FILE$; CLOSE (14); GOTO 0460
0450 SERIAL SAP_FILE$,0,0
0460 CLOSE (14); OPEN (14,ERR=0461)SAP_FILE2$; CLOSE (14); GOTO 0480
0470 SERIAL SAP_FILE2$,0,0
0480 CLOSE (14); OPEN (14,ERR=0481)ZZZ_FILE$; CLOSE (14); GOTO 0500
0490 SERIAL ZZZ_FILE$,0,0
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O "+SUN_FILE$+"  02O "+SAP_FILE$+"  03O "+SAP_FILE2$+"  04O "+ZZZ_FILE$
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0524 REM "Close and re-open as text files then save file slots in FILES[]
0525 CLOSE (Z[1]); OPEN LOCK (Z[1],OPT="TEXT")SUN_FILE$; FILES[0]=Z[1]
0530 CLOSE (Z[2]); OPEN LOCK (Z[2],OPT="TEXT")SAP_FILE$; FILES[1]=Z[2]
0535 CLOSE (Z[3]); OPEN LOCK (Z[3],OPT="TEXT")SAP_FILE2$; FILES[2]=Z[3]
0540 CLOSE (Z[4]); OPEN (Z[4],OPT="TEXT")ZZZ_FILE$; FILES[3]=Z[4]
0600 REM "Determine if SUN data
0610 DIM FILLER$(391)
0620 IF OPTION$(1,3)<>"SUN" THEN GOTO 0700
0640 IF OPTION$(4,1)="H" THEN GOSUB 1000
0641 IF OPTION$(4,1)="1" THEN GOSUB 1100
0642 IF OPTION$(4,1)="2" THEN GOSUB 1200
0643 IF OPTION$(4,1)="T" THEN GOSUB 1300
0699 GOTO 9900
0700 REM "Determine if SAP data
0720 IF OPTION$(1,3)<>"SAP" THEN GOTO 0800
0740 IF OPTION$(4,1)="H" THEN GOSUB 2000
0741 IF OPTION$(4,1)="1" THEN GOSUB 2100
0742 IF OPTION$(4,1)="2" THEN GOSUB 2200
0743 IF OPTION$(4,1)="T" THEN GOSUB 2300
0799 GOTO 9900
0800 REM "Determine if Non Sap/Sun
0820 IF OPTION$(1,3)<>"ZZZ" THEN GOTO 0900
0840 GOSUB 4000
0899 GOTO 9900
0900 REM "Determine if End Routine
0920 IF OPTION$(1,3)="END" THEN GOSUB 3000
0990 GOTO 9900
1000 REM "Export Header Record - SUN Interface - 338
1010 DIM ACCT_CODE$(10),ACCT_PER$(7),TRANS_DATE$(8),REC_TYPE$(1),TOT_AMT$(18),DC_CODE$(1),ALLOC_IND$(1),JRNL_TYPE$(5),JRNL_SOURCE$(5),TRANS_REF$(10),TRANS_DESC$(25),DUE_DATE$(8),AN_CODES$(150)
1020 ACCT_CODE$(1)="VCA245",DATENO=FN%DTN(FND$(X3$(15,6)),"MMDDYY"),TRANS_DATE$(1)=FN%NTD$(DATENO,"YYYYMMDD"),REC_TYPE$(1)="L",TOT_AMT$(1)=STR(SUNTOT[1]*1000:"000000000000000000"),JRNL_TYPE$(1)="GHPA",TRANS_REF$(1)=SUMBIL$,TRANS_DESC$(1)=SUMBIL$+"/"+FN%NTD$(DATENO,"MM/YYYY")
1030 IF SUNTOT[1]<0 THEN DC_CODE$="D" ELSE DC_CODE$="C"
1080 DIM SUNREC$(391)
1090 SUNREC$(1)=ACCT_CODE$+FILLER$(1,5)+ACCT_PER$+TRANS_DATE$+FILLER$(1,2)+REC_TYPE$+FILLER$(1,14)+TOT_AMT$+DC_CODE$+ALLOC_IND$+JRNL_TYPE$+JRNL_SOURCE$+TRANS_REF$+FILLER$(1,5)+TRANS_DESC$+FILLER$(1,15)+DUE_DATE$+FILLER$(1,101)+AN_CODES$
1095 GOSUB 1800
1099 RETURN 
1100 REM "Export Detail Record 1 - Net Amount Posting - One per property/unit
1110 DIM ACCT_CODE$(10),ACCT_PER$(7),TRANS_DATE$(8),REC_TYPE$(1),NET_AMT$(18),DC_CODE$(1),ALLOC_IND$(1),JRNL_TYPE$(5),JRNL_SOURCE$(5),TRANS_REF$(10),TRANS_DESC$(25),DUE_DATE$(8),AN_CODES$(150)
1120 ACCT_CODE$(1)="5252007",DATENO=FN%DTN(FND$(X3$(15,6)),"MMDDYY"),TRANS_DATE$(1)=FN%NTD$(DATENO,"YYYYMMDD"),REC_TYPE$(1)="L",NET_AMT$(1)=STR((EXAMT[2]+EXAMT[0])*1000:"000000000000000000"),JRNL_TYPE$(1)="GHPA",TRANS_REF$(1)=SUMBIL$,TRANS_DESC$(1)=SHIPTO$(214,9)+"/"+FN%NTD$(DATENO,"MM/YYYY"),AN_CODES$(61,15)="PSTD",AN_CODES$(91,15)=SHIPTO$(347,8),AN_CODES$(121,15)="VCA245"
1130 IF EXAMT[2]+EXAMT[0]<0 THEN DC_CODE$="C" ELSE DC_CODE$="D"
1140 SUNTOT[0]=SUNTOT[0]+1
1150 SUNTOT[1]=SUNTOT[1]+EXAMT[2]+EXAMT[0]
1160 SUNTOT[2]=SUNTOT[2]+EXAMT[2]+EXAMT[0]
1180 DIM SUNREC$(391)
1190 SUNREC$(1)=ACCT_CODE$+FILLER$(1,5)+ACCT_PER$+TRANS_DATE$+FILLER$(1,2)+REC_TYPE$+FILLER$(1,14)+NET_AMT$+DC_CODE$+ALLOC_IND$+JRNL_TYPE$+JRNL_SOURCE$+TRANS_REF$+FILLER$(1,5)+TRANS_DESC$+FILLER$(1,15)+DUE_DATE$+FILLER$(1,101)+AN_CODES$
1195 GOSUB 1800
1199 RETURN 
1200 REM "Export Detail Record 2 - Vat Posting - One per property/unit
1210 DIM ACCT_CODE$(10),ACCT_PER$(7),TRANS_DATE$(8),REC_TYPE$(1),VAT_AMT$(18),DC_CODE$(1),ALLOC_IND$(1),JRNL_TYPE$(5),JRNL_SOURCE$(5),TRANS_REF$(10),TRANS_DESC$(25),DUE_DATE$(8),AN_CODES$(150)
1220 ACCT_CODE$(1)="5252007",DATENO=FN%DTN(FND$(X3$(15,6)),"MMDDYY"),TRANS_DATE$(1)=FN%NTD$(DATENO,"YYYYMMDD"),REC_TYPE$(1)="L",VAT_AMT$(1)=STR(EXAMT[1]*1000:"000000000000000000"),JRNL_TYPE$(1)="GHPA",TRANS_REF$(1)=SUMBIL$,TRANS_DESC$(1)=SHIPTO$(214,9)+"/"+FN%NTD$(DATENO,"MM/YYYY"),AN_CODES$(61,15)="VSTD",AN_CODES$(91,15)=SHIPTO$(347,8),AN_CODES$(121,15)="VCA245"
1230 IF EXAMT[1]<0 THEN DC_CODE$="C" ELSE DC_CODE$="D"
1240 SUNTOT[0]=SUNTOT[0]+1
1250 SUNTOT[1]=SUNTOT[1]+EXAMT[1]
1260 SUNTOT[2]=SUNTOT[2]+EXAMT[1]
1270 SUNTOT[3]=SUNTOT[3]+EXAMT[1]
1280 DIM SUNREC$(391)
1290 SUNREC$(1)=ACCT_CODE$+FILLER$(1,5)+ACCT_PER$+TRANS_DATE$+FILLER$(1,2)+REC_TYPE$+FILLER$(1,14)+VAT_AMT$+DC_CODE$+ALLOC_IND$+JRNL_TYPE$+JRNL_SOURCE$+TRANS_REF$+FILLER$(1,5)+TRANS_DESC$+FILLER$(1,15)+DUE_DATE$+FILLER$(1,101)+AN_CODES$
1295 GOSUB 1800
1299 RETURN 
1300 REM "Update Header Record
1310 CLOSE (FILES[0]); OPEN LOCK (FILES[0],OPT="TEXT")SUN_FILE$
1320 READ (FILES[0])SUNREC$
1330 SUNREC$(48,18)=STR(SUNTOT[1]*1000:"000000000000000000")
1340 IF SUNTOT[1]<0 THEN SUNREC$(66,1)="D" ELSE SUNREC$(66,1)="C"
1350 CLOSE (FILES[0]); OPEN LOCK (FILES[0],OPT="TEXT")SUN_FILE$
1360 WRITE RECORD (FILES[0])SUNREC$
1399 RETURN 
1800 REM "Write SUN record
1820 PRINT (FILES[0])SUNREC$
1890 RETURN 
2000 REM "Export Header Record - SAP Interface - 338
2010 DIM REC_H$(2),F_NAME$(10),DATE_PROD$(8),TIME_PROD$(6),SEQ_NO$(3),CYC_NO$(2),COMP_CODE$(4),FISCAL_YR$(4),CUR_CODE$(5),FILLER$(150)
2020 REC_H$(1)="01",F_NAME$(1)="CARWIN",DATENO=FN%DTN(FND$(X3$(21,6)),"MMDDYY"),DATE_PROD$(1)=FN%NTD$(DATENO,"YYYYMMDD"),TIME_PROD$(1)=FN%NTD$(0,"HHMMSS"),SEQ_NO$(1)=SAP_FILE$(4,3),CYC_NO$(1)="00",FISCAL_YR$(1)="    ",COMP_CODE$(1)="NW01",CUR_CODE$(1)="GBP"
2080 DIM SAPREC$(150)
2090 SAPREC$(1)=REC_H$+F_NAME$+DATE_PROD$+TIME_PROD$+SEQ_NO$+CYC_NO$+COMP_CODE$+FISCAL_YR$+CUR_CODE$+FILLER$(1,106)
2095 GOSUB 2800
2099 RETURN 
2100 REM "Export Detail Record 1 - Vendor
2110 DIM REC_D1$(2),INV_NO$(16),VEND_NO$(10),POST_AMT$(13),AMT_SIGN$(1),VAT_AMT$(13),PAY_METH$(1)
2120 REC_D1$(1)="20",INV_NO$(1)=SUMBIL$,VEND_NO$(1)="1000507",POST_AMT$(1)=STR(0:"0000000000.00"),VAT_AMT$(1)=STR(0:"0000000000.00"),PAY_METH$(1)="B"
2130 AMT_SIGN$(1)="C"
2140 SAPTOT[0]=SAPTOT[0]+2
2180 DIM SAPREC$(150)
2190 SAPREC$(1)=REC_D1$+INV_NO$+VEND_NO$+POST_AMT$+AMT_SIGN$+VAT_AMT$+PAY_METH$(1)+FILLER$(1,94)
2195 GOSUB 2800; GOSUB 2850
2199 RETURN 
2200 REM "Export Detail Record 2 - GL Account Item
2210 DIM REC_D2$(2),GL_NO$(10),GL_AMT$(13),GL_SIGN$(1),TAX_CODE$(2),COST_CTR$(10),POST_DESC$(50)
2220 REC_D2$(1)="21",GL_NO$(1)="5055030",GL_AMT$(1)=STR(EXAMT[0]+EXAMT[2]:"0000000000.00"),COST_CTR$(1)=SHIPTO$(369,6),POST_DESC$(1,8)=SHIPTO$(369,6),POST_DESC$(9,1)=" ",POST_DESC$(10,10)=FMYREC$(34,15),POST_DESC$(20,1)=" ",POST_DESC$(21,8)="CARWIN",POST_DESC$(29,1)=" ",POST_DESC$(30,10)=FMYREC$(11,8),POST_DESC$(40,1)=" ",POST_DESC$(41,10)=FMYREC$(49,8)
2225 IF STP(SHIPTO$(224,10),1)="UK" THEN TAX_CODE$(1)="V5" ELSE IF STP(SHIPTO$(224,10),1)="EC" THEN TAX_CODE$(1)="V0"
2230 IF EXAMT[1]=0 THEN TAX_CODE$(1)="V0"; REM "Revised logic for Non VAT tax ssp #125688; Rev. ssp# 127535
2235 IF EXAMT[0]+EXAMT[2]<0 THEN GL_SIGN$(1)="C" ELSE GL_SIGN$(1)="D"
2240 SAPTOT[0]=SAPTOT[0]+1
2250 SAPTOT[1]=SAPTOT[1]+EXAMT[0]+EXAMT[2]+EXAMT[1]
2260 IF GL_SIGN$(1)="C" THEN SAPTOT[2]=SAPTOT[2]+EXAMT[0]*(-1)+EXAMT[2]*(-1) ELSE SAPTOT[2]=SAPTOT[2]+EXAMT[0]+EXAMT[2]
2270 SAPTOT[3]=SAPTOT[3]+EXAMT[1]
2280 DIM SAPREC$(150)
2290 SAPREC$(1)=REC_D2$+GL_NO$+GL_AMT$+GL_SIGN$+TAX_CODE$+COST_CTR$+POST_DESC$+FILLER$(1,62)
2295 IF TAX_CODE$="V0" THEN SAPTOT[4]=SAPTOT[4]+EXAMT[0]+EXAMT[2]; GOSUB 2800 ELSE SAPTOT[5]=SAPTOT[5]+EXAMT[0]+EXAMT[2]+EXAMT[1]; GOSUB 2850
2299 RETURN 
2300 REM "Export Trailer Record
2310 DIM REC_T$(2),TOT_REC$(6),TOT_AMT$(13)
2320 REC_T$(1)="99",TOT_REC$(1)=STR(SAPTOT[0]:"000000"),TOT_AMT$(1)=STR(SAPTOT[1]+SAPTOT[2]:"0000000000.00")
2380 DIM SAPREC$(150)
2390 SAPREC$(1)=REC_T$+TOT_REC$+TOT_AMT$+FILLER$(1,129)
2395 GOSUB 2850
2400 REM "Update Export Detail Record 1 - Vendor; 1st part
2410 CLOSE (FILES[1]); OPEN LOCK (FILES[1],OPT="TEXT")SAP_FILE$
2420 READ (FILES[1])SAPREC$
2425 READ (FILES[1])SAPREC$
2430 SAPREC$(29,13)=STR(SAPTOT[4]:"0000000000.00")
2440 IF SAPTOT[4]<0 THEN SAPREC$(42,1)="D" ELSE SAPREC$(42,1)="C"
2450 CLOSE (FILES[1]); OPEN LOCK (FILES[1],OPT="TEXT")SAP_FILE$
2455 READ (FILES[1])X$
2460 PRINT (FILES[1])SAPREC$
2500 REM "Update Export Detail Record 1 - Vendor; 2nd part
2510 CLOSE (FILES[2]); OPEN LOCK (FILES[2],OPT="TEXT")SAP_FILE2$
2520 READ (FILES[2])SAPREC$
2530 SAPREC$(29,13)=STR(SAPTOT[5]:"0000000000.00"),SAPREC$(43,13)=STR(SAPTOT[3]:"0000000000.00")
2540 IF SAPTOT[5]<0 THEN SAPREC$(42,1)="D" ELSE SAPREC$(42,1)="C"
2550 CLOSE (FILES[2]); OPEN LOCK (FILES[2],OPT="TEXT")SAP_FILE2$
2560 PRINT (FILES[2])SAPREC$
2599 RETURN 
2800 REM "Write SAP record Non-Vat
2820 PRINT (FILES[1])SAPREC$
2840 RETURN 
2850 REM "Write SAP record Vat
2870 PRINT (FILES[2])SAPREC$
2890 RETURN 
3000 REM "End Routines - Copy files to export path
3020 CLOSE (FILES[0])
3030 RENAME DATA_PATH$+DLM+SUN_FILE$,NW_PATH$+SUN_FILE$
3040 CLOSE (FILES[1])
3050 CLOSE (FILES[2])
3060 RENAME DATA_PATH$+DLM+SAP_FILE2$,DATA_PATH$+DLM+SAP_FILE$
3070 RENAME DATA_PATH$+DLM+SAP_FILE$,NW_PATH$+SAP_FILE$+".txt"
3080 ERASE DATA_PATH$+DLM+SAP_FILE2$
3090 CLOSE (FILES[3])
3095 RENAME DATA_PATH$+DLM+ZZZ_FILE$,NW_PATH$+ZZZ_FILE$
3100 REM "Update Export File Nubmer
3120 IF FILE_NUM=999 THEN FILE_NUM=1 ELSE FILE_NUM=FILE_NUM+1
3140 WRITE (CH_ZZPARM,KEY="338EXPORTFILE")FILE_NUM
3190 RETURN 
4000 REM "Detail Record - Non Sap/Sun
4010 DIM REC_D1$(2),INV_NO$(16),VEND_NO$(10),POST_AMT$(13),AMT_SIGN$(1),VAT_AMT$(13),POST_DESC$(50)
4020 REC_D1$(1)="ZZ",INV_NO$(1)=SUMBIL$,VEND_NO$(1)="1000507",POST_AMT$(1)=STR(EXAMT[0]+EXAMT[2]:"0000000000.00"),VAT_AMT$(1)=STR(EXAMT[1]:"0000000000.00"),POST_DESC$(1,8)=SHIPTO$(367,8),POST_DESC$(9,1)=" ",POST_DESC$(10,10)=FMYREC$(34,15),POST_DESC$(20,1)=" ",POST_DESC$(21,8)="CARWIN",POST_DESC$(29,1)=" ",POST_DESC$(30,10)=FMYREC$(11,8),POST_DESC$(40,1)=" ",POST_DESC$(41,10)=FMYREC$(49,8)
4030 IF EXAMT[0]+EXAMT[2]<0 THEN AMT_SIGN$(1)="C" ELSE AMT_SIGN$(1)="D"
4040 ZZZTOT[0]=ZZZTOT[0]+1
4050 ZZZTOT[1]=ZZZTOT[1]+EXAMT[0]+EXAMT[2]+EXAMT[1]
4070 ZZZTOT[3]=ZZZTOT[3]+EXAMT[1]
4080 DIM ZZZREC$(150)
4090 ZZZREC$(1)=REC_D1$+INV_NO$+VEND_NO$+POST_AMT$+AMT_SIGN$+VAT_AMT$+POST_DESC$
4095 WRITE RECORD (FILES[3])ZZZREC$+$0D0A$
4099 RETURN 
8000 REM "Define functions
8955 DEF FND$(Z9$)=Z9$(3,4)+STR(MOD((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)),100):"0#")
8960 DEF FNE$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR(MOD(ASC(Z9$(1,1))-65,10)*10+NUM(Z9$(2,1)):"00")
9000 REM "Error Processing
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM "Transfer Control
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "Ctrl Logic
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "End Program
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
