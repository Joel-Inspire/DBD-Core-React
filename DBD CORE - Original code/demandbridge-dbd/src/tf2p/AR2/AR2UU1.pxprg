0010 REM "<AR2UU1> Apply Invoices of Zero Balance Customers
0020 SETESC 9300; SETERR 9000
0035 REM "4.1 - 05/01/98 - 10.93 - dmm - SSP# 091152
0040 REM "Copyright 1997 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="AR2UU1",X1$="Apply Invoices of Zero Balance Customers"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 AR1_IOLIST:IOLIST AR1$,AR1[0],AR1[1],AR1[2],AR1[3],AR1[4],AR1[5],AR1[6],AR1[7],AR1[8],AR1[9],AR1[10],AR1[11],AR1[12],AR1[13],AR1[14],AR1[15]
0320 DEP_IOLIST:IOLIST DEP$,DEP[0],DEP[1],DEP[2]; REM "ARP Deposit header
0330 CHK_IOLIST:IOLIST CHK$,CHK[0],CHK[1],CHK[2]; REM "ARP Check header
0340 INV_IOLIST:IOLIST INV$,INV[0],INV[1],INV[2],INV[3],INV[4],INV[5],INV[6],INV[7]; REM "ARP Invoice record
0360 AR6_IOLIST:IOLIST AR6$,AR6[0],AR6[1],AR6[2],AR6[3],AR6[4],AR6[5],AR6[6],AR6[7],AR6[8],AR6[9],AR6[10],AR6[11],AR6[12],AR6[13],AR6[14],AR6[15]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O AR1... 02L AR6... 03O ARP... 04OSARP... 05O ZYB... 06O ZY9  13O ZZPARM "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0603 DIM A$(10)
0605 GOSUB 7800; REM "Get current fiscal year
0610 GOSUB 6000
0615 REM "Fiscal year
0620 CALL "ZZENTR","ZXUX",A{ALL},A$,X4$,X3$,37,10,1,4,C0,"","{1"+X$,"","AR2UU100","","",""
0630 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 0631,9900
0635 FIND (Z[13],KEY=X3$(9,3)+"G/LYE"+A$(1,4),DOM=0620)P1$
0640 IF POS(P1$(99,1)="CR")<>0 THEN CALL "ZZPROM",".4",X3$,Z,"Closed years must be reopened by System Administrator prior to posting.","","",0; GOTO 0620
0665 REM "Accounting period
0670 CALL "ZZENTR","ZUX",A{ALL},A$,X4$,X3$,37,11,5,2,C0,"","{1"+X$,"","AR2UU102","","",""
0680 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 0681,9900
0685 IF A$(5,2)<"01" OR A$(5,2)>P1$(13,2) THEN GOTO 0670
0690 GOSUB 7700
0715 REM "Bank code
0720 CALL "ZZENTR","SZUX",A{ALL},A$,X4$,X3$,37,12,7,3,C0,"","{1"+X$,"","AR2UU104","","",""
0730 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 0731,9900
0740 DIM ZYB$(200); FIND (Z[5],KEY=A$(7,3),DOM=0720)ZYB$
0750 PRINT @(43,12),ZYB$(4,30),
0775 REM "Deposit number
0780 CALL "ZZENTR","SUX",A{ALL},A$,X4$,X3$,37,13,10,1,C0,"","{1"+X$,"","AR2UU106","","",""
0785 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 0786,9900
0790 GOSUB 8100
0800 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0801,9900
0900 GOSUB DEP_HEADER
0950 DIM AR1$(599),AR1[15],AR6$(120),AR6[15],INV$(35),INV[7],CHK$(71),CHK[2]
1000 REM "Read thru AR1 for customers with zero a/r balance
1010 READ (Z[1],KEY="",DOM=1011)
1015 READ (Z[1],END=5000)IOL=AR1_IOLIST
1020 C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
1025 GOSUB CHECK_BALANCE
1030 IF F1$="Y" AND BALANCE=0 THEN GOSUB CHK_HEADER ELSE GOTO 1015; REM "If customer's balance is not zero or we did not find any non-zero invoices go back and read for another customer
1050 READ (Z[2],KEY=AR1$(1,10),DOM=1051)
1055 READ (Z[2],END=1015)IOL=AR6_IOLIST
1060 IF AR6$(1,10)<>AR1$(1,10) THEN GOTO 1015
1070 DIM INV$(35),INV[7]
1080 INV$(1,26)=CHK$(1,26),INV$(27,8)=AR6$(11,8)
1085 INV[1]=AR6[3],INV[6]=AR6[3]; REM "Total applied, cash posted
1090 WRITE (Z[4],KEY=INV$(1,34))IOL=INV_IOLIST
1095 GOTO 1055
4900 REM "END OF LINES
4910 Y[4]=-1; WRITE (F,IND=0)IOL=0290
4920 CALL "ZZEXPF",X3$,X4$,"X"+STR(F),"",G9; IF G9>0 THEN EXITTO 1300
4925 EXTRACT (F,IND=0)IOL=0290
4930 Z=NUM(FIN(F,"MAXREC")); Y[3]=Z-1
4935 Y[4]=-2; WRITE (F,IND=0)IOL=0290
4940 GOTO 5215
4980 EXITTO 1300
5000 REM "EOJ
5005 C=T; GOSUB 8150
5010 PRINT @(0,15),'CE',
5015 GOSUB 5100; REM "Check for at least one record besides deposit header otherwise delete deposit header, if batch processing then close and erase ARP and remove batch header
5020 IF Q1$="" AND NO_RECS$="Y" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete! NO records created.","","",0; GOTO 5031
5030 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete! Print the Cash Receipts Journal and Update.","","",0
5040 GOTO 9900
5100 REM "
5110 READ (Z[4],KEY=A$(1,10),DOM=5190)IOL=DEP_IOLIST
5120 K$=KEY(Z[4],END=5130)
5125 IF K$(1,10)=A$(1,10) THEN GOTO 5190; REM "There is at least one record besides the deposit header
5130 REMOVE (Z[4],KEY=A$(1,10))
5135 NO_RECS$="Y"
5140 READ (Z[13],KEY="CMP"+X3$(9,3))CMP$
5150 IF CMP$(194,1)<>"1" THEN GOTO 5190; REM "Don't do batch processing
5155 READ (Z[4],KEY="",DOM=5156)
5156 K$=KEY(Z[4],END=5160); GOTO 5190; REM "File not empty, don't erase
5160 CLOSE (Z[3]); CLOSE (Z[4])
5165 ERASE "ARP"+X3$(174,4),ERR=5166
5170 REMOVE (Z[6],KEY=X3$(174,4)+" ",DOM=5190)
5190 RETURN 
5200 REM "GET NEXT INDEX TO FILE IN A9
5205 F=Z[3]; DIM Y[4]
5210 EXTRACT (F,IND=0,ERR=5295)IOL=0290
5212 IF Y[4]=-2 THEN READ (F); GOTO 5270
5215 IF Y[0]>=Y[3] OR Y[2]>=Y[3] THEN GOTO 4900 ELSE Y[4]=-2
5220 WRITE (F,IND=0)IOL=0290
5230 IF Y[1]<1 THEN A9=Y[2],Y[2]=Y[2]+1 ELSE A9=Y[1]; READ (F,IND=A9)Y[1]
5240 Y[0]=Y[0]+1,Y[4]=-1
5250 WRITE (F,IND=0)IOL=0290
5255 U1=0
5260 RETURN 
5270 LOCK (F,ERR=5210); Y[4]=-1; GOTO 5215
5295 IF ERR<>0 THEN EXITTO 1300 ELSE RETRY 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,3),"This utility will read through the Customer Master file and for each customer   with a zero balance, cash receipt records will be created.  Any on account      balance will be distributed.  When the process is complete you will need to     print the Cash Receipts Journal and update if correct."
6030 PRINT @(24,10),"Fiscal year:",@(18,11),"Accounting period:",@(26,12),"Bank code:",@(21,13),"Deposit number:",
6040 PRINT @(37,10),A$(1,4),@(37,11),A$(5,2),
6050 GOSUB 7700
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7700 REM "PRINT P/E DATE"
7720 PRINT @(43,11),"Ending ",FND$(P1$(NUM(A$(5,2))*6+15,6)),
7749 RETURN 
7800 REM "Get G/L Z Mask
7810 CALL "ZZDISP","AX","000000000000","G/L",X3$,W2$,"",0,0,X4$
7815 M2=0; FOR X=1 TO LEN(W2$); M2=M2+POS("0"=W2$(X,1)); NEXT X
7850 REM "Set Fiscal year & period"
7860 FIND (Z[13],DOM=8900,KEY=X3$(9,3)+"A/R")P0$
7870 FIND (Z[13],DOM=8900,KEY=X3$(9,3)+"G/LYE"+P0$(7,4))P1$
7880 A$(1,6)=P0$(7,6)
7890 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F2$
8115 PRINT @(0,16),"There are "+STR(T)+" records to process"
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8135 T1=0
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,C
8195 RETURN 
8200 DEP_HEADER:REM "Write deposit header ARP record
8210 DIM DEP$(40),DEP[2]
8215 READ (Z[3],KEY=A$(1,10),DOM=8220); IF A$(10,1)="Z" THEN PRINT @(1,10),"PROGRAM BREAK-CAN'T INCREMENT DEPOSIT NO-CALL CSI-AR2UU1-LINE 8215"; ESCAPE ELSE IF A$(10,1)="9" THEN A$(10,1)="A"; GOTO 8215 ELSE A$(10,1)=CHR(ASC(A$(10,1))+1); GOTO 8215
8225 DEP$(1,10)=A$(1,10),DEP$(11,6)=X3$(15,6)
8230 WRITE LOCK (Z[3],KEY=DEP$(1,10))IOL=DEP_IOLIST
8295 DEP_HEADER_END:RETURN 
8300 CHECK_BALANCE:REM "Check invoices for this customer, zero balance but some are non-zero invoices?
8305 F1$="N",BALANCE=0; REM "If we find any non-zero invoice balances we will set F1$ to Y, we will accumulate invoice balances for this customer in BALANCE
8310 READ (Z[2],KEY=AR1$(1,10),DOM=8311)
8315 READ (Z[2],END=CHECK_BALANCE_END)IOL=AR6_IOLIST
8320 IF AR6$(1,10)<>AR1$(1,10) THEN GOTO CHECK_BALANCE_END
8330 IF AR6[3]<>0 THEN F1$="Y"
8335 BALANCE=BALANCE+AR6[3]
8340 GOTO 8315
8395 CHECK_BALANCE_END:RETURN 
8400 CHK_HEADER:REM "Create check header record
8410 DIM CHK$(71),CHK[2]
8420 CHK$(1,10)=A$(1,10),CHK$(11,10)=AR1$(1,10),CHK$(21,6)="ARSU2 "; REM "Fiscal year/ap, bank code, deposit number, customer code, check reference
8425 CHK$(27,6)=X3$(15,6),CHK$(33,35)=AR1$(11,35); REM "Check date, cust name
8440 WRITE (Z[4],KEY=CHK$(1,26))IOL=CHK_IOLIST
8445 CHK_HEADER_END:RETURN 
8910 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"      ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
