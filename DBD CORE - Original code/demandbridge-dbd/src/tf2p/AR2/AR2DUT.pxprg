0010 REM "Release 3.8 Sales Tax/Pending Update <AR2DUT>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 08/11/11 - 12.051111 - tma - SSP# 248479
0037 REM "248479-Many AR invoices in July have the sales tax amount doubled  
0040 REM "Copyright 2011 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "F9=0 means coming from A/R update AR2DUA - Sales Tax Payable
0051 REM "F9=1 mean coming from A/R update AR2DUA - Sales Tax Pending
0052 REM "F9=2 means coming from C/R update AR2CUA - Sales Tax Pending
0053 REM "     T0=prorata factor, S0 total sales tax to moved from ARK
0054 REM "F9=3 means coming from A/R Period End - Delete pending from ASE
0055 REM "WO91564, do G/L by sales tax code, come here if parm set
0056 REM "F9=4 coming from A/R update AR2DUA - Sales Tax Pay or Pend G/L
0057 REM "F9=5 coming from C/R update AR2CUA - Sales Tax Pay and Pend G/L
0058 REM "      invoice has only one tax code
0080 REM "Changed ENTER for WO91564, added string and array on the end
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,F9,A$,A{ALL},D$,D{ALL},T0,S0,ST$,ST{ALL}
0100 SETERR 9000
0110 X0$="AR2DUT",X1$="Compute Sales Tax"
0120 DIM S$(40)
0135 C9=-1
0140 M2$="###,###.00-"
0200 REM "
0300 REM "IOLISTS
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16]
0350 IOLIST E$,E[0],E[1],E[2],E[3],E[4],E[5],E[6],E[7],E[8],E[9],E[10],E[11],E[12],E[13],E[14],E[15],E[16]
0400 IOLIST J$,J[0],J[1],J[2],J[3],J[4],J[5],J[6],J[7],J[8],J[9],J[10],J[11],J[12],J[13],J[14]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="03O ASD...  05O ASE...  10O ARL...  11O AR5...  12O AR2...  15O AXL...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 DIM J[14],C[16]
0550 FIND (Z[13],KEY=X3$(9,3)+"A/R")P0$
0900 REM 
0910 ON F9 GOSUB 1000,1500,2000,2500,3000,3500
0990 GOTO 9900
1000 REM "Sales Tax Payable 3.8 - Read new ASD file
1010 REM "Only look at 1,13 in case split terms
1015 FIND (Z[12],KEY=A$(65,2),DOM=1016)TERM_CODE$,DISC_P,NO_PAY,NO_DAY
1020 IF NO_PAY>1 THEN READ (Z[3],KEY=A$(1,13),DOM=1021) ELSE READ (Z[3],KEY=A$(1,14),DOM=1021) ! ssp#248479
1040 READ (Z[3],END=1490)IOL=0330
1060 IF NO_PAY<=1 AND A$(1,14)<>C$(1,14) THEN GOTO 1490 ELSE IF NO_PAY>1 AND A$(1,13)<>C$(1,13) THEN GOTO 1490 ! SSP#248479
1100 REM "Update Sales tax payable ARL
1120 DIM J$(47),J[14]
1140 EXTRACT (Z[10],KEY=C$(1,6)+C$(15,10)+C$(7,8),DOM=1141)IOL=0400; GOTO 1200
1160 REM "Setup new record"
1180 J$(1,6)=C$(1,6),J$(7,10)=C$(15,10),J$(17,8)=C$(7,8),J$(25,10)=D$(1,10),J$(35,6)=A$(86,6)
1200 REM "add to transaction values
1205 DIM AR5$(190); FIND (Z[11],KEY=J$(7,10),DOM=1206)AR5$
1210 J[0]=J[0]+A[9]; IF NO_PAY>1 THEN IF A$(14,1)>"1" THEN GOTO 1280; REM "SPLIT TERMS
1211 REM "IF C$(25,1)="T" THEN I=C[2]+C[10]+C[9],J[1]=J[1]+I,J[2]=J[2]+C[10]; GOTO 1240; REM " SSP# 158548
1220 I=C[0]+C[1]+C[2]+C[10]+C[9],J[1]=J[1]+I,J[2]=J[2]+C[10]
1240 FOR I=3 TO 8; J[I]=J[I]+C[I]; NEXT I
1245 J[9]=C[11],J[10]=C[12],J[11]=C[13],J[12]=C[14],J[13]=C[15],J[14]=C[16]
1280 WRITE (Z[10],KEY=J$(1,24))IOL=0400
1281 IF MID(P0$,134,1)="Y" THEN WRITE (Z[15],KEY=J$(1,6)+AR5$(119,2)+AR5$(121,25)+AR5$(79,25)+J$(17,8)+AR5$(1,10)); REM "SSP$ 149046
1390 GOTO 1040
1490 RETURN 
1500 REM "Update Sales Tax Pending file ASE
1520 READ (Z[3],KEY=A$(1,14),DOM=1521)
1540 READ (Z[3],END=1990)IOL=0330
1560 IF A$(1,14)<>C$(1,14) THEN GOTO 1990
1600 REM "Update Sales tax pending ASE
1620 DIM E[16]
1640 E$=D$,E$(25,10)=C$(15,10)
1660 EXTRACT (Z[5],KEY=E$(1,18)+E$(25,10),DOM=1661)IOL=0350
1680 I=C[0]+C[1]+C[2]+C[9]+C[10],E[0]=E[0]+I,E[1]=E[1]+I-C[9]-C[10],E[2]=E[2]+C[10]
1700 FOR I=5 TO 10; E[I]=E[I]+C[I-2]; NEXT I
1820 WRITE (Z[5],KEY=E$(1,18)+E$(25,10))IOL=0350
1890 GOTO 1540
1990 RETURN 
2000 REM "Update Sales Tax from Commission pending - multiple tax codes
2005 REM "WO91564, if parm set for sales tax g/l by state we'll handle that here
2020 S=S0,S0=0; IF P0$(119,1)="Y" THEN PAY_DFLT$=ST$(1,12),ACC_DFLT$=ST$(13,12),ST$="",STBS$="Y"; DIM ST[60]; REM "WO91564, sales tax g/l by state
2030 DIM E[16]
2040 READ (Z[5],KEY=D$(1,18),DOM=2041)
2060 EXTRACT (Z[5],END=2490)IOL=0350
2080 IF D$(1,18)<>E$(1,18) THEN GOTO 2490
2100 DIM J$(47),J[14]; TAX_AMT=0
2120 J$(1,6)=A$(1,6),J$(7,10)=E$(25,10),J$(17,8)=E$(11,8),J$(25,10)=E$(1,10),J$(35,6)=E$(19,6)
2140 EXTRACT (Z[10],DOM=2141,KEY=J$(1,24))IOL=0400
2160 J[0]=J[0]+D[2]
2200 X=E[0]*T0,J[1]=J[1]+X,E[0]=E[0]-X
2220 X=E[2]*T0,J[2]=J[2]+X,E[2]=E[2]-X
2240 FOR I=3 TO 8; X=E[I+2]*T0,J[I]=J[I]+X,E[I+2]=E[I+2]-X; IF FPT(I/2)=0 THEN TAX_AMT=TAX_AMT+X,S0=S0+X END_IF ; NEXT I
2250 IF STBS$<>"Y" THEN GOTO 2400
2260 TAX_CODE$=J$(7,10),DFLT_GL_NUM$=PAY_DFLT$,AR5_POS=55; GOSUB 8000; PAY_GL_NUM$=GL_NUM$
2265 DFLT_GL_NUM$=ACC_DFLT$,AR5_POS=67; GOSUB 8000; ACC_GL_NUM$=GL_NUM$
2270 GL_NUM$=PAY_GL_NUM$+ACC_GL_NUM$
2275 P=POS(GL_NUM$=ST$,24); IF P>0 THEN P=INT(P/24); ST[P]=ST[P]+TAX_AMT ELSE ST$=ST$+GL_NUM$; GOTO 2275
2400 REM "Write Sales Tax Payable
2420 WRITE (Z[10],KEY=J$(1,24))IOL=0400
2430 IF MID(P0$,134,1)="Y" THEN WRITE (Z[15],KEY=J$(1,6)+AR5$(119,2)+AR5$(121,25)+AR5$(79,25)+J$(17,8)+AR5$(1,10)); REM "SSP$ 149046
2440 WRITE (Z[5])IOL=0350
2460 GOTO 2060
2490 RETURN 
2500 REM "Remove pending records from ASE
2510 READ (Z[5],KEY=D$(1,18),DOM=2511)
2520 K$=KEY(Z[5],END=2590)
2530 IF K$(1,18)<>D$(1,18) THEN GOTO 2590
2540 REMOVE (Z[5],KEY=K$)
2550 GOTO 2520
2590 RETURN 
3000 REM "WO91564, coming from AR2DUA, need sales tax pay or pend g/l numbers and amts
3010 DFLT_GL_NUM$=ST$,ST$="",AR5_POS=T0; DIM ST[60]
3020 READ (Z[3],KEY=A$(1,14),DOM=3021)
3025 READ (Z[3],END=3090)IOL=0330
3030 IF C$(1,14)<>A$(1,14) THEN GOTO 3090
3035 TAX_CODE$=C$(15,10); GOSUB 8000
3040 P=POS(GL_NUM$=ST$,12); IF P>0 THEN P=INT(P/12),ST[P]=ST[P]+C[9] ELSE ST$=ST$+GL_NUM$; GOTO 3040
3050 GOTO 3025
3090 RETURN 
3500 REM "WO91564, coming from AR2CUA, need sales tax pay and pend g/l numbers, one tax code on this invoice, if multiple this is handled in 2000's
3510 PAY_DFLT$=ST$(1,12),ACC_DFLT$=ST$(13,12),ST$=""; DIM ST[60]
3515 TAX_CODE$=D$(25,10)
3520 DFLT_GL_NUM$=PAY_DFLT$,AR5_POS=55; GOSUB 8000; ST$=GL_NUM$
3525 DFLT_GL_NUM$=ACC_DFLT$,AR5_POS=67; GOSUB 8000; ST$=ST$+GL_NUM$
3530 ST[0]=S0
3590 RETURN 
8000 REM "Given a sales tax code, read AR5 for the state portion of that code, look for the g/l code, if blank use the default from the interface sent in ST$ from calling program and set to DFLT_GL_NUM$ in this program. AR5_POS is either 55 for payable or 67 for accrued.
8010 GL_NUM$=""
8020 IF P0$(134,1)<>"Y" THEN DIM AR5$(190); FIND (Z[11],KEY=TAX_CODE$(1,2)+DIM(8),DOM=8021)AR5$ ELSE IF P0$(134,1)="Y" THEN DIM AR5$(190); FIND (Z[11],KEY=TAX_CODE$,DOM=8021)AR5$; REM " SSP 159397
8030 IF POS(" "<>AR5$(AR5_POS,12))>0 THEN GL_NUM$=AR5$(AR5_POS,12) ELSE GL_NUM$=DFLT_GL_NUM$
8090 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56001 REM "248479-Many AR invoices in July have the sales tax amount doubled  
