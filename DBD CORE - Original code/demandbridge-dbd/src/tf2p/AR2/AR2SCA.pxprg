0010 REM "Customer Sales Trend <AR2SCA>
0015 REM "Prog Type: O -2.5     
0035 REM "5.7 - 01/27/12 - 10.101666 - tma - SSP# 251985
0037 REM "251985-Customer Sales Trend report - Minor Report Format changes   
0040 REM "Copyright 2012 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 SETERR 9000; SETESC 9300
0110 X0$="AR2SCA",X1$="Customer Sales Analysis Report"
0120 DIM A$(128),A[31],C$(53),C[22],S$(50),D$(50)
0140 Z0$="#,###,##0-",Z1$="###-"
0145 DIM Z6$(LEN(Z0$),"-"),Z7$(LEN(Z1$),"-")
0210 T=1,T0=1,T1=30
0215 F9$="A/R"
0220 W3=210
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR AR+ - A/R Statistical Data Print File
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30],A[31]
0325 REM "IOLIST FOR AR0 - Customer Credit Analysis
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20],C[21],C[22]
0340 IOLIST D$
0350 IOLIST E$
0360 IOLIST F$
0420 IOLIST X3$,X4$,V1$,V3$,V2$,IOL=0425
0425 IOLIST V$,V[0],V[1]
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01X SA1"+X3$(1,3)+"  03O AR0...  04O AR3...  05O ARG...  "; IF V1$(1,3)<>Y$(5,3) THEN Y$=Y$+"02O "+V1$+"  "
0515 Y$=Y$+"06O FR1...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0590 GOSUB 4000
0720 REM 
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4),U5$(100),Y[5,2]
0810 ON V0 GOTO 0820,0820,0830,0835,0840,0830
0820 U1=Z[1]; DIM U[2,2]; U=10,U0=1,U[0]=0,U[1]=10,V1=10,T0=1,U0$="2   ",T5$="S           ",U5$(21)="Customer"; GOTO 0850
0830 U1=Z[1]; DIM U[2,2]; U=4,U0=1,U[0]=0,U[1,0]=1,U[1,1]=4,U[2,0]=5,U[2,1]=12,V1=12,V2=4,T0=1,U0$="233 ",T5$="S   S       ",U5$(21)="Salesperson"; GOTO 0850
0835 U1=Z[1]; DIM U[3,2]; U=13,U0=2,U[0]=0,U[1,0]=1,U[1,1]=4,U[2,0]=5,U[2,1]=9,U[3,0]=14,U[3,1]=12,V1=4,V2=9,T0=2,U0$="232 ",T5$="    S   AARG",U5$(21)="Salesperson",U5$(41)="Category"; GOTO 0850 ! SSP#251985
0840 U1=Z[1]; DIM U[2,2]; U=9,U0=1,U[0]=0,U[1,0]=1,U[1,1]=9,U[2,0]=10,U[2,1]=12,V1=9,V2=12,T0=1,U0$="222 ",T5$="    AARGS       ",U5$(21)="Category"; GOTO 0850
0850 DIM T[T0,T1]; U5$(1,20)=Q4$
0890 REM "Open Printer
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0950 REM " Position read"
0970 IF V2$="" THEN GOTO 0990 ELSE DIM V4$(U); U9=1; FOR X=1 TO U0; V4$(U[X,0],U[X,1])=V2$(U9,U[X,1]); U9=U9+2*U[X,1]; NEXT X
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record and check range
1005 U$=KEY(U1,END=5000); READ (U1)
1006 GOTO 1100
1010 FOR X=1 TO U0; U2=U[X,0],U3=U[X,0]*2-1,U4=U[X,1]
1020 IF U$(U2,U4)<V2$(U3,U4) THEN READ (U1); EXITTO 1000
1030 IF U$(U2,U4)>V2$(U3+U4,U4) THEN IF X=1 THEN EXITTO 5000 ELSE READ (U1); EXITTO 1000
1040 NEXT X
1100 REM "Get record"
1110 U9$=U$
1115 X=0
1120 IF Y[X,0]>0 THEN U$=U$+U9$(Y[X,0],Y[X,1]),X=X+1; GOTO 1120
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1200 REM "Reporting criteria
1400 REM "Subtotals"
1410 IF U2$="" THEN GOSUB 6000; T9=1; GOTO 1480 ELSE FOR T=1 TO U0; IF U9$(U[T,0],U[T,1])<>U2$(U[T,0],U[T,1]) THEN EXITTO 1420 ELSE NEXT T; GOTO 1480
1420 T9=T
1425 GOSUB 3100
1480 IF V0=3 AND T<=1 THEN GOSUB 6300
1485 IF V0=4 AND T<=1 THEN GOSUB 6400
1490 T=0,U2$=U9$
1500 REM "Print Data
1510 IF W+1>W0 THEN GOSUB 6000
1512 CALL "ZZDISP","AXS",A$(1,10),"A/R",X3$,Y5$,"",1,0,X4$
1514 Y5$(17)=A$(31,35)
1515 Y=60
1520 FOR I=1 TO 15
1525 Y5$(Y)=STR(A[I]:Z0$),Y=Y+LEN(Z0$)
1526 NEXT I
1530 GOSUB 7300
1650 REM 
1900 REM "Accumulate Totals"
1920 FOR X=1 TO 15; T[T0,X]=T[T0,X]+A[X]; NEXT X
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,X$,"",0,0,X4$; T6$=X$ ! SSP#251985
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "Subtotal Logic
3110 FOR T=U0 TO T STEP -1
3120 IF U[T,0]=0 THEN U[T,0]=1
3130 T$=FNS$(U5$(T*20+1,20)),T6$=U2$(U[T,0],U[T,1]),T7$=T5$(T*4+1,4); GOSUB 3050; IF POS(U0$(T+1,1)="23")>0 THEN GOSUB 7000 ELSE GOSUB 7100
3150 IF POS(U0$(T+1,1)="13")>0 THEN GOSUB 6000
3180 NEXT T
3195 RETURN 
3280 RETURN 
3380 RETURN 
3480 RETURN 
4000 REM "Figure Dates D0= curr yr, D1= last yr
4010 D0$=" "+V$(76,2)
4020 D1$=STR(NUM(V$(74,4))-1:"0000"),D1$=" "+D1$(3,2)
4025 D2$=V$(74,4)
4030 D3$=STR(NUM(V$(74,4))-1:"0000")
4035 YEAR$=STR(NUM(V$(74,4)):"0000")
4050 REM "Set title
4055 ON V0 GOTO 4080,4060,4065,4070,4075,4078,4080
4060 GOTO 4080
4065 X1$=X1$+" - by Salesperson"; GOTO 4080
4070 X1$=X1$+" - by Salesperson and Customer Category"; GOTO 4080
4075 X1$=X1$+" - by Customer Category"; GOTO 4080
4078 X1$=X1$+" - by Second Salesperson"; GOTO 4080
4100 REM "Set column and min val title
4105 IF V[0]=0 THEN GOTO 4190
4110 W2$="In Descending Order by"
4115 IF V[0]=14 THEN W2$=W2$+" Last 12 Periods"; GOTO 4150 ELSE IF V[0]=15 THEN W2$=W2$+" Last 3 Periods"; GOTO 4150
4120 P5=NUM(V$(78,2))+V[0]-1
4125 IF P5<=12 THEN W2$=W2$+" Sales Period "+STR(P5)+" "+YEAR$ ELSE W2$=W2$+" Sales Period "+STR(P5-12)+" "+YEAR$
4150 REM "Minimum value
4155 IF V[1]=0 THEN GOTO 4190
4160 W2$=W2$+",  Minimum Value "+FNT$(STR(V[1]:Z0$))
4190 RETURN 
4900 REM "LINE FEED
4910 T9=NUM(T9$(2))
4915 IF T9>0 THEN Y5$(1)=""; GOSUB 7300; T9=T9-1
4990 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5050 T=0; GOSUB 3100
5200 REM "
5295 GOTO 9900
6000 REM "Page header
6010 IF V0=2 OR V0=3 OR V0=5 THEN IF T0$="END" THEN W0$="" ELSE GOSUB 6200
6015 IF V0=4 THEN IF T0$="END" THEN W0$="" ELSE GOSUB 6350
6035 IF T0$="END" THEN W0$=""
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6060 ELSE ON W5 GOTO 6041,9910
6045 IF FID(0)=FID(W9) THEN W3=Q5
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6115 DIM D5$(130,"-"); D5$(57,19)=" Accounting Period ",Y5$(60)=D5$
6120 Y=194; Y5$(Y)="Last",Y=Y+LEN(Z0$),Y5$(Y)="Last"
6125 GOSUB 7300
6128 Y=62,P5=NUM(V$(78,2))
6129 FOR I=P5 TO P5+12
6130 IF I<13 THEN Y5$(Y)=STR(I:" 0")+"/"+D3$ ELSE Y5$(Y)=STR(I-12:" 0")+"/"+D2$
6132 Y=Y+LEN(Z0$)
6135 NEXT I
6140 Y=Y+2; Y5$(Y-1)="12 Pds",Y=Y+LEN(Z0$),Y5$(Y)="3 Pds"
6145 GOSUB 7300
6195 IF FID(W9)=FID(0) THEN PRINT 'SF',
6199 RETURN 
6200 REM "Salesperson title
6210 IF U9$(1,4)=D$(1,4) THEN GOTO 6240
6215 DIM D$(50); READ (Z[4],KEY=U9$(1,4),DOM=6216)IOL=0340
6230 W0$=U9$(1,4)+" - "+FNS$(D$(5,35))
6240 RETURN 
6300 REM "PRINT CATEGORY
6310 IF U9$(5,9)=MID(E$,1,9) THEN GOTO 6316
6315 DIM E$(40); READ (Z[5],KEY=U9$(5,9),DOM=6316)IOL=0350
6320 CALL "ZZDISP","AX",U9$(5,9),"ARG",X3$,X$,"",0,0,X4$
6330 Y5$(1)=X$+" - "+E$(10,30); GOSUB 7300
6340 RETURN 
6350 REM "SET CATEGORY
6355 GOTO 6390
6360 IF U9$(1,9)=E$(1,9) THEN GOTO 6390
6365 DIM E$(40); READ (Z[5],KEY=U9$(1,9),DOM=6366)IOL=0350
6370 CALL "ZZDISP","AX",U9$(1,9),"ARG",X3$,X$,"",0,0,X4$
6380 W0$=X$+" - "+FNS$(E$(10,30))
6390 RETURN 
6400 REM "Print category
6410 IF U9$(1,9)=MID(E$,1,9) THEN GOTO 6416
6415 DIM E$(40); READ (Z[5],KEY=U9$(1,9),DOM=6416)IOL=0350
6420 CALL "ZZDISP","AX",U9$(1,9),"ARG",X3$,X$,"",0,0,X4$
6430 Y5$(1)=X$+" - "+E$(10,30); GOSUB 7300
6440 RETURN 
6640 REM "Print category
6900 REM "SORT
6955 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6970 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,S9; ON S9 GOTO 6971,9920
6990 RETURN 
7000 REM "Totals logic
7001 IF T=T0 THEN IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN U8$=U9$,U9$=U2$; GOSUB 6000; U9$=U8$
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7011 IF T$<>"" THEN Y5$(T0+2-T)=T$+" "+Q2$+" ("+STR(T[T,0]:"0")+"):"
7020 Y=60
7025 FOR I=1 TO 15; Y5$(Y)=Z6$,Y=Y+LEN(Z6$); NEXT I
7027 GOSUB 7300
7030 Y=60
7035 FOR I=1 TO 15; Y5$(Y)=STR(T[T,I]:Z0$),Y=Y+LEN(Z0$); NEXT I
7045 GOSUB 7300
7090 IF T=0 AND T0$="END" THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO 15
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7330 REM 
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     13O ZZPARM  "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7435 DIM V[1]
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7455 READ (Z[13],KEY="STATRPT"+F9$+V$(80,2))G2$,S0$
7480 X3$(178,7)=Y4$,V0=NUM(V$(71,1)),W0$=V$(19,POS("   "=V$(19,40)+"  ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7487 IF V$(80,2)="01" THEN X1$="Customer Sales Comparison" ELSE IF V$(80,2)="02" THEN X1$="Customer Gross Profit Analysis" ELSE IF V$(80,2)="10" THEN X1$="Customer Sales Trend"
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
8900 REM "Function Statements
8910 DEF FNT$(Z$)=Z$(POS(" "<>Z$+"X"))
8940 DEF FNS$(Z$)=Z$(1,POS("   "=Z$+"   ")-1)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 IF Y5=68 THEN RETRY ELSE ON C9 GOTO 1140,2040
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 ERASE "SA1"+X3$(1,3),ERR=9926; GOTO 9925
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
