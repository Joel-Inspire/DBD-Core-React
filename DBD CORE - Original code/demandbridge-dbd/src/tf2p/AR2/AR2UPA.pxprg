0010 REM "Credit Card Processing Plus - Charge Cards & Printing <AR2UPA>
0035 REM "5.7 - 09/11/24 - 16.23491 - jvv - SSP# 307500
0037 REM "307500-Display issues with Credit Card Processing Report           
0040 REM "Copyright 2024 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0093 IF %GUI THEN CLEAR ; SETERR 9000 ELSE BEGIN ; SETERR 9000; GOTO 0095
0094 ENTER X3$,X4$,Q0$,Q1$
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 0000; SETESC 0000
0110 X0$="AR2UPA",X1$="Credit Card Processing"
0119 ! %CC_DEBUG=0 ! Setting to '1' will print helpful debug messages to the report
0120 DIM A$(689),A[30],B$(54),B[6],M$(20),M[1]
0140 M0$="####.00-",M1$="###,##0-",M2$="###,##0.00-",M3$="####"
0160 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0180 DEF FNS$(X$)=X$(1,POS("      "=X$+"      ")-1)
0210 T=1,T0=1,T1=1
0220 W3=125
0225 W=999,W8=0
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9910
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0250 DIM TOTAL_INVOICE_AMOUNT[2],TOTAL_TAX_AMOUNT[2],TOTAL_CC_INVOICES[2]
0300 REM "I/O lists
0305 REM "IOLIST FOR ARB - A/R Invoice Data Entry Header
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30]
0315 REM "IOLIST FOR ART - Invoice Entry Print Detail
0320 REM IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19],B[20]
0330 IOLIST C$
0340 IOLIST D$
0350 IOLIST EDD$,EDD[0],EDD[1],EDD[2],EDD[3],EDD[4],EDD[5],EDD[6]
0380 IOLIST FS1$,FS1[0]
0390 IOLIST G$
0400 IOLIST M$,M[0],M[1]
0401 IOLIST TERMS$
0402 IOLIST AR1$
0410 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0420 REM "Files
0430 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0440 Y$="01O ARB...  02L ART...  03O AR2...  04O AR3...  05O FMP...  06O FM0...  07O AR1...  08O EDD...  09O FS1...  10O FSV...  14O PO3...  " ! SSP#283829 -lock ART, ARB is reopend in program
0450 Y$=Y$+"13O ZZPARM  "
0460 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1; ON Z0 GOTO 0461,9900 ! SSP#283829
0470 ! Check for Credit Card processing mode-"Plus" mode (1) or "Regular" mode (0) - "Plus" will charge inv. amounts to credit cards via Net1
0471 ! Check if either of the credit card modules - "CP" or "CC", has been activated
0472 CCV_MODULE=0; CALL "ZZ2PRP","CV",CP_ENABLED$,""; IF CP_ENABLED$="Y" THEN CCV_MODULE=1; GOTO 0475 ! SSP 229993
0473 CALL "ZZ2PRP","CP",CP_ENABLED$,""; IF CP_ENABLED$<>"Y" THEN CALL "ZZ2PRP","CC",CC_ENABLED$,""; IF CC_ENABLED$<>"Y" THEN GOTO 9900
0474 ! Check if processing has been turned on based on which modules are activated - "CP" has priority over "CC"
0475 DIM ARPARAM$(255); FIND (Z[13],KEY=X3$(9,3)+"A/R")ARPARAM$
0476 CCMODE=-1
0477 IF ARPARAM$(241,1)="Y" THEN CCMODE=1 ELSE IF ARPARAM$(242,1)="Y" THEN CCMODE=0 ELSE GOTO 9900
0478 IF CCMODE<0 THEN GOTO 9900
0485 FIND (Z[13],KEY=X3$(9,3)+"AR1",DOM=*NEXT)AR1PARAM$ ! SSP 218074
0550 REM "Open Printer
0570 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0575,9910
0580 ! IF FID(0)<>FID(W9) THEN W0=34
0585 IF %HAS_IMAGE_LIB THEN CALL "ILGAUD",ERR=*NEXT,X3$,X4$,"RE" ! ssp 197202
0600 REM "Alternate Sort Info & total dim"
0610 DIM U0$(4)
0620 ON V0 GOTO 0630,0630,0635,0635
0630 DIM U[3]; U=14,U0=3,U[0]=0,U[1]=4,U[2]=2,U[3]=8,T0=0,V1$="ARB...",U0$="    ",T5$="                "; GOTO 0640
0640 DIM T[T0,T1]
0650 V5=V1+1,V6=V5+V2,V7=V6+V3
0655 GOSUB 6000
0660 REM " Position read"
0680 CALL "ZZFLES",X3$,Y1$,Y0$,"11OS"+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[11]
0700 IF LEN(V2$)>=U*2 THEN GOTO 0770
0710 REM "create default 'all' range key
0720 DIM V2$(U*2),V4$(U,"~"); U3=1
0730 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0740 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0750 NEXT U9
0760 REM "Get starting key from range key
0770 DIM V4$(U); U3=1,U4=1
0780 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0790 V4$(U4,U[U9])=V2$(U3,U[U9])
0800 NEXT U9
0810 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0820 READ (U1,KEY="",DOM=0825)
0840 PROCESS_NEXT_INVOICE:
0845 DIM EDD(6),EDD$(316) ! SSP#283387
0850 U$=KEY(U1,END=BATCH_DONE); READ (U1,END=BATCH_DONE)
0851 ! Y5$="Processing next invoice :"+U$; GOSUB PRINT_LINE; GOTO PROCESS_NEXT_INVOICE
0860 REM "check key against range info"
0870 ! U3=1,U2=1
0880 ! FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
0890 ! IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN READ (U1,END=9900); EXITTO PROCESS_NEXT_INVOICE
0900 ! IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U=1 THEN EXITTO 9900 ELSE READ (U1,END=9900); EXITTO PROCESS_NEXT_INVOICE
0910 ! NEXT U9
0920 REM "Get record"
0930 ! U9$=U$
0940 ! ON V0 GOTO 1150,1150,1130,1140
0960 INV_UPDATE$="N"; EDD_UPDATE$="N"; MSG_UPDATE$="N" ! ESCAPE
0961 READ (Z[1],KEY=U$,DOM=PROCESS_NEXT_INVOICE)IOL=0310
0965 IF A[9]<=0 THEN GOTO PROCESS_NEXT_INVOICE ! Zero invoice or Credits are not supported
0980 ! Check if invoice already processed
1001 IF A$(420,1)="Y" THEN Y5$="Invoice processing completed previously"; IF %CC_DEBUG THEN GOSUB PRINT_LINE
1010 ! If the terms code is not a credit card terms code skip this invoice, but only if it was always this way
1020 DIM TERMS$(100); READ (Z[3],KEY=A$(65,2),DOM=PROCESS_NEXT_INVOICE)IOL=0401
1100 Y5$="Terms code: "+TERMS$(1,58)+"|, Credit card type:"+TERMS$(57,1); IF %CC_DEBUG THEN GOSUB PRINT_LINE
1200 IF A$(420,1)=" " AND TERMS$(57,1)<>"Y" THEN Y5$="Terms code type - not credit card type."; IF %CC_DEBUG THEN GOSUB PRINT_LINE END_IF ; GOTO PROCESS_NEXT_INVOICE
1210 ! Find order for this invoice
1300 FOUND=0; CALL "UPDFS1;READBYKEY",Z[9],0,A$(92,2),A$(94,6),"","","",FS1$,FS1{ALL},FOUND
1301 IF FOUND=0 THEN Y5$=A$(92,8)+": Order not found for invoice"; GOSUB PRINT_LINE; GOTO INVOICE_DONE ! Order not found
1310 Y5$="Order :"+FS1$(118,8)+", Customer: "+FS1$(6,10); IF %CC_DEBUG THEN GOSUB PRINT_LINE
1315 DIM PO3$(428); READ (Z[14],KEY=A$(92,8)+DIM(5),DOM=*NEXT)PO3$(1) ! WO239471
1320 ! Get last credit card on file for this order
1321 ENTRY_EXISTS=0; CALL "UPDEDD;GET_LAST_INVOICE_ENTRY",Z[8],A$(92,2),A$(94,6),A$(7,8),EDD$,EDD{ALL},ENTRY_EXISTS
1400 IF ENTRY_EXISTS=0 THEN CALL "UPDEDD;GET_PREV_ENTRY",Z[8],A$(92,2),A$(94,6),EDD$,EDD{ALL},ENTRY_EXISTS
1402 IF ENTRY_EXISTS=0 THEN {
1403 Y5$=A$(7,8)+": No credit card specified for order               "; CALL "ZZDISP","AXS",A$(92,8),"O/P",X3$,Y5$,"",46,0,X4$
1404 A$(420,1)=" "; INV_UPDATE$="Y"
1405 GOSUB PRINT_LINE; GOTO INVOICE_DONE ! No credit card info for this order
1406  }
1410 Y5$=" EDD entry found for order! Key:"+EDD$(1,11)+", Status: "+EDD$(119,1)+", Approval code: "+EDD$(88,30); IF %CC_DEBUG THEN GOSUB PRINT_LINE
1412 ! 
1418 IF A$(420,1)=" " AND STP(EDD$(88,30),2)<>"" AND EDD$(213,8)=A$(7,8) THEN EDD$(119,1)="A",EDD_UPDATE$="Y",A$(420,1)="Y",INV_UPDATE$="Y",MSG_UPDATE$="Y" ! 204772
1420 IF A$(420,1)="Y" THEN GOTO INVOICE_DONE ELSE IF A$(420,1)="Z" THEN GOTO CARD_DECLINED ELSE A$(420,1)="P"; INV_UPDATE$="Y"
1430 ! Is a new EDD file entry needed to record this transaction?
1513 NEW_EDD_REQD=0; IF EDD$(213,8)<>DIM(8) AND EDD$(213,8)<>A$(7,8) AND (CCMODE=0 OR (CCMODE=1 AND EDD$(88,30)<>DIM(30))) THEN NEW_EDD_REQD=1 ! Invoice number not present, use this record
1514 IF NEW_EDD_REQD THEN Y5$="Could not use previous EDD info...invoice number present...record used for some other invoice"; IF %CC_DEBUG THEN GOSUB PRINT_LINE
1520 IF NEW_EDD_REQD THEN GOTO *NEXT ELSE GOTO DO_CC_MODE
1521 ! Create new EDD record as the existing record already used up
1530 EDD_NEXT_SEQ=-1; VALID_SEQ=0; CALL "UPDEDD;GET_NEXT_SEQ",Z[8],A$(92,2),A$(94,6),EDD_NEXT_SEQ,VALID_SEQ; IF VALID_SEQ<=0 THEN GOTO INVOICE_DONE
1531 EDD$(9,3)=STR(EDD_NEXT_SEQ:"000"),EDD$(80,38)=DIM(38),EDD$(119,1)=" ",EDD$(120,6)=DIM(6),EDD$(213,8)=DIM(8); FOR EDD0=0 TO 6; EDD[EDD0]=0; NEXT EDD0 ! Clear other EDD fields from "new" record
1540 EDD_OK=0; CALL "UPDEDD;INSERT",Z[8],EDD$,EDD{ALL},EDD_OK; IF EDD_OK=0 THEN Y5$="Error inserting duplicate EDD record."; IF %CC_DEBUG THEN GOSUB PRINT_LINE END_IF ; GOTO INVOICE_DONE
1550 ! New EDD record created
1600 ! 
1603 DO_CC_MODE:
1604 ! Is this "Plus" mode or "Regular" mode?
1616 FLAG$=""; CALL "AR2VCC",X3$,X4$,STP(EDD$(16,20),1)+"|"+EDD$(36,4),EDD$(12,4),FLAG$ ! Validate card number and expiry date
1626 IF FLAG$="GOOD" THEN EDD$(119,1)="G" ELSE EDD$(119,1)="B"
1630 EDD_UPDATE$="Y"
1710 IF CCMODE=0 THEN GOTO CHECK_EDD_CARD
1720 ! Prepare credit card transaction parameters
1800 CHARGE_CARD:
1802 IF NUL(EDD$(126,87)) THEN IF MID(V0$,72,1)="Y" THEN GOSUB GET_BILLING_ADDR ! SSP 218074
1805 NUM_ENTRIES=19 ! WO215237, WO227413, WO239471, 307309, 307455
1810 DIM CC$[NUM_ENTRIES+1,1] ! Data starts at index 1
1815 CC$[1,0]="CC_NUMBER"
1820 CC$[1,1]=STP(EDD$(16,20),2)
1822 IF CCV_MODULE THEN CC$[1,0]="CC_GUID",CC$[1,1]=STP(MID(EDD$,235,64),2) ! SSP 229993       
1825 CC$[2,0]="CC_NAME"
1830 CC$[2,1]=STP(EDD$(40,40),2)
1835 CC$[3,0]="CC_EXP"
1840 CC$[3,1]=STP(EDD$(36,4),2)
1845 CC$[4,0]="ADDRESS1"
1850 CC$[4,1]=STP(EDD$(126,30),2)
1855 CC$[5,0]="CITY"
1860 CC$[5,1]=STP(EDD$(186,16),2)
1865 CC$[6,0]="STATE"
1870 CC$[6,1]=STP(EDD$(202,2),2)
1875 CC$[7,0]="ZIP"
1880 CC$[7,1]=STP(EDD$(204,9),2)
1885 CC$[8,0]="INVOICE_TOTAL"
1890 CC$[8,1]=STR(A[9])
1895 CC$[9,0]="ADDRESS2"
1900 CC$[9,1]=STP(EDD$(156,30),2)
1910 CC$[10,0]="INVOICE_TAX"
1911 CC$[10,1]=STR(A[4]+A[6]+A[8])
1920 CC$[11,0]="CUSTOMER_ID"
1925 V_X$=""; CALL "ZZDISP","AX",A$(15,10),"A/R",X3$,V_X$,"",0,0,X4$; CC$[11,1]=V_X$
1930 CC$[12,0]="ORDER_NUM"
1940 V_X$=""; CALL "ZZDISP","AX",A$(92,8),"O/P",X3$,V_X$,"",0,0,X4$; CC$[12,1]=V_X$
1950 CC$[13,0]="CUSTOMER_UNFORMATTED" ! WO215237
1960 CC$[13,1]=A$(15,10) ! WO215237
1970 CC$[14,0]="CC_CVV" ! WO227413
1975 CC$[14,1]=STP(EDD$(231,4),2) ! WO227413
1980 CC$[15,0]="EMAIL" ! WO239471
1985 CC$[15,1]=STP(PO3$(299,40),3) ! WO239471, strip all blank spaces
1990 CC$[16,0]="CC_TYPE" ! WO239471
1995 CC$[16,1]=STP(EDD$(12,4),3) ! WO239471, strip all blank spaces
1997 CC$[17,0]="INV_NUM" ! 307309
1998 CC$[17,1]=STP(A$(7,8)) ! 307309
1999 CC$[18,0]="CUST_PROFILE_ID" ! 307309
2000 CC$[18,1]=STP(EDD$(299,40)) ! 307309
2001 CC$[19,0]="MERCH_ACCT" ! 307455
2002 CC$[19,1]=STP(EDD$(221,10)) ! 307455
2038 Y5$="Invoking credit card transaction program..."; IF %CC_DEBUG THEN GOSUB PRINT_LINE
2040 ! Charge credit card for the invoice amount
2050 SUCCESS_YN=0,RESPONSE_MSG$="",APPR_CD$="",MERCH_ACCT$="" ! WO215237
2055 IF CCV_MODULE THEN GOSUB CCV_APPRV; GOTO 2063 ! SSP 229993
2060 CALL "CAGCCV",X3$,X4$,CC${ALL},NUM_ENTRIES,SUCCESS_YN,RESPONSE_MSG$,APPR_CD$,MERCH_ACCT$ ! WO215237, need merchant account to write into EDD and print on the journal
2062 ! 
2063 EDD$(213,8)=A$(7,8) ! Invoice Number
2066 EDD[2]=A[9] ! Invoice Total
2068 EDD$(221,10)=MERCH_ACCT$ ! WO215237
2070 ! 
2080 ON SUCCESS_YN+1 GOTO CARD_DECLINED,CARD_ERROR,CARD_APPROVED ! 0-Card Declined, 1-Card Error, 2-Card Approved
2090 ! 
2100 CARD_ERROR: Y5$="Card transaction error. Response message: "+PAD(RESPONSE_MSG$,50); GOSUB PRINT_LINE
2101 EDD$(88,30)="ERROR" ! Approval code
2102 EDD$(119,1)="E" ! EDD Status - Card authorization error
2110 A$(420,1)="E"; INV_UPDATE$="Y"
2120 GOTO INVOICE_DONE
2300 ! 
2305 CARD_DECLINED:
2306 IF A$(420,1)="P" THEN MSG_UPDATE$="Y"; Y5$=A$(7,8)+": Card declined. Response message: "+RESPONSE_MSG$; GOSUB PRINT_LINE ! 
2310 ! Change invoice terms code to alt. terms code for this customer
2315 DIM ALT_TERMS_CD$(244)
2320 READ (Z[5],KEY="D"+FS1$(6,10),DOM=*NEXT,ERR=*NEXT)ALT_TERMS_CD$ ! ALT_TERMS_CD$(64,2)="02"
2322 DIM CCPLUS_ALT_TERMS_CD$(100); IF STP(ALT_TERMS_CD$(64,2),3)<>"" THEN FIND (Z(3),KEY=ALT_TERMS_CD$(64,2),DOM=2325)CCPLUS_ALT_TERMS_CD$ ! SSP#258784
2323 IF CCPLUS_ALT_TERMS_CD$(57,1)="Y" THEN EDD$(119,1)="Z",A$(420,1)="Z"; GOTO 2326 ! SSP#258784
2325 IF ALT_TERMS_CD$(64,2)=DIM(2) THEN EDD$(119,1)="Z"; A$(420,1)="Z" ELSE A$(65,2)=ALT_TERMS_CD$(64,2); EDD$(119,1)="D"; A$(420,1)="Y"
2326 INV_UPDATE$="Y"
2330 ! If terms code successfully changed, EDD$(119,1)="D", else EDD$(119,1) = "Z"
2335 A$(64,1)=" " ! Clear invoice print flag - SSP 213435
2340 EDD$(88,30)="DECLINED" ! Approval Code from credit card transaction
2345 INV_LINE_STATUS$="N"
2350 Y5$="The terms code "; IF EDD$(119,1)="D" THEN Y5$=Y5$+" was changed to the alt.terms code" ELSE Y5$=Y5$+" could not be changed" END_IF ; IF %CC_DEBUG THEN GOSUB PRINT_LINE
2365 GOTO INVOICE_DONE
2370 ! 
2400 CARD_APPROVED: Y5$="Card approved"; IF %CC_DEBUG THEN GOSUB PRINT_LINE ! 
2410 EDD$(88,30)=APPR_CD$ ! Approval Code from credit card transaction
2420 EDD$(119,1)="A" ! EDD Status : "Card Authorized - Amount charged"
2430 A$(420,1)="Y",INV_UPDATE$="Y"
2435 A$(64,1)=" " ! Clear invoice print flag - SSP 213435
2440 Y5$="Invoice paid with credit card "+EDD$(16,20)+" and approval code is "+APPR_CD$; IF %CC_DEBUG THEN GOSUB PRINT_LINE
2450 MSG_UPDATE$="Y"
2460 GOTO INVOICE_DONE
2470 ! 
2480 CHECK_EDD_CARD:
2481 Y5$="Regular credit card mode "; IF %CC_DEBUG THEN GOSUB PRINT_LINE ! 
2484 EDD$(213,8)=A$(7,8) ! Invoice Number
2485 EDD[2]=A[9] ! Invoice Total
2486 IF POS(EDD$(119,1)=" B")>0 THEN EDD_UPDATE$="Y",A$(420,1)=" ",INV_UPDATE$="Y" ELSE EDD_UPDATE$="Y",A$(420,1)="Y",INV_UPDATE$="Y"
2487 Y5$=Y5$+"EDD code : "+EDD$(119,1); IF %CC_DEBUG THEN GOSUB PRINT_LINE
2489 GOTO INVOICE_DONE
2490 ! 
2500 INVOICE_DONE:
2530 IF EDD_UPDATE$="Y" THEN EDD_BUSY=0; CALL "UPDEDD;UPDATE",Z[8],EDD$,EDD{ALL},EDD_BUSY; IF EDD_BUSY=1 THEN Y5$=EDD$(1,11)+": Unable to update EDD record! Error!!"; GOSUB PRINT_LINE
2560 IF INV_UPDATE$="Y" THEN WRITE (Z[1],KEY=U$,ERR=*NEXT)IOL=0310; GOTO 2620 ELSE GOTO 2620 ! Update invoice header
2590 Y5$=A$(7,8)+": Unable to update invoice header! Error !!"; GOSUB PRINT_LINE
2620 IF MSG_UPDATE$="Y" THEN GOSUB INVOICE_MESG_LINE
2650 Y5$="Completed this invoice... going to next invoice"; IF %CC_DEBUG THEN GOSUB PRINT_LINE
2710 DIM Y5$(W3)
2720 Y5$(1)=STR(++SEQ_NO)+"."; REM "Seq No 
2740 DIM AR1$(46); FIND (Z[7],KEY=A$(15,10),DOM=2800)IOL=0402
2770 CALL "ZZDISP","AX",AR1$(1,10),"A/R",X3$,X$,"",0,0,X4$; Y5$(14)=X$,Y5$(26)=AR1$(11,25); REM "Customer Id and Name
2800 IF POS(" "<>A$(86,6))<>0 THEN Y5$(54)=FND$(A$(86,6)); REM "Invoice date
2830 Y5$(65)=STR(EDD[2]:M2$); REM "Invoice Amount charged to card
2860 GOSUB GET_EDD_STATUS; Y5$(78,30)=STP(EDD_STATUS$,2); IF CCMODE THEN Y5$(110)=STP(EDD$(88,30),2); REM "EDD Status and Approval codes
2890 GOSUB PRINT_LINE
2891 Y5$(1)=A$(7,8); REM "Invoice number
2894 CALL "ZZDISP","AXS",EDD$(1,8),"O/P",X3$,Y5$,"",14,0,X4$; REM "Order Number
2897 Y5$(26)=EDD$(9,3); REM "EDD Sequence
2898 SPY=5,SPA=4; FOR SP0=0 TO 4; Y5$(35+(SPY*SP0))=A$(133+(SPA*SP0),4); NEXT SP0; REM "Salesperson codes 1-5
2899 Y5$(65)=STR(A[4]+A[6]+A[8]:M2$),Y5$(78)=MID(EDD$,12,4),Y5$(110)=EDD$(221,10); REM "Sales tax, WO215237, merchant acct
2900 GOSUB PRINT_LINE; GOSUB PRINT_LINE
2910 IF "DECLINED"=STP(UCS(EDD$(88,30)),2) THEN SUCCESS_YN=0 ELSE IF ("ERROR"=STP(UCS(EDD$(88,30)),2) OR EDD_STATUS$="UNKNOWN STATUS") THEN SUCCESS_YN=1 ELSE SUCCESS_YN=2; END_IF ; END_IF ; GOSUB SUBTOTALS ! SSP217126 jdf
2920 GOTO PROCESS_NEXT_INVOICE
2950 ! 
2960 BATCH_DONE:
2970 IF CCMODE>0 THEN GOSUB SUMMARY
2980 GOTO 9900
2990 ! 
5000 GET_BILLING_ADDR:! SSP 218074
5005 IF NOT(NUL(EDD$(126,87))) THEN GOTO *RETURN
5010 DIM BILLAR1$(500); FIND (Z[7],KEY=FS1$(6,10),DOM=*RETURN)BILLAR1$
5015 IF NOT(NUL(MID(BILLAR1$,46,10))) THEN FIND (Z[7],KEY=BILLAR1$(46,10),DOM=*NEXT)BILLAR1$
5020 IF MID(AR1PARAM$,64,1)="Y" THEN {
5025 DIM FSV$(155); FIND (Z[10],KEY=A$(92,8),DOM=*NEXT)FSV$
5030 IF MID(FSV$,9,10)=BILLAR1$(1,10) THEN EDD$(126,87)=MID(FSV$,54,87)
5035  }
5040 IF NUL(EDD$(126,87)) THEN EDD$(126,87)=MID(BILLAR1$,56,87)
5045 RETURN 
5049 ! 
6000 REM "Page header
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W4$,R6$; ON W5 GOTO 6041,9900
6060 GOSUB 6200
6090 RETURN 
6200 REM "Column titles"
6220 GOSUB PRINT_LINE
6230 DIM Y5$(W3); Y5$(1)="S.No.",Y5$(14)="Customer Name",Y5$(64)="Inv. Amount",Y5$(78)="Status",Y5$(110)="Approval Code" ! WO215237, WO 224979
6233 GOSUB PRINT_LINE
6234 DIM Y5$(W3); Y5$(1)="Invoice #",Y5$(14)="Order       Seq #",Y5$(35)="Salesperson",Y5$(54)="Date",Y5$(66)="Sales Tax",Y5$(78)="Card Type"; IF CCMODE THEN Y5$(110)="Merchant Acct" ! ,Y5$(120)="Terms Code" WO215237, add merchant acct, WO 224979
6290 GOSUB PRINT_LINE
6291 Y5$=T1$
6292 GOSUB PRINT_LINE
6295 RETURN 
7300 PRINT_LINE:REM "Output line Y5$ to output device
7310 W=W+1
7320 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$
7322 IF %IL_PDF THEN PRINT (%IL_PDF,ERR=*NEXT)Y5$ ! ssp 197202
7324 DIM Y5$(W3)
7325 IF W>=W0 THEN GOSUB 6000
7330 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     13O ZZPARM  "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7425,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7440 READ (Z[12],KEY=X3$(1,8),DOM=7445)IOL=0410
7450 V1$="ARB   "
7460 READ (Z[13],KEY="CMP"+X3$(9,3))C0$
7470 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1) ! SSP 307500
7480 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7510 INVOICE_MESG_LINE:
7520 Y5$="Invoice message lines..."; IF %CC_DEBUG THEN GOSUB PRINT_LINE
7530 CALL "AR2EPT",X3$,X4$,"AR2UPA",A$(1,14)
7531 ! ESCAPE
7540 RETURN 
7541 GET_EDD_STATUS:
7570 SWITCH EDD$(119,1)
7575 CASE "G"
7580 EDD_STATUS$="GOOD VALIDATION"; BREAK
7585 CASE "B"
7590 EDD_STATUS$="BAD VALIDATION"; BREAK
7595 CASE "N"
7600 EDD_STATUS$="NOT VALIDATED DURING ENTRY"; BREAK
7605 CASE "W"
7610 EDD_STATUS$="VALIDATED WEBEC"; BREAK
7615 CASE "A"
7620 EDD_STATUS$="AUTHORIZED-AMOUNT CHARGED"; BREAK
7625 CASE "E"
7630 EDD_STATUS$="CARD AUTHORIZATION ERROR"; BREAK
7635 CASE "D"
7640 EDD_STATUS$="DECLINED-TERMS CODE CHANGED"; BREAK
7645 CASE "Z"
7650 EDD_STATUS$="DECLINED-NO ALT. TERMS CODE"; BREAK
7655 DEFAULT 
7660 EDD_STATUS$="UNKNOWN STATUS"
7665 END SWITCH 
7700 RETURN 
7710 ! 
7800 SUMMARY:
7805 Y5$(1)="" ! 231960
7810 GOSUB PRINT_LINE; GOSUB PRINT_LINE; Y5$(1)=T1$; GOSUB PRINT_LINE
7811 Y5$(1)="Summary",Y5$(35)=FNS$("Declined"),Y5$(51)="Error",Y5$(65)="Approved"
7820 GOSUB PRINT_LINE; Y5$(1)=T1$; GOSUB PRINT_LINE
7830 Y5$(1)="Total credit card invoices :"; FOR I=0 TO 2; Y5$(30+I*15+6)=STR(TOTAL_CC_INVOICES[I]:"###0"); NEXT I; GOSUB PRINT_LINE
7840 Y5$(1)="Total invoice amount       :"; FOR I=0 TO 2; Y5$(30+I*15)=STR(TOTAL_INVOICE_AMOUNT[I]:M2$); NEXT I; GOSUB PRINT_LINE
7850 Y5$(1)="Total sales tax amount     :"; FOR I=0 TO 2; Y5$(30+I*15)=STR(TOTAL_TAX_AMOUNT[I]:M2$); NEXT I; GOSUB PRINT_LINE
7870 RETURN 
7880 ! 
7900 SUBTOTALS:
7910 TOTAL_INVOICE_AMOUNT[SUCCESS_YN]+=A[9]
7920 TOTAL_TAX_AMOUNT[SUCCESS_YN]+=A[4]+A[6]+A[8]
7930 TOTAL_CC_INVOICES[SUCCESS_YN]++
7940 RETURN 
7950 ! 
8000 REM "Modified stmts:
8010 REM "Added stmts:
8020 REM "Deleted stmts:
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9020 SETERR 9030; Y8$=LST(PGM(Y6))
9030 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9110,9175,9070,9100
9060 RETRY 
9070 SETERR 9090
9080 EXECUTE Y7$
9090 SETERR 9000; RETRY 
9100 SETERR 0000; RETRY 
9110 REM " TRANSFER CONTROL
9120 GOTO PROCESS_NEXT_INVOICE
9130 GOTO 9175
9140 SETESC 9170
9150 SETERR 9170
9160 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9170 SETERR 9000; RETURN 
9900 REM "End program
9905 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0 ! ESCAPE
9920 IF Y4$<>"" THEN RUN Y4$,ERR=9925
9930 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
10000 CCV_APPRV:! SSP 229993
10050 CALL "CVGCCV",X3$,X4$,CC${ALL},NUM_ENTRIES,SUCCESS_YN,RESPONSE_MSG$,APPR_CD$,MERCH_ACCT$ ! SSP 229993 Routine to get authorization for Vault Id 
10090 RETURN 
10099 ! 
56000 REM "196939-Alt. terms code still hardcoded to "02", rem out
56002 REM "200462-DT2-credit card being charged twice when coming through
56004 REM "200625-Credit Card Plus: Validation of card fails when billing addr
56006 REM "197202-Does the Credit Card Processing Report get saved to the
56008 REM "203254-A/R, IC report lines overflowing end of page, causing some
56010 REM "190790-Net1, wants to use Level II for a lower transaction rate
56014 REM "215237-Ability to use multiple Net1 merchant accounts in DemandBdge
56015 REM "213435-Force invoice reprint after processing in A/R, IC        
56016 REM "224979-Print the credit card type from EDD record on report
56018 REM "227413-Credit card transactions need to support CVV security field
56019 REM "231960-Getting message at bottom of AR-IC report "terms code type- 
56020 REM "218074-Changes to handle missing billing address, read from order 
56022 REM "239471-Use of Cybersource's Simple Order and Silent POST APIs to
56023 REM "258784-Add logic to prevent the use of a credit card terms code     as the alternate terms code
56024 REM "229993-PCI/PA-DSS New CC Module                                    
56025 REM "283387-Credit Card Plus Report is incorrect if the order has       
56026 REM "283829-add lock to Credit Card Processing report for ARB           
56027 REM "307455-DBD-387: Braintree support in CC Vault                      
56028 REM "307500-Display issues with Credit Card Processing Report           
