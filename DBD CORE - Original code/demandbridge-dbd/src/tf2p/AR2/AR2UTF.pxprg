0010 REM "Salesperson Change Utility  <AR2UTF>
0020 SETESC 9300; SETERR 9000
0035 REM "5.6 - 05/11/09 - 10.075277 - tma - SSP# 229742
0037 REM "229742-Change verbage on utility description for AR2UTF            
0040 REM "Copyright 2009 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="AR2UTF",X1$="A/R Salesperson Change Utility"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0340 IOLIST D$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O AR1...  03O ARU...  04O AR3...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0585 RT_PARM$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,RT_PARM$
0600 REM "
0610 GOSUB 6000
0700 REM 
0705 DIM B$(8)
0710 CALL "ZZENTR","SZUX",B{ALL},B$,X4$,X3$,30,10,1,4,C0,"","{1"+X$,"","AR2UTF00","AR3","",""; IF ABS(C0)>4 THEN ON C0 GOTO 0710,0711
0720 IF FNS$(B$(1,4))="" THEN GOTO 9900
0730 FIND (Z[4],KEY=B$(1,4),DOM=0710)IOL=0340; PRINT @(35,10),D$(5,35),
0760 CALL "ZZENTR","SZUX",B{ALL},B$,X4$,X3$,30,12,5,4,C0,"","{1"+X$,"","AR2UTF02","AR3","",""; IF ABS(C0)>4 THEN ON C0 GOTO 0760,0761
0770 IF FNS$(B$(5,4))="" THEN GOTO 9900
0780 IF B$(1,4)=B$(5,4) THEN GOTO 0760
0790 FIND (Z[4],KEY=B$(5,4),DOM=0760)IOL=0340; PRINT @(35,12),D$(5,35),
0800 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0801,9900
0910 READ (Z[1],KEY="",DOM=0911)
0990 PRINT @(10,13),"Customer:",
1000 REM 
1020 F9$="",K$=KEY(Z[1],END=5000); READ (Z[1],KEY=K$)A$
1025 C=C+1; IF MOD(C,100)=1 THEN PRINT @(20,13),"Record No: ",C,"  ",K$,
1030 A1$=A$(278,20)
1040 P=POS(B$(1,4)=A1$,4); IF P>0 THEN F9$="Y",A1$(P,4)=B$(5,4); GOTO 1040
1100 IF F9$="" THEN GOTO 1000
1120 EXTRACT RECORD (Z[1],KEY=K$)A$
1140 A$(278,20)=A1$
1160 WRITE RECORD (Z[1])A$; IF RT_PARM$<>"" THEN CUST$=A$(1,10); CALL "RT2WOC",ERR=1161,X3$,X4$,CUST$,"AR1...","U",A$(1,10)
1180 IF A$(278,4)<>B$(5,4) THEN GOTO 1000
1200 REM "Update Primary Salesperson Sort File
1220 WRITE (Z[3],KEY=A$(278,4)+K$)
1240 REMOVE (Z[3],KEY=B$(1,4)+K$,DOM=1241)
1600 GOTO 1000
5000 REM "EOJ
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(8,4),"This DBI utility program will allow you to change the salesperson",@(8,5),"code in the customer master to a new code.",@(16,7),"***Progress will be reported every 100 entries***", ! SSP#229742
6030 PRINT @(2,10),"Salesperson Code to Change:",@(8,12),"New Salesperson Code:", ! SSP#229742
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "229742-Change verbage on utility description for AR2UTF            
