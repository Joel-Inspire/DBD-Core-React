0010 REM "<AR2MCP> Copy Customer
0020 SETESC 9300; SETERR 9000
0035 REM "5.6 - 12/26/07 - 15.025277 - crg - SSP# 209901
0037 REM "209901-System check to prevent duplicate customer registration
0040 REM "Copyright 2007 DemandBridge, Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0094 REM "Options are passed in Q0$: Q0$(1,10)-Template customer code, Q0$(11,10)-New customer code
0100 SETERR 9000
0110 X0$="AR2MCP",X1$="Copy Customer Utility"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0136 DIM BLANK$(20)
0142 IF LEN(Q0$)=20 THEN GOSUB SET_OPTIONS; Q0$="" ! Options present, process them; Replace Q0$ contents with error message if any, else blank
0200 REM "
0240 IF NUL(Q1$) THEN CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOL_AR1:IOLIST AR1$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0507 Z$="01O FMP... 02O AR0...  03O AR1...  04O ARE...  05O ARU...  06O ARW...  07O ASH...  08O AS5...  09O ASI...  10O ASW...  11O ASX...  12O AS8...  13O ZZPARM  15O ECS...  16O XPE...  17O XPD...  18O XPZ...  19O XPR...  41OSFMP... 42OSAR0...  43OSAR1...  44OSARE...  45OSARU...  46OSARW...  47OSASH...  48OSAS5...  49OSASI...  50OSASW...  51OSASX...  52OSAS8...  55OSECS...  56OSXPE...  57OSXPD...  58OSXPZ...  59OSXPR...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 IF POS(X3$(9,3)="060338363364365366476",3)<>0 THEN ARWKEYLEN=22 ELSE ARWKEYLEN=20 ! 209901, UK companies have 12 char phone numbers
0600 REM "If interactive mode, ask for user input
0610 IF NUL(Q1$) THEN GOSUB 6000 ELSE GOTO 0700
0615 DIM OLD_CUST$(10),NEW_CUST$(10)
0630 CALL "ZZENTR","AXUF",TMP{ALL},OLD_CUST$,X4$,X3$,30,10,1,10,C0,"A/R","{1"+"Enter customer code to use for template customer","","AR2MCP00","AR1","",""; IF ABS(C0)=4 THEN GOTO 9900 ELSE IF ABS(C0)>4 THEN GOSUB 6000; ON C0 GOTO 0630,0631
0631 READ (Z[3],KEY=OLD_CUST$,DOM=0630)IOL=IOL_AR1; PRINT @(42,10),AR1$(11,35),
0640 CALL "ZZENTR","AXUF",TMP{ALL},NEW_CUST$,X4$,X3$,30,11,1,10,C0,"A/R","{1"+"Enter customer code to use for new customer","","AR2MCP02","AR1","",""; IF ABS(C0)=4 THEN GOTO 9900 ELSE IF ABS(C0)>4 THEN GOSUB 6000; ON C0 GOTO 0640,0641
0650 ! 
0660 IF NUL(Q1$) THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO COPY_CUSTOMER,0600
1099 ! 
2000 COPY_CUSTOMER:
2020 FIND (Z[3],KEY=NEW_CUST$,DOM=*NEXT)NEW_AR1$; GOTO ERR_CUST_EXISTS
2040 RESTORE BEGIN_KEYED_DATA
2060 NEXT_FILE:
2080 READ DATA FCHAN,FILENAME$,FTYPE$,KEYPFX$,KEYLEN,KEYOFFSET,SORTOFFSET,ERR=EOJ
2100 DIRECT_FILE:
2110 IF FTYPE$="D" THEN {
2120 READ (Z[FCHAN],KEY=KEYPFX$+OLD_CUST$(1,9)+CHR(ASC(OLD_CUST$(10,1))-1),DOM=*NEXT)
2140 NEXT_REC:
2150 FCHAN_KEY$=KEY(Z[FCHAN],END=2240); READ RECORD (Z[FCHAN],KEY=FCHAN_KEY$,DOM=2240)FREC$
2160 IF KEYPFX$+OLD_CUST$<>MID(FCHAN_KEY$,1,LEN(KEYPFX$+OLD_CUST$)) THEN GOTO 2240
2180 FREC$(KEYOFFSET,10)=NEW_CUST$
2181 WRITE RECORD (Z[40+FCHAN],KEY=FREC$(1,LEN(FCHAN_KEY$)))FREC$
2190 IF FILENAME$="ASW" THEN GOSUB COPY_ASX END_IF 
2200 GOTO NEXT_REC
2220  }
2240 SORT_FILE:
2250 IF FTYPE$="S" THEN IF FILENAME$="ARW" THEN KEYLEN=ARWKEYLEN; END_IF ; WRITE (Z[FCHAN],KEY=AR1$(SORTOFFSET,KEYLEN-10)+NEW_CUST$+TBL(FILENAME$="ASH","",DIM(4)),ERR=*NEXT)
2260 GOTO NEXT_FILE
2280 ! 
2300 ERR_CUST_EXISTS:
2320 IF NUL(Q1$) THEN {
2340 CALL "ZZPROM",".4",X3$,Z,"Error! Customer exists : "+NEW_AR1$(11,35),"","",0
2360 GOTO 0600
2380  } ELSE {
2400 Q0$="Error! Customer exists : "+NEW_AR1$(11,35)
2420  }
2440 ! 
5000 REM "EOJ
5010 EOJ:
5070 IF NUL(Q1$) THEN {
5071 PRINT @(0,15),'CE',
5080 CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5081 GOTO 0600
5082  }
5090 GOTO 9900
5099 ! Direct file data entries: File channel, File, File type, Key prefix, Key length, Cust key offset
5100 BEGIN_KEYED_DATA:
5120 DATA 1,"FMPE","D","E",15,2,0
5130 DATA 1,"FMPD","D","D",11,2,0
5140 DATA 1,"FMPA","D","A",11,2,0
5150 DATA 1,"FMPR","D","R",21,2,0
5160 DATA 2,"AR0","D","",10,1,0
5170 DATA 3,"AR1","D","",10,1,0
5220 DATA 8,"AS5","D","",10,1,0
5230 DATA 9,"ASI","D","",10,1,0
5240 DATA 10,"ASW","D","",14,1,0
5270 DATA 15,"ECS","D","",10,1,0
5280 DATA 16,"XPE","D","E",15,2,0
5290 DATA 17,"XPD","D","D",11,2,0
5300 DATA 18,"XPZ","D","A",11,2,0
5310 DATA 19,"XPR","D","R",21,2,0
5311 ! Sort file data entries: File channel, File, File type, Key prefix, Key length, AR1 data offset
5320 BEGIN_SORT_DATA:
5330 DATA 4,"ARE","S","",20,11,155
5340 DATA 5,"ARU","S","",14,5,278
5350 DATA 6,"ARW","S","",20,11,205
5360 DATA 7,"ASH","S","",23,10,134
5370 ! DATA 11,"ASX","S","",14,5,10
5380 DATA 12,"AS8","S","",30,21,411
6000 REM "BACKGROUND
6002 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(5,5),"This TopForm utility copies a customer to a new customer.",
6030 PRINT @(5,10),"Existing customer code :",
6035 IF NOT(NUL(OLD_CUST$)) THEN CALL "ZZDISP","A",OLD_CUST$(1,10),"A/R",X3$,"","",30,10,X4$
6040 PRINT @(5,11),"     New customer code :",
6050 IF NOT(NUL(NEW_CUST$)) THEN CALL "ZZDISP","A",NEW_CUST$(1,10),"A/R",X3$,"","",30,11,X4$
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6199 ! 
7000 REM "Process options from calling program
7005 SET_OPTIONS:
7010 DIM OLD_CUST$(10),NEW_CUST$(1)
7020 OLD_CUST$=MID(Q0$,1,10) ! Old customer code
7030 NEW_CUST$=MID(Q0$,11,10) ! New customer code
7045 RETURN 
7049 ! 
7050 REM "Special handling for ASX sort file, multiple records 
7060 COPY_ASX:
7070 WRITE (Z[FCHAN],KEY=FREC$(11,4)+NEW_CUST$)
7095 RETURN 
7100 ! 
8199 ! 
8910 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"      ")-1)
9000 REM "ERROR PROCESSING
9001 MSGBOX "Error handler"; ESCAPE 
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1150,2090
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; IF NOT(NUL(Q1$)) THEN Q1$=A1$
9935 EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56001 REM "209900-TopForm utility to allow customer to be copied
56002 REM "209901-System check to prevent duplicate customer registration
