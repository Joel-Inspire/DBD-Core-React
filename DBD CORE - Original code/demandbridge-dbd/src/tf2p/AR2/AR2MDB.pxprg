0010 REM "House Charge Setup <AR2MDB>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 01/24/14 - 12.827222 - tma - SSP# 267962
0037 REM "267962-Would like to setup a house charge with a maximum amount.   
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0044 ! *******************************************************************
0045 ! THIS PROGRAM WILL NO LONGER BE MODIFIED. THE GUI PANEL WILL BE PROCESSED  FROM HERE. ALL FUTURE CHANGES NEED TO BE MADE IN ARGMDB PANEL AND         PROGRAM . (RE SSP#267962) 
0046 ! ********************************************************************
0050 IF TCB(88)=0 THEN MSGBOX "You must be using Windx to access this program" ELSE PROCESS "ARGMDB","../ARG/AR.EN" ! SSP#267962                         
0055 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT ! SSP#267962                      
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="AR2MDB",X1$="House Charge Code Setup",K9$="@",K9=0
0120 DIM Z0$(80,"-"),S0$(30)
0130 K0=5,K1=1
0135 C9=-1
0140 M0$="###.00-",M1$="##,###,###.00-"
0160 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1),A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X AR4...  02S AR4...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0590 READ (Z[1],KEY=K9$,DOM=0591)
0600 REM "
0620 GOSUB 6000
0700 REM "J8 is X0 value of first range field
0705 J8=5
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1035 DIM A$(48),A[23]
1045 A$(1)=A1$; IF A1$="" THEN A$(1)="@"
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1500
1200 REM "Commission code
1210 CALL "ZZENTR","SZX",A{ALL},A$,X4$,X3$,19,3,2,1,C0,"","{1"+X$,K9$,"AR2MDB00","AR4","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9800,6970
1220 IF POS(A$(2,1)="123456789")=0 THEN GOTO 1210
1240 GOTO 1120
1500 REM "
1505 Z9=-1,A2$="",A3$="",A4$="",A5$=""
1510 A1$=A$(1,K9+5)
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1525 Z9=0
1590 GOTO 1700
1600 REM "
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630
1630 GOTO 1000
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1820 CALL "ZZPROM",STR(6+C9),X3$,Z,"","","V",X0; ON Z GOTO 1850,6500,1630,6700
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310
1865 C9=1
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF C0>3 OR X0<0 THEN GOTO 1800
2040 ON X0 GOTO 2100,2200,2300,2550,2150,2500,2250,2400,2450,2400,2450,2400,2450,2400,2450,2400,2450,2400,2450,2400,2450,1800
2100 REM "Description
2110 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,19,4,6+K9,25,C0,"","{3"+X$,"","AR2MDB02","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2110,2111
2140 GOTO 2020
2150 REM "House charge minimum
2160 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,19,7,0,320,C0,M0$,"{3"+X$,"","AR2MDB04","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2160,2161
2190 GOTO 2020
2200 REM "Commission method
2205 X$="1=cost, 2=# of cartons, 3=# of lines, 4=sale, 5=GP/%sale, 6=storage, 7=Adj GP" ! SSP 234793
2210 CALL "ZZENTR","SRU",A{ALL},A$,X4$,X3$,19,6,31+K9,1,C0,"","{3"+X$,"1234567","AR2MDB06","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2210,2211 ! SSP 234793
2240 GOTO 2020
2250 REM "Base commission
2255 IF A$(31,1)="6" THEN X$="If applicable, use for # days in a storage period otherwise leave zero"
2260 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,45,10,1,320,C0,M0$,"{3"+X$,"","AR2MDB08","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2260,2261
2290 GOTO 2020
2300 REM "Chargeback against commission
2302 IF A$(31,1)="6" THEN A$(32,1)=""; GOTO 2350
2305 X$="Y=reduction in commission, N=reduction in gross profit"
2310 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,55,6,32+K9,1,C0,"","{3"+X$,"","AR2MDB10","","",""
2340 GOTO 2020
2350 REM "Type 6 additional % load - apply to cost when storage charge is >0
2360 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,72,7,17,320,C0,M0$,"{3"+X$,"","AR2MDB48","","",""
2390 GOTO 2020
2400 REM "Range info
2402 REM IF POS(A$(31,1)="PN")=0 THEN GOTO 02020
2405 J5=X0-J8,J6=10+J5/2
2406 IF J5<>2 THEN IF A[J5-2]=0 THEN GOTO 2020
2407 J4=A[J5]
2410 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,1,J6,J5,822,C0,M1$,"{3"+X$,"","AR2MDB12","","",""
2415 IF C0=-2 THEN IF A[J5]=0 THEN PRINT @(1,J6),S0$(1,LEN(M1$)),; GOTO 2440 ELSE GOTO 2440
2420 IF A[J5]=0 THEN A[J5+1]=0; PRINT @(0,J6),'CL',; GOSUB 7300; GOTO 2440
2425 IF J5>2 THEN IF A[J5]<=A[J5-2] THEN GOTO 2410
2430 IF J4<>A[J5] THEN IF J5<>2 THEN PRINT @(18,J6-1),"to",@(22,J6-1),A[J5]-.01:M1$,; IF J4=0 THEN PRINT @(22,J6),"and over",
2440 GOTO 2020
2450 REM "Commission rate
2455 J5=X0-J8,J6=10+(J5-1)/2
2456 IF A[J5-1]=0 THEN GOTO 2020
2460 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,45,J6,J5,320,C0,M0$,"{3"+X$,"","AR2MDB14","","",""
2490 GOTO 2020
2500 REM "House Charge Maximum
2510 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,47,7,16,320,C0,M0$,"{3"+X$,"","AR2MDB04","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2160,2161
2540 GOTO 2020
2550 REM "Is charge back on line or invoice basis
2552 IF A$(31,1)="6" OR A$(32,1)<>"Y" THEN A$(33,1)=""; PRINT @(75,6),"  ",; GOTO 2020
2555 X$="Y=chargeback on invoice total based upon comm code in header"
2560 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,75,6,33,1,C0,"","{3"+X$,"","AR2MDB12","","",""
2590 GOTO 2020
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(0,3+V0),"House charge code:",@(6,4+V0),"Description:",@(0,5+V0),Z0$,
6070 PRINT @(1,6+V0),"House chg method:",@(33),"Chargback commission?",@(60),"Invoice total?",@(3,7+V0),"Minimum charge:",@(31),"Maximum charge:",@(56),"Method 6 addl%:",@(0,8),Z0$,
6090 PRINT @(8,9),"----House Charge % Table----   House charge rate",
6100 PRINT @(8,10),"From",@(30),"To",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 PRINT @(19,4),A$(6+K9,25),@(19,6),A$(31+K9,1),@(55,6),A$(32+K9,1),@(75,6),A$(33,1),@(19,7),A[0]:M0$,@(47,7),A[16]:M0$,@(72,7),A[17]:M0$,
6250 GOSUB 6300
6290 RETURN 
6300 REM "Print table
6310 PRINT @(45,10),A[1]:M0$,
6315 FOR I=2 TO 14 STEP 2
6320 IF I<14 AND A[I+2]>0 THEN PRINT @(1,10+I/2),A[I]:M1$,@(18),"to",@(22),A[I+2]-.01:M1$,@(45),A[I+1]:M0$, ELSE IF I=2 AND A[I+1]=0 THEN EXITTO 6331 ELSE PRINT @(1,10+I/2),A[I]:M1$,@(22),"and over",@(45),A[I+1]:M0$,; EXITTO 6331
6330 NEXT I
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 PRINT @(19,3),A$(2,1),
6490 RETURN 
6500 REM "DELETE
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY
6705 IF Z9=0 THEN GOTO 6790
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6825 A$(1,LEN(A1$))=A1$
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
7300 REM "Move other amounts up if necessary
7305 I=J5
7310 IF I>=14 THEN GOTO 7340
7315 IF A[I+2]<>0 THEN A[I]=A[I+2],A[I+1]=A[I+3],A[I+2]=0,A[I+3]=0,I=I+2; GOTO 7310
7330 IF I<>J5 THEN PRINT @(0,11),'CE',; GOSUB 6300 ELSE IF J5<>2 THEN PRINT @(18,J6-1),"  ",@(22),S0$(1,LEN(M1$)),@(22),"and over",
7340 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "234793-New House Charge Method, like method 5, but apply to GP amt 
