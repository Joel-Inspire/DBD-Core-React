0010 REM "Customer Constant Data <AR2MAB>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 04/04/17 - 17.009722 - tma - SSP# 291356
0037 REM "291356-CSR field in AR1 has wrong message identifying field        
0038 REM "203458 - SPR FOR TOPS (JV)
0040 REM "Copyright 2017 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$; CALLED$="Y"
0100 SETERR 9000
0110 X0$="AR2MAB",X1$="Customer Constant Data",K9$="",K9=0
0120 DIM Z0$(80,"-"),C1$(40),C2$(40),C3$(40),C4$(40),C5$(40),S0$(30)
0125 V8$="0204111499"
0130 K0=10,K1=1
0135 C9=-1,V9=-2
0140 M0$="-###.00"
0145 IF Q1$="" THEN GOSUB 8950 ELSE IF Q1$<>"" AND POS(" "<>Q1$)=0 OR Q1$="INV ENTRY" THEN Q1$="",Q7=1
0200 REM "
0210 CALL "ZZ2PRP","OS",RESULT$,DATE$
0230 REM "IF Q1$<>"" THEN LET X2=-1; PRINT @(0,6),'CE', SP [112013] rem'ed this line so that the correct program name would be displayed.
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1),A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15]
0320 IOLIST B$,B[0],B[1],B[2],B[3]
0330 IOLIST C$
0331 IOLIST C1$
0332 IOLIST C2$
0333 IOLIST C3$
0334 IOLIST C4$
0335 IOLIST C5$
0350 IOLIST E$
0360 IOLIST A5$
0390 IOLIST I$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X AR1...  02O AR5...  03O AR3...  05O ARG...  06O ZZPARM  07O ARH...  08O ARU...  09O FMP...  10O ARW...  11O OSA...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 DIM SEC$(255); FIND (Z[6],KEY="osec"+X3$(40,3),DOM=0531)SEC$
0550 RESTRICT_SLSP=0; IF MID(SEC$,169,1)="Y" THEN RESTRICT_SLSP=1 ! WO 274987
0585 W9$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,W9$
0600 REM "
0700 REM "Get date"
0720 D0$=X3$(15,6)
0725 READ (Z[6],KEY=X3$(9,3)+"A/R",DOM=9800)P0$
0730 READ (Z[6],KEY=X3$(9,3)+"AR1",DOM=9800)P2$
0820 GOSUB 6000
0900 REM 
0910 CALL "ZZDISP","AX","XXXXXXXXX","ARG",X3$,J$,"",0,0,X4$
0920 J3=22+LEN(J$),J2=47-J3
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(37),B[3],C$(234),A5$(100),E$(50)
1035 DIM A$(599+K9),A[15]
1036 %IL_IMAGE_TYPE$="",%IL_IMAGE_KEY$=""
1040 IF V9<>-1 THEN GOSUB 6900
1045 A$(1,0+K9)=K9$+A1$
1060 IF Q1$<>"" THEN GOTO 1100 ELSE PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1105 IF X3$(188,8)="DEFAULTS" THEN READ (Z[13],KEY="DFLT"+X0$,DOM=2000)IOL=0310; GOSUB 7900; GOSUB 6200; GOTO 2000
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1500
1200 REM "Customer code
1205 IF Q1$<>"" THEN A$(1,10)=Q1$; GOSUB 6450; GOTO 1220
1210 CALL "ZZENTR","AXUX",A{ALL},A$,X4$,X3$,18,3+V0,1+K9,10,C0,"A/R","{1"+X$,"","AR2MAB00","AR1","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9800,6970
1220 IF A$(1,2)<>"00" THEN READ (Z[7],KEY=A$(1,2),DOM=1225); GOTO 1230 ELSE GOTO 1230
1225 CALL "ZZPROM","4",X3$,S3,"Division "+A$(1,2)+" is not on file. Press |<Return>","","",0; GOTO 1210
1230 IF Q7=1 AND POS(" "<>Q1$)=0 THEN Q1$=A$(1,10)
1240 GOTO 1120
1500 REM "
1505 Z9=-1
1510 A1$=K9$+A$(LEN(K9$)+1,10),A$(1,LEN(K9$))=K9$,A2$="",A3$="",A7$=""
1511 IF Q0$="1" OR Q7=1 THEN READ (Z[13],KEY="DFLT"+X0$,DOM=1512)IOL=0310; A$(229,6)=""; IF Q7<>1 OR Q1$<>"" THEN A$(1,10)=Q1$; GOSUB 6200
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1524 IF %HAS_IMAGE_LIB THEN %IL_IMAGE_TYPE$="CUSTOMER_NOTE",%IL_IMAGE_KEY$=A1$
1525 Z9=0
1526 A2$=A$(155,10),A7$=A$(134,9)
1528 A3$=A$(278+K9,4)+A$(1+K9,10)
1529 A4$=A$(205,10)+A$(1,10),A6$=A$(411,20)+A$(1,10); IF POS(X3$(9,3)="060338363364365366476",3)<>0 THEN A4$=A$(205,12)+A$(1,10)
1530 FIND (Z[1],KEY=A$(46+K9,10),DOM=1531)IOL=0360
1535 FIND (Z[2],KEY=A$(235+K9,10),DOM=1536)IOL=0320
1540 FIND (Z[3],KEY=A$(278+K9,4),DOM=1541)IOL=0331
1545 FIND (Z[3],KEY=A$(282+K9,4),DOM=1546)IOL=0332
1550 FIND (Z[3],KEY=A$(286+K9,4),DOM=1551)IOL=0333
1555 FIND (Z[3],KEY=A$(290+K9,4),DOM=1556)IOL=0334
1560 FIND (Z[3],KEY=A$(294+K9,4),DOM=1561)IOL=0335
1565 FIND (Z[5],KEY=A$(220+K9,9),DOM=1566)IOL=0350
1590 GOTO 1700
1600 REM "
1605 IF W9$>"" THEN IF W9$(7,1)="R" THEN CALL "ZZPROM",".4",X3$,0,"Remote T/M users cannot add records to this information.","","",0; GOTO 1630
1610 IF Q0$="1" OR Q7=1 THEN IF SEC$(44,1)="Y" THEN GOTO 1650 ELSE Q0$="",Z9=1; GOSUB 7800; GOTO 1950
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1621,6975
1625 A1$=""
1640 IF Q1$<>"" OR Q7=1 THEN GOTO 9700 ELSE GOTO 1000
1650 IF SEC$(44,1)="Y" OR SEC$(57,1)="Y" THEN CALL "ZZPROM",".4",X3$,0,"Access denied! You may not set up a new customer","","",0; A1$(1)="",A$(1,10)=""; IF Q0$="1" OR Q7=1 THEN GOTO 9700 ELSE GOTO 1000
1655 A$(397,1)="Y"; GOSUB 7800; O9$="1"
1660 IF V9>=0 THEN GOSUB 6200
1690 GOTO 2000
1700 REM "
1710 GOSUB 7800
1790 GOSUB 6200
1800 REM "
1802 IF POS(X3$(9,3)="060338363364365366476",3)>0 THEN IF X3$(43,2)<"90" THEN CALL "ZZPROM","4",X3$,Z,"","","",0; GOTO 6700
1803 IF X3$(30,1)="M" THEN IF X3$(330,1)<>"1" THEN IF A$(1,2)>"10" THEN CALL "ZZPROM","4",X3$,0,"","","",0; GOTO 9700
1810 IF Z9<>-1 THEN IF P0$(126,1)="Y" THEN CALL "ZZPROM","1 AR2MAB",X3$,Z,"","","V",X0; ON Z+1 GOTO 1850,6700,1850,4000 ELSE CALL "ZZPROM","1",X3$,Z,"","","V",X0; ON Z+1 GOTO 1850,6700,1850; REM "SP [112013] cred. requestor on
1820 IF P0$(126,1)="Y" THEN CALL "ZZPROM",STR(6+C9)+"S",X3$,Z,"","","V",X0; ON Z GOTO 1850,6500,1630,6700,6970,4000 ELSE CALL "ZZPROM",STR(6+C9),X3$,Z,"","","V",X0; ON Z GOTO 1850,6500,1630,6700,6970; REM "SP [112013] cred. requestor on.
1830 GOTO 2000
1850 REM "MODIFY
1851 IF W9$>"" THEN IF W9$(7,1)="R" THEN CALL "ZZPROM",".4",X3$,0,"Remote T/M users cannot modify this information","","",0; GOTO 1800
1852 IF SEC$(45,1)="Y" THEN CALL "ZZPROM",".4",X3$,0,"Access denied! You may not modify this data","","",0; GOTO 1800
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310
1865 C9=1
1870 IF A$(229+K9,2)="  " THEN A$(229+K9,6)=D0$
1875 REM GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
1950 REM 
1960 IF P2$(66,1)="Y" THEN CALL "AR2MAS",X3$,X4$,A$,X$; IF X$>"" THEN GOSUB 6400
2000 REM "DATA SEQ
2005 IF A$(229+K9,2)="  " THEN A$(229+K9,6)=D0$
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF INT(C0)=3 THEN GOSUB 7300 ELSE IF C0>3 OR X0<0 THEN GOTO 2050
2030 IF SEC$(57,1)="Y" THEN IF X0>3 THEN GOTO 2050 ELSE IF X0<>0 AND X0<>2 THEN X0=X0+1; GOTO 2030
2040 ON X0 GOTO 2100,2150,2200,2700,2750,2800,2850,2900,2950,3000,3300,3350,3400,3450,3050,3100,3700,3150,3200,3250,3200,3250,3200,3250,3200,3250,3200,3250,3500,3550,3600,3650,2050
2050 GOSUB 7600; GOSUB 8000; GOTO 1800
2100 REM "Customer name
2110 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,44,3+V0,11+K9,35,C0,"","{3"+X$,"","AR2MAB02","","",""
2140 GOTO 2020
2150 REM "Bill-to customer
2155 IF P0$(15,1)<>"Y" THEN GOTO 2020
2160 CALL "ZZENTR","AX",A{ALL},A$,X4$,X3$,18,4+V0,46+K9,10,C0,"A/R","{3"+X$,"","AR2MAB04","AR1","AR2MAA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2160,2161
2165 IF POS(" "<>A$(46+K9,10))=0 THEN PRINT @(44,4+V0),'CL',; GOTO 2020
2170 FIND (Z[1],KEY=A$(46+K9,10),DOM=2160)IOL=0360
2175 PRINT @(44,4+V0),A5$(11,35),
2190 GOTO 2020
2200 REM "Address Module"
2202 DIM TAX$(10," "); REM SSP 199160
2205 A9$=A$(56+K9,163)+TAX$; REM SSP 199160
2210 CALL "ZY2AAA",X3$,X4$,0,6+V0,"A","Purchasing Agent",2,A9$,A$(11+K9,35),0,C0,A9
2220 A$(56+K9,163)=A9$(1,163)
2221 IF A9$(164,10)<>DIM(10) THEN A$(235,10)=A9$(164,10); REM SSP 199160,251220
2225 IF P2$(67,1)="Z" THEN IF A$(298,54)<>A$(165,54) THEN A$(298,54)=A$(165,54)
2230 IF ABS(C0)>4 THEN GOSUB 6400; GOTO 2210
2231 GOSUB 6210; REM SSP 199160,251220
2235 FIND (Z[2],KEY=A$(235+K9,10),DOM=2236)IOL=0320; GOSUB 6350; REM SSO 199160
2240 GOTO 2020
2700 REM "Customer Status
2703 GOTO 2740
2705 IF A$(219+K9,1)=" " AND C0<>2 THEN GOTO 2020
2710 CALL "ZZENTR","SURX",A{ALL},A$,X4$,X3$,21,11+V0,219+K9,1,C0,"","{3"+X$," IAS","AR2MAB26","IAS","",""
2720 IF A$(219+K9,1)=" " AND C0<>-2 THEN GOTO 2710
2740 GOTO 2020
2750 REM "Customer category
2760 CALL "ZZENTR","AU",A{ALL},A$,X4$,X3$,21,12+V0,220+K9,9,C0,"ARG","{3"+X$,"","AR2MAB28","ARG","AR2MPF",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2760,2761
2770 IF POS(" "<>A$(220+K9,9))=0 THEN GOTO 2020
2780 FIND (Z[5],KEY=A$(220+K9,9),DOM=2760)IOL=0350
2785 PRINT @(J3,12+V0),E$(10,J2),
2790 GOTO 2020
2800 REM "Date account opened
2810 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,21,13+V0,229+K9,6,C0,"","{3"+X$,"","AR2MAB30","","",""
2840 GOTO 2020
2850 REM "Sales Tax code
2860 CALL "ZZENTR","AUX",A{ALL},A$,X4$,X3$,21,14+V0,235+K9,10,C0,"AR5","{3"+X$,"","AR2MAB32","AR5","AR2MEA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2860,2861
2865 IF C0<0 THEN GOTO 2020
2870 FIND (Z[2],KEY=A$(235+K9,10),DOM=2860)IOL=0320
2875 GOSUB 6350
2890 GOTO 2020
2900 REM "Customer tax exempt?
2905 X$="Y = exempt, N = taxable, S = varies by invoice line"
2910 CALL "ZZENTR","SRU",A{ALL},A$,X4$,X3$,21,15+V0,245+K9,1,C0,"","{3"+X$,"YNS","AR2MAB34","","",""
2940 GOTO 2020
2950 REM "Resale number
2960 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,15,16+V0,246+K9,16,C0,"","{3"+X$,"","AR2MAB36","","",""
2990 GOTO 2020
3000 REM "SIC code
3010 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,15,17+V0,262+K9,5,C0,"","{3"+X$,"","AR2MAB38","","",""
3040 GOTO 2020
3050 REM "Finance  charge code
3055 X$="Y=if appropriate, charge finance charge, N=never charge finance charges"
3060 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,65,11+V0,267+K9,1,C0,"","{3"+X$," YN1234","AR2MAB40","","",""
3090 GOTO 2020
3100 REM "Statement code
3105 X$="Y=if appropriate, send statement, N=never send a statement"
3110 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,65,12+V0,268+K9,1,C0,"","{3"+X$,"","AR2MAB42","","",""
3140 GOTO 2020
3150 REM "Mailing label types
3160 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,65,13+V0,269+K9,3,C0,"","{3"+X$,"","AR2MAB44","","","02H1"
3190 GOTO 2020
3200 REM "Salesperson code #1
3202 IF RESTRICT_SLSP OR POS("r"=X3$(154,6))>0 THEN GOTO 2020 ! WO 274987
3205 J0=(X0-18)/2,J1=J0*4; IF J0=0 THEN GOTO 3210
3207 IF POS(" "<>A$(278+K9+(J0-1)*4,4))=0 OR P2$(8,1)<STR(J0+1:"0") THEN GOTO 2020
3210 CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,40,16+V0+J0,278+K9+J1,4,C0,"","{3"+X$,"","AR2MAB46","AR3","AR2MBB",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3210,3211
3212 S0=J0+1; GOSUB 7700
3215 IF C0<0 THEN GOTO 2020
3220 FIND (Z[3],KEY=A$(278+K9+J1,4),DOM=3210)IOL=0330
3225 PRINT @(46,16+V0+J0),C$(5,35),
3230 ON J0 GOTO 3231,3232,3233,3234,3235
3231 C1$=C$; GOTO 3236
3232 C2$=C$; GOTO 3236
3233 C3$=C$; GOTO 3236
3234 C4$=C$; GOTO 3236
3235 C5$=C$
3240 GOTO 2020
3250 REM "Salesperson #1 comm rate
3251 IF RESTRICT_SLSP OR POS("r"=X3$(154,6))>0 THEN GOTO 2020 ! WO 274987
3253 J0=(X0-19)/2
3255 IF P2$(8,1)="1" OR POS(" "<>A$(278+K9+J0*4,4))=0 OR P2$(8,1)<STR(J0+1:"0") THEN GOTO 2020
3260 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,72,16+V0+J0,0+J0,320,C0,M0$,"{3"+X$,"","AR2MAB48","","",""
3265 IF A[0+J0]>100 THEN GOTO 3250
3270 GOSUB 7650
3290 GOTO 2020
3300 REM "Price class
3310 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,21,18+V0,398,3,C0,"","{3"+X$,"C","AR2MAB50","FMPC","AR2MJA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3310,3311
3320 IF NUM(A$(398,3),ERR=3321)<100 THEN GOTO 2020
3325 IF A$(398+K9,3)<>S0$(1,3) THEN FIND (Z[9],KEY="C"+A$(398+K9,3),DOM=3300)IOL=0390; PRINT @(20,22),I$(5,30),
3340 GOTO 2020
3350 REM "T/M List type
3352 IF ABS(C0)<>2 THEN X0=X0+2; GOTO 2020
3355 X$="Enter Contact Management List Type" ! ssp#230604
3360 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,21,19,401,4,C0,"","{3"+X$,"","AR2MAB52","ZP0","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3360,3361
3390 GOTO 2020
3400 REM "T/M Contact code
3405 X$="Enter Contact Management Contact Code",Q$=A$(401,4) ! ssp#230604
3410 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,26,19,405,6,C0,"","{3"+X$,Q$,"AR2MAB54","ZP4","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3410,3411
3440 GOTO 2020
3450 REM "Alternate Customer Code
3452 IF P0$(107,1)<>"Y" THEN GOTO 2020 ELSE A6$=A$(411,20)+A$(1,10)
3460 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,21,20,411,20,C0,"","{3"+X$,"","AR2MAB56","","",""
3470 IF A6$<>A$(411,20)+A$(1,10) THEN CALL "AR2MAT",X3$,X4$,A6$,A$(411,20)+A$(1,10),"E",X$; IF X$>"" THEN X$=""; GOTO 3460
3490 GOTO 2020
3500 REM "Number of months for RON - rem " ssp# 166931
3510 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,21,21,434,2,C0,"","{L"+X$,"","AR2MAB02","","",""
3520 IF NUM(A$(434,2),ERR=3510)<0 THEN GOTO 3510
3540 GOTO 2020
3550 REM "United/SPR Account number if TOPS - ssp 176595
3551 IF RESULT$<>"Y" THEN GOTO 3590
3555 X$="Enter account for O/S Vendor"
3560 CALL "ZZENTR","SZXUX",A{ALL},A$,X4$,X3$,40,21+V0,436+K9,6,C0,"","{1"+X$,K9$,"OS2PAA00","OSA","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3560,3561
3565 PRINT @(40,21+V0),A$(436+K9,6),
3590 GOTO 2020
3600 ! CSR, for WebEC orders, WO184914
3605 X$="CSR to use for eCommerce orders"
3610 CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,52,21+V0,442,4,C0,"","{3"+X$,"","AR2MAB58","AR3","AR2MBB",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3610,3611
3615 IF C0<0 THEN GOTO 2020
3620 FIND (Z[3],KEY=A$(442,4),DOM=3610)CSR$
3640 GOTO 2020
3645 ! 
3650 ! Market Channel, WO182560
3660 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,69,21+V0,446,10,C0,"","{3"+X$,"","AR2MAB60","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3650,3651
3690 GOTO 2020
3695 ! *******************************************************
3700 ! Auto Reorder Notices
3705 X$="Y will ask for info during order entry"
3710 CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,75,12+V0,473,1,C0,"","{3"+X$,"","AR2MAB62","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3710,3711
3740 GOTO 2020
4000 REM "Credit Requestor
4005 REM "                            type  cust.code cust.name
4010 Q0$=""; Q0$="REQU"+A$(1,10)+A$(11,35)
4020 CALL "AR2CRS",X3$,X4$,Q0$,Q1$
4025 X0$="AR2MAB",X1$="Customer Constant Data"
4028 GOSUB 6400
4030 GOTO 1800
6000 REM "BACKGROUND
6010 CALL "AR2MAJ",X3$,X4$,"",A{ALL},0,"","",P2$,"","","","","",P0$,A5$
6190 RETURN 
6210 CALL "AR2MAJ",X3$,X4$,A$,A{ALL},1,B$,E$,P2$,C1$,C2$,C3$,C4$,C5$,P0$,A5$
6350 REM "Sales Tax Rate 3.0.6
6360 PRINT @(36,14),B[0]+B[2]+B[3]:"##.000%",@(44,14),B$(11,25),
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 CALL "ZZDISP","A",A$(1+K9,10),"A/R",X3$,"","",18,3+V0,X4$
6490 RETURN 
6500 REM "DELETE
6502 IF W9$>"" THEN IF W9$(7,1)="R" THEN CALL "ZZPROM",".4",X3$,0,"Remote T/M users cannot modify this information","","",0; GOTO 6590
6505 IF X3$(188,8)="DEFAULTS" THEN REMOVE (Z[13],KEY="DFLT"+X0$,DOM=9900); GOTO 9900
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6511 IF W9$<>"" THEN CUST$=A1$; CALL "RT2WOC",ERR=6512,X3$,X4$,CUST$,"AR1...","D",A1$
6515 CALL "ZZ2SKY",X3$,X4$,"AR1","D",A$,A1$,A2$,"","","","",A7$,"",""
6525 REMOVE (Z[8],KEY=A3$,DOM=6526)
6530 REMOVE (Z[10],KEY=A4$,DOM=6531)
6540 IF P0$(107,1)="Y" THEN CALL "AR2MAT",X3$,X4$,A$(411,20)+A$(1,10),"","D",""
6552 Q5$="Customer Sort     Commission PayableCommission BookingOrder booking     "
6555 A8$="Activity"; IF LEN(Q2$)>1 THEN A8$=Q5$((NUM(Q2$(2,1))-1)*18+1,18)
6560 A8$=A8$+" detected. - Record set to inactive for later removal.",A9$=".4"
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY
6702 IF W9$>"" AND Z9<>0 THEN IF W9$(7,1)="R" THEN CALL "ZZPROM",".4",X3$,0,"Remote T/M users cannot modify this information","","",0; GOTO 6790
6703 IF X3$(188,8)="DEFAULTS" THEN WRITE (Z[13],KEY="DFLT"+X0$)IOL=0310; GOTO 9900
6704 %IL_IMAGE_TYPE$="",%IL_IMAGE_KEY$=""
6705 IF Z9=0 THEN GOTO 6785
6707 IF X3$(330,1)="1" THEN A$(431,3)=X3$(9,3)
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6711 IF W9$<>"" THEN CUST$=A1$; CALL "RT2WOC",ERR=6712,X3$,X4$,CUST$,"AR1...","U",A1$
6715 CALL "ZZ2SKY",X3$,X4$,"AR1","A",A$,A1$,A2$,"","","","",A7$,"",""
6730 REMOVE (Z[8],KEY=A3$,DOM=6731)
6735 WRITE (Z[8],KEY=A$(278+K9,4)+A$(1+K9,10))
6740 REMOVE (Z[10],KEY=A4$,DOM=6741)
6745 IF POS(X3$(9,3)="060338363364365366476",3)<>0 THEN WRITE (Z[10],KEY=A$(205,12)+A$(1,10)) ELSE WRITE (Z[10],KEY=A$(205,10)+A$(1,10))
6750 REM "Telemarketing Interface
6755 IF POS(" "<>A$(401,10))=0 OR POS(P2$(67,1)="YAZ")=0 THEN GOTO 6775
6760 IF P2$(67,1)<>"A" THEN GOTO 6770
6765 CALL "ZZPROM",".Y",X3$,Z,"Update corresponding Contact Management Record?","","",0; ON Z GOTO 6766,6775 ! ssp#230604
6770 CALL "AR2MAS",X3$,X4$,A$,"SYNC"
6775 IF X3$(330,1)="1" THEN CALL "CM2AAA",X3$,Z[1],0,A1$,"",X9
6785 CALL "AR2UDH",X3$,X4$,A1$,""; IF CALLED$<>"Y" THEN GOSUB 6400; REM "WO119423, User Defined Fields
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 A$(1,LEN(A1$))=A1$
6825 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 1140
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
7300 REM 
7310 ON FPT(C0)*10 GOTO 7390,7325,7330,7390
7325 IF V8$="" THEN X0=X0+4; GOTO 7390 ELSE X0=NUM(V8$(POS(STR(X0:"00")<=V8$,2),2)); GOTO 7390
7330 IF V8$="" THEN X0=X0-6; GOTO 7335 ELSE X0=POS(STR(X0-1:"00")<=V8$,2)-2; IF X0<0 THEN X0=0; GOTO 7390 ELSE X0=NUM(V8$(X0,2)); GOTO 7390
7335 IF X0<0 THEN X0=0; GOTO 7390
7390 C0=3; RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts: 
7550 REM "Added stmts: 
7575 REM "Deleted stmts: 
7600 REM "Check for 100% addition
7601 REM "Assumes that no valid codes follow the 1st blank code
7605 IF POS(" "<>A$(278+K9,20))=0 THEN RETURN 
7607 T=0,T0=-1
7610 FOR I=0 TO NUM(P2$(8,1))-1
7615 IF A$(278+I*4,4)="    " THEN A[I]=0; GOTO 7640
7617 FIND (Z[3],KEY=A$(278+I*4,4),DOM=7618); GOTO 7620
7618 X0=17,C0=0,X$="Customer must have a valid salesperson code"; EXITTO 2040
7620 T0=I,T=T+A[I]
7640 NEXT I
7645 IF T0<>-1 THEN IF T<>100 THEN A[T0]=A[T0]+100-T; IF P2$(8,1)>"1" THEN PRINT @(72,T0+16+V0),A[T0]:M0$,
7650 REM "Display total %
7670 IF P2$(8,1)>"1" THEN IF POS(" "<>A$(282,16))<>0 THEN PRINT @(65,NUM(P2$(8,1))+16+V0),"Total: ",A[0]+A[1]+A[2]+A[3]+A[4]:M0$,
7690 RETURN 
7700 REM " move salesperson codes up
7710 IF POS(" "<>A$(274+S0*4+K9,4))<>0 THEN RETURN 
7715 DIM SLS_REP$[5]; SLS_REP$[1]=C1$,SLS_REP$[2]=C2$,SLS_REP$[3]=C3$,SLS_REP$[4]=C4$,SLS_REP$[5]=C5$ ! SSP#232935
7720 FOR I=S0-1 TO 3
7730 I0=K9+I*4,A$(278+I0,4)=A$(282+I0,4),A[I]=A[I+1]
7735 FIND (Z[3],KEY=A$(278+I0,4),DOM=*NEXT)IOL=0330; SLS_REP$[I+1]=C$ ! SSP#232935
7740 NEXT I
7741 A$(294,4)=DIM(4),A(4)=0,C5$="",C1$=SLS_REP$[1],C2$=SLS_REP$[2],C3$=SLS_REP$[3],C4$=SLS_REP$[4] ! SSP#232935
7742 CALL "AR2MAJ",X3$,X4$,A$,A{ALL},2,B$,E$,P2$,C1$,C2$,C3$,C4$,C5$,P0$,A5$
7745 RETURN 
7800 REM "Set & Display defaults
7810 IF A$(229+K9,2)="  " THEN A$(229+K9,6)=D0$; CALL "ZZDISP","D",A$(229+K9,6),"",X3$,"","",21,13+V0,X4$
7820 IF A$(267,1)=" " THEN A$(267,1)="Y"
7825 IF A$(268,1)=" " THEN A$(268,1)="Y"
7830 IF A$(245,1)=" " THEN A$(245,1)="N"
7840 RETURN 
7900 REM "Default Record                                                      
7910 FIND (Z[2],KEY=A$(235+K9,10),DOM=7911)IOL=0320
7915 FIND (Z[3],KEY=A$(278+K9,4),DOM=7916)IOL=0331
7920 FIND (Z[3],KEY=A$(282+K9,4),DOM=7921)IOL=0332
7925 FIND (Z[3],KEY=A$(286+K9,4),DOM=7926)IOL=0333
7930 FIND (Z[3],KEY=A$(290+K9,4),DOM=7931)IOL=0334
7935 FIND (Z[3],KEY=A$(294+K9,4),DOM=7936)IOL=0335
7990 RETURN 
8000 REM "Check for sales tax & category & salesperson
8005 IF X3$(188,8)="DEFAULTS" THEN RETURN 
8010 FIND (Z[5],KEY=A$(220+K9,9),DOM=8011); GOTO 8015
8011 X0=4,C0=0,X$="Customer must have a valid category code"; EXITTO 2040
8020 FIND (Z[2],KEY=A$(235+K9,10),DOM=8021); GOTO 8040
8030 X0=6,C0=0,X$="Customer must have a valid sales tax code"; EXITTO 2040
8040 IF A$(278,20)<>S0$(1,20) THEN GOTO 8070
8060 X0=17,C0=0,X$="Customer must have a valid salesperson code"; EXITTO 2040
8090 RETURN 
8950 REM "Program flow logic
8960 DIM U0$(6); U0$(1)=FID(0); U0$=U0$+"PF"
8970 CLOSE (14); OPEN (14)"ZZPARM"; FIND (14,KEY=U0$,DOM=8990)U0$,U1$
8980 Q1=NUM(U1$(7,2)),Q1$=U1$(9,Q1),U1$=U1$(Q1+9)
8981 IF LEN(Q1$)=11 THEN Q0$=Q1$(11,1),Q1$=Q1$(1,10)
8990 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9705 %IL_IMAGE_TYPE$="",%IL_IMAGE_KEY$=""
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9725 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9730 RUN U1$(1,6)
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9905 %IL_IMAGE_TYPE$="",%IL_IMAGE_KEY$=""
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$+O9$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "199160-Add sales tax codes to ZY2MBA Zip Code Table  
56001 REM "191992-Generate welcome email during order entry and flag items   
56002 REM "232935-AR customer setup screen does strange things when you try to
56004 REM "251220-ZY1 zip code lookups no longer retrieving the sales tax code
56005 REM "274987-Appl Security by Operator; Restrict display/access to sls   
56006 REM "291356-CSR field in AR1 has wrong message identifying field        
