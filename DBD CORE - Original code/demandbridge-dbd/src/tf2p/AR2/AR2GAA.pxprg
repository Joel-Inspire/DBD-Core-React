0010 REM "TESTING - A/R Invoice Data Entry <AR2EAA>
0020 SETESC 9300; SETERR 9000
0035 REM "5.2 - 03/02/04 - 10.671666 - lms - SSP# 167214
0040 REM "Copyright 2004 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0042 REM "KMc - 12/15/87
0080 REM "Try  the enter, if being called L$="Y". L0$ 1,1=control (0=initial call), 2,6=fy-a/p, 8,8=order#,I1$ returns the FY/AP and invoice # (the key)
0081 L$="N",L1=0; SETERR 0083
0082 ENTER X3$,X4$,L0$,I1$,L1; IF LEN(L0$)<15 THEN EXIT ELSE L$="Y"; GOTO 0100
0090 ON L0 GOTO 0100,6700,6500,6500,1000
0100 IF L$="Y" THEN SETERR 9000; SETESC 9300 ELSE BEGIN ; SETERR 9000; SETESC 9300
0110 X0$="AR2EAA",X1$="A/R Invoice Data Entry",K9$="",K9=0
0126 F1$="*"
0130 K0=14,K1=3
0135 C9=-1,V9=-2
0139 REM "Dup of line 140 is in AR2EAK"
0140 M0$="-#,###,###.00",M1$="-###,###.00",M3$="-###.00"
0145 IF L$<>"Y" THEN GOSUB 8950
0150 IF Q1$<>"" THEN F1$=""
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0190 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0200 REM "
0240 IF L$<>"Y" THEN CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30]
0320 IOLIST B$
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15]
0340 IOLIST D$
0350 IOLIST E$,E[0],E[1],E[2],E[3]
0370 IOLIST G$,G[0],G[1],G[2],G[3]
0380 IOLIST H$,H[0],H[1],H[2],H[3],H[4],H[5],H[6],H[7],H[8],H[9],H[10],H[11],H[12],H[13],H[14],H[15]
0390 IOLIST I$
0430 IOLIST M$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0509 REM "See opens in AR2EAH as well
0510 Z$="01X ARB...  02ORFMP...  03O AR1...  04O PO3...  05ORAR2...  06O ZZPARM  07O AR5...  08ORAR4...  09ORAR3...  10O FS1...  11ORARC...  12O FMN...  13O FM0...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0580 FIND (Z[6],KEY=X3$(9,3)+"G/L")G0$; G8=NUM(G0$(170,2),ERR=0581)
0585 DIM I0$(40); FIND (Z[6],KEY=X3$(9,3)+"I/C",DOM=0586)I0$
0600 REM "
0615 GOSUB 7800
0700 REM "Get date"
0720 IF POS(" "<>X3$(15,6))<>0 THEN D0$=X3$(15,6)
0725 READ (Z[6],KEY=X3$(9,3)+"A/R",DOM=9800)P0$
0730 READ (Z[6],KEY=X3$(9,3)+"AR1",DOM=9800)P2$
0731 D8$=D0$; IF POS(" "<>D7$)=0 THEN D7$=D0$
0732 IF L$="Y" THEN IF POS(" "<>D8$)=0 THEN D8$=D7$
0740 O9$=""; FIND (Z[6],KEY=X3$(9,3)+"F/M",DOM=0741); O9$="*"
0800 REM "IF Q1$<>"" extract key & goto AR2EAL
0805 IF L$="Y" OR Q1$="" THEN GOTO 0820
0807 IF Q1$(1,2)="1S" THEN GOTO 0850
0808 IF LEN(Q1$)>14 THEN F5$=Q1$(15),Q1$=Q1$(1,14)
0810 DIM A[20]; EXTRACT (Z[1],KEY=Q1$,DOM=0811)IOL=0310; A1$=Q1$
0815 WRITE (Z[6],KEY=U0$)U0$,U1$
0816 IF F5$="*" THEN A$(1,LEN(A1$))=A1$; GOTO 0850
0818 RUN "AR2EAL"
0850 GOSUB 6000
0900 GOSUB 7400
1000 REM "
1005 A0=0
1010 IF L$<>"Y" THEN CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(20),C$(391),C[15],E$(54),E[3],G$(37),G[3],H$(31),H[15],M$(250)
1032 T0=0,V0=0,V8$="000511232599"
1035 DIM A$(393),A[20],T[9]
1045 A$(1,6)=K9$+A1$,F9$="",A5$=""
1047 IF Q1$(1,2)="1S" THEN A1$=Q1$(3,14),F9$="*"; REMOVE (Z[1],KEY=A1$,DOM=1048)
1050 IF L$="Y" THEN GOTO 1100 ELSE IF L0<3 THEN GOSUB 6000; GOSUB 6450; GOTO 1071
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1250,1300,1500
1200 REM "Fiscal Year Ending
1205 IF LEN(A1$)>0 THEN A$(1,4)=A1$(1,4); GOTO 1212
1209 J$=X3$(9,3)+"G/LYE"
1210 CALL "ZZENTR","ZXUX",A{ALL},A$,X4$,X3$,16,3,1,4,C0,"","{1"+X$,J$,"AR2EAA00","GLYE","GL2MQA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1211 F1$=""
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1216,6800,9700,6970
1220 FIND (Z[6],KEY=X3$(9,3)+"G/LYE"+A$(1,4),DOM=1210)P1$
1223 IF POS(P1$(99,1)="CR")<>0 THEN A1$=""; CALL "ZZPROM",".4",X3$,Z,"Closed Years must be reopened by System Administrator prior to posting.","","",0; GOTO 1210
1240 GOTO 1120
1250 REM "Accounting Period
1254 IF ABS(C0)=2 THEN A1$=""; GOTO 1120
1255 IF LEN(A1$)>4 THEN A$(5,2)=A1$(5,2); GOTO 1262
1260 CALL "ZZENTR","ZUX",A{ALL},A$,X4$,X3$,44,3,5,2,C0,"","{2"+X$,"","GL2JAA02","","",""
1262 IF ABS(C0)=2 AND LEN(A1$)>0 THEN A1$=A1$(1,0)
1265 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1266,6800,9700,6970
1267 IF A$(5,2)<"01" OR A$(5,2)>P1$(13,2) THEN IF L$="Y" THEN L1=2; GOTO 9900 ELSE GOTO 1260; REM "IF L$="Y" then Error 2 - Accounting Period not valid
1270 IF L$<>"Y" THEN CALL "AR2EAK",X3$,X4$,A$,A{ALL},3,C$,E$,G$,G{ALL},P1$,P2$
1290 GOTO 1120
1300 REM "Invoice number
1302 IF L$="Y" THEN A$(7,7)="A"; GOTO 1318 ELSE IF LEN(A1$)>6 THEN A$(7,8)=A1$(7,8); GOTO 1311
1304 IF F1$="*" THEN F1$="",A1$="",X0=0; GOTO 1140
1305 X$="Enter "+QUO+"A"+QUO+" to automatically assign the next invoice number"
1309 J1$=A$(1,6)
1310 CALL "ZZENTR","SUX",A{ALL},A$,X4$,X3$,16,4,7,8,C0,"","{2"+X$,J1$,"AR2EAA04","ARB","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1310,1311
1311 IF Q2$<>"" AND ABS(C0)=4 THEN GOTO 8930
1312 IF ABS(C0)=2 AND LEN(A1$)>4 THEN A1$=A1$(1,4)
1315 IF C0=-1 THEN GOTO 1310 ELSE IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1120,6800,9800,6970
1318 IF A$(7,7)="A      " THEN GOSUB 3000
1320 A=NUM(A$(7,7),ERR=1310),A$(7,8)=STR(A:"0000000")+A$(14,1)
1330 IF L$<>"Y" THEN CALL "ZZDISP","A",A$(7,8),"AR6",X3$,"","",16,4,X4$
1340 GOTO 1120
1500 REM "
1505 Z9=-1,A[20]=0,Z7=0
1510 A1$=K9$+A$(LEN(K9$)+1,14),A$(1,LEN(K9$))=K9$
1515 I1$=A1$; REM "Save off key to pass back if we were called
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1523 IF L$="Y" THEN L1=3; GOTO 9900; REM "Error 3 - Invoice should not be on file!
1525 T0=A[1],A0=A[0]
1530 FIND (Z[3],KEY=A$(15,10),DOM=1531)IOL=0330
1540 FIND (Z[5],KEY=A$(65,2),DOM=1541)IOL=0350
1550 FIND (Z[8],KEY=A$(128,5),DOM=1551)IOL=0380
1555 FIND (Z[9],KEY=A$(133,4),DOM=1556)IOL=0390
1560 FIND (Z[2],KEY="S"+A$(186,1),DOM=1561)IOL=0320
1565 FIND (Z[13],KEY="C"+A$(15,10)+A$(187,4),DOM=1566)IOL=0430
1570 FIND (Z[7],KEY=A$(67,10),DOM=1571)IOL=0370
1575 O9$=""; FIND (Z[10],KEY=A$(92,8),DOM=1576); O9$="*"
1580 Z9=1,Z7=1
1590 GOTO 1700
1600 REM "
1610 IF L$="Y" OR F9$="*" THEN GOTO 1650
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630,6975
1630 A1$=A1$(1,6); GOTO 1000
1655 A$(338,4)=X3$(9,3)+X3$(330,1)
1660 IF V9>=0 THEN GOSUB 6200
1690 GOTO 2000
1700 REM "
2000 REM "Run Entry overlay
2010 C0=0,X0=-1,X1=1,J9=0
2020 L0=4
2030 IF Z9<>1 THEN WRITE (Z[1],KEY=A1$)IOL=0310
2040 EXTRACT (Z[1],KEY=A1$)IOL=0310
2045 A$(92,8)=L0$(8,8); IF L$="Y" THEN A$(86,6)=D8$,A$(180,6)=D7$; CALL "AR2GAB",X3$,X4$,A1$,A$,A{ALL},L1,Z{ALL},F5$,D8$,D7$,D0$; CALL "AR2GAD",X3$,X4$,A1$,A$,A{ALL},L1,F5$; GOTO 9900
2090 RUN "AR2EAB"
3000 REM "Next invoice number
3010 EXTRACT RECORD (Z[6],KEY=X3$(9,3)+"A/R",DOM=9800)P9$
3020 A$(7,7)=P9$(89,7)
3025 P0=NUM(P9$(89,7)),P0=P0+1; IF P0>9999999 THEN P0=1
3026 P9$(89,7)=STR(P0:"0000000"); WRITE RECORD (Z[6],KEY=X3$(9,3)+"A/R")P9$
3027 P9$=P9$(1,POS(SEP=P9$+SEP)-1)
3030 FIND (Z[1],KEY=A$(1,14),DOM=3031); GOTO 3010
3035 F9$="*"
3040 RETURN 
5900 REM "REMOVE LINES
5905 A0=A[20],Q9$=""; DIM Y[4]
5913 REM "Also close ARG on 12 and open FMN, Just in case from AR2EAD overlay
5914 Z$="12C ARG...  13C FM0...  13O ART...  11C ARC...  11O ARD...  "
5915 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 5916,9900
5916 F=Z[13]; IF A0=0 THEN GOTO 5955
5920 EXTRACT (F,IND=0,ERR=5960)IOL=0290
5925 IF Y[4]=-2 THEN READ (F,IND=0); WAIT 0; GOTO 5920 ELSE Y[4]=-2; WRITE (F,IND=0)IOL=0290
5930 A1=Y[1],Y[1]=A0,Y[0]=Y[0]-1
5935 READ (F,IND=A0)A; IF A>0 THEN Y[0]=Y[0]-1,A0=A; GOTO 5935
5940 WRITE (F,IND=A0)A1
5950 Y[4]=-1; WRITE (F,IND=0)IOL=0290
5955 IF Q9$="" AND A[0]>0 THEN Q9$="*",A0=A[0],F=Z[11]; GOTO 5920 ELSE GOTO 5970
5960 IF ERR=0 THEN RETRY ELSE GOTO 9000
5970 Z$="13C ART...  13O FM0...  11C ARD...  11O ARC...  12O FMN...  "
5975 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 5976,9900
5995 RETURN 
6000 REM "BACKGROUND
6002 IF L$="Y" THEN RETURN 
6005 CALL "AR2EAK",X3$,X4$,A$,A{ALL},0,"","","",G{ALL},P1$,P2$
6190 RETURN 
6200 REM "DISPLAY DATA
6205 IF L$="Y" THEN RETURN 
6210 CALL "AR2EAK",X3$,X4$,A$,A{ALL},1,C$,E$,G$,G{ALL},P1$,P2$
6290 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF L$="Y" THEN RETURN 
6470 CALL "AR2EAK",X3$,X4$,A$,A{ALL},2,C$,E$,G$,G{ALL},P1$,P2$
6490 RETURN 
6500 REM "DELETE
6505 REM "Gosub 5900 to delete lines AND to reset Z(12) if from AR2EAD
6506 GOSUB 5900
6507 IF O9$="*" AND POS(" "<>A$(92,8))<>0 THEN EXTRACT RECORD (Z[10],KEY=A$(92,8))O$; O$(63,1)=""; WRITE RECORD (Z[10])O$; CALL "AR2EAQ",X3$,A$
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6520 REMOVE (Z[11],KEY=A1$,DOM=6521)
6530 IF L0=2 THEN PRINT @(0,3),'CE',; GOSUB 6000
6580 A1$=A1$(1,6)
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY
6705 A[0]=A0
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6720 D8$=A$(86,6),D7$=A$(180,6)
6730 GOSUB 8950
6740 A1$=A1$(1,6)
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 IF A1$="" THEN GOTO 6800 ELSE IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6825 A$(1,LEN(A1$))=A1$
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM "PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
7400 REM "Audit already run
7410 FIND (Z[1],KEY="",DOM=7490)V0$,K0$
7420 IF K0$>"" THEN CALL "ZZPROM","X1AUDITS",X3$,S3,"","","",0; ON S3 GOTO 9900,7490
7430 EXTRACT (Z[1],KEY="",ERR=9800)
7440 Q$="+("+V0$(22,4)+"."+V0$(26,2)+")"
7445 CALL "ZZPROM","X2AUDITS",X3$,S3,Q$,"","",0; ON S3 GOTO 9900,7446
7450 REMOVE (Z[1],KEY="")
7490 RETURN 
7810 CALL "ZZDISP","AX","000000000000","G/L",X3$,W2$,"",0,0,X4$
7815 M2=0; FOR X=1 TO LEN(W2$); M2=M2+POS("0"=W2$(X,1)); NEXT X
7850 REM "Set Fiscal year & period"
7860 FIND (Z[6],DOM=8900,KEY=X3$(9,3)+"A/R")P0$
7865 IF L$="Y" THEN A1$=L0$(2,6) ELSE IF A1$="" AND Q1$<>"" THEN A1$=Q1$(1,6)
7870 IF LEN(A1$)=6 THEN FIND (Z[6],DOM=7875,KEY=X3$(9,3)+"G/LYE"+A1$(1,4))P1$; GOTO 7881
7875 FIND (Z[6],DOM=8900,KEY=X3$(9,3)+"G/LYE"+P0$(7,4))P1$
7880 A1$=P0$(7,6)
7890 RETURN 
8900 REM "
8905 IF L$="Y" THEN L1=1; GOTO 9900; REM "Error 1 - Fiscal Year not found
8910 CALL "ZZPROM","4",X3$,S3,"Check the parameters for this company.","","",0; GOTO 9900
8930 REM "Verify skip of A/P entry from P/O receipt
8940 CALL "ZZPROM",".Y",X3$,S3,"|Verify that you wish to bypass entry of this A/R invoice","","",0
8945 ON S3 GOTO 9700,1310
8950 REM "Program flow logic
8960 DIM U0$(6); U0$(1)=FID(0),U0$=U0$+"PF"; CLOSE (14); OPEN (14)"ZZPARM"
8970 FIND (14,KEY=U0$,DOM=8990)U0$,U1$
8975 IF U1$="" THEN RETURN 
8977 IF U1$(1,6)<>"AR2EAA" THEN DIM Z[6]; Z[6]=14; GOTO 9700
8980 Q1=NUM(U1$(7,2)),Q1$=U1$(9,Q1),U1$=U1$(Q1+9)
8985 IF LEN(Q1$)=41 THEN Q2$=Q1$(7,8),A1$=Q1$(1,6),D0$=Q1$(15,6),F5$=Q1$(21,11),I5$=Q1$(32,10),Q1$="",F1$=""; WRITE (14,KEY=U0$)U0$,U1$
8987 IF U1$="" THEN REMOVE (14,KEY=U0$,DOM=8990) ELSE WRITE (14,KEY=U0$)U0$,U1$
8990 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9710 IF U1$="" THEN REMOVE (Z[6],KEY=U0$,DOM=9900); GOTO 9900
9715 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9720 WRITE (Z[6],KEY=U0$)U0$,U1$
9725 IF U1$(1,6)="PO2MAG" THEN PRINT @(0,3),'CE',
9730 RUN U1$(1,6)
9800 REM "EXIT PROGRAM
9805 GOTO 9700
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
