0010 REM "Item by Customer Report <AR2SIA>
0025 REM "(C) Copyright 1986 - All Rights Reserved
0035 REM "4.4 - 05/09/00 - 12.9 - tma - SSP# 126291
0040 REM "Copyright 2000 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="AR2SIA",X1$="Product by Customer Sales Analysis"
0120 DIM A$(17),A[14],B$(52)
0140 Z0$="##,###,###.00-",Z1$="##,###,##0-"
0160 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
0165 DEF FND$(Z9$)=Z9$(3,2)+"/"+Z9$(5,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0210 T=1,T0=0,T1=8
0220 W3=129
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "I/O lists
0310 IOLIST A$
0330 IOLIST AR1$
0340 IOLIST IC0$
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$,K0
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O AST... 02OSAST... 03O AR1...  04O IC0... 13O ZZPARM "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z9,Z1
0550 READ (Z[13],KEY="STAT"+"AST")AST$
0700 REM "Open Printer
0720 REM 
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[4]; U=35,U0=4,T0=2,T1=5,U[0]=0,U[1]=10,U[2]=1,U[3]=19,U[4]=4,V1=10,V2=1,V3=0,V4=0,U0$="Y   ",T5$="AA/RS   ",U5$="Customer",U6$="Department"; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=21
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM " Position read"
0905 REM "set key file to read from"
0910 U1=Z[1]
0915 REM "Create range (V2$) and start (V4$) from v2$ & V0$ -> range needs to include FY from V0$, etc
0917 V2_HOLD$=V2$; V2$=V2_HOLD$(1,20)+" "+$7E$+DIM(19)+DIM(19,$7E$)+V0$(72,4)+V0$(72,4),V4$=V2_HOLD$(1,10)+DIM(20)+V0$(72,4)
0920 GOTO 0990
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(U1,END=5000); READ (U1)IOL=0310
1007 IF POS(A$(35,1)="US")=0 THEN GOTO 1000
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])>=V2$(U3,U[U9]) THEN GOTO 1030 ELSE EXTRACT (U1,KEY=U$(1,U2-1)+V2$(U3,U[U9]),DOM=1021)
1025 EXITTO 1000
1030 IF U$(U2,U[U9])<=V2$(U3+U[U9],U[U9]) THEN GOTO 1040 ELSE READ (U1,KEY=U$(1,U2-1)+$FF$,DOM=1032)
1035 EXITTO 1000
1040 NEXT U9
1100 REM "Base year amounts into BASE_U and BASE_S
1110 U9$=U$
1120 STAT_KEY$=U$(1,34)
1125 GOSUB 7700
1130 BASE_U=UIN,BASE_S=SIN
1150 REM "Prior year
1155 STAT_KEY$=U$(1,30)+STR(NUM(U$(31,4))-1:"0000")
1160 GOSUB 7700
1165 PRIOR_U=UIN,PRIOR_S=SIN
1190 REM "Skip other stats for this customer/item
1195 READ (Z[1],KEY=U9$(1,34)+$FF$,DOM=1196)
1300 REM "Read other data
1310 DIM IC0$(188); READ (Z[4],KEY=A$(11,20),DOM=1311)IOL=0340
1400 REM "Subtotals"
1415 GOSUB 3100
1490 T=0
1500 REM "Print Data
1510 IF W+2>W0 THEN GOSUB 6000
1520 Y5$(1)=A$(21,10),Y5$(13)=IC0$(21,40)
1525 Y5$(53)=STR(BASE_U:Z1$),Y5$(66)=STR(BASE_S:Z0$)
1530 Y5$(82)=STR(PRIOR_U:Z1$),Y5$(95)=STR(PRIOR_S:Z0$)
1540 Y5$(111)=STR(BASE_S-PRIOR_S:Z0$)
1595 GOSUB 7300
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+BASE_U,T[T0,2]=T[T0,2]+BASE_S,T[T0,3]=T[T0,3]+PRIOR_U,T[T0,4]=T[T0,4]+PRIOR_S,T[T0,5]=T[T0,5]+BASE_S-PRIOR_S
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 TMP$=T6$; IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",TMP$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; GOSUB 7000
3155 IF U0$(1,1)="Y" AND T0$<>"END" THEN U1$=U9$(1,V1); DO_CUST=1; GOSUB 6000
3160 U1$=U9$(1,V1)
3180 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$,T6$=U2$; GOSUB 3050; GOSUB 7000
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3260 U2$=U9$(V5,V2)
3280 RETURN 
3300 REM "Next to lowest order break"
3380 RETURN 
3400 REM "lowest order break"
3480 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; GOSUB 7000
5200 REM "
5290 GOTO 9900
6000 REM "Page header
6020 IF W=999 THEN DO_CUST=1
6035 IF T0$="END" THEN W0$=""
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6060 ELSE ON W5 GOTO 6041,9910
6045 ON W5 GOTO 6046,9900
6050 IF DO_CUST THEN GOSUB 6200; DO_CUST=0
6060 GOSUB 6100
6090 RETURN 
6100 REM "
6105 Y5$(51)="Current Year",Y5$(67)="Current Year",Y5$(83)="Last Year",Y5$(99)="Last Year",Y5$(112)="Increase/Dec"
6120 GOSUB 7300
6125 Y5$(53)="Unit Sales",Y5$(67)="Dollar Sales",Y5$(82)="Unit Sales",Y5$(96)="Dollar Sales",Y5$(112)="Dollar Sales"
6130 GOSUB 7300
6140 GOSUB 7300
6199 RETURN 
6200 REM "Read in customer & print info
6205 DIM AR1$(600)
6210 FIND (Z[3],KEY=A$(1,10),DOM=6211)IOL=0330
6215 Y5$(1)="Customer",Y5$(22)="Customer",Y5$(53)="Customer"
6220 GOSUB 7300
6225 Y5$(1)="Number",Y5$(22)="Name & Address",Y5$(53)="Phone No."
6230 GOSUB 7300; GOSUB 7300
6235 CALL "ZZDISP","AXS",A$(1,10),"A/R",X3$,Y5$,"",1,0,X4$
6237 Y5$(22)=AR1$(11,35)
6239 CALL "ZZDISP","PXS",AR1$(205,14),"",X3$,Y5$,"",53,0,X4$
6240 GOSUB 7300
6245 CALL "ZZDISP","ZX",AR1$(134,9),"",X3$,TMP$,"",0,0,X4$; CSZ$=STP(AR1$(116,16),1)+", "+AR1$(132,2)+"  "+TMP$
6250 IF STP(AR1$(86,30),3," ")="" THEN L1$=CSZ$,L2$="" ELSE L1$=AR1$(86,30),L2$=CSZ$
6255 Y5$(22)=L1$; GOSUB 7300; IF L2$<>"" THEN Y5$(22)=L2$; GOSUB 7300
6295 RETURN 
7000 REM "Totals logic
7001 IF T=T0 THEN IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(53)=T1$(1,12),Y5$(66)=T1$(1,15),Y5$(82)=T1$(1,12),Y5$(95)=T1$(1,15),Y5$(111)=T1$(1,15); GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" Total ("+STR(T[T,0]:"0")+"):"
7020 Y5$(53)=STR(T[T,1]:Z1$),Y5$(66)=STR(T[T,2]:Z0$),Y5$(82)=STR(T[T,3]:Z1$),Y5$(95)=STR(T[T,4]:Z0$),Y5$(111)=STR(T[T,5]:Z0$); GOSUB 7300
7030 SETERR 9000
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed: "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 IF INT(T2)>0 THEN T4$=STR(INT(T2))+" Hours " ELSE T4$=""
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+STR(T3:"##.##")+" Minutes"
7260 Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7330 REM 
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7455 DIM P1$(105); FIND (Z[13],KEY=X3$(9,3)+"G/LYE"+V0$(72,4),DOM=7740)P1$
7460 D1$=P1$(NUM(V0$(76,2))*6+15,6),D0$=D1$
7465 W0$="For fiscal year "+V0$(72,4)+", period "+V0$(76,2)+" ending "+FND$(D0$)
7480 X3$(178,7)=Y4$,W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1); V0=NUM(V0$(71,1),ERR=7481)
7485 K0=NUM(V0$(81,2)),K0$=STR(NUM(V0$(74,4))-1:"00"),P0=NUM(V0$(78,2))
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
7700 REM "GET DATA
7710 DIM A[14]
7720 CALL "ZZPACK",X3$,"R","","",0,0,A{ALL},Z[2],"U",STAT_KEY$,AST$
7725 UIN=0; FOR X=0 TO NUM(V0$(76,2)); UIN=UIN+A[X]; NEXT X
7730 DIM A[14]
7735 CALL "ZZPACK",X3$,"R","","",0,0,A{ALL},Z[2],"S",STAT_KEY$,AST$
7740 SIN=0; FOR X=0 TO NUM(V0$(76,2)); SIN=SIN+A[X]; NEXT X
7790 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 RUN "ZMENU"
9999 END 
