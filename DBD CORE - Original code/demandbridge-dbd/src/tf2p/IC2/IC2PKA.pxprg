0010 REM "Physical Count Variance Analysis <IC2PKA>
0035 REM "5.7 - 08/18/14 - 15.228333 - tma - SSP# 272072
0037 REM "272072-Physical Count Variance report ranges do not work correctly 
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="IC2PKA",X1$="Physical Count Variance Analysis"
0120 DIM A$(54),A0[12],B$(214),C$(256),C[19],D$(45),D[9],E$(21),E[1],M$(20)
0123 DIM J[5]
0125 DIM S$(20)
0130 DIM U[10]
0140 Z0$="-##,###,###.00",Z1$="-###,###.00",Z2$="-##,##0",Z3$=Z1$(2)+"#"
0170 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0175 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
0210 T=1,T0=0,T1=3
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR IC3 - Physical Count Data Entry
0310 IOLIST A0$,A0[0],A0[1],A0[2],A0[3],A0[4],A0[5],A0[6],A0[7],A0[8],A0[9],A0[10],A0[11],A0[12]
0315 REM "IOLIST FOR FM0 - Warehouse Locations
0320 IOLIST B$
0325 REM "IOLIST FOR IC0 - Item Masterfile
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19]
0335 REM "IOLIST FOR IC1 - Inventory Detail Location
0340 IOLIST D$,D[0],D[1],D[2],D[3],D[4],D[5],D[6],D[7],D[8],D[9]
0350 IOLIST E$,E[0],E[1]
0360 IOLIST F$
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0502 F0$="PI0"+X3$(1,3); CALL "ZZTFLE",X3$,F0$,"ICL...",500,45,43
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O ICL...  02O FM0...  03O IC0...  04O IC1...  06O FMP...  07O AR1...  13O ZZPARM  "
0515 Y$=Y$+"05O PI0"+X3$(1,3)+"  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0550 REM "Write out fy record for Phys inv
0560 WRITE (Z[13],KEY=X3$(9,3)+"PII/C")X3$(9,3)+"PII/C"+V0$(72,6)
0605 N0$="IC2PKA"; GOSUB 6900
0700 REM "Open Printer
0710 GOSUB 8000
0720 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0721,9920
0725 IF %HAS_IMAGE_LIB THEN CALL "ILGAUD",ERR=*NEXT,X3$,X4$,"RA" ! 174140
0730 GOSUB 8500
0750 REM "Check for restart record"
0755 READ (Z[1],KEY="",DOM=0780)
0760 IF K0$>"" THEN X3$(184,1)="1"
0765 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W4,W4$,W5$
0770 CALL "ZZREST",X3$,Z[13],Z[1],W9,W3,W
0775 GOSUB 6000
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840,0842
0820 DIM U[3]; U=24,U0=3,U[0]=0,U[1]=10,U[2]=10,U[3]=4,T0=1,T1=3,V1=20,U0$="    ",T5$="AI/C            ",U5$="Item code",V2$=V2$(1,10)+V2$(21,10)+V2$(11,10)+V2$(31); GOTO 0850 ! SSP#272072
0830 DIM U[3]; U=23,U0=3,U[0]=0,U[1]=3,U[2]=10,U[3]=10,T0=2,T1=3,V1=3,V2=20,U0$="    ",T5$="S   AI/C        ",U5$="Product code",U6$="Item code",S9$="X",V2$=V2$(1,6)+V2$(7,10)+V2$(27,10)+V2$(17,10)+V2$(37); GOSUB 6900; GOTO 0850 ! SSP#272072
0840 DIM U[4]; U=27,U0=4,U[0]=0,U[1]=4,U[2]=3,U[3]=10,U[4]=10,V1=4,V2=3,V3=20,T0=3,U0$="Y   ",U5$="Location",U6$="Product code",T5$="S   S           "; GOTO 0850
0842 DIM U[4]; U=34,U0=4,U[0]=0,U[1]=10,U[2]=10,U[3]=10,U[4]=4,V1=10,V2=20,V3=4,T0=3,U0$="    ",U5$="Bin",U6$="Customer Code",T5$="S   S           "; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0900 REM " Position read"
0905 REM "set key file to read from"
0910 U1=Z[5]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]; IF V0=3 AND U9=3 THEN U2=U2+10
1020 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN READ (U1,END=5000); EXITTO 1000
1030 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U0=1 THEN EXITTO 5000 ELSE READ (U1,END=5000); EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1150 F0$=""; DIM J[3]
1153 READ (U1)U$
1155 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1156 IF V0$(79,1)<>"Y" AND A0$(222,1)<>"Y" THEN GOTO 1000
1157 READ (Z[3],KEY=A0$(1,20),DOM=1158)IOL=0330
1160 GOSUB 4500; REM "Also figures unit cost C for lotted items
1162 IF V0$(81,1)="Y" AND A[0]=A[2] THEN GOTO 1000; REM " SSP# 160115
1165 READ (Z[4],KEY=A$(15,20)+A$(1,4),DOM=1166)IOL=0340
1170 A0=A[2]-A[0]; IF A$(54,1)="Y" THEN A1=C ELSE A1=A[4]
1173 REM "IF A[0]<0 AND A[2]=0 THEN LET A0=0
1175 IF A$(54,1)="Y" THEN GOSUB 4650 ELSE GOSUB 4600
1190 J[0]=A[0],J[1]=A[2],J[2]=A0,J[3]=A2,C9=A1
1200 REM "Adjust totaled quantities for not counted case
1205 IF A0$(222,1)<>"Y" THEN A3=0,J[3]=0,J[2]=0
1300 REM "Read other data
1305 H8$=C$
1310 DIM J1$(46); READ (Z[7],KEY=A$(15,10),DOM=1311)J1$
1400 REM "Subtotals"
1415 GOSUB 3100
1420 ON T GOTO 1470,1430,1440,1450,1460
1440 IF V0<>2 THEN GOSUB 6300
1490 T=0
1500 REM "Print Data
1510 IF W+4>W0 THEN GOSUB 6000
1520 IF POS(" "=J1$(1,10))<>0 THEN GOTO 1550
1530 CALL "ZZDISP","AX",J1$(1,10),"A/R",X3$,X$,"",0,0,X4$; Y5$(5)=X$,Y5$(18)=J1$(11,35)
1540 GOSUB 7300
1550 CALL "ZZDISP","AX",U$(15,20),"I/C",X3$,X$,"",0,0,X4$; Y5$(1)=X$
1553 GOSUB 7700
1556 Y5$(12)=H9$
1558 IF A0$(222,1)<>"Y" THEN Y5$(55)="            " ELSE Y5$(53)=FNQ$(J[1],Z0$)
1560 Y5$(67)=FNQ$(J[0],Z0$)
1564 IF A0$(222,1)<>"Y" THEN Y5$(84)="Not Counted" ELSE Y5$(81)=FNQ$(J[2],Z0$)
1567 Y5$(95)=STR(C9:Z3$),Y5$(108)=FNS$(C$(124,4))+"/"+FNR$(STR(C[15]:X2$))
1568 IF A0$(222,1)<>"Y" THEN Y5$(120)="Not Counted" ELSE Y5$(120)=FNQ$(J[3],Z1$)
1570 GOSUB 7300
1600 IF V0$(80,1)<>"Y" THEN GOTO 1900; REM "107966, addt'l detail line
1610 IF A0$(100,10)<>"          " THEN Y5$(2)="Bin: "+A0$(100,10); REM "Bin number
1615 IF A0$(25,6)<>"      " THEN Y5$(20)="Rec: "+FND$(A0$(25,6)); REM "Receipt date
1620 IF A0$(31,2)<>"  " THEN Y5$(38)="Seq: "+A0$(31,2); REM "Sequence no
1630 Y5$(53)=STR(A3:Z0$),Y5$(67)=STR(A4:Z0$); REM "Physical and computer cost extension
1690 GOSUB 7300
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+J[3]
1915 T[T0,2]=T[T0,2]+A3
1920 T[T0,3]=T[T0,3]+A4
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,X$,"",0,0,X4$; T6$=X$ ! SSP#251727
3075 IF V0=4 AND T=2 AND LEN(T6$)>=10 THEN CALL "ZZDISP","AX",T6$(1,10),"A/R",X3$,X$,"",0,0,X4$; T6$=X$+" "+T6$(11) ! SSP#251727
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; GOSUB 7000
3154 U1$=U9$(1,V1); IF V0=3 THEN GOSUB 6200
3155 IF V0$(82,1)="Y" AND V0=4 AND BIN$>"" AND BIN$<>U9$(1,10) THEN GOSUB 6000; GOTO 3160
3156 IF U0$(1,1)="Y" AND T0$<>"END" THEN GOSUB 6000
3160 U1$=U9$(1,V1); IF V0=2 THEN GOSUB 6300 ELSE IF V0=3 THEN GOSUB 6200
3170 IF V0$(82,1)="Y" AND V0=4 THEN BIN$=U9$(1,10) ELSE BIN$=""
3180 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$,T6$=U2$; GOSUB 3050; GOSUB 7000
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3260 U2$=U9$(V5,V2)
3280 RETURN 
3300 REM "Next to lowest order break"
3310 IF V3=0 THEN RETURN ELSE IF U3$=U9$(V6,V3) THEN GOSUB 3400; RETURN 
3320 GOSUB 3420
3330 IF U3$="" THEN GOTO 3360
3350 T=3,T$=U7$,T6$=U3$; GOSUB 3050; GOSUB 7000
3355 IF U0$(3,1)="Y" THEN GOSUB 6000
3360 U3$=U9$(V6,V3)
3380 RETURN 
3400 REM "lowest order break"
3410 IF V4=0 THEN RETURN ELSE IF U4$=U9$(V7,V4) THEN RETURN 
3430 IF U4$="" THEN GOTO 3460
3450 T=4,T$=U8$,T6$=U4$; GOSUB 3050; GOSUB 7000
3455 IF U0$(4,1)="Y" THEN GOSUB 6000
3460 U4$=U9$(V7,V4)
3480 RETURN 
4500 REM "Convert A0$, and A0() into IC3 format record in A$ and A()
4501 REM "A$(54,1)='Y' will mean that this was a lotted itme
4502 REM "For lotted items, A(4) average cost will be Total cost instead
4505 DIM A$(54),A[4]
4510 A$(1)=A0$(21,4)+A0$(100,10)+A0$(1,20)+A0$(121,4)+A0$(223,4)
4511 A$(44,10)=A0$(247,10)
4515 A$(54,1)=A0$(221,1)
4519 REM "Handle non lots here, then goto 4550
4520 IF A0$(221,1)<>"Y" THEN A[0]=A0[4],A[1]=A0[2],A[2]=A0[8]+A0[10]+A0[12],A[3]=A0[2],A[4]=A0[0]; GOTO 4550
4524 REM "Handle lotted quantities here
4529 REM "Set lotted flag and put qties in buckets, remember A(4) is TOTAL cost, NOT average cost
4530 IF A0$(125,4)="    " THEN A[0]=A0[4]*A0[3],A[2]=A0[8]*A0[3],A[1]=A0[3],A[3]=A0[3] ELSE A[0]=A0[4]*A0[2]*A0[3],A[2]=A0[8]*A0[2]*A0[3],A[1]=A0[2]*A0[3],A[3]=A0[2]*A0[3]
4532 IF A[0]=0 THEN Q0=A[2] ELSE Q0=A[0]; REM "Use phys qty if computer qty is zero
4533 IF C[15]=0 THEN C[15]=1
4534 REM "Figure unit cost C for use in calculations
4535 CALL "FM2EXT",Z[13],1,C$(124,4),C[15],Q0,C,A0[0],2
4540 A[4]=A0[0]
4595 RETURN 
4600 REM "Figure variance dollars for non-lotted items
4605 A2=A0*A1
4610 IF C$(124,4)<>M$(4,4) THEN DIM M$(20),M[1]; READ (Z[13],KEY="U/M"+C$(124,4))M$,M[0],M[1]
4615 IF M$(20,1)="Y" AND M[0]<>0 THEN A2=A2/M[0] ELSE IF C[15]<>0 THEN A2=A2/C[15]
4620 A3=A[2]*A1
4625 IF M$(20,1)="Y" AND M[0]<>0 THEN A3=A3/M[0] ELSE IF C[15]<>0 THEN A3=A3/C[15]
4630 A4=A[0]*A1; REM "Computer qty cost extension
4635 IF M$(20,1)="Y" AND M[0]<>0 THEN A4=A4/M[0] ELSE IF C[15]<>0 THEN A4=A4/C[15]
4645 RETURN 
4650 REM "Figure variance dollars for lotted items
4651 REM "Read in u/m stuff for later use
4654 IF A[0]=0 THEN A[0]=A0
4655 IF A[0]=0 THEN A2=0 ELSE A2=A[4]*A0/A[0]
4657 REM LET A2=A0*A1/C(15); GOTO 04660
4660 IF C$(124,4)<>M$(4,4) THEN DIM M$(20),M[1]; READ (Z[13],KEY="U/M"+C$(124,4))M$,M[0],M[1]
4665 A3=A[4]+A2
4670 A4=A[4]
4695 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; W1$=""; GOSUB 6000; GOSUB 7000
5060 IF X3$(184,1)="1" THEN PRINT (W9)@(INT(W3/2)-39),"** The update was interrupted and will be restarted! See page 1 for details **"; W=W+1
5200 REM "Must Print for Full Range to Update
5270 FOR I=1 TO LEN(V2$)
5280 IF V2$(I,1)=$7E$ THEN V2$(I,1)=" "
5285 NEXT I
5290 IF V3$(2,1)<>"T" AND POS(" "<>V2$)=0 THEN Y4$="IC2PIU" ELSE Y4$=""
5295 GOTO 9900
6000 REM "Page header
6001 IF V0=4 THEN W2$="By Bin Location"
6002 IF V0=4 THEN W2$="By Bin Location"
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W4$,W5$
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6090 RETURN 
6100 REM "
6125 Y5$(1)="Item",Y5$(58)="Physical",Y5$(72)="Computer",Y5$(83)="+------------------ Variance -------------------+"
6130 GOSUB 7300
6135 Y5$(1)="Code",Y5$(12)="Description"
6136 Y5$(58)="Quantity",Y5$(72)="Quantity",Y5$(86)="Quantity",Y5$(100)="Cost",Y5$(106)="Inv UM",Y5$(123)="Amount"
6140 GOSUB 7300
6199 RETURN 
6200 REM " Location Print"
6204 IF V0<>3 THEN RETURN 
6205 DIM B$(LEN(B$)); FIND (Z[2],KEY="D"+S$(1,10)+U1$,DOM=6206)IOL=0320
6210 W1$=U1$+" - "+FNS$(B$(16,30)); IF POS(" "<>B$(111,16))<>0 THEN W1$=W1$+" - "+FNS$(B$(111,16))+", "+B$(127,2)
6249 RETURN 
6300 REM "Print product code
6302 IF V0=1 THEN RETURN 
6303 IF V0=4 THEN RETURN 
6304 IF V0=3 THEN J5$=U9$(5,3) ELSE J5$=U9$(1,3)
6305 DIM F$(100); FIND (Z[6],KEY="T"+J5$,DOM=6306)IOL=0360
6310 IF W+2>W0 THEN GOSUB 6000
6315 Y5$(1)=J5$+"  "+F$(5,15)
6320 GOSUB 7300
6390 RETURN 
6900 REM "Auto run update
6905 IF N0$="" OR V3$(1,1)<>"I" THEN GOTO 6990
6910 EXTRACT (Z[1],KEY="",DOM=6990)V0$,K0$
6915 IF K0$>"" THEN GOTO 6990
6935 CALL "ZZPROM","R0",X3$,S3,"+("+V0$(22,4)+"."+V0$(26,2)+")","","",0
6940 ON S3 GOTO 6941,5200
6950 REMOVE (Z[1],KEY="",DOM=6960)
6960 GOSUB 7400
6990 RETURN 
7000 REM "Totals logic
7001 IF T=T0 AND T[T,0]<2 THEN GOTO 7100
7002 IF T=3 THEN GOTO 7100
7003 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(120)=T1$(1,LEN(Z1$)); GOSUB 7300
7010 IF T<4 THEN Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T=4 OR T$<>"" THEN Y5$(T0+2-T)=T$+" "+Q2$+" ("+STR(T[T,0]:"0")+"):"+S$(1,6)+"Physical value: "+FNQ$(T[T,2],Z1$)+S$(1,6)+"Computer value: "+FNQ$(T[T,3],Z1$)
7020 Y5$(120)=FNQ$(T[T,1],Z1$)
7030 GOSUB 7300
7065 IF T0$="END" THEN Y5$(120)=T2$(1,LEN(Z1$)); GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 W=W+1
7325 IF %IL_PDF THEN PRINT (%IL_PDF,ERR=*NEXT)Y5$ ! 174140
7330 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
7700 REM "Build desc from H8$
7710 H9$=FNS$(H8$(64,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+"x"
7720 H9$=H9$+FNS$(H8$(72,8)); IF H9$<>"" THEN IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
7730 IF NUM(H8$(80,2))<>0 THEN H9$=H9$+H8$(80,2)+" Ply "
7740 H9$=H9$+FNS$(H8$(21,40)); H8$=""
7750 RETURN 
8000 REM "Sort records into PI0 temp file
8001 REM "Don't add skipped records to the PI0 file if the print skipped records <>"Y"
8004 PRINT @(10,19),"NOW SORTING..."
8005 K$=KEY(Z[1],END=8090); READ (Z[1])A0$
8006 PRINT @(24,19),'CE',K$,
8008 IF V0$(79,1)<>"Y" AND A0$(222,1)<>"Y" THEN GOTO 8005
8010 READ (Z[3],KEY=K$(15,20),DOM=8005)B$
8015 IF V0=1 THEN K1$=A0$(1,20)+A0$(21,4)+A0$(100,10)+A0$(25,8) ELSE IF V0=2 THEN K1$=B$(61,3)+A0$(1,20)+A0$(21,4)+A0$(100,10)+A0$(25,8) ELSE IF V0=3 THEN K1$=K$(1,4)+B$(61,3)+K$(5) ELSE IF V0=4 THEN K1$=A0$(100,10)+A0$(1,20)+A0$(21,4)+A0$(25,6)+A0$(31,2)
8020 K2$=K$
8030 WRITE (Z[5],KEY=K1$)K2$
8040 GOTO 8005
8090 PRINT @(0,19),'CE',
8093 READ (Z[5],KEY="",DOM=8094)
8095 RETURN 
8500 REM "PRINT P/E DATE"
8510 DIM P2$(105); FIND (Z[13],KEY=X3$(9,3)+"G/LYE"+V0$(72,4),DOM=8011)P2$
8520 D1$=P2$(NUM(V0$(76,2))*6+15,6),D0$=D1$
8530 W0$="For Fiscal year "+V0$(72,4)+", period "+V0$(76,2)+" ending "+FND$(D0$)
8545 RETURN 
8900 REM "Function Statements
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8950 DEF FNQ$(Z9,Z9$)=STR(Z9:Z9$)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End program
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 IF %GUI AND Y4$<>"" THEN CALL Y4$
9927 IF %GUI THEN EXIT 
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 RUN "ZMENU"
9999 END 
56001 REM "272072-Physical Count Variance report ranges do not work correctly 
