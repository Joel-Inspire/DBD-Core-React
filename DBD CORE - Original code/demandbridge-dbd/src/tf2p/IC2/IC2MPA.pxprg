0010 REM "Inventory Control Parameter Setup <IC2MPA>
0020 SETESC 9300; SETERR 9000
0035 REM "5.1 - 12/20/02 - 11.321111 - dmm - SSP# 148759
0040 REM "Copyright 2002 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="IC2MPA",X1$="Inventory Control Parameter Setup"
0120 DIM Z0$(80,"-")
0135 C9=-1,V9=-2
0175 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 K9$=X3$(9,3)+"I/C",K9=LEN(K9$)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1,LEN(A$)),A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X ZZPARM  06O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0620 GOSUB 6000
0700 REM "Default I/C Account type parameter
0710 IF POS("I/C"=X4$(7),12)=0 THEN CALL "ZZACCT",X3$,X4$,"A","I/C","0A5"
0750 REM "Set statistics codes verify string"
0760 GOSUB 7800
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1034 REM "(49,1) highest byte used 5/9/90
1035 DIM A$(95+K9),A[8]; A$(8+K9,1)="4"
1045 A$(1,0+K9)=K9$+A1$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1500
1200 REM "
1215 IF C0<0 THEN GOTO 9900
1500 REM "
1505 Z9=-1
1510 A1$=K9$
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1525 Z9=0
1530 GOSUB 6600
1540 READ (Z[1],KEY=X3$(9,3)+"G/LYE"+A$(1+K9,4),DOM=1541)B$
1590 GOTO 1700
1600 REM "
1610 GOSUB 7700; GOTO 1500
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630,6975
1630 GOTO 9900
1660 IF V9>=0 THEN GOSUB 6200
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1820 CALL "ZZPROM","P",X3$,Z,"","","",X0; ON Z GOTO 1850,6700
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310
1865 C9=1
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF C0>3 OR X0<0 THEN GOTO 1800
2035 IF X0*6+11<LEN(V9$) THEN IF V9$(X0*6+11,1)="2" THEN GOTO 2020
2040 ON X0 GOTO 2100,2150,2250,2350,2900,2550,2600,2950,2450,2650,3100,2700,2750,2800,2850,3000,3050,3150,1800
2100 REM "Current fiscal year
2110 CALL "ZZENTR","ZX",A{ALL},A$,X4$,X3$,21,3+V0,1+K9,4,C0,"","{3"+X$,"","IC2MPA00","ZZPD","ZZ2MDX",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2110,2111
2120 READ (Z[1],KEY=X3$(9,3)+"G/LYE"+A$(1+K9,4),DOM=2110)B$
2140 GOTO 2020
2150 REM "Current accounting period
2160 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,67,3+V0,5+K9,2,C0,"","{3"+X$,"","IC2MPA02","","",""
2170 IF A$(5+K9,2)>B$(13,2) OR NUM(A$(5+K9,2))<1 THEN GOTO 2160
2190 GOTO 2020
2250 REM "Size of inv code
2260 CALL "ZZENTR","ZX",A{ALL},A$,X4$,X3$,67,5+V0,7+K9,2,C0,"","{3"+X$,"","IC2MPA06","","",""
2270 IF NUM(A$(7+K9,2))<1 OR NUM(A$(7+K9,2))>10 THEN GOTO 2260
2280 IF C0>0 THEN GOSUB 7600
2290 GOTO 2020
2350 REM "Customer specific inventory"
2360 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,67,7+V0,32+K9,1,C0,"","{3"+X$,"","IC2MPA10","","",""
2390 GOTO 2020
2450 REM "Keep inv trans detail
2460 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,67,12+V0,29+K9,1,C0,"","{3"+X$,"","IC2MPA14","","",""
2490 GOTO 2020
2550 REM "Stat codes to retain
2553 I$=A$(9+K9,10)
2555 X$=D9$; IF D8$>"" THEN PRINT @(0,22),D9$; X$=D8$
2560 CALL "ZZENTR","SU",A{ALL},I$,X4$,X3$,67,9+V0,1,10,C0,"","{3"+X$,"","IC2MPA18","","",""
2570 I1$=""
2575 FOR I=1 TO 10; IF I$(I,1)<>" " THEN IF POS(I$(I,1)=S0$)=0 THEN EXITTO 2560 ELSE I1$=I1$+I$(I,1)
2580 NEXT I
2585 A$(9+K9,10)=I1$,S0=POS(" "=A$(9+K9,10))-1
2587 PRINT @(67,9+V0),A$(9+K9,10),@(0,22),'CL',
2590 GOTO 2020
2600 REM "Prior year stats to keep
2605 PRINT @(67,10+V0),'CL',A$(19+K9,S0),; A$(19+K9+S0,10-S0)=""
2610 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,67,10+V0,19+K9,S0,C0,"","{3"+X$,"","IC2MPA20","","",""
2615 S1=NUM(A$(19+K9,10),ERR=2610)
2640 GOTO 2020
2650 REM "Periods to retain history
2655 IF A$(29+K9,1)<>"Y" THEN GOTO 2020
2660 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,67,13+V0,30+K9,2,C0,"","{3"+X$,"","IC2MPA22","","",""
2690 GOTO 2020
2700 REM "Post G/L by location
2710 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,67,15+V0,33+K9,1,C0,"","{3"+X$,"","IC2MPA71","","",""
2720 IF A$(33+K9,1)<>"Y" THEN A$(34+K9,3)=""; PRINT @(67,16),"  ",@(67,17),"  ",
2740 GOTO 2020
2750 REM "Starting digit of sub-acct
2760 IF A$(33+K9,1)<>"Y" THEN GOTO 2790
2770 CALL "ZZENTR","ZX",A{ALL},A$,X4$,X3$,67,16+V0,34+K9,2,C0,"","{3"+X$,"","IC2MPA73","","",""
2790 GOTO 2020
2800 REM "# of digit for sub-acct
2810 IF A$(33+K9,1)<>"Y" THEN GOTO 2840
2820 CALL "ZZENTR","ZX",A{ALL},A$,X4$,X3$,67,17+V0,36+K9,1,C0,"","{3"+X$,"","IC2MPA74","","",""
2830 IF NUM(A$(34+K9,2))+NUM(A$(36+K9,1))-1>12 THEN GOTO 2820
2840 GOTO 2020
2850 REM "Distributed purchasing
2851 GOTO 2890
2860 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,67,18+V0,37+K9,1,C0,"","{3"+X$,"","IC2MPA76","","",""
2890 GOTO 2020
2900 REM "Lot inventory?
2905 IF X3$(30,1)="S" THEN X$="Lot inventory MUST be set to 'N' for TopForm SL Series"
2910 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,67,8+V0,44,1,C0,"","{3"+X$,"","IC2MPA77","","",""
2920 IF X3$(30,1)="S" THEN A$(44,1)="N"; PRINT @(67,8+V0),A$(44,1),
2940 GOTO 2020
2950 REM "Unit Price/Cost Precision"
2955 IF POS(X3$(9,3)="500021",3)=0 THEN GOTO 2020
2960 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,67,11+V0,45,1,C0,"","{3"+X$,"","IC2MPA79","","",""
2970 IF POS(A$(45,1)=" 03")=0 THEN GOTO 2960
2990 GOTO 2020
3000 REM "Diversified I/C Feature Driven Pricing
3005 IF POS(X3$(9,3)="500021",3)=0 THEN GOTO 2020
3010 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,67,19,46,1,C0,"","{3"+X$,"","IC2MPA80","","",""
3040 GOTO 2020
3050 REM "Second description line
3055 ! IF POS(X3$(9,3)="500021",3)=0 THEN GOTO 2020
3060 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,67,20,47,1,C0,"","{3"+X$,"","IC2MPA82","","",""
3090 GOTO 2020
3100 REM "Enable backorder fill reporting
3110 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,67,14,48,1,C0,"","{3"+X$,"","IC2MPA84","","",""
3140 GOTO 2020
3150 REM "Back Order Qty to reduce Committed? - QBS only
3155 IF POS(X3$(9,3)="032500",3)=0 THEN GOTO 2020
3160 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,67,21,49,1,C0,"","{3"+X$,"","IC2MPA86","","",""
3190 GOTO 2020
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(0,3+V0),"Current fiscal year:",@(40,3+V0),"Current accounting period:",@(0,4+V0),Z0$,
6070 PRINT @(26,5+V0),"What size inventory code should be used:",@(0,6+V0),Z0$,
6080 PRINT @(23,7+V0),"Do you require customer specific inventory?",@(17,9+V0),"Enter code(s) of statistical data to be retained:",@(2,10+V0),"Enter the corresponding number of prior year statistics to keep:",@(22,12+V0),"Do you require inventory transaction detail?",@(21,13+V0),"Number of periods to retain inventory detail:",@(0,15+V0),"Does this company post G/L inventory control accounts by location?",@(7,16+V0),"Starting digit of the G/L sub-account number to substitute:",@(33,17+V0),"Number of digits for sub-account:"
6090 REM "PRINT @(6,18+V0),"IS THE PURCHASING FUNCTION PERFORMED AT THE WAREHOUSE LEVEL?",
6100 PRINT @(8,8),"Do you require a lot control sub-ledger to your inventory?",
6110 PRINT @(36,11),"Reserved for future use (DRD):",
6112 PRINT @(26,14),"Do you require backorder fill reporting?",
6115 PRINT @(36,19),"Reserved for future use (FDP):",@(36,20),@(36,21),"Reserved for future use (BOC):",
6120 PRINT @(16,20),"Allow access to item code second description line?", ! SSP148759, was Reserved for future use (SDL)
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 PRINT @(21,3+V0),A$(1+K9,4),@(67,3+V0),A$(5+K9,2),@(67,5+V0),A$(7+K9,2),@(67,7+V0),A$(32+K9,1),@(67,9+V0),A$(9+K9,10),@(67,10+V0),A$(19+K9,10),@(67,12+V0),A$(29+K9,1),@(67,13+V0),A$(30+K9,2),@(67,15+V0),A$(33+K9,1),@(67,16+V0),A$(34+K9,2),@(67,17+V0),A$(36+K9,1),
6225 PRINT @(67,8),A$(44,1),@(67,11),A$(45,1),@(67,14),A$(48,1),
6230 PRINT @(67,19),A$(46,1),@(67,20),A$(47,1),@(67,21),A$(49,1),
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6490 RETURN 
6500 REM "DELETE
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6590 A1$=""; GOTO 1630
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY
6705 IF Z9=0 THEN GOTO 6790
6709 GOSUB 7600
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6790 C9=0; IF Z=6 OR K1=0 THEN GOTO 9900 ELSE IF Z<4 THEN GOTO 1000 ELSE C0=-(3.1+(Z-4)/10); GOTO 6800
6800 REM "NEXT ENTRY IN FILE
6810 A1$=KEY(Z[1],END=1140)
6820 A$(1,LEN(A1$))=A1$
6825 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 1140
6830 GOSUB 6600
6835 GOSUB 6450
6890 Z9=0; GOTO 1515
6999 ESCAPE 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts: 
7550 REM "Added stmts: 
7575 REM "Deleted stmts: 
7600 REM "Write out customer display params
7605 REM "ICE is for input of second 10 chars of item code by themselves
7620 IF NUM(A$(7+K9,2))=10 THEN C$="S" ELSE C$=A$(8+K9,1)
7630 C0$=" "+C$+"A",C$=" aA"+C$+"A"
7635 CALL "ZZACCT",X3$,X4$,"A","ICE",C0$
7640 CALL "ZZACCT",X3$,X4$,"A","I/C",C$
7645 RETURN 
7700 REM "Setup defaults"
7705 DIM B$(50)
7710 FIND (Z[6],KEY=X3$(9,3)+"G/L",DOM=7711)B$
7720 A$(1)=K9$+B$(34,6)+"  SPU       111       N  NN"
7725 GOSUB 7600
7730 WRITE (Z[6],KEY=K9$)IOL=0310
7790 RETURN 
7800 REM "Set S0$ and D9$ for statistics
7810 S0$="",D9$="",D8$=""
7820 READ (Z[1],KEY="STATI/C",DOM=0790)D$
7825 FOR I=49 TO LEN(D$)-7 STEP 17
7830 IF D$(I,1)<>" " THEN S0$=S0$+D$(I,1); IF I<134 THEN D9$=D9$+", "+D$(I,1)+"="+FNS$(D$(I+5,12)) ELSE D8$=D8$+", "+D$(I,1)+"="+FNS$(D$(I+5,12))
7850 NEXT I
7880 D9$=D9$(3)
7882 IF D8$>"" THEN D8$=D8$(3)
7890 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "IC2MPB"
9955 REM 
9999 END 
