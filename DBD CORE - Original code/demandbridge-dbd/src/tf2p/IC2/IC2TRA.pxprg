0010 REM "I/C Transaction Journal <IC2TRA>
0035 REM "5.6 - 07/17/08 - 10.488333 - tma - SSP# 221051
0037 REM "221051-I/C T1 prints 2 Adobe reports, but only one images.         
0040 REM "Copyright 2008 DemandBridge, Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 SETERR 9000; SETESC 9300
0110 X0$="IC2TRA",X1$="I/C Transaction Journal"
0120 DIM A$(53),A[1],B$(54),B[4],C$(224),D$(139),D[20],E$(397),E[15],F$(310),F[2],C1$(224),C2$(224),P2$(105)
0140 Z0$="##,###,###.00-",Z1$="####0-",Z2$="###,###.00#",Z3$="####,###.00-"
0210 T=1,T0=0,T1=1
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR ICA - Inventory Transaction Header
0310 IOLIST A$,A[0],A[1]
0315 REM "IOLIST FOR ICB - Inventory Transaction Lines
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4]
0325 REM "IOLIST FOR FM0 - Warehouse Locations
0330 IOLIST C$
0331 IOLIST C1$
0332 IOLIST C2$
0335 REM "IOLIST FOR IC0 - Item Masterfile
0340 IOLIST D$,D[0],D[1],D[2],D[3],D[4],D[5],D[6],D[7],D[8],D[9],D[10],D[11],D[12],D[13],D[14],D[15],D[16],D[17],D[18],D[19],D[20]
0345 REM "IOLIST FOR AR1 - Customer Masterfile
0350 IOLIST E$,E[0],E[1],E[2],E[3],E[4],E[5],E[6],E[7],E[8],E[9],E[10],E[11],E[12],E[13],E[14],E[15]
0355 REM "IOLIST FOR AP4 - Vendor Masterfile
0360 IOLIST F$,F[0],F[1],F[2]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O ICA...  02O ICB...  03O FM0...  04O IC0...  05O AR1...  06O AP4...  07O ICC...  13O ZZPARM  "; IF V1$(1,3)<>Y$(5,3) THEN Y$=Y$+"02O "+V1$+"  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0550 READ (Z[13],KEY=X3$(9,3)+"I/C")P0$
0605 N0$="IC2TRA"; GOSUB 6900
0610 GOSUB 8500
0620 CALL "ZZDAPP",X3$,"DZZINPTZZENTR"
0700 REM "Open Printer
0720 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0721,9920
0725 IF %HAS_IMAGE_LIB THEN CALL "ILGAUD",ERR=*NEXT,X3$,X4$,"R9" ! 174140
0750 REM "Check for restart record"
0755 READ (Z[1],KEY="",DOM=0780)
0760 IF K0$>"" THEN X3$(184,1)="1"
0765 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W4,W4$,W5$
0770 CALL "ZZREST",X3$,Z[13],Z[1],W9,W3,W,RES$,K0$,K1$,K0,K1
0775 GOSUB 6000
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[3]; U=12,U0=3,U[0]=0,U[1]=4,U[2]=2,U[3]=6,T0=1,V1=6,U0$="Y   ",T5$="S               ",U5$="Fiscal year"; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN READ (U1,END=5000); EXITTO 1000
1030 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U=1 THEN EXITTO 5000 ELSE READ (U1,END=5000); EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1300 REM "Read other data
1350 DIM E$(LEN(E$)); FIND (Z[5],DOM=1351,KEY=C$(2,10))IOL=0350
1400 REM "Subtotals"
1415 GOSUB 3100
1420 ON T GOTO 1470,1430,1440,1450,1460
1490 T=0
1500 REM "Print Data
1510 IF W+1>W0 THEN GOSUB 6000
1515 Y5$(1)="Reference:",Y5$(12)=A$(7,6),Y5$(20)=A$(13,40)
1810 GOSUB 2000
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+V[1]
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
2000 REM "Print Lines or Detail"
2001 DIM V[1]
2010 IF A[0]<1 THEN RETURN ELSE X3=A[0],T4=0,B1$=""
2015 GOSUB 2500
2020 IF X3<1 THEN GOSUB 2700; RETURN 
2030 READ (Z[2],IND=X3)X3,IOL=0320
2040 REM "Read Secondary Information"
2050 IF B$(1,1)="T" THEN F9$="Y"
2080 DIM C$(LEN(C$)); FIND (Z[3],DOM=2081,KEY="D"+C$(1,10)+B$(8,4))IOL=0330
2090 DIM D$(LEN(D$)); FIND (Z[4],DOM=2091,KEY=B$(26,20))IOL=0340
2100 DIM E$(LEN(E$)); FIND (Z[5],DOM=2101,KEY=B$(26,10))IOL=0350
2110 DIM F$(LEN(F$)); FIND (Z[6],DOM=2111,KEY=B$(12,10))IOL=0360
2120 DIM C1$(LEN(C1$)); FIND (Z[3],KEY="P"+B$(12,14),DOM=2121)IOL=0331
2125 DIM C2$(LEN(C2$)); FIND (Z[3],KEY="D"+C2$(1,10)+B$(50,4),DOM=2126)IOL=0332
2200 REM "Print Each Line
2205 T4=T4+1
2210 GOSUB 7550; GOSUB 7300
2213 IF B1$<>"" AND B$(8,4)=B1$ THEN GOTO 2224
2214 C8$=C$; GOSUB 7800
2215 C7$=C9$,B1$=B$(8,4),Y5$(2)="Location:",Y5$(12)=B$(8,4),Y5$(18)=C9$
2220 GOSUB 7300
2225 GOSUB 7500
2230 Y5$(13)=FND$(B$(2,6)); IF B$(2,6)=DIM(6) THEN DATEFLG=1
2232 CALL "ZZDISP","AX",B$(26,20),"I/C",X3$,X$,"",0,0,X4$
2233 H8$=D$; GOSUB 7750
2235 Y5$(24)=X$+"  "+H9$,Y5$(78)=""
2237 Y5$(80)=FNS$(B$(46,4))+"/"+FNR$(STR(B[2]:Z1$)),Y5$(92)=STR(B[0]:Z0$); IF B$(1,1)<>"C" THEN Y5$(107)=STR(B[1]:Z2$),Y5$(120)=STR(B[3]:Z3$)
2238 IF V0$(59,1)="S" THEN IF D[14]=0 THEN D[14]=1; REM 'REPORT IN SELLING UNITS IF S IS 1ST CHAR OF OPTIONS FIELD
2239 IF V0$(59,1)="S" THEN Y5$(80,5+LEN(Z1$))=D$(120,4)+"/"+STR(D[14]:Z1$),Y5$(92,LEN(Z0$))=STR(B[0]/D[14]:Z0$)
2240 GOSUB 7300
2244 IF POS(" "<>B$(26,10))=0 THEN GOTO 2256
2245 Y5$(14)="Customer:"
2247 CALL "ZZDISP","AX",B$(26,10),"A/R",X3$,X$,"",0,0,X4$
2250 Y5$(24)=X$+"  "+E$(11,35)
2255 GOSUB 7300
2259 IF POS(" "<>B$(50,4))=0 THEN GOTO 2266 ELSE C8$=C2$; GOSUB 7800
2260 Y5$(11)="to location:",Y5$(25)=B$(50,4),Y5$(31)=C9$
2265 GOSUB 7300
2269 IF POS(" "<>B$(12,10))=0 THEN GOTO 2281
2270 Y5$(16)="Vendor:"
2272 CALL "ZZDISP","AX",B$(12,10),"A/P",X3$,X$,"",0,0,X4$
2273 GOSUB 7850
2275 Y5$(24)=X$+C9$
2280 GOSUB 7300
2300 REM "Post recap total
2305 IF B$(1,1)="C" THEN GOTO 2321
2310 O1$=B$(8,4)+B$(1,1),O2$=C7$
2320 GOSUB 8600; O[O4]=O[O4]+B[3]
2400 REM "Accumulate Line Totals"
2410 V[1]=V[1]+B[3]
2499 GOTO 2020
2500 REM "Line Header
2515 GOSUB 7300
2690 RETURN 
2700 REM "Line Totals"
2701 IF T4<2 THEN GOSUB 7300; GOTO 2790
2702 IF W+2>W0 THEN GOSUB 6000
2705 Y5$(119)=T1$(1,14); GOSUB 7300
2720 Y5$(120)=STR(V[1]:Z3$); GOSUB 7300
2765 IF T$="END" THEN Y5$(119)=T2$(1,14); GOSUB 7300
2780 GOSUB 7300
2795 RETURN 
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOSUB 8000; GOTO 3160
3150 T=1,T$=U5$,T6$=U1$(1,4)+" Period "+U1$(5,2); GOSUB 3050; GOSUB 7000; IF T0$<>"END" THEN GOSUB 8000
3155 IF U0$(1,1)="Y" THEN GOSUB 6000
3160 U1$=U9$(1,V1)
3180 RETURN 
3280 RETURN 
3380 RETURN 
3480 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,W0$="",T$="Transaction Journal"; GOSUB 7000
5100 REM "Print recap totals
5110 GOSUB 8650
5120 READ (Z[7],KEY="",DOM=5121)
5125 K$=KEY(Z[7],END=5200)
5130 READ (Z[7],KEY=K$)R$,R0
5140 IF K0$="" OR K$(1,4)<>K0$ THEN GOSUB 5160
5142 IF W+2>W0 THEN GOSUB 6000
5144 B$(1,1)=K$(5,1)
5145 GOSUB 7500; Y5$(120)=STR(R0:Z3$); GOSUB 7300
5150 GOTO 5125
5160 REM "New location
5162 IF K0$="" THEN GOSUB 7300; GOSUB 7300; GOSUB 7300
5165 IF W+4>W0 THEN GOSUB 6000
5166 GOSUB 7300
5170 Y5$(1)=K$(1,4)+" - "+R$
5175 GOSUB 7300; GOSUB 7300
5176 K0$=K$(1,4)
5180 RETURN 
5200 REM "
5205 IF DATEFLG=1 THEN J$="An Invalid Trans date or Fiscal YR/Acct Per has been detected, no update allowed"; CALL "ZZPROM",".4",X3$,Z,J$,"","",0; GOTO 9900
5210 GOSUB 7200
5290 IF P0$(67,1)="Y" AND F9$="Y" THEN Y4$="IC2TRC" ELSE IF P0$(44,1)="Y" THEN Y4$="IC2TRB" ELSE IF V3$(2,1)<>"T" THEN Y4$="IC2TUA" ELSE Y4$=""
5295 GOTO 9900
6000 REM "Page header
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W4$,W5$
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6115 Y5$(1)="----- Transaction -----",Y5$(80)="Unit of"
6120 GOSUB 7300
6125 Y5$(4)="Type",Y5$(16)="Date",Y5$(26)="Item code / Description",Y5$(80)="Measure",Y5$(96)="Quantity",Y5$(110)="Cost",Y5$(122)="Extension"
6199 RETURN 
6200 REM "Break #1 header
6299 RETURN 
6900 REM "Auto run update
6905 IF N0$="" OR V3$(1,1)<>"I" THEN GOTO 6990
6910 EXTRACT (Z[1],KEY="",DOM=6990)RES$,K0$
6915 IF K0$>"" THEN GOTO 6990
6930 Q$="+ ("+RES$(22,4)+"."+RES$(26,2)+")"
6935 CALL "ZZPROM","X0AUDITS",X3$,S3,Q$,"","",0; ON S3 GOTO 6940,5290,9900
6940 REM "CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,X9
6950 REMOVE (Z[1],KEY="")
6990 RETURN 
7000 REM "Totals logic
7001 REM IF T=T0 THEN IF T(T0,0)<2 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(119)=T1$(1,14); GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" "+Q2$+" ("+STR(T[T,0]:"0")+"):"
7020 Y5$(120)=STR(T[T,1]:Z3$); GOSUB 7300
7065 IF T$="END" THEN Y5$(119)=T2$(1,14); GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600),T4=MOD(T3,60),T3=INT(T3/60)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")+":"+STR(T4:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 W=W+1
7325 IF %IL_PDF THEN PRINT (%IL_PDF,ERR=*NEXT)Y5$ ! 174140
7330 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "Print trans-type desc
7505 ON POS(B$(1,1)="TCRAP") GOTO 7535,7510,7515,7520,7525,7530
7510 Y5$(2)="Transfer"; GOTO 7535
7515 Y5$(2)="Commit"; GOTO 7535
7520 Y5$(2)="Receipt"; GOTO 7535
7525 Y5$(2)="Adjust"; GOTO 7535
7530 Y5$(2)="P/O"; GOTO 7535
7535 IF B$(57,1)="W" THEN Y5$(2)="W-Adjust"
7540 RETURN 
7550 REM "Check # of lines needed to print a detail line
7555 X5=2
7560 IF B1$<>"" AND B1$<>B$(8,4) THEN X5=X5+1
7565 IF POS(" "<>B$(26,10))<>0 THEN X5=X5+1
7570 IF POS(" "<>B$(12,10))<>0 THEN X5=X5+1
7575 IF POS(" "<>B$(50,4))<>0 THEN X5=X5+1
7580 IF W+X5>W0 THEN GOSUB 6000; GOSUB 2500
7590 RETURN 
7750 REM "Build desc from H8$
7760 H9$=FNS$(H8$(64,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+"x"
7770 H9$=H9$+FNS$(H8$(72,8)); IF H9$<>"" THEN IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
7775 IF NUM(H8$(80,2))<>0 THEN H9$=H9$+H8$(80,2)+" Ply "
7780 H9$=H9$+FNS$(H8$(21,40)); H8$=""
7790 RETURN 
7800 REM "Take loc and desc C8$ return C9$ formatted
7805 C9$=FNS$(C8$(16,35))
7810 IF POS(" "<>C8$(111,16))<>0 THEN C9$=C9$+" - "+FNS$(C8$(111,16))+", "+C8$(127,2)
7840 RETURN 
7850 REM "Get plant & address or vendor address
7855 IF POS(" "<>B$(22,4))<>0 THEN GOTO 7880
7860 DIM C8$(129); C8$(16,35)=F$(11,35),C8$(111,18)=F$(106)
7865 GOSUB 7800; C9$="  "+C9$
7870 GOTO 7890
7880 C8$=C1$; GOSUB 7800
7885 C9$=" - "+B$(22,4)+" "+C9$
7890 RETURN 
8000 REM "PRINT P/E DATE"
8010 IF P2$(9,4)<>A$(1,4) THEN DIM P2$(105); FIND (Z[13],KEY=X3$(9,3)+"G/LYE"+A$(1,4),DOM=8011)P2$
8020 D1$=P2$(NUM(A$(5,2))*6+15,6),D0$=D1$
8025 IF POS(" "=A$(1,6))<>0 THEN DATEFLG=1
8030 W0$="For Fiscal year "+A$(1,4)+", period "+A$(5,2)+" ending "+FND$(D0$)
8060 RETURN 
8500 REM "Initialize Recap stuff
8510 REM "O0=max # to hold in core,O1=max size of key,O2=# of totals to keep,O3=device # of recap file,O4=starting index of totals in O(),O$=string of keys,O0$=blank string used in 8600 routine,O1$=key to add,IOL is at 8550,O2$=desc, O4$=string to hold desc, O7=max size of desc.
8520 O0=40,O1=5,O2=1,O3=Z[7],O7=50
8530 DIM O0$(O1),O1$(O1)
8534 REM "Set iolist at 8550
8535 O$=""; FOR O4=1 TO O2; O$=O$+",O("+STR(O4)+")"; NEXT O4
8540 O$="8550 IOLIST O5$,"+O$(2)
8545 EXECUTE O$
8550 IOLIST O5$,O(1)
8560 CALL "ZZINIT",STR(O3:"00")
8570 REM "Don't put anything between 8580 and 8590, because a gosub 8580 is used in 8650 routine
8580 DIM O[(O0+1)*O2],O$(O0*O1),O4$(O0*O7)
8590 RETURN 
8600 REM "Set O1$ to key, then gosub here to get O4 set to position of the 1st total in the O array
8601 REM "exit from routine through 8620
8610 O4=POS(O1$=O$,O1)
8619 REM "if found / index by O1 and return
8620 IF O4>0 THEN O8=INT(O4/O1),O4=(O8+1)*O2+1,O4$(O8*O7+1,O7)=O2$; RETURN 
8629 REM "if not found check for room in string
8630 O4=POS(O0$=O$,O1)
8639 REM "If no room,use clear rountine & set O4 to 1st position
8640 IF O4=0 THEN GOSUB 8650; O4=1
8644 REM "use 8620 exit to finish up, now that index is found
8645 O$(O4,O1)=O1$; GOTO 8620
8650 REM "Write totals out to file & clear O$,O()
8660 FOR O4=1 TO LEN(O$) STEP O1
8661 IF O$(O4,O1)=O0$ THEN GOTO 8680
8665 O5=(INT(O4/O1)+1)*O2; REM "One less than index set in 8620
8668 FOR O6=1 TO O2; O[O6]=0; NEXT O6
8670 READ (O3,KEY=O$(O4,O1),DOM=8671)IOL=8550
8672 FOR O6=1 TO O2; O[O6]=O[O5+O6]; NEXT O6
8673 O5$=O4$(1+INT(O4/O1)*O7,O7)
8675 WRITE (O3,KEY=O$(O4,O1))IOL=8550
8680 NEXT O4
8684 REM "Use last part of init logic to clear O$ & O()
8685 GOSUB 8580
8690 RETURN 
8900 REM "Function Statements
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8920 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
8925 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "End program
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9915 WAIT 5 ! SSP#221051
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 IF %GUI<>0 AND Y4$<>"" THEN CALL Y4$,ERR=*NEXT
9927 IF %GUI THEN EXIT 
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 RUN "ZMENU"
9999 END 
56000 REM "221051-I/C T1 prints 2 Adobe reports, but only one images.         
