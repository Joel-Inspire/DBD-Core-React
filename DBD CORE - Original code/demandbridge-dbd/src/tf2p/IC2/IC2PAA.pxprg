0010 REM "Physical Inventory Setup  <IC2PAA>
0020 SETESC 9300; SETERR 9000
0035 REM "5.2 - 02/18/04 - 10.805277 - tma - SSP# 169078
0040 REM "Copyright 2004 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="IC2PAA",X1$="Physical Inventory Setup",K9$="",K9=0
0120 DIM Z0$(80,"-"),S$(20)
0130 K0=24,K1=2
0135 C9=-1
0140 M0$="##,###,##0-",M1$="###,###.00-",M2$="##,###,###.00-"
0160 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0175 DEF FNR$(Z8$)=Z8$(POS(" "<>Z8$))
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1),A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10]
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19]
0330 IOLIST C$
0340 IOLIST D$,D[0],D[1],D[2],D[3],STR(D[4])
0370 IOLIST G$
0390 IOLIST E$,E[0],E[1]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X IC1...  02O IC0...  03O FM0...  04L IC3...  05O IC6...  06O IC7...  07O AR1... 08O FMP... 13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0625 DIM A$(46),A[10],B$(128),B[19],C$(53),D$(53),D[4],L$(4),E$(21),E[1],L1$(2),L2$(10)
0630 L0=8
0700 READ (Z[4],END=0701); GOTO 8900
0990 GOSUB 6000
1000 GOTO 2000
1800 REM "
1820 CALL "ZZPROM","0",X3$,Z,"","","V",X0; ON Z GOTO 2500,9900,2000
1830 GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF C0>3 OR X0<0 THEN GOTO 1800
2040 ON X0 GOTO 2300,2350,2400,1800
2300 REM "Location to print
2305 X$="(leave blank to combine all)"
2310 CALL "ZZENTR","SZ^",A{ALL},L$,X4$,X3$,38,8+L0,1,4,C0,"","{3"+X$,"D"+S$(1,10),"IC2PVS08","FM0D","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2310,2311
2320 DIM C$(50); IF L$(1,4)<>"    " THEN FIND (Z[3],KEY="D"+S$(1,10)+L$(1,4),DOM=2310)IOL=0330
2325 PRINT @(44,8+L0),C$(16,30),
2340 GOTO 2020
2350 REM "Cycle to print"
2352 X$="(leave blank to combine all)"
2360 CALL "ZZENTR","SZ^",A{ALL},L1$,X4$,X3$,38,9+L0,1,2,C0,"","{3"+X$,"","IC2PVS08","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2360,2361
2390 GOTO 2020
2400 REM "Count Date
2405 IF L2$="" THEN DIM L2$(10)
2410 CALL "ZZENTR","AU^",A{ALL},L2$,X4$,X3$,38,10+L0,1,10,C0,"A/R","{3"+X$,"","IC2MAC12","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2410,2411
2430 DIM G$(200); IF L2$<>S$(1,10) THEN READ (Z[7],KEY=L2$,DOM=2400)IOL=0370
2435 PRINT @(38,L0+11),G$(11,30),
2440 GOTO 2020
2500 CALL "ZZPROM","9",X3$,Z,"","","",0; ON Z GOTO 2501,9900
3000 REM "Freeze Inventory & Build IC3"
3002 PRINT @(0,12+L0),"CHECKING DATA...",
3010 READ (Z[5],KEY=L$,DOM=3011)
3020 K$=KEY(Z[5],END=4000); READ (Z[5])
3025 PRINT @(28,12+L0),A$(1,20),"-",A$(21,4),'CL',
3030 IF L$<>S$(1,4) THEN IF K$(1,4)<>L$ THEN GOTO 4000
3032 IF L2$<>S$(1,10) THEN IF K$(8,10)<>L2$ THEN READ (Z[5],KEY=K$(1,17)+$FF$,DOM=3020)
3040 READ (Z[1],KEY=K$(8,20)+K$(1,4),DOM=3020)IOL=0310
3050 REM "CYCLE
3055 IF L1$="  " THEN GOTO 3070 ELSE IF A$(45,2)<>"  " THEN IF L1$<>A$(45,2) THEN GOTO 3020
3060 REM IF A$(1,10)<>S$(1,10) THEN READ (Z(8),KEY="D"+A$(1,10),DOM=03020) D0$; IF D0$(46,2)<>L1$ THEN GOTO 03020
3065 IF A$(45,2)<>L1$ THEN GOTO 3020
3070 FIND (Z[2],KEY=A$(1,20))IOL=0320
3072 EXTRACT (Z[1],KEY=K$(8,20)+K$(1,4))IOL=0310
3075 PRINT " Written.",; REM PRINT @(28,12+L0),A$(1,20),"-",A$(21,4),
3080 FIND (Z[13],KEY="U/M"+B$(124,4),DOM=3081)IOL=0390
3085 IF X3$(9,3)="027" THEN E$(20,1)="N"
3100 IF E$(20,1)<>"Y" THEN D$(36,4)=B$(124,4),D[1]=B[15] ELSE D$(36,4)="CASE",D[1]=B[19]
3110 D[0]=A[3]+A[4]-A[5]+A[6]
3120 IF D[1]<=0 THEN D[1]=1
3125 IF A[2]<>0 THEN D[4]=A[2] ELSE D[4]=B[18]
3170 D$(1,35)=A$(21,4)+A$(35,10)+A$(1,20)+" "
3180 WRITE (Z[4],KEY=D$(1,35))IOL=0340
3190 WRITE (Z[6],KEY=D$(1,4)+B$(61,3)+D$(15,21)+D$(5,10))
3200 GOTO 3020
4000 REM "EOJ"
4020 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This program will 'freeze' the computer count for the quantity on-hand",'LF',"against which the physical count will be compared."
6030 PRINT @(0,7),"Also, the physical inventory count sheet file will be created in bin location",'LF',"sequence."
6060 PRINT @(30,10),"IMPORTANT--------->  Each time run, the",'LF',"program will reset the computer count against which the physical count",'LF',"variance will be computed and ultimately posted to the inventory file."
6080 PRINT @(28,8+L0),"Location:",@(24,9+L0),"Cycle Number:",
6085 PRINT @(28,10+L0),"Customer:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "FOREGROUND"
6210 PRINT (0,ERR=6211)'CF',
6220 PRINT @(38,8+L0),L$(1,4),
6290 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 GOSUB 6200
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
8900 REM "CLEAR FILE
8915 PRINT @(12,5),"You currently have entries in the Physical Inventory Files."
8916 PRINT @(12,7),"The CLEAR option will remove all previous entries which"
8917 PRINT @(12,9),"have not been updated!"
8920 CALL "ZZPROM","X0AP2CBA",X3$,S3,"","","",0
8930 ON S3 GOTO 8960,8940,9800
8940 PRINT 'CF',@(12,10),"Files now being cleared.",@(12,12),"Do not Interrupt!!",
8950 CALL "ZZINIT",STR(Z[4]:"00")+STR(Z[6]:"00")
8960 PRINT @(0,3),'CE',
8990 GOTO 0701
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9730 RUN U1$(1,6)
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
