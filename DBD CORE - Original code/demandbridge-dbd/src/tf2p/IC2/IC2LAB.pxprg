0010 REM "P/O & Returns Lot Receiving <IC2LAB>
0015 REM "Prog Type: I -3.0A
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 01/10/25 - 14.652983 - crg - SSP# 307483
0037 REM "307483-DBD-457:Support for IMS price in purchase order line        
0040 REM "Copyright 2025 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0080 REM "Used by P/O receiving and O/P lot returns
0081 REM "On exit of lot returns Q0$=STR(TOTAL QTY), Q1$=STR(TOTAL COST)
0082 REM "File slots at max coming from FM2OB0
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000; SETESC 9300
0102 REM " LET Q0$="ICN00008220001"
0110 X0$="IC2LAB",X1$="Inventory Lot Receiving",K9$="",K9=0
0120 DIM Z0$(80,"-"),S0$(80),H[31]
0130 K0=32,K1=5
0135 C9=-1
0140 M0$="###,###.00-",M1$="####,###.00-",M2$="###,##0-",M3$="###,##0-",M4$="#,###,##0-"
0145 IF Q1$="" THEN GOSUB 8950 ELSE IF LEN(Q0$)>=3 THEN IF Q0$(1,3)="ICN" THEN Q2$=Q0$(4),Q0$=Q0$(1,3)
0150 V8$="000210121499"
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(65,1)>"2" THEN PRECISION NUM(X3$(65,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1),A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12]
0315 IOLIST A0$(1),A0[0],A0[1],A0[2],A0[3],A0[4],A0[5],A0[6],A0[7],A0[8],A0[9],A0[10],A0[11],A0[12]
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20],C[21],C[22],C[23],C[24],C[25],C[26],C[27],C[28],C[29],C[30]
0340 IOLIST D$
0345 IOLIST D1$
0350 IOLIST E$,E[0],E[1],E[2],E[3]
0360 IOLIST F$
0370 IOLIST G$
0375 IOLIST G5$,G5[0],G5[1]
0376 IOLIST G6$,G6[0],G6[1]
0377 IOLIST G7$,G7[0],G7[1]
0380 IOLIST H$,H[0],H[1],H[2],H[3],H[4],H[5],H[6],H[7],H[8],H[9],H[10],H[11],H[12],H[13],H[14],H[15],H[16],H[17],H[18],H[19],H[20],H[21],H[22],H[23],H[24],H[25],H[26],H[27],H[28],H[29],H[30],H[31]
0400 IOLIST L$(1),L[0],L[1],L[2],L[3],L[4],L[5],L[6],L[7],L[8],L[9],L[10],L[11],L[12],L[13],L[14],L[15],L[16],L[17],L[18],L[19],L[20],L[21],L[22],L[23],L[24],L[25],L[26],L[27]
0410 IOLIST R$,R[0],R[1],R[2],R[3],R[4],R[5],R[6]
0415 IOLIST R0$,R0[0],R0[1],R0[2],R0[3],R0[4],R0[5],R0[6]
0420 IOLIST S9$
0425 IOLIST I9$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0509 IF Q0$="" THEN Z$="01X POH...  06O PO1...  " ELSE Z$="01X "+Q0$+"...  "
0510 Z$=Z$+"02O AR1...  03O IC0...  04O FM0...  05O FS6...  07O AP4...  08O FM1...  09O PO7...  10O PO2...  11O PO3...  12O IC1...  13O ZZPARM  25O FMP...  26O AR3...  "
0520 GOSUB 9750; ON Z0 GOTO 0521,9900
0530 FIND (Z[13],KEY=X3$(9,3)+"F/M")P0$
0540 FIND (Z[13],KEY=X3$(9,3)+"I/C")P1$
0550 LBL_PRT=0; IF FN%HAS_MODULE("WW") THEN FIND (Z[13],KEY=X3$(9,3)+"W/M",DOM=*NEXT)WM$; IF MID(WM$,7,1)="Y" THEN LBL_PRT=1 ! WO251758
0590 READ (Z[1],KEY=K9$,DOM=0591)
0595 DIM SBO$(255); READ (Z[13],KEY="osec"+X3$(40,3),DOM=0596)SBO$
0600 REM "
0605 IF Q0$="ICN" THEN V1=5,V2=-2; GOSUB 6000 ELSE V1=0,V2=0
0920 IF Q0$="ICN" THEN Q0=0,Q1=0,A1$=Q1$(1,30); GOSUB 8800
1000 REM "
1010 Y8=1; CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1015 DIM V7$(28); V7$(1,1)="1",V7$(2,1)="1",V7$(5,1)="1",V7$(6,1)="1",V7$(12,1)="1",V7$(13,1)="1",V7$(20,1)="1"
1030 DIM B$(400),B[15],C$(128),C[30],D$(224),E$(168),E[7],F$(259),F[23],G$(310),G[2],S$(50),D1$(200),G5[1],G6[1],G7[1]
1031 F0$=""
1035 DIM A$(269),A[12]
1038 S1=S1+1
1039 A$(31,2)=STR(S1:"00"); IF LEN(A1$)>=32 THEN A1$(31,2)=STR(S1:"00")
1040 A$(33,8)=R9$,A$(100,10)=B9$,A$(82,9)=N9$; IF B8$>"" THEN A$(133,15)=B8$(1,15),A$(129,4)=B8$(16,4),A[5]=NUM(B8$(20,11)),A[6]=NUM(B8$(31,9)),A$(215,6)=B8$(40,6)
1041 A$(263,4)=B$(278,4)
1042 IF Q0$="ICN" THEN A$(68,14)=Q1$(31),A$(110,11)=Q2$; GOSUB 5820
1045 IF Q0$="ICN" THEN A$(1,LEN(A1$))=A1$; IF A1$="" THEN A$(1,LEN(K9$))=K9$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 7900; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1075 GOSUB 7900
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 SETERR 9000; C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1250,1300,1350,1400,1500
1200 REM "Customer number
1201 IF Q0$="ICN" THEN GOSUB 6200; GOTO 1240 ELSE GOTO 1240
1205 IF LEN(A1$)>0 THEN A$(1,10)=A1$(1,10); GOTO 1220
1206 IF ABS(C0)<>2 THEN GOTO 1220
1210 CALL "ZZENTR","AXUX",A{ALL},A$,X4$,X3$,18,5+V2,1,10,C0,"A/R","{1"+X$,K9$,"IC2LAB00","AR1","AR2MAB",""; IF C0=6 THEN GOSUB 6400; C0=1
1212 IF INT(ABS(C0))=3 THEN C0=-ABS(C0); READ (Z[1],KEY=A$(1,10),DOM=1213)
1214 IF C0=-1 AND POS(" "<>A$(1,10))=0 THEN GOTO 1220
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9800,6970
1219 IF POS(" "<>A$(1,10))=0 THEN DIM B$(100); GOTO 1225
1220 IF V9<>-2 THEN FIND (Z[2],KEY=A$(1,10),DOM=1210)IOL=0320
1225 PRINT @(31,5),B$(11,30),
1240 GOTO 1120
1250 REM "Item number
1251 C0=0; GOTO 1290
1252 IF Q1$>"" THEN A$(1)=Q1$; GOSUB 6450; GOTO 1270
1255 IF LEN(A1$)>10 THEN A$(11,10)=A1$(11,10); GOTO 1270
1259 J$=A$(1,10)
1260 CALL "ZZENTR","AUX",A{ALL},A$,X4$,X3$,18,6+V2,11,10,C0,"ICE","{2"+X$,J$,"IC2LAB02","IC0","IC2MAB",""; IF C0=6 THEN GOSUB 6400; C0=1
1261 IF ABS(C0)=2 AND LEN(A1$)>0 THEN A1$=A1$(1,0)
1262 READ (Z[1],KEY=A$(1,20),DOM=1263)
1263 IF ABS(C0)=4 THEN GOTO 9800
1264 IF ABS(INT(C0))=3 THEN GOTO 6800
1265 IF ABS(C0)=2 THEN GOTO 1290
1270 IF V9<>-2 THEN FIND (Z[3],KEY=A$(1,20),DOM=1260)IOL=0330
1275 PRINT @(31,6),C$(21,40),
1290 GOTO 1120
1300 REM "Location code
1305 IF LEN(R5$)=4 THEN A$(21,4)=R5$(1,4); PRINT @(18,7),A$(21,4),; GOTO 1320
1306 Q$=A$(1,20)
1307 IF ABS(C0)<>2 THEN IF POS(" "<>A$(21,4))<>0 THEN GOTO 1320
1310 CALL "ZZENTR","SZUX",A{ALL},A$,X4$,X3$,18,7+V2,21,4,C0,"","{2"+R6$,Q$,"IC2LAB04","-POH","FM2SKA",""; IF C0=6 THEN GOSUB 6400; C0=1
1311 IF ABS(C0)=2 AND LEN(A1$)>10 THEN A1$=A1$(1,10)
1312 READ (Z[1],KEY=A$(1,24),DOM=1313)
1313 IF ABS(C0)=4 THEN GOTO 1340
1319 IF POS(" "<>A$(21,4))<>0 THEN IF POS(A$(21,4)=R5$,4)=0 THEN CALL "ZZPROM",".4",X3$,Z,"This item is not being received to that location","","",0; GOTO 1310
1320 IF V9<>-2 THEN FIND (Z[4],KEY="D"+S$(1,10)+A$(21,4),DOM=1310)IOL=0340
1325 GOSUB 7550; GOSUB 7700; GOSUB 8100
1330 IF C0>0 THEN S1=1,A$(31,2)="01"
1340 GOTO 1120
1350 REM "Receipt date
1351 IF Q0$<>"ICN" THEN GOTO 1120
1355 IF POS(" "<>A$(25,6))=0 THEN A$(25,6)=X3$(21,6)
1357 Q$=A$(1,24)
1360 CALL "ZZENTR","DUX",A{ALL},A$,X4$,X3$,45,10,25,6,C0,"","{2"+X$,Q$,"IC2LAB06","ICF","",""; IF C0=6 THEN GOSUB 6400; C0=1
1361 IF ABS(C0)=2 AND Q0$="ICN" THEN GOTO 1360 ELSE IF ABS(C0)=2 AND LEN(A1$)>20 THEN A1$=A1$(1,20)
1362 READ (Z[1],KEY=A$(1,30),DOM=1363)
1390 GOTO 1120
1400 REM "Sequence no
1407 Q$=A$(1,30)
1410 CALL "ZZENTR","SZUX",A{ALL},A$,X4$,X3$,18,8+V2,31,2,C0,"","{2"+X$,Q$,"IC2LAB08","POH","",""; IF C0=6 THEN GOSUB 6400; C0=1
1411 REM IF ABS(C0)=2 AND LEN(A1$)>24 THEN LET A1$=A1$(1,24)
1412 REM READ (Z(1),KEY=A$(1,32),DOM=01413)
1415 IF INT(ABS(C0))=3 THEN GOTO 1400 ELSE IF ABS(C0)=4 THEN GOTO 9900
1440 GOTO 1120
1500 REM "
1505 Z9=-1,C9=0,A2$="",A3$="",A4$="",A5$=""
1510 A1$=A$(1,32); IF Q0$="" THEN A1$=Q1$(1,9)+Q1$(20,3)+A1$
1520 READ (Z[1],ERR=7200,KEY=A1$,DOM=1600)IOL=0310
1522 IF Q1$>"" AND Q0$<>"ICN" AND A$(41,9)<>Q1$(1,9) OR (Q0$="ICN" AND A$(110,11)<>Q2$) THEN DIM A$(269),A[12]; IF Q0$="" THEN A1$(43,2)=STR(NUM(A1$(43,2))+1:"00"),A$(1,32)=A1$(13,32); PRINT @(18,8+V2),A$(31,2),; GOTO 1520 ELSE A1$(31,2)=STR(NUM(A1$(31,2))+1:"00"),A$(1,LEN(A1$))=A1$; PRINT @(18,8+V2),A$(31,2); GOTO 1520
1525 Z9=0,V7$=""; GOSUB 5800
1530 IF Q9=1 THEN Q9=0; RETURN 
1535 S1=NUM(A$(31,2),ERR=1536)
1540 IF A$(125,4)="    " THEN N0=A[4]*A[3] ELSE N0=A[4]*A[2]*A[3]
1545 N0=-N0; GOSUB 8150
1550 S0=1; GOSUB 8300
1590 GOTO 1700
1600 REM "
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630
1630 GOTO 1000
1680 GOSUB 7450
1685 GOSUB 7650
1686 GOSUB CREATE_STORAGE_EXPIRED ! SSP 200130
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1814 IF %HAS_IMAGE_LIB THEN %IL_IMAGE_TYPE$="PO",%IL_IMAGE_KEY$=A$(41,9) ! SSP 211905
1820 CALL "ZZPROM","7",X3$,Z,"","","V",X0; %IL_IMAGE_TYPE$="",%IL_IMAGE_KEY$=""; WML=Z; ON Z GOTO 1850,6500,1630,6700,1821,1821 ! SSP 211905
1825 GOTO 6700
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 GOSUB 7400
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 SETERR 9000; X$="",Q$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF INT(C0)=3 THEN GOSUB 7300 ELSE IF C0>3 OR X0<0 THEN GOTO 1800
2038 IF X0<LEN(V7$) THEN V7$(X0+1,1)=""
2040 ON X0 GOTO 2200,2250,3400,2300,2350,2100,2600,3350,3800,2950,2750,2850,2800,2900,3250,2400,2450,3300,3600,3150,3650,3700,3000,3050,3100,3450,3500,3550,2500,2550,3750,3850,1800
2100 REM "Receiving report
2110 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,18,10,33,8,C0,"","{3"+X$,"","IC2LAB10","PO7","PO2MAC",""; IF C0=6 THEN GOSUB 6400; C0=1
2140 GOTO 2020
2200 REM "P/O number
2205 IF Q0$<>"ICN" THEN GOTO 2020
2206 X$="For reports to print correctly, enter the PO number from the original order."
2210 CALL "ZZENTR","A",A{ALL},A$,X4$,X3$,18,3+V1,41,9,C0,"P/O","{3"+X$,"","IC2LAB80","PO1","PO2MAA",""; IF C0=6 THEN GOSUB 6400; C0=1
2240 GOTO 2020
2250 REM "P/O date
2255 IF Q0$<>"ICN" THEN GOTO 2020
2260 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,41,3+V1,50,6,C0,"","{3"+X$,"","IC2LAB82","","",""; IF C0=6 THEN GOSUB 6400; C0=1
2290 GOTO 2020
2300 REM "Vendor
2305 IF Q0$<>"ICN" THEN GOTO 2020
2310 CALL "ZZENTR","AU",A{ALL},A$,X4$,X3$,18,9,68,10,C0,"A/P","{3"+X$,"","IC2LAG09","AP4","AP2MAB",""; IF C0=6 THEN GOSUB 6400; C0=1
2320 DIM G$(LEN(G$)); FIND (Z[7],KEY=A$(68,10),DOM=2321)IOL=0370
2325 GOSUB 7500
2340 GOTO 2020
2350 REM "Plant
2355 IF POS(" "<>A$(68,10))=0 OR Q0$<>"ICN" THEN GOTO 2020
2360 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,30,9,78,4,C0,"","{3"+X$,"","IC2LAG11","FM0","",""; IF C0=6 THEN GOSUB 6400; C0=1
2364 IF POS(" "<>A$(78,4))=0 THEN GOTO 2390
2365 DIM D1$(LEN(D1$)); FIND (Z[4],KEY="P"+A$(68,10)+A$(78,4),DOM=2360)IOL=0345
2370 GOSUB 7500
2390 GOTO 2020
2400 REM "Starting number
2410 CALL "ZZENTR","R",A{ALL},A$,X4$,X3$,18,18,82,9,C0,"","{3"+X$,"","IC2LAB22","","",""
2415 IF C0>0 THEN GOSUB 8200
2440 GOTO 2020
2450 REM "Ending number
2460 CALL "ZZENTR","R",A{ALL},A$,X4$,X3$,18,19,91,9,C0,"","{3"+X$,"","IC2LAB24","","",""
2490 GOTO 2020
2500 REM "Total cost
2503 IF SBO$(69,1)="Y" THEN GOTO 2020; REM "WO113448
2508 X$="Enter total extended cost - F2 key to extend entered cost per M"
2510 CALL "ZZENTR","NF",A{ALL},A$,X4$,X3$,45,18,0,621,C0,M0$,"{3"+X$,"","IC2LAB26","","",""
2515 IF C0<>11 THEN GOTO 2520
2516 IF A$(125,4)="    " THEN N=A[4]*A[3] ELSE N=A[4]*A[2]*A[3]
2517 C0=1; CALL "FM2EXT",Z[13],0,"M   ",1000,N,A[0],E,2
2518 A[0]=E; PRINT @(45,18),A[0]:M0$,
2540 GOTO 2020
2550 REM "Total Freight
2560 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,45,19,1,621,C0,M0$,"{3"+X$,"","IC2LAB28","","",""
2590 GOTO 2020
2600 REM "Bin number
2610 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,18,11,100,10,C0,"","{3"+X$,"","IC2LAB30","","",""
2640 GOTO 2020
2650 REM "Order number
2655 GOTO 2020
2660 CALL "ZZENTR","A",A{ALL},A$,X4$,X3$,66,12,110,8,C0,"O/P","{3"+X$,"","IC2LAB32","FS1","FM2ODA",""; IF C0=6 THEN GOSUB 6400; C0=1
2670 IF V9<>-2 THEN FIND (Z[5],KEY=A$(110,8),DOM=2671)IOL=0350
2690 GOTO 2020
2700 REM "Line number on order
2710 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,76,12,118,3,C0,"","{3"+X$,"","IC2LAB34","FS2","FM2ODB",""; IF C0=6 THEN GOSUB 6400; C0=1
2720 IF V9<>-2 THEN FIND (Z[8],KEY=A$(118,3),DOM=2721)IOL=0360
2740 GOTO 2020
2750 REM "Primary unit
2755 J1=1; GOSUB 8500
2760 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,J0,15,121,4,C0,"","{3"+X$,"","IC2LAB36","","",""
2762 FIND (Z[13],KEY="U/M"+A$(121,4),DOM=2760)IOL=0375 ! IF G5$(20,1)="Y" THEN GOTO 2760
2763 IF C0>0 THEN GOSUB 8200; GOSUB 8350
2765 IF ABS(C0)>2 THEN J1=-1; GOSUB 8500
2790 GOTO 2020
2800 REM "Packaging unit
2803 IF A[2]=0 THEN GOTO 2840
2804 IF ABS(C0)>1 AND POS(" "<>A$(125,4))=0 THEN GOTO 2840
2805 J1=3; GOSUB 8500
2810 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,J0,15,125,4,C0,"","{3"+X$,"","IC2LAB38","","",""
2812 IF A$(125,4)<>"    " THEN FIND (Z[13],KEY="U/M"+A$(125,4),DOM=2810)IOL=0375; IF G5$(20,1)="Y" THEN GOTO 2810
2815 IF A$(125,4)="    " THEN IF A[3]=0 THEN A[3]=A[2],A[2]=0,F0$="*"; J1=-1; GOSUB 8500 ELSE A[2]=0,J1=-1; GOSUB 8500
2816 IF C0>0 THEN GOSUB 8200; GOSUB 8350
2820 IF ABS(C0)>2 THEN J1=-1; GOSUB 8500
2840 GOTO 2020
2850 REM "Units per package
2854 IF ABS(C0)=2 AND A[2]=0 THEN GOTO 2020
2855 J1=2; GOSUB 8500
2860 CALL "ZZENTR","N&",A{ALL},A$,X4$,X3$,J0,15,2,601,C0,M3$,"{3"+X$,"","IC2LAB40","","",""
2862 IF C0=4 THEN C0=1
2864 IF C0>0 AND A[2]=0 THEN A$(125,4)="",J1=-1; GOSUB 8500
2865 IF ABS(C0)>2 THEN J1=-1; GOSUB 8500
2875 IF C0>0 THEN GOSUB 8200; GOSUB 8350
2890 GOTO 2020
2900 REM "Units per primary/package unit
2904 IF F0$="*" THEN F0$=""; GOTO 2940
2905 J1=4; GOSUB 8500
2910 CALL "ZZENTR","N&",A{ALL},A$,X4$,X3$,J0,15,3,601,C0,M3$,"{3"+X$,"","IC2LAB42","","",""
2920 J1=-1; GOSUB 8500
2925 IF C0>0 THEN GOSUB 8200; GOSUB 8350
2940 GOTO 2020
2950 REM "Quantity
2955 J1=0; GOSUB 8500
2960 CALL "ZZENTR","NM&",A{ALL},A$,X4$,X3$,18,15,4,702,C0,M4$,"{3"+X$,"","IC2LAB44","","TPALAA",""; IF C0=6 THEN GOSUB 6400; C0=1
2962 IF ABS(C0)<>2 AND A[4]=0 THEN GOTO 2960
2965 IF ABS(C0)>1 THEN J1=-1; GOSUB 8500
2975 IF C0>0 THEN GOSUB 8200; GOSUB 8350
2990 GOTO 2020
3000 REM "Sales Price
3002 IF MID(CUST_DFT$,101,1)<>"Y" THEN GOTO 3010
3004 GOTO 2020
3005 IF SBO$(69,1)="Y" THEN GOTO 2020; REM "WO113448
3010 CALL "ZZENTR","N&",A{ALL},A$,X4$,X3$,48,12,5,621,C0,M0$,"{3"+X$,"","IC2LAB46","","",""
3040 GOTO 2020
3050 REM "U/M
3055 IF A[5]=0 THEN A$(129,4)="",A[6]=0; GOTO 3085
3057 IF SBO$(69,1)="Y" THEN GOTO 3085; REM "WO113448
3060 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,66,12,129,4,C0,"","{3"+X$,"","IC2LAB48","ZZPARM","",""; IF C0=6 THEN GOSUB 6400; C0=1
3065 IF A$(129,4)="    " AND A[5]<>0 THEN GOTO 3060
3070 IF A$(129,4)<>"    " THEN FIND (Z[13],KEY="U/M"+A$(129,4),DOM=3060)IOL=0376 ELSE A[6]=0; GOTO 3085
3080 IF G6$(21,1)="Y" THEN GOTO 3060 ELSE IF G6$(20,1)="Y" THEN IF G6[0]<>0 THEN A[6]=G6[0] ELSE A[6]=G6[1]
3085 PRINT @(66,12),A$(129,4),@(72,12),A[6]:M2$,
3090 GOTO 2020
3100 REM "Units per u/m
3105 IF A$(129,4)="    " OR G6$(20,1)="Y" THEN GOTO 2020
3107 IF SBO$(69,1)="Y" THEN GOTO 2020; REM "WO113448
3110 X$="Quantity per unit of measure"; CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,72,12,6,501,C0,M2$,"{3"+X$,"","IC2LAB50","","",""
3120 IF A[6]=0 THEN GOTO 3110
3140 GOTO 2020
3150 REM "Customer PO
3160 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,45,11,133,15,C0,"","{3"+X$,"","IC2LAB52","","",""
3190 GOTO 2020
3200 REM "Invoice number
3205 GOTO 2020
3210 CALL "ZZENTR","A",A{ALL},A$,X4$,X3$,71,15,148,7,C0,"AR6","{3"+X$,"","IC2LAB54","","",""
3240 GOTO 2020
3250 REM "SHORT CARTON
3260 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,18,16,155,1,C0,"","{3"+X$,"","IC2LAB67","","",""
3290 GOTO 2020
3300 REM "Comments
3310 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,18,20,156,40,C0,"","{3"+X$,"","IC2LAB48","","",""
3340 GOTO 2020
3350 REM "Item Rev date
3360 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,18,12,215,6,C0,"","{3"+X$,"","IC2LAB50","","",""
3390 GOTO 2020
3400 REM "Factory Job number
3405 IF Q0$<>"ICN" THEN GOTO 2020
3410 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,66,3+V1,56,12,C0,"","{3"+X$,"","IC2LAB84","","",""; IF C0=6 THEN GOSUB 6400; C0=1
3440 GOTO 2020
3450 REM "Standard Unit Cost
3455 IF P1$(64,1)<>"Y" THEN GOTO 2020
3457 IF SBO$(69,1)="Y" THEN GOTO 2020; REM "WO113448
3460 CALL "ZZENTR","N&",A{ALL},A$,X4$,X3$,48,13,7,621,C0,M0$,"{3"+X$,"","IC2LAB70","","",""
3490 GOTO 2020
3500 REM "Standard Cost Unit of Measure
3505 IF P1$(64,1)<>"Y" OR A[7]=0 THEN A$(256,4)="",A[8]=0; GOTO 3535
3510 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,66,13,256,4,C0,"","{3"+X$,"","IC2LAB72","ZZPARM","",""; IF C0=6 THEN GOSUB 6400; C0=1
3520 IF FNS$(A$(256,4))="" THEN GOTO 3510 ELSE FIND (Z[13],KEY="U/M"+A$(256,4),DOM=3510)IOL=0377
3525 IF G7$(21,1)="Y" THEN GOTO 3510 ELSE IF G7$(20,1)="Y" THEN IF G7[0]<>0 THEN A[8]=G7[0] ELSE A[8]=G7[1]
3535 PRINT @(66,13),A$(256,4),@(72,13),A[8]:M2$,
3540 GOTO 2020
3550 REM "Standard Cost Qty/UM
3555 IF P1$(64,1)<>"Y" OR FNS$(A$(256,4))="" OR G7$(20,1)="Y" THEN GOTO 2020
3560 X$="Quantity per Unit of Measure"; CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,72,13,8,501,C0,M2$,"{3"+X$,"","IC2LAB74","","",""
3570 IF A[8]=0 THEN GOTO 3560
3590 GOTO 2020
3600 REM "Storage Expiration Date"
3605 IF P1$(64,1)<>"Y" THEN GOTO 2020
3610 X$="Storage Expiration Date"; CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,66,10,250,6,C0,"","{3"+X$,"","IC2LAB76","","",""
3640 GOTO 2020
3650 REM "Product Code
3655 IF P1$(64,1)<>"Y" THEN GOTO 2020
3660 X$="Product Code"; CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,66,11,242,3,C0,"","{3"+X$,"","IC2LAB78","","",""
3670 IF FNS$(A$(242,3))>"" THEN CLOSE (14); OPEN (14)"FMP"+X3$(9,3); FIND (14,DOM=3660,KEY="T"+A$(242,3))
3680 CLOSE (14)
3690 GOTO 2020
3700 REM 
3705 IF P1$(64,1)<>"Y" THEN GOTO 2020
3710 X$="Commission Code"; CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,74,11,245,5,C0,"","{3"+X$,"","IC2LAB79","","",""
3720 IF FNS$(A$(245,5))>"" THEN CLOSE (14); OPEN (14)"AR4"+X3$(9,3); FIND (14,KEY=A$(245,5),DOM=3710)
3730 CLOSE (14)
3740 GOTO 2020
3750 REM " FMS Price - ssp# 157344                                              
3752 IF MID(P0$,306,1)<>"Y" THEN GOTO 3790
3753 IF H[29]<>0 THEN A[9]=H[29]; REM " DEFAULT TO FMS PRICE FROM FM1
3755 X$="This FMS Price will update to FMZ if entered here"
3760 CALL "ZZENTR","N&",A{ALL},A$,X4$,X3$,69,19,9,621,C0,M0$,"{3"+X$,"","IC2LAG28","","",""
3790 GOTO 2020
3800 REM "Salesperson ssp 186762
3801 IF A$(1,10)=DIM(10) THEN GOTO 3840; REM ONLY DEFAULT IF CUSTOMER OWNED
3802 IF A$(263,4)=DIM(4) THEN A$(263,4)=B$(278,4)
3803 READ (Z[26],KEY=A$(263,4),DOM=3810)
3804 IF SBO$(94,1)<>"Y" THEN GOTO 3840; REM " IF NOT ALLOW, THEN EXIT
3805 X$="You Must Enter a Valid Salesperson Code"
3810 CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,18,13,263,4,C0,"","{3"+X$,"","AR2MAB46","AR3","AR2MBB",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3810,3811
3815 READ (Z[26],KEY=A$(263,4),DOM=3810)
3840 GOTO 2020
3850 REM price class 190538
3855 X$="190538 Enter A Price Class for Lot Pricing"
3860 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,71,20,267,3,C0,"","{3"+X$,"","IC2LAA36","","",""
3870 IF A$(267,3)<>"   " THEN FIND (Z[25],KEY="I"+A$(267,3),DOM=3850)PRICE_CLASS$
3890 GOTO 2020
5800 REM "Alt sort keys
5815 IF Q0$<>"ICN" THEN FIND (Z[6],KEY=A$(41,9),DOM=5816)IOL=0360
5820 FIND (Z[7],KEY=A$(68,10),DOM=5821)IOL=0370
5825 FIND (Z[4],KEY="P"+A$(68,10)+A$(78,4),DOM=5826)IOL=0345
5840 FIND (Z[13],KEY="U/M"+A$(121,4),DOM=5841)IOL=0375
5845 FIND (Z[13],KEY="U/M"+A$(129,4),DOM=5846)IOL=0376
5850 FIND (Z[13],KEY="U/M"+A$(256,4),DOM=5851)IOL=0377
5890 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6050 PRINT @(6,3+V1),"P/O number:",@(31,3+V1),"P/O date:",@(53,3+V1),"Factory Job:",@(4,4+V1),"Vendor/Plant:",
6060 PRINT @(1,5+V2),"Customer number:",@(5,6+V2),"Item number:",@(3,7+V2),"Location code:",@(1,8+V2),"Sequence number:",; IF P1$(65,1)="Y" THEN PRINT @(31,8+V2),"Lot number:",
6075 PRINT @(0,9+V2),Z0$,@(0,10),"Receiving report:",@(31,10),"Receipt date:",@(6,11),"Bin number:",@(32,11),"Customer PO:",@(3,12),"Item Rev date:",; IF P1$(64,1)="Y" THEN PRINT @(61,10),"SED:",@(62,11),"PC:",@(70,11),"CC:",
6076 PRINT @(32,12),"Sales price:",@(61,12),"Per:",; IF P1$(64,1)="Y" THEN PRINT @(34,13),"Std. cost:",@(61,13),"Per:",
6077 PRINT @(5,13),"Salesperson:",; REM ssp 186762
6078 PRINT @(8,15),"Quantity:",@(38,14),"Left to distribute:",@(12,16),"Runt?",
6080 PRINT @(1,18),"Starting number:",@(39,18),"Cost:",@(3,19),"Ending number:",@(36,19),"Freight:",@(8,20),"Comments:",
6082 IF MID(P0$,306,1)="Y" THEN PRINT @(58,19),"FMS Price:",; REM " ssp# 157344  
6084 PRINT @(58,20),"Price Class:", ! 190538
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6205 REM "D9 controls returns for displays of pieces of the screen
6215 GOSUB 6450
6216 IF P1$(65,1)="Y" THEN PRINT @(43,8+V2),A$(234,8),
6217 IF Q0$="ICN" THEN GOSUB 8850
6218 PRINT @(45,10),FND$(A$(25,6)); IF P1$(64,1)="Y" THEN PRINT @(66,10),FND$(A$(250,6)),@(66,11),A$(242,3),@(74,11),A$(245,5),
6220 PRINT @(18,10),A$(33,8),@(18,11),A$(100,10),@(45,11),A$(133,15),@(18,12),A$(215,6),
6225 PRINT @(71,20),A$(267,3),
6237 IF D9=1 THEN D9=0; RETURN ; REM "P/O stuff
6240 DIM CUST_DFT$(243); READ (Z[25],KEY="D"+A$(1,10),DOM=*NEXT)CUST_DFT$; REM " SSP# 146930
6250 IF MID(CUST_DFT$,101,1)<>"Y" THEN GOTO 6256
6251 IF NUM(A$(21,4))>=9000 THEN GOTO 6256
6254 A[5]=0
6256 PRINT @(48,12),A[5]:M0$,@(66,12),A$(129,4),@(72,12),A[6]:M2$,
6257 PRINT @(18,13),A$(263,4),; REM "SSP186762
6258 IF P1$(64,1)="Y" THEN PRINT @(48,13),A[7]:M0$,@(66,13),A$(256,4),@(72,13),A[8]:M2$,
6280 PRINT @(18,16),A$(155,1),
6292 J1=-1; GOSUB 8500
6293 PRINT @(18,18),A$(82,9),@(45,18),A[0]:M0$,@(18,19),A$(91,9),@(45,19),A[1]:M0$,@(18,20),A$(156,40),
6294 FIND (Z[8],KEY=A$(1,20),DOM=*NEXT)IOL=0380
6295 IF MID(P0$,306,1)="Y" AND A[9]=0 THEN IF DIM(READ NUM(L))>0 AND L[25]<>0 THEN A[9]=L[25] ELSE A[9]=H[29]; REM " DEFAULT TO FMS PRICE FROM FM1, 307483-check PO2 first
6296 IF MID(P0$,306,1)="Y" THEN PRINT @(69,19),A[9]:M0$,; REM " SSP# 157344  
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 REM IF C9<0 THEN GOTO 06445
6460 IF Q0$<>"ICN" THEN GOSUB 8850
6463 CALL "ZZDISP","A   ",A$(1,10),"A/R",X3$,"","",18,5+V2,X4$
6464 PRINT @(31,5+V2),B$(11,30),
6468 CALL "ZZDISP","A   ",A$(11,10),"I/C",X3$,"","",18,6+V2,X4$
6472 PRINT @(31,6+V2),C$(21,40),@(18,7+V2),A$(21,4),
6474 GOSUB 7550; GOSUB 8100
6490 RETURN 
6500 REM "DELETE
6505 S1=S1-1
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6590 A1$=""; GOTO 1630
6600 REM "ALT KEY DATA READS
6605 FIND (Z[2],KEY=A$(1,10),DOM=6606)IOL=0320
6610 FIND (Z[3],KEY=A$(11,10),DOM=6611)IOL=0330
6615 FIND (Z[4],KEY=A$(21,4),DOM=6616)IOL=0340
6620 IF Q0$<>"ICN" THEN FIND (Z[6],KEY=A$(41,9),DOM=6621)IOL=0360
6625 READ (Z[7],KEY=A$(68,10),DOM=6626)IOL=0370
6690 RETURN 
6700 REM "ADD ENTRY
6703 IF Q0$="ICN" THEN A$(110,11)=Q2$
6704 IF A$(125,4)="    " THEN N0=A[4]*A[3] ELSE N0=A[4]*A[2]*A[3]
6705 GOSUB 8150; S0=-1; GOSUB 8300
6707 R9$=A$(33,8),B9$=A$(100,10); DIM B8$(45); B8$(1,15)=A$(133,15),B8$(16,4)=A$(129,4),B8$(20,11)=STR(A[5]),B8$(31,9)=STR(A[6]),B8$(40,6)=A$(215,6); IF POS(" "<>A$(91,9))<>0 THEN J0=NUM(A$(91,9),ERR=6709),N9$=STR(J0+1)
6708 IF LEN(N9$)<9 THEN N9$=" "+N9$; GOTO 6708
6709 REM "If not new write out in old position, else look for new one.
6710 IF Z9<>-1 THEN WRITE (Z[1],KEY=A1$)IOL=0310 ELSE GOSUB 7800
6720 IF LBL_PRT THEN GOSUB WMS_LABELS ! REM "CHECK FOR WMS AND PARAMETER SETTING TO PRINT LABELS WO 251758
6790 C9=0; IF Z=6 OR K1=0 THEN GOTO 9900 ELSE IF Z<4 THEN GOTO 1000 ELSE C0=-(3.1+(Z-4)/10); GOTO 6800
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 IF A1$="" THEN GOTO 6800 ELSE IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6825 REM "A$(1,LEN(A1$))=A1$
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM "PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 PRINT 'RB',; CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6865 GOTO 1100
6870 GOTO 6895
6890 GOTO 1515
6895 IF A1$="" THEN GOTO 6860 ELSE PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=6896)
6898 ON S9 GOTO 6810,1100
7200 REM "Forced read
7210 IF ERR<>0 THEN GOTO 9000
7220 CALL "ZZREAD",Z[1],A1$,A$,A{ALL},S8; ON S8 GOTO 7221,1630
7230 GOSUB 6200
7240 CALL "ZZPROM","E0",X3$,0,"","","",0; GOTO 7290
7250 CALL "ZZPROM","E0A",X3$,0,"","","",0; GOTO 7290
7290 GOTO 1630
7300 REM 
7310 ON FPT(C0)*10 GOTO 7390,7390,7330,7390 ! SSP#271291
7325 IF V8$="" THEN X0=X0+4; GOTO 7390 ELSE X0=NUM(V8$(POS(STR(X0:"00")<=V8$,2),2)); GOTO 7390
7330 IF V8$="" THEN X0=X0-6; GOTO 7335 ELSE X0=POS(STR(X0-1:"00")<=V8$,2)-2; IF X0<0 THEN X0=0; GOTO 7390 ELSE X0=NUM(V8$(X0,2)); GOTO 7390
7335 IF X0<0 THEN X0=0; GOTO 7390
7390 IF X0>0 THEN X0=X0-0
7395 C0=3; RETURN 
7400 REM "EXTRACT
7410 EXTRACT (Z[1],ERR=7200,KEY=A1$,DOM=7440)IOL=0310
7420 C9=1,Z9=1; GOSUB 5800
7440 RETURN 
7450 REM "EXTRACT NEW ENTRY
7455 IF A$(267,3)=DIM(3) THEN A$(267,3)=C$(109,3) ! 190538 default price class for lot
7460 WRITE (Z[1],KEY=A1$,DOM=7250)IOL=0310; PRINT @(71,20),A$(267,3); GOTO 7400
7490 RETURN 
7500 REM "Print Vendor/Plant
7505 DIM D1$(200); D1$(16,117)=G$(11,117)
7506 IF POS(" "<>A$(78,4))<>0 THEN FIND (Z[4],KEY="P"+A$(68,14),DOM=7507)IOL=0345
7509 L9$=FNS$(D1$(16,35))
7510 IF POS(" "<>D1$(111,16))<>0 THEN L9$=L9$+" "+FNS$(D1$(111,16))+", "+D1$(127,2)
7512 IF LEN(L9$)>45 THEN L9$=D1$(16,35)
7514 IF 20+LEN(J$)>31 THEN J0=20+LEN(J$) ELSE J0=31
7520 IF POS(Q0$="ICN",3)>0 THEN GOTO 7600
7540 PRINT @(J0,4+V1),'CL',L9$,
7545 RETURN 
7550 REM "Print location
7555 L9$=FNS$(D$(16,35))
7560 IF POS(" "<>D$(111,16))<>0 THEN L9$=L9$+" "+FNS$(D$(111,16))+", "+D$(127,2)
7562 IF LEN(L9$)>37 THEN L9$=D$(16,35)
7565 IF POS(Q0$="ICN",3)>0 THEN PRINT @(35,5),'CL',L9$,; GOTO 7595
7580 PRINT @(31,7+V1),'CL',L9$,
7595 RETURN 
7600 REM 
7610 IF LEN(L9$)>44 THEN L9$=L9$(1,44)
7620 PRINT @(35,9),'CL',L9$,
7630 RETURN 
7650 REM "Assign next Lot Number
7655 CALL "UPDICF;GET_NEXT_LOT_NUM",Z[13],X3$,NEXT_NUM$; A$(234,8)=NEXT_NUM$
7680 IF P1$(65,1)="Y" THEN PRINT @(43,8+V2),A$(234,8),
7690 RETURN 
7700 REM "Default bin number,if necessary
7703 IF POS(" "<>A$(100,10))<>0 THEN GOTO 7745
7705 DIM I9$(50)
7710 FIND (Z[12],KEY=A$(1,24),DOM=7711)IOL=0425
7715 A$(100,10)=I9$(35,10)
7718 FIND (Z[8],KEY=A$(1,20),DOM=7719)IOL=0380; IF POS(""<>A$(215,6))=0 THEN A$(215,6)=H$(167,6)
7720 PRINT @(18,11),A$(100,10),@(18,12),A$(215,6),
7745 RETURN 
7800 REM "Write out new transaction ensuring no duplicates
7802 PRINT @(0,22),'CL','RB',"In IC2LAB at 7800 routine!!!",; INPUT *,
7804 K1=0
7810 WRITE (Z[1],KEY=A1$,DOM=7811)IOL=0310; GOTO 7845
7815 K1=K1+1,A1$(24,2)=FNQ$(K1),A$(31,2)=A1$(24,2)
7820 GOTO 7810
7845 RETURN 
7900 REM "Default based on Q1$ and display
7901 FIND (Z[2],KEY=A$(1,10),DOM=7902)IOL=0320
7904 IF Q1$="" OR Q0$="ICN" THEN RETURN 
7905 A$(41,9)=Q1$(1,9)
7910 IF Q0$<>"ICN" THEN READ (Z[6],KEY=A$(41,9),DOM=7911)IOL=0360
7912 IF MID(F$,361,1)="Y" THEN A[10]=1; REM SSP 198988 Customer Supplier Vendor - need to add to POH to be added to ICF in PO Receiving Update No room in string...1 = YES
7915 A$(50,6)=F$(192,6),A$(68,14)=F$(7,14),A$(56,12)=F$(248,12)
7920 DIM G$(100); READ (Z[7],KEY=A$(68,10),DOM=7921)IOL=0370
7925 DIM L$(170),L[27]; READ (Z[10],KEY=A$(41,9)+Q1$(20,3),DOM=7926)IOL=0400
7927 IF POS(" "<>L$(119,12))<>0 THEN A$(56,12)=L$(119,12)
7929 GOSUB 8600
7930 IF B8$<>"" THEN GOTO 7934 ELSE DIM E$(168),E[7]; READ (Z[5],KEY=E0$,DOM=7931)IOL=0350
7932 IF F$(173,1)="S" THEN GOSUB 8650; IF L[21]<>0 THEN A$(129,4)=L$(146,4),A[5]=L[21],A[6]=L[20]
7933 A$(148,7)=E$(40,7),A$(133,15)=F$(328,15); IF L[21]=0 AND E[1]<>0 THEN A$(129,4)=E$(101,4),A[5]=E[1],A[6]=E[2]
7934 A$(1,20)=L$(91,20)
7936 A$(256,4)=L$(164,4),A[7]=L[22],A[8]=L[23],A$(242,3)=L$(156,3),A$(245,5)=L$(159,5),A$(250,6)=L$(150,6)
7945 FIND (Z[2],KEY=A$(1,10),DOM=7946)IOL=0320
7950 FIND (Z[3],KEY=A$(1,20),DOM=7951)IOL=0330
7960 GOSUB 8000
7965 REM "Wait till here to do first background so check can be made
7966 IF F2$<>"*" THEN CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,0; GOSUB 6000; F2$="*"
7990 GOSUB 6456; GOSUB 6200
7992 IF F$(173,1)="S" AND L[21]=0 AND E[1]<>0 THEN CALL "ZZPROM",".4",X3$,0,"Lot selling price defaulted from Item Master","","",0
7995 RETURN 
8000 REM "Setup from PO7; only do once per receipt, F0 is used to flag this
8001 REM "R6$ is for loc entry, R5$=listof location#(from PO3)
8002 REM "R4(),R5(),R6(),R7() match R5$ list, Only room for 25 locations setup now, see line 8013 to change
8003 REM "R4=qty lotted so far,R5=qty, R6=extension, R7=freight****> being ignored until freight by line implemented;see lines 8040,8070,8315,8365
8004 REM "R$ will be the master PO7 record for this line.
8005 IF F0=1 THEN A$(25,6)=R$(39,6); RETURN 
8008 REM "Read base PO7 record, if special ship it will pos file for other reads, for general ship, extract so it will be picked up @ 8015
8009 DIM R$(61),R[6]
8010 IF L$(14,1)="Y" THEN READ (Z[9],KEY=Q1$(1,22)+" ",DOM=8011)IOL=0410 ELSE EXTRACT (Z[9],KEY=Q1$(1,22)+" ",DOM=8011)IOL=0410
8012 A$(25,6)=R$(39,6)
8013 DIM R0$(61),R0[6],R4[25],R5[25],R6[25],R7[25]; R5$="",R6$="Select location from:"
8014 REM "Read thru PO7 for lines that match to add to R5$ and R5
8015 READ (Z[9],END=8050)IOL=0415
8020 IF R0$(1,22)<>Q1$(1,22) THEN GOTO 8050
8024 REM "Read PO3 record, if not D location then, skip; read for s/o line, p/o line, s/o general, p/o general
8025 READ (Z[11],KEY=R0$(1,8)+" "+R0$(20,4),DOM=8026)IOL=0420; GOTO 8030
8026 READ (Z[11],KEY=R0$(1,9)+R0$(20,4),DOM=8027)IOL=0420; GOTO 8030
8027 READ (Z[11],KEY=R0$(1,8)+" "+"   "+R0$(23,1),DOM=8028)IOL=0420; GOTO 8030
8028 READ (Z[11],KEY=R0$(1,9)+"   "+R0$(23,1),DOM=8015)IOL=0420
8030 IF S9$(11,1)<>"D" THEN GOTO 8015
8035 R5$=R5$+S9$(12,4),R6$=R6$+" "+S9$(12,4)
8040 I0=INT(LEN(R5$)/4),R5[I0]=R0[0],R6[I0]=R0[2]; REM ",R7(I0)=NUM(R0$(51,11),ERR=08041)
8045 GOTO 8015
8049 REM "Now read thru POH and add any existing records
8050 IF R5$="" THEN EXITTO 9905
8051 READ (Z[1],KEY=Q1$(1,9)+Q1$(20,3),DOM=8052)
8054 DIM A0$(269),A0[12]; F1$=""
8055 X$=KEY(Z[1],END=8090); READ (Z[1],KEY=X$)IOL=0315
8058 IF X$(1,12)<>Q1$(1,9)+Q1$(20,3) THEN GOTO 8090
8064 F1$="*"
8065 I0=INT(POS(A0$(21,4)=R5$,4)/4)+1
8066 R9$=A0$(33,8),B9$=A0$(100,10); DIM B8$(45); B8$(1,15)=A0$(133,15),B8$(16,4)=A0$(129,4),B8$(20,11)=STR(A0[5]),B8$(31,9)=STR(A0[6]),B8$(40,6)=A$(215,6); IF POS(" "<>A0$(91,9))<>0 THEN J0=NUM(A0$(91,9),ERR=8067),N9$=STR(J0+1)
8068 S1=NUM(A0$(31,2),ERR=8068)
8069 IF A0$(125,4)="    " THEN N0=A0[4]*A0[3] ELSE N0=A0[4]*A0[2]*A0[3]
8070 R4[I0]=R4[I0]+N0,R6[I0]=R6[I0]-A0[0]; REM ",R7(I0)=R7(I0)-A0(1)
8075 GOTO 8054
8090 F0=1
8091 IF F1$="*" THEN F1$="" ELSE GOTO 8094
8092 IF LEN(N9$)<9 THEN N9$=" "+N9$; GOTO 8092
8093 PRINT @(0,22),R9$,; INPUT *; S1=S1+1,A$(31,2)=STR(S1:"00"),A$(33,8)=R9$,A$(100,10)=B9$,A$(82,9)=N9$; A$(133,15)=B8$(1,15),A$(129,4)=B8$(16,4),A[5]=NUM(B8$(20,11)),A[6]=NUM(B8$(31,11)),A$(215,6)=B8$(40,6)
8095 RETURN 
8100 REM "Display the amount remaining
8102 IF Q0$="ICN" THEN RETURN 
8105 I0=INT(POS(A$(21,4)=R5$,4)/4)+1
8110 IF R5[I0]-R4[I0]<>0 THEN PRINT @(69,14),R5[I0]-R4[I0]:M4$, ! SSP249318 jdf
8145 RETURN 
8150 REM "Add amount N0 To R4$ bucket
8153 IF Q0$="ICN" THEN Q0=Q0+N0; RETURN 
8155 I0=INT(POS(A$(21,4)=R5$,4)/4)+1
8160 R4[I0]=R4[I0]+N0
8195 RETURN 
8200 REM "Setup ending number based on starting number
8205 IF POS(" "<>A$(82,9))=0 THEN GOTO 8245
8210 J0=NUM(A$(82,9),ERR=8245)
8215 IF A$(125,4)="    " THEN N0=A[4]*A[3] ELSE N0=A[4]*A[2]*A[3]
8220 J0=J0+N0-1
8230 J$=STR(J0)
8235 IF LEN(J$)<9 THEN J$=" "+J$; GOTO 8235
8238 A$(91,9)=J$
8240 PRINT @(18,19),A$(91,9),
8245 RETURN 
8300 REM "Update cost and freight buckets
8301 REM "S0 is sign to use 1 or -1
8302 IF Q0$="ICN" THEN Q1=Q1-S0*A[0]; IF UID="jvv" THEN ESCAPE ; RETURN ELSE RETURN ; REM 'YES IT IS Q1-S0
8305 I0=INT(POS(A$(21,4)=R5$,4)/4)+1
8310 R6[I0]=R6[I0]+S0*A[0]
8315 REM LET R7(I0)=R7(I0)+S0*A(1)
8345 RETURN 
8350 REM "Calculate cost and freight
8353 IF Q0$="ICN" THEN RETURN 
8355 I0=INT(POS(A$(21,4)=R5$,4)/4)+1
8357 IF A$(125,4)="    " THEN N0=A[4]*A[3] ELSE N0=A[4]*A[2]*A[3]
8360 IF R5[I0]-R4[I0]=0 THEN A[0]=0 ELSE A[0]=R6[I0]*N0/(R5[I0]-R4[I0])
8365 REM IF R5(I0)-R4(I0)=0 A(1)=0 ELSE LET A(1)=R7(I0)*N0/(R5(I0)-R4(I0))
8370 PRINT @(45,18),A[0]:M0$,@(45,19),A[1]:M0$,
8395 RETURN 
8400 REM "Check if qty, cost, and freight are fully extended
8401 IF Q0$="ICN" THEN RETURN 
8405 X$="",I0=INT(LEN(R5$)/4)
8409 REM "See if any non-zero amounts are left to distribute
8410 FOR I=1 TO I0
8412 IF R5[I]-R4[I]<>0 OR (R6[I]<>0 AND L$(77,4)<>"LOT") OR R7[I]<>0 THEN EXITTO 8420
8414 NEXT I
8415 RETURN ; REM "if still here then all are zero, we're ok
8420 PRINT @(0,17-I0),'CE',@(0,18-I0),"Location   Remaining:",@(24),"Quantity",@(39),"Cost",@(47),"Freight",
8425 FOR I=1 TO I0
8430 PRINT @(2,18-I0+I),R5$(I*4-3,4),@(23),R5[I]-R4[I]:M4$,@(34),R6[I]:M0$,@(45),R7[I]:M0$,
8435 NEXT I
8440 CALL "ZZPROM",".4",X3$,Z,"There are amounts left to be distributed","","",0
8442 PRINT @(0,18-I0),'CE',; GOSUB 6000
8445 EXITTO 1100
8446 RETURN 
8500 REM "Display Qty and set J0 to xpos of input of field J1
8501 REM "J1 -1=display, 0=qty, 1=primary unit, 2=units/package, 3=package, 4=unit/primary/package
8505 J0$="",J0=0; IF A[4]=0 THEN PRINT @(18,15),'CL',; RETURN 
8510 IF J1=0 THEN J0$=STR(A[4]:M4$) ELSE J0$=FNR$(STR(A[4]:M4$))
8512 IF J1=1 THEN J0=18+LEN(J0$)
8515 IF POS(" "<>A$(121,4))<>0 THEN IF A[4]>1 THEN J0$=J0$+FNS$(A$(121,4))+"s of " ELSE J0$=J0$+FNS$(A$(121,4))+" of "
8517 IF J1=2 THEN J0=18+LEN(J0$); IF A[3]<>0 AND POS(" "<>A$(125,4))=0 THEN J0$=J0$+S0$(1,LEN(M3$))+" of "
8520 IF A[2]<>0 THEN IF J1=2 THEN J0$=J0$+STR(A[2]:M3$) ELSE J0$=J0$+FNR$(STR(A[2]:M3$))
8522 IF J1=3 THEN J0=18+LEN(J0$); IF A[3]<>0 AND POS(" "<>A$(125,4))=0 THEN J0$=J0$+"     of "
8525 IF POS(" "<>A$(125,4))<>0 THEN IF A[2]>1 THEN J0$=J0$+FNS$(A$(125,4))+"s of " ELSE J0$=J0$+FNS$(A$(125,4))+" of "
8527 IF J1=4 THEN J0=18+LEN(J0$)
8530 IF J1<>4 AND A[3]<>0 THEN J0$=J0$+FNR$(STR(A[3]:M3$))
8535 IF A$(125,4)="    " THEN Q=A[4]*A[3] ELSE Q=A[4]*A[2]*A[3]
8540 IF Q<>0 THEN PRINT @(18,15),J0$,S0$(1,51-LEN(J0$)),Q:M4$, ELSE PRINT @(18,15),J0$,S0$(1,51-LEN(J0$)), ! SSP249318 jdf
8545 RETURN 
8600 REM "Setup key E0$ for FS6 read
8602 IF F$(173,1)="S" THEN L$(11,3)=L$(7,3)
8605 E0$=F$(179,10)
8606 REM "This is here because L$(10,1) is set to "I" at line 8240 in FM2ODZ, because a D special shipping record was detected.
8607 IF POS(" "<>L$(91,10))<>0 THEN E0$=E0$+"B"; GOTO 8620
8610 ON POS(L$(10,1)="CXINS") GOTO 8620,8611,8612,8613,8614,8615
8611 E0$=E0$+"B"; GOTO 8620
8612 E0$=E0$+"D"; GOTO 8620
8613 E0$=E0$+"F"; GOTO 8620
8614 E0$=E0$+"H"; GOTO 8620
8615 E0$=E0$+"J"; GOTO 8620
8620 E0$=E0$+L$(101,10)+L$(82,8)+CHR(NUM(L$(11,3))+48)
8640 RETURN 
8650 REM "Ship and bill - stock p/o
8670 FIND (Z[8],KEY=L$(91,20),DOM=8690)IOL=0380
8680 IF H[3]<>0 THEN E$(101,4)=H$(100,4),E[1]=H[3],E[2]=H[4]
8690 RETURN 
8800 REM "Total up any exist records on the same day that belong to this line
8805 DIM A$(269),A[12]
8810 READ (Z[1],KEY=Q1$(1,30),DOM=8811)
8815 READ (Z[1],END=8840)IOL=0310
8819 IF A$(110,11)<>Q2$ THEN GOTO 8815
8825 IF A$(125,4)="    " THEN N0=A[4]*A[3] ELSE N0=A[4]*A[2]*A[3]
8830 Q0=Q0+N0,Q1=Q1+A[0],S1=NUM(A$(31,2),ERR=8831)
8835 GOTO 8815
8845 RETURN 
8850 REM "PRINT P/O AND VENDOR STUFF
8855 CALL "ZZDISP","A",A$(41,9),"P/O",X3$,"","",18,3+V1,X4$
8860 PRINT @(41,3+V1),FND$(A$(50,6)),@(66,3+V1),A$(56,12),
8865 CALL "ZZDISP","A",A$(68,10),"A/P",X3$,J$,"",18,4+V1,X4$
8875 PRINT @(30,4+V1),A$(78,4),; GOSUB 7500
8895 RETURN 
8905 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8920 DEF FNB$(Z9$)=X3$(214-6*NUM(Z9$),1)
8925 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
8930 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
8935 DEF FNQ$(Z9)=CHR(32+INT(Z9/95))+CHR(32+MOD(Z9,95))
8949 DEF FNC$(Z9$)=Z9$(1,POS(" "=Z9$+" ")-1)
8950 REM "Program flow logic
8951 REM "Q1$:(1,9)=P/O#, (10,10)=A/P INV#, (20,3)=P/O LINE#
8958 REM LET Q1$="00006914 6914      001"
8960 DIM U0$(6); U0$(1)=FID(0),U0$=U0$+"PF"; CLOSE (14); OPEN (14)"ZZPARM"
8970 FIND (14,KEY=U0$,DOM=8990)U0$,U1$
8975 IF U1$="" THEN Q1$=""; GOTO 8990
8980 Q1=NUM(U1$(7,2)),Q1$=U1$(9,Q1),U1$=U1$(Q1+9)
8990 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9120 ON Y8 GOTO 9900,0990,9920
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9730 RUN U1$(1,6)
9740 RETURN 
9750 REM "FILES
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9790 RETURN 
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9904 GOSUB 8400
9905 Y8=2
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; IF Q0$="ICN" THEN Q0$=STR(Q0),Q1$=STR(Q1) ELSE Q1$=A1$
9935 EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
11000 CREATE_STORAGE_EXPIRED:! SSP 200130
11005 IF LEN(L$)=0 THEN RETURN ! ssp 203785
11010 O_D$=A$(25,6); CALL "ZZWDTE;DISPLAY",O_D$,OUT_D$
11015 IF X3$(48,1)="1" THEN OUT$="%Mz/%Dz/%Yl",MMM=1,DDD=4,YYY=7 ELSE IF X3$(48,1)="2" THEN OUT$="%Dz/%Mz/%Yl",MMM=4,DDD=1,YYY=7 ELSE MMM=5,DDD=7,YYY=1,OUT$="%Yl/%Mz/%Dz"
11020 MM=NUM(MID(O_D$,MMM,2)),DD=NUM(MID(O_D$,DDD,2)),YY=NUM(MID(O_D$,YYY,4))
11025 IF POS(X3$(9,3)="139")<>0 AND L[24]=0 THEN L[24]=12 ! SSP#210491
11030 FOR AA=1 TO L(24)
11040 MM+=1; IF MM=13 THEN MM=1,YY+=1
11050 NEXT 
11055 IF MM=2 AND DD>=29 THEN DD=28
11060 ML_STORAGE_EXPIRES$=DTE(JUL(YY,MM,DD,ERR=BAD_DATE):OUT$,ERR=BAD_DATE)
11070 CALL "ZZWDTE;VALIDATE",ML_STORAGE_EXPIRES$,JJ$,JJJ$
11075 A$(250,6)=ML_STORAGE_EXPIRES$
11077 PRINT @(66,10),FND$(A$(250,6))
11080 RETURN 
11090 BAD_DATE: DD=DD-1; GOTO 11060
12000 WMS_LABELS:! WO251758
12010 CALL "ZZPROM","WL",X3$,ZW,"","","V",X0; ON ZW GOTO 12080,12020 ! SSP 211905
12030 WM1$=A$,WM2$=STR(A[3]),WM3$=B$(11,30)
12050 PROCESS "RFGRFQ","../RFG/RF.EN",X3$,X4$,WM1$,WM2$,WM3$
12090 RETURN 
12099 ! 
56000 REM "190538-Item price class pricing by bin identifier.                 
56001 REM "198988-Manage Customer Supplied Inventory. Item Setup, Purch ord  
56002 ! "200130-Storage Expiration Date (SED) needs to be entered at the    
56010 ! "203785-Trying to enter an order going back to inventory but she is 
56011 ! "       getting negative value"
56015 REM "211905-Add F11 - View Images to Inventory Lot Receiving Screen to  
56016 REM "251758-How to handle a negative release with WMS Labeling          
56017 REM "271291-During PO receiving, lot receiving, after entering in the   
56018 REM "272902-GUI Returns - if multiple lots are setup during a return,   
56019 REM "307483-DBD-457:Support for IMS price in purchase order line        
