0010 REM "Inventory Detail Transaction <IC2RTA>
0035 REM "5.7 - 04/25/13 - 16.165833 - tma - SSP# 262816
0037 REM "262816-Program ICGRTS Inventory Detail Transaction - fields let    
0040 REM "Copyright 2013 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 SETERR 9000; SETESC 9300
0110 X0$="IC2RTA",X1$="Inventory Detail Transaction"
0120 DIM A$(73),A[2],B$(397),B[15],C$(139),C[20],D$(224),S$(10),P0$(20),H$(224)
0130 TTOTAL=0,RTOTAL=0,ATOTAL=0,STOTAL=0,GTTOTAL=0,GRTOTAL=0,GATOTAL=0,GSTOTAL=0
0140 Z0$="-##,###,###.00",Z1$="##,##0",Z2$="$##,###,###.00-"
0210 T=1,T0=0,T1=2
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR IC8 - I/C Detail Transactions
0310 IOLIST A$,A[0],A[1],A[2]
0315 REM "IOLIST FOR AR1 - Customer Masterfile
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15]
0325 REM "IOLIST FOR IC0 - Item Masterfile
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20]
0335 REM "IOLIST FOR FM0 - Warehouse Locations
0340 IOLIST D$
0390 IOLIST I$,I[0],I[1]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0507 REM "Slot 6 will be used for tmp file if sequence 2, WO103249, now using ICP semi-permanent file
0510 Y$="01O IC8...  02O AR1...  03O IC0...  04O FM0...  05O AP4...  06L ICP...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0525 IF V0=2 THEN GOSUB 7800; REM "Create temp sort file for sequence 2
0700 REM "Open Printer
0720 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0721,9920
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[6]; U=38,U0=6,U[0]=0,U[1]=10,U[2]=10,U[3]=4,U[4]=4,U[5]=2,U[6]=6,T0=3,V1=20,V2=4,V3=6,U0$="    ",T5$="AI/CS   S       ",U5$="Item",U6$="Location",U7$="Fiscal Year"; GOTO 0850
0830 DIM U[6]; U=38,U0=6,U[0]=0,U[1]=4,U[2]=10,U[3]=10,U[4]=4,U[5]=2,U[6]=6,T0=3,V1=4,V2=20,V3=6,U0$="    ",T5$="S   AI/CS       ",U5$="Location",U6$="Item",U7$="Fiscal Year"; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0890 IF V0=2 THEN GOSUB 7850; REM "Write temp sort file
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0931 ESCAPE 
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])>=V2$(U3,U[U9]) THEN GOTO 1030 ELSE EXTRACT (U1,KEY=U$(1,U2-1)+V2$(U3,U[U9]),DOM=1021)
1025 EXITTO 1000
1030 IF U$(U2,U[U9])<=V2$(U3+U[U9],U[U9]) THEN GOTO 1040 ELSE IF U9<>6 THEN READ (U1,KEY=U$(1,U2-1)+$FF$,DOM=1032) ELSE IF U9=6 THEN READ (U1,KEY=U$(1,U2-1)+"z91231",DOM=1032) ! SSP#210899
1035 EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1130 U$=U$(5,20)+U$(1,4)+U$(25,14); READ (U1)
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1180 IF POS(" "<>V0$(72,7))>0 THEN IF POS(A$(39,1)=V0$(72,7))=0 THEN GOTO 1000
1300 REM "Read other data
1315 IF POS(" "<>A$(40,10))=0 THEN B0$=""; GOTO 1321 ELSE IF POS(A$(39,1)="IS")=0 THEN F0=Z[5],B0$="A/P" ELSE F0=Z[2],B0$="A/R"
1320 DIM B$(LEN(B$)); FIND (F0,DOM=1321,KEY=A$(40,10))IOL=0320
1330 DIM C$(LEN(C$)); FIND (Z[3],DOM=1331,KEY=A$(1,20))IOL=0330
1331 DIM J$(46); FIND (Z[2],KEY=C$(1,10),DOM=1332)J$
1335 DIM I$(20),I[1]; FIND (Z[13],DOM=1336,KEY="U/M"+C$(124,4))IOL=0390
1338 DIM H$(LEN(D$)); H$=D$
1340 DIM D$(LEN(D$)); FIND (Z[4],DOM=1341,KEY="D"+S$(1,10)+A$(21,4))IOL=0340
1400 REM "Subtotals"
1415 GOSUB 3100
1420 ON T GOTO 1470,1430,1440,1450,1460
1430 IF V0=1 THEN GOSUB 6200
1440 IF V0=1 THEN GOSUB 6300 ELSE IF V0=2 THEN GOSUB 6200
1450 GOSUB 6400
1490 T=0
1500 REM "Print Data
1505 GOSUB 7600
1510 IF W+2>W0 THEN GOSUB 6000
1515 Y5$(7)=FND$(A$(31,6))
1517 Y5$(20)=A$(39,1); GOSUB 7500
1520 Y5$(38)=A$(50,15),Y5$(61)=A$(67,4)+"."+A$(71,2),Y5$(76)=A$(65,2),Y5$(96)=FNQ$(A[0],Z0$),Y5$(118)=STR(A[1]:Z0$)
1525 GOSUB 7300
1530 IF B0$="" THEN GOTO 1550 ELSE IF B0$="A/P" THEN Y5$(23)="Vendor:" ELSE Y5$(22)="Customer:"
1535 CALL "ZZDISP","AX",A$(40,10),B0$,X3$,X$,"",0,0,X4$
1540 Y5$(31)=X$+"  "+B$(11,35)
1545 GOSUB 7300
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+A[0],T[T0,2]=T[T0,2]+A[1]
1920 IF A$(39,1)="T" THEN TTOTAL=TTOTAL+A[1]
1925 IF A$(39,1)="R" THEN RTOTAL=RTOTAL+A[1]
1930 IF A$(39,1)="A" THEN ATOTAL=ATOTAL+A[1]
1935 IF A$(39,1)="S" THEN STOTAL=STOTAL+A[1]
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,X$,"",0,0,X4$; T6$=X$ ! SSP#262816
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; GOSUB 7000
3155 IF U0$(1,1)="Y" THEN GOSUB 6000
3160 U1$=U9$(1,V1)
3180 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$,T6$=U2$; GOSUB 3050; GOSUB 7000
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3260 U2$=U9$(V5,V2)
3280 RETURN 
3300 REM "Next to lowest order break"
3310 IF V3=0 THEN RETURN ELSE IF U3$=U9$(V6,V3) THEN GOSUB 3400; RETURN 
3320 GOSUB 3420
3330 IF U3$="" THEN GOTO 3360
3350 T=3,T$=U7$,T$=T$+" "+U3$(1,4)+" period "+U3$(5,2),T6$=""; GOSUB 3050; GOSUB 7000
3355 IF U0$(3,1)="Y" THEN GOSUB 6000
3360 U3$=U9$(V6,V3)
3380 RETURN 
3480 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; GOSUB 7000
5200 REM "
5290 GOTO 9900
6000 REM "Page header
6010 IF V0=2 AND T0$<>"END" AND W2$="" THEN W2$=D$(12,4)+" "+FNS$(D$(16,35)); IF POS(" "<>D$(111,16))<>0 THEN W2$=W2$+" - "+FNS$(D$(111,16))+", "+D$(127,2); GOTO 6021
6020 IF V0=2 AND T0$<>"END" AND W2$<>"" THEN W2$=H$(12,4)+" "+FNS$(H$(16,35)); IF POS(" "<>H$(111,16))<>0 THEN W2$=W2$+" - "+FNS$(H$(111,16))+", "+H$(127,2)
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W4$,W5$
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6115 Y5$(7)="--- Transaction ---",Y5$(62)="Audit",Y5$(74)="Source"
6120 GOSUB 7300
6125 Y5$(9)="date",Y5$(20)="code",Y5$(38)="Reference",Y5$(61)="Control",Y5$(73)="Journal",Y5$(102)="Quantity",Y5$(126)="Amount"
6199 RETURN 
6200 REM "Break #1 header
6205 IF W+2>W0 THEN GOSUB 6000
6206 GOSUB 7300
6207 IF POS(" "=J$(1,10))<>0 THEN GOTO 6220
6210 CALL "ZZDISP","AX",J$(1,10),"A/R",X3$,X$,"",0,0,X4$; Y5$(2)=X$,Y5$(15)=J$(11,35)
6213 GOSUB 7300
6220 CALL "ZZDISP","AX",C$(1,20),"I/C",X3$,X$,"",0,0,X4$
6225 H8$=C$; GOSUB 7750
6230 H7$=FNS$(C$(124,4))+"/"+FNR$(STR(C[15]:Z1$))
6250 Y5$(1)=X$+"  "+H9$
6255 Y5$(78)="  "+H7$
6280 GOSUB 7300
6299 RETURN 
6300 REM "Break #2 header
6310 IF W+2>W0 THEN GOSUB 6000
6320 Y5$(3)=A$(21,4)
6321 D0$=FNS$(D$(16,35)); IF POS(" "<>D$(111,16))<>0 THEN D0$=D0$+" - "+FNS$(D$(111,16))+", "+D$(127,2)
6324 Y5$(9)=D0$
6325 GOSUB 7300
6399 RETURN 
6400 REM "Break #3 header
6410 IF W+2>W0 THEN GOSUB 6000
6420 IF P0$(9,4)<>A$(25,4) THEN DIM P0$(100); FIND (Z[13],KEY=X3$(9,3)+"G/LYE"+A$(25,4),DOM=6421)P0$
6425 G0$="   ending "+FND$(P0$(NUM(A$(29,2))*6+15,6))
6450 Y5$(5)="Fiscal year "+A$(25,4)+" period "+A$(29,2)+G0$
6455 GOSUB 7300
6499 RETURN 
6900 REM "SORT
6905 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6940 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,X9
6990 RETURN 
7000 REM "Totals logic
7001 IF T=T0 AND T[T0,0]<2 THEN GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7004 IF T<>0 THEN Y5$(96)=T1$(1,14)
7005 Y5$(118)=T1$(1,14); GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" "+Q2$+" ("+STR(T[T,0]:"0")+"):"
7017 IF T<>0 THEN Y5$(96)=FNQ$(T[T,1],Z0$)
7020 Y5$(118)=STR(T[T,2]:Z0$); GOSUB 7300
7025 IF V0=2 AND T=1 THEN GOSUB 8000
7065 IF T0$="END" AND T=0 THEN Y5$(118)=T2$(1,14); GOSUB 7300
7070 IF V0=2 AND T0$="END" AND T=0 THEN GOSUB 8100
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600),T4=MOD(T3,60),T3=INT(T3/60)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")+":"+STR(T4:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 W=W+1
7330 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"    ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "Print trans-type desc
7505 ON POS(A$(39,1)="TCRAPISKW") GOTO 7535,7510,7515,7520,7525,7530,7531,7532,7533,7525
7510 Y5$(21)="-Transfer"; GOTO 7535
7515 Y5$(21)="-Commit"; GOTO 7535
7520 Y5$(21)="-Receipt"; GOTO 7535
7525 Y5$(21)="-Adjust"; GOTO 7535
7530 Y5$(21)="-P/O"; GOTO 7535
7531 Y5$(21)="-Issue"; GOTO 7535
7532 Y5$(21)="-Sale"; GOTO 7535
7533 Y5$(21)="-Kit Prod"; GOTO 7535
7540 RETURN 
7600 REM "Convert SKU's in A(0) to proper units
7610 IF I$(20,1)="Y" THEN A[0]=A[0]/I[1] ELSE A[0]=A[0]/C[15]
7640 RETURN 
7750 REM "Build desc from H8$
7755 IF POS("D"=V0$(59,12))>0 THEN H9$=H8$(21,40); RETURN 
7760 H9$=FNS$(H8$(64,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+"x"
7770 H9$=H9$+FNS$(H8$(72,8)); IF H9$<>"" THEN IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
7775 IF NUM(H8$(80,2))<>0 THEN H9$=H9$+H8$(80,2)+" Ply "
7780 H9$=H9$+FNS$(H8$(21,40)); H8$=""
7790 RETURN 
7800 REM "Create temp sort by location for sequence 2
7805 K9$="I/C Detail Transactions by Location"
7806 GOTO 7826; REM "WO103249, now using ICP file
7810 CALL "ZZ2SRT",X3$,X4$,Z[13],Z[1],38,K9$,Z,"  "; ON Z GOTO 7811,9900
7820 Y$="06O "+K9$
7825 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
7830 U1=Z[6],V1$="ICP"+X3$(9,3),J1$=K9$
7845 RETURN 
7850 REM "Write temp sort file, WO103249, now using ICP file
7851 GOSUB 7900; ON COMMAND GOTO 7852,7895
7852 IF NOT(%GUI) THEN PRINT @(7,20),"Now re-sorting data for report:", ELSE PRINT 'MESSAGE'("Now re-sorting data for report",0),
7854 CALL "ZZINIT",STR(Z[6]:"00")
7855 WRITE (Z[6],KEY="")X3$(21,6)+STR(TIM*100:"0000")
7856 V5$=V2$(9,40)+V2$(1,8)+V2$(49,4)+V2$(57,2)+V2$(53,4)+V2$(59,18)
7858 READ (Z[1],KEY="",DOM=7866)
7860 READ (Z[1],END=7895)IOL=0310
7863 IF NOT(%GUI) THEN PRINT @(40,20),A$(1,36), ELSE PRINT 'MESSAGE'("Re-sorting data : "+A$(1,36),0),; WAIT 0
7865 IF A$(1,10)<V5$(1,10) OR A$(1,10)>V5$(11,10) THEN GOTO 7860
7867 IF A$(11,10)<V5$(21,10) OR A$(11,10)>V5$(31,10) THEN GOTO 7860
7869 IF A$(21,4)<V5$(41,4) OR A$(21,4)>V5$(45,4) THEN GOTO 7860
7871 IF A$(25,6)<V5$(49,6) OR A$(25,6)>V5$(55,6) THEN GOTO 7860
7880 K1$=A$(21,4)+A$(1,20)+A$(25,14)
7885 WRITE (Z[6],KEY=K1$)K1$
7890 GOTO 7860
7895 IF NOT(%GUI) THEN PRINT @(0,20),'CL', ELSE PRINT 'MESSAGE'("Printing....",0),'CURSOR'(1), END_IF ; RETURN 
7900 REM "WO103249, see if ICP file exists, if so display date/time of last resort, ask if resort
7910 READ (Z[6],KEY="",DOM=7911)ICP$; GOTO 7915
7911 COMMAND=0; RETURN 
7915 Q$=""; IF LEN(ICP$)=10 THEN Q$="Last resort :"+FND$(ICP$(1,6))+"  "+FNT$(ICP$(7,4))
7918 IF %MULT_RPT_DRV_MODE THEN COMMAND=0; GOTO 7921 ! SSP 228228
7920 COMMAND=0; CALL "ZZPROM","Y",X3$,COMMAND,"Do you wish to resort the data? "+Q$,"","",0
7945 RETURN 
8000 REM "Print transaction code totals by location for sequence 2
8005 IF W+7>W0 THEN GOSUB 6000
8010 GOSUB 7300
8015 Y5$(13)="Transfers = "+STR(TTOTAL:Z2$); GOSUB 7300
8020 Y5$(14)="Receipts = "+STR(RTOTAL:Z2$); GOSUB 7300
8025 Y5$(11)="Adjustments = "+STR(ATOTAL:Z2$); GOSUB 7300
8030 Y5$(17)="Sales = "+STR(STOTAL:Z2$); GOSUB 7300
8035 Y5$(3)=T$+" total = "+STR(T[T,2]:Z2$); GOSUB 7300
8040 GOSUB 7300
8050 GTTOTAL=GTTOTAL+TTOTAL,GRTOTAL=GRTOTAL+RTOTAL,GATOTAL=GATOTAL+ATOTAL,GSTOTAL=GSTOTAL+STOTAL
8060 TTOTAL=0,RTOTAL=0,ATOTAL=0,STOTAL=0
8090 IF T0$="END" THEN W2$=""; GOSUB 6000 ELSE H$=D$; GOSUB 6000
8095 RETURN 
8100 REM "Print transaction code grand totals for sequence 2
8105 IF W+7>W0 THEN GOSUB 6000
8110 GOSUB 7300
8115 Y5$(13)="Transfers = "+STR(GTTOTAL:Z2$); GOSUB 7300
8120 Y5$(14)="Receipts = "+STR(GRTOTAL:Z2$); GOSUB 7300
8125 Y5$(11)="Adjustments = "+STR(GATOTAL:Z2$); GOSUB 7300
8130 Y5$(17)="Sales = "+STR(GSTOTAL:Z2$); GOSUB 7300
8135 Y5$(10)="Report total = "+STR(T[T,2]:Z2$); GOSUB 7300
8140 Y5$(25)=T2$(1,15); GOSUB 7300
8195 RETURN 
8900 REM "Function Statements
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8920 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
8925 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
8950 DEF FNQ$(Z9,Z9$)=STR(Z9:Z9$(1,POS(".00"=STR(Z9:Z9$)+".00")-1))
8960 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "End program
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 IF V0=2 THEN ERASE J1$,ERR=*NEXT; REM "Erase temp file used for sequence 2
9927 IF %GUI<>0 AND Y4$<>"" THEN CALL Y4$,ERR=*NEXT
9928 IF %GUI THEN EXIT 
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 RUN "ZMENU"
9999 END 
56000 REM "210899-Some programs set file pointer using $FF$.  This does not   
56001 ! "228228-Ability to split a report into multiple PDFs based on sales 
