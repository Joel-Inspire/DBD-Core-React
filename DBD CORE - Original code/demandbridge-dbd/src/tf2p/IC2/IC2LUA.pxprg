0010 REM "Update FMZ with Lot Detail <IC2LUA>
0011 REM "SSP# 097975 - cwj 5/12/98 - add 2140
0035 REM "5.7 - 03/02/23 - 10.410291 - jvv - SSP# 307434
0036 REM "MOD LINES 3010 DIF A(DIM) AND 3252 TO SET FMS UNIT PRICE
0037 REM "307434-Add support for Lot Number in FMZ/FMY                       
0038 REM "Add 3010/3230 for lot cust PO # Enhance, 3230 =numbering bug fix
0039 REM "*** LINE 2055 FOR CREDIT REQS NOT UPDATING FMZ!!! 6/3/93
0040 REM "Copyright 2023 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0110 X0$=PGN
0120 REM "THIS ROUTINE SHOULD BE CALLED IN PLACE OF WRITING THE
0121 REM "FMZ RECORD.  IT WILL WRITE ONE OR MORE RECORDS AS REQUIRED
0122 REM "TO UPDATE THE LOT DETAIL.
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12]
0360 IOLIST F$
0444 IOLIST R$,R[0],R[1],R[2],R[3],R[4],R[5],R[6],R[7],R[8],R[9],R[10],R[11],R[12],R[13]
1010 REM "F0=FILE FOR ICF, F1=FILE FOR FMZ, R$=FMZ RECORD
1011 REM "B$=SALES ORDER AND LINE, C$=ITEM CODE
1012 REM "F0$ IS LOT DETAIL FILE **** MUST BE PASSED EVEN IF F1 IS SET (ALREADY OPEN); USED AT 2000-2099
1013 REM "S0 IF -1 MEANS NEGATIVE QTY
1014 REM P1$ ARE CUSTOMER DEFAULTS, P2$ IS FMS PARAMS, P3$ IS SPARE
1020 ENTER X3$,X4$,F0,F0$,F1,F2,R0$,P1$,P2$,P3$,Q{ALL},B$,S0
1022 SETERR 9000
1025 DIM Z[NUM(X3$(60,3))]
1035 IF F0=0 THEN Z$="01O "+F0$+"... "; GOSUB 9700; F0=Z[1],F9=1
1040 IF F1=0 THEN Z$="02O FMZ... 20O FTP...  "; GOSUB 9700; F1=Z[2],F9=1
1041 IF F1<>0 THEN Z$="20O FTP...  "; GOSUB 9700
1045 IF F2=0 THEN Z$="03O FSZ... "; GOSUB 9700; F2=Z[3],F9=1
1048 IF F3=0 THEN Z$="04O FM0... "; GOSUB 9700; F3=Z[4],F9=1
1049 Z$="13O ZZPARM "; GOSUB 9700; DIM OPS_PARMS$(512); FIND (Z[13],KEY=X3$(9,3)+"F/M")OPS_PARMS$
1050 IF S0=0 THEN S0=1
1055 GOSUB 7600
1060 R$=R0$,C0$=R$(36,10)+R$(16,10)
1070 C1$=C0$; IF F0$="POH" THEN C1$=B$(1,12),WHSE$=B$(13,4)
1075 IF F0$="ICK" THEN C1$=B$; REM "ssp#81052
1080 DIM P[10],A[17],C[12],R[17],S$(20); REM SSP 198988
1085 FOR X=0 TO 17; R[X]=Q[X]; NEXT X
1900 REM "Setup Totals for possible proration
1905 REM "P(0=FMS VALUE, P(1=NET BILLING, P(2=FREIGHT, P(3=TAX, P(4)=OTHER
1910 P[0]=R[3],P[1]=R[6],P[2]=R[7],P[3]=R[8],P[4]=R[9],P[5]=R[5]
2000 REM "process ICF TYPE  for records FOR THIS ORDER / LINE (ITEM)
2010 GOSUB SET_READONLY ! SSP#301715
2020 READ (F0,KEY=C1$,DOM=2021)
2040 C2$=KEY(F0,END=2200); READ (F0)IOL=0330
2042 GOSUB CLEAR_READONLY ! SSP#301715
2045 IF F0$="POH" THEN IF C2$(1,12)<>C1$ THEN GOTO 2200 ELSE IF C$(21,4)<>WHSE$ THEN GOTO 2040 ELSE GOTO 2080
2050 IF C$(1,20)<>C0$ THEN GOTO 2200
2052 T0$=C$(110,11); IF B$(1,2)="TR" THEN T0$=C$(221,11)
2055 IF POS(F0$="ICFICN",3)>0 THEN IF T0$<>B$ THEN GOTO 2040
2056 REM "HANDLED AT 2045; WAS: IF F0$="POH" THEN IF C$(41,9)<>B$ THEN GOTO 02040
2057 IF F0$="ICK" THEN IF T0$<>B$ THEN GOTO 2040
2080 GOSUB 8000
2100 REM 
2120 GOSUB 3000
2140 IF F0$="ICN" THEN REMOVE (F0,KEY=C2$,DOM=2141); REM "mod 5/6/98 cwj SSP#097975
2190 GOTO 2040
2200 REM "Done with lots
2210 GOSUB CLEAR_READONLY ! SSP#301715
2290 GOTO 9900
3000 REM "SET FMZ RECORD
3010 DIM A[17],A$(625)
3020 A$(1)=R$
3200 REM "String fields
3210 A$(231,9)=C$(41,9),A$(240,6)=C$(50,6),A$(246,12)=C$(56,12)
3211 IF C[10]=1 THEN A$(445,1)="Y" ELSE A$(445,1)="N"; REM SSP 198988 Customer Supplied PO - ran out of string in ICF - using 1 to indicate YES
3215 A$(258,10)=C$(100,10)
3220 A$(268,8)=C$(121,8),A[11]=C[3],A[12]=C[4]
3222 IF POS(" "<>C$(156,40))>0 THEN A$(276,40)=C$(156,40)
3225 A$(316,6)=C$(196,6),A$(322,7)=C$(202,7),A$(329,6)=C$(209,6)
3230 A$(350,15)=C$(133,15),A$(198,18)=C$(82,18),A$(223,8)=C$(33,8)
3232 A$(447,6)=C$(25,6) ! WO220977, Lot Receipt Date
3233 A$(453,8)=C$(234,8) ! SSP 307434, Lot number
3234 IF OPS_PARMS$(250,1)<>"Y" THEN GOTO 3246
3235 FM0_KEY$=A$(11,1)+A$(1,10)+A$(12,4),FM4_KEY$=A$(1,10)+A$(16,10)+A$(12,4); IF A$(11,1)="D" THEN COMMAND$="1" ELSE COMMAND$="3"
3240 CALL "FM2DPA",X3$,X4$,COMMAND$,FM0_KEY$,FM4_KEY$,REC_DEPT$,BILL_DEPT$
3244 IF POS(X3$(9,3)="598101288475497499500522524568589029",3)>0 OR (MID(OPS_PARMS$,333,1)="Y") THEN IF STP(A$(385,20),3," ")="" THEN GOTO 3245 ELSE GOTO 3246; REM "SSP102342 SSP#202439 SSP 211619 !SSP#240316 ! WO247674 jdf SSP307430-DBD-333-DBSPT-149880
3245 A$(365,20)=REC_DEPT$,A$(385,20)=BILL_DEPT$
3250 REM "NUMERICS
3251 IF R[0]=0 THEN R[0]=1
3252 A[2]=R[2],A[7]=R[7],A[8]=R[8],A[9]=R[9],A[4]=R[4],A[13]=R[13],A[15]=R[15],A[16]=R[16],A[17]=R[17],A[10]=R[10] ! SSP#258031
3254 REM "C(5) IS LOT SELL PRICE, C(6) IS QTY PER UNIT
3255 IF C[5]=0 THEN GOTO 3265 ELSE A[4]=C[5],A[0]=C[6],A$(180,4)=C$(129,4); IF C[6]<>R[1] AND R[0]<>0 THEN A[2]=A[2]*C[6]/R[0] END_IF ; IF A[10]=0 AND C[5]<>0 THEN A[10]=C[5] ! SSP#258031
3256 REM "IF C$(125,4)<>"    " THEN LET A$(180,4)=C$(125,4)
3265 A[0]=R[0]; REM IF C(3)<>0 THEN LET A(0)=C(3)
3269 REM "GET TOTAL EACH QTY THIS LOT
3270 Q0=C[4]; IF C[3]<>0 THEN Q0=Q0*C[3]; IF C[2]<>0 THEN Q0=Q0*C[2]
3272 Q0=Q0*S0
3300 REM "Multiple Lots - Must Pro-Rate FMZ Values
3305 IF R[1]=0 THEN GOTO *RETURN ! SSP#235072
3310 A[1]=Q0,A[3]=Q0*R[3]/R[1],A[6]=Q0*R[6]/R[1],A[7]=Q0*R[7]/R[1],A[8]=Q0*R[8]/R[1],A[9]=Q0*R[9]/R[1],A[15]=Q0*R[15]/R[1],A[16]=Q0*R[16]/R[1],A[17]=Q0*R[17]/R[1]
3315 IF Q[1]<>0 THEN A[7]=Q0*Q[7]/Q[1],A[8]=Q0*Q[8]/Q[1],A[9]=Q0*Q[9]/Q[1]
3320 R[1]=R[1]-A[1],R[6]=R[6]-A[6],R[7]=R[7]-A[7],R[8]=R[8]-A[8],R[9]=R[9]-A[9],R[15]-=A[15],R[16]-=A[16],R[17]-=A[17]
3350 REM "RE-EXTEND ALWAYS BASED ON SELL PRICE
3351 IF A[0]=0 THEN A[0]=1
3355 Q1=A[1]*A[4]/A[0],A[5]=Q1
3400 REM "Write FMZ record
3405 IF A$(180,4)=DIM(4) AND A(4)=0 THEN A(0)=0 ! SSP#268599
3410 WRITE (F1,KEY=A$(1,34),DOM=3415)IOL=0310; GOTO 3450
3415 IF A$(32,3)="999" THEN EXIT 43 ELSE A$(32,3)=STR(NUM(A$(32,3))+1:"000"); GOTO 3410
3450 WRITE (Z[20],KEY=A$(1,10)+A$(16,10)+A$(11,5)+A$(26,9)); REM " SSP# 142993
3455 IF P1$>"" THEN GOSUB 3500
3490 RETURN 
3500 REM "ALTERNATE SORT FSZ
3520 GOSUB 7500
3540 WRITE (F2,KEY=A$(1,10)+K$(1,20)+A$(26,6)+A$(11,15)+A$(32,3))
3590 RETURN 
7500 REM "GET HIGH ORDER KEY FOR FSZ IN K$
7505 S1=4,S2=5; DIM F$(347)
7510 IF POS("4"=S0$)>0 THEN DIM F$(347); FIND (F3,KEY="C"+R$(1,10)+R$(12,4),DOM=7511)IOL=0360
7515 K$="",S0$=P2$(18,8)
7520 FOR X=1 TO LEN(S0$)
7530 ON POS(S0$(X,1)="1234") GOTO 7585,7531,7532,7533,7534,7585
7531 K$=K$+A$(335,S1); GOTO 7580
7532 K$=K$+A$(335+S1,S2); GOTO 7580
7533 K$=K$+A$(12,4); GOTO 7580
7534 K$=K$+F$(214,9); GOTO 7580
7585 NEXT X
7588 K$=K$+S$,K$=K$(1,20)
7590 RETURN 
7600 REM "Get cost code format for this customer
7605 S1=9,S2=0; IF LEN(P1$)<235 THEN GOTO 7690
7615 Q=0; Q$="FM"+P1$(235,1),Q=POS(Q$=X4$(7),12); IF Q>0 THEN Q=Q+10
7620 IF Q>0 THEN S1=NUM(X4$(Q+1,1)),S2=NUM(X4$(Q+3,1))
7690 RETURN 
8000 REM "Create the FMZ buckets from the lot record C$, C(ALL)
8010 DIM R$(625),R[17]
8020 R$(1)=R0$
8025 FOR X=0 TO 13; R[X]=Q[X]; NEXT X
8028 IF C[5]<>0 THEN R[4]=C[5],R[0]=C[6],R$(180,4)=C$(129,4)
8030 R$(231,27)=C$(41,27),R$(258,10)=C$(100,10)
8035 R$(268,8)=C$(121,8)
8040 R$(276,40)=C$(156,40),R$(316,19)=C$(196,19)
8045 R$(86,6)=C$(215,6)
8060 REM "Q0=QTY ON THIS LOT
8065 Q0=C[4]; IF C[3]<>0 THEN Q0=Q0*C[3]; IF C[2]<>0 THEN Q0=Q0*C[2]
8070 Q0=Q0*S0,R[1]=Q0
8071 IF C[9]<>0 THEN R[2]=C[9]; REM " SSP# 157344
8075 IF R[0]<>0 THEN R[5]=R[1]*R[4]/R[0],R[3]=R[1]*R[2]/R[0] ELSE R[5]=R[1]*R[4],R[3]=R[1]*R[2]; REM "ssp#098771
8080 IF Q[6]<>0 THEN R[6]=R[6]*R[1]/Q[1]
8085 R[13]=C[4]
8086 REM "R[2]=C[9]; REM "SSP 157344
8090 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 IF Y5=68 THEN RETRY ELSE ON C9 GOTO 1140,2040
9700 REM "FILES
9740 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
9790 RETURN 
9920 IF F9=1 THEN Z$="END"; GOSUB 9700
9940 EXIT 
9999 END 
10000 ! SSP#301715 set read only
10010 SET_READONLY:
10020 SET_PARAM 'XI'
10030 SET_READONLY_END:RETURN 
10035 ! SSP#301715 Clear read only     
10040 CLEAR_READONLY:
10050 SET_PARAM -'XI'
10060 CLEAR_READONLY_END:RETURN 
56000 ! Changes since 03/01/2006
56005 REM "190474-Additional Services - Enhance TF to provide for tax codes   
56006 REM "198988-Manage Customer Supplied Inventory. Item Setup, Purch ord   
56007 REM "202439-They need to have the same custom change for the function   
56008 REM "211619-Needs documentation on using the function field as billing  
56010 REM "220977-Add new field, Lot Receipt Date, to FMZ file, modify updates
56011 REM "235072-Items for Broadview that have inventory variances           
56012 REM "240316-Needs a field to store a "cost center" at the item level,   
56013 REM "247674-Need to use program logic to make function field write to   
56014 REM "258031-FMZ file - where do these fields come from?  And why        
56015 REM "268599-There is an issue with PO receiving and what is being       
56016 REM "301715-SJ update - file in use message caused when another operator
56018 REM "307430-DBD-333;Add 101 to some of 288's company specfic code       
56019 REM "307434-Add support for Lot Number in FMZ/FMY                       
