0010 REM "I/C Backorder Status Report <IC2RHA>
0035 REM "5.7 - 08/13/14 - 15.961666 - tma - SSP# 272017
0037 REM "272017-The I/C Backorder status report does not work correctly with
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 IF %GUI THEN GOTO 0091
0090 BEGIN 
0091 SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 SETERR 9000; SETESC 9300
0110 X0$="IC2RHA",X1$="I/C Backorder Status Report"
0120 DIM B$(58),B[20],C$(128),C[20],D$(310),D[2],E$(214),F$(32),J$(46),J[7],K[29],G[15]
0140 Z0$="-##,###,###.00",Z1$="##,##0",Z2$="-##,###,##0.00"
0210 T=1,T0=0,T1=9
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR IC4 - Item sort by Vendor , Product code
0310 IOLIST A$
0315 REM "IOLIST FOR IC1 - Inventory Detail Location
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19],B[20]
0325 REM "IOLIST FOR IC0 - Item Masterfile
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20]
0335 REM "IOLIST FOR AP4 - Vendor Masterfile
0340 IOLIST D$,D[0],D[1],D[2]
0345 REM "IOLIST FOR FM0 - Warehouse Locations
0350 IOLIST E$
0355 REM "IOLIST FOR FMP2 - Product code file layout
0360 IOLIST F$
0370 IOLIST G$,G[0],G[1],G[2],G[3],G[4],G[5],G[6],G[7],G[8],G[9],G[10],G[11],G[12],G[13],G[14],G[15]
0380 IOLIST I$,I[0],I[1]
0385 IOLIST L$,L[0],L[1],L[2],L[3],L[4],L[5],L[6]
0390 REM "IOLIST FOR AR1
0400 IOLIST J$,J[0],J[1],J[2],J[3],J[4],J[5],J[6],J[7]
0410 IOLIST K$,K[0],K[1],K[2],K[3],K[4],K[5],K[6],K[7],K[8],K[9],K[10],K[11],K[12],K[13],K[14],K[15],K[16],K[17],K[18],K[19],K[20],K[21],K[22],K[23],K[24],K[25],K[26],K[27],K[28],K[29]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O IC4...  02O IC1...  03O IC0...  04O AP4...  05O FM0...  06O FMP...  07O AR1...  08O PO3... 09O FS5... 10O FS1...  11O FS2... 13O ZZPARM  "; IF V1$(1,3)<>Y$(5,3) THEN Y$(1,10)="01O "+V1$+"  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0580 DIM P[5]; P[0]=60,P[1]=75,P[2]=90,P[3]=105,P[4]=120
0590 READ (Z[13],KEY=X3$(9,3)+"I/C")P0$
0600 REM "Set len of vend L0 and item  L1 codes
0610 DIM X5$(10,"X"); CALL "ZZDISP","AX",X5$,"A/P",X3$,X$,"",0,0,X4$; L0=LEN(X$)+2
0615 DIM X5$(20,"X"); CALL "ZZDISP","AX",X5$,"I/C",X3$,X$,"",0,0,X4$; L1=LEN(X$)+2
0700 REM "Open Printer
0720 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0721,9920
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840,0845
0820 DIM U[4]; U=37,U0=4,U[0]=0,U[1]=14,U[2]=3,U[3]=10,U[4]=10,T0=3,V1=14,V2=3,V3=20,U0$="Y   ",T5$="AA/PS   AI/CAI/C",U5$="Vendor",U6$="Product code",U7$="Item code",X1$=X1$+" by Vendor",V2$=V2$(1,34)+V2$(35,10)+V2$(55,10)+V2$(45,10)+V2$(65,10); GOTO 0850 ! SSP#272017
0830 DIM U[3]; U=23,U0=3,U[0]=0,U[1]=3,U[2]=10,U[3]=10,T0=2,V1=3,V2=20,U0$="    ",T5$="S   AI/C        ",U5$="Product code",U6$="Item code",S9$="X",X1$=X1$+" by Product Code",V2$=V2$(1,6)+V2$(7,10)+V2$(27,10)+V2$(17,10)+V2$(37,10); GOSUB 6900; GOTO 0850 ! SSP#272017
0840 DIM U[4]; U=27,U0=4,U[0]=0,U[1]=4,U[2]=3,U[3]=10,U[4]=10,T0=4,I0=3,V1=4,V2=3,V3=20,U0$="Y   ",T5$="S   S   AI/C    ",U5$="Location",U6$="Product code",U7$="Item code",S9$="X",X1$=X1$+" by Location"; GOSUB 6900; GOTO 0850 ! SSP#251711
0841 REM "DIM U[3]; LET U=27, U0=3, U[0]=0, U[1]=4, U[2]=3, U[3]=20, T0=3, V1=4, V2=3, V3=20, U0$="Y   ", T5$="S   S   AI/C    ", U5$="LOCATION", U6$="PRODUCT CODE", U7$="ITEM CODE", S9$="X", X1$=X1$+" BY LOCATION"; GOSUB 06900; GOTO 00850
0845 DIM U[3]; U=24,U0=3,U[0]=0,U[1]=10,U[2]=10,U[3]=4,T0=3,V1=10,V2=10,V3=4,U0$="Y   ",T5$="AA/RAI/CS   ",U5$="Customer",U6$="Item code",U7$="Location",X1$=X1$+" by Customer"; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0915 IF V0=4 THEN V2$=V2$(1,10)+V2$(21,10)+V2$(11,10)+V2$(31,10)+V2$(41)
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 EXTRACT (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])>=V2$(U3,U[U9]) THEN GOTO 1030 ELSE EXTRACT (U1,KEY=U$(1,U2-1)+V2$(U3,U[U9]),DOM=1021)
1025 EXITTO 1000
1030 IF U$(U2,U[U9])<=V2$(U3+U[U9],U[U9]) THEN GOTO 1040 ELSE READ (U1,KEY=U$(1,U2-1)+$FF$,DOM=1032)
1035 EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140,1145
1130 U$=U9$(4); READ (U1,END=5000); GOTO 2000
1140 U$=U9$(8)+U9$(1,4); READ (U1,END=5000); READ (Z[2],KEY=U$,DOM=1000)IOL=0320; GOTO 1160
1145 READ (U1,KEY=U$,DOM=1000)IOL=0320; GOTO 1160
1150 U$=U9$(18,20); READ (U1); GOTO 2000
1160 REM 
1170 B0=B[3]+B[4]-B[5]+B[6]
1175 IF V0$(74,1)="O" THEN IF B0-B[7]>=0 THEN GOTO 1000
1180 IF V0$(74,1)<>"O" THEN IF B[11]=0 THEN GOTO 1995
1300 REM "Read other data
1330 DIM C$(LEN(C$)); FIND (Z[3],DOM=1331,KEY=B$(1,20))IOL=0330
1335 IF POS("I"=V0$(59,12))<>0 AND C$(108,1)="I" THEN GOTO 1000
1340 H8$=C$
1350 DIM J$(46); FIND (Z[7],KEY=C$(1,10),DOM=1351)IOL=0400
1360 IF V0=1 THEN DIM F$(LEN(F$)); FIND (Z[6],DOM=1365,KEY="T"+U9$(15,3))IOL=0360; GOTO 1365
1361 IF V0=2 THEN DIM F$(LEN(F$)); FIND (Z[6],DOM=1365,KEY="T"+U9$(1,3))IOL=0360; GOTO 1365
1362 IF V0=3 THEN DIM F$(LEN(F$)); FIND (Z[6],DOM=1363,KEY="T"+U9$(5,3))IOL=0360
1400 REM "Subtotals"
1415 GOSUB 3100
1420 ON T GOTO 1470,1430,1440,1450,1460
1430 IF V0=1 THEN GOSUB 6200 ELSE IF V0=3 THEN GOSUB 6300 ELSE IF V0=4 THEN GOSUB 6600
1440 GOSUB 6400
1450 GOSUB 6500
1490 T=0
1500 REM "Print Data
1510 REM IF I$(20,1)="Y" THEN LET I0=I[1] ELSE IF I$(20,1)<>"Y" AND V0$(72,1)<>"Y" THEN LET I0=C[15] ELSE IF I$(20,1)<>"Y" AND V0$(72,1)="Y" THEN LET I0=C[14]
1513 REM IF I0=0 THEN LET I0=1
1515 FOR I=3 TO 8; B[I]=B[I]/I0; NEXT I; B0=B0/I0
1520 I=I0; IF I$(20,1)="Y" THEN IF I[0]<>0 THEN I=I[0]
1525 C7$=FNS$(C7$)+"/"+STR(I)
1530 IF W+2>W0 THEN GOSUB 6000
1535 Y5$(55)=B$(21,4)
1540 GOSUB 2500
1590 IF V0$(73,1)="Y" THEN GOSUB 7300
1595 GOSUB 7300
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+B[3],T[T0,2]=T[T0,2]+B[4],T[T0,3]=T[T0,3]+B[5],T[T0,4]=T[T0,4]+B[6],T[T0,5]=T[T0,5]+B0,T[T0,6]=T[T0,6]+B[7],T[T0,7]=T[T0,7]+B1,T[T0,8]=T[T0,8]+B[8],T[T0,9]=T[T0,9]+B2
1990 T[T0,0]=T[T0,0]+1
1995 IF V0<>3 THEN GOTO 2000 ELSE GOTO 1000
2000 REM "For V0=1 or 2 Read into Location file
2001 REM "F0$='' means position read first else just read
2004 IF F0$="*" THEN GOTO 2010
2005 READ (Z[2],KEY=U$,DOM=2006)
2006 F0$="*"
2010 READ (Z[2],END=2050)IOL=0320
2015 IF B$(1,20)<>U$(1,20) THEN GOTO 2050
2018 U3=1,U2=1
2019 IF V0=2 THEN GOTO 2040
2020 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
2025 IF B$(U2,U[U9])>=V2$(U3,U[U9]) THEN GOTO 2030
2030 IF B$(U2,U[U9])<=V2$(U3+U[U9],U[U9]) THEN GOTO 2039 ELSE EXITTO 2050
2039 NEXT U9
2040 GOTO 1160
2050 IF V0<>4 THEN F0$=""; GOTO 1000
2055 REM "For V0=4 read to end of locations for this item
2060 READ (U1,KEY=U9$(1,20)+"~~~~",DOM=1000); GOTO 1000
2500 REM "PRINT ORDER DETAIL
2501 IF W+3>W0 THEN GOSUB 6000
2505 B1=0,B2=0,B3=0
2506 IF V0$(73,1)="Y" THEN GOSUB 7300; IF V0$(75,1)="Y" THEN Y5$(2+L1)=C7$; GOSUB 7300 ELSE GOSUB 7300
2510 READ (Z[9],KEY=B$(1,24),DOM=2511)
2519 IF W+2>W0 THEN GOSUB 6000
2520 I0$=KEY(Z[9],END=2590); READ (Z[9]); IF I0$(1,24)<>B$(1,24) THEN GOTO 2590 ELSE READ (Z[10],KEY=I0$(25,8),DOM=2520)IOL=0400
2521 Q$=" "; IF J$(48,1)="Y" THEN Q$="B"
2522 IF V0$(73,1)="Y" THEN Y5$(13)=I0$(25,8)+"-"+I0$(33,3)+Q$,Y5$(27)=FND$(J$(16,6))
2523 DIM X$(300); READ (Z[8],KEY=I0$(25,8)+"     ",DOM=2524)X$
2525 READ (Z[11],KEY=I0$(25,11),DOM=2520)IOL=0410; K[0]=K[0]/I0,K[7]=K[7]/I0,K[8]=K[8]/I0
2528 IF V0$(73,1)="Y" THEN DIM G$(120); FIND (Z[7],KEY=J$(6,10),DOM=2529)IOL=0370; Y5$(40)=X$(11,5)+"-"+X$(46,35)
2530 IF POS(X3$(9,3)="063")<>0 THEN Y5$(40)=X$(11,5)+"-"+X$(225,35)
2531 IF V0$(76,1)="Y" THEN FIND (Z[5],KEY="C"+J$(6,10)+X$(12,4),DOM=2532)X$; Y5$(46)=FNS$(X$(214,9))+"-"+FNS$(X$(16,35))
2532 IF K[8]>0 THEN K[0]=K[0]-K[8]
2533 IF K[7]<>0 THEN K[0]=0
2534 K[0]=K[0]+K[7]+K[8]
2535 IF V0$(73,1)<>"Y" THEN GOTO 2555 ELSE Y5$(75)=FNQ$(K[7],Z0$),Y5$(90)=FNQ$(K[8],Z0$),Y5$(105)=FNQ$(K[0],Z0$); B[7]=B[7]+K[0]
2537 REM "ORDER HEADER BACKORDER FLAG
2550 GOSUB 7300
2555 B1=B1+K[7],B2=B2+K[8],B3=B3+K[0]
2560 GOTO 2519
2590 GOSUB 2600
2595 RETURN 
2600 REM "TOTAL FOR THIS WHSE
2605 IF W+4>W0 THEN YA5$=Y5$; DIM Y5$(W3); GOSUB 6000; Y5$=YA5$
2606 IF V0$(73,1)<>"Y" THEN GOTO 2615
2610 Y5$(60)=T1$(1,LEN(Z0$)),Y5$(75)=T1$(1,LEN(Z0$)),Y5$(90)=T1$(1,LEN(Z0$)),Y5$(105)=T1$(1,LEN(Z0$)),Y5$(120)=T1$(1,LEN(Z0$)); GOSUB 7300
2615 Y5$(60)=FNQ$(B0,Z0$),Y5$(75)=FNQ$(B1,Z0$),Y5$(90)=FNQ$(B2,Z0$),Y5$(105)=FNQ$(B3,Z0$),Y5$(120)=FNQ$(B0-B3,Z0$)
2630 IF V0$(73,1)<>"Y" AND V0$(75,1)="Y" THEN GOSUB 7300; Y5$(2+L1)=C7$
2645 RETURN 
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; GOSUB 7000
3155 IF U0$(1,1)="Y" AND T0$<>"END" THEN U1$=U9$(1,V1); GOSUB 6800; GOSUB 6000
3160 U1$=U9$(1,V1)
3180 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$,T6$=U2$; GOSUB 3050; GOSUB 7000
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3260 U2$=U9$(V5,V2)
3280 RETURN 
3300 REM "Next to lowest order break"
3310 IF V3=0 THEN RETURN ELSE IF U3$=U9$(V6,V3) THEN GOSUB 3400; RETURN 
3320 GOSUB 3420
3330 IF U3$="" THEN GOTO 3360
3350 T=3,T$=U7$,T6$=U3$; GOSUB 3050; GOSUB 7000
3355 IF U0$(3,1)="Y" THEN GOSUB 6000
3360 U3$=U9$(V6,V3)
3380 RETURN 
3400 REM "lowest order break"
3410 IF V4=0 THEN RETURN ELSE IF U4$=U9$(V7,V4) THEN RETURN 
3430 IF U4$="" THEN GOTO 3460
3450 T=4,T$=U8$,T6$=U4$; GOSUB 3050; GOSUB 7000
3455 IF U0$(4,1)="Y" THEN GOSUB 6000
3460 U4$=U9$(V7,V4)
3480 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; GOSUB 7000
5200 REM "
5290 GOTO 9900
6000 REM "Page header
6010 IF W0$="" THEN DIM W0$(55) ! SSP#251711
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W4$,W5$
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6105 IF V0$(73,1)="Y" THEN Y5$(28)="Order"
6110 Y5$(77)="Un-billed",Y5$(108)="Total",Y5$(122)="Available"; GOSUB 7300
6115 Y5$(1)="Item",Y5$(55)="Whse",Y5$(64)="On-hand",Y5$(77)="Shipments",Y5$(92)="Back order",Y5$(107)="Open Order",Y5$(123)="In-Stock"
6130 IF V0$(73,1)="Y" THEN Y5$(14,35)=" Order/Line    Date       Customer  "; GOSUB 7300 ELSE Y5$(14,31)="Description"; GOSUB 7300
6199 RETURN 
6200 REM "Break #1 header
6210 DIM D$(LEN(D$)); FIND (Z[4],DOM=6211,KEY=U1$(1,10))IOL=0340
6215 DIM E$(LEN(E$)); FIND (Z[5],DOM=6216,KEY="P"+U1$(1,14))IOL=0350
6220 CALL "ZZDISP","AX",U1$(1,10),"A/P",X3$,X$,"",0,0,X4$; W0$=FNS$(X$)
6222 W0$=W0$+" - "+FNS$(D$(11,35)); IF POS(" "<>D$(106,16))<>0 THEN W0$=W0$+" - "+FNS$(D$(106,16))+", "+D$(122,2)
6232 IF POS(" "<>U9$(11,4))=0 THEN W1$=""; GOTO 6250
6234 W1$=U1$(11,4)+" - "+FNS$(E$(16,35)); IF POS(" "<>E$(111,16))<>0 THEN W1$=W1$+" - "+FNS$(E$(111,16))+", "+E$(127,2)
6299 RETURN 
6300 REM "Break #2 header - Location
6315 DIM E0$(LEN(E$)); FIND (Z[5],KEY="D"+E0$(1,10)+U1$,DOM=6316)E0$
6350 W0$=U1$+" - "+FNS$(E0$(16,35)); IF POS(" "<>E0$(111,16))<>0 THEN W0$=W0$+" - "+FNS$(E0$(111,16))+", "+E0$(127,2)
6399 RETURN 
6400 REM "Break #3 header - Product code
6405 IF V0=4 THEN GOTO 6499
6410 IF W+2>W0 THEN GOSUB 6000
6415 GOSUB 7300
6420 Y5$(3)=F$(2,3),Y5$(7)=F$(5,15)
6430 GOSUB 7300
6499 RETURN 
6500 REM "Break #4 header - item code
6505 IF W+4>W0 THEN GOSUB 6000 ! SSP#260901
6506 GOSUB 7300
6507 IF V0=4 OR POS(" "=J$(1,10))<>0 THEN GOTO 6515
6510 CALL "ZZDISP","AX",J$(1,10),"A/R",X3$,X$,"",0,0,X4$; Y5$(2)=X$
6512 Y5$(15)=J$(11,35)
6513 GOSUB 7300
6515 CALL "ZZDISP","AX",B$(1,20),"I/C",X3$,X$,"",0,0,X4$; Y5$(2)=X$
6517 GOSUB 7700
6519 REM "Pick selling u/m or inv u/m based on parameter
6520 IF V0$(72,1)="Y" THEN C7$=C$(120,4) ELSE C7$=C$(124,4)
6525 DIM I$(20),I[1]; FIND (Z[13],KEY="U/M"+C7$,DOM=6526)IOL=0380
6530 IF V0$(72,1)="Y" THEN C0$=FNS$(C7$)+"/"+FNR$(STR(C[14]:Z1$)),I0=C[14] ELSE C0$=FNS$(C7$)+"/"+FNR$(STR(C[15]:Z1$)),I0=C[15]
6535 IF I$(20,1)="Y" THEN I0=I[1]
6537 IF I0=0 THEN I0=1
6540 Y5$(2+L1)=H9$
6580 REM "GOSUB 7300
6599 RETURN 
6600 REM "Heading based on Customer code
6605 IF FNS$(U1$)="" THEN W0$="Distributor's Inventory"; GOTO 6690
6610 DIM J$(LEN(J$)); FIND (Z[7],KEY=U1$,DOM=6611)IOL=0400
6615 CALL "ZZDISP","AX",U1$,"A/R",X3$,X$,"",0,0,X4$
6620 W0$=X$+" - "+FNS$(J$(11,35))
6690 RETURN 
6800 REM "Read proper headings in, based on V0
6810 ON V0 GOTO 6840,6820,6840,6825,6830
6820 GOSUB 6200; GOTO 6840
6825 GOSUB 6300; GOTO 6840
6830 GOSUB 6600; GOTO 6840
6840 RETURN 
6900 REM "SORT
6905 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6940 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,X9
6990 RETURN 
7000 REM "Totals logic
7001 GOTO 7090
7002 IF T<>T0 OR T[T0,0]<2 THEN GOTO 7100
7003 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(6)=T1$(1,14),Y5$(20)=T1$(1,14),Y5$(34)=T1$(1,14),Y5$(48)=T1$(1,14),Y5$(62)=T1$(1,14),Y5$(76)=T1$(1,14),Y5$(90)=T1$(1,14),Y5$(104)=T1$(1,14),Y5$(118)=T1$(1,14); GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 Y5$(1)="Total"
7020 Y5$(6)=FNQ$(T[T,1],Z0$),Y5$(20)=FNQ$(T[T,2],Z0$),Y5$(34)=FNQ$(T[T,3],Z0$),Y5$(48)=FNQ$(T[T,4],Z0$),Y5$(62)=FNQ$(T[T,5],Z0$),Y5$(76)=FNQ$(T[T,6],Z0$),Y5$(90)=FNQ$(T[T,7],Z0$),Y5$(104)=FNQ$(T[T,8],Z0$),Y5$(118)=FNQ$(T[T,9],Z0$); GOSUB 7300
7065 IF T$="END" THEN Y5$(7)=T2$(1,14),Y5$(21)=T2$(1,14),Y5$(35)=T2$(1,14),Y5$(49)=T2$(1,14),Y5$(63)=T2$(1,14),Y5$(77)=T2$(1,14),Y5$(91)=T2$(1,14),Y5$(105)=T2$(1,14),Y5$(119)=T2$(1,14); GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed"+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600),T4=MOD(T3,60),T3=INT(T3/60)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")+":"+STR(T4:"00")
7260 Y5$(1)="Elapsed time"+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 W=W+1
7330 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"  ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
7750 REM "Build desc from H8$
7755 H9$=H8$(21,40); RETURN 
7790 RETURN 
7800 REM "quantity explantion line
7805 Q$=FNR$(FNQ$(L(4),Z0$))
7810 IF L[4]=1 THEN Q$=Q$+" "+FNS$(L$(121,4))+" of " ELSE Q$=Q$+" "+FNS$(L$(121,4))+"s of "
7815 IF L$(125,4)="    " THEN Q$=Q$+FNR$(STR(L[3]:M3$)) ELSE Q$=Q$+FNR$(STR(L[2]:M3$))+FNC$(L$(125,4)); IF L[2]=1 THEN Q$=Q$+" of "+FNR$(STR(L[3]:M3$)) ELSE Q$=Q$+"s of "+FNR$(STR(L[3]:M3$))
7820 IF L$(125,4)="    " THEN Q=L[4]*L[3] ELSE Q=L[4]*L[2]*L[3]
7825 Y5$(78)=Q$+" = "+FNR$(FNQ$(Q,Z2$))
7845 RETURN 
8900 REM "Function Statements
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8920 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
8930 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
8940 DEF FNC$(Z9$)=Z9$(1,POS(" "=Z9$+" ")-1)
8950 DEF FNQ$(Z9,Z9$)=STR(Z9:Z9$(1,POS(".00"=STR(Z9:Z9$)+".00")-1))
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "End program
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 IF %GUI<>0 AND Y4$<>"" THEN CALL Y4$,ERR=*NEXT
9928 IF %GUI THEN EXIT 
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 RUN "ZMENU"
9999 END 
56001 REM "272017-The I/C Backorder status report does not work correctly with
