0010 REM "Inventory Delete Routine <IC2UPB>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 01/07/25 - 9.570687 - crg - SSP# 307513
0037 REM "307513-CSPT-342: IC Period-end processing hangs for ever           
0040 REM "Copyright 2025 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$,Q2$,Q3$
0095 IF %GUI THEN RESET 
0100 SETERR 9000
0110 X0$="IC2UPB",X1$="Inventory Delete Routine",K9$="",K9=0
0120 DIM Z0$(80,"-"),C[10],Y[4],F[14],K[14],J[14]
0160 DEF FNS$(Z9$)=Z9$(1,POS("  "=Z9$+"  ")-1)
0200 REM "
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0300 REM "IOLISTS
0310 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10]
0320 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0360 IOLIST F$,F[0],F[1],F[2],F[3],F[4],F[5],F[6],F[7],F[8],F[9],F[10],F[11],F[12],F[13],F[14]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01OSIC0...  02ORIC8...  03ORPO5...  04ORIC1...  05ORFS5...  06ORIC4...  07ORIC5...  08ORIC6...  10ORIC9...  11ORIC2...  12O ICD...  13ORZZPARM  14O PP3...  18O ZZO...  " ! SSP 234111
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0585 RT$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,RT$
0586 IF %GUI THEN RESET 
0600 READ (Z[13],KEY="STAT"+"I/C")S9$
0610 READ (Z[13],KEY=X3$(9,3)+"I/C")P0$
1000 REM "
1100 READ (Z[1],KEY=Q1$(1,20),DOM=5000)A$
1200 REM "Check if active
1210 REM "Open balances?
1220 READ (Z[4],KEY=A$(1,20),DOM=1221)
1225 IF %GUI THEN RESET 
1230 READ (Z[4],END=1300)IOL=0310
1240 IF C$(1,20)<>A$(1,20) THEN GOTO 1300
1250 IF C[3]+C[4]-C[5]+C[6]<>0 THEN Q2$="an "+MSG("ONHAND")+" quantity, " ! SSP#047384
1260 GOTO 1230
1300 REM "Open P/O?
1310 READ (Z[3],KEY=A$(1,20),DOM=1311)
1320 K$=KEY(Z[3],END=1350); READ (Z[3])
1330 IF K$(1,20)=A$(1,20) THEN Q2$=Q2$+"Open PO, " ! SSP#047384
1400 REM "I/C Detail Trans IC8 - Only disallow if current period trans
1410 READ (Z[2],KEY=A$(1,20),DOM=1411)
1420 K$=KEY(Z[2],END=1500); READ (Z[2])
1430 IF K$(1,20)<>A$(1,20) THEN GOTO 1500
1440 IF K$(25,6)>=P0$(7,6) THEN Q2$=Q2$+MSG("DET_TRAN")+", " ELSE GOTO 1420 ! SSP#047384
1500 REM "Open Orders?
1510 READ (Z[5],KEY=A$(1,20),DOM=1511)
1520 K$=KEY(Z[5],END=1600); READ (Z[5])
1530 IF K$(1,20)=A$(1,20) THEN Q2$=Q2$+MSG("OPEN_ORD") ! SSP#047384
1600 REM "If Q2$<>"" Do not delete, Build return message!SSP#047384
1610 IF Q2$="" THEN GOTO 2000 ! SSP#047384
1690 GOTO 5000 ! SSP#047384
2000 REM "Delete Inventory Information
2001 IF Q3$<>"P" THEN GOTO 2100
2003 PRINT @(0,16),'CL',@(12),"Inventory Item No:  ",
2005 CALL "ZZDISP","A",A$(1,20),"I/C",X3$,"","",32,12,X4$
2100 REM "Inv. by loc. and sort
2120 READ (Z[4],KEY=A$(1,20),DOM=2130)
2130 K$=KEY(Z[4],END=2200); READ (Z[4])B$
2140 IF K$(1,20)<>A$(1,20) THEN GOTO 2200
2150 REMOVE (Z[8],KEY=B$(21,4)+A$(61,3)+A$(1,20),DOM=2160)
2160 REMOVE (Z[4],KEY=K$); GOTO 2130
2162 IF RT$>"" THEN IF RT$(7,1)="H" THEN CUST$=K$(1,10); CALL "RT2WOC",ERR=2163,X3$,X4$,CUST$,"IC1...","D",K$
2200 REM "Supplier Price List and Supplier Item Number Sort
2210 READ (Z[11],KEY=A$(1,20),DOM=2211)
2220 K$=KEY(Z[11],END=2250); READ (Z[11])J$
2230 IF K$(1,20)<>A$(1,20) THEN GOTO 2250
2235 REMOVE (Z[12],KEY=J$(21,10)+J$(31,4)+J$(35,20)+J$(1,20),DOM=2236)
2240 REMOVE (Z[11],KEY=K$); GOTO 2220
2250 REM "Item sort by vendor
2260 REMOVE (Z[6],KEY=A$(82,14)+A$(61,3)+A$(1,20),DOM=2261)
2300 REM "Item by Product Code
2310 REMOVE (Z[7],KEY=A$(61,3)+A$(1,20),DOM=2311)
2400 REM "Combine Stats
2430 READ (Z[10],KEY=A$(1,20),DOM=2431)
2440 K$=KEY(Z[10],END=2600)
2445 IF K$(1,20)<>A$(1,20) THEN GOTO 2600
2447 IF K$(29,1)="U" THEN GOTO 2545
2450 Q1$=K$(1,28),Q0$=K$(29,1),P9$=Q1$+Q0$
2460 DIM K[14],J[14]
2470 GOSUB 7100
2480 FOR X=0 TO 13; J[X]=K[X]; NEXT X
2500 Q1$(1,20)="z                   ",P9$=Q1$+Q0$
2505 GOSUB 6000
2510 DIM K[14]
2520 GOSUB 7100
2530 FOR X=0 TO 13; K[X]=K[X]+J[X]; NEXT X
2540 GOSUB 7150
2545 REMOVE (Z[10],KEY=K$)
2547 READ (Z[10],KEY=K$,DOM=2548)
2550 GOTO 2440
2600 REM "I/C Detail Tran IC8 12/14/90
2610 READ (Z[2],KEY=A$(1,20),DOM=2611)
2620 K$=KEY(Z[2],END=2650); READ (Z[2])
2630 IF K$(1,20)<>A$(1,20) THEN GOTO 2650
2640 REMOVE (Z[2],KEY=K$,DOM=2641)
2645 GOTO 2620
2650 REMOVE (Z[14],KEY=A$(1,20),DOM=*NEXT) ! WO171176
2652 READ (Z[18],KEY="003"+A$(1,20)+DIM(30),DOM=*NEXT) ! SSP 234111
2654 ZZO_KEY$=KEY(Z[18],END=2660); READ (Z[18]) ! SSP 234111
2655 IF ZZO_KEY$(1,23)<>"003"+A$(1,20) THEN GOTO 2660 ! SSP 234111
2658 REMOVE (Z[18],KEY=ZZO_KEY$); GOTO 2654 ! SSP 234111
2660 ! If WebEC is present, then open ECR and remove
2665 CALL "ZZ2PRP","EC",RESULT$,DATE$; IF RESULT$<>"Y" THEN GOTO 2685
2670 Z$="15O ECR...  16O FM1...  " ! SSP 217512
2675 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 2676,9900
2679 FIND (Z[16],KEY=A$(1,20),DOM=*NEXT); GOTO 2681 ! SSP 217512 - If IC0 exists, do not remove ECR
2680 REMOVE (Z[15],KEY=A$(1,20),DOM=*NEXT)
2685 ! 
2800 REMOVE (Z[1],KEY=A$(1,20))
2805 IF RT$>"" THEN IF RT$(7,1)="H" THEN CUST$=A$(1,10); CALL "RT2WOC",ERR=2806,X3$,X4$,CUST$,"IC0...","D",A$(1,20)
3100 REM "Delete Return to Overlay
3110 Q2$="Y"
3140 GOTO 9900
5000 REM "No Delete Return to Overlay
5010 ! Q2$="N"!SSP#047384
5090 GOTO 9900
5900 REM "REMOVE LINES
5910 IF A0=0 THEN GOTO 5990
5920 EXTRACT (F,IND=0,ERR=5960)IOL=0320
5925 IF Y[4]=-2 THEN READ (F,IND=0); WAIT 0; GOTO 5920 ELSE Y[4]=-2; WRITE (F,IND=0)IOL=0320
5930 A1=Y[1],Y[1]=A0,Y[0]=Y[0]-1
5935 READ (F,IND=A0,ERR=5940)A; IF A>0 THEN Y[0]=Y[0]-1,A0=A; GOTO 5935
5940 WRITE (F,IND=A0)A1
5950 Y[4]=-1; WRITE (F,IND=0)IOL=0320
5955 GOTO 5990
5960 IF ERR=0 THEN RETRY ELSE GOTO 9000
5990 RETURN 
6000 REM "Write z record to master & sorts
6010 DIM A2$(128); A2$(1,45)=Q1$(1,20)+"Deleted item's statistics",A2$(61,3)="z  ",A2$(82,1)="z"; WRITE (Z[1],KEY=A2$(1,20),DOM=6011)A2$,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0; IF RT$>"" THEN IF RT$(7,1)="H" THEN CUST$=A2$(1,10); CALL "RT2WOC",ERR=6011,X3$,X4$,CUST$,"IC0...","U",A2$(1,20)
6020 WRITE (Z[7],KEY="999"+Q1$(1,20),DOM=6021)
6030 WRITE (Z[6],KEY="z         z  "+Q1$(1,20),DOM=6031)
6040 WRITE (Z[8],KEY=K$(21,4)+"z  "+Q1$(1,20),DOM=6041)
6050 RETURN 
7000 REM "Update stats
7010 DIM K[14]; P9$=Q1$+Q0$
7020 GOSUB 7100
7030 FOR Q1=0 TO 13
7040 K[Q1]=K[Q1]+J[Q1]
7050 NEXT Q1
7060 GOSUB 7150
7090 RETURN 
7100 REM "Read stats (Packed/Unpacked)
7105 Q=POS(Q0$=S9$(49),17); IF Q=0 THEN EXITTO 7090 ELSE M0$=S9$(Q+48,17)
7110 CALL "ZZPACK",X3$,"E",M0$(3,2),"",0,0,K{ALL},Z[10],Q0$,Q1$,S9$
7135 GOTO 7190
7150 REM "Write stats
7160 CALL "ZZPACK",X3$,"W",M0$(3,2),"",0,0,K{ALL},Z[10],Q0$,Q1$,S9$
7190 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 9900
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56001 REM "217512-Delete an item from inventory it deletes from buyer catalog  
56002 REM "234111-Modify Custom Item Notes feature to allow for a blank cust  
56003 REM "307513-CSPT-342: IC Period-end processing hangs for ever           
