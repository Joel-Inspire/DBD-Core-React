0010 REM "Valued Inventory Report <IC2RDA>
0035 REM "5.7 - 01/30/17 - 16.382222 - tma - SSP# 289922
0037 REM "289922-Create new field for Options to Omit Zero Extension from    
0040 REM "Copyright 2017 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 SETERR 9000; SETESC 9300
0110 X0$="IC2RDA",X1$="Valued Inventory Report"
0120 DIM A$(45),A[9],B$(187),B[40],C$(214),D$(32),J$(46)
0140 Z0$="-##,###,##0.00",Z1$="-##,##0.00#",Z2$="-##,##0"
0210 T=1,T0=0,T1=1
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR IC1 - Inventory Detail Location
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9]
0315 REM "IOLIST FOR IC0 - Item Masterfile
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19],B[20],B[21],B[22],B[23],B[24],B[25],B[26],B[27],B[28],B[29],B[30],B[31],B[32],B[33],B[34],B[35],B[36],B[37],B[38],B[39],B[40]
0325 REM "IOLIST FOR FM0 - Warehouse Locations
0330 IOLIST C$
0335 REM "IOLIST FOR FMP2 - Product code file layout
0340 IOLIST D$
0370 IOLIST G$,G[0],G[1]
0385 IOLIST L$,L[0],L[1],L[2],L[3],L[4],L[5],L[6],L[7],L[8],L[9],L[10],L[11],L[12]
0386 IOLIST ICF$,ICF[0],ICF[1],ICF[2],ICF[3],ICF[4],ICF[5],ICF[6],ICF[7],ICF[8],ICF[9],ICF[10],ICF[11],ICF[12]; REM SSP 187353
0390 IOLIST I$,I[0],I[1]
0400 IOLIST J$
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O IC1...  05O IC0...  03O FM0...  04O FMP...  06O AR1...  07O IC8...  08O ICF...  09O AR3...  13O ZZPARM  "; IF V1$(1,3)<>Y$(5,3) THEN Y$=Y$+"02O "+V1$+"  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0595 READ (Z[13],KEY=X3$(9,3)+"I/C")P0$
0700 REM "Open Printer
0720 REM 
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840,0845
0820 DIM U[3]; U=24,U0=3,U[0]=0,U[1]=10,U[2]=10,U[3]=4,T0=1,T1=2,V1=20,U0$="    ",T5$="AA/RAI/C            ",U5$="Customer",U6$="Item code",V2$=V2$(1,10)+V2$(21,10)+V2$(11,10)+V2$(31,10)+V2$(41); GOTO 0850 ! ssp#271826
0830 DIM U[3]; U=23,U0=3,U[0]=0,U[1]=3,U[2]=10,U[3]=10,T0=2,T1=3,V1=3,V2=20,U0$="    ",T5$="S   AA/RAI/C        ",U5$="Product code",U6$="Customer Code",U7$="Item Code",S9$="X",V2$=V2$(1,6)+V2$(7,10)+V2$(27,10)+V2$(17,10)+V2$(37,10); GOSUB 6900; GOTO 0850 ! SSP#271826
0840 DIM U[4]; U=27,U0=4,U[0]=0,U[1]=4,U[2]=3,U[3]=10,U[4]=10,T0=2,V1=4,V2=3,U0$="Y   ",T5$="S   S           ",U5$="Location",U6$="Product code",S9$="X"; GOSUB 6900; GOTO 0850
0845 DIM U[4]; U=28,U0=4,U[0]=0,U[1]=4,U[2]=10,U[3]=10,U[4]=4,T0=4,T1=2,V1=4,V2=10,V3=10,V4=4,U0$="Y    ",T5$="S   AA/RS   S   ",U5$="Salesperson",U6$="Customer",U7$="Item code",U8$="Location"; GOTO 0846; REM SSP 187353
0846 IF V0$(93,1)="Y" THEN U5$="Lot_Salesperson"; GOSUB 4100; REM SSP 187353
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 IF V0$(71,1)="4" THEN V4$(14,1)=CHR(ASC(V4$(14,1))-1); READ (U1,KEY=V4$(1,14),DOM=0991) ELSE READ (U1,KEY=V4$,DOM=0991)
0995 D1$=X3$(21,6); IF V0=4 THEN D1=NUM(V0$(80,3),ERR=0996),D0$=D1$; CALL "ZZDATE",X3$,"",D0$,D1$,-D1,0,0,0,0,0,0,"","",""
0998 F0=0; IF V0=4 THEN F0=NUM(V0$(83,7),ERR=0999)
1000 REM "Read next record
1005 IF V0$(71,1)="4" THEN GOSUB 7900; GOTO 1041 ELSE U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])>=V2$(U3,U[U9]) THEN GOTO 1030 ELSE EXTRACT (U1,KEY=U$(1,U2-1)+V2$(U3,U[U9]),DOM=1021)
1025 EXITTO 1000
1030 IF U$(U2,U[U9])<=V2$(U3+U[U9],U[U9]) THEN GOTO 1040 ELSE READ (U1,KEY=U$(1,U2-1)+$FF$,DOM=1032)
1035 EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$; HOLD_SLSP$=U9$(1,4); REM SSP 187353
1120 ON V0 GOTO 1150,1150,1130,1140,1145
1130 U$=U$(4,20); READ (U1,END=5000); GOTO 1150
1140 U$=U9$(8,20)+U9$(1,4); READ (U1,END=5000); GOTO 1150
1145 U$=U9$(5,24); GOTO 1150
1150 IF V0=2 THEN GOTO 1300 ELSE READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1160 IF POS("0"=V0$(59,12))>0 THEN IF A[3]+A[4]-A[5]+A[6]=0 THEN GOTO 1000 ! SSP#236934
1162 IF MID(V0$,98,1)="Y" THEN IF A[3]+A[4]-A[5]+A[6]<=0 THEN GOTO 1000 ! SSP#289922
1300 REM "Read other data
1320 DIM B$(LEN(B$)); FIND (Z[5],DOM=1321,KEY=U$(1,20))IOL=0320
1325 IF POS("I"=V0$(59,12))<>0 AND MID(B$,108,1)="I" THEN GOTO 1000 ! SSP#289922
1330 H8$=B$
1340 DIM D$(LEN(D$)); FIND (Z[4],KEY="T"+B$(61,3),DOM=1341)IOL=0340
1350 DIM J$(46); FIND (Z[6],KEY=B$(1,10),DOM=1351)IOL=0400
1400 REM "Subtotals"
1415 IF V0<>2 THEN GOSUB 2400
1500 REM "Print Data
1502 IF V0=2 THEN GOTO 2000
1510 IF V0=3 THEN GOSUB 2350
1515 GOSUB 2300
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+A1
1920 IF V0=1 THEN T[T0,2]=T[T0,2]+A0
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
2000 REM "Print locations if product code item sort
2004 REM "IF F$="*" print item description stuff else print just location stuff
2005 F$="*"; READ (Z[1],KEY=U$,DOM=2006)
2010 READ (Z[1],END=2050)IOL=0310
2012 IF MID(V0$,98,1)="Y" THEN IF A[3]+A[4]-A[5]+A[6]<=0 THEN GOTO 2050 ! SSP#289922
2015 IF A$(1,20)<>U$(1,20) THEN GOTO 2050
2020 IF F$="*" THEN GOSUB 2400; GOSUB 2350; F$=""
2030 GOSUB 2300
2035 T[T0,1]=T[T0,1]+A1,T[T0,0]=T[T0,0]+1,T[T0,2]=T[T0,2]+A0
2040 GOTO 2010
2090 GOTO 1000
2200 REM "Print customer heading
2205 IF W+4>W0 THEN GOSUB 6000
2210 CALL "ZZDISP","AX",J$(1,10),"A/R",X3$,X$,"",0,0,X4$; Y5$(1)=X$,Y5$(15)=J$(11,35)
2215 GOSUB 7300
2220 GOSUB 7300
2245 RETURN 
2250 REM "Set heading for salesperson
2260 DIM J1$(50); FIND (Z[9],KEY=U9$(1,4),DOM=2261)J1$
2270 W0$=U9$(1,4)+" - "+FNS$(J1$(5,35))
2295 RETURN 
2300 REM "Print location data
2305 GOSUB 7600
2306 B9=A[2]; IF POS("G"=V0$(59,12))>0 THEN B9=B[34]
2307 IF POS(" "<>V0$(74,4))<>0 THEN GOSUB 8100
2308 IF W+3>W0 THEN GOSUB 6000
2309 IF V0<>3 THEN Y5$(64)=A$(21,4)
2310 Y5$(74)=FNQ$(A0,Z0$),Y5$(93)=STR(B9:Z1$),Y5$(107)=A0$
2315 A1=A0*B9; IF I[0]<>0 THEN A1=A1/I[0]
2317 IF NUM(A$(21,4),ERR=2318)>8999 THEN Y5$(93)="        N/A"
2318 Y5$(107)=A0$
2319 IF V0$(73,1)="Y" AND P0$(44,1)="Y" AND B$(112,1)="Y" THEN GOSUB 2500; GOTO 2326
2320 Y5$(118)=STR(A1:Z0$,ERR=2321)
2323 IF NUM(A$(21,4),ERR=2324)>8999 THEN Y5$(118)="           N/A",A1=0
2325 GOSUB 7300
2340 RETURN 
2350 REM "Print item data
2353 IF W+4>W0 THEN GOSUB 6000
2355 IF V0=4 OR POS(" "<>J$(1,10))=0 THEN GOTO 2360
2356 CALL "ZZDISP","AX",J$(1,10),"A/R",X3$,X$,"",0,0,X4$; Y5$(2)=X$,Y5$(15)=J$(11,35)
2358 GOSUB 7300
2360 CALL "ZZDISP","AX",A$(1,20),"I/C",X3$,X$,"",0,0,X4$; Y5$(1)=X$
2363 GOSUB 8000
2365 Y5$(13)=H9$
2390 RETURN 
2400 REM "Bring subtotal logic here so they can be done right on V0=2
2415 GOSUB 3100
2420 IF V0=2 THEN ON T GOTO 2470,2430,2440,2450,2460
2425 IF V0=3 THEN ON T GOTO 2470,2430,2430,2450,2460
2426 IF V0=4 THEN ON T GOTO 2470,2430,2440,2450,2460
2430 IF V0<>4 THEN GOSUB 6300
2440 IF V0=4 THEN GOSUB 2200 ELSE GOSUB 6200
2460 IF V0=4 THEN GOSUB 2350
2480 T=0
2490 RETURN 
2500 REM "Print Inv Lot Detail
2502 GOSUB 7300; REM "Print Y5$ info
2503 DIM L[12]
2504 F1$="*",L0=0
2505 READ (Z[8],KEY=A$(1,24),DOM=2506)
2510 L9$=KEY(Z[8],END=2580); READ (Z[8],KEY=L9$,DOM=2510)IOL=0385
2511 IF V0=4 AND V0$(93,1)="Y" AND HOLD_SLSP$(1,4)<>L$(263,4) THEN GOTO 2510; REM When sorting by lot salesperson, must have salesperson in lot SSP 187353
2515 IF L$(1,24)<>A$(1,24) THEN GOTO 2579
2516 IF V0=4 THEN IF L$(25,6)>D1$ THEN GOTO 2510
2517 IF V0=4 AND V0$(92,1)="Y" AND L$(110,8)<>DIM(8) THEN GOTO 2510; REM " SSP# 146327
2520 IF F1$="*" THEN GOSUB 2600; F1$=""
2523 IF W+2>W0 THEN GOSUB 6000; GOSUB 2600
2525 Y5$(10)=FND$(L$(25,6))
2526 CALL "ZZDISP","AX",L$(68,10),"A/P",X3$,X$,"",22,0,X4$
2527 IF POS(" "<>L$(78,4))<>0 THEN X$=FNS$(X$)+"/"+L$(78,4)
2528 Y5$(22)=X$
2530 CALL "ZZDISP","AXS",L$(41,9),"P/O",X3$,Y5$,"",38,0,X4$
2540 Y5$(49)=L$(56,12)
2550 GOSUB 7800
2555 IF L[7]<>0 AND L[8]<>0 AND V0$(90,1)="Y" THEN L[0]=Q*L[7]/L[8]
2560 Y5$(118)=STR(L[0]:Z0$)
2565 L0=L0+L[0]
2570 GOSUB 7300
2575 GOTO 2510
2579 IF V0=4 THEN J9$=KEP(Z[8],END=2585); READ (Z[8],KEY=J9$,DOM=2585); REM "To reposition Z(8) correctly for seq 4 report
2580 IF L0<>0 THEN Y5$(118)=T1$(1,LEN(Z0$)); GOSUB 7300; Y5$(118)=STR(L0:Z0$); GOSUB 7300
2582 IF V0=4 THEN L5=L0*F0/100,T[T0,2]=T[T0,2]+L5
2583 IF V0=4 AND F0<>0 THEN Y5$(90)="Finance charge @ "+FNR$(FNS$(STR(F0:"##0.0##"))+" %")+":",Y5$(118)=STR(L5:Z0$); GOSUB 7300
2585 A1=L0
2595 RETURN 
2600 REM "Print lot detail header
2605 IF W+4>W0 THEN GOSUB 6000
2610 Y5$(10)="Receipt Dt",Y5$(22)="Vendor / Plant",Y5$(38)="P/O Number",Y5$(49)="Job Number"
2615 GOSUB 7300
2645 RETURN 
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,X$,"",0,0,X4$; T6$=X$ ! SSP#251640
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOSUB 2250; GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; GOSUB 7000
3155 IF U0$(1,1)="Y" AND T0$<>"END" THEN U1$=U9$(1,V1); GOSUB 2250; GOSUB 6000
3160 U1$=U9$(1,V1)
3180 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$,T6$=U2$; GOSUB 3050; GOSUB 7000
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3260 U2$=U9$(V5,V2)
3280 RETURN 
3310 IF V3=0 THEN RETURN ELSE IF U3$=U9$(V6,V3) THEN GOSUB 3400; RETURN 
3320 GOSUB 3420
3330 IF U3$="" THEN GOTO 3360
3350 T=3,T$=U7$,T6$=U3$; GOSUB 3050; GOSUB 7000
3355 IF U0$(3,1)="Y" THEN GOSUB 6000
3360 U3$=U9$(V6,V3)
3380 RETURN 
3410 IF V4=0 THEN RETURN ELSE IF U4$=U9$(V7,V4) THEN GOSUB 3500; RETURN 
3420 GOSUB 3520
3430 IF U4$="" THEN GOTO 3460
3450 T=4,T$=U8$,T6$=U4$; GOSUB 3050; GOSUB 7000
3455 IF U0$(4,1)="Y" THEN GOSUB 6000
3460 U4$=U9$(V7,V4)
3480 RETURN 
3580 RETURN 
4100 REM "Create Temporary sort by Lot Salesperson ssp 187353    
4102 K9$="Valued Inventory by Salesperson"
4105 CALL "ZZ2SRT",X3$,X4$,Z[13],Z[8],36,K9$,Z,"YY"; ON Z GOTO 4106,4106,9900
4107 Y$="15X "+K9$
4108 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
4109 U1=Z[15],V0=4
4110 CALL "ZZINFO",Z[1],0,X3$,R,0,0,0,0,0,0,Q$+""
4115 V1$=K9$; IF Z=1 THEN GOTO 4180
4120 READ (Z[8],KEY="",DOM=4125)
4130 DIM ICF[12]; READ (Z[8],END=4180,ERR=4180)IOL=0386
4135 K$=ICF$(263,4)+ICF$(1,10)
4136 FIND (Z[6],KEY=ICF$(1,10),DOM=4130)
4140 R0=R0+1; WRITE (Z[15],KEY=K$)
4145 IF MOD(R0,50)=0 THEN CALL "ZZBARG",X3$,"HG",23,12,30,D0,R,R0
4160 GOTO 4130
4190 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; IF V0=3 THEN GOSUB 6000; GOSUB 7000 ELSE GOSUB 7000
5200 REM "
5290 GOTO 9900
6000 REM "Page header
6010 IF POS(" "<>V0$(74,4))<>0 THEN W2$="For Fiscal Year "+V0$(74,4)+" Accounting Period "+V0$(78,2)
6020 IF V0=4 THEN GOTO 6031 ELSE IF V0<>3 OR T$="Report" THEN W0$=""; GOTO 6031
6025 DIM C$(LEN(C$)); FIND (Z[3],KEY="D"+C$(1,10)+U1$,DOM=6026)IOL=0330
6030 W0$=U1$+" - "+FNS$(C$(16,35)); IF POS(" "<>C$(111,16))<>0 THEN W0$=W0$+" - "+FNS$(C$(111,16))+", "+C$(127,2)
6035 IF T0$="END" THEN W0$=""
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6060 ELSE ON W5 GOTO 6041,9910
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6115 Y5$(3)="Item",Y5$(97)="Average"
6120 GOSUB 7300
6125 Y5$(3)="Code",Y5$(13)="Description",Y5$(79)="On hand",Y5$(99)="cost",Y5$(108)="UM",Y5$(120)="Extension"
6126 IF V0<>3 THEN Y5$(62,8)="Location"
6130 GOSUB 7300
6199 RETURN 
6200 REM "Break #1 header
6201 RETURN 
6210 IF W+2>W0 THEN GOSUB 6000
6220 Y5$(1)=A$(21,4),Y5$(7)=C$(16,35)
6225 GOSUB 7300
6230 GOSUB 7300
6299 RETURN 
6300 REM "Break #2 header
6305 IF V0=1 THEN GOSUB 2350; RETURN 
6310 IF W+2>W0 THEN GOSUB 6000
6320 Y5$(3)=B$(61,3),Y5$(11)=D$(5,15)
6325 GOSUB 7300
6399 RETURN 
6900 REM "SORT
6905 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6940 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,X9
6990 RETURN 
7000 REM "Totals logic
7001 IF T=T0 OR (V0=4 AND T=T0-1) THEN IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(118)=T1$(1,14); IF V0<>3 AND T=T0 THEN Y5$(74,14)=T1$(1,14); GOSUB 7300 ELSE GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" Total"+" ("+STR(T[T,0]:"0")+"):"
7020 Y5$(118)=FNQ$(T[T,1],Z0$); IF V0<>3 AND T=T0 THEN Y5$(74,LEN(Z0$))=FNQ$(T[T,2],Z0$); GOSUB 7300 ELSE GOSUB 7300
7040 IF V0=4 AND F0<>0 THEN Y5$(90)="Finance charge @ "+FNR$(FNS$(STR(F0:"##0.0##"))+" %")+":",Y5$(118)=STR(T[T,2]:Z0$); GOSUB 7300
7065 IF T$="Report" THEN Y5$(118)=T2$(1,14); GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed"+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600),T4=MOD(T3,60),T3=INT(T3/60)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")+":"+STR(T4:"00")
7260 Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7330 REM 
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"  ")-1)
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
7600 REM "Figure numerics, put total into A0, UM string into A0$
7601 REM "This will change the values of A() also
7605 DIM I$(20),I[1]
7610 FIND (Z[13],KEY="U/M"+B$(124,4),DOM=7611)IOL=0390
7620 IF I$(20,1)="Y" THEN I0=I[1],A0$=B$(124,4) ELSE I0=B[15],A0$=FNS$(B$(124,4))+"/"+FNR$(STR(B[15]:Z2$))
7625 IF I0=0 THEN I0=1
7630 FOR I=3 TO 8; A[I]=A[I]/I0; NEXT I
7640 A0=A[3]+A[4]-A[5]+A[6]
7645 RETURN 
7800 REM "quantity explantion line
7805 Q$=FNR$(FNQ$(L(4),Z0$))
7810 IF L[4]=1 THEN Q$=Q$+" "+FNS$(L$(121,4))+" of " ELSE Q$=Q$+" "+FNS$(L$(121,4))+"s of "
7815 IF L$(125,4)="    " THEN Q$=Q$+FNR$(STR(L[3])) ELSE Q$=Q$+FNR$(STR(L[2]))+FNC$(L$(125,4)); IF L[2]=1 THEN Q$=Q$+" of "+FNR$(STR(L[3])) ELSE Q$=Q$+"s of "+FNR$(STR(L[3]))
7820 IF L$(125,4)="    " THEN Q=L[4]*L[3] ELSE Q=L[4]*L[2]*L[3]
7825 Y5$(68)=Q$+" = ",Y5$(105)=STR(Q:Z2$)
7845 RETURN 
7900 REM "Read for type 4 report, read ICF as long as customer matches, then read next customer by salesperson, create correct U$ key as salesperson code + custmer code + item code + location code
7905 J9$="",J9$=KEY(Z[8],END=7906)
7906 REM PRINT "K0$=",K0$,"| K1$=",K1$,"| NEXT KEY=",J9$,"|",; INPUT *
7910 IF K0$<>"" THEN K1$=KEY(Z[8],END=7915); IF K1$(1,10)=K0$(5,10) THEN IF K1$(25,6)>D1$ THEN READ (Z[8]); GOTO 7910 ELSE GOTO 7940
7915 K0$=KEY(U1,END=5000); READ (U1)
7920 REM READ (Z(8),KEY=K0$(5,10),DOM=07910) ; GOTO 07910
7925 IF K0$(1,4)<V2$(1,4) THEN READ (U1,KEY=V2$(1,4),DOM=7915); GOTO 7915 ELSE IF K0$(1,4)>V2$(5,4) THEN GOTO 5000; REM "Salesperson range check
7927 IF K0$(5,10)<V2$(9,10) THEN EXTRACT (U1,KEY=K0$(1,4)+V2$(9,10),DOM=7915); GOTO 7915 ELSE IF K0$(5,10)>V2$(19,10) THEN GOTO 7915
7928 READ (Z[8],KEY=K0$(5,10),DOM=7910); GOTO 7910
7930 IF K1$(11,10)<V2$(29,10) THEN READ (Z[8],KEY=K1$(1,10)+V2$(29,10),DOM=7910); GOTO 7910 ELSE IF K1$(11,10)>V2$(39,10) THEN GOTO 7915
7940 IF K1$(21,4)<V2$(49,4) OR K1$(21,4)>V2$(53,4) THEN READ (Z[8]); GOTO 7910 ELSE READ (Z[8])
7941 IF K1$(11,10)<V2$(29,10) OR K1$(11,10)>V2$(39,10) THEN GOTO 7910
7942 U$=K0$(1,4)+K1$(1,24)
7945 RETURN 
8000 REM "Build desc from Y8$
8005 IF POS("D"=V0$(59,12))>0 THEN H9$=H8$(21,40); RETURN 
8010 H9$=FNS$(H8$(64,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+"x"
8020 H9$=H9$+FNS$(H8$(72,8)); IF H9$<>"" THEN IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
8030 IF NUM(H8$(80,2))<>0 THEN H9$=H9$+H8$(80,2)+" Ply "
8040 H9$=H9$+FNS$(H8$(21,40)); H8$=""
8050 RETURN 
8100 REM "Figure: A0=total quantity, B9=avg cost. Extension (A1) is figured later based on A0 & B9
8101 REM "Adjust A0, and B9 by subtracting qty and amts found in IC8 file for records that have a FY (V0$(74,4)) and AP (V0$(78,2)) later than the requested one.
8103 REM "Read with the item#, location code, fy supplied and ap supplied+1 to start, stop reading when item# or location code changes. This will get all records greater than the supplied FY and AP.
8104 DIM G$(72),G[1]
8105 K5$=A$(1,24)+V0$(74,4)+STR(NUM(V0$(78,2))+1:"00")
8110 READ (Z[7],KEY=K5$,END=8140,DOM=8111)
8115 READ (Z[7],END=8140)IOL=0370
8120 IF G$(1,24)<>K5$(1,24) THEN GOTO 8140
8125 G0=A0*B9; IF I[0]<>0 THEN G0=G0/I[0]; REM "See line 2315 for analagous equation for A1
8126 G1=G0-G[1]
8127 IF I[0]<>0 THEN G1=G1*I[0]
8130 A0=A0-G[0]/I0
8131 IF A0=0 THEN B9=0 ELSE B9=G1/A0
8135 GOTO 8115
8145 RETURN 
8900 REM "Function Statements
8905 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
8920 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
8940 DEF FNC$(Z9$)=Z9$(1,POS(" "=Z9$+" ")-1)
8950 DEF FNQ$(Z9,Z9$)=STR(Z9:Z9$(1,POS(".00"=STR(Z9:Z9$)+".00")-1))
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 IF %GUI THEN EXIT 
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 RUN "ZMENU"
9999 END 
56000 REM "187353-Modify Valued Inventory Report (P/O, R4) to have a new sor
56001 REM "236934-Valued Inventory report does not skip items with zero on hand like it should
56002 REM "271826-I/C R4.  Valued Inventory Report  does not work correctly   
