0010 REM "Statistical Data Report <IC2RRA>
0035 REM "5.7 - 08/14/14 - 9.854444 - tma - SSP# 272026
0037 REM "272026-IC Statistical data report, sequence 2 ranges not correct   
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "This program has been modified spec. for I/C stuff
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="IC2RRA",X1$="Statistical Data Report"
0120 DIM A$(17),A[14],B$(52),E$(20),F$(214),J$(46)
0140 Z0$="-###,###,###.00",Z1$="+###.0%",Z0=LEN(Z0$),Z2$="##,##0"
0160 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0175 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
0210 T=1,T0=0,T1=8
0220 W3=127
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "I/O lists
0305 REM "IOLIST FOR GL3 - G/L Account Balances
0310 IOLIST A$
0315 REM "IOLIST FOR GL1 - General Ledger Master
0330 IOLIST C$
0340 IOLIST D$,D[0],D[1],D[2],D[3],D[4],D[5],D[6],D[7],D[8],D[9],D[10],D[11],D[12],D[13],D[14],D[15],D[16],D[17],D[18],D[19]
0350 IOLIST E$
0360 IOLIST F$
0390 IOLIST I$,I[0],I[1]
0400 IOLIST J$
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$,K0
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O IC9...  04O IC0...  03O IC6...  05O IC5...  06O FMP...  07O FM0...  08O AR1...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z9,Z1
0700 REM "Open Printer
0720 REM 
0770 IF V0=1 THEN V2$=V2$(1,10)+V2$(21,10)+V2$(11,10)+V2$(31)
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[3]; U=24,U0=3,U[0]=0,U[1]=10,U[2]=10,U[3]=4,T0=3,T1=4,V1=10,V2=10,V3=4,U0$="    ",T5$="AA/RS           ",U5$="Customer code",U6$="Item code"; GOTO 0850
0830 DIM U[3]; U=23,U0=3,U[0]=0,U[1]=3,U[2]=10,U[3]=10,T0=2,T1=4,V1=3,V2=20,U0$="    ",T5$="S   AI/C        ",U5$="Product code",U6$="Item code",V2$=V2$(1,6)+V2$(7,10)+V2$(27,10)+V2$(17,10)+V2$(37); GOTO 0850 ! SSP#272026
0840 DIM U[4]; U=27,U0=4,U[0]=0,U[1]=4,U[2]=3,U[3]=10,U[4]=10,T0=2,T1=4,V1=4,V2=3,U0$="Y   ",T5$="S   S           ",U5$="Location",U6$="Product code"; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0870 GOSUB 7800
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM " Position read"
0905 REM "set key file to read from"
0910 IF V0=1 THEN U1=Z[1] ELSE IF V0=2 THEN U1=Z[5] ELSE U1=Z[3]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(U1,END=5000)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])>=V2$(U3,U[U9]) THEN GOTO 1030 ELSE EXTRACT (U1,KEY=U$(1,U2-1)+V2$(U3,U[U9]),DOM=1021)
1025 EXITTO 1000
1030 IF U$(U2,U[U9])<=V2$(U3+U[U9],U[U9]) THEN GOTO 1040 ELSE READ (U1,KEY=U$(1,U2-1)+$FF$,DOM=1032)
1035 EXITTO 1000
1040 NEXT U9
1045 U9$=U$
1050 ON V0 GOTO 1051,1051,1055,1060
1051 IF U$(25,4)<>V0$(74,4) OR U$(29,1)<>V0$(80,1) THEN READ (U1)REC$; GOTO 1000 ELSE GOTO 1090
1055 READ (U1); U$=U$(4,20); GOTO 1080
1060 READ (U1); U$=U$(8,20)+U$(1,4)
1090 DIM D$(128),D[19]; READ (Z[4],KEY=U$(1,20),DOM=1091)IOL=0340
1091 H8$=D$
1092 GOSUB 8000
1095 DIM I$(20),I[1]; READ (Z[13],KEY="U/M"+D$(124,4),DOM=1096)IOL=0390
1096 IF I$(20,1)="Y" THEN M9=I[1] ELSE M9=D[15]
1097 A$=U$; IF M9=0 THEN M9=1
1098 IF V0=2 THEN GOTO 2000
1100 REM "Base year data
1105 DIM B[6],C[6]
1120 Q1$=U$(1,24)+V0$(74,4),Q0$=V0$(80,1),B$=Q1$
1125 GOSUB 7700
1130 B[0]=B0,B[1]=B1
1150 REM "Prior year
1155 Q1$=U$(1,24)+K0$,C$=Q1$
1160 GOSUB 7700
1165 C[0]=B0,C[1]=B1
1190 IF V0=1 THEN READ (Z[1],KEY=U9$,DOM=1191) ELSE IF V0=2 THEN READ (Z[1],KEY=U$,DOM=1191)
1300 REM "Read other data
1310 DIM J$(46); FIND (Z[8],KEY=D$(1,10),DOM=1311)IOL=0400
1400 REM "Subtotals"
1415 IF V0<>2 THEN GOSUB 2400
1500 REM "Print Data
1505 IF W+2>W0 THEN GOSUB 6000
1507 IF POS(" "=J$(1,10))<>0 THEN GOTO 1515
1508 CALL "ZZDISP","AX",J$(1,10),"A/R",X3$,X$,"",0,0,X4$; Y5$(2)=X$,Y5$(15)=J$(11,35)
1509 GOSUB 7300
1515 CALL "ZZDISP","AX",A$(1,K0),V1$(46,3),X3$,X$,"",0,0,X4$; Y5$(1)=X$
1520 Y5$(12)=H9$,Y5$(54)=B$(25,4); REM +" "+Q0$
1525 Y5$(61+Z0)=FNQ$(B[0],Z0$),Y5$(77+Z0)=FNQ$(B[0]-C[0],Z0$),Y5$(96+Z0)=FNQ$(B[1],Z0$),Y5$(112+Z0)=FNQ$(B[1]-C[1],Z0$)
1535 GOSUB 7300
1537 IF V0<>3 THEN A1$="Location: "+U$(21,4)+"   " ELSE A1$=""
1538 IF Q0$="U" THEN IF I$(20,1)="Y" THEN A2$="UM: "+D$(124,4) ELSE A2$="UM: "+FNS$(D$(124,4))+"/"+FNR$(STR(D[15]:Z2$))
1539 Y5$(12)=A1$+A2$
1540 Y5$(54)=C$(25,4),Y5$(61+Z0)=FNQ$(C[0],Z0$); IF C[0]=0 THEN Y5$(85)=STR(100:Z1$) ELSE Y5$(85)=STR(100*(B[0]-C[0])/C[0]:Z1$)
1545 Y5$(96+Z0)=FNQ$(C[1],Z0$); IF C[1]=0 THEN Y5$(120)=STR(100:Z1$) ELSE Y5$(120)=STR(100*(B[1]-C[1])/C[1]:Z1$)
1555 GOSUB 7300
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+B[0],T[T0,2]=T[T0,2]+C[0],T[T0,3]=T[T0,3]+B[1],T[T0,4]=T[T0,4]+C[1]
1990 T[T0,0]=T[T0,0]+1
1995 IF V0=2 THEN GOTO 2040 ELSE GOTO 1000
2000 REM "Print locations if product code item sort
2004 REM "IF F$="*" print item description stuff else print just location stuff
2005 F$="*"; READ (Z[1],KEY=U$(1,20),DOM=2006)
2010 READ RECORD (Z[1],END=2050)A$
2012 IF A$(25,4)<>V0$(74,4) OR A$(29,1)<>V0$(80,1) THEN GOTO 2010
2015 IF A$(1,20)<>U$(1,20) THEN GOTO 2050
2020 IF F$="*" THEN GOSUB 2400; F$=""
2025 U$=A$(1,29)
2030 GOTO 1100
2040 GOTO 2010
2090 GOTO 1000
2400 REM "Bring subtotal logic here so they can be done right on V0=2
2415 GOSUB 3100
2420 ON T GOTO 2470,2430,2430,2450,2460
2430 GOSUB 6300
2480 T=0
2490 RETURN 
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,X$,"",0,0,X4$; T6$=X$ ! SSP#252096
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; IF Q0$<>"U" THEN GOSUB 3050; GOSUB 7000
3154 U1$=U9$(1,V1); IF V0=3 THEN DIM F$(214); FIND (Z[7],KEY="D"+F$(1,10)+U1$,DOM=3155)IOL=0360
3155 IF U0$(1,1)="Y" AND T0$<>"END" THEN GOSUB 6000; GOTO 3161
3160 U1$=U9$(1,V1); IF V0=3 THEN DIM F$(214); FIND (Z[7],KEY="D"+F$(1,10)+U1$,DOM=3161)IOL=0360
3180 RETURN 
3200 REM "Second highest order break"
3210 IF V2=0 THEN RETURN ELSE IF U2$=U9$(V5,V2) THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$,T6$=U2$; GOSUB 3050; GOSUB 7000
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3260 U2$=U9$(V5,V2)
3280 RETURN 
3300 REM "Next to lowest order break"
3310 IF V3=0 THEN RETURN ELSE IF U3$=U9$(V6,V3) THEN GOSUB 3400; RETURN 
3320 GOSUB 3420
3330 IF U3$="" THEN GOTO 3360
3350 T=3,T$=U7$,T6$=U3$; GOSUB 3050; GOSUB 7000
3355 IF U0$(3,1)="Y" THEN GOSUB 6000
3360 U3$=U9$(V6,V3)
3380 RETURN 
3480 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 IF Q0$<>"U" THEN T=0,T$="Report"; IF V0=3 THEN GOSUB 6000; GOSUB 7000 ELSE GOSUB 7000
5200 REM "
5290 GOTO 9900
6000 REM "Page header
6030 IF V0<>3 OR T$="Report" THEN W0$="" ELSE W0$=U1$+" - "+FNS$(F$(16,35)); IF POS(" "<>F$(111,16))<>0 THEN W0$=W0$+" - "+FNS$(F$(111,16))+", "+F$(127,2)
6035 IF T0$="END" THEN W0$="" ELSE DIM W0$(80) ! SSP#252096
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6060 ELSE ON W5 GOTO 6041,9910
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6115 Y5$(61)="+------ Period",Y5$(77)=STR(P0:"00")+" ------------+",Y5$(95)="+----- Year to Date -----------+"
6120 GOSUB 7300
6125 Y5$(53)="Fiscal",Y5$(65)="This year/",Y5$(84)="Change/",Y5$(100)="This year/",Y5$(119)="Change/"
6130 GOSUB 7300
6135 Y5$(1)="Code",Y5$(17)="Description",Y5$(54)="Year",Y5$(65)="Prior year",Y5$(83)="% change",Y5$(100)="Prior year",Y5$(118)="% change"
6140 GOSUB 7300
6199 RETURN 
6300 REM "Break #2 header
6305 IF V0=1 THEN RETURN 
6307 IF D$(61,3)<>E$(2,3) THEN DIM E$(20); FIND (Z[6],KEY="T"+D$(61,3),DOM=6308)IOL=0350
6310 IF W+2>W0 THEN GOSUB 6000
6320 Y5$(3)=D$(61,3),Y5$(8)=E$(5,15)
6325 GOSUB 7300
6399 RETURN 
7000 REM "Totals logic
7001 IF T=T0 AND T[T,0]<2 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(61)=T1$(1,15),Y5$(77)=T1$(1,15),Y5$(96)=T1$(1,15),Y5$(112)=T1$(1,15); GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" Total ("+STR(T[T,0]:"0")+"):"
7020 Y5$(61+Z0)=FNQ$(T[T,1],Z0$),Y5$(77+Z0)=FNQ$(T[T,1]-T[T,2],Z0$),Y5$(96+Z0)=FNQ$(T[T,3],Z0$),Y5$(112+Z0)=FNQ$(T[T,3]-T[T,4],Z0$); GOSUB 7300
7025 Y5$(61+Z0)=FNQ$(T[T,2],Z0$); IF T[T,2]=0 THEN Y5$(85)=STR(100:Z1$) ELSE Y5$(85)=STR(100*(T[T,1]-T[T,2])/T[T,2]:Z1$)
7027 Y5$(96+Z0)=FNQ$(T[T,4],Z0$); IF T[T,4]=0 THEN Y5$(120)=STR(100:Z1$) ELSE Y5$(120)=STR(100*(T[T,3]-T[T,4])/T[T,4]:Z1$)
7028 GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed: "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 IF INT(T2)>0 THEN T4$=STR(INT(T2))+" Hours " ELSE T4$=""
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+STR(T3:"##.##")+" Minutes"
7260 Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7330 REM 
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7460 V0=NUM(V0$(71,1))
7480 X3$(178,7)=Y4$,W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1)
7485 K0=NUM(V0$(81,2)),K0$=STR(NUM(V0$(74,4))-1:"00"),P0=NUM(V0$(78,2))
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
7700 REM "GET DATA
7710 DIM A[14]
7720 CALL "ZZPACK",X3$,"R","","",0,0,A{ALL},Z[1],Q0$,Q1$,V1$
7725 IF M0$="0" THEN FOR I=0 TO P0; A[I]=A[I]*100; NEXT I
7730 B0=0,B1=0
7740 FOR X=0 TO P0
7745 IF Q0$="U" THEN A[X]=A[X]/M9
7755 B1=B1+A[X]
7760 NEXT X
7785 B0=A[P0]
7790 RETURN 
7800 REM "Set mask Z0$ based on parms
7810 M0=POS(V0$(80,1)=V1$(49),17),Z0=LEN(Z0$),M0$=" "
7815 IF M0<>0 THEN CALL "ZZMASK",V1$(M0+50,3),Z0$; Z0=Z0-LEN(Z0$),M0$=V1$(M0+51,1)
7840 RETURN 
8000 REM "Build desc from Y8$
8005 IF POS("D"=V0$(59,12))>0 THEN H9$=H8$(21,40); RETURN 
8010 H9$=FNS$(H8$(64,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+"x"
8020 H9$=H9$+FNS$(H8$(72,8)); IF H9$<>"" THEN IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
8030 IF NUM(H8$(80,2))<>0 THEN H9$=H9$+H8$(80,2)+" Ply "
8040 H9$=H9$+FNS$(H8$(21,40)); H8$=""
8050 RETURN 
8950 DEF FNQ$(Z9,Z9$)=STR(Z9:Z9$)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 IF %GUI<>0 AND Y4$<>"" THEN CALL Y4$,ERR=*NEXT
9928 IF %GUI THEN EXIT 
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 RUN "ZMENU"
9999 END 
56000 REM "272026-IC Statistical data report, sequence 2 ranges not correct   
