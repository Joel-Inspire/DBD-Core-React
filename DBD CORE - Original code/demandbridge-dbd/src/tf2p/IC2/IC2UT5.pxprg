0010 REM "Auto combine like Lots <IC2UT5>
0020 SETESC 9300; SETERR 9000
0035 REM "5.5 - 01/11/07 - 10.976388 - crg - SSP# 200525
0037 REM "200525-Ability to have storage cost and finance costs for inventory
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0095 F$="CALLED"
0100 SETERR 9000
0110 X0$="IC2UT5",X1$="Auto Combine Like Lot Records"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 IF X3$="" THEN CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12]
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O ICF...  02OSICF...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0550 DIM M6[1]; CALL "IC2PRE",X3$,Z[13],"",M6{ALL},"",0,"",0
0560 IF F$="CALLED" THEN GOTO 0700
0600 REM "
0610 GOSUB 6000
0640 IF Q1$<>"*" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0990 READ (Z[1],KEY="",DOM=0991)
1000 REM "Begin process
1010 DIM A$(269),A[12],B$(269),B[12]
1100 REM "Read File looking for records
1101 REM "Skip committed and PI records
1105 READ (Z[1],END=5000)IOL=0310
1107 PRINT @(15,15),A$(1,10)," ",A$(11,10)," ",A$(21,4)," ",A$(31,2),
1110 IF POS(" "<>A$(110,10))<>0 THEN GOTO 1105
1200 REM "Look for a match. If it matches, combine cost, frt & qty then remove.
1201 REM "If no match then start again.
1202 REM "Use seperate file, so we don't lose our place
1203 REM "Skip committed and PI records
1205 READ (Z[2],KEY=A$(1,32),DOM=1206)
1210 READ (Z[2],END=1290)IOL=0320
1212 IF B$(1,30)<>A$(1,30) THEN GOTO 1290
1213 PRINT @(15,17),B$(1,10)," ",B$(11,10)," ",B$(21,4)," ",B$(31,2),
1215 IF POS(" "<>B$(110,11))>0 OR POS(" "<>B$(221,11))>0 THEN GOTO 1210
1220 IF B$(33,182)=A$(33,182) AND B$(234,26)=A$(234,26) AND B[2]=A[2] AND B[3]=A[3] AND B[5]=A[5] AND B[6]=A[6] AND B[7]=A[7] AND B[8]=A[8] THEN GOSUB 3000
1225 GOTO 1210
1290 GOTO 1100
3000 REM "Combine records
3010 A[0]=A[0]+B[0],A[1]=A[1]+B[1],A[4]=A[4]+B[4]
3015 A[11]=+A[11]+B[11],A[12]=A[12]+B[12] ! 200525 - Combine storage and finance charges
3020 WRITE (Z[1],KEY=A$(1,32))IOL=0310
3025 REMOVE (Z[2],KEY=B$(1,32))
3095 RETURN 
5000 REM "EOJ"
5010 IF F$="CALLED" THEN GOTO 9900
5020 PRINT @(0,15),'CE',
5030 IF Q1$<>"*" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(5,4),"This program will try to combine lots in the ICF Lotted Inventory file."
6025 PRINT @(5,5),"Lots will be combined if every field matches except for these:",
6026 PRINT @(5,6),"      Sequence #, Total Cost, Total Freight, Quantity",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9011 IF MID(Q0$,1,2)="JS" THEN A1$=$FF$+X0$+"|Error|"+STR(Y5)+"|Line|"+STR(Y6)+"|"; GOTO 9900
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56002 REM "200525-Ability to have storage cost and finance costs for inventory
