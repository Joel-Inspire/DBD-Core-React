0010 REM "Automatic Lot Setup <IC2UT4>
0020 SETESC 9300; SETERR 9000
0035 REM "5.6 - 11/14/07 - 17.437222 - dmm - SSP# 212146
0037 REM "212146-New company name. Menu, documentation, etc.                 
0040 REM "Copyright 2007 DemandBridge, Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="IC2UT4",X1$="I/C Automatic Lot Setup"
0120 DIM Z0$(80,"-"),A[30],B[20],C[6],F$(20),F[1]
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30]
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19],B[20]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6]
0340 IOLIST D$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O IC0...  02O IC1...  03L ICF...  04O AR1...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0585 RT$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,RT$
0600 REM "
0610 GOSUB 6000
0680 X$="If customer is entered then only that customer's lot records will be cleared"
0700 CALL "ZZENTR","Y",F{ALL},F$,X4$,X3$,60,8,6,1,C0,"","{3"+X$,"","IC2UT400","","",""; X$=""
0710 CALL "ZZENTR","Y",F{ALL},F$,X4$,X3$,60,10,1,1,C0,"","{3"+X$,"","IC2UT402","","",""
0720 CALL "ZZENTR","Y",F{ALL},F$,X4$,X3$,60,12,2,1,C0,"","{3"+X$,"","IC2UT404","","",""
0730 CALL "ZZENTR","SUZ",F{ALL},F$,X4$,X3$,60,14,3,3,C0,"","{3"+X$,"","IC2UT406","","",""
0740 F$(7,10)=""; CALL "ZZENTR","AXU",F{ALL},F$,X4$,X3$,60,16,7,10,C0,"A/R","{3"+X$,"","IC2UT408","","",""
0750 IF C0>0 THEN FIND (Z[4],KEY=F$(7,10),DOM=0740)IOL=0340; PRINT @(35,17),D$(11,40),; F9$="Y" ELSE F9$=""
0760 CALL "ZZENTR","Y",F{ALL},F$,X4$,X3$,60,18,18,1,C0,"","{3"+X$,"","IC2UT410","","",""
0900 CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0901,9900
0910 IF F$(6,1)<>"Y" THEN GOTO 0980 ELSE IF F9$="" THEN CALL "ZZINIT",STR(Z[3]:"00"); GOTO 0980
0925 REM "Clear Lot records for this customer only
0930 READ (Z[3],KEY=F$(7,10),DOM=0931)
0935 K$=KEY(Z[3],END=0980)
0940 IF K$(1,10)<>F$(7,10) THEN GOTO 0980
0950 REMOVE (Z[3],KEY=K$)
0960 GOTO 0935
0980 READ (Z[1],KEY=F$(7,10),DOM=0981)
0990 PRINT @(20,18),"Item:",
1000 REM 
1020 READ (Z[1],END=5000)IOL=0310
1025 IF F9$="Y" THEN IF F$(7,10)<>A$(1,10) THEN GOTO 5000
1030 PRINT @(27,18),A$(1,10)," ",A$(11,10),
1035 IF F$(3,3)<>"   " THEN IF A$(61,3)<>F$(3,3) THEN GOTO 1000
1040 IF A$(112,1)="Y" THEN GOTO 1100
1050 IF F$(1,1)<>"Y" THEN GOTO 1000
1060 EXTRACT (Z[1],DOM=1000,KEY=A$(1,20)); A$(112,1)="Y"
1070 WRITE (Z[1])IOL=0310
1072 IF RT$>"" THEN IF RT$(7,1)="H" THEN CUST$=A$(1,10); CALL "RT2WOC",ERR=1073,X3$,X4$,CUST$,"IC0...","U",A$(1,20)
1100 REM "Warehouse records
1120 READ (Z[2],KEY=A$(1,20),DOM=1121)
1140 READ (Z[2],END=1900)IOL=0320
1150 IF B$(1,20)<>A$(1,20) THEN GOTO 1900
1180 Q=B[3]+B[4]-B[5]+B[6]
1200 IF F$(6,1)<>"Y" THEN GOSUB 3500
1220 GOSUB 3000
1290 GOTO 1140
1900 REM 
1990 GOTO 1000
3000 REM "Set up lot detail
3020 DIM C$(269),C[6]
3040 C$(1,24)=B$(1,24),C$(25,6)=X3$(21,6),C$(31,2)="01"
3060 C$(68,14)=A$(82,14)
3080 C$(100,10)=B$(35,10)
3090 C$(156,40)="Created using automatic lot set-up"
3100 C$(121,4)="CASE"
3160 IF F$(2,1)="Y" THEN GOSUB 3400
3180 C[4]=1,C[3]=Q
3200 GOSUB 3300
3290 RETURN 
3300 REM "WRITE LOT RECORD
3305 Q1=C[3]*C[4]
3310 IF Q1=0 THEN GOTO 3390
3315 IF F$(18,1)="Y" THEN CALL "FM2EXT",Z[13],0,A$(124,4),A[15],Q1,A[18],E,2; GOTO 3330
3320 CALL "FM2EXT",Z[13],0,A$(124,4),A[15],Q1,B[2],E,2
3330 C[0]=E
3380 WRITE (Z[3],KEY=C$(1,32),DOM=3381)IOL=0330; GOTO 3390
3385 IF C$(31,2)<>"99" THEN C$(31,2)=STR(NUM(C$(31,2))+1:"00"); GOTO 3380 ELSE ESCAPE 
3390 RETURN 
3400 REM "Use carton pack
3420 IF A[19]=0 THEN GOTO 3490
3440 C[4]=INT(Q/A[19]),C[3]=A[19]
3460 GOSUB 3300
3480 Q=Q-C[4]*C[3],C$(31,2)="02",C$(155,1)="Y"
3490 RETURN 
3500 REM "Subtract off existing lots
3510 READ (Z[3],KEY=B$(1,24),DOM=3511)
3520 READ (Z[3],END=3590)IOL=0330
3530 IF C$(1,24)<>B$(1,24) THEN GOTO 3590
3540 IF C[2]<>0 THEN Q=Q-C[4]*C[3]*C[2] ELSE Q=Q-C[4]*C[3]
3560 GOTO 3520
3590 RETURN 
5000 REM "EOJ
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This DBI utility program will process Item Master and set up a lot record",'LF',"in ICF for each item's on-hand balance.",
6025 PRINT @(24,8),"Clear the Lot Inventory File?",
6030 PRINT @(9,10),"Set lot flag to 'Y' for all items processed?",
6040 PRINT @(0,12),"Use item master's carton pack when setting up lot(s)?",
6050 PRINT @(9,14),"Enter a product code or leave blank for all:",
6060 PRINT @(8,16),"Enter a customer code or leave blank for all:",
6070 PRINT @(23,18),"Use the average cost from IC0?",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "212146-New company name. Menu, documentation, etc.
