0010 REM "UnForm Control Program<EF2UNF>
0015 REM "Prog Type: IP-3.1.2
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 02/09/21 - 14.633254 - jvv - SSP# 307332
0037 REM "307332-Create combined pdf of checks in a batch                    
0040 REM "Copyright 2021 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0069 REM "R$ is Electronic Forms Parameters
0070 REM "COMM$ is command string, "N" = new item
0071 REM "DEVICE is basic device number
0072 REM "RETURN$ is return information
0073 REM "JOB_MODIFIER$ is added to job name to modify the name to accomodate user defined specs, etc.
0074 REM "FORM_PARM$ is the data to go on a job card,plus parameters that may allow/disallow modifiers etc.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,R$,COMM$,DEVICE,FORM_PARM$,JOB_MODIFIER$,RETURN$,PDFCHL,PDFKY$,ERR=*NEXT ! SSP 307332
0095 ! IF WHO="crg" THEN ESCAPE 
0100 SETERR 9000
0110 X0$="EF2UNF",X1$="UnForm Control Program"
0900 REM 
0905 RETURN$=""
0910 IMGSTR$="" ! SSP 248993
1000 REM "Main parm
1010 ON POS(COMM$(1,1)="NE") GOTO 1495,NEW_FORM,NEXT_FORM_PAGE,1495
1050 NEW_FORM:REM "start a new item
1052 OPEN (HFN)"ZZPARM"; ZZPARM=LFO
1060 GOSUB DEVICE_SETUP
1070 CLOSE (ZZPARM)
1095 NEW_FORM_END:GOTO 1495
1099 ! 
1100 NEXT_FORM_PAGE:REM "issue form feed for next page
1120 PRINT (DEVICE)'FF',
1145 NEXT_FORM_PAGE_END:GOTO 1495
1495 GOTO 9900
2000 DEVICE_SETUP:REM "Reopen printer providing it with required options
2010 CURRENT_PREC=PRC; PRECISION 4; TIME=TIM; FILE_NAME$="/tmp/UFOPT."+FID(0)+STR(TIME*1000:"00000")+STR(RND(10000):"0000"); PRECISION CURRENT_PREC; REM "Generate unique name combining the task id and the time to 3600/10000'ths of a hour. Store and preserve the current precision SSP 266120
2012 IF %UFPRM_CHAN THEN CLOSE (%UFPRM_CHAN,ERR=*PROCEED) END_IF ; SERIAL FILE_NAME$,ERR=DEVICE_SETUP; OPEN PURGE (GFN)FILE_NAME$; %UFPRM_CHAN=LFO ! Global channel to Unform job parameters/options SSP 266120
2015 JOB_CARD$=JOB_CARD$+"-zx "+FILE_NAME$+" -o "+QUO+"/dev/null"+QUO+" " ! "/dev/%d%t-%j.pdf"+QUO+" " 
2020 DEV_CHANNEL=DEVICE,DEVICE_NAME$=FID(DEVICE); REM "determine correct channel to close
2025 CLOSE (DEV_CHANNEL,ERR=*NEXT)
2055 REM "If job card data is not blank put out 'job card' line
2060 IF STP(FORM_PARM$(12,60),3," ")="" AND JOB_MODIFIER$="" THEN GOTO *NEXT ELSE GOSUB FIX_UP_JOB_CARD ! ; PRINT (DEV_CHANNEL)JOB_CARD$
2070 OPEN (DEV_CHANNEL,OPT=JOB_CARD$)DEVICE_NAME$
2095 DEVICE_SETUP_END:RETURN 
2100 FIX_UP_JOB_CARD:REM "Create a job card line from FORM_PARM$(12 ,60) and the JOB_MODIFIER$ also check FORM_PARM$(72,1) to see if job modifier is allowed
2105 TMP$=STP(FORM_PARM$(12,60),1); DEL_OPT$=MID(FORM_PARM$,74,3) ! 245491
2120 IF FORM_PARM$(72,1)="Y" AND JOB_MODIFIER$<>"" THEN DEL_OPT$=MID(JOB_MODIFIER$,1,3) ! REM "If there is a job modifier then use it to override the delivery options code set, 245491
2140 JOB_CARD$+=TMP$
2141 GOSUB GET_DELIVERY_OPTIONS
2142 GOSUB PUT_IN_IMAGE_LIBRARY
2143 GOSUB COMP_SPECIFIC_OPTIONS ! 282897
2144 CLOSE (%UFPRM_CHAN); %UFPRM_CHAN=0
2145 FIX_UP_JOB_CARD_END:RETURN 
5000 PUT_IN_IMAGE_LIBRARY:! If globals are set, then get correct directory and add to image_library, then clear the globals
5005 IF NOT(%HAS_IMAGE_LIB) OR NUL(%IL_ADD_DOC_TYPE$) OR NUL(IMGSTR$) THEN GOTO PIIL_DONE ! SSP 248993
5010 IF NOT(FN%ISOPEN(%IL_CODES)) THEN PERFORM "ILGFUN;BUILD_DOC_LIST",ERR=PIIL_DONE ! 251100
5020 READ (%IL_CODES,KEY=%IL_ADD_DOC_TYPE$,DOM=PIIL_DONE)I_CODE$,I_DESC$,I_DIR$,I_TYPE$,I_SCAN$,I_SAVE_LAST$,I_ACCESS_LEVEL$,I_ADD_FORMAT$,VERSION_LABEL_1$,VERSION_LABEL_2$,VERSION_LABEL_3$ ! 211234
5022 PSC=POS("*":%IL_ADD_IMAGE_PATH$,ERR=*NEXT); IF PSC<>0 THEN IL_SUB$=%IL_ADD_IMAGE_PATH$(PSC,1),%IL_ADD_IMAGE_PATH$=SUB(%IL_ADD_IMAGE_PATH$,IL_SUB$,HTA(IL_SUB$)); GOTO *SAME ! SSP 218892
5025 FC$=""; IF I_ADD_FORMAT$="1" THEN IF NOT(NUL(%IL_FORMAT$)) THEN FC$="_"+%IL_FORMAT$
5026 NUM_COPIES=1,MULT_COPIES=1; NUM_COPIES=NUM(MID(FORM_PARM$,73,1),ERR=*NEXT)
5027 FOR COPY_COUNT=1 TO NUM_COPIES
5028 ICNT$=STR(COPY_COUNT:"00"); IF POS(ICNT$=IMGSTR$,2)=0 THEN GOTO COPY_LOOP ! SSP 248993
5029 M_SEQ$=""; IF MULT_COPIES THEN M_SEQ$=STR(COPY_COUNT:"_000")
5030 IF POS("MS-WINDOWS"=UCS(SYS))>0 AND POS("pdf"=LCS(I_TYPE$))>0 THEN WIN_I_TYPE$="PS"; WIN_FULL_PATH$=STP(I_DIR$+DLM+SUB(SUB(SUB(%IL_ADD_IMAGE_PATH$,QUO,""),"'",""),"&","")+FC$+M_SEQ$+"."+LCS(STP(WIN_I_TYPE$)),3) END_IF ; FULL_PATH$=STP(I_DIR$+DLM+SUB(SUB(SUB(%IL_ADD_IMAGE_PATH$,QUO,""),"'",""),"&","")+FC$+M_SEQ$+"."+LCS(STP(I_TYPE$)),3); IF NUL(WIN_FULL_PATH$) THEN WIN_FULL_PATH$=FULL_PATH$
5031 IF MULT_COPIES THEN MX=FN_ADD_OPT_PRM("imglibpath"+STR(COPY_COUNT)+"="+FULL_PATH$)
5032 VERSION_LABEL$=""; IF MULT_COPIES THEN VERSION_LABEL$=EVS("EF4_IL_LABEL_"+STR(COPY_COUNT)+"$",ERR=*PROCEED); IF NUL(VERSION_LABEL$) THEN VERSION_LABEL$=TBL(COPY_COUNT,"",VERSION_LABEL_1$,VERSION_LABEL_2$,VERSION_LABEL_3$,ERR=*NEXT) ! 211234
5035 IF IL1_CHAN=0 THEN IL1_CHAN=HFN; OPEN (IL1_CHAN,IOL=*)"IL1"+%C$
5040 CALL "ILGFUN;ADD_IMAGE",IL1_CHAN,I_KEY$,%IL_ADD_DOC_TYPE$,%IL_ADD_TF1$,%IL_ADD_TF2$+M_SEQ$,FULL_PATH$,VERSION_LABEL$ ! 211234
5042 IF (PDFCHL<>0 AND MID(PDFKY$,1,1)="Y") THEN WRITE (PDFCHL,KEY=PDFKY$(2)+FULL_PATH$,ERR=*NEXT)FULL_PATH$ ! SSP 307332
5045 COPY_LOOP:
5055 NEXT COPY_COUNT
5066 ! MX=FN_ADD_OPT_PRM("imglibcopies="+STR(NUM_COPIES))
5090 PIIL_DONE: %IL_ADD_DOC_TYPE$="",%IL_ADD_TF1$="",%IL_ADD_TF2$="",%IL_ADD_IMAGE_PATH$="",%IL_FORMAT$=""; CLOSE (IL1_CHAN,ERR=*PROCEED)
5095 RETURN 
5099 ! *************************************************************
6000 GET_DELIVERY_OPTIONS:
6005 IF NUL(DEL_OPT$) THEN RETURN ! 245491
6010 COPY_COUNT=0; OPEN (HFN,ERR=*RETURN)"EF4"+%C$; _EF4=LFO
6015 EF4_KEY$=MID(FORM_PARM$,1,10)+DEL_OPT$ ! 245491
6020 READ (_EF4,KEY=EF4_KEY$,DOM=*NEXT)
6030 NEXT_COPY:
6040 COPYK$=KEY(_EF4,END=COPY_DONE); IF EF4_KEY$<>MID(COPYK$,1,LEN(EF4_KEY$)) THEN GOTO COPY_DONE
6050 READ (_EF4,KEY=COPYK$)EF4$
6060 COPY_COUNT++; COPY_OPT$=""
6100 ! Print
6110 IF MID(EF4$,16,1)="Y" THEN {
6120 MX=FN_ADD_OPT_PRM("print"+STR(COPY_COUNT)+"="+"Y"); COPY_OPT$+="|print"
6125 IF NOT(NUL(MID(EF4$,52,2))) THEN {
6127 OPEN (HFN)"ZZ9"+%C$; ZZ9=LFO
6130 DIM ZZ9$(256); READ (ZZ9,KEY=MID(EF4$,52,2),DOM=*NEXT)ZZ9$
6132 IF NOT(NUL(MID(ZZ9$,173,60))) THEN MX=FN_ADD_OPT_PRM("devicecmd"+STR(COPY_COUNT)+"="+STP(MID(ZZ9$,173,60),2))
6134 IF NOT(NUL(EF4$(134,10))) THEN MX=FN_ADD_OPT_PRM("printdriver"+STR(COPY_COUNT)+"="+STP(EF4$(134,10),2)) ! 251148
6136 IF NOT(NUL(EF4$(144,20))) THEN MX=FN_ADD_OPT_PRM("printmodel"+STR(COPY_COUNT)+"="+STP(EF4$(144,20),2)) ! 251148
6138 IF NOT(NUL(EF4$(164,10))) THEN MX=FN_ADD_OPT_PRM("printtray"+STR(COPY_COUNT)+"="+STP(EF4$(164,10),2)) ! 251148
6139 MX=FN_ADD_OPT_PRM("printdevice"+STR(COPY_COUNT)+"="+STP(MID(EF4$,52,2),2)) ! 247032
6140 CLOSE (ZZ9); ZZ9=0 ! 247032
6142  }
6143  }
6145 IF FORM_PARM$(77,1)="Y" THEN MX=FN_ADD_OPT_PRM("dbuncollated=Y") ! ssp 247529
6150 ! Image Library
6160 IF MID(EF4$,17,1)="Y" THEN {
6170 MX=FN_ADD_OPT_PRM("imglib"+STR(COPY_COUNT)+"="+"Y"); COPY_OPT$+="|imglib"
6180 TMPV$="EF4_IL_LABEL_"+STR(COPY_COUNT)+"$"; IF NOT(NUL(MID(EF4$,22,20))) THEN VIA TMPV$=STP(MID(EF4$,22,20),2),ERR=*NEXT
6185 IMGSTR$=IMGSTR$+STR(COPY_COUNT:"00") ! SSP 248993
6190  }
6200 ! Fax
6210 IF MID(EF4$,21,1)="Y" THEN {
6220 MX=FN_ADD_OPT_PRM("fax"+STR(COPY_COUNT)+"="+"Y"); COPY_OPT$+="|fax"
6222 IF POS("C"=MID(EF4$,42,10)) THEN COPY_OPT$+="|faxC"
6223 IF POS("R"=MID(EF4$,42,10)) THEN COPY_OPT$+="|faxR" ! 279215 - Reorder notice to customer contact
6224 IF POS("V"=MID(EF4$,42,10)) THEN COPY_OPT$+="|faxV"
6226 IF POS("P"=MID(EF4$,42,10)) THEN COPY_OPT$+="|faxP"
6228 IF POS("T"=MID(EF4$,42,10)) THEN COPY_OPT$+="|faxT"
6230 IF POS("W"=MID(EF4$,42,10)) THEN COPY_OPT$+="|faxW"
6232 IF POS("S"=MID(EF4$,42,10)) THEN COPY_OPT$+="|faxS"
6233 IF POS("A"=MID(EF4$,42,10)) THEN COPY_OPT$+="|faxA" ! 271319 - CSR email feature
6234 IF MID(EF4$,174,1)="Y" THEN MX=FN_ADD_OPT_PRM("landscape"+STR(COPY_COUNT)+"="+"Y") ! SSP 251148 ssp 263992
6235 OPEN (HFN,ERR=6248)"EF5"+%C$; _EF5=LFO
6238 DIM EF5$(840); READ (_EF5,KEY=COPYK$,DOM=*NEXT)EF5$
6240 IF NOT(NUL(EF5$(16,120))) THEN MX=FN_ADD_OPT_PRM("emlsubject"+STR(COPY_COUNT)+"="+STP(EF5$(16,120),2)) ! 269734
6241 IF NOT(NUL(EF5$(136,60))) THEN MX=FN_ADD_OPT_PRM("emlfrom"+STR(COPY_COUNT)+"="+STP(EF5$(136,60),2)) ! 269734
6242 IF NOT(NUL(EF5$(196,60))) THEN MX=FN_ADD_OPT_PRM("emlfromaddr"+STR(COPY_COUNT)+"="+STP(EF5$(196,60),2)) ! 269734
6243 IF NOT(NUL(EF5$(256,160))) THEN MX=FN_ADD_OPT_PRM("emlmsgfile"+STR(COPY_COUNT)+"="+STP(EF5$(256,160),2)) ! 269734
6244 IF NOT(NUL(EF5$(416,30))) THEN MX=FN_ADD_OPT_PRM("emlbodymime"+STR(COPY_COUNT)+"="+STP(EF5$(416,30),2)) ! 269734
6245 IF NOT(NUL(EF5$(446,160))) THEN MX=FN_ADD_OPT_PRM("emlattach"+STR(COPY_COUNT)+"="+STP(EF5$(446,160),2)) ! 269734
6248 MX=FN_ADD_OPT_PRM("emlkey"+STR(COPY_COUNT)+"="+COPYK$) ! SSP 298391 
6250 DEFEREML$=""
6255 IF MID(FORM_PARM$,1,3)="810" THEN ZA_KEY$=PAD(%C$,3)+"AR3"; READ (ZZPARM,ERR=*NEXT,KEY=ZA_KEY$,DOM=*NEXT)AR3PARAM$; IF MID(AR3PARAM$,13,1)="Y" THEN DEFEREML$="Y" ! SSP#298391
6258 IF MID(EF4$,175,1)="Y" THEN DEFEREML$="Y"
6260 IF DEFEREML$="Y" THEN MX=FN_ADD_OPT_PRM("deferemail"+STR(COPY_COUNT)+"="+"Y") ! SSP 298391 
6268  }
6270 ! PDF Printing
6275 IF MID(EF4$,18,1)="Y" THEN {
6280 MX=FN_ADD_OPT_PRM("pdf"+STR(COPY_COUNT)+"="+"Y"); COPY_OPT$+="|pdf"
6282 IF NOT(NUL(EF4$(134,10))) THEN MX=FN_ADD_OPT_PRM("printdriver"+STR(COPY_COUNT)+"="+STP(EF4$(134,10),2)) ! 307196
6284 IF NOT(NUL(EF4$(144,20))) THEN MX=FN_ADD_OPT_PRM("printmodel"+STR(COPY_COUNT)+"="+STP(EF4$(144,20),2)) ! 307196
6285 IF NOT(NUL(EF4$(164,10))) THEN MX=FN_ADD_OPT_PRM("printtray"+STR(COPY_COUNT)+"="+STP(EF4$(164,10),2)) ! 307196
6290  }
6300 ! PS Printing
6310 IF MID(EF4$,19,1)="Y" THEN {
6320 MX=FN_ADD_OPT_PRM("ps"+STR(COPY_COUNT)+"="+"Y"); COPY_OPT$+="|ps"
6325  }
6329 ! Archiving
6330 IF MID(EF4$,20,1)="Y" THEN {
6340 MX=FN_ADD_OPT_PRM("archive"+STR(COPY_COUNT)+"="+"Y"); COPY_OPT$+="|archive"
6345  }
6350 MX=FN_ADD_OPT_PRM("copyoptions"+STR(COPY_COUNT)+"="+COPY_OPT$+"|") ! List of all output options enabled for this copy 
6355 IF NOT(NUL(EF4$(74,60))) THEN MX=FN_ADD_OPT_PRM("joboptions"+STR(COPY_COUNT)+"="+STP(EF4$(74,60),2)) ELSE MX=FN_ADD_OPT_PRM("joboptions"+STR(COPY_COUNT)+"="+STP(FORM_PARM$(12,60),2)) ! 246969
6360 GOTO NEXT_COPY
6400 COPY_DONE:
6410 MX=FN_ADD_OPT_PRM("dbdcopies"+"="+STR(COPY_COUNT)) ! Total number of copies under this print job 
6420 MX=FN_ADD_OPT("-c "+STR(COPY_COUNT)) ! Total number of copies under this print job 
6490 IF _EF4>0 THEN CLOSE (_EF4); _EF4=0
6492 IF _EF5>0 THEN CLOSE (_EF5); _EF5=0 ! SSP 269734
6495 RETURN 
6499 ! *************************************************************
6500 COMP_SPECIFIC_OPTIONS:! 282897
6505 MX=FN_ADD_OPT_PRM("dbdcomp="+PAD(%C$,3)) ! 262063 - Output the company code for multi-company use
6506 MX=FN_ADD_OPT_PRM("dbddatapath="+%DATAPATH$) ! 287040 - Output this company's datapath
6510 ! OPEN (HFN)"ZZPARM"; ZZPARM=LFO
6512 ZE_KEY$=PAD(%C$,3)+"E/F"; READ (ZZPARM,ERR=*NEXT,KEY=ZE_KEY$,DOM=*NEXT)EFPARAM$; GOSUB EF_DEFAULTS; GOTO 6590 ! ssp 298391
6515 ZR_KEY$=PAD(%C$,3)+"PDF"; READ (ZZPARM,ERR=6590,KEY=ZR_KEY$,DOM=6590)ZR_REC$ ! SSP#286570
6525 BASE_PRINT_DIR$=STP(MID(ZR_REC$,7,60),2),BASE_ARCHIVE_DIR$=STP(MID(ZR_REC$,67,60),2)
6581 MX=FN_ADD_OPT_PRM("dbdprintdir="+BASE_PRINT_DIR$)
6582 MX=FN_ADD_OPT_PRM("dbdarchivedir="+BASE_ARCHIVE_DIR$)
6590 ! CLOSE (ZZPARM)
6595 RETURN 
6599 ! 
6600 EF_DEFAULTS:
6610 BASE_COMP$=PAD(%C$,3),BASE_COMPNAME$=STP(MID(EFPARAM$,1,30),2)
6620 BASE_ROOTDIR$=STP(MID(EFPARAM$,31,30),2),BASE_PDFDIR$=STP(MID(EFPARAM$,91,30),2)
6630 BASE_PRINTDIR$=STP(MID(EFPARAM$,121,30),2),BASE_EMAILDIR$=STP(MID(EFPARAM$,151,30),2)
6640 BASE_ARCHIVEDIR$=STP(MID(EFPARAM$,181,30),2),BASE_LOGFILE$=STP(MID(EFPARAM$,61,30),2)
6650 MX=FN_ADD_OPT_PRM("dbdcomp="+BASE_COMP$)
6655 MX=FN_ADD_OPT_PRM("dbdcompname="+BASE_COMPNAME$)
6660 MX=FN_ADD_OPT_PRM("dbdpdfdir="+BASE_PDFDIR$)
6665 MX=FN_ADD_OPT_PRM("dbdprintdir="+BASE_PRINTDIR$)
6670 MX=FN_ADD_OPT_PRM("dbdemldir="+BASE_EMAILDIR$)
6675 MX=FN_ADD_OPT_PRM("dbdarchivedir="+BASE_ARCHIVEDIR$)
6680 MX=FN_ADD_OPT_PRM("dbdlogfile="+BASE_LOGFILE$)
6695 RETURN 
6699 ! 
8000 UNFORM_OPTIONS:
8005 DEF FN_ADD_OPT_PRM(LOCAL OPTSTR$)
8010 PRINT (%UFPRM_CHAN)"-prm "+QUO+OPTSTR$+QUO+" "
8040 RETURN 0
8045 END DEF
8049 ! 
8050 ! 
8055 DEF FN_ADD_OPT(LOCAL OPTSTR$)
8060 PRINT (%UFPRM_CHAN)OPTSTR$
8070 RETURN 0
8085 END DEF
8099 ! 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9120 ON Y8 GOTO 9900,0990,9920
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9740 RETURN 
9750 REM "FILES
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9790 RETURN 
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 IF COPY_COUNT<1 THEN IF COMM$="N" THEN EXIT 48 ! 273783; 275068-Only for page 1 or "New" E/F jobs
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
10000 REM "Setup Unform Graphics record for use with Unform printer devices
10005 SETUP_GRAPHICS:
10010 ENTER X3$,X4$,OPTIONS$,ERRMSG$,ERR=*NEXT
10020 OPEN (HFN)"ZZPARM"; ZZPARM=LFO
10025 ! ZZPARM Graphics record key
10030 K$="GRUNFORM   "
10035 ! ZZPARM Graphics record data
10040 A$="GRUNFORM                                                                                                                                                     C0C1CAC2C3C4C5C7C6C8C90000CBCCCD2D212B2B2B2B2B2B2B2B "
10060 WRITE (ZZPARM,KEY=K$,DOM=*NEXT)A$ ! Do not overwrite if record exists
10095 EXIT 
10099 ! 
56002 REM "199496-Set up Ghostscript for Windows. Reqd for Image Library
56004 REM "190626-Keep both costed stub and regular invoice copies in Image
56006 REM "189107-Installation - Image Library Small Distributor License
56008 REM "218892-using an asterisk as the PO code causes issues in image
56010 REM "211234-Designate labels for up to 3 versions of Image Type
56012 REM "245093-ZZX, S1 not in GUI. Required to setup UNFORM graphics type  
56014 REM "245491-Unform-Job card override, delivery options override  
56015 REM "247529-Need for the Unform invoices to print using the J1 feature  
56016 REM "246969-Custom PO in Unform                                         
56018 REM "248993-UNFORM question on invoice  - not to image the first copy,  
56020 REM "251100-PE(014, ILGFUN-2510) when trying to view an order in order  
56021 REM "251148-Make an option available on the E/F S2 screen to change     
56022 REM "247032-Using send mail for emailing, how do you check to see if    
56024 REM "262063-Install UnForm on tfcentral servers and get a multi-company 
56025 REM "263992-Typo in EF2UNF                                              
56026 REM "266120-Changed to UnForm and invoices are printing very slow.  Is  
56027 REM "269734-Unform Emails: Ability to customize email settings like     
56028 REM "271319-New fax/email type CSR. Modify PO Printing to honor this new
56029 REM "279215-EFGMAD-R types are not emailing thru Unform
56030 REM "282897-Setup Electronic Forms - Unform for SaaS platform
56031 REM "286570-I'm unable to send an RFQ.  I get a program exception.      
56032 REM "298391-Ability to send E/F emails of Invoices after SJ Update      
56033 REM "307196-Additional support features for proxy printing in Electronic
56034 REM "307332-Create combined pdf of checks in a batch                    
