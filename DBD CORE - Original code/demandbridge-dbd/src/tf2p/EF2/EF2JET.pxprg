0010 REM "JetForm Control Program<EF2JET>
0015 REM "Prog Type: IP-3.1.2   
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 12/20/11 - 12.098888 - crg - SSP# 251100
0037 REM "251100-PE(014, ILGFUN-2510) when trying to view an order in order  
0040 REM "Copyright 2011 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0069 REM "R$ is Electronic Forms Parameters
0070 REM "COMM$ is command string, "N" = new item, "E"= eject page same item
0071 REM "DEVICE is basic device number
0072 REM "RETURN$ is return information
0073 REM "JOB_MODIFIER$ is added to job name to modify the name to accomodate user defined specs, etc.
0074 REM "FORM_PARM$ is the data to go on a job card,plus parameters that may allow/disallow modifiers etc.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,R$,COMM$,DEVICE,FORM_PARM$,JOB_MODIFIER$,RETURN$
0100 SETERR 9000
0110 X0$="EF2JET",X1$="JetForm Control Program"
0600 REM "JF_MODIFIER$ is a Global variable incremented each time and used as the last part of the file name to help uniquicity
0605 MOD$=GBL("JF_MODIFIER",ERR=0606); GOTO 0610
0606 MOD$="1"
0610 IF MOD$="9" THEN MOD$="A" ELSE IF MOD$="Z" THEN MOD$="0" ELSE MOD$=CHR(ASC(MOD$)+1)
0615 MOD$=GBL("JF_MODIFIER",MOD$)
0900 REM 
0905 RETURN$=""
1000 REM "Main parm
1010 ON POS(COMM$(1,1)="NE") GOTO 1495,NEW_FORM,EJECT_PAGE,1495
1050 NEW_FORM:REM "start a new item to go into the jet item DIRectory
1060 GOSUB JETFORM_SETUP
1095 NEW_FORM_END:GOTO 1495
1100 EJECT_PAGE:REM "Same item, just eject current page
1120 PRINT (DEVICE)"^eject"
1145 EJECT_PAGE_END:GOTO 1495
1495 GOTO 9900
2000 JETFORM_SETUP:REM "Reopen printer to print to jetform collector DIRectory. File is built in the /tmp DIRectory and then moved to the collector DIRectoy with a .dat extension
2001 WAIT 1; REM "TO ENSURE THAT WE HAVE DIFFERENT FILE NAMES FOR ALL FILES
2010 CURRENT_PREC=PRC; PRECISION 4; TIME=TIM; FILE_NAME$=FID(0)+STR(TIME*1000:"00000")+MOD$+".dat"; PRECISION CURRENT_PREC; REM "Generate an 8 character name combining the task id and the time to 3600/10000'ths of a hour. Store and preserve the current precision
2015 IF X3$(77,1)="D" THEN PRINTER_CFG$=STP(R$(162,60),1)+"\"+FILE_NAME$ ELSE PRINTER_CFG$=STP(R$(162,60),1)+"/"+FILE_NAME$; REM "Set to DIRectory with file name
2020 DEV_CHANNEL=DEVICE,DEVICE_NAME$=FID(DEVICE); REM "determine correct channel to close
2025 CLOSE (DEV_CHANNEL); OPEN (DEV_CHANNEL,OPT=PRINTER_CFG$)DEVICE_NAME$
2055 REM "If job card data is not blank put out 'job card' line
2060 IF STP(FORM_PARM$(12,60),3," ")="" AND JOB_MODIFIER$="" THEN GOTO 2061 ELSE GOSUB FIX_UP_JOB_CARD; PRINT (DEV_CHANNEL)JOB_CARD$
2095 JETFORM_SETUP_END:RETURN 
2100 FIX_UP_JOB_CARD:REM "Create a job card line from FORM_PARM$(12 ,60) and the JOB_MODIFIER$ also check FORM_PARM$(72,1) to see if job modifier is allowed
2105 TMP$=STP(FORM_PARM$(12,60),1)
2110 IF FORM_PARM$(72,1)="Y" AND JOB_MODIFIER$<>"" THEN IF TMP$="" THEN TMP$=JOB_MODIFIER$ ELSE P=POS(" "=TMP$); IF P>1 THEN TMP$=TMP$(1,P-1)+JOB_MODIFIER$+TMP$(P); REM "If there is a job modifier add it to the end of the first word on the line. Note the P>1 means that if the first character is blank then no modification will be made even if there is a job_modifier$
2140 JOB_CARD$="^job "+TMP$
2142 GOSUB PUT_IN_IMAGE_LIBRARY
2145 FIX_UP_JOB_CARD_END:RETURN 
5000 PUT_IN_IMAGE_LIBRARY:! If globals are set, then get correct directory and add to image_library, then clear the globals
5005 IF NOT(%HAS_IMAGE_LIB) OR NUL(%IL_ADD_DOC_TYPE$) THEN GOTO PIIL_DONE
5010 IF NOT(FN%ISOPEN(%IL_CODES)) THEN PERFORM "ILGFUN;BUILD_DOC_LIST",ERR=PIIL_DONE ! 251100
5020 READ (%IL_CODES,KEY=%IL_ADD_DOC_TYPE$,DOM=PIIL_DONE)I_CODE$,I_DESC$,I_DIR$,I_TYPE$,I_SCAN$,I_SAVE_LAST$,I_ACCESS_LEVEL$,I_ADD_FORMAT$,VERSION_LABEL_1$,VERSION_LABEL_2$,VERSION_LABEL_3$ ! 211234
5022 PSC=POS("*":%IL_ADD_IMAGE_PATH$,ERR=*NEXT); IF PSC<>0 THEN IL_SUB$=%IL_ADD_IMAGE_PATH$(PSC,1),%IL_ADD_IMAGE_PATH$=SUB(%IL_ADD_IMAGE_PATH$,IL_SUB$,HTA(IL_SUB$)); GOTO *SAME ! SSP 218892
5025 FC$=""; IF I_ADD_FORMAT$="1" THEN IF NOT(NUL(%IL_FORMAT$)) THEN FC$="_"+%IL_FORMAT$
5026 NUM_COPIES=1; NUM_COPIES=NUM(MID(FORM_PARM$,73,1),ERR=*NEXT); IF NUM_COPIES>1 THEN MULT_COPIES=1 ELSE NUM_COPIES=1,MULT_COPIES=0
5027 FOR COPY_COUNT=1 TO NUM_COPIES
5029 M_SEQ$=""; IF MULT_COPIES THEN M_SEQ$=STR(COPY_COUNT:"_000")
5030 IF POS("MS-WINDOWS"=UCS(SYS))>0 AND POS("pdf"=LCS(I_TYPE$))>0 THEN WIN_I_TYPE$="PS"; WIN_FULL_PATH$=STP(I_DIR$+DLM+SUB(SUB(SUB(%IL_ADD_IMAGE_PATH$,QUO,""),"'",""),"&","")+FC$+M_SEQ$+"."+LCS(STP(WIN_I_TYPE$)),3) END_IF ; FULL_PATH$=STP(I_DIR$+DLM+SUB(SUB(SUB(%IL_ADD_IMAGE_PATH$,QUO,""),"'",""),"&","")+FC$+M_SEQ$+"."+LCS(STP(I_TYPE$)),3); IF NUL(WIN_FULL_PATH$) THEN WIN_FULL_PATH$=FULL_PATH$
5031 IF MULT_COPIES THEN APPEND_TO_JOBCARD$+=" -tf"+STR(COPY_COUNT)+FULL_PATH$
5032 VERSION_LABEL$=""; IF MULT_COPIES THEN VERSION_LABEL$=TBL(COPY_COUNT,"",VERSION_LABEL_1$,VERSION_LABEL_2$,VERSION_LABEL_3$,ERR=*NEXT) ! 211234
5035 IF IL1_CHAN=0 THEN IL1_CHAN=HFN; OPEN (IL1_CHAN,IOL=*)"IL1"+%C$
5040 CALL "ILGFUN;ADD_IMAGE",IL1_CHAN,I_KEY$,%IL_ADD_DOC_TYPE$,%IL_ADD_TF1$,%IL_ADD_TF2$+M_SEQ$,FULL_PATH$,VERSION_LABEL$ ! 211234
5055 NEXT COPY_COUNT
5060 ! IF POS("MS-WINDOWS"=UCS(SYS))>0 THEN JOB_CARD$=STP(JOB_CARD$)+" -Z"+WIN_FULL_PATH$ ELSE JOB_CARD$=STP(JOB_CARD$)+" -tf"+FULL_PATH$+APPEND_TO_JOBCARD$; REM " 189107 - Disabled the job card modification for Windows, not reqd anymore
5065 JOB_CARD$=STP(JOB_CARD$)+" -tf"+FULL_PATH$+APPEND_TO_JOBCARD$
5090 PIIL_DONE: %IL_ADD_DOC_TYPE$="",%IL_ADD_TF1$="",%IL_ADD_TF2$="",%IL_ADD_IMAGE_PATH$="",%IL_FORMAT$=""; CLOSE (IL1_CHAN,ERR=*PROCEED)
5095 RETURN 
5099 ! *************************************************************
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9120 ON Y8 GOTO 9900,0990,9920
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9740 RETURN 
9750 REM "FILES
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9790 RETURN 
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56002 REM "199496-Set up Ghostscript for Windows. Reqd for Image Library    
56004 REM "190626-Keep both costed stub and regular invoice copies in Image   
56006 REM "189107-Installation - Image Library Small Distributor License      
56008 REM "218892-using an asterisk as the PO code causes issues in image     
56010 REM "211234-Designate labels for up to 3 versions of Image Type
56012 REM "251100-PE(014, ILGFUN-2510) when trying to view an order in order  
