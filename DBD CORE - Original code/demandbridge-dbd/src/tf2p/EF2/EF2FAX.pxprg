0010 REM "Add Fax info to a item <EF2FAX>
0015 REM "Prog Type: IP-3.1.2   
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 07/12/18 - 10.448333 - jvv - SSP# 298391
0037 REM "298391-Ability to send E/F emails of Invoices after SJ Update      
0040 REM "Copyright 2018 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0070 REM "P$ is param string read in earlier,P9$ is fax parameters (read in by EF2PRM just like P$)
0071 REM "P is channel # of device to print to
0072 REM "N$ is list of fax data formatted as follows, each entry is 51 bytes:
0073 REM "   1,1=fax number type
0074 REM "   2,25=fax number
0075 REM "   27,25=tag 2 identifier
0076 REM "T1$ is tag 1 identifier
0077 REM "EM$ is list of codes to check to see if replace fax# with email, each is type (1,1)+data_key(2,20), will lookup in EF3
0078 REM "EFFORM_MODE is JETFORM_MODE=1, UNFORM_MODE=2
0090 CLEAR ; SETERR 0095; ENTER X3$,X4$,P$,P9$,P,N1$,T1$,EFFORM_MODE,EM$
0092 IF POS("|"=EM$)>0 THEN CUST_REP$=EM$(POS("|"=EM$)+1); EM$=EM$(1,POS("|"=EM$)-1); CUST_REP$=PAD(CUST_REP$,4," ",ERR=*NEXT) ! SSP247389  jdf
0095 N$=N1$
0100 SETERR 9000
0110 X0$="EF2FAX",X1$="Add Fax Data to Item"
0120 DIM S$(40)
0130 JETFORM_MODE=1,UNFORM_MODE=2
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X EF3...  02O EML...  "
0520 GOSUB 9750
1000 REM "Is there fax information to add, if yes then add the ones needed
1010 REM "I0 is set to last fax number to do (1-5)
1011 IF EFFORM_MODE THEN FAX_LIST$=""; IF LEN(N$)>=51 THEN FOR J=1 TO LEN(N$)-50 STEP 51; FAX_LIST$=FAX_LIST$+N$(J,1); NEXT J END_IF ELSE FAX_LIST$=P$(139,5)
1012 FAX_LIST$=STP(FAX_LIST$,3)
1015 I0=POS(" "=FAX_LIST$+" ")
1020 IF I0<=1 OR N$="" THEN GOTO 9900; REM "No fax data to look up.
1035 FSP$="sp"; IF MID(P9$,727,1)="Y" THEN IF X3$(9,3)="549" THEN FSP$="," ELSE FSP$="|" ! SSP 207601 SSP 221569
1050 FOR I=1 TO I0-1
1055 IF NOT(EFFORM_MODE) THEN IF FAX_LIST$(I,1)=" " THEN PRINT (P)""; GOTO 1090; REM " if  blank then skip it, but print a line to keep permissions right.
1060 P9=POS(FAX_LIST$(I,1)=N$,51); IF P9=0 THEN IF EFFORM_MODE THEN GOTO 1090 ELSE PRINT (P)""; GOTO 1090; REM " if no number informat supplied by calling program the just print blank line.
1065 JPFX$=N$(P9,1); IF JPFX$="P" AND MID(T1$,1,3)="840" THEN JPFX$="V" ! SSP 232848
1070 DIM X$(75)
1071 IF NUL(CUST_REP$) THEN GOTO 1075 ELSE READ (Z[2],KEY="CS"+CUST_REP$,KNO=2,DOM=*NEXT) ! SSP247389 jdf
1072 EML_KEY$=KEY(Z[2],END=1075); IF EML_KEY$(1,2)<>"CS" OR MID(EML_KEY$,3,LEN(CUST_REP$))<>CUST_REP$ THEN GOTO 1075 ! SSP247389 jdf
1073 READ (Z[2],KEY=EML_KEY$)EMAIL$
1074 REP_EMAIL$=STP(EMAIL$(41,60),2," ")
1075 X$(1,25)=N$(P9+1,25),X$(26,25)=T1$,X$(51,25)=N$(P9+26,25)
1077 P7$=X$(1,25); IF POS(" "<>P7$)<>0 THEN P8$="S"; CALL "FX2SBA",ERR=1090,X3$,X4$,P7$,P9$,P8$,X9; IF POS("@-FNUM"=P8$)=1 THEN X$(1,25)=P8$(8) ELSE X$(1,25)=P8$; REM "FIX FAX NUMBER ACCORDING TO FAX PARAMETERS, FOR NOW WE WILL HAVE TO STRIP OFF THE "@-FNUM" ADDED BY FX2SBA, This appears that this is needed to adjust number long distance and for dropping AC when needed
1078 VSIFAX_NUMBER$=X$(1,25),X$(1,25)=""; GOSUB 2000; X$(1,25)=VSIFAX_NUMBER$
1079 IF EFFORM_MODE=UNFORM_MODE THEN CALL "ZSTRTU",X3$,LOGIN$,TMP$,TMP2$; PRINT (P)"DELIVER"+"|"+JPFX$+"|"+STP(VSIFAX_NUMBER$,1)+"|"+STP(X$(26,25),1)+"|"+STP(X$(51,25),1)+"|"+LOGIN$+"|"+REP_EMAIL$+"|"+KEYDATA$+"|" ! SSP 232848, Unform mode SSP 237164 ssp298391
1080 IF EFFORM_MODE=JETFORM_MODE THEN CALL "ZSTRTU",X3$,LOGIN$,TMP$,TMP2$; PRINT (P)"^field "+STP(P$(146,3),1)+"FAX"+JPFX$,'LF',"TFFAX"+FSP$+STP(VSIFAX_NUMBER$,1)+FSP$+STP(X$(26,25),1)+FSP$+STP(X$(51,25),1)+FSP$+LOGIN$+FSP$+REP_EMAIL$+FSP$ ELSE IF EFFORM_MODE<>UNFORM_MODE THEN PRINT (P)X$ ! SSP 207601 SSP 232848
1081 IF EFFORM_MODE=JETFORM_MODE THEN CALL "ZSTRTU",X3$,LOGIN$,TMP$,TMP2$; PRINT (P)"^field "+STP(P$(146,3),1)+"EMAIL"+JPFX$,'LF',"TFJEMAILV||"+STP(VSIFAX_NUMBER$,1)+"||"+STP(X$(26,25),1)+"||"+STP(X$(51,25),1)+"||"+LOGIN$+"||"+REP_EMAIL$+"||" ELSE IF EFFORM_MODE<>UNFORM_MODE THEN PRINT (P)X$ ! SSP 232848
1082 IF P9=1 THEN IF LEN(N$)>=51 THEN N$=N$(52) ELSE IF N$="" THEN GOTO 1090
1084 IF P9>1 THEN IF P9+50=LEN(N$) THEN N$=N$(1,P9-1) ELSE N$=N$(1,P9-1)+N$(P9+51)
1090 NEXT I
1995 GOTO 9900
2000 REM "check VSIFAX_NUMBER to see if replace with email address
2002 KEYDATA$="" ! SSP 298391
2005 E9=POS(FAX_LIST$(I,1)=EM$,21); IF E9=0 THEN GOTO 2045
2006 IF EM$(E9+1,16)="ALIGNMENTPATTERN" THEN VSIFAX_NUMBER$=P$(222,30); GOTO 2045; REM "If doing alignment pattern, use email from parameters
2010 FIND (Z[1],KEY=EM$(E9,21),DOM=2045)EF3$
2015 VSIFAX_NUMBER$=STP(MID(EF3$,22,200),1) ! SSP 302017   
2020 KEYDATA$=EM$(E9,21) ! SSP 209391
2045 RETURN 
8910 DEF FNQ$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9120 ON Y8 GOTO 9900,0990,9920
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9740 RETURN 
9750 REM "FILES
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9790 RETURN 
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56005 REM "207601-When emailing a laser form, if there is an "sp" in the email
56008 REM "232848-RFQ System parm, use "P" tag for output.  Modify RFQ output 
56010 REM "262640-EF2FAX does not call ZZFLES to close files when exiting.    
56011 REM "271319-New fax/email type CSR. Modify PO Printing to honor this new
56012 REM "298391-Ability to send E/F emails of Invoices after SJ Update      
