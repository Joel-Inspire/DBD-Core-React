0010 REM "Convert Vendor File <CA2AP4>
0020 SETESC 9300; SETERR 9000
0035 REM "5.3 - 10/13/04 - 16.297777 - sbb - SSP# 177262
0040 REM "Copyright 2004 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "H0$=End of Record Mark
0051 REM " T0$=Date Format,T1$=Phone Format, T2$="NUMERIC FORMAT
0080 BEGIN 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="CA2AP4",X1$="Convert Vendor File to AP4"
0120 DIM Z0$(80,"-"),Z1$(20,"0"),S0$(80),BLANKS$(100)
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2]
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6]
0330 IOLIST C$
0340 IOLIST D$,D[0],D[1],D[2],D[3]
0350 IOLIST E$,E[0],E[1],E[2]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O AP4...  03O AP5...  04O FM0...  11O CA0... 12O MAPDAT  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0522 GOSUB 6000; CALL "CA2MCA",X3$,X4$,10,CA0_SELECTED$,CA0_OPTIONS$
0524 IF CA0_SELECTED$(1,4)=S0$(1,4) THEN GOTO 9900
0530 READ (Z[13],KEY=X3$(9,3)+"A/P")P0$
0540 READ (Z[11],KEY=CA0_SELECTED$(1,4))CA0$
0600 REM "
0605 READER$="CA2R"+STP(CA0$(25,2),1)
0610 GOSUB 6000
0620 CALL READER$,X3$,X4$,CA0$,CA1${ALL},"O","",MEM${ALL},R$,RET_CODE
0640 CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0700 IF CA0$(62,1)="I" THEN CALL "ZZINIT",STR(Z[2]:"00")+STR(Z[3]:"00")
0710 X$="AP4 AP2 AP3 AP0 AP1 "
0720 FOR I=1 TO LEN(X$) STEP 4
0725 J$="Clear Mapping for "+FNS$(X$(I,4))+"?"
0730 CALL "ZZPROM",".Y",X3$,Z,J$,"","",0; ON Z GOTO 0731,0790
0735 C9=0,C9$="\|/-",C8=LEN(C9$); PRINT @(20,15),"Clearing ",FNS$(X$(I,4)),":",
0740 READ (Z[12],KEY=X$(I,4),DOM=0741)
0750 K$=KEY(Z[12],END=0780); C9=C9+1; PRINT @(35,15),C9$(MOD(C9,C8)+1,1),; IF K$(1,4)=X$(I,4) THEN REMOVE (Z[12],KEY=K$); GOTO 0750
0780 PRINT @(0,15),'CL',
0790 NEXT I
0800 REM "Remove plant codes
0805 C9=0,C9$="\|/-",C8=LEN(C9$); PRINT @(20,15),"Clearing Plants",
0810 READ (Z[4],KEY="P",DOM=0811)
0820 K$=KEY(Z[4],END=0845); C9=C9+1; PRINT @(37,15),C9$(MOD(C9,C8)+1,1),; IF K$(1,1)<>"P" THEN GOTO 0845 ELSE REMOVE (Z[4],KEY=K$,DOM=0820); GOTO 0820
0845 PRINT @(0,15),'CL',
0920 H0$=$0A$
0930 READ (Z[12],KEY="*PARAMETERS")P9$
0940 ADDR "CA2CVT"
1000 REM "Read Data file
1010 DIM R$(1000); CALL READER$,X3$,X4$,CA0$,CA1${ALL},"R","",MEM${ALL},R$,RET_CODE; IF RET_CODE=2 THEN GOTO 5000
1180 GOSUB 2000
1300 REM "Display progress every 2%
1900 GOTO 1000
2000 REM "Initialize TopForm Record
2010 REM "Determine if Vendor or Plant record
2015 IF CA0$(27,1)="G" THEN IF R$(138,2)<>"00" AND R$(138,2)<>"  " THEN GOTO 4990; REM "If galaxy, then shipto code of 00 or blank indicates vendor,otherwise the plant
2100 DIM A$(310),A[2],S$(50),B$(244)
2200 REM "Vendor Code
2205 Q$=R$(1,10); DIM X$(100); X$(1)="AP4 "+Q$
2210 X=NUM(P0$(14,1)),A$(1,10)=Z1$,A$(3,X)=R$(1,10),A$(3,X)=STR(NUM(R$(1,10),ERR=2211):Z1$(1,X))
2215 IF P0$(13,1)="Y" THEN A$(1,2)=R$(1,2),A$(1,2)=STR(NUM(A$(1,2)):"00")
2220 F9$=""; FIND (Z[12],DOM=2221,KEY=X$(1,24))X$; A$(1,10)=X$(25); F9$="Y"
2225 REM 'CK IF APPENDING PASS THEN RETREIEVE EXISTING VENDOR RECORD AND BYPASS IF IT DOES NOT EXIST
2226 IF CA0$(62,1)<>"A" THEN GOTO 2240
2230 READ (Z[2],KEY=A$(1,10),DOM=4995)IOL=0310
2240 REM "Vendor name
2242 IF R$(11,32)=BLANKS$(1,32) THEN GOTO 2260
2250 A$(11,35)=R$(11,32)
2260 REM "Address line 1
2262 IF R$(43,31)=BLANKS$(1,31) THEN GOTO 2280
2270 A$(46,30)=R$(43,31)
2280 REM "Address line 2
2282 IF R$(74,32)=BLANKS$(1,32) THEN GOTO 2300
2290 A$(76,30)=R$(74,32)
2295 IF STP(A$(46,30),3," ")="" THEN A$(46,30)=A$(76,30),A$(76,30)=""; REM "If addr line 1 = all blanks, then move addr line 2 to addr line 1
2300 REM "City
2302 IF R$(106,16)=BLANKS$(1,16) THEN GOTO 2320
2310 A$(106,16)=R$(106,16)
2320 REM "State
2322 IF R$(122,6)=BLANKS$(1,6) THEN GOTO 2340
2330 A$(122,2)=R$(122,6)
2340 REM "Zip Code
2342 IF R$(128,10)=BLANKS$(1,10) THEN GOTO 2360
2350 Q$=STP(R$(128,10),0); GOSUB 7800; A$(124,9)=X$
2360 REM "Fax number
2362 IF R$(150,14)=BLANKS$(1,14) THEN GOTO 2390
2370 Q$=R$(150,14); GOSUB 7750; A$(133,12)=X$
2390 REM "Lookup Sequence
2400 A$(145,10)=A$(11,10)
2410 CALL "YUCASE",X3$,A$(145,10),X$; A$(145,10)=X$
2420 REM "Purchasing Agent
2422 IF R$(164,20)=BLANKS$(1,20) THEN GOTO 2440
2430 A$(155,20)=R$(164,20)
2440 REM "Greeting
2442 IF R$(242,20)=BLANKS$(1,20) THEN GOTO 2460
2450 A$(175,20)=R$(242,20); IF STP(A$(175,20),3," ")="" THEN A$(175,20)=A$(155,20)
2460 REM "Phone Number (195,14)=Phone number + Extension
2462 IF R$(216,14)=BLANKS$(1,14) THEN GOTO 2490
2470 Q$=CVS(R$(216,14),16); GOSUB 7750; A$(195,10)=X$
2490 REM "Phone Extension
2500 A$(205,4)=""
2510 REM "Vendor Status
2520 A$(246,1)="A"
2530 REM "Vendor Category
2540 Q$=R$(406,20); GOSUB 7000; DIM X$(100); X$(1,24)="AP3 "+Q$,I1=247,I2=9; GOSUB 7500
2570 REM "Std Comment field for invoices
2572 IF R$(262,15)=BLANKS$(1,15) THEN GOTO 2590
2580 A$(256,15)=R$(262,15)
2590 REM "Std G/L Account
2592 IF R$(184,12)=BLANKS$(1,12) THEN GOTO 2610 ELSE IF R$(184,5)="52000" THEN GOTO 2610
2600 Q$=R$(184,12); IF FNS$(Q$)>"" THEN DIM X$(100); X$(1,24)="AP0 "+Q$,I1=271,I2=12; GOSUB 7500
2602 IF X3$(9,3)="529" THEN DIM ZERO$(12,"0"); DATA$=STP(A$(271,12),1); A$(271,12)=DATA$+ZERO$(1,12-LEN(DATA$))
2610 REM "Std Bank Code
2620 Q$=R$(481,3); DIM X$(100); X$(1,24)="AP1 "+Q$,I1=283,I2=3; GOSUB 7500
2625 REM "Terms Code
2626 IF R$(196,20)=BLANKS$(1,20) THEN GOTO 2650
2630 Q$=R$(196,20); DIM X$(100); X$(1)="AP2 "+Q$,I1=286,I2=2; GOSUB 7500
2650 REM "Standard Payment Selection Code
2660 A$(288,2)="00"
2680 REM "Payment Priority Code
2690 A$(290,1)="G"
2700 REM "Vendor On Hold?
2710 A$(291,1)="N"; IF A$(291,1)<>"Y" THEN A$(291,1)="N"
2720 REM "Subcontractor?
2730 A$(292,1)=""
2740 REM "Always Take Past Due Discounts
2750 A$(293,1)="N"; IF A$(293,1)<>"Y" THEN A$(293,1)="N"
2755 REM "Exclude Special Amts from Discount
2760 A$(294,6)=""
2765 GOTO 2860
2770 REM "Mailing Label code #1
2772 IF R$(279,3)=BLANKS$(1,3) THEN GOTO 2790
2780 A$(300,3)=R$(279,3)
2790 REM "Mailing Label code #2
2792 IF R$(282,3)=BLANKS$(1,3) THEN GOTO 2810
2800 A$(303,3)=R$(282,3)
2810 REM "Mailing Label code #3
2812 IF R$(285,3)=BLANKS$(1,3) THEN GOTO 2830
2820 A$(306,3)=R$(285,3)
2830 REM "Standard Amount
2832 IF R$(288,15)=BLANKS$(1,15) THEN GOTO 2842
2840 Q$=R$(288,15); GOSUB 7650; A[0]=X
2842 REM "web address
2843 IF R$(441,40)=DIM(40) THEN GOTO 2845 ELSE A$(324,40)=R$(441,40)
2845 REM "account
2846 IF R$(426,15)=DIM(15) THEN GOTO 2847 ELSE A$(309,15)=R$(426,15)
2847 REM "email address
2848 IF R$(482,40)=DIM(40) THEN GOTO 2850 ELSE A$(324,40)=R$(482,40)
2850 REM "# of Extra Due Days
2860 A[1]=0
3000 REM "AP5 Record
3010 DIM B$(38),B[6]
3015 B$(1,10)=A$(1,10)
3020 REM "Last Invoice Date
3022 IF R$(303,10)=BLANKS$(1,10) THEN GOTO 3060
3030 Q$=R$(303,10); GOSUB 7600; B$(11,6)=X$
3060 REM "Last Check Date
3062 IF R$(313,10)=BLANKS$(1,10) THEN GOTO 3080
3070 Q$=R$(313,10); GOSUB 7600; B$(17,6)=X$
3080 REM "1099 ID Number
3082 IF R$(323,14)=BLANKS$(1,14) THEN GOTO 3100
3090 B$(23,14)=R$(323,14)
3100 REM "1099 Type Code
3120 IF FNS(B$(23,14))>0 THEN B$(37,1)="1"
3160 REM "Balance Due Vendor
3162 IF R$(338,15)=BLANKS$(1,15) THEN GOTO 3180
3170 Q$=R$(338,15); GOSUB 7650; B[0]=X
3180 REM "Retainage Due Vendor - Not used
3182 IF R$(353,15)=BLANKS$(1,15) THEN GOTO 3200
3190 Q$=R$(353,15); GOSUB 7650; B[1]=X
3200 REM "Payments - Current 1099 Year
3202 IF R$(368,15)=BLANKS$(1,15) THEN GOTO 3220
3210 Q$=R$(368,15); GOSUB 7650; B[2]=X
3220 REM "# Checks Current 1099 Year
3222 IF R$(383,4)=BLANKS$(1,4) THEN GOTO 3240
3230 Q$=R$(383,4); GOSUB 7650; B[3]=INT(X)
3240 REM "Payments - Next 1099 Year
3242 IF R$(387,15)=BLANKS$(1,15) THEN GOTO 3260
3250 Q$=R$(387,15); GOSUB 7650; B[4]=X
3260 REM "# Checks - Next 1099 Year
3262 IF R$(402,4)=BLANKS$(1,4) THEN GOTO 3280
3270 Q$=R$(402,4); GOSUB 7650; B[5]=INT(X)
4800 REM "Vendor Map
4803 IF CA0$(62,1)="A" THEN GOTO 4831
4804 IF F9$="Y" THEN GOTO 4831
4805 REM "LET A$(1,10)=A$(1,2)+A$(11,8); REM "VENDOR CODE FROM DESCRIPTION
4806 Q9=0
4807 FIND (Z[2],KEY=A$(1,10),DOM=4810); IF Q9=0 THEN A$(10,1)="A",Q9=1; GOTO 4807 ELSE A$(10,1)=CHR(ASC(A$(10,1))+1); GOTO 4807
4810 DIM X$(100); Q$=R$(1,10),X$(1)="AP4 "+Q$
4820 READ (Z[12],KEY=X$(1,24),DOM=4821)X$; GOTO 4840
4830 X$(25,20)=A$(1,10); X$(45,35)=A$(145,10)+"/"+A$(11,35); WRITE (Z[12],KEY=X$(1,24))X$
4900 REM "Write file(s)
4909 IF CA0$(62,1)="A" THEN WRITE (Z[2],KEY=A$(1,10))IOL=0310; C2=C2+1; GOTO 4915
4910 WRITE (Z[2],KEY=A$(1,10),DOM=4911)IOL=0310; C2=C2+1; GOTO 4915
4912 PRINT @(0,21),"Duplicate: "+R$(1,17)+"|"+A$(1,10),; INPUT *; PRINT @(0,21),'CL',
4920 IF POS(" "<>B$(11))>0 THEN GOTO 4970
4930 FOR I=0 TO 6
4935 IF B[I]<>0 THEN EXITTO 4970
4940 NEXT I
4945 GOTO 4990
4970 WRITE (Z[3],KEY=B$(1,10))IOL=0320; C3=C3+1
4990 R5$=R$; REM "Save off previous R$ to use if needed in creating Plant record
4995 RETURN 
5000 REM "EOJ
5010 GOSUB 5200
5020 CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
5200 REM "COMPLETE CYCLE
5210 CALL "AP2UT5",X3$,"","","*"
5220 REM CALL "CA2AR6",X3$,"","","*"
5230 REM CALL "AR2UT4",X3$,"","","*"
5240 REM CALL "AR2UT6",X3$,"","","*"
5250 REM CALL "AR2UTD",X3$,"","","*"
5290 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This program converts the Vendor file to TopForm's AP4."
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7000 REM "Convert Q$ to upper case
7010 FOR I=1 TO LEN(Q$)
7015 IF Q$(I)>="a" AND Q$(I)<="z" THEN Q$(I)=CHR(ASC(Q$(I))-32)
7020 NEXT I
7045 RETURN 
7500 REM "Generalized Mapping
7510 X=0
7520 FIND (Z[12],KEY=X$(1,24),DOM=7521)X$,X; A$(I1,I2)=X$(25,I2); GOTO 7590
7560 X$(25)=Q$; WRITE (Z[12],KEY=X$(1,24))X$; GOTO 7520
7590 RETURN 
7600 REM "Generalized Date conversion
7610 IF FNS$(Q$)="" THEN X$=""; GOTO 7640
7620 CALL "CA2CVT",X3$,X4$,P9$(1,2),Q$,0,X$,0,"",0
7640 RETURN 
7650 REM "Generalized Numeric conversion
7660 IF FNS$(Q$)="" THEN X$="",X=0; GOTO 7690
7670 CALL "CA2CVT",X3$,X4$,P9$(5,2),Q$,0,X$,X,"",0
7690 RETURN 
7750 REM "Convert Phone Number
7760 IF FNS$(Q$)="" THEN X$=""; GOTO 7790
7770 CALL "CA2CVT",X3$,X4$,P9$(3,2),Q$,0,X$,0,"",0
7790 RETURN 
7800 REM "Convert Zip Code
7810 CALL "CA2CVT",X3$,X4$,P9$(7,2),Q$,0,X$,0,"",0
7840 RETURN 
8920 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
8930 DEF FNQ$(Z9$,Z9)=STR(NUM(Z9$):Z1$(1,Z9))
8940 DEF FNE$(Z9)=CHR(ASC("A")+INT(Z9/10))+STR(MOD(Z9,10))
8950 DEF FNS(Z9$)=POS(" "<>Z9$)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
