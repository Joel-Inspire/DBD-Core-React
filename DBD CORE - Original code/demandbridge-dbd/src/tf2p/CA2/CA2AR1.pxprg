0010 REM "Convert Customer File <CA2AR1>
0020 SETESC 9300; SETERR 9000
0035 REM "5.2 - 08/20/03 - 11.267777 - dmm - SSP# 163697
0040 REM "Copyright 2003 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0042 REM "Sales tax map numeric overrides customer tax exempt question
0080 BEGIN 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="CA2AR1",X1$="Convert to TopForm Customer File"
0120 DIM Z0$(80,"-"),Z1$(20,"0"),S0$(1000)
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15]
0320 IOLIST B$,B[0],B[1]
0330 IOLIST C$
0340 IOLIST D$,D[0],D[1],D[2],D[3]
0350 IOLIST E$,E[0],E[1],E[2]
0360 IOLIST DFLTS$,DFLTS[0],DFLTS[1],DFLTS[2],DFLTS[3]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O AR1...  03O FMP...  04O CA0... 10O AR0...  11O AR1ERR  12O MAPDAT  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0522 GOSUB 6000; CALL "CA2MCA",X3$,X4$,10,CA0_SELECTED$,CA0_OPTIONS$
0524 IF CA0_SELECTED$(1,4)=S0$(1,4) THEN GOTO 9900
0530 READ (Z[13],KEY=X3$(9,3)+"A/R")P0$
0535 READ (Z[4],KEY=CA0_SELECTED$(1,4))CA0$
0600 REM "
0605 READER$="CA2R"+STP(CA0$(25,2),1)
0610 GOSUB 6000
0615 CALL READER$,X3$,X4$,CA0$,CA1${ALL},"O","",MEM${ALL},R$,RET_CODE
0640 CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0700 IF CA0$(62,1)="I" THEN CALL "ZZINIT",STR(Z[2]:"00")+STR(Z[10]:"00")+STR(Z[11]:"00")
0710 X$="AR1 AR2 AR3 AR5 ARG DIV "
0720 FOR I=1 TO LEN(X$) STEP 4
0725 J$="Clear Mapping for "+FNS$(X$(I,4))+"?"
0730 CALL "ZZPROM",".Y",X3$,Z,J$,"","",0; ON Z GOTO 0731,0790
0735 C9=0,C9$="\|/-",C8=LEN(C9$); PRINT @(20,15),"Clearing ",FNS$(X$(I,4)),":",
0740 READ (Z[12],KEY=X$(I,4),DOM=0741)
0750 K$=KEY(Z[12],END=0780); C9=C9+1; PRINT @(35,15),C9$(MOD(C9,C8)+1,1),; IF K$(1,4)=X$(I,4) THEN REMOVE (Z[12],KEY=K$); GOTO 0750
0780 PRINT @(0,15),'CL',
0790 NEXT I
0800 REM "Remove Customer Default Records
0805 IF CA0$(62,1)<>"I" THEN GOTO 0850; REM 'BYPASS IF NOT INIT PASS
0810 READ (Z[3],KEY="D",DOM=0811)
0820 X$=KEY(Z[3],END=0850); IF X$(1,1)="D" THEN PRINT @(20,22),X$,; REMOVE (Z[3],KEY=X$); GOTO 0820
0920 H0$=$0A$
0940 READ (Z[12],KEY="*PARAMETERS")P9$
0950 ADDR "CA2CVT"
0955 REM "set o1x, o1y from override #1 if needed in 2100's
0956 O1X=0,O1Y=0; IF STP(CA0$(32,10),3," ")="" THEN GOTO 0960 ELSE TMP$=CA0$(32,10),CPOS=POS(","=TMP$); IF CPOS<>0 THEN O1X=NUM(TMP$(1,CPOS-1)),O1Y=NUM(TMP$(CPOS+1))
1000 REM "Read Data file
1010 DIM R$(1000); CALL READER$,X3$,X4$,CA0$,CA1${ALL},"R","",MEM${ALL},R$,RET_CODE; IF RET_CODE=2 THEN GOTO 5000
1011 REM "DON'T FORGET TO REMOVE LINE 699
1020 IF STP(R$,3," ")="" THEN GOTO 1000
1180 GOSUB 2000
1300 REM "Display progress every 2%
1900 GOTO 1000
2000 REM "Initialize TopForm Record
2100 DIM A$(599),A[15],S$(50),B$(240),B[2]
2105 REM "If override #1 is not blank then lookup division based on R$(x,y) from map. If not in map set to 00. o1x,o1y set in 900's once
2110 IF O1X=0 OR O1Y=0 THEN DIVISION$="00"; GOTO 2116
2112 DIM X$(100); X$(1)="DIV "+R$(O1X,O1Y); FIND (Z[12],KEY=X$(1,24),DOM=2113)X$; GOTO 2114
2113 X$(25,2)="00"; WRITE (Z[12],KEY=X$(1,24))X$
2115 DIVISION$=X$(25,2)
2200 REM "Customer Code
2203 REM ' IF CA0$(27,1)="Q" THEN IF R$(5,1)<>" " THEN GOTO 04990; REM "For quantum, skip shiptos
2205 Q$=STP(R$(1,17),1); DIM X$(100); X$(1)="AR1 "+Q$
2210 X=NUM(P0$(14,1)),A$(1,10)=Z1$,A$(3,X)=Q$,A$(3,X)=STR(NUM(Q$,ERR=2211):Z1$(1,X))
2212 A$(1,2)=DIVISION$
2215 FIND (Z[12],DOM=2216,KEY=X$(1,24))X$; A$(1,10)=X$(25); GOTO 2217
2216 X$(25)=A$(1,10); WRITE (Z[12],KEY=X$(1,24))X$; REM 'WRITE OUT MAPPING WITH DIVISION
2217 IF CA0$(62,1)="A" THEN READ (Z[2],KEY=A$(1,10),DOM=4990)IOL=0310; GOTO 2225; REM 'APPENDING PASS
2220 FIND (Z[2],KEY=A$(1,10),DOM=2221)IOL=0310; GOSUB 8000; GOTO 4990; REM "Duplicate
2225 REM "Alternate Bill-to
2226 IF FNS$(R$(302,10))="" THEN GOTO 2240
2230 Q$=FNS$(R$(302,10)); IF Q$="" THEN GOTO 2240 ELSE DIM X$(100); X$(1)="AR1 "+Q$
2235 FIND (Z[12],DOM=2236,KEY=X$(1,24))X$; A$(46,10)=X$(25)
2240 REM "Customer name
2242 IF STP(R$(18,35),3," ")="" THEN GOTO 2260
2250 A$(11,35)=R$(18,35)
2260 REM "Address line 1
2262 IF STP(R$(53,30),3," ")="" THEN GOTO 2280
2270 A$(56,30)=R$(53,30)
2280 REM "Address line 2
2282 IF STP(R$(113,25),3," ")="" THEN GOTO 2300
2290 IF R$(83,POS(" "=R$(113,25))-1)<>R$(113,POS(" "=R$(113,25))-1) THEN A$(86,30)=R$(83,30)
2295 IF STP(A$(56,30),3," ")="" THEN A$(56,30)=A$(86,30),A$(86,30)=""
2300 REM "City
2305 IF STP(R$(552,30),3," ")="" THEN GOTO 2306 ELSE GOSUB 7900
2309 IF STP(R$(113,25),3," ")="" THEN GOTO 2320
2310 A$(116,16)=R$(113,25)
2320 REM "State
2322 IF STP(R$(138,2),3," ")="" THEN GOTO 2340
2330 A$(132,2)=R$(138,2)
2340 REM "Zip code
2342 IF STP(R$(140,10),3," ")="" THEN GOTO 2360
2345 Q$=R$(140,10)
2350 GOSUB 7800; A$(134,9)=X$
2360 REM "Fax number
2362 IF STP(R$(177,14),3," ")="" THEN GOTO 2370
2370 Q$=R$(177,14); GOSUB 7750; A$(143,10)=X$
2390 REM "Lookup Sequence
2400 IF STP(R$(201,10),3," ")="" AND STP(A$(155,10),3," ")="" THEN TMP$=STP(UCS(CVS(R$(18,35),16)),0); IF LEN(TMP$)>4 AND TMP$(1,4)="THE " THEN TMP$=TMP$(5) END_IF ELSE TMP$=R$(201,10)
2410 A$(155,10)=TMP$
2420 REM "Purchasing Agent
2422 IF STP(R$(225,20),3," ")="" THEN GOTO 2440
2430 A$(165,20)=R$(225,20)
2440 REM "Greeting
2450 A$(185,20)=R$(441,20)
2452 IF STP(R$(441,20),3," ")="" THEN GOTO 2460
2460 REM "Phone Number (205,14)=Phone number + Extension
2462 IF STP(R$(211,14),3," ")="" THEN GOTO 2490
2470 Q$=R$(211,14); GOSUB 7750; A$(205,10)=X$
2480 IF X3$(9,3)="121" AND FNS$(R$(542,3))>"" THEN A$(205,3)=R$(542,3)
2490 REM "Phone Extension
2500 A$(215,4)=""
2510 REM "Customer Status
2512 IF STP(R$(394,1),3," ")="" THEN GOTO 2530
2520 A$(219,1)="A"; IF POS(R$(394,1)=" Aa")=0 THEN A$(219,1)="I"
2530 REM "Customer Category
2535 Q$=R$(382,9)
2540 DIM X$(100); X$(1,24)="ARG "+Q$,I1=220,I2=9; GOSUB 7500
2570 REM "Date Account Opened
2572 IF STP(R$(167,10),3," ")="" THEN GOTO 2610
2590 Q$=R$(167,10); GOSUB 7600; A$(229,6)=X$
2610 REM "Sales Tax Code
2612 IF STP(R$(261,10),3," ")="" THEN GOTO 2650
2620 Q$=R$(261,10); DIM X$(100); X$(1)="AR5 "+Q$,I1=235,I2=10; GOSUB 7500
2650 REM "Customer Tax Exempt?
2652 IF STP(R$(166,1),3," ")="" THEN GOTO 2680
2660 A$(245,1)=R$(166,1); IF X=1 THEN A$(245,1)="Y"
2670 IF A$(245,1)<>"Y" THEN A$(245,1)="N"
2680 REM "Resale Number
2682 IF STP(R$(245,16),3," ")="" THEN GOTO 2700
2690 A$(246,16)=R$(245,16)
2700 REM "SIC Code
2702 IF STP(R$(537,5),3," ")="" THEN GOTO 2720
2710 A$(262,5)=R$(537,5)
2720 REM "Finance Charge Code
2722 IF STP(R$(405,1),3," ")="" THEN GOTO 2740
2730 A$(267,1)=R$(405,1); IF POS(A$(267,1)=" Yy")>0 THEN A$(267,1)="Y" ELSE A$(267,1)="N"
2740 REM "Statement Code
2742 IF STP(R$(290,2),3," ")="" THEN GOTO 2770
2750 A$(268,1)=R$(290,1); IF POS(A$(268,1)=" Yy")>0 THEN A$(268,1)="Y" ELSE A$(268,1)="N"
2770 REM "Mailing Label code #1
2772 IF STP(R$(431,3),3," ")="" THEN GOTO 2790
2780 A$(269,3)=R$(431,3)
2790 REM "Mailing Label code #2
2792 IF STP(R$(434,3),3," ")="" THEN GOTO 2810
2800 A$(272,3)=R$(434,3)
2810 REM "Mailing Label code #3
2812 IF STP(R$(437,3),3," ")="" THEN GOTO 2830
2820 A$(275,3)=R$(437,3)
2830 REM "Salesperson Code #1
2832 IF STP(R$(286,4),3," ")="" THEN GOTO 2870
2840 Q$=R$(286,4),I1=278,I2=4; GOSUB 7700
2870 REM "Salesperson Code #2
2872 IF STP(R$(521,4),3," ")="" THEN GOTO 2910
2880 Q$=R$(521,4),I1=282; GOSUB 7700
2910 REM "Salesperson Code #3
2912 IF STP(R$(525,4),3," ")="" THEN GOTO 2950
2920 Q$=R$(525,4),I1=286; GOSUB 7700
2950 REM "Salesperson Code #4
2952 IF STP(R$(529,4),3," ")="" THEN GOTO 2990
2960 Q$=R$(529,4),I1=290; GOSUB 7700
2990 REM "Salesperson Code #5
2992 IF STP(R$(533,4),3," ")="" THEN GOTO 3030
3000 Q$=R$(533,4),I1=294; GOSUB 7700
3030 REM "Credit Contact
3032 IF STP(R$(332,20),3," ")="" THEN GOTO 3060
3040 A$(298,20)=R$(332,20)
3050 IF FNS(A$(298,20))=0 THEN A$(298,20)=A$(165,20)
3060 REM "Credit Contact Greeting
3062 IF STP(R$(312,20),3," ")="" THEN GOTO 3090
3070 A$(310,20)=R$(312,20)
3080 IF FNS(A$(310,20))=0 THEN A$(310,20)=A$(185,20)
3090 REM "Credit Contact Phone Number
3092 IF STP(R$(352,14),3," ")="" THEN GOTO 3160
3130 Q$=R$(352,14); GOSUB 7750; A$(338,14)=X$
3140 IF FNS(A$(338,14))=0 THEN A$(338,14)=A$(205,14)
3160 REM "Dun Letter Type Last Printed
3170 A$(352,1)=""
3180 REM "Date of Last Dun Letter
3190 Q$=""; GOSUB 7600; A$(353,6)=X$
3200 REM "Terms Code
3202 IF STP(R$(292,10),3," ")="" THEN GOTO 3240
3210 Q$=R$(292,10); DIM X$(100); X$(1)="AR2 "+Q$,I1=359,I2=2; GOSUB 7500
3240 REM "Credit Hold
3242 IF STP(R$(150,1),3," ")="" THEN GOTO 3270
3250 A$(361,1)=R$(150,1); IF POS(A$(361,1)="Yy")>0 THEN A$(361,1)="Y"
3270 REM "Credit Rating
3272 IF STP(R$(366,6),3," ")="" THEN GOTO 3290
3280 A$(362,5)=R$(366,6)
3290 REM "Credit Rating Date
3292 IF STP(R$(372,10),3," ")="" THEN GOTO 3320
3300 Q$=R$(372,10); GOSUB 7600; A$(367,6)=X$
3320 REM "Date Credit Limit Set
3322 IF STP(R$(395,10),3," ")="" THEN GOTO 3350
3330 Q$=R$(395,10); GOSUB 7600; A$(373,6)=X$
3350 REM "Last Active Date
3352 IF STP(R$(191,10),3," ")="" THEN GOTO 3380
3370 Q$=R$(191,10); GOSUB 7600; A$(379,6)=X$
3380 REM "High Credit Date
3382 IF STP(R$(421,10),3," ")="" THEN GOTO 3400
3390 Q$=R$(421,10); GOSUB 7600; A$(385,6)=X$
3400 REM "Print Dun Letter
3402 IF STP(R$(440,1),3," ")="" THEN GOTO 3430
3410 A$(397,1)=R$(440,1)
3420 IF A$(397,1)=" " THEN A$(397,1)="Y"
3430 REM "Customer Price Category
3432 IF STP(R$(391,3),3," ")="" THEN GOTO 3450
3440 A$(398,3)=R$(391,3)
3450 REM "Telemarketing List Code
3460 A$(401,4)=""
3470 REM "Telemarketing Contact Code
3480 A$(405,6)=""
3490 REM "Salesperson 1 Commission Split
3500 Q$=R$(271,15); GOSUB 7650; A[0]=X; IF X=0 THEN A[0]=100
3510 REM "Salesperson 2 Commission Split
3512 IF STP(R$(461,15),3," ")="" THEN GOTO 3530
3520 Q$=R$(461,15); GOSUB 7650; A[1]=X
3530 REM "Salesperson 3 Commisssion Split
3532 IF STP(R$(476,16),3," ")="" THEN GOTO 3550
3540 Q$=R$(476,15); GOSUB 7650; A[2]=X
3550 REM "Salesperson 4 Commission Split
3552 IF STP(R$(491,15),3," ")="" THEN GOTO 3570
3560 Q$=R$(491,15); GOSUB 7650; A[3]=X
3570 REM "Salesperson 5 Commission Split
3572 IF STP(R$(506,15),3," ")="" THEN GOTO 3590
3580 Q$=R$(506,15); GOSUB 7650; A[4]=X
3590 REM "Credit Limit
3592 IF STP(R$(151,15),3," ")="" THEN GOTO 3610
3600 Q$=R$(151,15); GOSUB 7650; A[5]=INT(X)
3610 REM "A/R Balance
3620 A[6]=0
3630 REM "Future Balance
3640 A[7]=0
3650 REM "Current Balance
3660 A[8]=0
3670 REM "30 Day Balance
3680 A[9]=0
3690 REM "60 Day Balance
3700 A[10]=0
3710 REM "90 Day Balance
3720 A[11]=0
3730 REM "120 Day Balance
3740 A[12]=0
3750 REM "Open Order Value
3760 A[13]=0
3770 REM "High Credit
3772 IF STP(R$(406,15),3," ")="" THEN GOTO 3800
3780 Q$=R$(406,15); GOSUB 7650; A[14]=X
3800 REM "Alternate customer code = old code
3802 IF STP(R$(1,17),3," ")="" THEN GOTO 4000
3810 A$(411,20)=R$(1,17)
4000 REM "Build Customer Default Record
4020 B$=""; DIM DFLTS[3]
4025 REM 'GET DEFAULTS
4027 READ (Z[13],KEY="DFLTFM2SHA",DOM=4800)IOL=0360
4030 B$="Y"; DFLTS$(1,11)="D"+A$(1,10)
4035 GOTO 4800
4050 REM "Finder's Fee Vendor Code
4060 IF FNS$(R$(542,10))="" OR FNU$(R$(542,10))="" THEN GOTO 4200
4070 DIM X$(100); X$(1)="AP4 "+R$(542,10)
4080 FIND (Z[12],KEY=X$(1,24))X$
4090 B$(13,10)=X$(25,10)
4100 REM "Finder's Fee Type
4110 B$(12,1)="S"
4120 REM "Finder's Fee Percentage
4130 CALL "CA2CVT",X3$,X4$,P9$(5,2),R$(812,4),0,X$,X,"",0; A[0]=X
4200 REM "Customer P/O Required?
4210 IF R$(276,1)="Y" THEN B$(25,1)="Y"
4800 REM "Customer Map
4810 DIM X$(100); Q$=STP(R$(1,17),1),X$(1)="AR1 "+Q$
4820 READ (Z[12],KEY=X$(1,24),DOM=4821)X$; GOTO 4900
4830 X$(25,20)=A$(1,10),X$(45,35)=A$(155,10)+"/"+A$(11,35); WRITE (Z[12],KEY=X$(1,24))X$
4900 REM "Write file(s)
4905 GOSUB 8600
4910 WRITE (Z[2],KEY=A$(1,10))IOL=0310; C2=C2+1
4920 IF B$="" THEN GOTO 4990
4930 WRITE (Z[3],KEY=DFLTS$(1,11))IOL=0360; C3=C3+1
4990 RETURN 
5000 REM "EOJ
5010 GOSUB 5200
5020 CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
5200 REM "COMPLETE CYCLE
5210 CALL "AR2UT8",X3$,"","","*"
5220 REM CALL "CA2AR6",X3$,"","","*"
5230 REM CALL "AR2UT4",X3$,"","","*"
5240 REM CALL "AR2UT6",X3$,"","","*"
5250 REM CALL "AR2UTD",X3$,"","","*"
5290 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This program converts the Customer file to TopForm's AR1."
6025 PRINT @(0,7),"If override #1 is not blank, then assume x,y format.",@(0,8),"    Use 'DIV '+R$(x,y) as key to get division from map.",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7500 REM "Generalized Mapping
7510 X=0
7520 FIND (Z[12],KEY=X$(1,24),DOM=7521)X$,X; A$(I1,I2)=X$(25,I2); GOTO 7590
7560 X$(25)=Q$; WRITE (Z[12],KEY=X$(1,24))X$; GOTO 7520
7590 RETURN 
7600 REM "Generalized Date conversion
7610 IF FNS$(Q$)="" THEN X$=""; GOTO 7640
7620 CALL "CA2CVT",X3$,X4$,P9$(1,2),Q$,0,X$,0,"",0
7640 RETURN 
7650 REM "Generalized Numeric conversion
7660 IF STP(Q$,1)="" THEN X$="",X=0; GOTO 7690
7670 CALL "CA2CVT",X3$,X4$,P9$(5,2),Q$,0,X$,X,"",0
7690 RETURN 
7700 REM "Salesperson's Map
7705 REM 'IF FNS$(Q$)="" THEN RETURN
7710 DIM X$(100); X$(1)="AR3 "+Q$
7715 IF R$(1,1)="M" THEN X$(1)="AR3 "+"M"+Q$
7720 GOTO 7500
7750 REM "Convert Phone Number
7760 IF FNS$(Q$)="" THEN X$=""; GOTO 7790
7770 CALL "CA2CVT",X3$,X4$,P9$(3,2),Q$,0,X$,0,"",0
7790 RETURN 
7800 REM "Convert Zip Code
7810 CALL "CA2CVT",X3$,X4$,P9$(7,2),Q$,0,X$,0,"",0
7840 RETURN 
7900 REM "Decode combined city, state zip field and place into approriate places in R$ IF they are blank. Assumes 'CITY, STATE, ZIP' format where state is 2 chars and comma seperates city and state. If no comma then looks for 1st blank
7905 TMP$=STP(R$(552,30),2); CITY$="",STATE$="",ZIP$="",CPOS=POS(","=TMP$); IF CPOS=0 THEN CPOS=POS(" "=TMP$); IF CPOS=0 THEN CITY$=TMP$; GOTO 7920
7910 CITY$=TMP$(1,CPOS-1); TMP$=STP(TMP$(CPOS+1),2),STATE$=TMP$(1,2),ZIP$=STP(TMP$(3),2)
7920 IF CITY$<>"" AND STP(R$(113,25),3," ")="" THEN R$(113,25)=CITY$
7925 IF STATE$<>"" AND STP(R$(138,2),3," ")="" THEN R$(138,2)=STATE$
7930 IF ZIP$<>"" AND STP(R$(140,10),3," ")="" THEN R$(140,10)=ZIP$
7945 RETURN 
8000 REM "AR1 Duplicate File
8010 I8$=STR(NUM(I8$)+1:"0000")
8020 WRITE (Z[11],KEY=R$(1,15)+I8$)R$
8040 RETURN 
8600 REM " CUSTOM SPECIFIC DEFAULTS
8695 RETURN 
8920 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
8925 DEF FNU$(Z9$)=Z9$(1,POS("000"=Z9$+"000")-1)
8930 DEF FNQ$(Z9$,Z9)=STR(NUM(Z9$):Z1$(1,Z9))
8940 DEF FNE$(Z9)=CHR(ASC("A")+INT(Z9/10))+STR(MOD(Z9,10))
8950 DEF FNS(Z9$)=POS(" "<>Z9$)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 DROP "CA2CVT",ERR=9921
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "UT2NUM"
9999 END 
