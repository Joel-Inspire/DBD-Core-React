0010 REM "Convert to TopForm Formats <CA2CVT>
0035 REM "5.2 - 08/20/03 - 11.282222 - dmm - SSP# 163697
0040 REM "Copyright 2003 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "T$=Type, - D(ate), P(hone)"
0051 REM "A$,A=Source, B$,B=Answer
0052 REM "C$,C=not used
0100 ENTER X3$,X4$,T$,A$,A,B$,B,C$,C
0110 B$="",B=0; DIM S$(20)
0200 IF T$(1,1)="D" THEN GOTO 1000; REM "Date
0210 IF T$(1,1)="P" THEN GOTO 1500; REM "Phone number
0220 IF T$(1,1)="N" THEN GOTO 2000; REM "Numeric
0230 IF T$(1,1)="Z" THEN GOTO 2500; REM "Zip Code
1000 REM "Date Conversion
1004 IF T$(2,1)="2" THEN B1$=A$; GOTO 1150
1005 SEPERATORS$="/-."
1006 FOR II=1 TO LEN(SEPERATORS$)
1007 P9=POS(SEPERATORS$(II,1)=A$); IF P9>0 THEN IF P9=1 THEN A$=A$(2) ELSE A$=STR(NUM(A$(1,P9-1)):"00")+A$(P9+1); GOTO 1007
1008 NEXT II
1010 B1$=STR(NUM(A$,ERR=1490):"000000")
1020 IF LEN(B1$)<6 OR POS(" "<>B1$)=0 OR POS("0"<>B1$)=0 THEN GOTO 1490
1030 ON NUM(T$(2,1)) GOTO 1050,1100,1150
1050 REM "Type 0 - YYMMDD format
1060 IF B1$(1,2)<"50" THEN B1$="20"+B1$ ELSE B1$="19"+B1$ ! Convert to YYYYMMDD format first
1065 B$=CHR(NUM(B1$(1,3),ERR=*NEXT)-125)+B1$(4,1)+B1$(5,4)
1070 ! IF B1$(1,2)>="90" THEN B$=FNE$(NUM(B1$(1,2),ERR=1490))+B1$(3,4) ELSE B$=FNF$(NUM(B1$(1,2),ERR=1490))+B1$(3,4)
1090 GOTO 1490
1100 REM "Type 1 - MMDDYY format
1110 IF B1$(5,2)<"50" THEN B1$="20"+B1$(5,2)+B1$(1,4) ELSE B1$="19"+B1$(5,2)+B1$(1,4) ! Convert to YYYYMMDD format first
1115 B$=CHR(NUM(B1$(1,3),ERR=*NEXT)-125)+B1$(4,1)+B1$(5,4)
1120 ! IF B1$(5,2)>="90" THEN B$=FNE$(NUM(B1$(5,2),ERR=1490))+B1$(1,4) ELSE B$=FNF$(NUM(B1$(5,2),ERR=1490))+B1$(1,4)
1130 GOTO 1490
1150 REM "Type 2 - YYYYMMDD format
1160 IF POS(" "<>B1$)=0 THEN B$=""; GOTO 1490 ELSE B$=""
1163 IF B1$(1,1)=" " THEN B1$=B1$(2)+" "; GOTO 1163
1165 ! IF B1$(3,2)>="90" THEN B$=FNE$(NUM(B1$(3,2),ERR=1490))+B1$(5,4) ELSE B$=FNF$(NUM(B1$(3,2),ERR=1490))+B1$(5,4)
1166 B$=CHR(NUM(B1$(1,3),ERR=*NEXT)-125)+B1$(4,1)+B1$(5,4)
1195 GOTO 1490
1490 GOTO 9900
1500 REM "Phone number conversion
1510 IF POS(" "<>A$)=0 THEN GOTO 1990
1520 ON NUM(T$(2,1)) GOTO 1550
1550 REM "Type 0 - Take first 10 digits
1570 FOR I=1 TO LEN(A$)
1580 IF POS(A$(I,1)="0123456789")>0 THEN B$=B$+A$(I,1)
1590 NEXT I
1900 REM "Fix up A1$
1910 IF LEN(B$)>3 AND LEN(B$)<=7 THEN B$="   "+B$
1990 GOTO 9900
2000 REM "Numeric Conversion
2010 IF LEN(A$)>10 AND A$(1,1)="0" THEN A$=A$(2); GOTO 2010
2020 ON NUM(T$(2,1)) GOTO 2050
2050 REM "Type 0 - Strip - sign at end of string
2055 X=1,B$=A$
2057 P=POS("+"=B$); IF P>0 THEN B$=B$(1,P-1)+B$(P+1); GOTO 2057
2058 P=POS("$"=B$); IF P>0 THEN B$=B$(1,P-1)+B$(P+1); GOTO 2058
2059 P=POS(","=B$); IF P>0 THEN B$=B$(1,P-1)+B$(P+1); GOTO 2059
2060 P=POS("-"=B$); IF P>0 THEN B$=B$(1,P-1)+B$(P+1),X=-1; GOTO 2060
2070 B=NUM(B$,ERR=2071)*X; GOTO 2400
2080 B=0; GOTO 2400
2400 REM 
2490 GOTO 9900
2500 REM "Zip Code Conversion
2510 IF POS(" "<>A$)=0 THEN GOTO 2990
2520 ON NUM(T$(2,1)) GOTO 2550
2550 REM "eliminate, dash's and 4-digit 0's
2560 B$=A$
2565 IF LEN(B$)<1 THEN GOTO 2990 ELSE IF B$(1,1)=" " THEN B$(1)=B$(2); GOTO 2565
2570 P0=POS("-"=B$); IF P0>0 THEN B$=B$(1,P0-1)+B$(P0+1); GOTO 2570
2575 IF LEN(B$)>9 THEN B$=B$(1,9)
2580 IF B$(6,4)="0000" THEN B$(6,4)=""
2590 GOTO 2990
2990 GOTO 9900
8920 DEF FNS$(Z9$)=Z9$(1,POS(S$=Z9$+S$)-1)
8940 DEF FNE$(Z9)=CHR(ASC("A")+INT(Z9/10))+STR(MOD(Z9,10))
8942 DEF FNF$(Z9)=CHR(ASC("K")+INT(Z9/10))+STR(MOD(Z9,10))
9900 EXIT 
9999 END 
