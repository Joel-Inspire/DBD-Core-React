0010 REM "Convert Customer Ship-to File <CA2FM0>
0020 SETESC 9300; SETERR 9000
0035 REM "5.2 - 09/24/03 - 14.946666 - dmm - SSP# 163697
0040 REM "Copyright 2003 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0080 BEGIN 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="CA2FM0",X1$="Convert Customer Ship-to File"
0120 DIM Z0$(80,"-"),Z1$(20,"0"),C[15],C$(512)
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST A$
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))],BLANKS$(100)
0510 Z$="02O FM0...  03O AR1...  11O CA0... 12O MAPDAT  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0522 GOSUB 6000; CALL "CA2MCA",X3$,X4$,10,CA0_SELECTED$,CA0_OPTIONS$
0524 IF CA0_SELECTED$(1,4)=DIM(4) THEN GOTO 9900
0530 READ (Z[13],KEY=X3$(9,3)+"A/R")P0$
0535 READ (Z[11],KEY=CA0_SELECTED$(1,4))CA0$
0600 REM "
0605 READER$="CA2R"+STP(CA0$(25,2),1)
0610 GOSUB 6000
0615 CALL READER$,X3$,X4$,CA0$,CA1${ALL},"O","",MEM${ALL},R$,RET_CODE
0640 CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0710 X$="FM0C"
0720 FOR I=1 TO LEN(X$) STEP 4
0725 J$="Clear Mapping for "+FNS$(X$(I,4))+"?"
0730 CALL "ZZPROM",".Y",X3$,Z,J$,"","",0; ON Z GOTO 0731,0790
0735 C9=0,C9$="\|/-",C8=LEN(C9$); PRINT @(20,15),"Clearing ",FNS$(X$(I,4)),":",
0740 READ (Z[12],KEY=X$(I,4),DOM=0741)
0750 K$=KEY(Z[12],END=0780); C9=C9+1; PRINT @(35,15),C9$(MOD(C9,C8)+1,1),; IF K$(1,4)=X$(I,4) THEN REMOVE (Z[12],KEY=K$); GOTO 0750
0780 PRINT @(0,15),'CL',
0790 NEXT I
0800 REM "Clear Ship-tos
0805 C9$="\|/-",C8=LEN(C9$); PRINT @(15,15),"Clearing Ship-tos",
0810 READ (Z[2],KEY="C",DOM=0811)
0820 K$=KEY(Z[2],END=0830); IF K$(1,1)="C" THEN REMOVE (Z[2],KEY=K$); C5=C5+1; PRINT @(34,15),C9$(MOD(C5,C8)+1,1),; GOTO 0820
0830 PRINT @(15,15),'CL',
0900 REM 
0920 H0$=$0A$
0940 READ (Z[12],KEY="*PARAMETERS")P9$
0950 ADDR "CA2CVT"
0990 INDEX=1
0999 DIM BLANKS$(100)
1000 REM "Read Data file
1010 DIM R$(1000); CALL READER$,X3$,X4$,CA0$,CA1${ALL},"R","",MEM${ALL},R$,RET_CODE; IF RET_CODE=2 THEN GOTO 5000
1180 GOSUB 2000
1900 GOTO 1000
2000 REM "Initialize TopForm Record
2100 DIM A$(618),S$(50)
2110 A$(1,1)="C",A$(124,1)="1"
2118 REM ' IF CA0$(27,1)="Q" THEN IF R$(5,1)<>" " THEN GOTO 02126 ELSE GOTO 04990; REM "For quantum, skip non-shiptos
2150 REM "Customer Sold-to Code
2155 IF CA0$(27,1)="Q" THEN Q$=R$(1,4) ELSE Q$=STP(R$(1,21),2)
2160 DIM X$(100); X$(1)="AR1 "+Q$
2170 FIND (Z[3],KEY=X$(5,10),DOM=2171)IOL=0330; A$(2,10)=Q$; GOTO 2200
2180 FIND (Z[12],DOM=2181,KEY=X$(1,24))X$; A$(2,10)=X$(25); GOTO 2200
2183 DIM C$(LEN(C$)); A$(2,10)=Q$,X$(25)=Q$; WRITE (Z[12],KEY=X$(1,24))X$; GOTO 2200
2185 REM PRINT X$,; INPUT *
2190 A$(1)="Customer code "+Q$+" missing from map"; GOTO 4990
2205 REM "Customer Ship-to Code
2208 Q$=R$(22,4); IF STP(Q$,3," ")="" THEN Q$="0001"
2210 DIM X$(100); X$(1)="FM0C"+FNS$(R$(1,21))+"|"+Q$
2215 A$(12,4)=Q$,A$(12,4)=STR(NUM(A$(12,4),ERR=2216):"0000")
2220 FIND (Z[12],DOM=2221,KEY=X$(1,24))X$; A$(12,4)=X$(25); GOTO 2230
2225 X$(25)=A$(12,4); X$(45)=R$(11,30); WRITE (Z[12],KEY=X$(1,24))X$
2235 FIND (Z[2],KEY=A$(1,15),DOM=2236); A$(12,4)=STR(NUM(A$(12,4))+1:"0000"); GOTO 2235
2236 IF LEN(A$)<512 THEN IF A$(2,10)=BLANKS$(1,10) THEN GOTO 1000
2240 REM "Customer Ship-to name
2250 A$(16,35)=R$(26,35)
2260 REM "Address line 1
2270 A$(51,30)=R$(66,30)
2280 REM "Address line 2
2285 IF R$(106,30)=PREV_LINE2$ THEN GOTO 2300 ELSE PREV_LINE2$=R$(106,30)
2290 A$(81,30)=R$(106,30)
2295 IF POS(" "<>A$(51,30))=0 THEN A$(51,30)=A$(81,30),A$(81,30)=""
2300 REM "City
2310 A$(111,16)=R$(146,20)
2320 REM "State
2330 A$(127,2)=R$(166,2)
2340 REM "Zip code
2350 Q$=R$(168,10); GOSUB 7800; A$(129,9)=X$
2360 REM "Fax number
2365 Q$=FNS$(R$(357,14)); IF X3$(9,3)="125" THEN IF LEN(Q$)>2 THEN Q$=Q$(3)
2370 GOSUB 7750; A$(138,10)=X$
2390 REM "Lookup Sequence
2400 Q$=R$(220,10); IF STP(Q$,3," ")="" THEN Q$=A$(16,10)
2402 A$(150,10)=UCS(Q$)
2420 REM "Purchasing Agent
2430 A$(160,20)=R$(250,20)
2440 REM "Greeting
2450 A$(180,20)=R$(270,20)
2460 REM "Phone Number
2470 Q$=R$(178,14); GOSUB 7750; A$(200,10)=X$
2490 REM "Phone Extension
2500 A$(210,4)=""
2510 REM "Customer Location Code
2520 IF STP(R$(203,9),3," ")="" THEN Q$=A$(12,4) ELSE Q$=R$(203,9)
2530 A$(214,9)=Q$
2540 REM "Customer Ship-to FMS Method
2550 A$(223,1)=" "
2610 REM "Sales Tax Code
2620 Q$=R$(192,10); IF FNS$(Q$)="" THEN A$(224,10)=C$(235,10); GOTO 2650
2630 DIM X$(100); X$(1)="AR5 "+Q$,I1=224,I2=10; GOSUB 7500
2650 REM "Customer Ship-to Tax Exempt?
2660 A$(283,1)=R$(202,1); IF A$(283,1)=" " THEN A$(283,1)=C$(245,1)
2670 IF A$(283,1)<>"Y" THEN A$(283,1)="N"
2680 REM "Cost Center
2690 A$(236,9)=""
2700 REM "Multiple Count Stops
2710 A$(245,1)=""
2720 REM "Digits for Count Stop
2730 A$(246,2)=""
2740 REM "Location Name - Only set to Name if Description is different from name
2750 A$(248,35)=A$(16,35)
2760 REM " Default Supply Warehouse
2770 A$(284,4)=R$(492,4); REM ' IF P0$(13,1)="Y" THEN LET A$(286,2)=A$(2,2)
2780 REM "Default FOB Code
2790 A$(288,1)=R$(373,1)
2800 REM "How to ship code
2810 A$(289,1)=R$(374,1)
2820 REM "Inside delivery
2830 A$(290,15)=R$(298,15)
2840 REM "Ship via code
2850 A$(305,1)=R$(375,1)
2860 REM "Ship via text
2870 A$(306,15)=R$(376,15)
2880 REM "Count schedule for estimated sites
2890 A$(321,26)=""
3000 REM "Receiving Dept
3010 IF STP(R$(230,20),3," ")="" THEN Q$="" ELSE Q$=R$(230,20)
3015 A$(382,20)=Q$
3030 REM "CE Id"
3040 A$(375,1)=R$(391,1)
3050 REM "Email Address"
3060 A$(522,40)=R$(392,40)
3070 REM "Exclude from WebEC
3080 A$(235,1)=R$(371,1)
4900 REM "Write file(s)
4910 WRITE (Z[2],KEY=A$(1,15))IOL=0310; C2=C2+1
4990 RETURN 
5000 REM "EOJ
5010 GOSUB 5200
5020 CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
5200 REM "COMPLETE CYCLE
5210 CALL "FM2UTT",X3$,"","","*"
5290 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This program converts the Customer Ship-to file to TopForm's FM0."
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7500 REM "Generalized Mapping
7510 X=0
7520 FIND (Z[12],KEY=X$(1,24),DOM=7521)X$,X; A$(I1,I2)=X$(25,I2); GOTO 7590
7560 X$(25)=Q$; WRITE (Z[12],KEY=X$(1,24))X$; GOTO 7520
7590 RETURN 
7600 REM "Generalized Date conversion
7610 IF FNS$(Q$)="" THEN X$=""; GOTO 7640
7620 CALL "CA2CVT",X3$,X4$,P9$(1,2),Q$,0,X$,0,"",0
7640 RETURN 
7650 REM "Generalized Numeric conversion
7660 IF FNS$(Q$)="" THEN X$="",X=0; GOTO 7690
7670 CALL "CA2CVT",X3$,X4$,P9$(5,2),Q$,0,X$,X,"",0
7690 RETURN 
7750 REM "Convert Phone Number
7760 IF FNS$(Q$)="" THEN X$=""; GOTO 7790
7770 CALL "CA2CVT",X3$,X4$,P9$(3,2),Q$,0,X$,0,"",0
7790 RETURN 
7800 REM "Convert Zip Code
7810 CALL "CA2CVT",X3$,X4$,P9$(7,2),Q$,0,X$,0,"",0
7840 RETURN 
8200 REM "Decode Addresses
8210 IF R$(117,4)="ATTN" OR POS(" "<>R$(117,25))=0 THEN GOTO 8250; REM "Addr line 2 contains city, state, and zipCA"
8215 J1$=R$(117,25); GOSUB 8300; IF J1=0 THEN GOTO 8250; REM "Zip is in line 3.
8219 REM "Else city, state,zip in addr line 3
8220 A5$=R$(87,30)
8225 A6$=R$(117,25),A6$=A6$(1,POS(","=A6$+",")-1)
8230 IF POS(","=R$(117,25))=0 THEN A7$="",A8$=""; GOTO 8245
8235 J$=R$(117,25),J$=J$(POS(","=J$)+1)
8236 IF LEN(J$)<2 THEN A7$="",A8$=""; GOTO 8245 ELSE IF J$(1,1)=" " THEN J$=J$(2); GOTO 8236
8237 GOSUB 8350
8240 IF POS(" "<>J$)<>0 THEN A8$=J$(POS(" "<>J$))
8245 RETURN 
8246 PRINT "A5$=",A5$,"A6$=",A6$,'LF',"A7$=",A7$,'LF',"A8$=",A8$,; INPUT *; RETURN 
8250 REM "Address line 2 has city & state, and zip is in 2 or 3
8255 A5$="",A6$="",A7$="",A8$=""
8256 A6$=R$(87,30),A6$=A6$(1,POS(","=A6$+",")-1)
8260 IF POS(","=R$(87,30))=0 THEN GOTO 8290
8263 J$=R$(87,30),J$=J$(POS(","=J$)+1)
8265 IF LEN(J$)<2 THEN A7$="",A8$=""; GOTO 8290 ELSE IF J$(1,1)=" " THEN J$=J$(2); GOTO 8265
8267 GOSUB 8350
8270 IF R$(117,1)>="0" AND R$(117,1)<="9" THEN A8$=R$(117,25) ELSE A5$=R$(117,25),A8$=J$(POS(" "<>J$))
8295 RETURN 
8300 REM "Determine if J1$ is a zip or not. if all nine values are numbers, a "-" or blanks then we willhave  to call it a zip
8303 J1=0
8305 FOR I=1 TO LEN(J1$)
8310 IF POS(J1$(1,1)="0123456789- ")=0 THEN J1=1; EXITTO 8345
8320 NEXT I
8345 RETURN 
8350 REM 'GET STATE CODE OUT OF J$ AND INTO A7$
8355 A7$=""
8360 IF LEN(J$)=0 THEN GOTO 8390
8365 IF J$(1,1)=" " THEN GOTO 8390 ELSE IF J$(1,1)<"A" OR J$(1,1)>"Z" THEN J$=J$(2); GOTO 8360 ELSE A7$=A7$+J$(1,1),J$=J$(2)
8370 IF LEN(A7$)<2 THEN GOTO 8360
8374 REM 'PULL OFF CHARS AFTER DONE WITH STATE UNTIL WE FIND A BLANK OR A NUMBER
8375 IF LEN(J$)=0 THEN GOTO 8390 ELSE IF J$(1,1)<>" " AND (J$(1,1)<"0" OR J$(1,1)>"9") THEN J$=J$(2); GOTO 8375
8395 RETURN 
8400 REM 
8401 ESCAPE 
8410 J$=R$(117,25)
8420 GOSUB 8300
8425 ESCAPE 
8920 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
8930 DEF FNQ$(Z9$,Z9)=STR(NUM(Z9$):Z1$(1,Z9))
8940 DEF FNE$(Z9)=CHR(ASC("A")+INT(Z9/10))+STR(MOD(Z9,10))
8950 DEF FNS(Z9$)=POS(" "<>Z9$)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
