0010 REM "Convert Order History File <CA2FS6>
0020 SETESC 9300; SETERR 9000
0035 REM "5.2 - 08/20/03 - 11.405833 - dmm - SSP# 163697
0040 REM "Copyright 2003 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0080 BEGIN 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="CA2FS6",X1$="Convert to TopForm Order History File"
0120 DIM Z0$(80,"-"),Z1$(20,"0"),S$(20)
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15]
0340 IOLIST D$,D[0],D[1],D[2],D[3],D[4],D[5],D[6],D[7],D[8],D[9],D[10],D[11],D[12],D[13],D[14],D[15],D[16],D[17],D[18],D[19],D[20],D[21],D[22],D[23],D[24],D[25],D[26],D[27],D[28],D[29],D[30],D[31],D[32],D[33],D[34],D[35],D[36],D[37],D[38],D[39],D[40]
0360 IOLIST F$,F[0],F[1],F[2],F[3],F[4],F[5],F[6],F[7],F[8],F[9],F[10],F[11],F[12],F[13],F[14],F[15],F[16],F[17],F[18],F[19],F[20],F[21],F[22],F[23],F[24],F[25],F[26],F[27],F[28],F[29],F[30],F[31]
0370 IOLIST G$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O FS6...  03O AR1...  04O IC0...  06O FM1...  08O FSY...  09O FS7...  10O FS8...  11O FS9...  12O MAPDAT  13O ZZPARM  14O CA0... "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0522 CALL "CA2MCA",X3$,X4$,10,CA0_SELECTED$,CA0_OPTIONS$
0524 IF CA0_SELECTED$(1,4)=S0$(1,4) THEN GOTO 9900
0530 READ (Z[13],KEY=X3$(9,3)+"A/R")P0$
0535 READ (Z[14],KEY="FS6 ")CA0$
0600 REM "
0605 READER$="CA2R"+STP(CA0$(25,2),1)
0610 GOSUB 6000
0615 CALL READER$,X3$,X4$,CA0$,CA1${ALL},"O","",MEM${ALL},R$,RET_CODE
0640 CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0699 CALL "ZZPROM",".Y",X3$,Z,"Clear existing data?","","",0; ON Z GOTO 0700,0701
0700 CALL "ZZINIT",STR(Z[2]:"00")+STR(Z[8]:"00")+STR(Z[9]:"00")+STR(Z[10]:"00")+STR(Z[11]:"00")
0710 X$=""
0715 IF X$="" THEN GOTO 0900
0720 FOR I=1 TO LEN(X$) STEP 4
0730 CALL "ZZPROM",".Y",X3$,Z,"Clear Mapping for "+X$(I,3)+"?","","",0; ON Z GOTO 0731,0790
0740 READ (Z[12],KEY=X$(I,4),DOM=0741)
0750 READ (Z[12],END=0790)R2$
0760 IF X$(I,4)<>R2$(1,4) THEN GOTO 0790
0770 REMOVE (Z[12],KEY=R2$(1,24))
0780 GOTO 0750
0790 NEXT I
0900 REM 
0910 DIM C[15],D[40],F[31]
0920 H0$=$0A$
0940 READ (Z[12],KEY="*PARAMETERS")P9$
0950 ADDR "CA2CVT"
1000 REM "Read Data file
1010 DIM R$(1000); CALL READER$,X3$,X4$,CA0$,CA1${ALL},"R","",MEM${ALL},R$,RET_CODE; IF RET_CODE=2 THEN GOTO 5000
1180 GOSUB 2000
1900 GOTO 1000
2000 REM "Initialize TopForm Record
2100 DIM A$(113),A[7],S$(50),F9$(0),G$(80)
2110 F8$="B"
2150 REM "Customer Code
2160 Q$=FNS$(R$(1,18)); DIM X$(100); X$(1)="AR1 "+Q$
2210 FIND (Z[12],DOM=2211,KEY=X$(1,24))X$; A$(1,10)=X$(25); GOTO 2220
2215 A$(1)="**Mapping entry missing** "+R$; GOTO 4990
2220 FIND (Z[3],DOM=4990,KEY=A$(1,10))IOL=0330
2225 REM "Form Number
2230 IF FNS$(R$(19,10))="" THEN A$(12,10)="*NONE*"; GOSUB 7700; GOTO 2240
2235 Q$=FNS$(R$(19,10)),A$(12,10)=Q$
2239 TYPE$=""
2240 READ (Z[4],DOM=2241,KEY=A$(1,10)+A$(12,10))IOL=0340; TYPE$="ITEM"; GOTO 2245
2241 F8$="H"
2245 IF F9$<>"S" THEN READ (Z[6],KEY=A$(1,10)+A$(12,10),DOM=2246)IOL=0360; TYPE$="FORM"; F8$="B"
2250 REM "Type
2260 A$(11,1)=F8$
2280 REM "Order Number
2290 Q$=STR(NUM(R$(29,8)):"000000")
2300 REM IF FNS$(Q$)="" OR Q$="000000" THEN LET Q$=R$(1,6)
2310 A$(22,8)=A$(1,2)+Q$
2320 REM "Seq #
2330 A$(30,1)="1"
2340 REM "Product Code
2350 Q$=R$(37,3); DIM X$(100); X$(1)="P/C "+Q$,I1=31,I2=3; GOSUB 7500
2352 IF A$(I1,I2)<>"   " THEN GOTO 2360; REM 'ELSE RETRIEVE PRODUCT CODE FROM FORM MASTER OR ITEM MASTER DEPENDING ON TYPE SETTING IN 2100-2200
2354 IF TYPE$="" THEN GOTO 2360
2356 IF TYPE$="ITEM" THEN A$(I1,I2)=D$(61,3) ELSE IF TYPE$="FORM" THEN A$(I1,I2)=F$(21,3)
2360 REM "Order Date
2370 Q$=R$(40,10)
2380 GOSUB 7600; A$(34,6)=X$
2400 REM "Invoice Number
2405 Q=NUM(R$(142,8),ERR=2420)
2410 A$(40,7)=FNQ$(R$(142,8),7)
2420 REM "Invoice Date
2430 Q$=R$(120,10)
2440 GOSUB 7600; A$(48,6)=X$
2442 IF X3$(9,3)="516" THEN IF STP(A$(48,6),3," ")="" AND A$(40,7)<>"       " THEN A$(40,6)=A$(34,6)
2450 REM "Customer P/O
2460 A$(54,15)=R$(95,15)
2470 REM "Job Number
2480 A$(69,12)=R$(130,12)
2500 REM "Est Reorder Date
2510 Q$=""
2520 GOSUB 7600; A$(81,6)=X$
2530 REM "Vendor Code
2540 Q$=R$(223,10)
2550 DIM X$(100); X$(1)="AP4 "+Q$
2560 FIND (Z[12],KEY=X$(1,24),DOM=2561)X$; A$(87,10)=X$(25,10)
2570 REM "Plant Code
2580 A$(97,4)=R$(150,4)
2590 REM "Selling Unit
2600 Q$=FNS$(R$(233,4)); IF FNS$(Q$)="" THEN Q$="M   "
2610 DIM X$(100); X$(1)="U/M "+Q$; I1=101,I2=4; GOSUB 7500
2630 REM "Not used
2640 A$(105,1)=""
2650 REM "Selling Quantity
2660 Q$=R$(237,15)
2670 GOSUB 7650; A[0]=INT(X)
2680 REM "Unit Selling Price
2690 Q$=R$(199,15)
2700 GOSUB 7650; A[1]=X
2710 REM "Combination Quantity
2720 Q$=R$(50,15)
2730 GOSUB 7650; A[3]=INT(X)
2740 REM "Unit Cost
2750 Q$=R$(80,15)
2760 GOSUB 7650; A[4]=X
2770 REM "Quantity Invoiced to Date  (including overruns)
2780 Q$=R$(169,15)
2790 GOSUB 7650; A[5]=INT(X)
2800 REM "Price Extension Invoiced to Date
2810 Q$=R$(154,15)
2820 GOSUB 7650; A[6]=X
2830 REM "Cost Extension Invoiced to Date
2840 Q$=R$(65,15)
2850 GOSUB 7650; A[7]=X
3000 REM "Estimated Reorder Date
3010 Q$=R$(110,10)
3020 GOSUB 7600; A$(81,6)=X$
3030 REM "Last invoice number
3040 A$(40,8)=R$(142,8)
3050 REM "Qty per unit of measure
3052 IF STP(R$(184,15),3," ")="" THEN GOTO 3053 ELSE GOTO 3060
3053 IF A$(101,4)="M   " THEN Q$="1000" ELSE Q$="1"; GOTO 3061
3060 Q$=R$(184,15)
3061 REM 'IF THE QUANTITY IS NOT PROVIDED AND THE UNIT MATCHES THE FORM THEN USE THE QUANTITY FROM THE FORM
3062 READ (Z[6],KEY=A$(1,10)+A$(12,10),DOM=3070)IOL=0360
3064 IF F$(100,4)<>A$(101,4) THEN GOTO 3070
3066 A[2]=F[4]
3070 GOSUB 7650; IF X<>0 THEN A[2]=X
4900 REM "Write file(s)
4910 WRITE (Z[2],KEY=A$(1,30),DOM=4911)IOL=0310; GOTO 4920
4915 IF A$(30,1)<"9" THEN A$(30,1)=STR(NUM(A$(30,1))+1:"0"); GOTO 4910 ELSE IF A$(30,1)<"z" THEN A$(30,1)=CHR(ASC(A$(30,1))+1) ELSE ESCAPE 
4980 C2=C2+1
4990 RETURN 
5000 REM "EOJ
5010 GOSUB 5200
5020 CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
5200 REM "COMPLETE CYCLE
5210 CALL "FM2UT4",X3$,"","","*"
5290 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This program converts Order History to TopForm's FS6."
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7500 REM "Generalized Mapping
7520 FIND (Z[12],KEY=X$(1,24),DOM=7521)X$,X; A$(I1,I2)=X$(25,I2); GOTO 7590
7560 X$(25)=Q$; WRITE (Z[12],KEY=X$(1,24))X$; GOTO 7520
7590 RETURN 
7600 REM "Generalized Date conversion
7610 IF FNS$(Q$)="" THEN X$=""; GOTO 7640
7615 IF X3$(9,3)="121" THEN Q$=Q$(1,2)+Q$(5,2)+Q$(3,2)
7620 CALL "CA2CVT",X3$,X4$,P9$(1,2),Q$,0,X$,0,"",0
7640 RETURN 
7650 REM "Generalized Numeric conversion
7655 Q$=STP(Q$,2)
7660 IF Q$="" THEN X$="",X=0; GOTO 7690
7670 CALL "CA2CVT",X3$,X4$,P9$(5,2),Q$,0,X$,X,"",0
7690 RETURN 
7700 REM "Nonstock
7720 G$(1,8)=A$(22,8); REM "Order No.
7730 G$(9,10)=A$(12,10); REM "Form Code
7735 G$(19,1)=A$(30,1); REM "Sequence number
7740 G$(20,3)=A$(31,3); REM "Product Code
7750 G$(23,8)=""; REM "Left-Right
7760 G$(31,8)=""; REM "Top-Bottom
7770 G$(39,2)=""; REM "Plys
7780 G$(41,40)=R$(258,40); REM "Description
7790 C3=C3+1; WRITE (Z[11],KEY=G$(1,19))IOL=0370
7795 RETURN 
8000 REM "U/M Conversion
8020 IF Q$="1" THEN Q$="EACH",X=1; GOTO 8090
8030 IF Q$="100" THEN Q$="C",X=100; GOTO 8090
8040 IF Q$="1000" OR Q$="0" THEN Q$="M",X=1000; GOTO 8090
8050 GOSUB 7650
8060 IF X<100 THEN Q$="PKG" ELSE Q$="CASE"
8090 RETURN 
8200 REM "Setup for 2nd and 3rd rec (based on R0=2 or 3)
8210 IF R0=2 THEN R1=298,R2=308 ELSE R1=323,R2=333
8215 REM "Change order date
8220 Q$=R$(R1,10)
8225 GOSUB 7600; A$(34,6)=X$
8230 REM "Change quantity
8235 Q$=R$(R2,15)
8240 GOSUB 7650; A[0]=INT(X)
8245 RETURN 
8920 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
8930 DEF FNQ$(Z9$,Z9)=STR(NUM(Z9$):Z1$(1,Z9))
8940 DEF FNE$(Z9)=CHR(ASC("A")+INT(Z9/10))+STR(MOD(Z9,10))
8950 DEF FNS(Z9$)=POS(" "<>Z9$)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9915 SETERR 9921
9920 DROP "CA2CVT"
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
