0010 REM "Convert to TopForm Order Shipping <CA2OF3>
0020 SETESC 9300; SETERR 9000
0035 REM "5.4 - 02/21/06 - 15.386666 - dmm - SSP# 190633
0037 REM "190633-Two WebEC tags, bill_m and shipper_id, data to pass into    
0040 REM "Copyright 2006 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "LEAVE I/C lookup errors in I/C! map
0080 BEGIN 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="CA2OF3",X1$="Convert to TopForm Order Shipping"
0120 DIM Z0$(80,"-"),Z1$(20,"0"),S0$(464),S9$(40),BLANKS$(100)
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O OF3...  03O AR1...  11O CA0... 12O MAPDAT  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0522 GOSUB 6000; CALL "CA2MCA",X3$,X4$,10,CA0_SELECTED$,""
0524 IF STP(CA0_SELECTED$,3," ")="" OR CA0_SELECTED$="" THEN GOTO 9900
0535 READ (Z[11],KEY=CA0_SELECTED$(1,4))CA0$
0600 REM "
0605 READER$="CA2R"+STP(CA0$(25,2),1)
0610 GOSUB 6000
0615 CALL READER$,X3$,X4$,CA0$,CA1${ALL},"O","",MEM${ALL},R$,RET_CODE
0640 CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0700 IF CA0$(62,1)="I" THEN CALL "ZZINIT",STR(Z[2]:"00")
0702 READ (Z[12],KEY="of3I",DOM=0703)
0703 PRINT @(20,15),'CL',"Clearing mapping of initial order shipping pass (of3I)",
0704 VV$=KEY(Z[12],END=0705); IF VV$(1,4)<>"of3I" THEN GOTO 0705 ELSE REMOVE (Z[12],KEY=VV$); GOTO 0704
0705 PRINT @(20,15),'CL',
0710 X$="OSER"
0720 FOR I=1 TO LEN(X$) STEP 4
0725 J$="Clear Mapping for "+FNS$(X$(I,4))+"?"
0730 CALL "ZZPROM",".Y",X3$,Z,J$,"","",0; ON Z GOTO 0731,0790
0735 C9=0,C9$="\|/-",C8=LEN(C9$); PRINT @(20,15),"Clearing ",FNS$(X$(I,4)),":",
0740 READ (Z[12],KEY=X$(I,4),DOM=0741)
0750 K$=KEY(Z[12],END=0780); C9=C9+1; PRINT @(35,15),C9$(MOD(C9,C8)+1,1),; IF K$(1,4)=X$(I,4) THEN REMOVE (Z[12],KEY=K$); GOTO 0750
0780 PRINT @(0,15),'CL',
0790 NEXT I
0920 H0$=$0A$
0940 READ (Z[12],KEY="*PARAMETERS")P9$
0950 ADDR "CA2CVT"; ADDR "YUCASE"
1000 REM "Read Data file
1010 DIM R$(1000); CALL READER$,X3$,X4$,CA0$,CA1${ALL},"R","",MEM${ALL},R$,RET_CODE; IF RET_CODE=2 THEN GOTO 5000
1180 C1=C1+1; GOSUB 2000
1900 GOTO 1000
2000 REM "Initialize TopForm Record
2100 DIM A$(500),A[5]
2101 IF POS(" "<>R$(9,15))=0 THEN GOTO 2110
2102 IF R$(9,15)=" " THEN R$(9,15)=R$(10,14); GOTO *SAME
2119 REM "Apply opposite checks as employed in CA2AR1
2225 ! FOG Number
2230 IF STP(R$(1,8))="" THEN GOTO 4990 ELSE FOG$=STP(R$(1,8)); IF LEN(FOG$)>6 THEN FOG$=MID(FOG$,-6) ! if too long get last 6 digits
2232 A$(1,8)="00"+FOG$ ! Use div 00 because CA2OF3 doesn't get customer code
2240 ! P/O Code
2245 A$(9,1)=R$(9,1)
2260 ! FOG line Number
2265 A$(10,3)=R$(10,3)
2280 ! FOG ship sequence
2285 A$(13,1)=R$(13,1)
2300 ! Location type
2305 A$(14,1)=R$(14,1)
2320 ! Ship to code
2325 DIM X$(100); X$(1)="FM0C"+R$(15,4); X$(25)=R$(15,4); FIND (Z[12],KEY=X$(1,24),DOM=*NEXT)X$
2328 W$=X$(25,4); IF LEN(STP(W$,3))<4 THEN W=NUM(W$,ERR=*NEXT); W$=STR(W:"0000")
2330 A$(15,4)=W$
2340 ! Location code
2345 A$(19,9)=R$(19,9)
2360 ! Receiving department
2365 A$(28,20)=R$(28,20)
2380 ! Attention
2385 A$(48,30)=R$(48,30)
2400 ! Address line 1
2405 A$(78,30)=R$(78,30)
2420 ! Address line 2
2425 A$(108,30)=R$(108,30)
2440 ! Combined city,state,zip found
2445 IF STP(R$(347,40))<>"" THEN GOSUB 7900
2460 ! City
2465 A$(138,16)=R$(138,16)
2480 ! State
2485 A$(154,2)=R$(154,2)
2500 ! Zip
2505 Q$=R$(156,10); GOSUB 7800; A$(156,10)=SUB(X$,"-","") ! Remove dash if found
2520 ! FOB code
2525 A$(166,1)=R$(166,1)
2540 ! How to ship code
2545 A$(167,1)=R$(167,1)
2560 ! ship via code
2565 A$(168,1)=R$(168,1)
2580 ! Inside delivery
2585 A$(169,15)=R$(169,15)
2600 ! Ship with
2605 A$(184,15)=R$(184,15)
2620 ! Mark For
2625 A$(199,35)=R$(199,35)
2640 ! Company name
2645 A$(234,35)=R$(234,35)
2660 ! Ship to Warehouse
2665 DIM X$(100); X$(1)="FM0D"+R$(269,4); X$(25)=R$(269,4); FIND (Z[12],KEY=X$(1,24),DOM=*NEXT)X$
2668 W$=X$(25,4); IF LEN(STP(W$,3))<4 THEN W=NUM(W$,ERR=*NEXT); W$=STR(W:"0000")
2670 A$(269,4)=W$
2680 ! Shipping Unit
2685 A$(273,4)=R$(273,4)
2700 ! Email Address
2705 A$(278,40)=R$(277,40)
2720 ! Ship Qty per unit
2725 Q$=R$(317,15); GOSUB 7650; A[0]=X
2740 ! Quantity to ship
2745 Q$=R$(332,15); GOSUB 7650; A[1]=X
4900 REM "Write file(s)
4910 WRITE (Z[2],KEY=A$(1,13))IOL=0310
4920 C2=C2+1
4990 RETURN 
5000 REM "EOJ
5010 GOSUB 5200
5020 CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
5200 REM "COMPLETE CYCLE
5290 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This program converts the Order file to TopForm's OF3 Order Shipping."
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7500 REM "Generalized Mapping
7520 FIND (Z[12],KEY=X$(1,24),DOM=7521)X$,X; A$(I1,I2)=X$(25,I2); GOTO 7590
7560 X$(25)=Q$; WRITE (Z[12],KEY=X$(1,24))X$; GOTO 7520
7590 RETURN 
7600 REM "Generalized Date conversion
7610 IF FNS$(Q$)="" THEN X$=""; GOTO 7640
7620 CALL "CA2CVT",X3$,X4$,P9$(1,2),Q$,0,X$,0,"",0
7640 RETURN 
7650 REM "Generalized Numeric conversion
7660 IF FNS$(Q$)="" THEN X$="",X=0; GOTO 7690
7670 CALL "CA2CVT",X3$,X4$,P9$(5,2),Q$,0,X$,X,"",0
7690 RETURN 
7800 REM "Convert Zip Code
7805 IF STP(Q$)="" THEN X$=""; GOTO 7840
7810 CALL "CA2CVT",X3$,X4$,P9$(7,2),Q$,0,X$,0,"",0
7840 RETURN 
7900 REM "Decode combined city, state zip field and place into approriate places in R$ IF they are blank. Assumes 'CITY, STATE, ZIP' format where state is 2 chars and comma seperates city and state. If no comma then looks for 1st blank
7905 TMP$=STP(R$(347,40),2); CITY$="",STATE$="",ZIP$="",CPOS=POS(","=TMP$); IF CPOS=0 THEN CPOS=POS(" "=TMP$); IF CPOS=0 THEN CITY$=TMP$; GOTO 7920
7910 CITY$=TMP$(1,CPOS-1); TMP$=STP(TMP$(CPOS+1),2),STATE$=TMP$(1,2),ZIP$=STP(TMP$(3),2)
7920 IF CITY$<>"" AND STP(R$(138,16),3," ")="" THEN R$(138,16)=CITY$
7925 IF STATE$<>"" AND STP(R$(154,2),3," ")="" THEN R$(154,2)=STATE$
7930 IF ZIP$<>"" AND STP(R$(156,10),3," ")="" THEN R$(156,10)=ZIP$
7945 RETURN 
8920 DEF FNS$(Z9$)=Z9$(1,POS(S9$=Z9$+S9$)-1)
8930 DEF FNQ$(Z9$,Z9)=STR(NUM(Z9$):Z1$(1,Z9))
8940 DEF FNE$(Z9)=CHR(ASC("A")+INT(Z9/10))+STR(MOD(Z9,10))
8950 DEF FNS(Z9$)=POS(" "<>Z9$)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 DROP "CA2CVT",ERR=9921; DROP "YUCASE",ERR=9921
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 ! Program changes starting 02/15/06
56010 REM "190633-Two WebEC tags, bill_m and shipper_id, data to pass into
