0010 REM "Convert to TopForm Order Entry Lines <CA2OF2>
0020 SETESC 9300; SETERR 9000
0035 REM "5.2 - 08/20/03 - 11.790555 - dmm - SSP# 163697
0040 REM "Copyright 2003 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "LEAVE I/C lookup errors in I/C! map
0080 BEGIN 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="CA2OF2",X1$="Convert to TopForm Order Entry Lines"
0120 DIM Z0$(80,"-"),Z1$(20,"0"),S0$(464),S9$(40),BLANKS$(100)
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15]
0340 IOLIST FM1$,FM1[0],FM1[1],FM1[2],FM1[3],FM1[4],FM1[5],FM1[6],FM1[7],FM1[8],FM1[9],FM1[10],FM1[11],FM1[12],FM1[13],FM1[14],FM1[15]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O OF2...  03O AR1...  04O FM1... 11O CA0... 12O MAPDAT  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0522 GOSUB 6000; CALL "CA2MCA",X3$,X4$,10,CA0_SELECTED$,""
0524 IF STP(CA0_SELECTED$,3," ")="" OR CA0_SELECTED$="" THEN GOTO 9900
0535 READ (Z[11],KEY=CA0_SELECTED$(1,4))CA0$
0600 REM "
0605 READER$="CA2R"+STP(CA0$(25,2),1)
0610 GOSUB 6000
0615 CALL READER$,X3$,X4$,CA0$,CA1${ALL},"O","",MEM${ALL},R$,RET_CODE
0640 CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0700 IF CA0$(62,1)="I" THEN CALL "ZZINIT",STR(Z[2]:"00")
0702 READ (Z[12],KEY="of2I",DOM=0703)
0703 PRINT @(20,15),'CL',"Clearing mapping of initial order pass (OF2I)",
0704 VV$=KEY(Z[12],END=0705); IF VV$(1,4)<>"of2I" THEN GOTO 0705 ELSE REMOVE (Z[12],KEY=VV$); GOTO 0704
0705 PRINT @(20,15),'CL',
0710 X$="OFER"
0720 FOR I=1 TO LEN(X$) STEP 4
0725 J$="Clear Mapping for "+FNS$(X$(I,4))+"?"
0730 CALL "ZZPROM",".Y",X3$,Z,J$,"","",0; ON Z GOTO 0731,0790
0735 C9=0,C9$="\|/-",C8=LEN(C9$); PRINT @(20,15),"Clearing ",FNS$(X$(I,4)),":",
0740 READ (Z[12],KEY=X$(I,4),DOM=0741)
0750 K$=KEY(Z[12],END=0780); C9=C9+1; PRINT @(35,15),C9$(MOD(C9,C8)+1,1),; IF K$(1,4)=X$(I,4) THEN REMOVE (Z[12],KEY=K$); GOTO 0750
0780 PRINT @(0,15),'CL',
0790 NEXT I
0920 H0$=$0A$
0940 READ (Z[12],KEY="*PARAMETERS")P9$
0950 ADDR "CA2CVT"; ADDR "YUCASE"
1000 REM "Read Data file
1010 DIM R$(1000); CALL READER$,X3$,X4$,CA0$,CA1${ALL},"R","",MEM${ALL},R$,RET_CODE; IF RET_CODE=2 THEN GOTO 5000
1180 C1=C1+1; GOSUB 2000
1900 GOTO 1000
2000 REM "Initialize TopForm Record
2100 DIM A$(198),A[7],C[15],FM1[31]
2101 IF POS(" "<>R$(15,15))=0 THEN GOTO 2110
2102 R$(15,15)=STP(R$(15,15),0) ! Strip leading blanks
2119 REM "Apply opposite checks as employed in CA2AR1
2200 REM "Customer Code
2205 Q$=R$(15,15); DIM X$(100); X$(1)="AR1 "+Q$,Q8=0
2210 FIND (Z[12],DOM=2211,KEY=X$(1,24))X$; A$(15,10)=X$(25); GOTO 2220
2215 DIM X$(100); X$(1)="OFER"+R$; WRITE (Z[12],KEY=X$(1,24))X$; GOTO 4990
2220 FIND (Z[3],KEY=A$(15,10),DOM=2215)IOL=0330; GOTO 2225
2225 ! FOG Number
2230 IF STP(R$(1,8))="" THEN GOTO 4990 ELSE FOG$=STP(R$(1,8)); IF LEN(FOG$)>6 THEN FOG$=MID(FOG$,-6) ! if too long get last 6 digits
2232 A$(1,8)="00"+FOG$ ! Use div 00 because CA2OF3 doesn't get customer code
2240 ! FOG Line number
2245 A$(9,3)=R$(9,3); TMP=NUM(A$(9,3),ERR=*NEXT); A$(9,3)=STR(TMP:"000")
2260 ! Line type
2265 A$(12,1)=R$(12,1)
2280 ! Custom Form Line type
2285 A$(13,1)=R$(13,1)
2300 ! P/O code
2305 A$(14,1)=R$(14,1)
2320 ! Item code
2322 IF STP(R$(30,10))="" THEN A$(25,10)="*NOF*",A$(12,1)="S"; GOTO END_ITEM ! if form code is blank convert to *NOF* special charge
2325 DIM X$(100); X$(1)="FM1I"+A$(1,10)+R$(30,10),X$(25)=R$(30,10)
2326 FIND (Z[12],KEY=X$(1,24),DOM=*NEXT)X$
2335 DIM FM1[31]; FIND (Z[4],KEY=A$(15,10)+X$(25,10),DOM=*NEXT)IOL=0340; GOTO ITEM_OK
2336 A$(12,1)="S",A$(25,10)="*NOF*"; GOTO END_ITEM ! If not on file then set to special charge
2338 ITEM_OK:
2340 A$(25,10)=X$(25),A$(12,1)="C"
2359 END_ITEM:
2360 ! Description
2365 A$(45,40)=R$(50,40)
2380 ! Vendor Code
2383 DIM X$(100); X$(1)="AP4 "+R$(90,10),X$(25)="*NOF*"
2384 FIND (Z[12],KEY=X$(1,24),DOM=*NEXT)X$
2385 A$(85,10)=X$(25)
2400 ! Plant code
2405 A$(95,4)=R$(100,4)
2420 ! Starting number
2425 A$(99,9)=R$(104,9)
2440 ! Ending number
2445 A$(108,9)=R$(113,9)
2460 ! Sell Unit of Measure
2465 Q$=R$(122,4); IF STP(Q$)="" THEN GOTO END_UM ELSE IF Q$="L   " THEN Q$="LOT "
2467 DIM X$(100); X$(1)="U/M "+Q$,I1=117,I2=4; GOSUB 7500
2469 IF X>0 THEN A[0]=INT(X)
2479 END_UM:
2480 ! Vendor Job Number
2485 A$(121,12)=R$(126,12)
2500 ! Vendor Job Date
2505 Q$=R$(138,10); GOSUB 7600; A$(133,6)=X$
2520 ! Ship From Warehouse code
2525 DIM X$(100); X$(1)="FM0D"+R$(148,4); X$(25)=R$(148,4); FIND (Z[12],KEY=X$(1,24),DOM=*NEXT)X$
2528 W$=X$(25,4); IF LEN(STP(W$,3))<4 THEN W=NUM(W$,ERR=*NEXT); W$=STR(W:"0000")
2530 A$(139,4)=W$
2540 ! Special Shipping?
2545 A$(143,1)=R$(152,1)
2560 ! print flags
2565 A$(193,5)=R$(153,5)
2580 ! Sell Quantity per u/m
2581 IF STP(R$(158,15))="" THEN GOTO END_QTY_PER
2583 Q$=R$(158,15); GOSUB 7650; IF X>0 THEN A[0]=INT(X)
2588 END_QTY_PER:
2590 IF A[0]=0 THEN A[0]=FM1[4]
2591 IF A[0]=0 THEN A[0]=1 ! If still 0 after default from fm1, then make it 1
2600 ! price per unit
2605 Q$=R$(173,15); GOSUB 7650; A[1]=X
2620 ! Cost per unit
2625 Q$=R$(188,15); GOSUB 7650; A[2]=X
2640 ! Quantity Ordered
2645 Q$=R$(203,15); GOSUB 7650; A[3]=X
2660 ! Carton Pack
2665 Q$=R$(218,15); GOSUB 7650; A[4]=X
4900 REM "Write file(s)
4910 WRITE (Z[2],KEY=A$(1,11))IOL=0310
4920 C2=C2+1
4990 RETURN 
5000 REM "EOJ
5010 GOSUB 5200
5020 CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
5200 REM "COMPLETE CYCLE
5290 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This program converts the Order file to TopForm's OF2 Order Lines."
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7500 REM "Generalized Mapping
7520 FIND (Z[12],KEY=X$(1,24),DOM=7521)X$,X; A$(I1,I2)=X$(25,I2); GOTO 7590
7560 X$(25)=Q$; WRITE (Z[12],KEY=X$(1,24))X$; GOTO 7520
7590 RETURN 
7600 REM "Generalized Date conversion
7610 IF FNS$(Q$)="" THEN X$=""; GOTO 7640
7620 CALL "CA2CVT",X3$,X4$,P9$(1,2),Q$,0,X$,0,"",0
7640 RETURN 
7650 REM "Generalized Numeric conversion
7660 IF FNS$(Q$)="" THEN X$="",X=0; GOTO 7690
7670 CALL "CA2CVT",X3$,X4$,P9$(5,2),Q$,0,X$,X,"",0
7690 RETURN 
8920 DEF FNS$(Z9$)=Z9$(1,POS(S9$=Z9$+S9$)-1)
8930 DEF FNQ$(Z9$,Z9)=STR(NUM(Z9$):Z1$(1,Z9))
8940 DEF FNE$(Z9)=CHR(ASC("A")+INT(Z9/10))+STR(MOD(Z9,10))
8950 DEF FNS(Z9$)=POS(" "<>Z9$)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 DROP "CA2CVT",ERR=9921; DROP "YUCASE",ERR=9921
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
