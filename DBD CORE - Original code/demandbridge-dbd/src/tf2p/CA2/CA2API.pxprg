0010 REM "Convert A/P Open Invoice File <CA2API>
0020 SETESC 9300; SETERR 9000
0035 REM "5.2 - 08/20/03 - 11.263333 - dmm - SSP# 163697
0040 REM "Copyright 2003 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0080 BEGIN 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="CA2API",X1$="Convert to TopForm A/P Open Invoice File"
0120 DIM Z0$(80,"-"),Z1$(20,"0"),S0$(20)
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27]
0330 IOLIST C$
0340 IOLIST D$,D[0],D[1],D[2]
0350 IOLIST E$,E[0],E[1],E[2],E[3]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O API...  04O AP4...  05O APJ...  06O AP2...  07O APIERR  11O CA0... 12O MAPDAT  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0522 GOSUB 6000; CALL "CA2MCA",X3$,X4$,10,CA0_SELECTED$,CA0_OPTIONS$
0524 IF CA0_SELECTED$(1,4)=S0$(1,4) THEN GOTO 9900
0530 READ (Z[13],KEY=X3$(9,3)+"A/P")P0$
0535 READ (Z[11],KEY=CA0_SELECTED$(1,4))CA0$
0600 REM "
0605 READER$="CA2R"+STP(CA0$(25,2),1)
0610 GOSUB 6000
0615 CALL READER$,X3$,X4$,CA0$,CA1${ALL},"O","",MEM${ALL},R$,RET_CODE
0640 CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0700 IF CA0$(62,1)="I" THEN CALL "ZZINIT",STR(Z[2]:"00")+STR(Z[5]:"00")+STR(Z[7]:"00")
0705 GOTO 0800
0710 X$="API AR2 AR3 AR5 ARG "
0720 FOR I=1 TO LEN(X$) STEP 4
0730 CALL "ZZPROM",".Y",X3$,Z,"Clear Mapping for "+X$(I,3)+"?","","",0; ON Z GOTO 0731,0790
0740 READ (Z[12],KEY=X$(I,4),DOM=0741)
0750 READ (Z[12],END=0790)R2$
0760 IF X$(I,4)<>R2$(1,4) THEN GOTO 0790
0770 REMOVE (Z[12],KEY=R2$(1,24))
0780 GOTO 0750
0790 NEXT I
0910 DIM D[2],E[3]
0920 H0$=$0A$,I9=980000
0940 READ (Z[12],KEY="*PARAMETERS")P9$
0950 ADDR "CA2CVT"
0965 GOSUB 8150
1000 REM "Read Data file
1010 DIM R$(1000); CALL READER$,X3$,X4$,CA0$,CA1${ALL},"R","",MEM${ALL},R$,RET_CODE; IF RET_CODE=2 THEN GOTO 5000
1030 C1=C1+1
1180 GOSUB 2000
1900 GOTO 1000
2000 REM "Initialize TopForm Record
2020 DIM A$(160),A[27],S$(50)
2025 IF R$(230,1)="Y" THEN GOTO 4990; REM "Skip already paid A/P invoices
2050 REM "A/P Vendor Code
2060 Q$=FNS$(R$(12,20)); DIM X$(100); X$(1)="AP4 "+Q$
2065 X=NUM(P0$(14,1)),A$(1,10)=Z1$,A$(3,X)=Q$,A$(3,X)=STR(NUM(Q$,ERR=2066):Z1$(1,X))
2067 IF P0$(13,1)="Y" THEN A$(1,2)=R$(1,2),A$(1,2)=STR(NUM(A$(1,2)):"00")
2070 FIND (Z[12],DOM=2071,KEY=X$(1,24))X$; A$(1,10)=X$(25,10); GOTO 2090
2080 C2=C2+1,A$(1)="**Vendor number missing from map**",I8$=STR(NUM(I8$)+1:"0000"); WRITE (Z[7],KEY=Q$+I8$)R$; GOTO 4990
2090 FIND (Z[4],KEY=A$(1,10))IOL=0340
2100 REM "Invoice Number
2110 A$(11,10)=STP(R$(32,10),0)
2140 REM "Invoice Date
2150 Q$=R$(42,10); GOSUB 7600; A$(21,6)=X$
2160 REM "Terms Code
2165 IF CA0$(27,1)="G" THEN GOTO 2175; REM "if galaxy, then no terms code from invoice, just set it to vendor's code
2170 Q$=R$(52,20); DIM X$(100); X$(1)="AP2 "+Q$,I1=27,I2=2; GOSUB 7500
2175 IF STP(A$(27,2),3," ")="" THEN A$(27,2)=D$(286,2); REM "if blank, then set from vendor's terms code
2180 REM "Invoice Due Date
2190 Q$=R$(162,10); GOSUB 7600; A$(29,6)=X$
2200 REM "Discount Expires
2212 Q$=R$(242,10); GOSUB 7600; A$(35,6)=X$
2220 REM "Invoice Comment
2230 A$(41,15)=""
2240 REM "Our Reference No.
2242 REM 'PRINT 'CS',A$(1,10)," REF: ",R$(232,10),; ESCAPE
2250 A$(56,10)=R$(232,10)
2260 REM "Print on Bank
2265 Q$="A"; DIM X$(100); X$(1,24)="AP1 "+Q$,I1=66,I2=3; GOSUB 7500
2290 REM "Vendor Category
2295 IF X3$(9,3)="112" THEN A$(69,9)=D$(247,9); GOTO 2310
2300 Q$="000000000"; GOSUB 7000; DIM X$(100); X$(1,24)="AP3 "+Q$,I1=69,I2=9; GOSUB 7500
2310 REM "Payment Selection Code
2320 A$(78,2)="00"
2330 REM "Payment Priority
2340 A$(80,1)="G"
2350 REM "Invoice on hold?
2360 A$(81,1)="N"
2370 REM "Invoice Special Code
2380 A$(82,1)=""
2390 REM "Inoice Type
2400 A$(83,1)="I"
2410 REM "Applies to Invoice No.
2420 A$(84,10)=""
2430 REM "Fiscal Year
2440 A$(94,4)=P0$(7,4)
2450 REM "Accounting Period
2460 A$(98,2)=P0$(11,2)
2470 REM "Highest Fiscal Year
2480 A$(100,4)=A$(94,4)
2490 REM "Highest Accounting Period
2500 A$(104,2)=A$(98,2)
2510 REM "Audit Control Number
2520 A$(106,6)=""
2600 REM "Salesperson Code
2610 Q$="",I1=114; GOSUB 7700
2620 REM "Total Booked to A/P
2630 Q$=""; GOSUB 7650; A[2]=X
2640 REM "Special Amount #1
2650 Q$=""; GOSUB 7650; A[3]=X
2660 REM "Special Amount #2
2670 Q$=""; GOSUB 7650; A[4]=X
2680 REM "Special Amount #3
2690 Q$=""; GOSUB 7650; A[5]=X
2700 REM "Special Amount #4)
2710 Q$=""; GOSUB 7650; A[6]=X
2720 REM "Original Discount Available
2730 Q$=R$(102,30); GOSUB 7650; A[7]=X
2740 REM "W/C Ins W/H
2750 Q$=""; GOSUB 7650; A[8]=X
2760 REM "Extend Due Date
2770 Q$=""; GOSUB 7650; A[9]=X
2780 REM "Original Invoice Amount / Invoice Amount
2790 Q$=R$(172,30); GOSUB 7650; A[10]=X
2800 REM "Discounts Taken to Date
2810 Q$=""; GOSUB 7650; A[11]=X
2820 REM "Payments applied to date
2830 Q$=""; GOSUB 7650; A[12]=X
2840 REM "Balance Due on this Invoice
2850 Q$=R$(72,30); GOSUB 7650; A[13]=X
2852 REM 'PRINT 'CS',@(0,5),R$,@(0,10),"ORIG: ",A[10]," BLANCE: ",A[13],; INPUT *
2860 REM "Posted to date Special Amt #1
2870 Q$=""; GOSUB 7650; A[14]=X
2880 REM "Posted to date Special Amt #2
2890 Q$=""; GOSUB 7650; A[15]=X
2900 REM "Posted to date Special Amt #3
2910 Q$=""; GOSUB 7650; A[16]=X
2920 REM "Posted to date Special Amt #4
2930 Q$=""; GOSUB 7650; A[17]=X
2940 REM "Unused
2950 Q$=""; GOSUB 7650; A[18]=X
4400 REM "Re-ko-bu-bu-late
4420 A[2]=A[10],A[10]=A[13]
4500 REM "Term code
4550 IF FNS$(A$(29,6))>"" THEN GOTO 4600
4555 FIND (Z[6],KEY=A$(51,2),DOM=4600)IOL=0350
4560 CALL "ZZDISC",X3$,E$,0,0,A$(21,6),Q$,X$,0,0
4570 A$(54,6)=Q$; IF E$(43,1)<>" " THEN A$(60,6)=X$
4600 REM 
4900 REM "Write file(s) - Put alpha suffix on duplicate invoices
4901 REM IF POS("BILL"=A$(1,20))<>0 THEN PRINT A$(1,20); ESCAPE
4902 IF X3$(9,3)="529" AND A[13]=0 THEN GOTO 4990
4905 F5$=CHR(64)
4910 WRITE (Z[2],DOM=4911,KEY=A$(1,20))IOL=0310; C1=C1+1; GOTO 4990
4911 PRINT @(0,20),'CE',@(0,21),A$(1,20); INPUT *
4915 IF F5$=CHR(64) THEN P0=LEN(FNS$(A$(11,10))); IF P0=10 THEN ESCAPE 
4920 F5$=CHR(ASC(F5$)+1),A$(11+P0,1)=F5$; GOTO 4910
4990 RETURN 
5000 REM "EOJ
5010 GOSUB 5200
5020 CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
5200 REM "COMPLETE CYCLE
5210 CALL "AP2UT9",X3$,"","","*"
5220 CALL "AP2UTA",X3$,"","","*"
5230 CALL "AP2UTD",X3$,"","","*"
5290 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This program converts the A/P Open Invoice file to TopForm's API.  Be sure to set the A/P parameter to the last closed Fiscal Year and Accounting Period prior to running this program."
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7000 REM "Convert Q$ to upper case
7010 FOR I=1 TO LEN(Q$)
7015 IF Q$(I)>="a" AND Q$(I)<="z" THEN Q$(I)=CHR(ASC(Q$(I))-32)
7020 NEXT I
7045 RETURN 
7500 REM "Generalized Mapping
7510 X=0
7520 FIND (Z[12],KEY=X$(1,24),DOM=7521)X$,X; A$(I1,I2)=X$(25,I2); GOTO 7590
7530 IF X$(1,4)="AP3 " THEN Q$=Q$(1,2)+"0000000"
7560 X$(25)=Q$; WRITE (Z[12],KEY=X$(1,24))X$; GOTO 7520
7590 RETURN 
7600 REM "Generalized Date conversion
7610 IF FNS$(Q$)="" THEN X$=""; GOTO 7640
7615 IF Q$(1,1)=" " THEN Q$=Q$(2); GOTO 7615
7620 CALL "CA2CVT",X3$,X4$,P9$(1,2),Q$,0,X$,0,"",0
7640 RETURN 
7650 REM "Generalized Numeric conversion
7660 IF FNS$(Q$)="" THEN X$="",X=0; GOTO 7690
7670 CALL "CA2CVT",X3$,X4$,P9$(5,2),Q$,0,X$,X,"",0
7690 RETURN 
7700 REM "Salesperson's Map
7705 IF FNS$(Q$)="" THEN RETURN 
7710 DIM X$(100); X$(1)="AR3 "+Q$
7720 GOTO 7500
7750 REM "Convert Phone Number
7760 IF FNS$(Q$)="" THEN X$=""; GOTO 7790
7770 CALL "CA2CVT",X3$,X4$,P9$(3,2),Q$,0,X$,0,"",0
7790 RETURN 
7901 RETURN 
7902 DIM BLANKS$(100); IF BLANKS$(1,LEN(DATEIN$))=DATEIN$ THEN DATEIN$=""; GOTO 7990
7910 FIRSTS=POS("/"=DATEIN$)
7915 SECONDS=POS("/"=DATEIN$(FIRSTS+1))+FIRSTS
7920 M$=DATEIN$(1,FIRSTS-1),D$=DATEIN$(FIRSTS+1,SECONDS-1-FIRSTS),Y$=DATEIN$(SECONDS+1)
7930 DATEIN$=STR(NUM(M$):"00")+STR(NUM(D$):"00")+STR(NUM(Y$):"00")
7990 RETURN 
8200 REM 'FIX DATE IN PLACE IN R$ BASED ON J
8203 RETURN 
8205 IF POS(" "<>R$(J,10))=0 THEN GOTO 8245
8206 J1$="",J2$="",J$=R$(J,10)
8207 IF J$(1,2)="  " THEN J$=J$(2)+" "
8208 ESCAPE 
8210 IF POS("/"=J$)=0 THEN IF J$(1,3)=" 19" THEN J$=J$(4),J$=J$(3)+J$(1,2); GOTO 8220 ELSE IF J$(1,2)="19" THEN J$=J$(3),J$=J$(3)+J$(1,2); GOTO 8220
8211 J1$=J$(1,POS("/"=J$)-1),J$=J$(POS("/"=J$)+1)
8213 IF LEN(J1$)<2 THEN J1$="0"+J1$
8215 J2$=J$(1,POS("/"=J$)-1),J$=J$(POS("/"=J$)+1)
8217 IF LEN(J2$)<2 THEN J2$="0"+J2$
8220 R$(J,10)=J1$+J2$+J$
8245 RETURN 
8920 DEF FNS$(Z9$)=Z9$(1,POS(S0$=Z9$+S0$)-1)
8930 DEF FNQ$(Z9$,Z9)=STR(NUM(Z9$):Z1$(1,Z9))
8940 DEF FNE$(Z9)=CHR(ASC("A")+INT(Z9/10))+STR(MOD(Z9,10))
8950 DEF FNS(Z9$)=POS(" "<>Z9$)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 DROP "CA2CVT",ERR=9921
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
