0010 REM "Convert A/R Open Invoice File <CA2AR6>
0020 SETESC 9300; SETERR 9000
0035 REM "5.2 - 08/20/03 - 11.272777 - dmm - SSP# 163697
0040 REM "Copyright 2003 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0080 BEGIN 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="CA2AR6",X1$="Convert to TopForm A/R Open Invoice File"
0120 DIM Z0$(80,"-"),Z1$(20,"0"),Z2$(20)
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14]
0330 IOLIST C$
0340 IOLIST D$,D[0],D[1],D[2],D[3],D[4],D[5],D[6],D[7],D[8],D[9],D[10],D[11],D[12],D[13],D[14],D[15]
0350 IOLIST E$,E[0],E[1],E[2],E[3]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O AR6...  04O AR1...  05O AR7...  06O AR2...  07O AR6ERR  11O CA0... 12O MAPDAT  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0522 GOSUB 6000; CALL "CA2MCA",X3$,X4$,10,CA0_SELECTED$,CA0_OPTIONS$
0524 IF CA0_SELECTED$(1,4)=MID(S0$,1,4) THEN GOTO 9900
0530 READ (Z[13],KEY=X3$(9,3)+"A/R")P0$
0535 READ (Z[11],KEY=CA0_SELECTED$(1,4))CA0$
0600 REM "
0605 READER$="CA2R"+STP(CA0$(25,2),1)
0610 GOSUB 6000
0615 CALL READER$,X3$,X4$,CA0$,CA1${ALL},"O","",MEM${ALL},R$,RET_CODE
0640 CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0700 IF CA0$(62,1)="I" THEN CALL "ZZINIT",STR(Z[2]:"00")+STR(Z[5]:"00")+STR(Z[7]:"00")
0705 GOTO 0800
0710 X$="AR6 AR2 AR3 AR5 ARG "
0720 FOR I=1 TO LEN(X$) STEP 4
0730 CALL "ZZPROM",".Y",X3$,Z,"Clear Mapping for "+X$(I,3)+"?","","",0; ON Z GOTO 0731,0790
0740 READ (Z[12],KEY=X$(I,4),DOM=0741)
0750 READ (Z[12],END=0790)R2$
0760 IF X$(I,4)<>R2$(1,4) THEN GOTO 0790
0770 REMOVE (Z[12],KEY=R2$(1,24))
0780 GOTO 0750
0790 NEXT I
0910 DIM D[15],E[3]
0920 H0$=$0A$,I9=980000
0940 READ (Z[12],KEY="*PARAMETERS")P9$
0950 ADDR "CA2CVT"
0955 REM "Fiscal Year/Accounting Period
0960 F9$="1993",F8$="10"
0970 GOSUB 8150
1000 REM "Read Data file
1010 DIM R$(1000); CALL READER$,X3$,X4$,CA0$,CA1${ALL},"R","",MEM${ALL},R$,RET_CODE; IF RET_CODE=2 THEN GOTO 5000
1180 GOSUB 2000
1900 GOTO 1000
2000 REM "Initialize TopForm Record
2020 DIM A$(180),A[14],S$(50)
2025 C1=C1+1
2050 REM "A/R Customer Code
2060 Q$=FNS$(R$(1,17)); DIM X$(100); X$(1)="AR1 "+Q$
2065 X=NUM(P0$(14,1)),A$(1,10)=Z1$,A$(3,X)=Q$(3),A$(3,X)=STR(NUM(Q$(3),ERR=2066):Z1$(1,X))
2070 FIND (Z[12],DOM=2071,KEY=X$(1,24))X$; A$(1,10)=X$(25,10); GOTO 2090
2080 A$(1)="**Customer number missing from map**",I8$=STR(NUM(I8$)+1:"0000"); WRITE (Z[7],KEY=Q$+I8$)R$; GOTO 4990
2090 FIND (Z[4],KEY=A$(1,10),DOM=2091)IOL=0340; GOTO 2095
2091 PRINT @(0,15),"missing: ",R$(1,17),; INPUT *; PRINT @(0,15),'CL',; A$(1)="**Customer in map, not on file**",I8$=STR(NUM(I8$)+1:"0000"); WRITE (Z[7],KEY=Q$+I8$)Q$+"|"+X$(25,10)+"|"+R$; GOTO 4990
2100 REM "Invoice Number
2110 A$(11,8)=STR(NUM(R$(18,8),ERR=2111):"0000000"); GOTO 2120
2115 REM " LET I9=I9+1; LET R$(1,6)=STR(I9:"000000"); GOTO 02110
2120 REM "Invoice Type
2121 IF CA0$(27,1)<>"G" THEN GOTO 2125
2122 FIND (Z[2],KEY=A$(1,18),DOM=2125); REM 'CONVERT RECORD IF EXISTING RECORD DOES NOT EXIST - OTHERWISE TREAT AS A TRANSACTION AGAINST THE EXISTING INVOICE REOCRD
2123 GOSUB 7800; C2=C2+1; GOTO 4990; REM "IF galaxy conv, try to find invoice and apply payment, then leave
2130 A$(19,1)="I"
2140 REM "Invoice Date
2145 J=198; GOSUB 8200
2150 Q$=R$(198,10); GOSUB 7600; A$(20,6)=X$
2220 REM "Alternate Bill-to
2225 Q$=R$(219,17); IF FNS(Q$)=0 THEN GOTO 2240 ELSE DIM X$(100); X$(1)="AR1 "+Q$
2235 FIND (Z[12],DOM=2236,KEY=X$(1,24))X$; A$(26,10)=X$(25)
2240 REM "Customer P/O Number
2250 A$(36,15)=R$(26,15)
2260 REM "Terms Code
2270 Q$=R$(209,10); DIM X$(100); X$(1)="AR2 "+Q$,I1=51,I2=2; GOSUB 7500
2310 REM "Split Terms?
2320 A$(53,1)=""
2330 REM "Invoice Due Date
2335 J=41; GOSUB 8200
2340 Q$=R$(41,10); GOSUB 7600; A$(54,6)=X$
2345 J=158; GOSUB 8200
2350 REM "Discount Due Date
2360 Q$=R$(158,10); GOSUB 7600; A$(60,6)=X$
2370 REM "Order Number
2380 Q$=R$(51,8); IF FNS$(Q$)>"" AND LEN(FNS$(Q$))<=6 THEN Q$=A$(1,2)+Q$
2390 IF FNS$(Q$)>"" THEN A$(66,8)=Q$,A$(68,6)=STR(NUM(A$(68,6),ERR=2391):"000000")
2830 REM "Salesperson Code #1
2840 Q$=R$(59,4); IF FNS$(Q$)="" THEN Q$=D$(278,4)
2850 I1=74,I2=4; GOSUB 7700
2870 REM "Salesperson Code #2
2880 Q$=R$(236,4),I1=78; GOSUB 7700
2910 REM "Salesperson Code #3
2920 Q$=R$(240,4),I1=82; GOSUB 7700
2950 REM "Salesperson Code #4
2960 Q$=R$(244,4),I1=86; GOSUB 7700
2990 REM "Salesperson Code #5
3000 Q$=R$(248,4),I1=90; GOSUB 7700
3030 REM "Apply to Invoice Number
3040 A$(94,8)=""
3060 REM "Audit Control Number
3070 A$(102,6)=""
3080 REM "Customer Category
3100 A$(108,9)=D$(220,9)
3300 REM "Sales Tax Code
3320 Q$=R$(63,10); IF FNS$(Q$)="" THEN A$(117,10)=D$(235,10); GOTO 3360
3330 DIM X$(100); X$(1)="AR5 "+Q$,I1=117,I2=10; GOSUB 7500
3360 REM "Fiscal Year
3370 A$(127,4)=P0$(7,4)
3380 REM "Accounting Period
3390 A$(131,2)=P0$(11,2)
3400 REM "Highest Fiscal Year
3410 A$(133,4)=A$(127,4)
3420 REM "Highest Accounting Period
3430 A$(137,2)=A$(131,2)
3440 REM "Ship Date
3445 J=73; GOSUB 8200
3450 Q$=R$(73,10); GOSUB 7600; A$(139,6)=X$
3480 REM "Ship Via
3490 A$(145,15)=R$(252,15)
3500 REM "Summary Bill Reference No
3510 A$(160,8)=""
3650 REM "Invoice Balance
3655 Q$=R$(183,15); GOSUB 7650; A[3]=X
3670 REM "Discount Amount
3675 Q$=R$(168,15); GOSUB 7650; A[4]=X
3690 REM "Cash in Progress
3700 A[5]=0
3710 REM "Original Invoice Amount / Invoice Amount
3720 Q$=R$(267,15); GOSUB 7650; A[6]=X,A[2]=X
3730 REM "Sales Tax Billed
3740 Q$=R$(83,15); GOSUB 7650; A[7]=X
3750 REM "Freight
3755 Q$=R$(98,15); GOSUB 7650; A[8]=X
3770 REM "Net Sale
3780 Q$=R$(113,15); GOSUB 7650; A[9]=X
3810 REM "Gross Profit
3820 Q$=R$(128,15); GOSUB 7650; A[10]=X
3840 REM "Total Commission
3850 Q$=R$(143,15); GOSUB 7650; A[11]=X
3880 REM "Costed Freight
3890 Q$=""; GOSUB 7650; A[12]=X
3910 A[12]=X
3920 REM "Cash basis freight billed to date?? (paid to date)
3930 Q$=""; GOSUB 7650; A[13]=X
3960 REM "Gross Profit Adjustment b4 comm calc
3970 Q$=""; GOSUB 7650; A[14]=X
4400 REM "DATAFLOW
4410 IF X3$(9,3)<>"121" THEN GOTO 4540
4420 REM 'WAB 1/11/94 *LET A(3)=A(6),A(2)=A(3),F9=0
4421 A[2]=A[3],F9=0
4540 REM "Term code
4550 REM IF FNS$(A$(54,6))>"" THEN GOTO 04600
4555 FIND (Z[6],KEY=A$(51,2),DOM=4600)IOL=0350
4560 CALL "ZZDISC",X3$,E$,0,0,A$(20,6),Q$,X$,0,0
4570 A$(54,6)=Q$; IF E$(43,1)<>" " THEN A$(60,6)=X$
4600 REM "Touch ups
4610 IF A[6]=0 THEN A[6]=A[3]; REM "Orig inv amount, if 0 set to balance
4615 IF A[2]=0 THEN A[2]=A[3]; REM "Invoice amount, if 0 set to balance
4900 REM "Write file(s)
4910 WRITE (Z[2],KEY=A$(1,18))IOL=0310; C2=C2+1
4990 RETURN 
5000 REM "EOJ
5005 IF C1<>C2 THEN CALL "ZZPROM",".4",X3$,Z,"Only "+STR(C2)+" out of "+STR(C1)+" records were loaded!","","",0
5010 GOSUB 5200
5020 CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
5200 REM "COMPLETE CYCLE
5210 CALL "AR2UT4",X3$,"","","*"
5220 CALL "AR2UT6",X3$,"","","*"
5230 CALL "AR2UTD",X3$,"","","*"
5290 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This program converts the A/R Open Invoice file to TopForm's AR6.  Be sure to set the A/R parameter to the last closed Fiscal Year and Accounting Period prior to running this program."
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7500 REM "Generalized Mapping
7510 X=0
7520 FIND (Z[12],KEY=X$(1,24),DOM=7521)X$,X; A$(I1,I2)=X$(25,I2); GOTO 7590
7560 X$(25)=Q$; WRITE (Z[12],KEY=X$(1,24))X$; GOTO 7520
7590 RETURN 
7600 REM "Generalized Date conversion
7610 IF FNS$(Q$)="" THEN X$=""; GOTO 7640
7620 CALL "CA2CVT",X3$,X4$,P9$(1,2),Q$,0,X$,0,"",0
7640 RETURN 
7650 REM "Generalized Numeric conversion
7660 IF FNS$(Q$)="" THEN X$="",X=0; GOTO 7690
7670 CALL "CA2CVT",X3$,X4$,P9$(5,2),Q$,0,X$,X,"",0
7690 RETURN 
7700 REM "Salesperson's Map
7705 IF FNS$(Q$)="" THEN RETURN 
7710 DIM X$(100); X$(1)="AR3 "+Q$
7720 GOTO 7500
7750 REM "Convert Phone Number
7760 IF FNS$(Q$)="" THEN X$=""; GOTO 7790
7770 CALL "CA2CVT",X3$,X4$,P9$(3,2),Q$,0,X$,0,"",0
7790 RETURN 
7800 REM "We have an R$ which we have determined is a payment. A$(1,18) contains the customer code and invoice number, so try to read in the record. if found then apply the payment to the invoice balance. IF not found then skip it
7805 EXTRACT (Z[2],KEY=A$(1,18),DOM=7845)IOL=0310
7820 Q$=R$(183,15); GOSUB 7650; A[3]=A[3]+X
7840 WRITE (Z[2],KEY=A$(1,18))IOL=0310
7845 RETURN 
8000 REM "Update Finance Charge for this invoice
8010 DIM A[14]; A$(11,7)="9000001",A$(19,1)="F",A[3]=F9,A[2]=F9,A[3]=F9,A[6]=F9
8020 WRITE (Z[2],KEY=A$(1,18),DOM=8021)IOL=0310; GOTO 8040
8030 A$(11,7)=STR(NUM(A$(11,7))+1:"0000000"); GOTO 8020
8040 RETURN 
8200 REM 'FIX DATE IN PLACE IN R$ BASED ON J
8201 RETURN 
8205 IF POS(" "<>R$(J,10))=0 THEN GOTO 8245
8210 J1$="",J2$="",J$=R$(J,10); IF POS("/"=J$)=0 THEN IF J$(1,3)=" 19" THEN J$=J$(4),J$=J$(3)+J$(1,2); GOTO 8220 ELSE IF J$(1,2)="19" THEN J$=J$(3),J$=J$(3)+J$(1,2); GOTO 8220
8211 J1$=J$(1,POS("/"=J$)-1),J$=J$(POS("/"=J$)+1)
8213 IF LEN(J1$)<2 THEN J1$="0"+J1$
8215 J2$=J$(1,POS("/"=J$)-1),J$=J$(POS("/"=J$)+1)
8217 IF LEN(J2$)<2 THEN J2$="0"+J2$
8220 R$(J,10)=J1$+J2$+J$
8245 RETURN 
8920 DEF FNS$(Z9$)=Z9$(1,POS(Z2$=Z9$+Z2$)-1)
8930 DEF FNQ$(Z9$,Z9)=STR(NUM(Z9$):Z1$(1,Z9))
8940 DEF FNE$(Z9)=CHR(ASC("A")+INT(Z9/10))+STR(MOD(Z9,10))
8950 DEF FNS(Z9$)=POS(" "<>Z9$)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 DROP "CA2CVT",ERR=9921
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
