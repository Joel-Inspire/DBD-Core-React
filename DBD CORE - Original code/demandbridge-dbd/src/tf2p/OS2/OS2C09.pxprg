0010 REM "Send SPR Stationer's Data to SPR - Interlink <OS2C09>
0035 REM "5.7 - 05/15/18 - 11.819166 - jvv - SSP# 300552
0037 REM "300552-SPR Orders duplicating - happens when I remove a line from  
0040 REM "Copyright 2018 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 SETERR 0055; ENTER Q0$; CALLED$="Y"; GOTO 0090
0055 IF %GUI THEN GOTO 0090
0060 BEGIN 
0090 SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="OS2C09",X1$="SPR Interlink Send"
0120 DIM A$(0),B$(228),B[27],C$(512),C[9],E[3],S$(40),J$(345),J[12]
0140 Z0$="-###,###.00",Z1$="##,##0",Z2$="###,##0-",M4$="-00000000.00",M4=LEN(M4$),M5$="#####",Z3$="-###,###",Z4$="-####.00",Z5$="-#####.00",Z6$="-###.0",M0$="-###"
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNE$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+Z9$(1,2)
0180 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0190 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
0210 T=1,T0=1,T1=1
0220 W3=85,P0=1
0222 CHKWRD$="000000"
0224 DIM SNDREC$(125),ACKREC$(125)
0225 REPRINT_HEADER$=""; REM "Used in 8200's to make sure A$(1,1) only gets incremented once per order
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 REM CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR FS1 - Sales order header
0310 IOLIST A$
0315 REM "IOLIST FOR FS2 - Sales order detail
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19],B[20],B[21],B[22],B[23],B[24],B[25],B[26],B[27]
0335 REM "IOLIST FOR AR1 - Customer Masterfile
0340 IOLIST D$
0345 REM "IOLIST FOR PO3- Shipping Information Masterfile
0350 IOLIST E$,E[0],E[1],E[2],E[3]
0355 REM "IOLIST FOR OFFICE SUPPLY PARAMETERS (OS2MPA - KEY = X3$(9,3) + 'OSP'
0360 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9]
0365 REM "IOLIST FOR OS3 - Error Code Description File
0370 IOLIST R1$
0375 REM "IOLIST FOR OS4 - Transmission Error Report File
0380 IOLIST O$
0385 REM "IOLIST FOR SM7 - CIG Plus Gateway Information (SM0)
0390 IOLIST J$,J[0],J[1],J[2],J[3],J[4],J[5],J[6],J[7],J[8],J[9],J[10],J[11],J[12]
0400 IOLIST IC0$,IC0[0],IC0[1],IC0[2],IC0[3],IC0[4],IC0[5],IC0[6],IC0[7],IC0[8],IC0[9],IC0[10],IC0[11],IC0[12],IC0[13],IC0[14],IC0[15],IC0[16],IC0[17],IC0[18],IC0[19],IC0[20],IC0[21],IC0[22],IC0[23],IC0[24],IC0[25],IC0[26],IC0[27],IC0[28],IC0[29],IC0[30],IC0[31],IC0[32],IC0[33],IC0[34],IC0[35],IC0[36],IC0[37],IC0[38],IC0[39],IC0[40]
0405 REM "IOLIST FOR OSP - TRANSMISSION SEND AND RECV DATA
0410 IOLIST OSP$,SNDREC$,ACKREC$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; REM "GOSUB 7400
0509 REM "Don't use 15,16,17, used on 4105
0510 Y$="01O PO6...  02O FS1...  03O FS2...  05O SM7...  06O PO3...  07O FM0...  09O OS2...  10O OS3...  11O OS4...  25O PC3...  26O PC6...  27O OSP...  28O PO1...  29O PO2...  30O IC0...  31O OSA...  32O EDW...  33OSPO6...  13O ZZPARM  "
0520 GOSUB 9750
0525 READ (Z[13],KEY=X3$(9,3)+"I/C")P8$
0526 READ (Z[13],KEY="CMP"+X3$(9,3),DOM=*NEXT)COMPANY$; REM SSP 189386
0530 DIM G9$(200); READ (Z[7],KEY="D"+S$(1,10)+"0001",DOM=0531)G9$
0535 READ (Z[13],KEY=X3$(9,3)+"F/M")FM_PARM$
0540 READ (Z[13],KEY=X3$(9,3)+"A/R")P9$
0550 READ (Z[13],KEY=X3$(9,3)+"AR1")P5$
0552 READ (Z[13],KEY=X3$(9,3)+"G/B",DOM=0553)GB$; GB$="Y"
1000 REM "GET FIRST 'V' RECORD
1011 REC$=""
1020 READ (Z[1],KEY="V"+X0$,DOM=1021)
1030 REC$=KEY(Z[1],END=5000); READ (Z[1],KEY=REC$)
1040 IF REC$(2,6)<>X0$ THEN GOTO 5000
1050 ORD$=REC$(10,9); REM " PURCHASE ORDER NUMBER
1055 READ RECORD (Z[2],KEY=ORD$(1,8),DOM=1056)FS1$; GOTO 1060; REM IF YOU CAN'T READ THE FS1 HEADER RECORD, REMOVE FROM PO6 AND GOTO NEXT RECORD
1056 REMOVE (Z[33],KEY=REC$,DOM=*NEXT); REM REMOVE PO6 RECORD
1058 GOTO 1030; REM GOTO NEXT RECORD
1060 READ (Z[1],KEY=REC$)
1070 READ (Z[13],KEY=X3$(9,3)+"OSP")IOL=0360
1072 TPFX$=STP(C$(583,5),2),TSEND_PATH$=STP(C$(588,30),2),TRECV_PATH$=STP(C$(618,30),2); REM "SET UP TRANSMISSION DIRECTORY PATHS FROM PARMS
1073 TDTE$=DTE(0:"%Mz%Dz")
1074 WILL_CALL$=C$(26,1); REM " SET FROM PARAMETERS - MAY BE CHANGED BY SHIP TO
1075 C[2]=C[2]+1,TRM$=STR(C[2]:"00000"); REM "Increment Transmission number
1076 SHIP_LABEL$=C$(57,1); REM " SET FROM PARAMETERS - MAY BE CHANGED BY SHIP TO
1080 WRITE (Z[13],KEY=X3$(9,3)+"OSP")IOL=0360
1090 ! 
1100 REM "Get record"
1110 READ (Z[28],KEY=ORD$,DOM=1056)IOL=0310; REM " READ ORDER HEADER
1112 IF MID(A$,352,1)<>"Y" THEN GOTO 1056
1115 GOSUB CREATE_UNIQUE_TEMP_FILE; REM "LN 7650
1120 READ (Z[29],KEY=ORD$,DOM=1121); REM " POSITION TO 1ST LINE ON ORDER DETAIL
1130 AH0$="",AS0$=""; REM "CLEAR FOR EACH ORDER (FILE SPR - ACK LOG) 
1150 DAY_FORMAT "YYYY-MM-DD"
1155 DATE$=DAY,D1$=DATE$(1,4)+DATE$(6,2)+DATE$(9,2),Q$=D1$(3)
1160 DAY_FORMAT 
1165 TIME$=DTE(0:"%hz:%mz:%sz")
1170 GOSUB 2500; REM " GET ADDRESS INFO
1180 GOSUB 3000; REM " BUILD MAIN TRANSMISSION HEADER H0
1190 GOSUB 3100; REM " BUILD TRANSMISSION HEADER H1
1200 GOSUB 3200; REM " BUILD TRANSMISSION HEADER H4
1205 GOSUB 3400
1210 IF SHIP_LABEL$=" " THEN GOTO 1300 ELSE GOSUB 3500; REM " BUILD SHIP TO REC S1,S2,S4
1215 GOSUB 3700; REM "BUILD L1, L2 PO COMMENT RECS
1220 DIM SNDREC$(125),ACKREC$(125)
1230 AKEY$=ORD$+"100"+DIM(15)+"0",OSP$=AKEY$,SNDREC$(1,LEN(AS0$))=AS0$; WRITE (Z(27),KEY=AKEY$)IOL=0410; AS0$=""
1300 REM "Read other data
1320 Y$="07C FM0...  12C AR4...  07O AR1...  12O FSV...  "; GOSUB 9750
1330 DIM D$(599); FIND (Z[7],KEY=A$(6,10),DOM=1331)IOL=0340
1370 Y$="07C AR1...  12C FSV...  07O FM0...  12O AR4...  "; GOSUB 9750
1400 REM "Get line detail information
1420 LINE$=KEY(Z[29],END=4000); REM "KEY TO ORDER DETAIL FILE - AFTER LAST LINE OR THE ENDOF THE FILE THEN GOTO 4000 TO SEND RECORD TO SPR STATIONERS
1430 IF LINE$(1,9)<>ORD$ THEN GOTO 4000; REM "SEND TO UNITED STATIONERS
1440 READ (Z[29],KEY=LINE$)IOL=0320
1441 READ (Z[3],KEY=LINE$(1,8)+LINE$(10,3))FS2$
1442 IF FS2$(155,1)<>"I" THEN GOTO 1420; REM SSP 192628    
1445 IF FS2$(94,10)<>C$(58,10) THEN GOTO 1400; REM " NOT RIGHT VENDOR CODE - COMPARED TO PARAMETER
1447 REM "IF B$(9,1)=DIM(1) THEN GOTO 1400; REM " NO PO
1449 I1=I1+1
1450 DIM H$(128)
1460 READ (Z[9],KEY=B$(101,10),DOM=1461)H$
1465 DIM RC$(200); READ (Z[32],KEY=LINE$(1,8)+LINE$(10,3),DOM=1470)RC$,RC0,RC1
1470 GOSUB 3300; REM "BUILD TRANSMISSION D1 RECORDS
1490 GOTO 1420; REM "GET NEXT LINE
1499 ! 
2500 REM "Print lines from P2$ for spec instr."
2510 DIM E$(394),OSA$(125),SHIP_ACT$(6),PSW$(8),CON_ID$(4),DC$(4),ORD_TKR$(3)
2511 READ (Z[6],KEY=A$(306,8)+"     ",DOM=2590)IOL=0350
2512 IF LEN(E$)<=339 THEN GOTO 2521; REM "IF E$(341,6)=DIM(6) THEN GOTO 2521
2513 READ (Z[31],KEY=MID(E$,341,6),DOM=2521)OSA$
2514 SHIP_ACT$=OSA$(1,6),PSW$=OSA$(37,8),CON_ID$=OSA$(45,4),ORD_TKR$=OSA$(49,3),DC$=OSA$(52,4)
2521 WILL_CALL$=C$(26,1)
2522 IF LEN(E$)=339 THEN GOTO 2526
2525 IF MID(E$,340,1)<>DIM(1) THEN WILL_CALL$=E$(340,1)
2526 SHIP_LABEL$=E$(339,1)
2527 IF SHIP_LABEL$=DIM(1) THEN SHIP_LABEL$=C$(57,1)
2528 IF SHIP_LABEL$="M" THEN SHIP_LABEL$="W"; REM IF THEY PUT IN A MULTI WRAP AND LABEL CHANGE IT TO A SINGLE...WE DON'T SUPPORT THE MULTI RIGHT NOW
2529 IF SHIP_LABEL$="S" THEN SHIP_LABEL$=" "; REM WE WANT IT TO BE BLANK
2530 IF SHIP_ACT$=DIM(6) THEN SHIP_ACT$=STR(C[1]:"000000")
2531 SHIP_ACT$=STR(NUM(SHIP_ACT$):"000000000")
2532 IF DC$=DIM(4) THEN DC$=C$(221,4)
2534 IF CON_ID$=DIM(4) THEN CON_ID$=C$(15,4)
2538 REM "Get additional 4 lines of ship-to address
2540 SHIP_TO$=""; FIND (Z[7],KEY="C"+A$(179,10)+E$(12,4),DOM=2590)SHIP_TO$
2550 F5$=""
2560 FOR INDEX=0 TO 3
2570 IF POS(" "<>MID(SHIP_TO$,402+INDEX*30,30))>0 THEN F5$=F5$+MID(SHIP_TO$,402+INDEX*30,30)
2580 NEXT INDEX
2590 RETURN 
2599 ! 
3000 REM " BUILD TRANSMISSION HEADER - H0
3004 DIM OREC$(80)
3005 OREC$(1,2)="H0"
3010 OREC$(3,9)=SHIP_ACT$; REM "COMES FROM THE OFFICE SUPPLY PARAMETERS
3011 ! OREC$(3,9)="089941700"; REM "COMES FROM THE OFFICE SUPPLY PARAMETERS
3015 OREC$(12,2)=STR(NUM(DC$):"00"); REM " SPR DISTRIBUTION CENTER
3020 OREC$(14,10)=CON_ID$; REM "USER ID =OSP PARM CONNECTION ID
3025 OREC$(24,10)="EZPO"
3030 OREC$(34,1)=""
3035 OREC$(35,1)="N"; REM " TEST FLAG
3040 OREC$(36,8)=""
3045 OREC$(44,1)=""
3050 OREC$(45,3)=""; REM SPR DC
3055 OREC$(48,8)=""; REM "ENDING VALUE
3060 OREC$(56,1)=""
3065 OREC$(57,1)=""; REM " AUTO SPLIT OF EZPO 'r' or 'N'- do not split (default)
3070 OREC$(58,1)=""; REM "
3075 OREC$(59,1)="N"; REM "
3076 OREC$(60,15)=DIM(15)
3080 GOSUB WRITE_OREC
3082 AH0$=OREC$(3,11)
3090 RETURN 
3099 ! 
3100 REM " BUILD TRANSMISSION RECORD H1
3105 DIM OREC$(80)
3108 SOURCECQ$=""
3110 OREC$(1,2)="H1"; REM " ALWAYS 'H1' FOR THE TYPE OF RECORD
3115 OREC$(3,9)=SHIP_ACT$; REM "COMES FROM OFFICE SUPPLY PARAMETER FILE FOR  DEALER SHIPPING ACCOUNT NUMBER
3120 OREC$(12,10)=A$(297,9); REM "CUSTOMER PO
3125 IF WILL_CALL$="W" THEN SHIP_CODE$="1" ELSE SHIP_CODE$="0"
3130 OREC$(22,1)=SHIP_CODE$; REM "'S/0FOR SHIP OUT or 'W/1' FOR WILL CALL - FROM O/S PARAMETER FILE
3132 ! SRCC$=C$(583,1); REM "SOURCE COMPLETE
3135 OREC$(23,1)=""; REM "SHOW DEALER COST ON PICKING TICKET
3140 ! REC$(24,2)=" "; REM "RESERVED FOR FUTURE USE
3142 DROPSH$="N",WRAPLBL$="N"
3144 IF SHIP_LABEL$="D" THEN DROPSH$="Y"
3145 IF SHIP_LABEL$="W" THEN WRAPLBL$="Y"
3146 OREC$(26,1)=DROPSH$
3147 OREC$(27,1)=WRAPLBL$
3148 OREC$(28,2)="NN"; REM " SSP
3150 OREC$(30,1)=""; REM "RESERVED FOR FUTURE USE
3155 OREC$(31,1)="L"; REM "USE LIST OR COST TO VERIFY QUANTITY
3160 OREC$(32,1)="N"; REM "WRAP & PACK PO 
3161 IF C$(23,1)="S" THEN ADOT$="Y" ELSE ADOT$="N"
3162 OREC$(33,1)=ADOT$; REM "SHIP FROM ALTERNATE WAREHOUSE
3164 OREC$(34,1)="Y"; REM "SEND ACK IF SHIP FROM ALTERNATE WAREHOUSE
3165 ! F C$(220,1)="Y" AND SHIP_LABEL$="D" THEN OREC$(70,5)=E$(124,5) ELSE OREC$(70,5)=DIM(5); REM "ZIP CODE SSP 171218..PER Jodie at SPR only used Zip code routing with Drop Ships!!
3170 OREC$(35,10)=MID(FS1$,129,10); REM "PO PLACED BY 
3175 OREC$(45,30)=""; REM "COMMENTS
3185 GOSUB WRITE_OREC
3188 AH0$=AH0$+OREC$(22,1)+OREC$(26,2)+OREC$(31,3)+OREC$(35,10)
3190 RETURN 
3199 ! 
3200 REM "SETUP 'H4' RECORD
3205 DIM OREC$(80)
3210 OREC$(1,2)="H4"; REM "ALWAYS 'H4' FOR THE TYPE OF RECORD
3215 OREC$(3,20)=""; REM "DEALER DELIVERY ROUTE INFO
3220 OREC$(23,5)=""; REM " FUTURE USE
3225 OREC$(28,20)=""; REM "DEALER DELIVERY REFERENCE NUMBER
3228 OREC$(48,1)=""; REM "CANCEL ENTIRE ORDER IF NOT FILLED- DEFAULT "N"
3230 REM "'D' FOR DROP/SHIP ORDER
3232 REM "'W' FOR SINGLE WRAP-AND-LABEL-ORDER -  '''' COULD COME FROM A FILE
3234 REM "'M' FOR MULTI WRAP-AND-LABEL-ORDER  -  '''' COULD COME FROM A FILE
3236 REM "'F' FOR FURNITURE ORDER WITH SET-UP
3238 OREC$(49,1)=""; REM "ROUTE TABLE
3240 OREC$(50,1)=""; REM "PRINT PRICED PACKING SLIPS
3245 OREC$(51,1)=""; REM "CANCEL NON UPS ITEMS
3250 OREC$(52,1)=""; REM "CROSS ZONES
3255 OREC$(53,1)=""; REM " ACKNOWLEDGE WAREHOUSE ZONE
3258 AUTOPO$=C$(220,1); IF AUTOPO$<>"Y" THEN AUTOPO$="N"; REM USE ZIP CODE ROUTING FROM PARMS
3260 ! IF SRCC$<>" " THEN AUTOPO$=SRCC$; REM "SOURCE COMPLETE 'C' OR 'Q'
3265 OREC$(54,1)=AUTOPO$; REM "Y=AUTOROUTE,N=NO AUTROUTE,C=SOURCE COMPLETE,Q=SC QUICK
3268 OREC$(55,1)="N"; REM "ACKNOWLEDGE LIST PRICE
3280 OREC$(56,1)="Y"; REM "SHOW LIST PRICE ON PICKING TICKET
3284 OREC$(57,18)=""
3288 AH0$=AH0$+OREC$(48,7)
3296 GOSUB WRITE_OREC
3298 RETURN 
3299 ! 
3300 REM "BUILD DETAIL TRANSMISSION RECORDS D1
3305 DIM OREC$(80)
3310 OREC$(1,2)="D1"; REM "ALWAYS 'D1' FOR RECORD TYPE
3315 OREC$(3,15)=H$(11,15); REM " MFG ID & STOCK NUMBER
3320 OREC$(18,2)=H$(59,2); REM "UOM
3368 DIM IC0[40]; READ (Z[30],KEY=DIM(10)+B$(101,10),DOM=*NEXT)IOL=0400
3370 IF B[2]<>0 THEN OQTY$=STR(B[1]/B[2]:"000000") ELSE OQTY$=STR(B[1]:"000000"); REM "ORDER QUANTITY  SSP 236116
3372 OREC$(20,6)=OQTY$
3374 PRICE=NUM(H$(27,9)),OREC$(26,7)=STR(PRICE*1000:"0000000")
3375 REM "I$(130,2)=C$(24,2); REM "BACK ORDER DISPOSITION - 'FP'-FILL PARTIALLY OR 'TM' FOR FILL TOTAL MERCHANDISE OR NOTHING - COMES FROM O/S PARAMETER FILE 'OSP'
3377 REM "ADOT OPTIONS ARE: 'S' SPLIT, WILL SPLIT ORDERS BETWEEN FACILITIES IF NEEDED
3378 OPTION$="T{60}"; REM " RESONSE TIME RAISED TO 60 SECONDS
3379 REM "ADOT OPTIONS ARE: 'N' DON'T 'ADOT', SEARCH PRIMARY FACIITY ONLY
3385 GOSUB WRITE_OREC
3388 DIM SNDREC$(125),ACKREC$(125)
3390 AKEY$=ORD$+"200"+H$(11,15)+"1",OSP$=AKEY$,SNDREC$=B$(101,10)+OREC$(18,52); WRITE (Z(27),KEY=AKEY$)IOL=0410
3395 REM "IF C$(460,1)="R" THEN LT$(106,20)=RC$(12,20)
3399 RETURN 
3400 REM "DETERMINE HEADER ACK RECORD IN OSP
3402 RERR=499 ! SET RECORD TYPE TO 500 IF AN INITIAL OPS RECORD OF 000 ALREADY EXISTS
3404 DIM SNDREC$(125),ACKREC$(125)
3405 AKEY$=ORD$+"000"+DIM(15)+"0",OSP$=AKEY$,SNDREC$(1,LEN(AH0$))=AH0$
3410 FIND (Z(27),KEY=AKEY$,DOM=3480)IOL=0410
3420 RERR=RERR+1; RERR$=STR(RERR:"000")
3425 AKEY$=ORD$+RERR$+DIM(15)+"0",OSP$=AKEY$,SNDREC$(1,LEN(AH0$))=AH0$
3430 GOTO 3410
3480 WRITE (Z(27),KEY=AKEY$)IOL=0410; AH0$=""
3485 HDRKEY$=AKEY$
3490 RETURN 
3499 ! 
3500 REM "SETUP SHIPPING RECORDS S1,S2,S4 IF DROPSHIP/WRAP LABEL- FORMAT CHANGED TO S5,S6,S7,S8; !SSP 236116
3505 DIM OREC$(80)
3510 OREC$(1,2)="S5"; REM "ALWAYS 'S5' FOR THE TYPE OF RECORD SSP 236116
3515 OREC$(3,35)=E$(225,35); REM "SHIP TO COMPANY NAME SSP 236116
3520 OREC$(38,35)=E$(46,30); REM "ADDRESS LINE 1 SSP 236116
3530 GOSUB WRITE_OREC
3549 ! 253952 - Changed the way 'S6' record is written
3550 ADDRESS2$=E$(76,30); REM "ADDRESS LINE 2 FOR DROP SHIP - ADDR LINE #2
3560 ADDRESS3$=E$(16,30); REM "ADDRESS LINE 3 FOR DROP SHIP - ATTN:
3570 IF NOT(NUL(ADDRESS2$)) OR NOT(NUL(ADDRESS3$)) THEN GOTO 3580 ELSE GOTO 3600
3580 DIM OREC$(80)
3585 OREC$(1,2)="S6"; REM "ALWAYS 'S6' FOR THE TYPE OF RECORD!SSP 236116
3588 OREC$(3,35)=ADDRESS2$; REM "ADDRESS LINE SSP 236116
3590 OREC$(38,35)=ADDRESS3$; REM "ADDRESS LINE 3  SSP 236116
3595 GOSUB WRITE_OREC
3600 REM "P O SHIPPING RECORD S7 SSP 236116
3605 DIM OREC$(80)
3610 OREC$(1,2)="S7"; REM "ALWAYS 'S7' FOR THE TYPE OF RECORD SSP 236116
3650 OREC$(3,35)=E$(106,16); REM "SHIP-TO CITY SSP 236116
3655 OREC$(38,20)=E$(122,2); REM "SHIP TO STATE SSP 236116
3660 OREC$(58,11)=E$(124,9); REM "SHIP-TO POSTAL CODE REQUIRED FOR DROP-SHIP SSP 236116
3665 OREC$(69,3)=""; REM "SHIP-TO COUNTRY CODE SSP 236116
3670 GOSUB WRITE_OREC
3680 RETURN 
3699 ! 
3700 REM "SETUP PO LABEL COMMENTS L1 - 253952
3705 DIM OREC$(80)
3710 OREC$(1,2)="L1"; REM "ALWAYS 'L1' FOR THE TYPE OF RECORD
3715 OREC$(3,25)=MID(FS1$,28,15); REM "SLCMT1 - CUST PO NUM
3720 OREC$(55,1)="R"; REM " PRINT SLCMT1 ON PACKLIST INSTEAD
3740 GOSUB WRITE_OREC
3745 RETURN 
3749 ! 
3800 REM "WRITE SERIAL FILE
3805 WRITE_OREC:
3810 OREC$(75,6)=CHKWRD$
3825 PRINT (TCH)OREC$
3890 RETURN 
3899 ! 
4000 REM "SEND FILE TO SPR FOR PROCESSING
4001 REM "USE DATA SENT BACK TO FILL WHAT IS GOING TO BE SHIPPED
4008 TFILE$=TPFX$+"."+ORD$+"."+TDTE$+"."+RND$
4010 TFILE_SEND$=TSEND_PATH$+TFILE$
4012 CLOSE (TCH)
4013 S2$="cp "+TEMP_FILE$+" "+TSEND_PATH$+"/hold/"+TFILE$; INVOKE S2$
4020 RENAME TEMP_FILE$ TO TSEND_PATH$+TFILE$
4030 RETURN$="OK" ! GOSUB 7200; REM "read RECV FILE FROM TRANSMISSION 
4080 IF RETURN$="OK" THEN ORDSENT$="Y",HDRSTAT$="A"; GOTO 4200 ! SSP 300552
4082 ORDSENT$="N"
4085 IF RETURN$="ERR" THEN HDRSTAT$="E"; GOTO 4200
4090 IF RETURN$="T/O" THEN HDRSTAT$="T"; GOTO 4200
4095 HDRSTAT$="N"; GOTO 4200
4100 REM "DETERMINE RESULTS SENT BACK BY SPR IF ACK FILE RECEIVED
4102 RDATA$="",PLIST$="",RORDER$=""
4105 READ RECORD (RCH,END=4200)RREC$
4110 IF RREC$(1,2)="H4" THEN GOTO 4140
4120 IF RREC$(1,2)="D1" THEN DR$="1"; GOSUB 5300; GOTO 4105
4125 IF RREC$(1,2)="D2" THEN DR$="2",D2REC$="Y"; GOSUB 5300; GOTO 4105
4130 GOTO 4105
4140 REM "GET PICKING TICKET NUMBER FROM 'H4' RECORD
4145 PTICKET$=DIM(8),PTICKET$=RREC$(57,8),SLOC$=RREC$(26,2) ! SSP 236116
4148 GOTO 4180
4150 REM "ACKNOWLEDGEMENT FROM 'D1' AND 'D2'(IF APPLICABLE FROM ALT WAREHOUSE)
4160 GOSUB 5300
4180 RDATA$="Y"
4190 GOTO 4105
4199 ! 
4200 REM "UPDATE ORDER HEADER IN "OSP" ACK FILE
4205 ! KEY$=ORD$+"000"+DIM(15)+"0",OSP$=AKEY$
4206 GOTO 4250 ! ssp 300552
4208 AKEY$=HDRKEY$
4210 FIND (Z(27),KEY=AKEY$,DOM=4290)IOL=0410
4220 IF ORDSENT$="N" THEN GOSUB 5555
4230 ACKREC$(1,1)=HDRSTAT$,ACKREC$(2,20)=HDRDESC$,ACKREC$(50,1)=DTLSTAT$
4235 ACKREC$(22,8)=PTICKET$,ACKREC$(30,2)=SLOC$ ! SSP 236116
4240 WRITE (Z(27),KEY=AKEY$)IOL=0410
4245 IF HDRSTAT$="T" THEN GOTO 4300
4250 REMOVE (Z[1],KEY=REC$,DOM=4260)
4260 IF ORDSENT$<>"Y" THEN GOTO 4290
4265 A$(353,5)=TRM$; REM " TRANSMISSION NUMBER
4270 ! IF MID(REC2$,7,1)="4" THEN A$(352,1)="C" ELSE A$(352,1)="A"; REM " FLAG TO SAY PO ALREADY SENT OR TO SEND A RE-CONFIRMATION REQUEST
4275 A$(352,1)="A"; REM " FLAG TO SAY PO ALREADY SENT OR TO SEND A RE-CONFIRMATION REQUEST
4280 WRITE (Z[28],KEY=ORD$)IOL=0310
4290 ORDSENT$=""
4295 GOTO 1030
4299 ! 
4300 REM "TIME OUT FOR SEND FILE - REMOVE FROM SEND DIR AND ALLOW TO RERUN FROM PO6
4310 S2$="rm "+TFILE_SEND$; INVOKE S2$,ERR=4320
4320 ORDSENT$=""
4330 GOTO 1030
4982 EXTRACT (Z[28],KEY=ORD$)IOL=0310; REM SSP 209077
4983 A$(353,5)=HEAD$(59,5); REM " TRANSMISSION NUMBER  SSP 209077
5000 REM "End of Print
5030 T0$="END"
5090 GOTO 9900
5299 ! 
5300 REM "PROCESS ACKNOWLEDGEMENT STATUS
5310 QORDRD=NUM(RREC$(20,6))
5315 QSHPPD=NUM(RREC$(26,6))
5320 QONSWI=NUM(RREC$(32,6))
5325 DLRNET=NUM(RREC$(38,7))
5330 ASTAT$=RREC$(45,1)
5335 RDESC$=RREC$(46,12)
5355 STKORD$=RREC$(60,15)
5400 REM "FIND SENT DATA IN FILE 'OSP'
5410 AKEY$=ORD$+"200"+STKORD$+DR$,OSP$=AKEY$
5420 FIND (Z(27),KEY=AKEY$,DOM=5500)IOL=0410
5430 ACKREC$(1,1)=ASTAT$,ACKREC$(2,12)=RDESC$
5440 ACKREC$(14,6)=STR(QORDRD:"000000")
5445 ACKREC$(20,6)=STR(QSHPPD:"000000")
5450 ACKREC$(26,6)=STR(QONSWI:"000000")
5455 ACKREC$(32,7)=STR(DLRNET:"0000000")
5460 ACKREC$(39,15)=STKORD$
5500 REM "UPDATE ACKNOWLEDGEMENT FILE FOR DETAIL
5530 WRITE (Z[27],KEY=AKEY$)IOL=0410
5535 IF D2REC$="Y" THEN D2REC$=""; RETURN 
5540 REM "SET ORDER HEADER STATUS
5550 IF ASTAT$="A" THEN DTLSTAT$="A" ELSE DTLSTAT$="E" ! Detail accepted or not
5552 RETURN 
5554 REM "FIND ERROR CODE FROM OS3
5555 R$="SPR"+HDRSTAT$+"  ",R1$="UNKNOWN"
5560 READ (Z[10],KEY=R$,DOM=5562)IOL=0370
5565 HDRDESC$=R1$,ORDSENT$="N"; GOTO 5590
5590 RETURN 
5599 ! 
5600 REM "Get name to print on shipto
5605 IF E$(11,1)="C" THEN GOSUB 5650 ELSE IF A1<>0 THEN GOSUB 5661
5610 IF LEN(E$)>=259 AND POS(" "<>E$(259))<>0 THEN E9$=E$(225,35); GOTO 5645 ELSE E9$=D$(11,35)
5615 ON POS(E$(11,1)="CPD") GOTO 5645,5620,5625,5630
5620 FIND (Z[7],KEY=E$(11,1)+D$(1,10)+E$(12,4),DOM=5645)E1$; GOTO 5635
5625 FIND (Z[7],KEY=E$(11,1)+A$(7,10)+E$(12,4),DOM=5645)E1$; GOTO 5635
5630 FIND (Z[7],KEY=E$(11,1)+Y6$(1,10)+E$(12,4),DOM=5645)E1$; GOTO 5635
5635 E9$=E1$(16,35); IF E$(11,1)="C" THEN SHIP_TO_ZIP$=E1$(129,9) ELSE SHIP_TO_ZIP$=""
5645 RETURN 
5699 ! 
6000 REM " GET THE FOUR CHARACTER UNIT OF MEASURE FROM PL3
6005 READ (Z[26],KEY=C$(58,10)+H$(36,4),DOM=6006)
6006 PC6_KEY$=KEY(Z[26])
6007 IF PC6_KEY$(1,14)<>C$(58,10)+H$(36,4) THEN RETURN 
6010 DIM PC3$(64); READ (Z[25],KEY=PC6_KEY$(1,10)+PC6_KEY$(15,10),DOM=6030)PC3$
6020 DIM UOM$(2); UOM$=PC3$(11,2)
6030 RETURN 
6999 ! 
7205 ! TRECV_PATH$="/usr/sprich/sparco/spr/recv/"
7210 TFILE_RECV$=TRECV_PATH$+TFILE$
7215 TFILE_RECVERR$=TRECV_PATH$+TFILE$+".er"
7216 X=TMR(0); RCH=HFN
7218 WAIT 30
7220 OPEN LOCK (RCH,ERR=7225)TFILE_RECVERR$; RETURN$="ERR"; GOTO 7299
7225 OPEN LOCK (RCH,ERR=7230)TFILE_RECV$; GOTO 7240
7228 ! RCH=HFN; OPEN LOCK (RCH,ERR=7230)TFILE_RECV$; GOTO 7240
7230 WAIT 30; IF TMR(1)>=360 THEN GOTO 7235 ELSE GOTO 7220
7235 RETURN$="T/O"; GOTO 7299
7240 RETURN$="OK"
7299 RETURN 
7600 ! 
7650 CREATE_UNIQUE_TEMP_FILE:! take TEMP_FILE$ and make it unique and make it  to use, we let caller make the temp_file name so we don't have to be opera ting system specific 
7655 TEMP_FILE$=TSEND_PATH$+"SPR",RND$=STR(INT(RND*1000):"0000")
7660 TCH=HFN; OPEN (TCH,ERR=7665)TEMP_FILE$+RND$; CLOSE (TCH); GOTO 7655 ! if opened then go get another random number
7665 TEMP_FILE$+=RND$; SERIAL TEMP_FILE$ ! name is unique, set it & crea te file to hold it 
7670 OPEN LOCK (TCH)TEMP_FILE$
7699 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000
9520 ON C9 GOTO 1140,2040
9750 REM 
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1; ON Z0 GOTO 9761,9900
9790 RETURN 
9900 REM "End program
9909 REM "IF V8$="A" THEN V3$(2,6)="AUTO"+V7$
9910 REM "CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9915 REM "IF V8$="A" OR (Y4$<>X0$ AND V3$(2,1)<>"T") THEN GOSUB 5300
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9921 Y4$="ZZ2PGC"
9922 IF %GUI THEN CALL Y4$,ERR=*NEXT
9923 IF %GUI THEN EXIT 
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 RUN "ZMENU"
9999 END 
56000 REM "191375-Modify SPR shipping type to be S instead of
56002 REM "203458-Addition of S.P. Richards to TOPS module                    
56003 REM "209077-Usco orders: If anyone goes back into these orders & then    
56004 REM "236116-Changes required for the SP Richards Order transmission to  
56005 REM "253952-SP Richards:  would like Attn field sent to SP Richards in  
56006 REM "300552-SPR Orders duplicating - happens when I remove a line from  
