REM OS2CSV
! 
! Marina's Price Matrix 2006/07/05 <MAPRMX>
! (w/ mods requested by Laura)
! 
BEGIN 
PATH$=%DATAPATH$+"/"
! PATH$="/home/bam/"
NMSK$="####.00"
FLE1$="MARINA_in.csv"
FLE2$="MARINA_out.csv"
ERASE PATH$+FLE1$,ERR=*NEXT
ERASE PATH$+FLE2$,ERR=*NEXT
SERIAL PATH$+FLE1$
SERIAL PATH$+FLE2$
! 
CODE_COUNT=700 ! set to max number of prcodes (origin 0).
CUSTOM1=10 ! first non-preset code subscript.
I_MAX=CUSTOM1
! 
DIM PRCODE$[CODE_COUNT](6)
DIM PP_CALC[CODE_COUNT]
! 
PRCODE$[0](1)="U9W"
PRCODE$[1](1)="UEAT"
PRCODE$[2](1)="UE1"
PRCODE$[3](1)="UH4"
PRCODE$[4](1)="UK2"
PRCODE$[5](1)="UK4"
PRCODE$[6](1)="UK6"
PRCODE$[7](1)="UK8"
PRCODE$[8](1)="UL4"
PRCODE$[9](1)="U12"
! 
OPEN (11)"OS1118"
OPEN (12)"OS1118"
OPEN (20)"OS2118"
OPEN LOCK (45,OPT="TXT")PATH$+FLE1$
! 
GOSUB DMDATA ! set data collection to blanks
! 
REM 990
L1000:
K$=KEY(11,END=L9000); READ (11)OS1$
IF OS1$(31,6)<"K60631" THEN GOTO L1000
! 
PRICE_PLAN_CODE$=OS1$(11,6)
IF PRICE_PLAN_CODE$(1,1)="U" THEN GOTO L1000 ! skip downloaded records
A_LVL$=K$(1,10) ! item_code$
PRINT "a_lvl$=",A_LVL$,"     b_lvl$=",B_LVL$
IF B_LVL$="" THEN GOTO PREC ! process record
IF A_LVL$<>B_LVL$ THEN GOSUB PLVL ! process level
! 
PREC:
B_LVL$=A_LVL$
I=CUSTOM1
PREC_LOOP:
IF PRCODE$[I]=DIM(6) THEN GOTO ADDCODE
IF PRCODE$[I]=PRICE_PLAN_CODE$ THEN GOTO ADDDATA
IF I=CODE_COUNT THEN GOTO OUTAROOM
I+=1
GOTO PREC_LOOP
! 
ADDCODE:
PRCODE$[I]=PRICE_PLAN_CODE$
I_MAX=I
! 
ADDDATA:
PRDATA$[I]=OS1$
GOTO L1000
! 
OUTAROOM:
PRINT "too many price plans - make code_count larger and rerun"
END 
! 
! 
PLVL:
READ (20,KEY=B_LVL$,DOM=*NEXT)OS2$; GOTO ITM_CDE
OS2$="123456789 "+"(no stock num.)"
ITM_CDE:
ITEM_CODE$=B_LVL$
! 
FOR J=0 TO CUSTOM1-1
READ (12,KEY=ITEM_CODE$+PRCODE$[J],ERR=*NEXT)PRDATA$[J](1)
NEXT J
! 
STOCK_NUM$=OS2$(11,15)
! 
FOR J=CUSTOM1 TO I_MAX ! was 0 to I_MAX before "cost" added below
PP_CALC[J]=NUM(PRDATA$[J](47,10)) ! AKA calc_price
NEXT J
! 
FOR J=0 TO CUSTOM1-1
PP_CALC[J]=NUM(PRDATA$[J](57,10)) ! "cost"
NEXT J
! 
! "Remove any embedded quotes.
! 
X$=STOCK_NUM$; GOSUB QLOOP; STOCK_NUM$=X$
X$=ITEM_CODE$; GOSUB QLOOP; ITEM_CODE$=X$
! X$=PRICE_PLAN_CODE$;! GOSUB QLOOP;! PRICE_PLAN_CODE$=X$
LPRT:
PRINT (45)QUO+STOCK_NUM$+QUO,",",QUO+ITEM_CODE$+QUO,",",
FOR J=0 TO I_MAX
! PRINT (45)QUO,PP_CALC[j]:NMSK$,QUO,
PRINT (45)PP_CALC[J]:NMSK$,
IF J<>I_MAX THEN PRINT (45)",", ELSE PRINT (45) ! ",",QUO+ADDNL_CODE$+QUO
NEXT J
COUNT=COUNT+1
GOSUB DMDATA
B_LVL$=A_LVL$
RETURN 
! 
DMDATA:
DIM PRDATA$[CODE_COUNT](200) ! Make sure records start empty.
! 
QLOOP:
X=POS(QUO=X$)
IF X<>0 THEN X$(X,1)="+"; GOTO QLOOP
RETURN 
! 
! "Print Heading
! 
PHEAD:
PRINT (46)QUO+"stock_num$"+QUO,",",QUO+"item_code$"+QUO,",",
FOR I=0 TO I_MAX
PRINT (46)QUO,PRCODE$[I],QUO,
IF I<>I_MAX THEN PRINT (46)",", ELSE PRINT (46) ! ",",QUO,"Addn'l Code",QUO
NEXT I
RETURN 
! 
REM 8990
L9000:
PRINT (45)"End Count=",COUNT
IF COUNT=0 THEN PRINT (45)'LF',"NO NON-DOWNLOADED RECORDS FOUND"
CLOSE (45)
! 
OPEN (45)PATH$+FLE1$
OPEN LOCK (46,OPT="TXT")PATH$+FLE2$
! 
GOSUB PHEAD
FINAL:
READ RECORD (45,END=L9900)L$
PRINT (46)L$
GOTO FINAL
! 
REM 9890
L9900:
CLOSE (45); CLOSE (46)
ERASE PATH$+FLE1$,ERR=*NEXT
END 
