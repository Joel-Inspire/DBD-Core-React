0010 REM "Parallel Inventory Pricing/Constant Setup <OS2MAB>
0020 SETESC 9300; SETERR 9000
0035 REM "5.1 - 06/11/02 - 12.222222 - lms - SSP# 148759
0040 REM "Copyright 2002 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$; F5$="*"
0095 IF Q1$="" THEN Q1$=Q0$
0100 SETERR 9000
0110 X0$="OS2MAB",X1$="OSP Inventory Constant Setup",K9$="",K9=0
0120 DIM Z0$(80,"-")
0125 V8$="000811152999"
0130 K0=10,K1=1
0135 C9=-1
0140 M0$="###,###.00-",M1$="###,##0-",M2$="#####0",M3$="##0-",M4$="##,###.0000",M7$="##,###,###.00-",M6$="####0-"
0145 IF Q1$="" THEN GOSUB 8950
0160 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0200 REM "
0230 IF Q1$<>"" THEN X2=-1; IF F5$="*" THEN PRINT @(0,3),'CE', ELSE PRINT @(0,8),'CE',
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0299 REM "CALL "ZZ2PRP","OS",RESULT$,DATE$; IF RESULT$<>"Y" THEN CALL "ZZPROM", ".4",X3$,Z,"ACCESS DENIED! The Office Supply Interface System is not an in stalled module.","","",0; RUN "ZMENU"                                      
0300 REM "IOLISTS
0310 IOLIST A$(1),A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29],A[30],A[31],A[32],A[33],A[34],A[35],A[36],A[37],A[38],A[39],A[40]
0311 IOLIST A$(1),STR(A[0]),A[1],STR(A[2]),A[3],STR(A[4]),A[5],STR(A[6]),A[7],STR(A[8]),A[9],STR(A[10]),A[11],STR(A[12]),STR(A[13]),A[14],A[15],A[16],STR(A[17]),STR(A[18]),A[19],STR(A[20]),A[21],STR(A[22]),A[23],STR(A[24]),A[25],STR(A[26]),A[27],STR(A[28]),A[29],STR(A[30]),A[31],STR(A[32]),A[33],A[34],A[35],A[36],A[37],A[38],A[39],A[40]
0320 IOLIST B$
0321 IOLIST B1$
0330 IOLIST C$,C[0],C[1],C[2]
0370 IOLIST I$,I[0],I[1]
0375 IOLIST G$,G[0],G[1],G[2],G[3],G[4],G[5],G[6],G[7],G[8],G[9],G[10],G[11],G[12],G[13],G[14],G[15],G[16],G[17],G[18],G[19],G[20]
0376 IOLIST G$,G[0],STR(G[1]),STR(G[2]),G[3],G[4],G[5],G[6],G[7],G[8],G[9],G[10],G[11],G[12],G[13],G[14],G[15],G[16],G[17],G[18],G[19],G[20]
0380 IOLIST I1$,S[0],S[1]
0390 IOLIST P$,P[0],P[1]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X OS0...  02O FMP...  03O AP4...  04O IC5...  05O IC4...  06O FM0...  07O IC1...  08O IC6...  09O IC2...  10O AR1...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0580 FIND (Z[13],KEY=X3$(9,3)+"G/B",DOM=0581); GB$="Y"; REM "Gold Bond
0585 W9$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,W9$
0590 READ (Z[1],KEY=K9$,DOM=0591)
0600 REM "
0605 DIM S9$(255); FIND (Z[13],KEY="osec"+X3$(40,3),DOM=0606)S9$
0610 FIND (Z[13],KEY=X3$(9,3)+"I/C")P0$
0611 P0$(38,1)="N"
0615 DIM M5[1]; M5$="###,###.00 ",M5[0]=621,M5[1]=2; IF P0$(45,1)>"2" THEN DIM M5$(11,"0"); M5[1]=NUM(P0$(45,1)),M5$(1,10-M5[1])="##########",M5$(POS("0"=M5$),1)=".",M5[0]=(10-M5[1])*100+M5[1]*10
0620 V1=2
0680 GOSUB 6000
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(60),C$(227),C[2],I$(21),I[1],I1$(21),S[1],P$(21),P[1],J[1],G[20],B1$(35)
1035 DIM A$(800),A[40]
1045 A$(1)=A1$; IF A1$="" THEN A$(1)=K9$
1048 IF P0$(69,1)="Y" AND A$(112,1)=" " THEN A$(112,1)="Y"
1050 IF Q1$<>"" THEN GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 C9=0,Z9=0
1200 REM "Item code
1205 IF Q1$<>"" THEN A$(1,10)=Q1$; GOSUB 6460; IF LEN(Q1$)>10 THEN GOTO 1220
1206 X$="F10 retrieves by Office Supply Item Number"
1208 A0=2
1210 CALL "ZZENTR","AXUX",A{ALL},A$,X4$,X3$,1,4+V0,1+K9,10,C0,"","{1"+X$,"","AP2MAA00","OSD","",""; IF C0<>-7 THEN IF C0<-10 THEN GOTO 1210 ELSE IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1211 REM "CALL "OS2ICE",X3$,X4$,A$,B[15],A0,Z[1],1,C0,X0,P0$(38,1); IF ABS(C0)> 4 THEN GOSUB 6400                                                          
1212 IF C0=-7 THEN C0=1; CALL "OS2PLU",X3$,A$,1,X$; IF X$="" THEN GOTO 1200
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9700,6970
1270 REM "FIND (Z[3],KEY=A$(534,10),DOM=1260)IOL=0330
1500 REM "
1505 Z9=-1,A2$="",A3$="",A4$="",A5$=""
1510 A1$=A$(1,K9+10)
1511 IF Q0$="1" THEN Q0$=""; GOTO 2000
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1522 A2$=A$(61,3)+A$(1,20),A3$=A$(82,14)+A2$,A4$=A$(61,3)
1525 Z9=0
1526 Z=0
1527 FOR I=1 TO 12; A[I]=NUM(A$(214+Z,10)); Z=Z+10; NEXT I
1535 FIND (Z[3],KEY=A$(98,10),DOM=1536)IOL=0330
1540 FIND (Z[13],KEY="U/M"+A$(124+K9,4),DOM=1541)IOL=0370
1545 FIND (Z[13],KEY="U/M"+A$(120+K9,4),DOM=1546)IOL=0390
1560 FIND (Z[2],KEY="I"+A$(109,3),DOM=1561)IOL=0321
1590 GOTO 1700
1600 REM "
1603 IF POS(X3$(185,1)="IJ")>0 THEN CALL "ZZPROM",".4",X3$,0,"New records may not be added using a Quick Change program.","","",0; GOTO 1630; REM "WO133043
1605 IF W9$>"" THEN IF W9$(7,1)="R" THEN CALL "ZZPROM",".4",X3$,0,"Remote T/M users cannot add records to this information.","","",0; GOTO 1630
1610 IF Q1$<>"" THEN GOTO 1690
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630
1630 IF Q1$<>"" THEN GOTO 9700 ELSE GOTO 1000
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1810 IF Q1$<>"" THEN CALL "ZZPROM","1",X3$,Z,"","","V",X0; ON Z+1 GOTO 1850,6700,1850
1815 IF POS(X3$(185,1)="IJ")>0 THEN CALL "ZZPROM","6A",X3$,Z,"","","",0; ON Z GOTO 6700,1850; REM "WO133043, modify base price only
1820 CALL "ZZPROM",STR(6+C9),X3$,Z,"","","V",X0; ON Z GOTO 1850,6500,1630,6700
1830 GOTO 2000
1850 REM "MODIFY
1851 IF W9$>"" THEN IF W9$(7,1)="R" THEN CALL "ZZPROM",".4",X3$,0,"Remote T/M users cannot modify this information","","",0; GOTO 1800
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310
1865 C9=1
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 Z=Z+1; C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF INT(C0)=3 THEN GOSUB 7300 ELSE IF C0>3 OR X0<0 THEN GOTO 1800
2030 IF C0=2 AND X0=23 THEN GOSUB 7600
2035 IF X3$(185,1)="J" THEN ON X0 GOTO 2150,1800; REM "WO134758, mod product code only 
2039 IF X3$(185,1)="I" THEN ON X0 GOTO 2500,1800; REM "W0133043, modify base price only
2040 ON X0 GOTO 2150,2200,2250,2300,2100,2800,3350,3400,2350,2400,3800,3950,2900,2950,3250,3300,2500,2550,2600,2550,2600,2550,2600,2550,2600,2550,2600,2550,2600,3000,3900,3450,3600,2850,3750,3550,3650,3700,3500,3850,1800
2100 REM "Description
2110 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,39,4+V1,37+K9,40,C0,"","{3"+X$,"","IC2MAB02","","",""
2140 GOTO 2020
2150 REM "Product code
2160 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,11,4+V1,77+K9,3,C0,"","{3"+X$,"T","IC2MAB04","FMPP","FM2SAA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2160,2161
2170 FIND (Z[2],KEY="T"+A$(77+K9,3),DOM=2160)IOL=0320
2180 PRINT @(10,22),"Product code: ",A$(77+K9,3)," - ",B$(5,15),
2190 GOTO 2020
2200 REM "Lft-rt
2210 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,16,4+V1,80+K9,8,C0,"","{3"+X$,T9$,"IC2MAB06","","",""
2240 GOTO 2020
2250 REM "Top-btm
2260 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,25,4+V1,88+K9,8,C0,"","{3"+X$,"","IC2MAB08","","",""
2290 GOTO 2020
2300 REM "Plys
2310 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,35,4+V1,96+K9,2,C0,"","{3"+X$,"","IC2MAB10","","",""
2340 GOTO 2020
2350 REM "Primary supplier
2360 CALL "ZZENTR","AU",A{ALL},A$,X4$,X3$,13,9,98+K9,10,C0,"A/P","{3"+X$,"","IC2MAB12","AP4","AP2UTE",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2360,2361
2370 FIND (Z[3],KEY=A$(98+K9,10),DOM=2360)IOL=0330
2375 PRINT @(37,9+V0),C$(11,35),
2390 GOTO 2020
2400 REM "Plant code
2405 O$="P"+A$(98+K9,10)
2410 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,22,10,108+K9,4,C0,"","{3"+X$,O$,"IC2MAB14","FM0P","X",""; IF ABS(C0)=102 THEN GOTO 7800 ELSE IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2410,2411
2414 IF POS("0"<>A$(108,4))=0 THEN A$(108,4)=""; PRINT @(22,10),A$(108,4),; GOTO 2421
2415 IF POS(" "<>A$(108,4))=0 THEN GOTO 2421
2420 FIND (Z[6],KEY="P"+A$(98,10)+A$(108,4),DOM=2410)
2440 GOTO 2020
2500 REM "Base Price
2510 PRECISION M5[1]; CALL "ZZENTR","N&",A{ALL},A$,X4$,X3$,33,14,0,M5[0],C0,M5$,"{3"+X$,"","IC2MAB18","","",""; PRECISION 2
2540 GOTO 2020
2550 REM "quantity break
2552 IF P$(20,1)<>"Y" THEN O1=A[14] ELSE O1=P[1]
2555 DIM J[1]; J=X0-16,Y=INT(J/2)+15,J[0]=A[J]/O1
2557 IF J>1 THEN IF A[J-2]=0 THEN GOTO 2020
2560 CALL "ZZENTR","NX.",J{ALL},A$,X4$,X3$,0,Y,0,822,C0,M7$,"{3"+X$,"","IC2MAB20","","",""
2570 IF C0>0 THEN A[J]=J[0]*O1; GOSUB 7500
2590 GOTO 2020
2600 REM "Price
2605 J=X0-16,Y=INT(J/2)+14; IF A[J-1]=0 THEN GOTO 2630
2610 PRECISION M5[1]; CALL "ZZENTR","N&X",A{ALL},A$,X4$,X3$,33,Y,J,M5[0],C0,M5$,"{3"+X$,"","IC2MAB22","","",""; PRECISION 2
2615 IF C0>0 THEN GOSUB 7500
2640 GOTO 2020
2800 REM "Second Description Line
2805 IF P0$(47,1)<>"Y" THEN GOTO 2020
2810 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,39,7,144,40,C0,"","{3"+X$,"","IC2MAB80","","",""
2840 GOTO 2020
2850 REM "Lot inventory?"
2855 IF P0$(44,1)<>"Y" THEN GOTO 2020
2857 IF POS(A$(128,1)="YN")>0 THEN IF S9$(60,1)="Y" THEN GOTO 2020; REM "WO91158
2858 IF P0$(69,1)="Y" THEN IF A$(128,1)=" " THEN A$(128,1)="Y"; PRINT @(67,15),A$(128,1),; GOTO 2020; REM "108802
2860 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,67,15,128,1,C0,"","{3"+X$,"","IC2MAB82","","",""
2890 GOTO 2020
2900 REM "Item Group
2905 IF P0$(46,1)<>"Y" THEN GOTO 2020
2909 J9$=A$(98,10),J8$=A$(184,10)
2910 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,48,8,184,10,C0,"","{3"+X$,J9$,"IC2MAB82","FE0","FE2MAA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2910,2911
2915 IF POS(" "<>A$(184,10))=0 THEN A$(194,10)=""; PRINT @(69,8),A$(194,10); GOTO 2930
2920 J9$=A$(98,10)+A$(184,10)
2925 CALL "IC2MAE",X3$,J9$,0,F9; IF F9=1 THEN GOTO 2910
2940 GOTO 2020
2950 REM "Pricing Features
2955 IF P0$(46,1)<>"Y" OR POS(" "<>A$(184,10))=0 THEN GOTO 2020
2959 J9$=A$(98,10)+A$(184,10),J8$=A$(194,10)
2960 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,69,8,194,10,C0,"","{3"+X$,J9$,"IC2MAB84","-FE1","FE2MBA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2960,2961
2965 IF J9$=A$(194,10) OR POS(" "<>A$(194,10))=0 THEN GOTO 2980
2970 J9$=A$(98,10)+A$(184,20)
2975 CALL "IC2MAE",X3$,J9$,1,F9; A$(194,10)=J9$(21,10); PRINT @(69,8),A$(194,10),; IF F9=1 THEN GOTO 2960
2990 GOTO 2020
3000 REM "Quantity Break Group
3002 IF P0$(50,1)<>"Y" THEN GOTO 2020
3005 X$="Quantity Break Pricing Group"; PRINT @(0,22),'CL',
3010 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,44,13,129,3,C0,"","{3"+X$,"","IC2MAB74","","",""
3040 GOTO 2020
3250 REM "Selling U/M
3252 O1$=A$(120+K9,4),Q5=A[14]
3260 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,14,13,136+K9,4,C0,"","{3"+X$,"U/M","IC2MAB48","U/M","ZZ2MUA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3260,3261
3265 FIND (Z[13],KEY="U/M"+A$(136+K9,4),DOM=3260)IOL=0390
3285 IF O1$<>A$(136+K9,4) OR Q5<>A[14] THEN GOSUB 7900
3290 GOTO 2020
3300 REM "Selling pack"
3302 Q5=A[14]
3305 IF P$(20,1)<>"Y" THEN GOTO 3310
3306 IF P[0]<>0 THEN A[14]=P[0] ELSE A[14]=P[1]
3307 PRINT @(30,13),A[14]:M2$,; GOTO 3311
3310 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,30,13,14,600,C0,M2$,"{3"+X$,"","IC2MAB50","","",""
3320 IF O1$<>A$(120+K9,4) OR Q5<>A[14] THEN GOSUB 7900
3340 GOTO 2020
3350 REM "Inventory U/M
3360 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,59,3+V1,140+K9,4,C0,"","{3"+X$,"U/M","IC2MAB52","U/M","ZZ2MUA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3360,3361
3370 FIND (Z[13],KEY="U/M"+A$(140+K9,4),DOM=3360)IOL=0370
3385 IF POS(" "<>A$(136+K9,4))=0 THEN A$(136+K9,4)=A$(140+K9,4),P[0]=I[0],P[1]=I[1]; PRINT @(14,13),A$(136+K9,4),
3390 GOTO 2020
3400 REM "Qty/UM
3404 O1=A[15]
3405 IF I$(20,1)<>"Y" THEN GOTO 3410
3406 IF I[0]<>0 THEN A[15]=I[0] ELSE A[15]=I[1]
3407 PRINT @(73,3+V1),A[15]:M2$,; GOTO 3430
3410 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,73,3+V1,15,600,C0,M2$,"{3"+X$,"","IC2MAB54","","",""
3425 IF I$(20,1)<>"Y" AND A[19]=0 THEN A[19]=A[15]; PRINT @(72,14),A[19]:M2$,
3435 IF A[14]=0 THEN A[14]=A[15]; PRINT @(30,13),A[14]:M2$,
3436 IF A[15]<>O1 THEN GOSUB 7700
3440 GOTO 2020
3450 REM "Weight per carton
3455 IF ABS(C0)<>2 THEN GOSUB 7500
3460 CALL "ZZENTR","N.",A{ALL},A$,X4$,X3$,67,13,16,621,C0,M0$,"{3"+X$,"","IC2MAB56","","",""
3490 GOTO 2020
3500 REM "Last sold
3510 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,67,20,112+K9,6,C0,"","{3"+X$,"","IC2MAB58","","",""
3540 GOTO 2020
3550 REM "Last purchased
3560 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,67,17,118+K9,6,C0,"","{3"+X$,"","IC2MAB60","","",""
3590 GOTO 2020
3600 REM "Carton pack
3610 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,72,14,19,600,C0,M2$,"{3"+X$,"","IC2MAB64","","",""
3640 GOTO 2020
3650 REM "Last Purchase Cost"
3660 PRECISION M5[1]; CALL "ZZENTR","N&",A{ALL},A$,X4$,X3$,67,18,17,M5[0],C0,M5$,"{3"+X$,"","IC2MAB62","","",""; PRECISION 2
3690 GOTO 2020
3700 REM "Average Purchase Cost"
3705 X$="Overall average cost for distributor owned inventory"
3710 PRECISION M5[1]; CALL "ZZENTR","N&",A{ALL},A$,X4$,X3$,67,19,18,M5[0],C0,M5$,"{3"+X$,"","IC2MAB64","","",""; PRECISION 2
3740 GOTO 2020
3750 REM "Standard Cost
3752 IF S9$(39,1)="Y" THEN GOTO 2020
3755 X$="Also known as STANDARD COST - 0 means use average cost"; PRINT @(0,22),'CL',
3760 PRECISION M5[1]; CALL "ZZENTR","N&",A{ALL},A$,X4$,X3$,67,16,20,M5[0],C0,M5$,"{3"+X$,"","1C2MAB70","","",""; PRECISION 2
3790 GOTO 2020
3800 REM "Item Price Class
3810 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,22,11,125,3,C0,"","{3"+X$,"I","IC2MAB72","FMPI","IC2MEA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3810,3811
3820 DIM B1$(60); IF A$(125,3)<>"   " THEN FIND (Z[2],KEY="I"+A$(125,3),DOM=3810)B1$
3825 PRINT @(27,11),B1$(5,30),
3840 GOTO 2020
3850 REM "Minimum Quantity
3851 IF GB$<>"Y" THEN GOTO 2020
3852 X$="Minimum charge will apply if quantity ordered is less this quantity"
3855 CALL "ZZENTR","NX.",A{ALL},A$,X4$,X3$,67,21,33,802,C0,M7$(1,10),"{3"+X$,"","IC2MAB74","","",""
3895 GOTO 2020
3900 REM "Gold Bond LIFO Cost
3902 IF GB$<>"Y" THEN GOTO 2020
3905 X$="Enter the LIFO unit cost relative to inventory unit of measure"
3910 PRECISION M5[1]; CALL "ZZENTR","N&",A{ALL},A$,X4$,X3$,15,21,34,M5[0],C0,M5$,"{3"+X$,"","1C2MAB76","","",""; PRECISION 2
3940 GOTO 2020
3950 REM "Never backorder?, WO105428
3955 X$="Y means this item will never backorder on an order"; PRINT @(0,22),'CL',
3960 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,76,11,133,1,C0,"","{3"+X$,"","IC2MAB86","","",""
3990 GOTO 2020
6000 REM "BACKGROUND                                                            
6005 PRINT (0,ERR=6016)'SB',
6060 REM "CALL "OS2ICE",X3$,X4$,"",0,0,0,0,0,0,P0$(38,1)                        
6070 PRINT @(0,3+V0),"Item code",@(11,3+V0),"P/C",@(16,3+V0),"Left-Rgt Top-Botm Plys",@(40,3+V0),"Description",'CL','SB',@(55,3+V0),"UM:",@(65,3+V0),"Qty   /UM:",@(0,5+V0),Z0$,
6080 PRINT @(3,9+V0),"Supplier:",@(1,10+V0),"Plant code:",@(0,11+V0),Z0$,
6085 REM "PRINT @(4,11+V0),"Item price class:",@(59,11+V0),"Never backorder?",
6090 PRINT @(0,12),"Supplier item no:",@(1,13+V0),"Purchasing unit:",@(3,14+V0),"Qty/Break:",@(24,13),"Qty/Unit:",@(23,14+V0),"Base cost:",
6095 IF P0$(50,1)="Y" THEN PRINT @(39,13),"QBG:",
6100 PRINT @(52,13),"Carton weight:",@(54,14),"Carton pack:",@(52,15),"Last Pur chase:",@(56,16),"Last cost:",
6110 IF P0$(44,1)="Y" THEN PRINT @(52,15),"Lot inventory?",
6115 IF P0$(46,1)="Y" THEN PRINT @(36,8+V0),"Item Group:",@(59),"Features:",
6120 IF P0$(47,1)="Y" THEN PRINT @(38,7+V0),'CL',
6125 IF P0$(50,1)="Y" THEN PRINT 
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 REM "IF A$(124,1)="I" THEN PRINT @(22,8),"Inactive", ELSE PRINT @(22,8),"Active",
6224 CALL "ZZDISP","AU",A$(98+K9,10),"A/P",X3$,"","",13,9,X4$
6228 PRINT @(18,12),A$(548,20),@(18,13),A$(568,4),@(35,13),NUM(A$(718,10)):M2$,@(33,14),NUM(A$(578,10)):M0$,
6230 REM "GOSUB 7500
6235 REM "IF P0$(50,1)="Y" THEN PRINT @(44,13),A$(129,3),
6240 PRINT @(67,13),NUM(A$(364,10)):M0$,@(72,14),NUM(A$(394,10)):M6$,@(67,16),NUM(A$(404,10)):M5$,@(67,17),FND$(A$(118,6)),@(67,18),NUM(A$(374,10)):M5$,@(67,19),NUM(A$(384,10)):M5$,@(67,20),FND$(A$(112,6)),
6245 REM "IF P0$(44,1)="Y" THEN PRINT @(67,15),A$(128,1),
6250 REM "IF P0$(47,1)="Y" THEN PRINT @(39,7),A$(144,40),
6255 REM "IF P0$(46,1)="Y" THEN PRINT @(48,8),A$(184,10),@(69,8),A$(194,10),
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6442 IF C0>0 THEN GOSUB 7500
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6465 REM "CALL "OS2ICE",X3$,X4$,A$,A[15],1,Z[1],Z[10],C0,X0,P0$(38,1)
6466 PRINT @(1,4+V0),A$(1,10),
6470 REM "CALL "ZZDISP","A",B$(534,10),"A/P",X3$,"","",13,9,X4$
6471 PRINT @(59,3+V0),A$(136,4),@(73,3+V0),NUM(A$(354,10)):"#####0",@(11,4+V0),A$(77,3),@(16,4+V0),A$(80,8),@(25,4+V0),A$(88,8),@(35,4+V0),A$(96,2),@(39,4+V0),A$(37,40),
6475 GOSUB 7200; PRINT @(13,10),A$(544,4),; GOSUB 7250
6490 RETURN 
6500 REM "DELETE
6502 IF W9$>"" THEN IF W9$(7,1)="R" THEN CALL "ZZPROM",".4",X3$,0,"Remote T/M users cannot modify this information","","",0; GOTO 6590
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6610 FIND (Z[13],KEY="U/M"+A$(140+K9,4),DOM=6611)IOL=0370
6620 FIND (Z[13],KEY="U/M"+A$(136+K9,4),DOM=6621)IOL=0390
6690 RETURN 
6700 REM "ADD ENTRY
6702 IF Z<>0 THEN IF W9$>"" THEN IF W9$(7,1)="R" THEN CALL "ZZPROM",".4",X3$,0,"Remote T/M users cannot modify this information","","",0; GOTO 6790
6705 REM " REMEMBER TO PUT THE NUMERIC PRICES BACK INTO THE STRING BEFORE WRITING THEM!!
6710 REM "WRITE (Z[1],KEY=A1$)IOL=0311
6712 IF W9$>"" THEN IF W9$(7,1)="H" THEN CUST$=A$(1,10); CALL "RT2WOC",ERR=6713,X3$,X4$,CUST$,"IC0...","U",A$(1,20)
6715 IF A1$(1,10)<>MID(S$,1,10) THEN CALL "FM2UTI",X3$,A1$,"I"
6720 IF A$(61,3)+A$(1,20)<>A2$ THEN REMOVE (Z[4],KEY=A2$,DOM=6721)
6725 WRITE (Z[4],KEY=A$(61,3)+A$(1,20))
6730 IF A$(82,14)+A$(61,3)+A$(1,20)<>A3$ THEN REMOVE (Z[5],KEY=A3$,DOM=6731)
6735 WRITE (Z[5],KEY=A$(82,14)+A$(61,3)+A$(1,20))
6740 REM "IF A4$<>A$(61,3) THEN GOSUB 7400
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6825 A$(1,LEN(A1$))=A1$
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
7200 REM "Display vendor name & loc                                             
7201 RETURN 
7220 PRINT @(26,9),FNS$(C$(11,35)),; IF POS(" "<>C$(106,16))=0 THEN PRINT 'CL', ELSE PRINT " - ",FNS$(C$(106,16)),", ",C$(122,2),
7240 RETURN 
7300 REM 
7310 ON FPT(C0)*10 GOTO 7390,7325,7330,7390
7325 IF V8$="" THEN X0=X0+4; GOTO 7390 ELSE X0=NUM(V8$(POS(STR(X0:"00")<=V8$,2),2)); GOTO 7390
7330 IF V8$="" THEN X0=X0-6; GOTO 7335 ELSE X0=POS(STR(X0-1:"00")<=V8$,2)-2; IF X0<0 THEN X0=0; GOTO 7390 ELSE X0=NUM(V8$(X0,2)); GOTO 7390
7335 IF X0<0 THEN X0=0; GOTO 7390
7390 C0=3; RETURN 
7400 REM "Look through locations and change IC6 sort if product code changed
7410 READ (Z[7],KEY=A$(1,20),DOM=7411)
7420 READ (Z[7],END=7450)J$
7425 IF J$(1,20)<>A$(1,20) THEN GOTO 7450
7430 REMOVE (Z[8],KEY=J$(21,4)+A4$+J$(1,20),DOM=7431)
7440 WRITE (Z[8],KEY=J$(21,4)+A$(61,3)+J$(1,20))
7445 GOTO 7420
7490 RETURN 
7500 REM "TO AND UP
7505 DIM S$(34+LEN(M5$))
7515 FOR X=1 TO 5
7520 Q5=X*2-1
7525 IF A[Q5]=0 THEN A[Q5+1]=0,A[Q5+2]=0,A[Q5+3]=0
7530 Q0=A[Q5],Q1=A[Q5+1],Q2=A[Q5+2]; GOSUB 7580
7550 NEXT X
7560 Q0=A[11],Q1=A[12]; IF A[21]=0 THEN Q2=0; GOSUB 7580 ELSE Q2=A[21]; GOSUB 7580; PRINT @(14,15+X),"--more breaks on screen 10--",
7570 RETURN 
7580 REM "Print a line based on Q0,Q1,Q2,X
7582 IF Q0=0 THEN PRINT @(0,14+X),S$,; RETURN 
7583 IF P$(20,1)<>"Y" THEN IF A[14]<>0 THEN Q0=Q0/A[14],Q2=Q2/A[14] ELSE IF P[1]<>0 THEN Q0=Q0/P[1],Q2=Q2/P[1]
7585 PRINT @(0,14+X),Q0:M7$,
7586 IF Q2=0 THEN PRINT @(14,14+X),"  and over       ",@(33),Q1:M5$, ELSE PRINT @(15,14+X),"to ",Q2-.01:M7$,@(33,14+X),Q1:M5$,
7595 RETURN 
7600 REM "Set X0 when backing into quan. break section
7605 IF A[12]<>0 THEN RETURN ELSE IF A[1]=0 THEN X0=11; RETURN ELSE X0=14
7610 FOR X=3 TO 11 STEP 2
7615 IF A[X]<>0 THEN X0=X0+2
7620 NEXT X
7640 RETURN 
7700 REM "Change costs if pack changed
7705 IF O1=0 THEN RETURN 
7710 PRECISION M5[1]; A[17]=A[17]*A[15]/O1,A[18]=A[18]*A[15]/O1; PRECISION 2
7720 PRINT @(67,18),A[17]:M5$,@(67,19),A[18]:M5$,
7730 READ (Z[7],KEY=A$(1,20),DOM=7731)
7740 J$=KEY(Z[7],END=7790)
7750 IF A$(1,20)<>J$(1,20) THEN GOTO 7790
7760 EXTRACT (Z[7],KEY=J$)IOL=0375
7770 PRECISION M5[1]; G[1]=G[1]*A[15]/O1,G[2]=G[2]*A[15]/O1; PRECISION 2
7780 WRITE (Z[7],KEY=J$)IOL=0376
7782 IF W9$>"" THEN IF W9$(7,1)="H" THEN CUST$=J$(1,10); CALL "RT2WOC",ERR=7783,X3$,X4$,CUST$,"IC1...","U",J$
7785 GOTO 7740
7790 RETURN 
7800 REM "Do setup for location
7810 Q9$=O$
7820 CALL "FM2SKA",X3$,X4$,Q0$,Q9$
7825 GOSUB 6400
7830 IF LEN(Q9$)>11 THEN A$(92+K9,4)=Q9$(12); GOTO 2411 ELSE GOTO 2410
7900 REM "Change pricing is pack changed"
7905 IF Q5=A[14] THEN GOTO 7950
7910 A[0]=A[0]*A[14]/Q5
7920 FOR X=1 TO 11 STEP 2
7925 A[X+1]=A[X+1]*A[14]/Q5
7930 NEXT X
7988 GOSUB 7500
7990 RETURN 
8950 REM "Program flow logic
8960 DIM U0$(6); U0$(1)=FID(0); U0$=U0$+"PF"
8970 CLOSE (14); OPEN (14)"ZZPARM"; FIND (14,KEY=U0$,DOM=8990)U0$,U1$
8975 IF U1$="" THEN RETURN 
8980 Q1=10,Q1$=U1$(7,10)
8985 REM "IF Q1<>20 THEN Q1$=""; GOTO 8975
8990 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9725 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9730 RUN U1$(1,6)
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
