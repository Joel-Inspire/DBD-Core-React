0010 ! SQLGEN - Generic SQL Class
0035 REM "5.5 - 01/03/07 - 13.743611 - jme - SSP# 202107
0037 REM "202107-Oracle, MySQL Database definition
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0100 DEF CLASS "SQLGEN" CREATE REQUIRED 
0200 ! start variable list
0210 LOCAL COMPANY$
0220 PROPERTY DB_PFX$
0225 PROPERTY OSMSG$,DB_LOC$,DB_NAME$,DB_USER$,DB_PASS$
1000 FUNCTION GENSQL(*)
1010 ENTER SQL_GEN$
1015 SQL_GEN$=STP(SQL_GEN$,3,$8A$)
1020 OSMSG$="",FIL_NO=HFN,STATUS=0; OPEN (FIL_NO,ERR=*NEXT)DB_PFX$+DB_NAME$+";;USER="+DB_USER$+";PSWD="+DB_PASS$+";"; STATUS=1
1025 IF NOT(STATUS) THEN OSMSG$=MSG(-1)
1027 SEP$=";"
1030 IF STATUS THEN {
1035 ANS$=""; SQL_GEN2$=""
1050 XP=POS("CREATE UNIQUE INDEX"=SQL_GEN$); IF XP>0 THEN SQL_GEN2$=SQL_GEN$(XP),SQL_GEN$=SQL_GEN$(1,XP-1)
1090 XP=POS("CREATE INDEX"=SQL_GEN$); IF XP>0 THEN SQL_GEN2$=SQL_GEN$(XP),SQL_GEN$=SQL_GEN$(1,XP-1)
1095 ! XP=POS("("=SQL_GEN$);NAME$=SQL_GEN$(14,XP-14);WRITE RECORD (FILE_NO)"DROP TABLE "+NAME$
1100 STATUS=0; WRITE RECORD (FIL_NO,ERR=*NEXT)SQL_GEN$; STATUS=1
1105 IF NOT(STATUS) THEN OSMSG$=MSG(-1)
1110 IF SQL_GEN2$<>"" AND STATUS THEN {
1112 XP=POS(SEP=SQL_GEN2$); IF XP>0 THEN SQL_GEN2$=SQL_GEN2$(1,XP-1)+SQL_GEN2$(XP+1); GOTO *SAME
1115 SQL$=SQL_GEN2$
1120 WHILE SQL$<>""
1130 XP=POS("CREATE "=SQL$(2))
1140 IF XP>0 THEN SQL_GEN2$=SQL$(1,XP),SQL$=SQL$(XP+1) ELSE SQL_GEN2$=SQL$,SQL$=""
1150 STATUS=0; WRITE RECORD (FIL_NO,ERR=*NEXT)SQL_GEN2$; STATUS=1
1155 IF NOT(STATUS) THEN OSMSG$=MSG(-1)
1165 WEND 
1167  } ! 1110
1170 CLOSE (FIL_NO)
1180  }
1190 RETURN STATUS
1200 FUNCTION GET_COUNT(X$)
1210 ENTER FILE$
1215 NUM_OF_REC=0
1220 NUM_REC_VAR$="%NUM_REC_"+MID(FILE$,1,3)
1230 NUM_OF_REC=EVN(NUM_REC_VAR$,ERR=*NEXT); IF NUM_OF_REC<>0 THEN GOTO 1391
1300 PREFIX$=DB_PFX$+DB_NAME$+";;USER="+STP(DB_USER$,2)+";PSWD="+STP(DB_PASS$)
1310 ! SQL$="SELECT /*+ INDEX_FFS(T KEY0_"+FILE$+")*/ COUNT(*) FROM "+FILE$+" T"
1311 ! SQL$="SELECT COUNT(*) FROM "+FILE$
1312 SQL$="SELECT NUM_ROWS FROM SYS.ALL_TABLES WHERE OWNER='"+DB_USER$+"' AND TABLE_NAME='"+FILE$+"'"
1320 SQL=HFN; OPEN (SQL,ERR=1400)PREFIX$
1330 WRITE RECORD (SQL)SQL$
1340 ANS$=""
1350 READ RECORD (SQL,END=*NEXT)RESP$; ANS$+=RESP$; GOTO *SAME
1360 CLOSE (SQL)
1370 ANS$=STP(ANS$,3," "); ANS$=STP(ANS$,3,$8A$)
1380 NUM_OF_REC=NUM(ANS$,ERR=*NEXT)
1390 VIA NUM_REC_VAR$=NUM_OF_REC
1400 RETURN NUM_OF_REC
2090 ! 
2095 REM "Execute SQL string and return result string
2100 FUNCTION EXECSQL(*)
2110 ENTER SQL$
2120 OSMSG$="",FIL_NO=HFN,STATUS=0; OPEN (FIL_NO,ERR=*NEXT)DB_PFX$+DB_NAME$+";;USER="+DB_USER$+";PSWD="+DB_PASS$+";"; STATUS=1
2125 IF NOT(STATUS) THEN GOTO ERR_EXECSQL
2130 STATUS=0; WRITE RECORD (FIL_NO,ERR=*NEXT)SQL$; STATUS=1
2135 IF NOT(STATUS) THEN GOTO ERR_EXECSQL
2185 GOTO 2195
2190 ERR_EXECSQL: OSMSG$=MSG(-1)
2195 RETURN STATUS
8999 END DEF
9999 END 
10000 ON_CREATE:
10010 ENTER COMPANY$,EX0$
10020 IF EX0$<>"" THEN {
10025 DB_LOC$=STP(EX0$(2,60),2)
10027 DB_NAME$=STP(EX0$(62,60),2)
10030 DB_USER$=STP(EX0$(164,20),2)
10035 DB_PASS$=STP(EX0$(184,20),2)
10040 IF POS("["=DB_NAME$)>0 THEN DB_PFX$="" ELSE IF EX0$(163,1)="M" THEN DB_PFX$="[odb]" ELSE DB_PFX$="[oci]"
10080  }
10090 RETURN 
