0010 REM "Contact Masterfile Setup <ZP2MEA>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 11/01/10 - 11.556388 - tma - SSP# 241998
0037 REM "241998-Working in contact management in CHUI the system keeps      
0040 REM "Copyright 2010 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000; SETESC 9300
0110 X0$="ZP2MEA",X1$="Contact Masterfile Setup",K9$="",K9=0
0115 X1$=X1$,M9$="###,###,###"
0120 DIM S$(80),V[1]
0130 K0=10,K1=2
0135 C9=-1,V9=0,S9=-2
0140 V8$="00030920233099"
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(65,1)>"2" THEN PRECISION NUM(X3$(65,1),ERR=0246)
0280 Z0$="30 Contact person"
0295 DIM Z1$(80,X3$(235,1)); Z1$=MNM('GS')+Z1$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1),A[0],A[1]
0330 IOLIST C$,C[0],C[1]
0340 IOLIST D$,D[0],D[1]
0350 IOLIST E$,E[0],E[1]
0360 IOLIST F$,F[0],F[1]
0390 IOLIST I$
0400 IOLIST J$
0410 IOLIST G$
0420 IOLIST H$(1),H[0],H[1],H[2],H[3],H[4],H[5],H[6],H[7],H[8],H[9],H[10],H[11],H[12],H[13],H[14],H[15]
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X ZP4...  02O ZPA...  03O ZP0...  04O ZP2...  05O ZP1...  06O ZP"+"3...  07O ZPD...  08O ZPE...  09O ZP6...  10O ZP7...  12O AR1...  13O"+" ZZPARM  "
0513 CALL "ZZ2FNC;OPENSORTUSERFILE","ZPC",_ZPC,USERNAME$
0520 GOSUB 9750; ON Z0 GOTO 0521,9900
0540 GOSUB 6000
0550 DIM P8$(32); READ (Z[13],KEY=X3$(9,3)+"T/M",DOM=0551)P8$
0580 W9$=""; CALL "RT2PRM",ERR=0581,X3$,X4$,W9$
0582 IF W9$<>"" THEN Z$="14O RT6... 15O RT7... "; GOSUB 9750; IF Z0>0 THEN W9$=""; REM "If Remote T/M then open RT6 file to get SEND to list, and RT7 to post resends,if error then don't do remote T/M at all
0590 READ (Z[1],KEY=K9$,DOM=0591)
0600 REM "
0660 FIND (Z[13],KEY=FID(0)+X0$,DOM=0661)A1$,S9,K1$; REMOVE (Z[13],KEY=FID(0)+X0$); Z=9
0670 IF S9=1 THEN READ (_ZPC,KEY=K1$,DOM=0671)
0690 GOSUB 8950
0700 GOSUB 8700
0720 DIM S9$(255); FIND (Z[13],KEY="osec"+X3$(40,3),DOM=0721)S9$
0800 REM "Turn auto add mode off
0810 M0=0
1000 REM "
1003 IF P8$(7,1)="Y" THEN J9$=A$
1005 IF Z=8 THEN GOTO 7900 ELSE IF A1$>"" AND Z<>9 THEN A1$=A1$(1,4)
1010 Y8=1; CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM C$(45),C[1],D$(44),D[1],E$(44),E[1],F$(42),F[1]
1032 IF W9$<>"" THEN PREV_SP_CODE$="",PREV_CUST_CODE$=""
1035 DIM A$(629),A[1],N$(1),G$(599); REM "w/o 108317, SSP232017
1038 Q7=0
1045 A$(1,LEN(A1$))=A1$; IF A1$="" THEN A$(1,LEN(K9$))=K9$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1080
1070 GOSUB 6000; GOSUB 6450
1080 IF POS(" "<>J9$)<>0 THEN A$(129,9)=J9$(129),A$(359,4)=J9$(359),A$(220,3)=J9$(220),A$(223,3)=J9$(223); GOSUB 5800; GOSUB 6200
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 SETERR 9000; C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9700
1140 ON X0 GOTO 1200,1250,1500
1200 REM "List type
1202 IF Q1$>"" THEN A$(1,4)=Q1$(1,4); PRINT @(16,3),A$(1,4); GOTO 1220
1206 IF LEN(A1$)>0 THEN A$(1,4)=A1$(1,4); PRINT @(16,3),A$(1,4),; GOTO 1220
1210 CALL "ZZENTR","SZXUX",A{ALL},A$,X4$,X3$,16,3,1,4,C0,"","{1"+X$,K9$,"ZP"+"2MEA00","ZP0","ZP2MAA",""; IF C0=6 THEN GOSUB 6400; C0=1
1211 IF POS(" "<>S9$(8,24))>0 AND POS(A$(1,4)=S9$(8,24),4)=0 AND ABS(C0)<>4 THEN GOTO 1210
1212 IF INT(ABS(C0))=3 THEN C0=-ABS(C0); READ (Z[1],KEY=A$(1,4),DOM=1213)
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9700
1220 FIND (Z[3],KEY=A$(1,4),DOM=1210)IOL=0330
1222 V1$=C$(1,4)
1225 PRINT @(21,3),C$(5,40),
1230 READ (Z[1],KEY=A$(1,4),DOM=1231)
1240 GOTO 1120
1250 REM "Prospect code
1251 IF Q1$>"" THEN IF POS(" "<>Q1$(5,6))>0 THEN A$(5,6)=Q1$(5,6); PRINT @(16,4),A$(5,6); GOTO 1262
1252 Q$=A$(1,4); IF LEN(A1$)>4 THEN A$(5,6)=A1$(5,6); PRINT @(16,4),A$(5,6),; GOTO 1262
1253 IF M0=1 THEN GOSUB 7500; GOTO 1261; REM "auto add mode is on
1254 K$=""; IF P8$(7,1)="Y" THEN J$="ZP2EKD",J0=10 ELSE J$="ZZCKEY",J0=9
1255 IF S7=5 THEN Q$=A$(1,4),C0=1; CALL J$,X3$,X0$,1,12,J0,20,Z[2],Q$,K$,58,6,78,Z[1],S9$; GOSUB 8000; GOSUB 6079; S7=0; IF K$>"" THEN P9$="S",A$(5,6)=K$(25),K$=K$(5,20) ELSE P9$="" END_IF ; IF K$="" THEN GOTO *NEXT ELSE GOTO 1261 ! SSP#237400!SSP#241998
1258 X$="[Ret]=alpha lookup; A=auto assign number; T=turn profile on/off; F"+"10=phone no"
1260 CALL "ZZENTR","SUXZ"+P9$,A{ALL},A$,X4$,X3$,16,4,5,6,C0,"","{2"+X$,Q$,"ZP2MEA02","ZP4","",""; P9$=""; IF C0=-7 THEN GOTO 7600 ELSE IF A$(5,6)="ADD   " THEN M0=1; GOSUB 7700; GOSUB 7500 ELSE IF A$(5,6)="A     " THEN GOSUB 7500 ELSE IF C0=-1 THEN S7=5; GOTO 1250 ELSE IF C0=6 THEN GOSUB 6400; C0=1 ELSE IF A$(5,6)="T     " THEN GOSUB 8800; A$(5,6)=""; GOTO 1258
1261 IF ABS(C0)=2 AND LEN(A1$)>0 THEN A1$=A1$(1,0)
1263 REM "IF an 'E' is part of the string it should be alpha, not mistaken "+"for a scientific notation number
1264 IF POS("E"=A$(5,6))=0 THEN A$(5,6)=STR(NUM(A$(5,6),ERR=1265):"000000"); PRINT @(16,4),A$(5,6),
1265 IF INT(ABS(C0))=3 THEN X9=1; GOTO 6800 ELSE IF ABS(C0)=4 THEN GOTO 9700
1290 GOTO 1120
1500 REM "
1505 Z9=-1,Z8=0,C9=0,A2$="",A3$="",A4$="",A5$=""
1510 A1$=A$(1,10),Q1$=""
1520 READ (Z[1],ERR=7200,KEY=A1$,DOM=1600)IOL=0310
1522 DIM G$(599); FIND (Z[12],DOM=1523,KEY=A$(363,10))IOL=0410
1524 A3$=A$(363,10),A4$=A$(200,10)+A$(1,10),A5$=A$(203,7)+A$(200,3)+A$(1,10); IF POS(X3$(9,3)="060",3)<>0 THEN A4$=A$(200,12)+A$(1,10)
1525 Z9=0,V7$=""; GOSUB 5800
1527 IF W9$<>"" THEN SP_CODE$=A$(359,4),PREV_SP_CODE$=A$(359,4),PREV_CUST_CODE$=A$(363,10); REM "Set current salesperson into SP_CODE$
1530 IF S9>0 THEN IF ABS(C0)>=3.1 AND ABS(C0)<=3.2 THEN GOSUB 8850
1590 GOTO 1700
1600 REM "
1610 GOSUB CHECK_CONTACT_RANGE; IF RANGE_OK$<>"Y" THEN GOTO 1000
1619 REM "M0=1 means auto add mode is on, Q7 means they used A to add and w"+"ant to skip the question
1620 IF M0=1 OR Q7=1 THEN GOTO 1650 ELSE CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630
1630 GOTO 1000
1655 A$(214,6)=X3$(21,6),A$(358,1)="0"; GOSUB 6292
1660 IF V9>=0 THEN GOSUB 6200
1665 Z8=1
1680 GOSUB 7450
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1820 CALL "ZZPROM","7 ZP2MEA",X3$,Z,"","","V",X0; ON Z GOTO 1850,6500,1821,6700,1821,1821,1821,7800,1821
1825 GOTO 6700
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 GOSUB 7400
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1; IF Z8=1 THEN X0=-1; IF P8$(7,1)<>"Y" THEN X0=25
2020 SETERR 9000; Q$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF INT(C0)=3 THEN GOSUB 7300 ELSE IF C0>3 OR X0<0 THEN GOTO 1800
2035 IF V9<>-2 THEN IF 6*(X0+K1)+5<LEN(V9$) THEN IF V9$((X0+K1)*6+5,1)="3" THEN GOTO 2020 ELSE IF V9$((X0+K1)*6+5,1)="2" AND C0<>2 THEN GOTO 2020
2038 IF X0<LEN(V7$) THEN V7$(X0+1,1)=""
2040 IF P8$(7,1)="Y" THEN GOTO 2050 ELSE ON X0 GOTO 2100,2150,2200,3350,2250,2300,2350,2400,3450,2450,2450,2450,2450,2450,2450,2450,2450,2450,2450,2450,2500,3000,3050,3100,3150,3200,3250,3300,3400,1800; REM " w/o 108317 ADDED 3450
2050 ON X0 GOTO 2100,2150,2300,2250,3350,2200,2350,2400,3450,2450,2450,2450,2450,2450,2450,2450,2450,2450,2450,2450,2500,3000,3050,3100,3150,3200,3250,3300,3400,1800; REM "w/o 108317 ADDED 3450
2100 REM "Name
2102 IF N$="NEW" AND P9$(7,1)<>"Y" THEN X0=25; GOTO 2020
2105 X$="DA for Source Code"
2106 IF M0=1 THEN X$=X$+" <F10> to turn off auto add mode"
2110 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,30,4,11,40,C0,"","{3"+X$,"","ZP2ME"+"A04","","",""
2115 IF C0=7 THEN M0=0; GOSUB 7700; GOTO 2105; REM "Turn off auto add mode
2140 GOTO 2020
2155 X$=A$(11,40)
2158 P7$="@"+P8$(7,1)+"!#$A"
2159 Q$=A$(51,163)+A$(373,66)
2160 CALL "ZY2AAA",X3$,X4$,0,6,P7$,Z0$,2,Q$,X$,0,C0,Q8
2161 A$(51,163)=Q$(1,163),A$(373,66)=Q$(164,66)
2170 IF ABS(C0)>4 THEN GOSUB 6400; GOTO 2160
2190 GOTO 2020
2200 REM "Date entered
2205 IF P8$(7,1)<>"Y" THEN X$="UA for Name/Address, DA for Selection Codes"; IF C0=1 THEN GOTO 2020
2210 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,22,11,214,6,C0,"","{3"+X$,"","ZP2M"+"EA08","","",""
2240 GOTO 2020
2250 REM "Source
2260 IF A$(5,6)="``````" THEN CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,22,12,220,3,C0,"","{3"+X$,"","ZP2MEA10","","","" ELSE CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,22,12,220,3,C0,"","{3"+X$,"","ZP"+"2MEA10","ZP2","ZP2MCA",""; IF C0=6 THEN GOSUB 6400; C0=1
2265 IF A$(5,6)="``````" THEN GOTO 2276
2270 FIND (Z[4],KEY=A$(220,3),DOM=2260)IOL=0340
2275 PRINT @(27,12),D$(4,40),
2280 X$=""
2290 GOTO 2020
2300 REM "Prospect category
2310 IF A$(5,6)="`````" THEN CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,22,13,223,3,C0,"","{3"+X$,"","ZP2MEA12","","","" ELSE CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,22,13,223,3,C0,"","{3"+X$,"","ZP"+"2MEA12","ZP1","ZP2MBA",""; IF C0=6 THEN GOSUB 6400; C0=1
2315 IF A$(5,6)="``````" THEN GOTO 2326
2320 FIND (Z[5],KEY=A$(223,3),DOM=2310)IOL=0350
2325 PRINT @(27,13),E$(4,40),
2340 GOTO 2020
2350 REM "SIC Code
2355 IF NUM(D$(43,3),ERR=2360)<>0 THEN CALL "ZZDATE",X3$,"",A$(11,6),Q$,NUM(D$(43,3)),0,0,0,0,0,0,"","",""; IF A$(239,6)="      " THEN A$(239,6)=Q$,A$(245,1)=D$(42,1)
2357 IF V9<>-2 THEN IF A$(245,1)<>" " THEN FIND (Z[4],KEY=A$(245,1),DOM=2358)IOL=0340; A$(246,40)=D$(2,40)
2360 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,22,14,226,5,C0,"","{3"+X$,"","ZP"+"2MEA14","","",""
2390 GOTO 2020
2400 REM "Annual sales
2410 CALL "ZZENTR","N.&",A{ALL},A$,X4$,X3$,47,14,0,902,C0,M9$,"{3"+X$,"","Z"+"P2MEA16","","",""
2440 GOTO 2020
2450 REM "Selection codes
2452 Q0=X0-9
2460 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,22+Q0*5,15,231+Q0*3,3,C0,"","{3"+X$,"","ZP2MEA18","ZP6","ZP2MFA",""; IF C0=6 THEN GOSUB 6400; C0=1
2470 X$=""; IF A$(231+Q0*3,3)="   " THEN GOTO 2490 ELSE READ (Z[9],KEY=A$(231+Q0*3,3),DOM=2460)IOL=0390; X$=I$(1,3)+" = "+I$(4)
2480 IF POS(" "<>I$(44,40))>0 THEN IF POS(A$(1,4)=I$(44,40))=0 THEN CALL "ZZPROM",".4",X3$,0,"This selection code may not be used with this list type","","",0; X$=""; GOTO 2460
2490 GOTO 2020
2500 REM "E-mail Address
2510 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,19,16,464,60,C0,"","{3"+X$,"","ZP"+"2MEA42","","","" ! SSP232017
2540 GOTO 2020
3000 REM "Last contact date
3005 X$="UA for selection codes"
3010 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,17,18,264,6,C0,"","{3"+X$,"","ZP2M"+"EA40","","",""
3040 GOTO 2020
3050 REM "Last contact
3060 CALL "ZZENTR","SUX",A{ALL},A$,X4$,X3$,30,18,270,1,C0,"","{3"+X$,"","ZP"+"2MEA42","ZP3","ZP2MDA",""; IF C0=6 THEN GOSUB 6400; C0=1
3070 IF V9<>-2 THEN IF A$(270,1)<>" " THEN FIND (Z[6],KEY=A$(270,1),DOM=3060)IOL=0360
3080 IF C0>0 THEN A$(271,40)=F$(2,40)
3090 GOTO 2020
3100 REM "Last contact comment
3110 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,34,18,271,40,C0,"","{3"+X$,"","ZP2"+"MEA44","","",""
3140 GOTO 2020
3150 REM "Follow-up date
3155 IF NUM(F$(43,3),ERR=3160)<>0 THEN CALL "ZZDATE",X3$,"",A$(264,6),Q$,NUM(F$(43,3)),0,0,0,0,0,0,"","",""; IF A$(311,6)=S$(1,6) THEN A$(311,6)=Q$,A$(317,1)=F$(42,1)
3157 IF V9<>-2 THEN IF A$(317,1)<>" " THEN FIND (Z[6],KEY=A$(317,1),DOM=3158)IOL=0360; A$(318,40)=F$(2,40)
3160 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,17,19,311,6,C0,"","{3"+X$,"","ZP2M"+"EA46","","",""
3190 GOTO 2020
3200 REM "Follow-up code
3210 CALL "ZZENTR","SUX",A{ALL},A$,X4$,X3$,30,19,317,1,C0,"","{3"+X$,"","ZP"+"2MEA48","ZP3","ZP2MDA",""; IF C0=6 THEN GOSUB 6400; C0=1
3220 IF V9<>-2 THEN IF A$(317,1)<>" " THEN FIND (Z[6],KEY=A$(317,1),DOM=3210)IOL=0360
3230 IF C0>0 THEN A$(318,40)=F$(2,40)
3240 GOTO 2020
3250 REM "Follow up comment
3260 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,34,19,318,40,C0,"","{3"+X$,"","ZP2"+"MEA50","","",""
3290 GOTO 2020
3300 REM "Barometer
3310 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,17,20,358,1,C0,"","{3"+X$,"","ZP2M"+"EA52","","",""; IF C0=6 THEN GOSUB 6400; C0=1
3315 A3=0; A3=NUM(A$(358,1),ERR=3316)
3316 CALL "ZZBARG",X3$,"Hp",20,29,20,0,9,A3
3340 GOTO 2020
3350 REM "METRO code
3360 IF A$(5,6)="``````" THEN CALL "ZZENTR","SZUX",A{ALL},A$,X4$,X3$,58,11,359,4,C0,"","{3"+X$,"","ZP2MEA52","","","" ELSE CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,58,11,359,4,C0,"","{3"+X$,"","Z"+"P2MEA52","ZP7","ZP2MGA",""; IF C0=6 THEN GOSUB 6400; C0=1
3365 IF A$(5,6)="``````" THEN GOTO 3381
3370 READ (Z[10],KEY=A$(359,4),DOM=3360)IOL=0400
3380 IF POS(" "<>J$)<>0 THEN X$=J$(1,4)+" = "+J$(5,40)
3385 IF W9$<>"" THEN SP_CODE$=A$(359,4),PREV_SP_CODE$=A$(359,4),PREV_CUST_CODE$=A$(363,10); REM "Set current salesperson into SP_CODE$
3390 GOTO 2020
3400 REM "CUSTOMER CODE
3405 IF U1$>"" AND A$(363,10)=S$(1,10) AND POS(" "<>A$(11,127))=0 THEN IF U1$(1,6)="AR2MAA" THEN A$(363,10)=U1$(9,10)
3410 CALL "ZZENTR","AXUX",A{ALL},A$,X4$,X3$,17,21,363,10,C0,"A/R","{3"+X$,"","ZP2MEA66","AR1","",""; IF C0=6 THEN GOSUB 6400; C0=1
3415 IF POS(" "<>A$(363,10))=0 THEN IF POS(" "<>A$(11,127))>0 THEN GOTO 2020 ELSE X0=-1; GOTO 2020
3420 DIM G$(599); FIND (Z[12],KEY=A$(363,10),DOM=3410)IOL=0410
3421 GOSUB VALIDATE_REP; IF NOT(VAL_REP) THEN CALL "ZZPROM",".4",X3$,0,"Access denied you may not use this customer","","",0; GOTO 3410 ! SSP#212992
3422 S3=1; IF G$(401,10)<>S$(1,10) THEN IF G$(401,10)<>A$(1,10) THEN CALL "ZZPROM",".Y",X3$,S3,"NOTE: Customer record already has a T"+"/M Code.  Replace with this code?","","",0; IF S3=2 THEN GOTO 6500 ELSE IF S3=1 THEN P6$="N"
3430 PRINT @(28,21),G$(11,35),
3435 IF Z8=1 AND POS(" "<>A$(11,127))=0 THEN A$(11,40)=G$(11,35),A$(51,163)=G$(56,163),A$(359,4)=G$(278,4),A$(223,3)=G$(220,3),A$(111,16)="",A$(150,10)="",A$(419,20)=G$(155,10),A$(391,28)=G$(116,16); GOSUB 6200; X0=2
3440 GOTO 2020
3450 REM "DUNS Number
3460 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,66,14,452,12,C0,"","{3"+X$,"","ZP"+"2MEA14","","",""
3490 GOTO 2020
5800 REM "Alt sort keys
5810 FIND (Z[4],KEY=A$(220,3),DOM=5811)IOL=0340
5815 FIND (Z[5],KEY=A$(223,3),DOM=5816)IOL=0350
5820 FIND (Z[6],KEY=A$(270,1),DOM=5821)IOL=0360
5825 FIND (Z[6],KEY=A$(317,1),DOM=5826)IOL=0360
5835 A2$=A$(1,4)+A$(419,20)+A$(5,6)
5890 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6055 GOSUB 8000
6060 CALL "ZZVDTP",X3$,X0$,X1$,"",0
6070 CALL "ZY2AAA",X3$,X4$,0,6,"#$A",Z0$,0,Q$,"",0,C1,Q9
6079 PRINT (0,ERR=6080)'SB',
6080 PRINT @(50,17),"Last mod:"
6085 PRINT @(3,16),'CL','SB',"E-Mail Address:"; REM "w/o 115558, SSP232017
6090 PRINT @(60,14),"DUNS:"; REM "w/o 108317
6100 GOSUB 8700
6110 GOSUB 7700
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 PRINT @(30,4),A$(11,40),
6225 Q$=A$(51,163)+A$(373,66); CALL "ZY2AAA",X3$,X4$,0,6,"#$A",Z0$,1,Q$,"",0,C1,Q9; A$(51,163)=Q$(1,163),A$(373,66)=Q$(164,66)
6230 GOSUB 6292
6234 PRINT @(58,11),A$(359,4),@(22,12),A$(220,3),@(27,12),D$(4,40),@(22,13),A$(223,3),@(27,13),E$(4,40),@(22,14),A$(226,5),@(66,14),A$(452,12),; REM "w/o 108317
6235 PRINT @(19,16),A$(464,60),; REM W/O 115558, SSP232017
6238 CALL "ZZDISP","NM. M",STR(A[0]:M9$),"",X3$,"","",47,14,X4$
6242 PRINT @(22,15),A$(231,3),@(27,15),A$(234,3),@(32,15),A$(237,3),@(37,15),A$(240,3),@(42,15),A$(243,3),@(47,15),A$(246,3),@(52,15),A$(249,3),@(57,15),A$(252,3),@(62,15),A$(255,3),@(67,15),A$(258,3),@(72,15),A$(261,3),
6244 CALL "ZZDISP","D   ",A$(439,6),"",X3$,"","",60,17,X4$; IF POS(" "<>A$(449,3))>0 THEN PRINT @(71,17),"By: ",A$(449,3),
6246 CALL "ZZDISP","D   ",A$(264,6),"",X3$,"","",17,18,X4$
6250 PRINT @(30,18),A$(270,1),@(34,18),A$(271,40),
6254 CALL "ZZDISP","D   ",A$(311,6),"",X3$,"","",17,19,X4$
6258 PRINT @(30,19),A$(317,1),@(34,19),A$(318,40),
6262 PRINT @(17,20),A$(358,1),
6263 A3=0; A3=NUM(A$(358,1),ERR=6264)
6264 CALL "ZZBARG",X3$,"Hp",20,29,20,0,9,A3
6292 CALL "ZZDISP","D   ",A$(214,6),"",X3$,"","",22,11,X4$
6293 CALL "ZZDISP","A",A$(363,10),"A/R",X3$,"","",17,21,X4$; PRINT @(28,21),G$(11,35),
6295 RETURN 
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>5 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 PRINT @(16,3),A$(1,4),@(21,3),C$(5,40),@(16,4),A$(5,6),
6490 RETURN 
6500 REM "DELETE
6505 IF POS(" "<>S9$(48,6))>0 THEN X$="  "; CALL "ZZPASW",X3$,"",S9$(48,6),X$,70,22,PWD; IF PWD<0 THEN GOTO 1800
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6515 REMOVE (Z[2],KEY=A2$,DOM=6516)
6520 REMOVE (Z[7],KEY=A4$,DOM=6521)
6527 REMOVE (Z[8],KEY=A5$,DOM=6528)
6528 IF S9=1 THEN REMOVE (_ZPC,KEY=K1$,DOM=6529)
6530 CALL "ZP2MEC",X3$,A1$,0,0
6531 IF W9$>"" THEN RT_COMM$="D"; RT_FILE$="ZP4...",RT_KEY$=A1$; GOSUB REMOTE_TM
6540 IF A$(363,10)<>S$(1,10) THEN DIM H$(599),H[15]; EXTRACT (Z[12],KEY=A$(363,10),DOM=6741)IOL=0420; IF A$(1,10)=H$(401,10) THEN H$(401,10)=""; WRITE (Z[12],KEY=A$(363,10))IOL=0420
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6605 FIND (Z[3],KEY=A$(1,4),DOM=6606)IOL=0330
6690 RETURN 
6700 REM "ADD ENTRY
6705 IF Z9=0 THEN GOTO 6750
6708 A$(439,6)=X3$(21,6),A$(445,4)=STR(TIM*100:"0000"),A$(449,3)=X3$(40,3)
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6720 IF A2$=A$(1,4)+A$(419,20)+A$(5,6) THEN GOTO 6726
6724 REMOVE (Z[2],KEY=A2$,DOM=6725)
6725 REMOVE (Z[7],KEY=A4$,DOM=6726)
6726 REMOVE (Z[8],KEY=A5$,DOM=6727)
6728 WRITE (Z[2],KEY=A$(1,4)+A$(419,20)+A$(5,6))
6729 IF P6$="N" THEN GOTO 6731
6730 S3=1; IF MID(G$,401,10)<>S$(1,10) AND A$(363,10)<>S$(1,10) THEN IF MID(G$,401,10)<>A$(1,10) THEN CALL "ZZPROM",".Y",X3$,S3,"NOTE: Customer record already has a T"+"/M Code.  Replace with this code?","","",0; IF S3=2 THEN P6$="" ELSE IF S3=1 THEN P6$="N"
6732 IF P6$="N" THEN P6$=""; GOTO 6742
6735 DIM H$(599),H[15]; IF A3$>"" THEN IF A3$<>S$(1,10) THEN EXTRACT (Z[12],KEY=A3$,DOM=6736)IOL=0420; H$(401,10)=""; WRITE (Z[12])IOL=0420
6740 IF A$(363,10)<>S$(1,10) THEN EXTRACT (Z[12],KEY=A$(363,10),DOM=6741)IOL=0420; H$(401,10)=A1$; WRITE (Z[12])IOL=0420
6742 IF POS(X3$(9,3)="060",3)<>0 THEN IF A4$<>A$(200,12)+A$(1,10) THEN REMOVE (Z[7],KEY=A4$,DOM=6743) ELSE IF A4$<>A$(200,10)+A$(1,10) THEN REMOVE (Z[7],KEY=A4$,DOM=6743)
6744 IF POS(X3$(9,3)="060",3)<>0 THEN ZPD_KEY$=A$(200,12)+A$(1,10); WRITE (Z[7],KEY=A$(200,12)+A$(1,10)) ELSE ZPD_KEY$=A$(200,10)+A$(1,10); WRITE (Z[7],KEY=A$(200,10)+A$(1,10))
6746 IF A5$<>A$(203,7)+A$(200,3)+A$(1,10) THEN REMOVE (Z[8],KEY=A5$,DOM=6747)
6748 WRITE (Z[8],KEY=A$(203,7)+A$(200,3)+A$(1,10))
6749 IF W9$>"" THEN RT_COMM$="U"; RT_FILE$="ZP4...",RT_KEY$=A1$; GOSUB REMOTE_TM
6780 IF Z=10 THEN GOTO 7950
6790 C9=0; IF Z=9 THEN GOTO 8150 ELSE IF Z=2 OR Z=8 THEN GOTO 7900 ELSE IF Z=6 OR K1=0 THEN GOTO 9700 ELSE IF Z<4 THEN GOTO 1000 ELSE C0=-(3.1+(Z-4)/10),X9=1; GOTO 6800
6800 REM "NEXT ENTRY IN FILE
6802 F9=Z[1]; IF S9>0 THEN IF INT(ABS(C0))=3 THEN F9=_ZPC
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(F9,END=6895)
6815 IF POS(" "<>S9$(8,24))>0 AND POS(A1$(1,4)=S9$(8,24),4)=0 THEN GOTO 1260
6820 IF A1$="" THEN GOTO 6800 ELSE IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6822 IF F9=_ZPC THEN K1$=A1$,A1$=A1$(1,4)+A1$(30,6)
6825 A$(1,LEN(A1$))=A1$
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM "PRIOR ENTRY
6852 IF F9=_ZPC THEN A1$=K1$
6853 READ (F9,KEY=A1$,DOM=6854)
6855 K2$=KEP(F9,ERR=6858); IF K2$(1,4)<>A1$(1,4) THEN GOTO 6858 ELSE A1$=K2$; GOTO 6815
6860 PRINT 'RB',; CALL "ZZKEYL",ERR=6861,F9,A1$; GOTO 6815
6865 GOTO 1000
6870 GOTO 6895
6890 GOTO 1515
6895 IF A1$="" THEN GOTO 6860 ELSE PRINT 'RB',; READ (F9,KEY=K9$,DOM=6896)
6898 GOTO 6810
7200 REM "Forced read
7210 IF ERR<>0 THEN GOTO 9000
7220 CALL "ZZREAD",Z[1],A1$,A$,A{ALL},S8; ON S8 GOTO 7221,1630
7230 GOSUB 6200
7240 CALL "ZZPROM","E0",X3$,0,"","","",0; GOTO 7290
7250 CALL "ZZPROM","E0A",X3$,0,"","","",0; GOTO 7290
7290 GOTO 1630
7300 REM 
7310 ON FPT(C0)*10 GOTO 7390,7325,7330,7390
7325 IF V8$="" THEN X0=X0+4; GOTO 7390 ELSE X0=NUM(V8$(POS(STR(X0:"00")<=V8$,2),2)); GOTO 7390
7330 IF V8$="" THEN X0=X0-6; GOTO 7335 ELSE X0=POS(STR(X0-1:"00")<=V8$,2)-2; IF X0<0 THEN X0=0; GOTO 7390 ELSE X0=NUM(V8$(X0,2)); GOTO 7390
7335 IF X0<0 THEN X0=0; GOTO 7390
7390 IF X0>0 THEN X0=X0-0
7395 C0=3; RETURN 
7400 REM "EXTRACT
7410 EXTRACT (Z[1],ERR=7200,KEY=A1$,DOM=7440)IOL=0310
7420 C9=1,Z9=1; GOSUB 5800
7440 RETURN 
7450 REM "EXTRACT NEW ENTRY
7460 WRITE (Z[1],KEY=A1$,DOM=7250)IOL=0310; GOTO 7400
7490 RETURN 
7500 REM "AUTO ASSIGN CODE
7510 EXTRACT (Z[3],KEY=A$(1,4))IOL=0330
7515 IF C[0]=0 THEN GOTO 7550
7520 C[0]=C[0]+1; IF C[0]>999999 THEN C[0]=1
7540 A$(5,6)=STR(C[0]:"000000")
7545 GOTO 7580
7550 REM "alpha add, work from right to left, at each step N5 is the number"+" to add to that column, if N5 is zero (no carry), then we are done
7555 N5=1,N5$=C$(45,6)
7556 REM "If all Z's, then roll over to next set of numbers.
7557 IF POS("Z"<>N5$)=0 THEN DIM N5$(LEN(N5$)); N5$(LEN(N5$),1)="0"; GOTO 7578
7560 FOR I=LEN(N5$) TO 1 STEP -1
7564 IF N5$(I,1)=" " THEN GOTO 7575
7565 N5$(I,1)=CHR(ASC(N5$(I,1))+N5)
7569 REM "Now check to see if overflow occured,( out of ranges 0-9, or A-Z)"+". If yes then set N5 to carry and adjust to proper value, else ex"+"it, we're done
7570 IF N5$(I,1)>"Z" AND (N5$(I,1)<"0" OR N5$(I,1)>"9") THEN N5$(I,1)="0",N5=1 ELSE IF N5$(I,1)>"9" AND (N5$(I,1)<"A" OR N5$(I,1)>"Z") THEN N5$(I,1)="A",N5=0; EXITTO 7578 ELSE N5=0; EXITTO 7578
7575 NEXT I
7578 C$(45,6)=N5$,A$(5,6)=N5$
7580 PRINT @(16,4),A$(5,6),
7585 WRITE (Z[3])IOL=0330
7586 IF P8$(7,1)="Y" THEN Q7=1; REM "Skip do you wish to add question.
7590 RETURN 
7600 REM "PHONE NUMBER LOOKUP
7605 X$="TM",C0=1; CALL "ZZ2PLU",X3$,A$,1,X$
7606 IF POS(" "<>S9$(8,24))>0 AND POS(A$(1,4)=S9$(8,24),4)=0 AND ABS(C0)<>4 THEN GOTO 1210
7610 IF X$="" THEN GOTO 1258 ELSE X$=""; GOTO 1261
7700 REM "Print Auto Add mode indiciator
7710 PRINT (0,ERR=7711)'SB',
7715 PRINT @(0,5),Z1$,; IF M0=1 THEN PRINT @(16,5),"Auto Add",
7720 PRINT (0,ERR=7721)'SF',
7745 RETURN 
7800 REM "VIEW NOTES
7810 CALL "ZP2MEC",X3$,A1$,6,Z
7820 IF Z>0 THEN PRINT @(0,3),'CE',; GOSUB 6400
7890 GOTO 1800
7900 REM "GO TO CALL PROGRAM
7902 IF Z<>8 THEN GOTO 7910; REM "86153 Notes/Contact issue
7904 UU0$=U0$,UU1$=X0$+STR(LEN(A1$):"00")+A1$; REM "86153
7905 IF U1$>"" THEN IF U1$(1,6)="AR2MAA" THEN UU1$=UU1$+U1$
7906 WRITE (Z[13],KEY=UU0$)UU0$,UU1$; REM "86153
7910 WRITE (Z[13],KEY=FID(0)+X0$)A1$,S9,K1$
7920 IF Z=2 THEN RUN "ZP2SMA" ELSE RUN "ZP2CAA"
7950 REM "Position Profile
7960 IF S9<1 THEN CALL "ZZPROM",".4",X3$,0,"Sorry, profile not on!","","",0; GOTO 1820
7970 READ (_ZPC,KEY="",DOM=7971)
7980 K$=KEY(_ZPC,END=7995); READ (_ZPC)
7982 PRINT @(25,22),K$,
7985 IF K$(1,4)+K$(30,6)<>A$(1,10) THEN GOTO 7980 ELSE K1$=K$
7990 CALL "ZZPROM",".4",X3$,0,"Profile now positioned to this record","","",0; GOTO 1820
7995 CALL "ZZPROM",".4",X3$,0,"This Contact is not in the Profile - Try ano"+"ther","","",0; GOTO 1820
8005 PRINT 'SB',
8010 CALL "ZZDISP","-","0510","",X3$,"","",X0,L0,X4$
8011 REM CALL "ZZDISP","-","051016","",X3$,"","",X0,L0,X4$
8015 PRINT 'SF',
8020 RETURN 
8100 REM "SET PROFILE
8110 S9=-1; READ (_ZPC,KEY="",DOM=8111)
8120 F1$=KEY(_ZPC,END=8121); IF F1$>"" THEN S9=0
8130 SETERR 9000
8140 RETURN 
8150 REM "CHECK FOR VALID CUSTOMER
8160 IF POS(" "<>A$(363,10))>0 THEN DIM G$(599); FIND (Z[12],DOM=8161,KEY=A$(363,10))IOL=0410; GOTO 8200
8170 CALL "ZZPROM",".4",X3$,0,"Invalid Customer Account Code","","",0; GOTO 1820
8200 REM "SETUP RUN SEQUENCE
8202 Q$="AR2MAA"
8205 P$=Q$
8210 CLOSE (14); OPEN (14,ERR=8250)P$; CLOSE (14)
8215 U0$=X3$(1,6)+"PF"
8220 Q$="10"+A$(363,10)
8225 IF LEN(U1$)<5 THEN U1$="" ELSE IF U1$(1,6)=X0$ THEN U1$=""
8230 U1$=P$+Q$+X0$+STR(LEN(A1$):"00")+A1$+U1$
8235 WRITE (Z[13],KEY=U0$)U0$,U1$
8236 WRITE (Z[13],KEY=FID(0)+X0$)A1$,S9,K1$
8240 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
8245 SETERR 8250; RUN P$
8250 SETERR 8260
8260 RETURN 
8300 REM "Sales History - customer
8315 Q$=A$(363,10)
8320 CALL "FM2IAL",X3$,X4$,"",Q$
8360 GOSUB 6400
8390 GOTO 1800
8400 REMOTE_TM:REM "Call RT2WOR to post to remote T/M based on paramters"+      "RT_FILE$ is file name, RT_COMM$ is command (Update or Delete) and "+      "RT_KEY$ is key value"
8403 IF SP_CODE$="" THEN SP_CODE$=A$(359,4) END_IF ; IF SP_CODE$=A$(359,4) THEN CALL "RT2LST",X3$,X4$,W9$,A$(1,4),SP_CODE$,SEND_TO$ ELSE CALL "RT2LST",X3$,X4$,W9$,A$(1,4),SP_CODE$,SEND_TO$; TEMP$=SEND_TO$; SP_CODE$=A$(359,4); CALL "RT2LST",X3$,X4$,W9$,A$(1,4),SP_CODE$,SEND_TO$; SEND_TO$=TEMP$+SEND_TO$
8404 IF PREV_SP_CODE$<>A$(359,4) OR PREV_CUST_CODE$<>A$(363,10) THEN RESEND=1 ELSE RESEND=0; REM "If salesperson or customer changed then resend all of the data for this contact and customer
8405 IF SEND_TO$="" THEN GOTO 8440
8410 FOR INDEX=1 TO LEN(SEND_TO$)-3 STEP 4
8420 CALL "RT2WOR",ERR=8421,X3$,X4$,SEND_TO$(INDEX,4),RT_FILE$,RT_COMM$,RT_KEY$,A$,1,A{ALL}
8425 IF RESEND THEN WRITE (Z[15],KEY=SEND_TO$(INDEX,4)+A$(1,4)+"ZP4"+$7E7E7E$+A$(1,10)); REM "To resend write to RT7 with filename of ZP4 + 3 tildes
8430 NEXT INDEX
8440 SEND_TO$="",SP_CODE$=""; REM "Reset flags
8445 REMOTE_TM_END:RETURN 
8550 CHECK_CONTACT_RANGE:REM "If Remote T/M then if this is the remote the"+      "n check the RT6 record to see if the contact #'s have a range, and if"+      " the contact is out of range, then prompt and disallow
8555 IF W9$="" THEN RANGE_OK$="Y"; GOTO CHECK_CONTACT_RANGE_END ELSE IF W9$(7,1)="H" THEN RANGE_OK$="Y"; GOTO CHECK_CONTACT_RANGE_END ELSE RANGE_OK$="N"
8560 READ (Z[14],KEY=A$(1,4)+W9$(8,4),DOM=8570)RT6$
8565 IF STP(RT6$(11,12),3," ")="" OR (A$(5,6)>=RT6$(11,6) AND A$(5,6)<=RT6$(17,6)) THEN RANGE_OK$="Y"
8580 IF RANGE_OK$<>"Y" THEN TEMP$="The contact number is restricted to "+STP(RT6$(11,6),2)+"-"+STP(RT6$(17,6),2)+" for this list"; CALL "ZZPROM",".4",X3$,Z,TEMP$,"","",0
8595 CHECK_CONTACT_RANGE_END:RETURN 
8700 REM "Display profile
8705 IF S9=-2 THEN PRINT @(63,3),'CL',
8710 X$="Profile "
8720 IF S9<=0 THEN X$=X$+"Off" ELSE IF S9=1 THEN X$=X$+"On" ELSE X$=X$+"N/A"
8730 PRINT @(63,3),'CL','SB',"**",X$,"**",'SF',; X$=""
8745 RETURN 
8810 REM "FIND LIST PROFILE...
8815 GOSUB 8100
8822 CALL "ZZPROM","X0ZP2MEA",X3$,S3,"","","",0; ON S3 GOTO 8840,8823,8846,9900
8830 IF S9=-1 THEN CALL "ZZDAPP",X3$,"DZZFLESZZCOMPZZDISP"; CALL "ZP2ZAA"; CALL "ZZDAPP",X3$,"AALL"; GOSUB 6400
8835 GOSUB 8100; IF S9=0 THEN S9=1
8840 GOSUB 8700
8845 RETURN 
8846 S9=-1; GOTO 8830
8850 REM "CHECK CRITERIA
8860 K1$=KEY(_ZPC,END=8890); READ (_ZPC)
8870 READ (Z[1],DOM=8890,KEY=K1$(1,4)+K1$(30,6))IOL=0310
8880 RETURN 
8890 PRINT 'RB'; READ (_ZPC,KEY="",DOM=8891)
8895 EXITTO 1000
8950 REM "Program flow logic
8960 DIM U0$(6); U0$(1)=FID(0); U0$=U0$+"PF"
8970 CLOSE (14); OPEN (14)"ZZPARM"; FIND (14,KEY=U0$,DOM=8990)U0$,U1$
8975 IF U1$="" THEN RETURN ELSE IF U1$(1,6)<>X0$ THEN RETURN 
8980 Q1=NUM(U1$(7,2)),Q1$=U1$(9,Q1),U1$=U1$(Q1+9)
8985 IF Q1<10 THEN Q1$=""; GOTO 8975
8988 REMOVE (14,KEY=U0$,DOM=8990)
8990 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9120 ON Y8 GOTO 9900,0990,9920
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9725 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9730 RUN U1$(1,6)
9740 RETURN 
9750 REM "FILES
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9790 RETURN 
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9905 Y8=2
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
10000 VALIDATE_REP:! SSP#212992
10010 VAL_REP=0,CUST$=G$
10020 IF CUST$(278,4)<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,X3$,CUST$(278,4),OK; IF OK THEN VAL_REP=1; GOTO *RETURN
10030 IF CUST$(282,4)<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,X3$,CUST$(282,4),OK; IF OK THEN VAL_REP=1; GOTO *RETURN
10040 IF CUST$(286,4)<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,X3$,CUST$(286,4),OK; IF OK THEN VAL_REP=1; GOTO *RETURN
10050 IF CUST$(290,4)<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,X3$,CUST$(290,4),OK; IF OK THEN VAL_REP=1; GOTO *RETURN
10060 IF CUST$(294,4)<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,X3$,CUST$(294,4),OK; IF OK THEN VAL_REP=0; GOTO *RETURN
10080 IF CUST$(278,20)=DIM(20) THEN VAL_REP=1
10090 RETURN 
56000 ! 232017-Contact email address is 30 characters in CHUI but 60 in GUI
