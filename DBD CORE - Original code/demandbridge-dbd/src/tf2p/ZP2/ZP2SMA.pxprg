0010 REM "Telemarketing Letter Setup <ZP2SMA>
0020 SETESC 9300; SETERR 9000
0035 REM "4.4 - 03/10/00 - 16.99 - plh - SSP# 124387
0040 REM "Copyright 2000 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 ON L0 GOTO 0100,6700,6500,7100; REM "<=0 - CONTINUE, 1-ADD ,2-DELETE, 3-RE DISPLAY SCREEN
0100 CLEAR ; SETERR 9000
0110 X0$="ZP2SMA",X1$="Telemarketing Letter Setup",K9$="",K9=0
0120 DIM Z0$(80,"-"),S0$(220)
0130 K0=15,K1=3
0135 C9=-1
0140 V8$="0006101599"
0200 REM "CLEAR SCREEN AND DISPLAY HEADER
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0250 IOLIST E$,E[0]
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1),A[0],A[1]
0320 IOLIST B$,B[0],B[1],B[2]
0330 IOLIST C$
0340 IOLIST D$
0350 IOLIST E$,E[0]
0370 IOLIST G$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X ZP8...  02O ZP9...  03O ZP4...  04O ZP0...  05O ZY3...  06O ZY4...  07O ZP7... 08O ZP5...  09O ZPB...  11O ZZS...  13O ZZPARM  "
0520 GOSUB 9750; ON Z0 GOTO 0521,9900
0530 F=Z[2]; DIM Y[6]
0540 REM "CHECK IF FAX IS AN OPTION, SET C9$ FLAG
0550 READ (Z[13],KEY=X3$(9,3)+"FX2",DOM=0551)C9$
0560 REM "CHECK IF WORD PROCESSOR IS AN OPTION, SET W$ AS FLAG"
0570 COKEY$=X3$(9,3)+"WORDLINK",W$="N"
0580 READ (Z[13],KEY=COKEY$,DOM=0600)WL$
0590 W$="Y"
0600 REM "DISPLAY BACKGROUND AND GET ZZPARMS"
0620 GOSUB 6000
0720 DIM S9$(255); FIND (Z[13],KEY="osec"+X3$(40,3),DOM=0721)S9$
0900 N0=0
0910 READ (Z[1],KEY="",DOM=0911)N0
1000 REM "START MAIN PROGRAM LOGIC, INITIALIZE VARS"
1005 IF E9>=1 THEN GOTO 9900
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(227),B[2]
1035 DIM A$(350),A[1],E[1]
1036 A$(198,6)=X3$(21,6),A$(5,1)="N",A$(231,2)="65"; A$(204,3)="L"
1037 IF X3$(9,3)="100" THEN A$(205,1)="M"
1045 A$(1,LEN(A1$))=A1$; IF A1$="" THEN A$(1,LEN(K9$))=K9$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450
1070 IF C9>-1 THEN GOSUB 6000
1080 IF E9=0 THEN FIND (Z[13],KEY=FID(0)+"ZP2MEA",DOM=1090)E$; A$(6,10)=E$(1,10),E9=1; GOTO 1100
1090 E9=-1
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1500
1200 REM "Memo no.
1210 IF N0=9999 THEN N0=1 ELSE A$(1,4)=STR(N0+1:"0000"); REM "auto increment to next number
1215 FIND (Z[1],KEY=A$(1,4),DOM=1216); N0=N0+1; GOTO 1210; REM "check for an existing entry at this number
1220 REM IF E9=1 THEN GOTO 1250
1240 CALL "ZZENTR","ZUX",A{ALL},A$,X4$,X3$,11,3,1,4,C0,"","{2"+X$,"","ZP2SMA00","ZP8","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1240,1241
1250 READ (Z[1],KEY=A$(1,4),DOM=1251)
1260 Q$=A$(1,4)
1265 REM "ON CO=0-REWRITE KEYS, 1-NEXT ENTRY, 2-EXIT, 3-RUN NOTES OVERLAY
1270 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1271,6800,9800,6970
1280 GOSUB 6200
1290 GOTO 1120
1500 REM "READ TM LETTER FILE - ZP8"
1505 Z9=-1,A2$="",A3$="",A4$="",A5$=""
1510 A1$=A$(1,4)
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1522 C9=1,E9=2
1525 Z9=0,A0=A[0]
1590 GOTO 1700
1600 REM "PROCESS UNSUCESSFUL READ OF TM LETTER FILE"
1605 C8=1
1610 IF E9=1 THEN GOTO 1650
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630
1630 GOTO 1000
1680 GOSUB 7450
1690 GOTO 2000
1700 REM "DISPLAY SCREEN DATA"
1790 GOSUB 6200
1800 REM "DISPLAY AND PROCESS SCREEN PROMPTS: 0-MODIFY, 1-DELETE, 2-CONTINUE, 3-IMMEDIATE PRINT"
1820 CALL "ZZPROM","6 ZP2SMA",X3$,Z,"","","V",X0; ON Z+1 GOTO 2040,1830,6500,7000,1821
1825 IF Z=3 THEN I9=1,Z=2; IF W$="Y" AND POS("W"=A$(204,3))>0 THEN GOTO 7500 ELSE GOTO 7000; REM "BRANCH TO NOTES OVERLAY"
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 GOSUB 7400
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",Q$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF INT(C0)=3 THEN GOSUB 7300 ELSE IF C0>3 OR X0<0 THEN GOTO 1800; REM "IF ARROW KEYS USED, DO NAVIGATION SUBROUTINE ESLE GOTO PROMPT"
2030 REM "ON XO= 0-WIDTH, 1-RIGHT JUSTIFY, 2-DATE, 3-LISTCODE, 4-CONTACT CODE, 5-FROM SALESPERSON, 6-ATTEN, 7-GREETING, 8-LOCATION, 9-ADDRESS INFO, 10-CORRESPONDANCE TYPE, 11-LITERATURE CODE, 12-LETTER CODE,13-LETTER TYPE CODE, 14-CUSTOMIZE, 15-SUBJECT, 16-DISPLAY PROMPTS"
2040 ON X0 GOTO 2100,2140,2170,2200,2250,3000,2450,2500,2600,2700,2300,2350,2800,2850,2900,3050,1800
2100 REM "Width
2105 IF C0<>2 THEN GOTO 2130
2110 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,28,3,231,2,C0,"","{3"+X$,"","ZP2SMA02","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2110,2111
2120 IF NUM(A$(231,2))>75 THEN GOTO 2110
2130 GOTO 2020
2140 REM "Right justify?
2145 IF C0<>2 THEN GOTO 2160
2150 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,51,3,5+K9,1,C0,"","{3"+X$,"","ZP2SMA04","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2160,2161
2160 GOTO 2020
2170 REM "Date
2175 IF C0<>2 THEN GOTO 2190
2180 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,63,3+V0,198+K9,6,C0,"","{3"+X$,"","ZP2SMA30","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2180,2181
2190 GOTO 2020
2200 REM "LIST CODE"
2205 IF E9=1 THEN C0=0; GOTO 2220
2210 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,12,5,6,4,C0,"","{3"+X$,"","ZP2SMA32","ZP0","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2210,2211
2215 IF POS(" "<>S9$(8,24))>0 AND POS(A$(6,4)=S9$(8,24),4)=0 AND ABS(C0)<>4 THEN GOTO 2210
2220 IF ABS(C0)<>2 OR POS(" "<>A$(6,4))>0 THEN FIND (Z[4],KEY=A$(6,4),DOM=2210)IOL=0340
2245 GOTO 2020
2250 REM "CONTACT CODE"
2252 IF E9=1 THEN J$="",E9=2; GOTO 2270
2255 J$=A$(6,10)
2260 Q$=A$(6,4); CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,35,5,10,6,C0,"","{3"+X$,Q$,"ZP2SMA34","ZP4","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2260,2261
2270 IF ABS(C0)<>2 OR POS(" "<>A$(10,6))>0 THEN FIND (Z[3],KEY=A$(6,10),DOM=2260)IOL=0330
2275 IF J$=A$(6,10) THEN GOTO 2290
2280 A$(91,20)=C$(180,20),A$(16,40)=C$(11,40),A$(111,87)=C$(51,87),A$(233,4)=C$(359,4),A$(56,28)=C$(391,28); IF POS(" "<>C$(373,18))>0 THEN A$(311,40)=FNS$(C$(160,20))+", "+FNS$(C$(373,18)) ELSE A$(311,40)=C$(160,20)
2285 A$(297,14)=C$(138,12)
2288 GOSUB 6200
2290 GOTO 2020
2300 REM "Correspondence type"
2305 X$="{3Any 3 of L=letter, M=mailing label"; IF C9$>"" THEN X$=X$+", F=Fax"
2306 IF W$="Y" THEN X$=X$+", W=MS Word"
2308 PRINT @(0,21),'CE',
2310 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,24,13,204,3,C0,"",X$,"","ZP2SMA36","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2310,2311
2320 FOR X=0 TO 2; IF POS(A$(204+X,1)=" FMLW")=0 THEN EXITTO 2310 ELSE NEXT X
2321 REM IF FAX SELECTED AND IT IS NOT SET UP, REPEAT PROMPT
2322 IF C9$="" THEN IF POS("F"=A$(204,3))>0 THEN GOTO 2310
2323 REM PREVENT COMBINING SELECTIONS OF WORDLINK WITH LETTER OR FAX BECAUSE THE LETTERS CAN BE DIFFERENT
2324 IF (POS("W"=A$(204,3))>0 AND POS("L"=A$(204,3))>0) OR (POS("W"=A$(204,3))>0 AND POS("F"=A$(204,3))>0) THEN GOTO 2310
2325 REM IF W$="Y" AND POS("W"=A$(204,3))>0 THEN GOSUB 07500 ELSE IF W$="N" AND POS("W"=A$(204,3))>0 THEN GOTO 02310
2330 IF POS("L"=A$(204,3))=0 AND POS("F"=A$(204,3))=0 THEN A$(217,14)=""; GOSUB 6288; GOTO 2020
2340 GOTO 2020
2350 REM "Literature code
2360 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,24,14,207,10,C0,"","{3"+X$,"","ZP2SMA38","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2360,2361
2390 GOTO 2020
2450 REM "Attn
2460 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,9,6+V0,311,40,C0,"","{3"+X$,"","ZP2SMA12","","",""
2490 GOTO 2020
2500 REM "Greeting
2510 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,57,6+V0,91,20,C0,"","{3"+X$,"","ZP2SMA32","","",""
2550 GOTO 2020
2600 REM "Location description
2610 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,9,7+V0,16,40,C0,"","{3"+X$,"","ZP2SMA34","","",""
2650 GOTO 2020
2700 REM "Address info
2710 A6$=A$(111+K9,87)+A$(297,14)+S0$(1,80)+A$(56,28)
2720 CALL "ZY2AAA",X3$,X4$,0,8+V0,"#C","",2,A6$,"",0,C0,C3
2730 IF ABS(C0)>4 THEN A$(111+K9,87)=A6$; GOSUB 6400; GOTO 2720
2735 A$(111+K9,87)=A6$(1,87),A$(297,14)=A6$(88,14),A$(56,28)=A6$(182,28)
2740 GOTO 2020
2800 REM "LETTER CODE"
2805 IF POS("L"=A$(204,3))=0 AND POS("F"=A$(204,3))=0 AND POS("W"=A$(204,3))=0 THEN A$(217,12)=""; GOTO 2020
2808 X$="Letter codes must start with TM"
2810 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,24,15,217,12,C0,"","{3"+X$,"","ZP2SMA40","ZY3","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2810,2811
2820 IF POS(" "<>A$(217,12))>0 AND A$(217,2)<>"TM" THEN GOTO 2808
2840 GOTO 2020
2850 REM "LETTER TYPE CODE"
2855 IF POS(" "<>A$(217,12))=0 THEN A$(229,1)=""; GOTO 2020
2860 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,24,16,229,1,C0,"","{3"+X$,"","ZP2SMA42","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2860,2861
2870 DIM E[1]; FIND (Z[5],KEY=A$(217,13)+"1",DOM=2885)IOL=0350; PRINT "   ",E$(15,35),
2875 IF E$(56,2)>A$(231,2) THEN A$(231,2)=E$(56,2); PRINT @(28,3),A$(231,2),
2880 GOTO 2020
2885 A$(217,13)="",C0=2; GOSUB 6288
2890 GOTO 2020
2900 REM "CUSTOMIZE?
2905 IF POS(" "<>A$(217,12))=0 THEN IF POS("L"=A$(204,3))=0 THEN A$(230,1)=""; GOSUB 6288; GOTO 2020 ELSE A$(230,1)="Y"; GOSUB 6288; GOTO 2020
2910 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,24,17,230,1,C0,"","{3"+X$,"","ZP2SMA44","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2910,2911
2940 GOTO 2020
3000 REM "From Salesperson
3010 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,57,5,233,4,C0,"","{3"+X$,Q$,"ZP2SMA35","ZP7","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3010,3011
3020 DIM G$(44); IF POS(" "<>A$(233,4))>0 THEN FIND (Z[7],KEY=A$(233,4),DOM=3010)IOL=0370
3030 PRINT @(20,22),G$(1,4),"=",G$(5,40),
3040 GOTO 2020
3050 REM "Subject
3060 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,15,18,237,60,C0,"","{3"+X$,"","ZP2SMA68","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3060,3061
3090 GOTO 2020
5400 REM "UPDATE LETTER
5440 DIM B$(291)
5445 B$(288,4)=STR(INT(TIM*100):"0000")
5446 X$="Letter"; IF POS("F"=A$(204,3))>0 THEN X$=X$+"/Fax"; IF POS("L"=A$(204,3))=0 THEN X$="Fax"
5450 B$(1,17)=A$(6,10)+A$(198,6)+"0",B$(18,41)="L"+X$+" to: "+A$(311,40)
5455 X$=""; IF A$(230,1)="Y" THEN X$="Custom Letter"
5460 IF POS(" "<>A$(217,12))>0 THEN X$=X$+" Code: "+FNS$(A$(217,12))+"-"+A$(229,1)
5465 IF X$(1,2)=" C" THEN X$="Letter"+X$
5470 IF POS(" "<>A$(207,10))>0 THEN X$=X$+", Literature: "+FNS$(A$(207,10))
5472 IF A$(233,4)<>"    " THEN X$="From: "+FNS$(A$(233,4))+", "+X$
5475 B$(59,60)=X$
5480 B$(119,60)=A$(237,60)
5485 B$(17,1)=CHR(ASC(B$(17,1))+1)
5490 WRITE (Z[8],DOM=5485,KEY=B$(1,17))B$; REM "T/M NOTES FILE
5500 CALL "ZZDATE",X3$,"","G10101",B$(11,6),D0,1,0,0,0,0,0,"","",""; A2$=B$(1,10)+STR(100000-D0:"00000")+B$(11,7)
5510 WRITE (Z[9],KEY=A2$); REM "T/M REVERSE DATE NOTE SORT
5520 EXTRACT (Z[3],DOM=5541,KEY=B$(1,10))IOL=0330; REM T/M CONTACT MASTER FILE
5530 IF B$(11,6)>C$(264,6) THEN C$(264,47)=B$(11,6)+B$(18,41)
5540 WRITE (Z[3],KEY=B$(1,10))IOL=0330
5550 IF A$(204,3)="   " THEN REMOVE (Z[1],KEY=A1$); EXITTO 9900
5555 WRITE (Z[1],KEY=A1$)IOL=0310
5590 RETURN 
5900 REM "REMOVE LINES F = Z[2] = ZP9= T/M LETTER LINES
5910 IF A0=0 THEN GOTO 5990
5920 EXTRACT (F,IND=0,ERR=5960)IOL=0290
5925 IF Y[4]=-2 THEN READ (F,IND=0); WAIT 0; GOTO 5920 ELSE Y[4]=-2; WRITE (F,IND=0)IOL=0290
5930 A1=Y[1],Y[1]=A0,Y[0]=Y[0]-1
5935 READ (F,IND=A0)A; IF A>0 THEN Y[0]=Y[0]-1,A0=A; GOTO 5935
5940 WRITE (F,IND=A0)A1
5950 Y[4]=-1; WRITE (F,IND=0)IOL=0290
5955 GOTO 5990
5960 IF ERR=0 THEN RETRY ELSE GOTO 9000
5990 RETURN 
6000 REM "BACKGROUND
6003 PRINT @(0,6+V0),'CL',@(0,8+V0),'CL',@(0,10+V0),'CL'
6005 PRINT (0,ERR=6016)'SB',
6010 PRINT @(0,3+V0),"Letter No:",@(20,3+V0),"Width:",@(34,3+V0),"Right justify?:",@(56,3+V0),"Date:",@(0,4+V0),Z0$,@(0,5+V0),"List type:",@(20),"Contact code:",@(50),"From:",@(0,7+V0),"Company:",@(3,6+V0),"Attn:",@(50,6+V0),"Dear:"
6080 CALL "ZY2AAA",X3$,X4$,0,8+V0,"#C","",0,"","",0,0,0
6085 PRINT @(0,13),"Correspondence Type(s):",@(4,14),"Literature Code(s):",@(11,15),"Letter code:",@(7,16),"Letter sub-code:",@(5,17),"Customize letter?:",@(6,18),"Subject:",
6090 PRINT @(0,12),Z0$
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6225 PRINT @(28,3),A$(231,2),@(51,3),A$(5,1)
6230 CALL "ZZDISP","D ",A$(198+K9,6),"",X3$,"","",63,3+V0,X4$
6235 PRINT @(12,5),A$(6,4),@(35,5),A$(10,6),@(57,5),A$(233,4),
6270 PRINT @(9,7+V0),A$(16,40),@(9,6+V0),A$(311,40),@(57,6+V0),A$(91+K9,20),
6280 CALL "ZY2AAA",X3$,X4$,0,8+V0,"#C","",1,A$(111+K9,87)+A$(297,14)+S0$(1,80)+A$(56,28),"",0,0,0
6286 PRINT @(24,13),A$(204,3),@(24,14),A$(207,10),
6288 PRINT @(24,15),A$(217,12),@(24,16),A$(229,1),'CL',@(24,17),A$(230,1),@(15,18),A$(237,60),
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6490
6460 PRINT @(11,3+V0),A$(1,4),
6470 RETURN 
6490 RETURN 
6500 REM "DELETE
6505 GOSUB 5900
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6520 A1$=""
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY, IF FAX,WORD OR IMMEDIATE PRINT THEN GO TO IMMEDIATE PRINT
6705 A[0]=A0
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6715 IF POS("F"=A$(204,3))>0 OR POS("W"=A$(204,3))>0 OR I9=1 THEN GOTO 7500
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6825 A$(1,LEN(Z1$))=A1$
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM "PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
6895 PRINT 'RB'; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
7000 REM "Run overlay
7010 IF POS(" "<>A$(217,14))=0 THEN A$(230,1)="Y"
7020 IF A$(230,1)<>"Y" THEN A0=A[0]; GOSUB 5900; A0=0; GOTO 6700
7050 PRINT @(0,5),'CE',A$(16,40),"  ",A$(56,35),@(0,6),Z0$,@(0,8),Z0$,@(0,9),"Line",@(8),"Text",@(0,10),Z0$
7070 L0=1,F=Z[2],A0=A[0]
7080 RUN "ZP2SMB"
7100 REM "RE-D0 SCREEN AND RUN 2ND OVERLAY
7110 GOSUB 6400
7190 GOTO 7020
7200 REM "Forced read
7210 IF ERR<>0 THEN GOTO 9000
7220 CALL "ZZREAD",Z[1],A1$,A$,A{ALL},S8; ON S8 GOTO 7221,1630
7230 GOSUB 6200
7240 CALL "ZZPROM","E0",X3$,0,"","","",0; GOTO 7290
7250 CALL "ZZPROM","E0A",X3$,0,"","","",0; GOTO 7290
7290 GOTO 1630
7300 REM "NAVIGATION FOR ARROW KEYS
7310 ON FPT(C0)*10 GOTO 7390,7325,7330,7390
7325 IF V8$="" THEN X0=X0+4; GOTO 7390 ELSE X0=NUM(V8$(POS(STR(X0:"00")<=V8$,2),2)); GOTO 7390
7330 IF V8$="" THEN X0=X0-6; GOTO 7335 ELSE X0=POS(STR(X0-1:"00")<=V8$,2)-2; IF X0<0 THEN X0=0; GOTO 7390 ELSE X0=NUM(V8$(X0,2)); GOTO 7390
7335 IF X0<0 THEN X0=0; GOTO 7390
7390 C0=3; RETURN 
7400 REM "EXTRACT
7410 EXTRACT (Z[1],ERR=7200,KEY=A1$,DOM=7440)IOL=0310
7420 C9=1
7440 RETURN 
7450 REM "EXTRACT NEW ENTRY
7453 N0=NUM(A1$(1,4))
7455 WRITE (Z[1],KEY="")N0
7460 WRITE (Z[1],KEY=A1$,DOM=7250)IOL=0310; GOTO 7400
7490 RETURN 
7500 REM "PRINT LETTER IMMEDIATELY
7501 IF POS("F"=A$(204,3))=0 AND POS("L"=A$(204,3))=0 AND POS("W"=A$(204,3))=0 THEN CALL "ZZPROM",".4",X3$,0,"You have not specified fax or letter!","","",0; GOTO 1800
7502 GOSUB 7800; REM INIT R0$ & V0$
7503 REM IF POS("F"=A$(204,3))>0 THEN IF A$(297,10)=S0$(1,10) THEN CALL "ZZPROM",".4",X3$,0,"The fax number has not been specified!","","",0; GOTO 01800
7504 IF POS("F"=A$(204,3))>0 THEN CALL "ZZPROM","Y",X3$,S3,"Do you want a hard copy of the fax?","","",0; ON S3 GOTO 7505,7555
7510 P=POS("L"=A$(204,3)); IF P>0 THEN A$(204+P-1,1)=""
7520 IF (W$="Y" AND POS("W"=A$(204,3))>0) THEN GOSUB 7900 ELSE CALL "ZZRPTO",X3$,X4$,Y0$,R0$,R{ALL},3,C0,X0
7530 Z$="10O ZZP "; GOSUB 9750; ON Z0 GOTO 7531,1800
7540 WRITE (Z[10],KEY=X3$(1,8))X3$,X4$,"ZP8   ",R0$,A1$+A1$,V0$
7545 Z$="10C ZZP  "; GOSUB 9750
7550 CALL "ZP2RSA",X0$; I9=2
7555 IF POS("F"=A$(204,3))=0 THEN GOTO 7600 ELSE CALL "ZZPROM","Y",X3$,S3,"Are you ready to submit the letter as a fax?","","",0; ON S3 GOTO 7556,7680
7560 IF R0$="" THEN DIM R0$(60); R0$(1,1)="I",R0$(8,3)="NNN" END_IF ; R0$(2,6)="F1"; CALL "ZZRPTO",X3$,X4$,Y0$,R0$,R{ALL},3,C0,X0
7565 Z$="10O ZZP "; GOSUB 9750; ON Z0 GOTO 7566,1800
7570 WRITE (Z[10],KEY=X3$(1,8))X3$,X4$,"ZP8   ",R0$,A1$+A1$,V0$
7575 CALL "ZP2RSA",X0$; I9=2
7580 Z$="10C ZZP  "; GOSUB 9750
7600 REM "UPDATE LETTER'S TEXT
7605 IF I9<>2 OR (W$="Y" AND POS("W"=A$(204,3))>0) THEN GOTO 7680
7610 CALL "ZZPROM","Y",X3$,S3,"Do you wish to update this letter?","","",0; ON S3 GOTO 7611,7700
7620 GOSUB 5400; REM "GOTO UPDATE LETTER"
7680 GOSUB 6400; REM "REWRITE SCREEN DATA"
7690 GOTO 9900; REM "EXIT"
7720 GOSUB 6400
7740 GOTO 1800
7800 REM DECLARE AND INITIALIZE R0$ & V0$ AND LOOK IN ZZS FOR VALUES
7820 DIM R0$(30); R0$(1,7)="IU",R0$(8,9)="NNN    66"
7830 DIM V0$(181); V0$(1,6)="ZP2SRA",V0$(71,1)="1",V0$(79,1)="N"
7835 FIND (Z[11],DOM=7836,KEY="ZP2RSAIMMEDIATE   ")V0$,R0$
7845 RETURN 
7900 REM DECLARE R0$ FOR WORDLINK AND SAVE LETTER FILE
7910 DIM R0$(125); R0$(1,7)="IWL",R0$(8,9)="NNN",R0$(125,1)="N"
7930 WRITE (Z[1],KEY=A1$)IOL=0310
7950 RETURN 
8910 DEF FNS$(Z9$)=Z9$(1,POS(S0$=Z9$+S0$)-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9790 RETURN 
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 IF E9<1 THEN RUN "ZMENU" ELSE RUN "ZP2MEA"
9999 END 
