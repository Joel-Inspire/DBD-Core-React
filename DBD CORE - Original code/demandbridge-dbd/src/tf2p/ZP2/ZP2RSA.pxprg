0010 REM "Telemarketing Letter Printing  <ZP2RSA>
0035 REM "5.7 - 08/18/14 - 9.191388 - tma - SSP# 199697
0037 REM "199697-T/M T3.  Letter & Label Printing  missing in GUI            
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR EXCEPT LETTERVAR$; SETERR 0100; ENTER C8$
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 SETERR 9000; SETESC 9300
0110 X0$="ZP2RSA",X1$="Contact Management Letter Printing"
0120 DIM A$(204),A[1],I[19],S$(40),C[1]
0140 Z0$="##,###,###.00-"
0170 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0175 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0180 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
0190 DEF FNE$(Z9$)=STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0210 T=1,T0=1,T1=1
0220 WDX$=%WDX$; %WDX$="[WDX]"
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=*NEXT)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR AR1 - Customer Masterfile
0310 IOLIST A$,L0
0320 IOLIST I$,I[0],I[1],I[2],I[3],I[4],I[5],I[6],I[7],I[8],I[9],I[10],I[11],I[12],I[13],I[14],I[15],I[16],I[17],I[18],I[19]
0330 IOLIST C$,C[0],C[1]
0370 IOLIST G$
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$; REM "ZZP, V1$ = FILENAME, V2$=KEY RANGE,V3$ = R0$, V0$ REMAINDER SELECTION SCREEN"
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB READ_RPT_SELECTION_PARAMETERS; REM "READ ZZP & ZZPARM"
0510 Y$="01L ZP8...  02O ZP9...  03O ZY3...  04O ZY4...  06O ZY2...  07O ZP7...  08O ZP5...  09O ZPB...  10O ZP4...  15O ZP3...  "
0511 IF C8$<>"" THEN Y$(1,3)="01O"
0516 CALL "ZZ2FNC;OPENSORTUSERFILE","ZPC",_ZPC,USERNAME$
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0560 DIM O[1]; O[0]=NUM(V0$(59,2),ERR=*NEXT),O[1]=NUM(V0$(61,2),ERR=*NEXT)
0565 IF O[0]>0 THEN O[0]=O[0]-1 ELSE O[0]=7
0600 REM "CALL WG2CWT TO WRITE TEXT HEADER IF LINKING TO WORD PROCESSOR"
0605 IF V3$(2,2)="WL" THEN W$="Y" ELSE W$="N"
0610 IF W$<>"Y" THEN GOTO OPEN_PRINTER
0620 DIM TPLACEMENT[12,1],TDATA$(10)
0630 FILENAME$=FN%FID$(Z[1]); TFIELDS$="05060910111213141722232425" ! [205703]-changed FID() to FN%FID$()
0650 CALL "WG2CWT",X3$,X4$,"C",FILENAME$(4,3),TPLACEMENT{ALL},TDATA$,TFIELDS$,TFILE$
0660 GOTO 0800
0700 OPEN_PRINTER:REM if not linking to word processor"
0720 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0721,9920
0750 X$=FID(W9); IF POS("F"=X$(1,1))>0 THEN CALL "FX2SBC",X3$,Z[13],C9$,Q; ON Q GOTO 9900,0770 ELSE GOTO 0770
0755 CALL "ZZPROM",".4",X3$,0,"You are not configured for the Fax output option!","","",0; GOTO 9900
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[1]; U=4,U0=1,U[0]=0,U[1]=4,T0=0,U0$="    ",T5$="                "; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0900 REM " Position read"
0905 REM "Check and Set channel for key file to read from, v1$ = ZP8"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965; REM "Multiple Keys, skip building default range"
0934 REM "create default 'all' range keys for 1 to # keys, keys are in double format, i.e. lowerhigherlowerhigher - fills high range with tildes"
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key for 1 to # of keys"
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0984 REM "READ PRIOR KEY TO POSITION POINTER
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
0995 IF V0$(79,1)="Y" THEN READ (_ZPC,KEY="",DOM=0996)
1000 READ_KEY:REM "do T/M Profile subroutine if flagged"
1002 IF V0$(79,1)="Y" THEN GOTO USE_PROFILE
1005 U$=KEY(U1,END=END_OF_PRINT); READ (U1)
1010 U3=1,U2=1
1020 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1030 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN READ (U1,END=END_OF_PRINT); EXITTO READ_KEY
1040 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U=1 THEN EXITTO 5000 ELSE READ (U1,END=END_OF_PRINT); EXITTO READ_KEY
1050 NEXT U9
1100 REM "Get record
1110 U9$=U$
1120 REM "Read Letter Master File ZP8, But first save A$ if WordLink has passed back a template name.
1125 IF W$="Y" AND Q9=1 THEN WRITE (Z[1],KEY=U$)A$
1130 READ (Z[1],KEY=U$,DOM=READ_KEY)IOL=0310
1131 L9=L0
1134 REM "If mailing labels are requested and they have not been printed yet, then set F1$ to run mailing label printing.
1135 IF POS("M"=A$(204,3))>0 THEN F1$="LABELS"
1136 REM "Q9 flag for Update Notes 1 = true"
1137 IF Q9=1 AND A$(204,3)="   " AND A$(84,3)="   " THEN REMOVE (Z[1],KEY=U$,DOM=READ_KEY); GOTO READ_KEY
1140 IF POS("L"=A$(204,3))=0 AND POS("F"=A$(204,3))=0 AND POS("W"=A$(204,3))=0 THEN GOTO READ_KEY
1144 REM "IF Q91-NOTEFLAG DO UPDATE NOTEFILE SUBROUTINE, THEN IF ON THE REMOVE PASS, ALL OF THE CODES HAVE BEEN PRINTED, THEN REMOVE IT, UNLESS IT WAS A 'F'AXED ONE, THEN WE WILL KEEP IT BECAUSE WE WILL BE RETURNING TO THE IMMEDIATE PRINT?"
1145 IF Q9=1 THEN IF POS("L"=A$(84,3))=0 AND POS("F"=A$(84,3))=0 AND POS("W"=A$(84,3))=0 THEN GOTO READ_KEY ELSE GOSUB 5400; IF A$(84,3)<>A$(204,3) OR V0$(79,1)="Y" THEN GOTO READ_KEY ELSE REMOVE (Z[1],KEY=U$,DOM=READ_KEY); GOTO READ_KEY
1150 IF V0$(79,1)<>"Y" THEN IF POS(" "<>A$(56,28))=0 THEN A9$=A$(171,16) ELSE A9$=A$(56,28)
1160 IF MID(V0$,182,1)="Y" AND (POS("L"=A$(84,3))<>0 OR POS("F"=A$(84,3))<>0) THEN GOTO READ_KEY; REM "If print unprinted letters flag, skip the printed ones
1350 REM "Do Build Letter Text subroutine"
1355 IF W$="Y" THEN CALL "WG2CWT",X3$,X4$,"W",FILENAME$(4,3),TPLACEMENT{ALL},A$,TFIELDS$,TFILE$; GOSUB 2000; GOTO READ_KEY
1370 GOSUB BUILD_LETTER_SECTION
1380 P=POS("!S"=L8$); IF P=4 THEN P0=POS($7E$=L8$),COMMAND$=STP(L8$(6,P0-7),1); DIM DATA$[25]; DATA$[1]=A$(297,14); CALL "FX2SBF",X3$,X4$,COMMAND$,DATA${ALL},X9; GOTO 2000; REM "WO127774, fax file instead of letter
1400 REM "Declare String for File ZY2, Load Date info, Convert to date description"
1410 DIM D0$(40); READ (Z[6],KEY=A$(200,2),DOM=1411)D0$
1420 P0=POS(" "=D0$(3,15))
1425 T=NUM(A$(202,2))
1430 D0$=D0$(3,P0-1)+" "+STR(T)+", "+FNE$(A$(198,2))
1490 T=0
1500 REM "Print Header, Address, Salutation"
1503 GOSUB NEW_PAGE_HEADER; REM "Header"
1504 GOSUB 6200; REM "Co. Address or Blank lines"
1505 Y5$(P8+O[1])=D0$; GOSUB PRINT_LINE
1508 IF NUM(V0$(190,1))=0 THEN V0$(190,1)="4"
1510 FOR I=1 TO NUM(V0$(190,1)); GOSUB PRINT_LINE; NEXT I; REM "Insert 4 blank lines"
1520 P7=0
1521 GOSUB PRINT_NAME_ADDRESS
1530 GOSUB PRINT_LINE
1556 IF A$(91,20)=S$(1,20) THEN IF POS(X3$(9,3)="043073",3)>0 THEN GOTO 1565 ELSE Y5$(P8)="Sir/Madam:"; GOTO 1565
1557 P0=POS("   "=A$(91,20)+"   ")
1560 Y5$(P8)="Dear",Y5$(P8+5)=A$(91,P0-1)+":"
1565 GOSUB PRINT_LINE; GOSUB PRINT_LINE; REM "Print Salutation"
1600 LETTER_BODY_LOOP:
1610 P0=POS("~"=L8$)
1615 IF LEN(L8$)=0 THEN GOTO 1995
1620 IF W>W0-3 THEN GOSUB NEW_PAGE_HEADER
1635 P9=POS(" "=L8$(2)); IF P9=0 THEN GOTO 1650 ELSE P9=P9+2
1637 IF POS("^"=L8$(P9,P0-P9))>0 THEN {
1638 GOSUB CONVERT_TOKENS; REM "Insert MergeFields at Tokens"
1640  } ELSE {
1641 Y5$(P8)=L8$(P9,P0-P9)
1642  }
1650 GOSUB PRINT_LINE
1665 L1=0
1670 L8$=L8$(P0+1)
1680 IF L8$="" THEN GOTO ACCUMULATE_TOTALS
1690 GOTO 1610
1691 END_LETTER_BODY_LOOP:
1900 ACCUMULATE_TOTALS:
1910 X$=FID(W9); IF POS("F"=X$(1,1))>0 THEN PRINT (W9)DIM(512); T0$="TM "+A$(5,4)+"-"+A$(10,6),T1$=A$(16,14),X$=FID(W9); CALL "FX2SBB",X3$,W9,T0$,T1$,"",T2$,"",X9; OPEN (W9)X$
1990 T[T0,0]=T[T0,0]+1
1995 GOSUB PRINT_LITERATURE_CODES
2000 UPDATE_PRINTED_FLAGS:
2005 IF V0$(79,1)<>"Y" THEN {
2010 P=POS("L"=A$(204,3)); IF P>0 THEN A$(84+P-1,1)="L"
2011 P=POS("W"=A$(204,3)); IF P>0 THEN A$(84+P-1,1)="W"
2015 P=POS("F"=A$(204,3)); IF P>0 THEN A$(84+P-1,1)="F"
2018 L0=L9
2020 WRITE (Z[1],KEY=U$)IOL=0310
2030  }
2045 IF W$="Y" THEN RETURN ELSE GOTO READ_KEY
3100 REM "Subtotal logic goes here, see -ZREPS
3180 RETURN 
5000 END_OF_PRINT:
5001 WAIT 1
5010 IF Q9=1 THEN GOSUB 5700; GOTO 5210
5015 IF FID(0)<>FID(W9) AND W$<>"Y" THEN PRINTER$=FID(W9); CLOSE (W9); OPEN (W9)PRINTER$
5020 IF W$="Y" THEN GOSUB CALL_WORDLINK; REM "FILE PRINTED, BRANCH TO WORDLINK"
5021 IF C8$="ZP2SMA" THEN GOTO 9900
5040 CALL "ZZPROM","Y",X3$,S3,"Do you wish to update the Contact Management Letter files?","","",0 ! ssp#273159
5050 ON S3 GOTO 5060,9916
5200 Q9=1; GOTO 0990
5210 IF F1$="LABELS" THEN Y4$="ZP2RMS"
5290 GOTO 9916
5400 REM "Update note file"
5405 IF W$="Y" OR V0$(79,1)="N" THEN CONTACT_TYPE$="L" ELSE CONTACT_TYPE$=V0$(183,1)
5410 DIM FOLLOW_UP$(47),ACTION_CODE$(47),ACTION_CODE1$(47); FIND (Z[15],KEY=CONTACT_TYPE$,DOM=5411)ACTION_CODE$; IF W$<>"Y" AND V0$(79,1)="Y" THEN FOLLOW_UP$=V0$(184,6)+V0$(183,1)+FOLLOW_UP$(2,40) ELSE IF W$="    Y" THEN FOLLOW_UP$="MSWORDL"+FOLLOW_UP$(2,40)
5411 IF V0$(79,1)="Y" OR W$="Y" THEN GOTO 5425
5412 IF NUM(ACTION_CODE$(43,3))<>0 THEN CALL "ZZDATE",X3$,"",A$(198,6),Q$,NUM(ACTION_CODE$(43,3)),0,0,0,0,0,0,"","",""; FIND (Z[15],KEY=ACTION_CODE$(42,1),DOM=5414)ACTION_CODE1$
5414 FOLLOW_UP$=Q$+ACTION_CODE$(42,1)+ACTION_CODE1$(2,40)
5425 IF W$<>"Y" OR POS("W"=A$(204,3))=0 THEN P=POS("L"=A$(204,3)) ELSE P=POS("W"=A$(204,3)); IF P=0 THEN GOTO 5590 ELSE IF POS("F"=A$(204,3))>0 THEN GOTO 5590
5435 PRINT @(20,22),"Updating: ",A$(1,4),"-",A$(5,10),
5440 DIM B$(291)
5445 B$(288,4)=STR(INT(TIM*100):"0000")
5450 B$(1,17)=A$(6,10)+A$(198,6)+"0",B$(18,41)="LLetter to: "+A$(311,40)
5455 X$=""; IF A$(230,1)="Y" THEN X$="Custom Letter"
5460 IF POS(" "<>A$(217,12))>0 THEN X$=X$+" Code: "+FNS$(A$(217,12))+"-"+A$(229,1)
5465 IF X$(1,2)=" C" THEN X$="Letter"+X$
5470 IF POS(" "<>A$(207,10))>0 THEN X$=X$+", Literature: "+FNS$(A$(207,10))
5472 IF A$(233,4)<>"    " THEN X$="From: "+FNS$(A$(233,4))+", "+X$
5475 B$(59,60)=X$
5480 B$(119,60)=A$(237,60)
5485 B$(17,1)=CHR(ASC(B$(17,1))+1)
5490 WRITE (Z[8],DOM=5485,KEY=B$(1,17))B$
5500 CALL "ZZDATE",X3$,"","G10101",B$(11,6),D0,1,0,0,0,0,0,"","",""; A2$=B$(1,10)+STR(100000-D0:"00000")+B$(11,7)
5510 WRITE (Z[9],KEY=A2$)
5520 EXTRACT (Z[10],DOM=5541,KEY=B$(1,10))IOL=0330
5525 IF B$(11,6)>C$(264,6) THEN C$(264,47)=B$(11,6)+B$(18,41)
5535 IF POS(" "<>V0$(184,6))>0 THEN C$(311,47)=FOLLOW_UP$
5540 WRITE (Z[10],KEY=B$(1,10))IOL=0330
5550 IF V0$(79,1)<>"Y" THEN WRITE (Z[1],KEY=A$(1,4))IOL=0310
5590 RETURN 
5700 REM "If file is empty except for the "" record, then reinit all letter files
5705 READ (Z[1],KEY="",DOM=5795); REM "If no "" record, then we are done
5710 K$=KEY(Z[1],END=5711); GOTO 5795; REM "if other records exist, don't fix the "" record, or clear the files, just leave
5712 REM "If here, then only the "" record exists, there are no letters left, init all files
5715 CALL "ZZINIT",STR(Z[1]:"00")+STR(Z[2]:"00")
5795 RETURN 
6000 NEW_PAGE_HEADER:
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,"X",W5$
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6090 RETURN 
6100 REM "
6199 RETURN 
6200 REM "COMPANY ADDRESS (FAX ONLY) OR TOP BLANK LINES
6205 X$=FID(W9); IF POS("F"=X$(1,1))<>1 THEN GOTO 6300
6215 CALL "FX2SBA",X3$,X4$,A$(297,14),C9$,N9$,X9; IF X9=1 THEN EXITTO 9900
6218 PRINT (W9)N9$
6220 FOR X=0 TO 5
6230 Q$=FNS$(C9$(23+X*78,78)); IF POS("//"=Q$)<>1 THEN Y5$(INT(W3/2-LEN(Q$)/2))=Q$ ELSE Y5$(1)=Q$
6235 GOSUB PRINT_LINE
6260 NEXT X
6290 RETURN 
6300 REM "NOT FAX
6310 IF O[0]=0 THEN GOTO 6340
6320 FOR I=1 TO O[0]; GOSUB PRINT_LINE; NEXT I
6340 RETURN 
6900 REM "SORT
6905 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6940 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,X9
6990 RETURN 
7000 REM "Totals logic
7010 GOTO 7130
7020 IF T=T0 THEN IF T[T0,0]<2 THEN GOSUB PRINT_LINE; GOTO 7080
7030 IF W+3>W0 THEN GOSUB NEW_PAGE_HEADER
7040 Y5$(1)=T3$(1,T0+1-T)+" "
7050 IF T$<>"" THEN Y5$(T0+2-T)=T$+" "+Q2$+" ("+STR(T[T,0]:"0")+"):"
7060 IF T0$="END" THEN IF T=0 THEN GOSUB 7140; GOTO 7130
7070 GOSUB PRINT_LINE
7080 REM "Accumulate sub totals
7090 IF T=0 THEN GOTO 7130
7100 FOR X=0 TO T1
7110 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7120 NEXT X
7130 RETURN 
7140 REM "Elapsed time routine"
7150 GOSUB PRINT_LINE; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB PRINT_LINE
7160 PRECISION 14; T2=TIM-T2
7170 T4$=STR(INT(T2):"##0")
7180 T3=INT(FPT(T2)*3600),T4=MOD(T3,60),T3=INT(T3/60)
7190 T3=T3/60
7200 T4$=T4$+":"+STR(T3:"00")+":"+STR(T4:"00")
7210 Y5$(1)=Q3$+": "+T4$; GOSUB PRINT_LINE
7220 PRECISION 2
7230 RETURN 
7240 PRINT_LINE:REM "Output line Y5$ to output device
7250 W=W+1
7260 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7270 RETURN 
7280 READ_RPT_SELECTION_PARAMETERS:
7290 Z$="12O ZZP  13O ZZPARM    "
7300 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7305,9900
7310 Y3$=X3$(1,6),Y4$=X3$(178,7)
7320 READ (Z[12],KEY=X3$(1,8),DOM=*NEXT)IOL=0420; IF LEN(V0$)<190 THEN V0$=PAD(V0$,190,1)
7330 READ (Z[13],KEY="CMP"+X3$(9,3))C9$
7340 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"  ")-1)
7350 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7360 RETURN 
7370 BUILD_LETTER_SECTION:
7380 REM "Message on screen building text
7390 F=Z[2]
7400 W3=NUM(A$(231,2))+10
7410 P8=INT((80-NUM(A$(231,2)))/2)
7420 DIM Y9$(W3),Y5$(W3),Y6$(W3)
7430 IF A$(230,1)="Y" THEN GOTO INDEX_LINE_DETAIL
7440 FIND (Z[3],KEY=A$(217,13)+"1",DOM=7445)L0$,L0; F=Z[4]
7450 W3=NUM(L0$(56,2))+10
7460 INDEX_LINE_DETAIL:
7470 L1=0
7480 READ (F,IND=L0)L0,L7$
7485 IF LEN(L7$)<W3-10 THEN GOTO END_BUILD_LETTER_SECTION
7490 IF L7$(1,W3-10)=Y9$ THEN L8$=L8$+STR(L1)+"1"+" ~"; GOTO 7520
7510 L8$=L8$+STR(L1)+"1"+" "+L7$(1,W3-10)+"~"
7520 IF L0=-1 THEN GOTO END_BUILD_LETTER_SECTION
7530 L1=L1+1
7540 GOTO 7470
7550 L1=0
7560 IF V3$(2,1)="T" THEN W3=79
7570 END_BUILD_LETTER_SECTION:RETURN 
7580 CONVERT_TOKENS:REM "Look for ^"
7590 IF POS("^FF"=L8$(P9,P0-P9))>0 THEN {
7600 GOSUB NEW_PAGE_HEADER
7610 FOR I=1 TO 6
7620 GOSUB PRINT_LINE
7630 NEXT I
7640  } ELSE IF POS("^FROM"=L8$(P9,P0-P9))>0 OR POS("^From"=L8$(P9,P0-P9))>0 THEN {
7650 GOSUB INSERT_CONTACT
7660  } ELSE {
7670 P7=POS("^TO"=L8$(P9,P0-P9))
7680 IF P7>0 THEN GOSUB 8350 ELSE P7=POS("^To"=L8$(P9,P0-P9)); IF P7>0 THEN GOSUB 8350 ELSE GOTO 1638
7690  }
7700 END_CONVERT_TOKENS:RETURN 
7730 INSERT_CONTACT:
7740 DIM G$(125); FIND (Z[7],DOM=7741,KEY=A$(233,4))IOL=0370
7750 IF W>W0-10 THEN GOSUB NEW_PAGE_HEADER
7760 GOSUB PRINT_LINE
7770 Y5$(P8)=G$(85,40)
7780 FOR I=1 TO 5; GOSUB PRINT_LINE; NEXT I
7790 Y5$(P8)=G$(5,40); GOSUB PRINT_LINE
7800 Y5$(P8)=G$(45,40); GOSUB PRINT_LINE; GOSUB PRINT_LINE
7810 IF POS(X3$(9,3)="038",3)<>0 THEN GOSUB PRINT_LINE; GOTO END_INSERT_CONTACT
7820 REM " everyone hates the initials so I'm taking them offLET Y5$(P8)=FNS$(G$(1,4))+"/TFS"; GOSUB PRINT_LINE
7830 RETURN 
7840 END_INSERT_CONTACT:
7850 PRINT_LITERATURE_CODES:
7860 IF POS(" "<>A$(207,10))=0 THEN RETURN 
7870 Y5$(P8)="("+FNS$(A$(207,10))+")"; GOSUB PRINT_LINE
7880 RETURN 
7890 USE_PROFILE:
7900 U$=KEY(_ZPC,END=END_OF_PRINT); READ (_ZPC); U$=U$(1,4)+U$(30,6),U9$=U$
7910 READ (Z[10],KEY=U$,DOM=READ_KEY)A1$
7920 GOSUB 7940
7930 GOTO 1134
7940 REM LOAD A$(ZP8 FORMAT) WITH PROFILE SCREEN(V0$) & A1$(ZP4 FORMAT)
7950 REM SET LIST TYPE TO NAME, PERSON, GREETING, ADDRESS1 TO ZIP, LONG CITY
7960 DIM A$(350); A$(6,50)=A1$(1,50),A$(311,40)=A1$(160,20),A$(91,20)=A1$(180,20),A$(111,87)=A1$(51,87),A$(56,28)=A1$(391,28)
7970 REM SET DATE, LETTER CODE, LETTER WIDTH, JUSTIFY, FROM,SUBJECT,LITERATURE
7980 A$(198,6)=V0$(174,6),A$(217,13)=V0$(86,13),A$(231,2)=V0$(80,2),A$(5,1)=V0$(82,1),A$(233,4)=V0$(159,4),A$(237,60)=V0$(99,60),A$(207,10)=V0$(164,10)
7990 REM SET PRINT TYPE AND SET LETTER TYPE AS NON CUSTOM
8000 IF W$="Y" THEN A$(204,1)="W" ELSE A$(204,1)="L"; A$(230,1)="N"
8010 REM IF TITLE NOT BLANK THEN USE IT WITH GREETING
8020 IF POS(" "<>A1$(373,18))=0 THEN A$(311,40)=A1$(160,20) ELSE A$(311,40)=FNS$(A1$(160,20))+", "+FNS$(A1$(373,18))
8030 IF V0$(163,1)="Y" AND A1$(359,4)<>"    " THEN A$(233,4)=A1$(359,4)
8040 A$(297,10)=A1$(138,10)
8050 P=POS("L"=A$(204,3)); IF P>0 THEN A$(84+P-1,1)="L"
8060 P=POS("F"=A$(204,3)); IF P>0 THEN A$(84+P-1,1)="F"
8070 A9$=A1$(391,28)
8080 RETURN 
8250 BUILD_DESC:REM from H8$
8260 H9$=FNS$(H8$(17,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>H8$(111,8))<>0 THEN H9$=H9$+"x"
8270 H9$=H9$+FNS$(H8$(111,8)); IF H9$<>"" THEN IF POS(" "<>H8$(111,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
8280 IF NUM(H8$(15,2))<>0 THEN H9$=H9$+H8$(15,2)+" Ply "
8290 H9$=H9$+FNS$(H8$(28,40)); H8$=""
8300 RETURN 
8350 PRINT_NAME_ADDRESS:
8360 IF P7>0 THEN IF POS(L8$(P9+P7+2,1)="1")>0 THEN GOTO 8380
8370 IF A$(311,40)<>S$(1,40) THEN Y5$(P8+P7)=A$(311,40); GOSUB PRINT_LINE
8380 IF A$(16,40)<>S$(1,40) THEN Y5$(P8+P7)=A$(16,40); GOSUB PRINT_LINE
8390 IF A$(111,30)<>S$(1,30) THEN Y5$(P8+P7)=A$(111,30); GOSUB PRINT_LINE
8400 IF POS(" "<>A$(141,30))>0 THEN Y5$(P8+P7)=A$(141,30); GOSUB PRINT_LINE
8410 CALL "ZZDISP","AX",A$(189,9),"ZIP",X3$,X$,"",0,0,X4$
8420 Y5$(P8+P7)=FNS$(A9$)+",  "+A$(187,2)+"    "+X$
8430 IF A$(187,2)="  " THEN Y5$(P8+P7)=FNS$(A9$)+"  "+A$(189,9)
8440 GOSUB PRINT_LINE
8450 RETURN 
8500 CALL_WORDLINK:
8505 DIM TEMPLATE$(12)
8510 IF POS(" "<>A$)=0 THEN TEMPLATE$=LETTERVAR$ ELSE TEMPLATE$=A$(217,12); LETTERVAR$=""
8515 REM "TEST IF THE USER IS REMOTE OR ON UNIX, IF REMOTE INITIATE WORDLINK EXE PROGRAM, IF UNIX, INITIATE LOAD KEYSTROKE BUFFER EXE"
8520 MACHINE$=X3$(77,1)
8525 ISWINDX=TCB(88)
8530 ISWINDOWS=TCB(82)
8535 IF C8$="ZP2SMA" THEN ROW=15,COL=24 ELSE ROW=11,COL=24
8536 _TFILE=FN%FFN(TFILE$) ! [205471]-changed FFN() to FN%FFN()
8537 IF _TFILE THEN CLOSE (_TFILE)
8540 ! 
8545 ! CLIENT PVX SESSION
8550 IF MACHINE$="D" AND ISWINDOWS AND NOT(ISWINDX) THEN {
8555 INVOKE "C:\Program Files\TopForm\WordLink\Wordlink.exe "+HWD+DLM+TFILE$+","+TEMPLATE$,ERR=WL_INSTALL_ERR
8560 GOSUB CALL_WORDLINK_END
8561  }
8562 ! 
8563 IF ISWINDX THEN {
8566 ! GOSUB UPDATE_FILES
8567 GOSUB DATA_TRANSFER
8568 IF SENT_TO_PC THEN {
8569 INVOKE %WDX$+"C:\Program Files\TopForm\WordLink\Wordlink.exe "+WLDATADIR$+"\"+RFILE$+","+TEMPLATE$,ERR=WL_INSTALL_ERR
8570 GOTO CALL_WORDLINK_END
8571  }
8572  }
8573 ! GET UNC SERVER NAME
8575 CALL "ZZ2FNC;GETSERVERNETBIOSNAME",SERVER$
8580 ! 
8585 ! REMOTE NT SESSION
8590 IF MACHINE$="D" AND ISWINDOWS AND ISWINDX THEN {
8591 ! \\servername\sharename\path\file
8595 INVOKE %WDX$+"C:\Program Files\TopForm\WordLink\Wordlink.exe "+"\\"+SERVER$+"\providex\"+TFILE$+","+TEMPLATE$,ERR=WL_INSTALL_ERR
8600 GOTO CALL_WORDLINK_END
8605  }
8610 IF MACHINE$<>"U" THEN GOTO CALL_WORDLINK_END
8615 ! 
8620 IF ISWINDX THEN {
8625 CMDLINE$="\\"+SERVER$+"\providex\"+SUB(TFILE$,DLM,"\")+","+TEMPLATE$
8630 IF POS("ZPGRSA"=PGN)=0 THEN PRINT @(10,22),CMDLINE$
8635 INVOKE %WDX$+"C:\PROGRAM FILES\TOPFORM\WORDLINK\WordLink.exe "+CMDLINE$,ERR=WL_INSTALL_ERR
8640 REM INPUT EDIT (0,SIZ=12,IND=2,TIM=15)@(COL,ROW),TEMPLATE$ }
8645 A$(217,12)=TEMPLATE$; REM PRINT 'CI'
8650  } ELSE {
8655 PRINT @(10,22),'BO',$5E$,'EO',"MANUALLY START WORDLINK"
8660 INPUT (0,SIZ=1,TIM=15)TT$
8665 PRINT @(10,22),"\\"+SERVER$+"\providex\"+SUB(TFILE$,DLM,"\")+","+TEMPLATE$+$0D$
8670  } ! ENDIF WINDX SESSION
8675 ! 
8680 CALL_WORDLINK_END:RETURN 
8681 ! 
8685 WL_INSTALL_ERR:IF ERR=12 THEN CALL "ZZPROM",".4",X3$,0,"WordLink is not Installed!","","",0; GOTO 9900
8900 DEF FNQ$(Z9,Z9$)=STR(Z9:Z9$)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO READ_KEY
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "End program
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9915 IF C8$="" OR (W$="Y" AND POS("W"=A$(204,3))>0) THEN IF Y4$<>X0$ THEN GOTO 5035
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 IF C8$<>"" THEN EXIT 
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9935 %WDX$=WDX$; X$=PTH(W9,ERR=*NEXT); CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$ ! SSP#199697
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
10000 ! 
11000 ! ^ 11000
11010 UPDATE_FILES:
11020 CALL "ZZPWLX;UPDATE_WORDLINK"
11030 RETURN 
11040 ! 
11050 DATA_TRANSFER:
11060 MSGBOX "Do you want the data file put in your PC's WordLink work directory?","WordLink Dialog","YESNO",RETVAL$
11071 IF RETVAL$="NO" THEN SENT_TO_PC=0; GOTO *RETURN
11080 ! read remote WORK DIRECTORY
11081 CALL %WDX$+"tfwinapi;GET_REGISTRY_VALUE",ERR=*NEXT,"HKU","Software\TopForm\WordLink\NetWorkPath","WindowsSavePath",WLDATADIR$
11082 ! 
11090 ! if no remote wd use my documents
11091 IF WLDATADIR$="" THEN {
11100 CSIDL_PERSONAL=5
11110 CALL %WDX$+"tfwinapi;getfolderpath",ERR=*NEXT,CSIDL_PERSONAL,WLDATADIR$,WINVER$
11111  }
11130 SENT_TO_PC=0
11131 IF NOT(WLDATADIR$="") THEN {
11140 P=POS("/\":TFILE$,-1)
11150 IF P>0 THEN RFILE$=MID(TFILE$,P+1) ELSE RFILE$=TFILE$
11160 CALL "ZZ2TRN",RFILE$,%WDX$+WLDATADIR$+"\"+RFILE$,SENT_TO_PC
11190  }
11200 RETURN 
56000 REM + Modification History
56002 REM "205471-Oracle - FFN usage in tf2g progs. to be replaced by FN%FFN  
