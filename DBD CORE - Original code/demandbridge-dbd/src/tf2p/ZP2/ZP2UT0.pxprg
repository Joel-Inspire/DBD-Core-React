0010 REM "Rebuild Telemarketing Contact Sort <ZP2UT0>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 06/18/09 - 16.929444 - tma - SSP# 230783
0037 REM "230783-Panel ZPGUTO - change T/M in heading and Telemarketing      
0040 REM "Copyright 2009 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="ZP2UT0",X1$="C/M Contact Sort Rebuild" ! ssp#230783
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O ZP4...  02O ZPA...  03O ZPD...  04O ZPE...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0610 GOSUB 6000
0620 GOSUB 8100
0640 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0700 CALL "ZZINIT",STR(Z[2]:"00")+STR(Z[3]:"00")+STR(Z[4]:"00")
0900 READ (Z[1],KEY="",DOM=0901)
0990 PRINT @(10,13),"Contact:",
1000 REM 
1010 K$=KEY(Z[1],END=5000)
1020 READ RECORD (Z[1],KEY=K$)A$
1025 C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150; PRINT @(20,13),A$(1,10),@(35),A$(11,40),
1030 GOSUB 3000
1100 WRITE (Z[2],KEY=A$(1,4)+A$(419,20)+A$(5,6))
1200 IF POS(X3$(9,3)="060338363364365366476",3)<>0 THEN WRITE (Z[3],KEY=A$(200,12)+A$(1,10)) ELSE WRITE (Z[3],KEY=A$(200,10)+A$(1,10))
1300 WRITE (Z[4],KEY=A$(203,7)+A$(200,3)+A$(1,10))
1600 GOTO 1000
3000 REM "Check the new long sort and set it if it is blank. If the first 10 chars match the name in upper case, then use the first 20 chars of the name (uppercased), else just use the 10 from the short one. ALSO, if the long city name is blank, transfer from the short city name.
3009 REM "Update long city if needed
3010 IF POS(" "<>A$(391,28))=0 THEN A$(391,28)=A$(111,16)
3044 REM "if long lookup already set, then just leave
3045 IF POS(" "<>A$(419,20))<>0 THEN GOTO 3090
3050 CALL "YUCASE",X3$,A$(11,40),Q$
3054 REM "if custom lookup sequence, then just get the 10 existing & leave
3055 IF Q$(1,10)<>A$(150,10) THEN A$(419,20)=A$(150,10); GOTO 3090
3059 REM "Set long lookup to uppercased name
3060 A$(419,20)=Q$(1,20)
3090 WRITE RECORD (Z[1],KEY=K$)A$
3095 RETURN 
5000 REM "EOJ
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(5,4),"This DBI utility program will rebuild the Contact Lookup Sequence",@(5,5),"Sort File ZPA, and the Phone Number Sorts (ZPD and ZPE), from the",@(5,6),"Contact Management Contact Master File  ZP4.", ! ssp#230783
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F$
8115 PRINT @(5,8),"There are "+STR(T)+" Contact Management records to process" ! ssp#230783
8129 REM "Set T0, we make sure T0 is > 1, because later on we MOD and look for avalue of 1. IF T0 is 1, then nothing would get reported. We look fora result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T0'th record to get the first report
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,C
8195 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "212146-New company name. Menu, documentation, etc.
