0010 REM "Support Issue Setup <ZW2ZZZ>
0015 REM "Prog Type: I -3.3.1
0020 SETESC 9300; SETERR 9000
0035 REM "5.3 - 04/29/04 - 11.522777 - jme - SSP# 171390
0040 REM "Copyright 2004 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0110 X0$="ZW2ZZZ",X1$="Support Issue Setup",K9$="",K9=0
0120 DIM S$(40),E$(200),F$(200)
0125 IF NOT(NUL(%WDX$)) THEN %USE_NOTEPAD=1; CALL "[WDX]*Windx.utl;Get_Val","ENV("+QUO+"TEMP"+QUO+")",%ENV_TEMP$; CALL "[WDX]*Windx.utl;Get_Val","ENV("+QUO+"COMSPEC"+QUO+")",%ENV_COMSPEC$ ELSE %USE_NOTEPAD=0
0130 K0=6,K1=1
0135 C9=-1,V9=-2
0140 M0$="###.00-",M2$="##,###.00",M3$="###0",V8$="000507132199"
0190 Q$=PUB(1,ERR=*NEXT); IF LEN(Q$)=0 THEN CALL "ZZADDR"; P9$="*"
0200 REM "
0210 DOTIME=0
0240 CALL "ZZCOMP",ERR=0241,X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920 ELSE GOTO 0245
0241 IF ERR<>11 THEN ESCAPE ELSE CALL "ZSTART",ERR=0240; GOTO 0240
0245 IF X3$(65,1)>"2" THEN PRECISION NUM(X3$(65,1),ERR=0246)
0250 X2$="Computer Software Inc.",X3$(86,40)=X2$
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0296 DIM DASH$(80,"-"),ZWN$(100)
0300 REM "IOLISTS
0310 IOLIST A$(1),A[0],A[1],A[2]
0330 IOLIST C$,C0$,C[1]
0340 IOLIST D$,D[0],D[1]
0350 IOLIST E$,E[0],E[1]
0360 IOLIST F$,F[0],F[1]
0390 IOLIST I$,I[0],I[1]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X ZW2... 02O ZWD...  03O SS1...  04O ZW9...  05O ZW7...  06O ZW8...  07O ZWE...  08O ZWF...  09O ZW2...  10O ZM1...  11O ZWP... 12O ZWH... 13O ZZPARM  14O ZWT...  15O SSR... 16O ZWN...  "
0520 GOSUB 9750; ON Z0 GOTO 0521,9900
0530 H0$="/usr/lib/basic/data/z.csi/tfin"
0535 S0$="",S1$=""; READ (Z[9],KEY="",DOM=0536)
0540 READ (Z[4],END=0541)Q$; IF Q$(3,1)<>"*" THEN S0$=S0$+Q$(1,2); GOTO 0540 ELSE GOTO 0540
0545 READ (Z[6],KEY="",DOM=0546)
0550 READ (Z[6],END=0561)Q$; IF Q$(24,1)="1" THEN S1$=S1$+Q$(1,1); IF Q$(23,1)="1" THEN S4$=S4$+Q$(1,1)
0560 GOTO 0550
0590 READ (Z[1],KEY=K9$,DOM=0591)
0600 REM "
0640 GOSUB 6000
0650 REM "Get operator code
0655 CALL "ZSTRTU",X3$,Q$,"",""
0660 REM CALL "ZUSER",X3$,Q$,"",""
0670 CALL "YUCASE",X3$,Q$,U0$
0671 IF U0$="ROOT" THEN U0$="   "
0672 IF LEN(U0$)=2 THEN U0$=U0$+" "
0673 IF LEN(U0$)>3 THEN U0$=U0$(1,3); REM " GOTO 00680
0675 READ (Z[5],KEY=U0$,DOM=0680)U1$; GOTO 0700
0680 CALL "ZZPROM","4",X3$,0,"You are not setup in the SSP system ("+U0$+")","","",0; GOTO 9900
0720 L9$=" "; IF U0$="JSC" THEN GOSUB 7000
1000 REM "
1005 F9$=""
1010 Y8=1; CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1015 DIM V7$(17); V7$(3,1)="1",V7$(7,1)="1",V7$(11,1)="1",V7$(13,1)="1"
1030 DIM C$(725),C[1],D$(23),D[1],E$(29),E[1],F$(22),F[1],I[2]
1035 DIM A$(410),A[4],ZWN$(100)
1040 IF V9<>-1 THEN GOSUB 6900
1041 IF DOTIME=1 THEN GOSUB 8970
1045 A$(1,LEN(A1$))=A1$; IF A1$="" THEN A$(1,LEN(K9$))=K9$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1105 IF F9$>"" THEN A$(1,6)=F9$; GOTO 1500
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 SETERR 9000; C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1500
1200 REM "Reference number
1201 IF DOTIME=1 THEN GOSUB 8970
1203 READ (Z[1],KEY="",DOM=1204)
1204 X$="Enter 'A' to generate next number"+", 'C' for client quick ssp issues"
1205 IF J0$="" THEN J0$="ZW2",J1$=""
1210 CALL "ZZENTR","SZXUX",A{ALL},A$,X4$,X3$,18,3,1,6,C0,"","{1"+X$,J1$,"ZW2ZZZ00",J0$+"","",""; IF C0=6 THEN GOSUB 6400; C0=1
1211 IF A$(1,6)="A     " THEN GOSUB 8800; GOSUB 6450; GOTO 1240 ELSE IF A$(1,6)="REBUIL" THEN GOSUB 7900; GOTO 1200
1212 IF INT(ABS(C0))=3 THEN C0=-ABS(C0); READ (Z[1],KEY=A$(1,6),DOM=1213)TMPA1$
1213 IF A$(1,1)="C" THEN GOTO 8400 ELSE IF A$(1,1)="L" THEN GOSUB 8850 ELSE IF A$(1,4)="STAT" THEN GOTO 8600 ELSE IF A$(1,1)="T" THEN J0$="ZW2A",J1$=U0$; GOTO 1210
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1216,6800,9800,6970
1220 IF POS(" "<>A$(1,6))=0 THEN GOTO 1210
1240 GOTO 1120
1500 REM "
1501 IF DOTIME=1 THEN GOSUB 8970
1505 Z9=-1,Z8=0,C9=0,A2$="",A3$="",A4$="",A5$=""
1510 A1$=A$(1,6)
1515 XP=PRM('XI'); SET_PARAM 'XI'
1520 READ (Z[1],ERR=7200,KEY=A1$,DOM=1600)IOL=0310
1521 DIM ZWN$(100); ZWN$(1,6)=A1$,ZWN$(7,1)="S",ZWN$(8,6)=A$(7,6),ZWN$(14)=STP(A$(175,8),3)+" "+STP(A$(183,3),3)+" "+STP(A$(186,5),3); READ (Z[16],KEY=A1$,DOM=*NEXT)ZWN$
1522 SET_PARAM 'XI'=XP
1525 Z9=0,V7$=""; GOSUB 5800
1590 GOTO 1700
1600 REM "
1615 IF X3$(185,1)="I" THEN CALL "ZZPROM","2A",X3$,0,"","","",0; GOTO 1630
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630
1630 IF Z8=1 THEN REMOVE (Z[1],KEY=A1$,DOM=1000); REMOVE (Z[16],KEY=A1$,DOM=1000); GOTO 1000 ELSE GOTO 1000
1655 Z8=1
1680 GOSUB 7450
1681 CALL "ZZ2FNC;GETUSERNAME",USERNAME$
1682 A$(7,10)=X3$(21,6)+STR(100*TIM:"0000"),A$(196,10)=A$(7,10),A$(359,1)=C$(691,1)
1683 A$(195,1)="N",A[0]=.25,A$(206,1)="S",A$(194,1)="A",A$(173,2)="PA"; IF C$(691,1)="A" THEN A$(194,1)="1"; REM premium support
1684 A$(253,3)=UCS(USERNAME$); REM CALL "ZSTRTU",x3$,x$,"",""; CALL "YUCASE","",x$,x$; a$(253,3)=x$
1685 GOSUB 6200
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1805 REM IF A(0)=0 OR INT(A(0)/.25)<>A(0)/.25 THEN LET X0=15; GOTO 02040
1810 IF V9=-2 THEN IF ABS(C0)=4 THEN PRINT 'CF',; GOTO 6980 ELSE GOTO 2000
1815 IF X3$(185,1)="I" THEN CALL "ZZPROM","4A",X3$,Z,"","","",0; GOTO 6790
1820 CALL "ZZPROM","6 ZW2ZZZ",X3$,Z,"","","V",X0; IF Z<>4 AND Z<>5 THEN DOTIME=1
1821 ON Z GOTO 1850,6500,1630,6700,1822
1822 IF Z=6 THEN GOTO 7600 ELSE IF Z=7 THEN GOTO 8000 ELSE IF Z=12 THEN GOTO 8050 ELSE IF Z=13 THEN GOTO 8070
1823 IF Z=8 THEN GOSUB 7800 ELSE IF Z=9 THEN GOTO 8700
1824 IF Z=10 OR Z=15 OR Z=16 THEN GOTO 8000 ELSE IF Z=11 THEN GOTO 8750
1825 IF Z=14 THEN GOTO 8950
1826 GOTO 6700
1830 GOTO 2000
1850 REM "MODIFY
1851 IF A$(207,1)<>" " THEN CALL "ZZPROM",".4",X3$,0,"This item has been processed by billing.  No changes may be made","","",0; GOTO 1800
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 GOSUB 7400
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 SETERR 9000; X$="",Q$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF INT(C0)=3 THEN GOSUB 7300 ELSE IF C0>3 OR X0<0 THEN GOTO 1800
2035 IF V9<>-2 THEN IF 6*(X0+K1)+5<LEN(V9$) THEN IF V9$((X0+K1)*6+5,1)="3" THEN GOTO 2020 ELSE IF V9$((X0+K1)*6+5,1)="2" AND C0<>2 THEN GOTO 2020
2038 IF X0<LEN(V7$) THEN V7$(X0+1,1)=""
2040 ON X0 GOTO 2100,2150,2200,2250,2300,3450,2350,2400,3600,3650,3700,3750,2450,2500,2550,2570,3000,3550,2600,2700,2750,2800,2950,3100,3150,3350,2650,3250,2850,2900,3300,3050,3200,3400,3500,1800
2100 REM "Initial date
2110 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,43,3,7,6,C0,"","{3"+X$,"","ZW2ZZZ02","","ZW2ZZZ",""; IF C0=6 THEN GOSUB 6400; C0=1
2140 GOTO 2020
2150 REM "Initial time
2160 CALL "ZZENTR","T",A{ALL},A$,X4$,X3$,62,3,13,4,C0,"","{3"+X$,"","ZW2ZZZ02","","",""; IF C0=6 THEN GOSUB 6400; C0=1
2165 PRINT @(72,3),FNT$(STR(NUM(A$(13,2))+NUM(C$(224,3),ERR=2166):"00")+A$(15,2))
2190 GOTO 2020
2200 REM "Client
2210 CALL "ZZENTR","AUZX",A{ALL},A$,X4$,X3$,18,5,240,10,C0,"S/S","{3"+X$,"","ZW2ZZZ06","SS1","SS2MAZ",""; IF C0=6 THEN GOSUB 6400; C0=1
2220 IF V9<>-2 THEN FIND (Z[3],KEY=A$(240,10),DOM=2210)IOL=0330
2221 IF LEN(C$)<725 THEN C$=C$+" "; GOTO 2221
2222 IF C0>0 THEN A$(261,30)=C$(631,30),A$(299,40)=C$(631,40),A$(23,16)=C$(231,30),A$(39,14)=C$(195,14),A$(359,1)=C$(691,1),A$(360,3)=C$(692,3),A$(363,3)=C$(695,3); PRINT @(18,6),A$(23,16),@(43,6),FNP$(A$(39,14)),@(34,7),A$(299,40),; IF POS(" "<>C$(133,10))>0 THEN PRINT @(2,5),"(F)",
2225 PRINT @(32,5),C$(11,35),
2230 IF C0>0 THEN GOSUB 8500
2240 GOTO 2020
2250 REM "Contact person
2260 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,18,6,23,16,C0,"","{3"+X$,"","ZW2ZZZ08","","ZW2MBZ",""; IF C0=6 THEN GOSUB 6400; C0=1
2290 GOTO 2020
2300 REM "Phone number
2310 CALL "ZZENTR","P",A{ALL},A$,X4$,X3$,43,6,39,14,C0,"","{3"+X$,"","ZW2ZZZ10","","",""
2340 GOTO 2020
2350 REM "Description
2360 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,18,8,53,60,C0,"","{3"+X$,"","ZW2ZZZ12","","","01V"
2390 GOTO 2020
2400 REM "Category
2410 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,18,11,173,2,C0,"","{3"+X$,"","ZW2ZZZ14","ZW9","ZW2GAA",""; IF C0=6 THEN GOSUB 6400; C0=1
2420 IF V9<>-2 THEN FIND (Z[4],KEY=A$(173,2),DOM=2410)IOL=0340
2425 PRINT @(22,11),D$(3,20),
2440 GOTO 2020
2450 REM "Program
2460 CALL "ZZENTR","SUX",A{ALL},A$,X4$,X3$,18,12,175,8,C0,"","{3"+X$,"","ZW2ZZZ16","","",""
2465 IF C0>0 THEN IF A$(175,8)=S$(1,8) THEN A$(183,8)=""; PRINT @(18,13),A$(183,3),@(41,13),A$(186,5)
2470 P0$=A$(175,8),P0=12; GOSUB 7500
2475 IF A$(175,8)=S$(1,8) AND ABS(C0)<>2 THEN X0=X0+2
2490 GOTO 2020
2500 REM "Error number
2510 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,18,13,183,3,C0,"","{3"+X$,"","ZW2ZZZ18","","",""
2540 GOTO 2020
2550 REM "Program line
2560 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,41,13,186,5,C0,"","{3"+X$,"","ZW2ZZZ20","","",""
2565 GOTO 2020
2570 REM "OLD#
2580 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,73,13,256,5,C0,"","{3"+X$,"","ZW2ZZZ69","ZW5","",""; IF C0=6 THEN GOSUB 6400; C0=1
2590 GOTO 2020
2600 REM "SSP person
2610 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,18,16,191,3,C0,"","{3"+X$,"","ZW2ZZZ22","ZW7","ZW2EAA",""; IF C0=6 THEN GOSUB 6400; C0=1
2611 IF C0=7 THEN GOSUB 7050
2620 IF V9<>-2 THEN FIND (Z[5],KEY=A$(191,3),DOM=2610)IOL=0350
2625 PRINT @(23,16),E$(4,25),
2640 GOTO 2020
2650 REM "Priority
2655 IF A$(194,1)="C" AND POS(A$(195,1)="FCGY")=0 THEN A$(194,1)="A"; IF C$(691,1)="A" THEN A$(194,1)="1"
2660 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,59,16,194,1,C0,"","{3"+X$,"","ZW2ZZZ24","","",""
2690 GOTO 2020
2700 REM "Status code
2705 S2$=A$(195,1)
2706 X$=""; IF A$(173,2)="WO" AND POS(S2$="9JKLZD")<>0 THEN X$="Allowable status codes include:  J/K/L/X/Z/D/Q"
2710 CALL "ZZENTR","SUX",A{ALL},A$,X4$,X3$,18,17,195,1,C0,"","{3"+X$,"","ZW2ZZZ26","ZW8","ZW2FAA",""; IF C0=6 THEN GOSUB 6400; C0=1
2715 IF C0>0 AND A$(173,2)="WO" THEN IF POS(S2$="9JKLZD")>0 THEN IF POS(A$(195,1)="9JKLXZWOSDQ")=0 THEN A$(195,1)=S2$; PRINT 'RB',; GOTO 2710
2717 IF POS(A$(195,1)="FCGY")<>0 THEN A$(194,1)="Z"; PRINT @(59,16),A$(194,1) ELSE A$(194,1)="A"; PRINT @(59,16),A$(194,1); IF C$(691,1)="A" THEN A$(194,1)="1"; PRINT @(59,16),A$(194,1)
2718 IF C0>0 THEN IF A$(195,1)<>"C" THEN IF A$(222,6)<>S$(1,6) THEN A$(222,6)=""; PRINT @(18,20),FND$(A$(222,6))
2720 IF V9<>-2 THEN FIND (Z[6],KEY=A$(195,1),DOM=2710)IOL=0360
2722 IF CTL=-7 THEN A$(196,6)=X3$(15,6),A$(202,4)=STR(100*TIM:"0000"); PRINT @(18,18),FND$(A$(196,6)),@(30,18),FNT$(A$(202,4)); X0=X0+1; GOTO 2020
2725 PRINT @(21,17),F$(2,20),
2730 IF C0>0 THEN A$(196,6)=X3$(21,6),A$(202,4)=STR(INT(100*TIM):"0000"); PRINT @(18,18),FND$(A$(196,6)),@(30,18),FNT$(A$(202,4)),
2740 GOTO 2020
2750 REM "Status date
2755 X$="Press <F10> to set to current date and time"
2760 CALL "ZZENTR","DF",A{ALL},A$,X4$,X3$,18,18,196,6,C0,"","{3"+X$,"","ZW2ZZZ28","","",""
2765 IF C0=7 THEN A$(196,6)=X3$(21,6),A$(202,4)=STR(100*TIM:"0000"); PRINT @(18,18),FND$(A$(196,6)),@(30,18),FNT$(A$(202,4)); X0=X0+1,C0=1; GOTO 2020
2790 GOTO 2020
2800 REM "Status time
2810 CALL "ZZENTR","T",A{ALL},A$,X4$,X3$,30,18,202,4,C0,"","{3"+X$,"","ZW2ZZZ30","","",""
2840 GOTO 2020
2850 REM "Total time spent
2855 X$="Minimum .25 hour, MUST be in .25 increments!!!"
2860 CALL "ZZENTR","N&",A{ALL},A$,X4$,X3$,59,18,0,320,C0,M0$,"{3"+X$,"","ZW2ZZZ32","","",""
2865 IF INT(A[0]/.25)<>A[0]/.25 THEN GOTO 2850
2890 GOTO 2020
2900 REM "BILLABLE STTUS
2905 X$="S=Support agr, B=Billable, N=Non-Charge, ?=Acct Rep. decides, C=Contract,W=WO"
2906 BILL_CODE$=A$(206,1)
2910 CALL "ZZENTR","SUR",A{ALL},BILL_CODE$,X4$,X3$,59,19,1,1,C0,"","{3"+X$,"BNSW?C","","","",""; IF C0=6 THEN GOSUB 6400; C0=1
2911 C0_HOLD=C0
2912 IF BILL_CODE$="N" AND BILL_CODE$<>A$(206,1) THEN PRINT @(0,22),'CE',@(10,22),"Enter Password:",; DIM PASS$(8); CALL "ZZENTR","SP",A{ALL},PASS$,X4$,X3$,26,22,1,8,C0,"","","","","","",""; PRINT @(0,22),'CE',; IF STP(PASS$,1)="yesdoit" OR STP(PASS$,1)="YESDOIT" THEN A$(206,1)=BILL_CODE$ END_IF ; PRINT @(59,19),A$(206,1), ELSE A$(206,1)=BILL_CODE$; PRINT @(59,19),A$(206,1),
2913 C0=C0_HOLD
2940 GOTO 2020
2950 REM "Promise date
2955 X$="INTERNAL USE ONLY- Will not print on hard copy!!!!!"
2960 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,18,19,208,6,C0,"","{3"+X$,"","ZW2ZZZ34","","",""
2990 GOTO 2020
3000 REM "Topform Menu Index
3010 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,18,14,214,8,C0,"","{3"+X$,"","","","",""; IF C0=6 THEN GOSUB 6400; C0=1
3020 P0$=A$(214,8),P0=14; GOSUB 7500
3040 GOTO 2020
3055 X$="Followup date, expiration date, etc. "
3060 CALL "ZZENTR","DX",A{ALL},A$,X4$,X3$,59,20,222,6,C0,"","{3"+X$,"","ZW2ZZZ34","","",""
3065 IF C0>0 THEN IF A$(222,6)=S$(1,6) THEN A$(239,1)=""; PRINT @(71,20)," ",
3090 GOTO 2020
3100 REM "Schedule date
3105 X$="Use this date for a working scheduled completion date"
3110 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,18,20,228,6,C0,"","{3"+X$,"","ZW2ZZZ40","","",""
3140 GOTO 2020
3150 REM "Schedule time
3155 IF A$(228,6)=S$(1,6) THEN A$(234,4)=""; PRINT @(30,20),FNT$(A$(234,4)); GOTO 2020
3160 CALL "ZZENTR","T",A{ALL},A$,X4$,X3$,30,20,234,4,C0,"","{3"+X$,"","ZW2ZZZ40","","",""
3190 GOTO 2020
3200 REM "Expiration type (F=Followup, C=Close)
3202 IF A$(222,6)=S$(1,6) THEN A$(239,1)=""; PRINT @(71,20)," "; GOTO 2020
3205 X$="Type of followup (C=Close call, F=Followup)"
3210 CALL "ZZENTR","SUR",A{ALL},A$,X4$,X3$,71,20,239,1,C0,"","{3"+X$,"CF","ZW2ZZZ40","","",""
3240 GOTO 2020
3250 REM "Time estimate
3260 CALL "ZZENTR","N&",A{ALL},A$,X4$,X3$,59,17,1,320,C0,M0$,"{3"+X$,"","ZW2ZZZ32","","",""
3290 GOTO 2020
3300 REM "Work order / Billable amount
3301 IF POS(A$(206,1)="SN")>0 THEN A[2]=0; PRINT @(70,19),A[2]:M2$,; GOTO 2020
3302 IF POS(A$(206,1)="W")=0 THEN IF ABS(C0)<>2 THEN GOTO 2020
3305 X$="Enter the fixed amount to bill for this item"
3310 CALL "ZZENTR","N&.",A{ALL},A$,X4$,X3$,70,19,2,520,C0,M2$,"{3"+X$,"","ZW2ZZZ32","","",""
3340 GOTO 2020
3350 REM "Resource person"
3355 X$="Programmer or person that provided the solution to the issue"
3360 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,19,21,250,3,C0,"","{3"+X$,"","ZW2ZZZ44","ZW7","ZW2EAA",""; IF C0=6 THEN GOSUB 6400; C0=1
3370 REM IF C0=7 THEN GOSUB 07050
3380 IF V9<>-2 THEN FIND (Z[5],KEY=A$(250,3),DOM=3360)IOL=0350
3390 GOTO 2020
3400 REM "Responsible person"
3405 X$="Support person that is responsible to client"
3410 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,44,21,253,3,C0,"","{3"+X$,"","ZW2ZZZ44","ZW7","ZW2EAA",""; IF C0=6 THEN GOSUB 6400; C0=1
3420 REM IF C0=7 THEN GOSUB 07050
3430 IF V9<>-2 THEN FIND (Z[5],KEY=A$(253,3),DOM=3410)IOL=0350
3440 GOTO 2020
3450 REM "e-mail address
3455 X$="Contact person's e-mail address for notification"
3460 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,34,7,299,40,C0,"","{3"+X$,"","ZW2ZZZ58","","",""; IF C0=6 THEN GOSUB 6400; C0=1
3465 REM LET A$(299,30)=A$(261,30)
3490 GOTO 2020
3500 REM "Credit Responsibility
3510 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,74,21,291,3,C0,"","{3"+X$,"","ZW2ZZZ48","ZW7","ZW2EAA",""; IF C0=6 THEN GOSUB 6400; C0=1
3530 FIND (Z[5],KEY=A$(291,3),DOM=3510)IOL=0350
3540 GOTO 2020
3550 REM "3rd Party Software Code
3555 X$="Enter 3rd Party Software code"
3560 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,73,14,294,5,C0,"","{3"+X$,"","ZW2ZZZ50","SSR","SS2MAR",""; IF C0=6 THEN GOSUB 6400; C0=1
3570 IF STP(A$(294,5),3," ")="" THEN GOTO 3571 ELSE FIND (Z[15],KEY=A$(294,5),DOM=3560)
3590 GOTO 2020
3600 REM "Keyword 1
3610 CALL "ZZENTR","SZ",A{ALL},ZWN$,X4$,X3$,51,11,30,12,C0,"","{3"+X$,"","ZW2ZZZ69","ZW5","",""; IF C0=6 THEN GOSUB 6400; C0=1
3615 REM IF STP(ZWN$(30,12),3)="" THEN GOTO 3600
3620 GOTO 2020
3650 REM keyword 2
3655 CALL "ZZENTR","SZ",A{ALL},ZWN$,X4$,X3$,64,11,42,12,C0,"","{3"+X$,"","ZW2ZZZ69","ZW5","",""; IF C0=6 THEN GOSUB 6400; C0=1
3660 GOTO 2020
3700 REM keyword 3
3710 CALL "ZZENTR","SZ",A{ALL},ZWN$,X4$,X3$,51,12,54,12,C0,"","{3"+X$,"","ZW2ZZZ69","ZW5","",""; IF C0=6 THEN GOSUB 6400; C0=1
3720 GOTO 2020
3750 REM keyword 4
3760 CALL "ZZENTR","SZ",A{ALL},ZWN$,X4$,X3$,64,12,66,12,C0,"","{3"+X$,"","ZW2ZZZ69","ZW5","",""; IF C0=6 THEN GOSUB 6400; C0=1
3790 GOTO 2020
5800 REM "Alt sort keys
5810 FIND (Z[3],KEY=A$(240,10),DOM=5811)IOL=0330
5812 IF A$(173,2)="  " THEN A$(173,2)="PA"
5815 FIND (Z[4],KEY=A$(173,2),DOM=5816)IOL=0340
5820 FIND (Z[5],KEY=A$(191,3),DOM=5821)IOL=0350
5825 FIND (Z[6],KEY=A$(195,1),DOM=5826)IOL=0360
5835 A2$=A$(195,1)+A$(7,6)+A$(194,1)+A$(13,6)+A$(1,6)
5840 A3$=A$(175,8)+A$(183,8)+A$(1,6)
5845 A4$=A$(240,10)+A$(1,6)
5850 A5$=A$(214,8)+A$(175,8)+A$(1,6)
5890 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(0,3+V0),"Reference number:",@(29,3+V0),"Initial date:",@(56,3+V0),"Time:",@(0,4+V0),Z0$,
6070 PRINT @(10,5+V0),"Client:",@(2,6+V0),"Contact person:",@(38,6+V0),"Tel:",@(0,7+V0),MNM('GS'),DIM(20,X3$(235,1)),MNM('GE'),@(18,7+V0),"e-mail address:",@(74,7+V0),MNM('GS'),DIM(6,X3$(235,1)),MNM('GE'),
6080 PRINT @(5,8+V0),"Brief descr:",@(0,10+V0),Z0$,
6085 PRINT @(45,11+V0),"KeyW:"
6090 PRINT @(8,11+V0),"Category:",@(9,12+V0),"Program:",@(4,13+V0),"Error number:",@(28,13+V0),"Line number:",@(69),"E#:",@(6,14),"Menu index:",@(67,14),"3PSW:",@(0,15+V0),Z0$,
6100 PRINT @(5,16+V0),"Current SSP:",@(49,16),"Priority:",@(5,17+V0),"Status code:",@(44,17+V0),"Time estimate:",@(68,17),"hours",@(5,18+V0),"Status date:",@(41,18+V0),"Total time spent:",@(68,18+V0),"hours",@(45,19),"Billing code:",@(62,19),"Amount:",
6120 PRINT @(4,19),"Promise date:",@(3,20),"Schedule date:",@(43,20),"Follow-up date:",
6130 PRINT 'SB',@(0,21),"Solution provider:",@(28),"SSP entered by:",@(51),"Credit responsibility:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6218 IF POS(" "<>C$(133,10))>0 THEN PRINT @(2,5),"(F)",
6219 CALL "ZZDISP","AU",A$(240,10),"S/S",X3$,"","",18,5,X4$
6220 PRINT @(43,3),FND$(A$(7,6)),@(62,3),FNT$(A$(13,4)),@(32,5),C$(11,35),@(18,6),A$(23,16),@(43,6),FNP$(A$(39,14)),@(34,7),A$(299,40),@(18,8),A$(53,60),@(18,9+V0),A$(113,60),@(18,11),A$(173,2),@(22,11),D$(3,20),@(18,12),A$(175,8),@(18,13),A$(183,3),@(41,13),A$(186,5),@(73,13),A$(256,5),@(18,14),A$(214,8),@(73,14),A$(294,5),@(18,16),A$(191,3),@(23,16),E$(4,25),@(59,16),A$(194,1),@(18,17),A$(195,1),@(21,17),F$(2,20),@(59,17),A[1]:M0$,@(18,18),FND$(A$(196,6)),@(30,18),FNT$(A$(202,4)),
6222 PRINT @(18,19),FND$(A$(208,6)),@(18,20),FND$(A$(228,6)),@(30,20),FNT$(A$(234,4)),@(59,20),FND$(A$(222,6)),@(71,20),A$(239,1)
6224 PRINT @(59,18),A[0]:M0$,
6225 PRINT @(72,3),FNT$(STR(NUM(A$(13,2))+NUM(C$(224,3),ERR=6226):"00")+A$(15,2))
6230 PRINT @(59,19),A$(206,1),@(70,19),A[2]:M2$,
6240 P0=12,P0$=A$(175,8); GOSUB 7500
6245 P0=14,P0$=A$(214,8); GOSUB 7500
6250 PRINT @(19,21),A$(250,3),@(44,21),A$(253,3),@(74,21),A$(291,3),
6255 PRINT @(51,11),ZWN$(30,12),@(64,11),ZWN$(42,12),@(51,12),ZWN$(54,12),@(64,12),ZWN$(66,12)
6280 GOSUB 7550
6285 GOSUB 8500
6390 RETURN 
6400 REM "WHOLE SCREEN
6402 IF C9=0 THEN IF ABS(C0)=17 THEN GOTO 6970
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 PRINT @(18,3),A$(1,6),
6490 RETURN 
6500 REM "DELETE
6505 REMOVE (Z[16],KEY=A1$,DOM=*NEXT)
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6520 REMOVE (Z[2],KEY=A2$,DOM=6521)
6525 CALL "ZW2ZZY",X3$,X4$,A1$,"D","ZWT"
6590 A1$=""; GOTO 1630
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY
6710 IF Z9=1 THEN WRITE (Z[1],KEY=A1$)IOL=0310; WRITE (Z[16],KEY=A1$)ZWN$
6720 IF A2$=A$(195,1)+A$(7,6)+A$(194,1)+A$(13,6)+A$(1,6) THEN GOTO 6726
6724 REMOVE (Z[2],KEY=A2$,DOM=6725)
6728 WRITE (Z[2],KEY=A$(195,1)+A$(7,6)+A$(194,1)+A$(13,6)+A$(1,6))
6730 Q$=A$(175,8)+A$(183,8)+A$(1,6); IF Q$<>A3$ THEN REMOVE (Z[7],KEY=A3$,DOM=6731)
6735 WRITE (Z[7],KEY=Q$)
6740 Q$=A$(240,10)+A$(1,6); IF Q$<>A4$ THEN REMOVE (Z[8],KEY=A4$,DOM=6741)
6745 WRITE (Z[8],KEY=Q$)
6750 Q$=A$(7,10)+A$(1,6); IF Q$<>A5$ THEN REMOVE (Z[12],KEY=A5$,DOM=6751)
6755 WRITE (Z[12],KEY=Q$)
6760 CLOSE (14); OPEN (14)"ZWI"
6765 IF POS(A$(195,1)="CFLZY")>0 THEN REMOVE (14,KEY=A1$,DOM=6770); GOTO 6770
6766 WRITE (14,KEY=A1$)A1$+A$(195,1)+S$(1,22)
6770 IF POS(" "<>A$(299,40))>0 THEN CALL "ZZPROM",".Y",X3$,S3,"Send e-mail confirmation?","","",0; IF S3=0 THEN GOSUB SEND_EMAIL
6775 IF A$(359,1)="A" AND A$(360,3)<>"   " THEN AUTOSSP$=A$(360,3); GOSUB 8750; AUTOSSP$=""
6777 IF A$(359,1)="A" AND A$(363,3)<>"   " THEN AUTOSSP$=A$(363,3); GOSUB 8750; AUTOSSP$=""
6790 C9=0; IF Z=6 OR K1=0 THEN GOTO 9900 ELSE IF Z<4 THEN GOTO 1000 ELSE C0=-(3.1+(Z-4)/10); GOTO 6800
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 IF A1$="" THEN GOTO 6800 ELSE IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6825 A$(1,LEN(A1$))=A1$
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM "PRIOR ENTRY
6852 A1$=""; A1$=KEP(Z[1],ERR=6856); GOTO 6820
6855 REM CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6856 CALL "ZZPROM","K0",X3$,S3,"","","",0; ON S3 GOTO 6857,9900
6860 PRINT 'RB',; CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6865 GOTO 1100
6870 GOTO 6895
6890 GOTO 1515
6895 CALL "ZZPROM","K1",X3$,S3,"","","",0; IF S3=1 THEN GOTO 9900 ELSE IF A1$="" THEN GOTO 6860 ELSE PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=6896)
6898 ON S9 GOTO 6810,1100
6900 REM "GET DEFAULTS
6902 V9$="1803  4303  6203  1805  1806  4306  1808  1811  1812  1813  1814  1816  6316  1817  1818  3018  5918  "
6910 IF V9>=0 THEN READ (Z[13],IND=V9)V9$,IOL=0310; GOTO 6930 ELSE V9=-1
6920 EXTRACT (Z[13],KEY=X0$+"DFLT",DOM=6921)V9$,IOL=0310; V9=IND(Z[13]); READ (Z[13])
6930 RETURN 
6940 V9=-1; REMOVE (Z[13],KEY=X0$+"DFLT",DOM=1000); GOTO 1000
6950 REM "WRITE DEFAULT
6955 WRITE (Z[13],KEY=X0$+"DFLT")V9$,IOL=0310
6960 GOSUB 6920; GOTO 1630
6970 REM "ENTER DEFAULT VALUES AND SETTINGS
6975 A$(1,K0+K9)=""; DIM ZWN$(100); GOSUB 6200; V9=-2,Q$=""; GOTO 2000
6980 FOR X=1+K1*6 TO LEN(V9$) STEP 6
6982 PRINT @(NUM(V9$(X,2)),NUM(V9$(X+2,2))),; IF V9=-2 THEN PRINT V9$(X+4,1), ELSE READ RECORD (0,SIZ=1)Q$; IF POS(Q$="1234+-"+$0D$)=0 THEN PRINT 'RB'; GOTO 6982
6990 IF Q$="+" THEN EXITTO 6950 ELSE IF Q$="-" THEN GOTO 6940 ELSE IF Q$<>$0D$ THEN V9$(X+4,LEN(Q$))=Q$
6992 NEXT X
6995 V9=0; GOTO 6980
7000 REM "Check / Create User Lookup File
7010 Q$="ZWK"
7015 CLOSE (14); OPEN (14,ERR=7016)Q$; L9$(1,1)="Y"; GOTO 7040
7045 RETURN 
7050 REM "Add this to personal file
7055 IF L9$(1,1)=" " THEN RETURN ELSE CLOSE (14); OPEN (14)"ZWK"; WRITE (14,KEY=U0$(1,3)+A$(1,6))
7060 RETURN 
7200 REM "Forced read
7210 REM IF ERR<>0 THEN GOTO 9000
7220 CALL "ZZREAD",Z[1],A1$,A$,A{ALL},S8; ON S8 GOTO 7221,1630
7230 GOSUB 6200
7240 CALL "ZZPROM","E0",X3$,0,"","","",0; GOTO 7290
7250 CALL "ZZPROM","E0A",X3$,0,"","","",0; GOTO 7290
7290 GOTO 1630
7300 REM 
7310 ON FPT(C0)*10 GOTO 7390,7325,7330,7390
7325 IF V8$="" THEN X0=X0+4; GOTO 7390 ELSE X0=NUM(V8$(POS(STR(X0:"00")<=V8$,2),2)); GOTO 7390
7330 IF V8$="" THEN X0=X0-6; GOTO 7335 ELSE X0=POS(STR(X0-1:"00")<=V8$,2)-2; IF X0<0 THEN X0=0; GOTO 7390 ELSE X0=NUM(V8$(X0,2)); GOTO 7390
7335 IF X0<0 THEN X0=0; GOTO 7390
7390 IF X0>0 THEN X0=X0-0
7395 C0=3; RETURN 
7400 REM "EXTRACT
7410 EXTRACT (Z[1],KEY=A1$,DOM=7440)IOL=0310
7415 DIM ZWN$(100); ZWN$(1,6)=A1$,ZWN$(7,1)="S",ZWN$(8,6)=A$(7,6),ZWN$(14)=STP(A$(175,8),3)+" "+STP(A$(183,3),3)+" "+STP(A$(186,5),3)
7417 READ (Z[16],KEY=A1$,DOM=*NEXT)ZWN$
7420 C9=1,Z9=1; GOSUB 5800
7440 RETURN 
7450 REM "EXTRACT NEW ENTRY
7455 DIM ZWN$(100); ZWN$(1,6)=A1$,ZWN$(7,1)="S",ZWN$(8,6)=A$(7,6),ZWN$(14)=STP(A$(175,8),3)+" "+STP(A$(183,3),3)+" "+STP(A$(186,5),3)
7457 WRITE (Z[16],KEY=A1$,DOM=*NEXT)ZWN$
7460 WRITE (Z[1],KEY=A1$,DOM=7250)IOL=0310; GOTO 7400
7490 RETURN 
7500 REM "GET Program / Menu Index Description
7502 X$=FNS$(P0$); DIM Q$(35); IF X$="" THEN GOTO 7540
7505 CLOSE (14); OPEN (14,ERR=7510)X$; CLOSE (14); CALL "ZZ2FNC;GET_PRG_REMARK",X$,QX$; Q$(1)=QX$; GOTO 7540
7510 P=POS("-"=X$); IF P>0 THEN X$=X$(1,P-1)+X$(P+1)
7515 IF LEN(X$)=5 THEN FIND (Z[10],KEY=X$(1,5),DOM=7542)X$; X$=X$(7,8); GOTO 7505
7540 PRINT @(28,P0),Q$,
7545 RETURN 
7550 REM "Check for existing notes
7551 IF A1$="" THEN A1$=A$(1,6)
7552 Q$="C"; CALL "ZW2ZZY",X3$,X4$,A1$,Q$,"ZWT"
7555 IF Q$="YES" THEN Q$="   (Notes)",A$(238,1)="*" ELSE Q$="(no notes)",A$(238,1)=""; ERASE "ssp"+FID(0),ERR=7556
7560 PRINT @(6,9),'BR',Q$,'ER',
7595 RETURN 
7600 REM "RESEARCH THIS ITEM
7605 L=999,L0=3,E0$=FNS$(A$(175,16)); IF E0$="" THEN GOTO 7662
7607 PRINT @(0,22),'CL',"Enter initial date to begin research -or- press enter for all:",; DIM D9$(6); CALL "ZZENTR","D",A{ALL},D9$,X4$,X3$,64,22,1,6,C0,"","","","ZW2ZZZ79","","",""; IF C0=6 THEN GOSUB 6400; C0=1
7610 READ (Z[7],KEY=A$(175,8)+A$(183,8),DOM=7611)
7615 K$=""; K$=KEY(Z[7],END=7660); READ (Z[7]); IF K$(17,6)=A$(1,6) THEN GOTO 7615
7616 IF K$(1,LEN(E0$))<>E0$ THEN K$=""; GOTO 7660
7620 READ (Z[9],KEY=K$(17,6),DOM=7615)IOL=0390
7622 IF POS(" "<>D9$)>0 THEN IF D9$>I$(7,6) THEN GOTO 7615
7625 READ (Z[14],KEY=I$(1,6),DOM=7626)
7630 NOTE$="",NOTE$=KEY(Z[14],END=7631); IF NOTE$(1,6)=I$(1,6) THEN NOTE$="NOTE" ELSE NOTE$=""
7635 GOSUB 7700
7645 GOTO 7615
7660 REM "
7662 IF L=999 THEN CALL "ZZPROM","4",X3$,0,"There are no matching entries","","",0; GOTO 1800
7665 IF L=999 THEN GOTO 7680 ELSE PRINT @(0,22),"Enter the ref#, [Return] for more, F4 to end:"
7670 DIM Q$(6); CALL "ZZENTR","SXZU",A{ALL},Q$,X4$,X3$,50,22,1,6,C0,"","",K9$,"ZW2ZZZ00","ZW2","",""; IF C0=6 THEN GOSUB 6400; C0=1
7671 IF C0=-1 THEN IF K$="" THEN GOSUB 6400; GOTO 1800 ELSE GOTO 7680
7672 IF ABS(C0)=4 THEN PRINT @(0,L0),'CE',; GOSUB 6000; GOSUB 6200; GOSUB 1800
7674 IF C0=1 THEN READ (Z[9],KEY=Q$,DOM=7665)IOL=0390; CALL "ZW2ZZD",X3$,X4$,I$,I{ALL},Z{ALL}; GOTO 7600
7680 REM 
7682 L=0
7685 PRINT @(0,L0),'CE',
7690 RETURN 
7700 REM "DISPLAY AN ITEM
7705 IF L>14 THEN GOSUB 7665
7715 PRINT @(1,L0+L),"Ref: ",I$(1,6),
7720 PRINT @(2,L0+L+1),FND$(I$(7,6)),@(4,L0+L+2),FNT$(I$(13,4)),
7725 PRINT @(15,L0+L),I$(53,60)," ",NOTE$,@(15,L0+L+1),I$(113,60),
7726 IF I$(17,6)=S$(1,6) THEN I$(17,6)=I$(242,6)
7730 DIM X$(120); FIND (Z[3],KEY="00"+I$(17,6)+"00",DOM=7731)X$; PRINT @(15,L0+L+2),X$(11,30),; GOTO 7732
7732 DIM X$(120); READ (Z[5],KEY=I$(191,3),DOM=7733)X$
7735 PRINT @(50,L0+L+2),"SSP: ",X$(4,20),
7740 PRINT @(77,L0+L+2),"(",I$(195,1),")",
7780 PRINT @(0,L0+L+3),Z0$
7790 L=L+4
7795 RETURN 
7800 REM "Print sheet
7810 IF A$(173,2)<>"WO" THEN GOTO 7840 ELSE CALL "ZZPROM",".Y",X3$,Z,"Output in Work Order Format?","","",0; IF Z=1 THEN GOTO 7840
7830 CALL "ZW2ZZI",X3$,X4$,A$,A{ALL},Z{ALL}; GOTO 7845
7840 CALL "ZW2ZZH",X3$,X4$,A$,A{ALL},Z{ALL}
7845 GOSUB 6400
7880 GOTO 1800
7900 REM "REBUILD SORT
7902 Q$=STR(Z[2]:"00")+STR(Z[7]:"00")+STR(Z[8]:"00"); CALL "ZZINIT",Q$
7905 PRINT @(0,22),'CE',@(0),"Rebuilding cross-indexes:",; READ (Z[1],KEY="",DOM=7906)
7910 A1$=KEY(Z[1],END=7940); READ (Z[1],END=7940)IOL=0310
7915 GOSUB 5800
7920 WRITE (Z[2],KEY=A2$)
7925 WRITE (Z[7],KEY=A3$)
7930 WRITE (Z[8],KEY=A4$)
7935 PRINT @(40,22),A1$,; GOTO 7910
7940 PRINT @(0,22),'CE',
7945 RETURN 
8000 EXTRACT (Z[1],KEY=A1$,KNO=0,DOM=8001)TMPA1$; REM "VIEW NOTES FOR THIS ITEM
8001 CALL "ZW2ZZY",X3$,X4$,A$(1,6),"R","ZWT"
8002 Q$="ssp"+FID(0)
8010 IF Z=10 OR Z=15 OR Z=16 THEN GOSUB 8100; GOTO 8028
8020 Q$="umask 0; cd "+H0$+"; vi "+Q$
8025 IF %USE_NOTEPAD THEN Q$=%WDX$+%ENV_COMSPEC$+" /c NOTEPAD.EXE "+%ENV_TEMP$+"\ssp"+A$(1,6)+".txt"; PRINT 'SHOW'(-1); INVOKE HIDE WAIT Q$ ELSE INVOKE Q$
8026 IF %USE_NOTEPAD THEN PRINT 'SHOW'(1)
8028 CALL "ZW2ZZY",X3$,X4$,A$(1,6),"W","ZWT"
8030 GOSUB 6400
8040 GOTO 1800
8050 REM "OLD
8060 CALL "ZW2DAA",X3$,X4$,"",A$(256,5)
8065 GOTO 8030
8070 REM "FILE LAYOUT
8075 CALL "UT2FLA"; GOTO 8030
8100 REM "Add notes
8105 IF Z=15 THEN TEMP$="clientout" ELSE IF Z=10 THEN TEMP$="internal note" ELSE TEMP$="clientin"
8106 IF %USE_NOTEPAD THEN GOSUB 10000; GOTO 8121
8110 S2$="umask 0;echo "+$220A$+"*"+TEMP$+"*"+U1$(1,3)+"*"+FND$(X3$(21,6))+"*"+FNT$(STR(INT(100*TIM):"0000"))+"*"+$0A22$
8112 IF Z=11 THEN S2$=S2$(1,LEN(S2$)-1)+N0$+$0A22$ ELSE IF Z=16 THEN S2$=S2$(1,LEN(S2$)-2)+A$(23,16)+$0A22$
8115 Q$="ssp"+FID(0); S2$=S2$+" >> "+H0$+"/"+Q$
8120 INVOKE S2$
8125 IF Z=11 THEN RETURN 
8130 Q$="umask 0; cd "+H0$+"; vi -c "+$22$+"$"+$22$+" "+Q$
8140 IF %USE_NOTEPAD THEN Q$=%WDX$+%ENV_COMSPEC$+" /c NOTEPAD.EXE "+%ENV_TEMP$+"\ssp"+A$(1,6)+".txt"; PRINT 'SHOW'(-1); INVOKE HIDE WAIT Q$ ELSE INVOKE Q$
8142 IF %USE_NOTEPAD THEN PRINT 'SHOW'(1)
8145 RETURN 
8150 REM "QUICK NOTE
8155 DIM Q$(60*4)
8160 PRINT @(0,11),'CL',@(0,12),'CL',@(0,13),'CL',@(0,14),'CL',
8165 PRINT @(0,11),'BB',"Enter quick note:",'ER',
8170 CALL "ZZENTR","S",A{ALL},Q$,X4$,X3$,18,11,1,60,C0,"","{3"+X$,"","","","","03V"; IF POS(" "<>Q$)=0 THEN GOTO 8190
8172 P=POS($22$=Q$); IF P>0 THEN Q$(P,1)="'"; GOTO 8172
8175 N0$=""; FOR X=0 TO 3; X$=FNS$(Q$(1+X*60,60)); IF X$>"" THEN N0$=N0$+X$+$0A$
8176 NEXT X
8180 CALL "ZZPROM","YE",X3$,S3,"Add this new note?","","",0; ON S3 GOTO 8181,8170,8030
8185 Q$="ssp"+FID(0); GOSUB 8100
8195 GOTO 8028
8200 REM "Check for open items
8202 CALL "ZW2ZCA",X3$,X4$,"",A9$
8205 DIM Z2$(0),X[16],Z1$(0)
8210 READ (Z[8],KEY=A$(240,10),DOM=8211)
8215 X$=KEY(Z[8],END=8250); READ (Z[8]); IF X$(1,10)<>A$(240,10) THEN GOTO 8250
8217 XP=PRM('XI'); SET_PARAM 'XI'
8220 READ (Z[9],KEY=X$(11),DOM=8215)X$
8221 SET_PARAM 'XI'=XP
8222 IF FNS$(A9$(1,2))>"" THEN IF A9$(1,2)<>X$(173,2) THEN GOTO 8215
8224 IF FNS$(A9$(3,3))>"" THEN IF A9$(3,3)<>X$(191,3) THEN GOTO 8215
8226 IF FNS$(A9$(6,15))>"" THEN IF POS(X$(195,1)=FNS$(A9$(6,15)))>0 THEN GOTO 8215
8228 IF X$(208,6)<A9$(21,6) OR X$(208,6)>A9$(27,6) THEN GOTO 8215
8230 IF X$(228,6)<A9$(33,6) OR X$(228,6)>A9$(39,6) THEN GOTO 8215
8232 IF X$(222,6)<A9$(45,6) OR X$(222,6)>A9$(51,6) THEN GOTO 8215
8234 IF FNS$(A9$(57,6))>"" THEN IF POS(X$(206,1)=A9$(57,6))=0 THEN GOTO 8215
8236 IF FNS$(A9$(63,3))>"" THEN IF X$(250,3)<>A9$(63,3) THEN GOTO 8215
8238 IF FNS$(A9$(66,3))>"" THEN IF X$(253,3)<>A9$(66,3) THEN GOTO 8215
8240 IF A9$(69,1)>" " THEN IF X$(239,1)<>A9$(69,1) THEN GOTO 8215
8242 IF X$(7,6)<A9$(70,6) OR X$(7,6)>A9$(76,6) THEN GOTO 8215
8244 IF X$(196,6)<A9$(82,6) OR X$(196,6)>A9$(88,6) THEN GOTO 8215
8246 IF A9$(94,1)>" " THEN IF X$(195,1)<>A9$(94,1) THEN GOTO 8215
8248 P=POS(X$(7,6)<=Z1$,12); IF P=0 THEN Z1$=X$(7,6)+X$(1,6)+Z1$ ELSE Z1$=Z1$(1,P-1)+X$(7,6)+X$(1,6)+Z1$(P)
8249 GOTO 8215
8250 REM "DISPLAY
8300 REM "OPEN ISSUES - BY DATE
8305 IF Z1$="" THEN GOTO 8360 ELSE L=0
8310 PRINT @(0,8),'CE',
8320 FOR X=1 TO LEN(Z1$) STEP 12
8325 L=L+1; IF L+8>18 THEN CALL "ZZPROM","8B",X3$,S3,"More issues remaining","","",S9; IF S3=2 THEN EXITTO 8350 ELSE IF S3=0 THEN L=1; PRINT @(0,8),'CE', ELSE F9$=Z1$((S9-1)*12+7,6); EXITTO 8347
8327 XP=PRM('XI'); SET_PARAM 'XI'
8328 READ (Z[9],KEY=Z1$(X+6,6),DOM=8340)X$
8329 SET_PARAM 'XI'=XP
8330 PRINT @(0,L+8),STR(1+(X-1)/12:"##0"),".  ",FND$(X$(7,6))," ",X$(1,6)," ",X$(195,1),"  ",X$(23,15),"  ",X$(173,2),"   ",X$(191,3),; IF X$(238,1)<>" " THEN PRINT @(72),"(Notes)",
8335 L=L+1; PRINT @(12,L+8),X$(53,60); IF POS(" "<>X$(113,60))>0 THEN L=L+1; PRINT @(12,L+8),X$(113,60),
8340 NEXT X
8345 CALL "ZZPROM","8B",X3$,S3,"End of list.","","",S9; IF S3<>-1 THEN GOTO 8350
8347 F9$=Z1$((S9-1)*12+7,6)
8350 REM 
8360 GOSUB 6000
8390 IF F9$="" THEN GOTO 1000 ELSE GOTO 1010
8400 REM 
8405 PRINT @(18,3),"CLIENT ",
8420 CALL "ZZENTR","AUZX",A{ALL},A$,X4$,X3$,18,5,240,10,C0,"S/S","{3"+X$,"","ZW2ZZZ06","SS1","SS2MAZ",""; IF C0=6 THEN GOSUB 6400; C0=1
8422 IF C0<0 THEN GOTO 1000
8425 READ (Z[3],KEY=A$(240,10),DOM=8420)IOL=0330
8430 PRINT @(32,5),C$(11,35),@(43,6),FNP$(C$(195,14)),
8445 GOTO 8200
8450 SEND_EMAIL:REM "Keith, need routine to send e-mail and if possible record an automatic internal note that it was sent.
8455 CALL "ZW2ZZE",X3$,X4$,A1$,Z{ALL}
8490 RETURN 
8500 REM "HOLD / MESSAGES
8502 PRINT @(68,5),'CL',@(68,6),'CL',
8506 X$=""; IF C$(223,1)="Y" THEN X$=X$+"Credit Hold" ELSE IF C$(223,1)="S" THEN X$=X$+"No SSP agree" ELSE IF C$(223,1)="X" THEN X$=X$+"NO Web/VP Spt" ELSE IF C$(223,1)="W" THEN X$=X$+"Watch AR Bal"
8580 IF X$>"" THEN IF %WDX$="" THEN PRINT @(68,5),'BB',X$,'EB', ELSE PRINT @(68,5),'_YELLOW',X$,'_BLUE'
8584 IF C$(691,1)="A" THEN IF %WDX$="" THEN PRINT @(68,6),'BB',"PLATINUM",'EB',; GOTO 8586 ELSE PRINT @(68,6),'_YELLOW',"PLATINUM",'_BLUE'; GOTO 8586
8585 IF C$(122,2)="CA" THEN IF %WDX$="" THEN PRINT @(68,6),'BB',"California",'EB', ELSE PRINT @(68,6),'_RED',"California",'_BLUE'
8595 RETURN 
8600 REM "Current Status
8601 CALL "ZZPROM","Y",X3$,S9,"Only look at your issues (SSP="+U1$(1,3)+")","","",0
8602 CALL "ZZPROM","Y",X3$,S8,"Only look at today's open calls","","",0; S8$=""; IF S8=0 THEN S8$=X3$(21,6)
8604 PRINT @(20,22),"Looking at open issues:",
8605 DIM S0[40,40]; R0=0,S0=0,S1=0,S2=0,S3=0,S4=0
8610 CLOSE (14); OPEN (14)"ZWI"
8612 SET_PARAM -'XI'
8615 READ (14,END=8630)Q$; READ (Z[1],KEY=Q$(1,6),DOM=8616)IOL=0310; IF S8$>"" THEN IF A$(7,6)<>S8$ THEN GOTO 8615
8616 R0=R0+1; IF MOD(R0,25-24*SGN(LEN(S8$)))=0 THEN PRINT @(44,22),R0:"##,##0",
8617 IF A$(195,1)="G" OR A$(195,1)="S" OR A$(173,2)="D " THEN GOTO 8615 ELSE IF A$(195,1)="0" THEN ESCAPE 
8618 IF S9=0 THEN IF A$(191,3)<>U1$(1,3) THEN GOTO 8615
8619 IF A$(195,1)="4" THEN P0=12,P1=12; GOTO 8622
8620 P0=POS(A$(195,1)=S1$),P1=POS(A$(173,2)=S0$,2); P1=INT(P1/2); IF P0>0 THEN P0=P0-1
8622 S0[P0,P1]=S0[P0,P1]+1,S0=S0+1
8625 GOTO 8615
8630 SET_PARAM 'XI'
8640 IF POS("8."=SYS)<>0 THEN PRINT @(0,3),'CE','MO','T2', ELSE PRINT @(0,3),'CE','T2',
8641 IF S9=0 THEN PRINT U1$(1,3),"  ",U1$(4,20)
8642 L9=135,L8=24; DIM Y5$(L9*L8)
8643 DIM Y5$(4000)
8644 FOR X=1 TO LEN(S0$) STEP 2; Y5$(18+X*2+L9*3,2)=S0$(X,2); NEXT X
8645 FOR X=1 TO LEN(S1$); READ (Z[6],KEY=S1$(X,1))Q$; Y5$(1+L9*(X+3))=Q$(1,1)+" "+Q$(2,20); NEXT X
8650 FOR Z1=0 TO INT(LEN(S0$)/2)-1; FOR Z0=0 TO LEN(S1$)-1
8654 Y5$(18+Z1*4+L9*(4+Z0),LEN(M3$))=STR(S0[Z0,Z1]:M3$),S8=S8+S0[Z0,Z1]
8656 IF POS(S1$(Z0+1,1)=S4$)>0 THEN S1=S1+S0[Z0,Z1]
8657 IF S1$(Z0+1,1)="B" THEN S2=S2+S0[Z0,Z1] ELSE IF S1$(Z0+1,1)="W" THEN S2=S2+S0[Z0,Z1] ELSE IF S1$(Z0+1,1)="U" THEN S4=S4+S0[Z0,Z1]
8658 IF S0$(Z1*2+1,2)="EH" THEN S3=S3+S0[Z0,Z1]
8660 NEXT Z0; Y5$(18+Z1*4+L9*(4+Z0),LEN(M3$))=STR(S8:M3$); S8=0; NEXT Z1
8665 REM "LET Y5$(L9*(L8-1))="TOTAL CALLS: "+STR(S0:M3$)+"  WO RELATED: "+STR(S1:M3$)+"  WAITING ON CUST: "+STR(S2:M3$)+" EH REQ: "+STR(S3:M3$)+" CUST NEEDS UPDATE: "+STR(S4:M3$)+"  NET OPEN SUPPORT: "+STR(S0-S1-S2-S3-S4:M3$)
8668 PRINT @(0,3),'CE'; FOR J=1 TO L8-1; PRINT @(1,J-1),Y5$(L9*J,L9),; NEXT J
8670 CALL "ZZPROM","4",X3$,0,"","","",0
8675 PRINT 'CS'; L8=22; Y5$(L9*(L8-1))="Total calls: "+STR(S0:M3$)+"  WO related: "+STR(S1:M3$)+"  waiting on cust: "+STR(S2:M3$)+" EH Req: "+STR(S3:M3$)+" Cust needs update: "+STR(S4:M3$)+"  Net open support: "+STR(S0-S1-S2-S3-S4:M3$)
8676 PRINT @(0,3),'CE'; FOR J=20 TO L8-1; PRINT @(1,J-1),Y5$(L9*J,L9),; NEXT J
8679 CALL "ZZPROM","4",X3$,0,"","","",0
8680 IF POS("8."=SYS)<>0 THEN PRINT @(0,3),'MN','T1' ELSE PRINT @(0,3),'T1'
8685 A$(1)=""
8690 GOSUB 6400
8695 GOTO 1200
8700 REM "Customer Profile
8720 CALL "SS2MAZ",X3$,X4$,A$(240,10),Q1$
8740 GOSUB 6400
8745 GOTO 1800
8750 REM SEND MAIL
8755 DIM Q$(20),A0$(55); PRINT @(0,21),'CL',@(4,21),"Send mail to: "
8757 IF AUTOSSP$<>"" THEN Q$=AUTOSSP$,A0$="Premium support"; GOTO 8764
8758 CALL "ZZENTR","S",Q{ALL},Q$,X4$,X3$,18,21,1,20,C0,"","","","","","",""
8760 PRINT @(0,21),'CL',; Q$=FNS$(Q$); IF Q$="" THEN GOTO 8790
8762 PRINT @(0,21),'CL',@(9,21),"Message:",; CALL "ZZENTR","S",Q{ALL},A0$,X4$,X3$,18,21,1,55,C0,"","","","","","",""; A0$=FNS$(A0$); PRINT @(0,21),'CL'
8764 MAIL_MSG$=""; IF A0$>"" THEN MAIL_MSG$="Message: "+A0$,A0$=">>> "+A0$+" <<<"
8765 A0$=A0$+"\n"+"Categ: "+A$(173,2)+"  Status: "+A$(195,1)+"  Current SSP: "+A$(191,3)+"  Date entered: "+FND$(A$(7,6))+"\n"+"Solution provider: "+A$(250,3)+"  Responsible person: "+A$(253,3)+"  Scheduled: "+FND$(A$(228,6))+""
8771 S3$="echo "+$22$+A0$+"\n\n"+FNS$(A$(53,60)); IF FNS$(A$(113,60))>"" THEN S3$=S3$+"\n"+FNS$(A$(113,60))
8772 S3$=S3$+"\n"+$22$+" |mail -s "+$22$+"SSP#"+A$(1,6)+"/"+STR(NUM(C$(3,6)))+" "+FNS$(C$(11,35))+$22$+" "+Q$
8775 INVOKE S3$
8776 IF AUTOSSP$<>"" THEN GOTO 8787
8778 READ (Z[14],KEY=A1$+$FF$,DOM=8779)
8780 P_KEY$=KEP(Z[14]); READ (Z[14],KEY=P_KEY$)A14$; IF A1$=A14$(1,LEN(A1$)) THEN A14$(7,6)=STR(NUM(A14$(7,6))+1:"000000") ELSE A14$=A1$+"000001"
8782 S3$="*internal e-mail from "+U1$(1,3)+"*"+FND$(X3$(21,6))+"*"+FNT$(STR(INT(100*TIM):"0000"))+"*"+" sent to: "+Q$
8783 NOTE_KEY$=A14$(1,12); S3$=PAD(NOTE_KEY$+S3$,125)
8784 WRITE (Z[14],KEY=NOTE_KEY$)S3$
8785 IF MAIL_MSG$<>"" THEN NOTE_KEY$=NOTE_KEY$(1,6)+STR(NUM(NOTE_KEY$(7,6))+1:"000000"); S3$=PAD(NOTE_KEY$+MAIL_MSG$,125); WRITE (Z[14],KEY=NOTE_KEY$)S3$
8786 IF MAIL_MSG$<>"" THEN NOTE_KEY$=NOTE_KEY$(1,6)+STR(NUM(NOTE_KEY$(7,6))+1:"000000"); S3$=PAD(NOTE_KEY$,125); WRITE (Z[14],KEY=NOTE_KEY$)S3$
8787 IF AUTOSSP$<>"" THEN RETURN 
8790 GOSUB 6130; GOSUB 6250; GOTO 1800
8800 REM "NEXT SSP NUMBER
8810 EXTRACT (Z[11],KEY="SSP",DOM=8811)Q$,Q0; A$(1,6)=STR(Q0:"000000"),Q0=Q0+1
8815 WRITE (Z[11],KEY="SSP")"SSP",Q0
8845 RETURN 
8850 REM "LAST NUMBER
8855 READ (Z[11],KEY="SSP")*,Q0
8860 Q0=Q0-1
8865 A$(1,6)=STR(Q0:"000000")
8870 PRINT @(18,3),A$(1,6)
8895 RETURN 
8905 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
8910 DEF FND$(Z9$)=Z9$(3,2)+"/"+Z9$(5,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8920 DEF FNB$(Z9$)=X3$(214-6*NUM(Z9$),1)
8925 DEF FNS$(Z9$)=Z9$(1,POS(S$=Z9$+S$)-1)
8950 REM "Get archive related information
8955 INVOKE "clear; bblog.pvx "+A$(1,6)
8958 CALL "ZZPROM","4",X3$,0,"","","",0
8960 GOSUB 6400; GOTO 1800
8970 REM "Enter today's time for this call               
8972 IF STP(A$(1,6),2)="" THEN RETURN 
8975 CALL "TK2MAE",X3$,X4$,A$(1,6),A$(240,10)
8980 GOSUB 6400
8985 DOTIME=0
8990 RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9120 ON Y8 GOTO 9900,0990,9920
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9740 RETURN 
9750 REM "FILES
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9790 RETURN 
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9905 Y8=2
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9945 REM IF P9$="*" THEN PRINT @(0,22),'CE',; RELEASE
9950 RUN "ZMENU"
9999 END 
10000 ADD_LINE_TO_FILE:! IF %USE_NOTEPAD is on add line to file
10005 TXTCH=HFN; OPEN LOCK (TXTCH,ERR=*RETURN)%WDX$+%ENV_TEMP$+"\ssp"+A$(1,6)+".txt"
10010 S2$='LF'+'LF'+"*"+TEMP$+"*"+U1$(1,3)+"*"+FND$(X3$(21,6))+"*"+FNT$(STR(INT(100*TIM):"0000"))+"*"+'LF'
10012 IF Z=11 THEN S2$=S2$(1,LEN(S2$)-1)+N0$+'LF' ELSE IF Z=16 THEN S2$=S2$(1,LEN(S2$)-3)+A$(23,16)+'LF'
10050 PRINT (TXTCH)S2$
10090 CLOSE (TXTCH)
10095 RETURN 
10099 ! ***********************************************************
