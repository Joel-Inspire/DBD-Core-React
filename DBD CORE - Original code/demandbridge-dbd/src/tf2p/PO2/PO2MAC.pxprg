0010 REM "Set Lot Inventory Flag in IC0 from P/O <PO2MAC>
0020 SETESC 9300; SETERR 9000
0035 REM "4.4 - 01/26/99 - 12.46 - dxm - SSP# 099149
0040 REM "Copyright 1999 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0085 REM "P$ =key for P03 - P$(1,9)=P/O #, P$(10,3)=P/O line #
0086 REM "P0$=p/o type, (173,1) from PO1 record, 'C' or 'S'
0087 REM "I$=IC0 record, at least 112 bytes long, will be reset depending upon results
0090 CLEAR ; ENTER X3$,X4$,P$,P0$,I$
0100 SETERR 9000
0110 X0$="PO2MAC",X1$="Set Lot Inventory Flag in IC0, if needed",K9$="",K9=0
0300 REM "IOLISTS
0310 IOLIST A$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X PO3...  02O IC0...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 READ (Z[13],KEY=X3$(9,3)+"I/C")P9$
0540 DIM OSEC$(255); FIND (Z[13],KEY="osec"+X3$(40,3),DOM=0541)OSEC$
0550 DIM LT$(512); READ (Z[13],KEY=X3$(9,3)+"F/M",DOM=0551)LT$; REM "W/O 99149
1000 REM "Check conditions first
1010 IF P9$(44,1)<>"Y" THEN GOTO 9900; REM "No lot inventory
1015 IF I$(112,1)="Y" OR POS(X3$(9,3)="020",3)>0 THEN GOTO 9900; REM "Already a lotted item
1020 IF I$(112,1)="N" AND OSEC$(60,1)="Y" THEN GOTO 9900; REM "WO91158
1100 REM " Look for D type record in PO3
1110 IF P0$="C" THEN P1$=P$(1,8)+" "+P$(10,3) ELSE GOTO 1300
1120 READ (Z[1],KEY=P1$,DOM=1121)
1125 K$=KEY(Z[1],END=1200)
1130 IF K$(1,9)<>P1$(1,9) OR (K$(10,3)<>"   " AND K$(10,3)<>P1$(10,3)) THEN GOTO 1200
1135 READ (Z[1],KEY=K$,DOM=1125)A$
1140 IF A$(11,1)="D" THEN GOTO 1300
1145 GOTO 1125
1200 REM "If here, then no PO3 records with type D were found, so exit
1210 GOTO 9900
1300 REM "We found at least one D type PO3 record, so ask the question
1305 IF LT$(278,1)="Y" THEN GOTO 1360; REM "WO99149
1310 CALL "ZZPROM",".Y",X3$,Z,"Do you want to track lots for this item?","","",0
1320 IF Z<>0 THEN GOTO 1200
1350 REM "Set the lot flag and reset I$ to new information
1360 EXTRACT RECORD (Z[2],KEY=I$(1,20),DOM=1200)I0$
1365 I0$(112,1)="Y"
1370 WRITE RECORD (Z[2],KEY=I$(1,20))I0$
1375 I$=I0$(1,LEN(I$))
1390 GOTO 9900
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9705 GOSUB 7440
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9715 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9730 RUN U1$(1,6)
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
