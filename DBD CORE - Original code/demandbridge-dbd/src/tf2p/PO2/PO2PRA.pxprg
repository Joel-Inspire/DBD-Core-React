0010 REM "Warehouse Receiving Report Printing <PO2PRA>
0035 REM "5.7 - 04/08/24 - 15.064195 - crg - SSP# 307480
0037 REM "307480-DBD-378: Email to warehouse                                 
0040 REM "Copyright 2024 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0045 ! PO2PRA was created for WO239078, copied from PO2PAB as a start
0050 SETERR 0060; ENTER Q0$; CALLED$="Y"; GOTO 0091
0080 REM 
0085 IF J9=-99 THEN J9=0; SETERR 9000; GOTO 0650
0087 IF %GUI THEN CLEAR ; GOTO 0091
0090 BEGIN 
0091 SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="PO2PRA",X1$="Warehouse Receiving Report Printing"
0120 DIM A$(0),B$(82),B[27],C$(350),L$(21),L[1],H$(315),H0$(315),D6$(402),B0$(204),B0[21],Z8$(20),B1$(356),B1[29],D62$(402),DIV$(402)
0140 M0$="######.00-",M1$="#####,###.00-",M2$="##,###,##0-",M3$="###,##0-",M4$="-00000000.00",M4=LEN(M4$),M6$="###.0-",M5$=M0$(1,LEN(M0$)-1)+"#"
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNE$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+Z9$(1,2)
0180 DEF FNS$(Z9$)=Z9$(1,POS(Z8$=Z9$+Z8$)-1)
0185 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
0190 DEF FNT$(Z9$)=Z9$(1,POS(Z8$=Z9$+Z8$))
0199 ! 
0210 T=1,T0=1,T1=1
0220 W3=120
0230 DIM T3$(50,"*"),Y5$(131),Y6$(131),W[4],E[3]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9930
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0299 ! 
0300 REM "I/O lists
0305 REM "IOLIST FOR PO1 - Purchase order header
0310 IOLIST A$(1)
0312 IOLIST Y8$,Y8[0],Y8[1],Y8[2],Y8[3],X9$
0313 IOLIST X9[0],Y9[0],X9[1],Y9[1],X9[2],Y9[2],X9[3],Y9[3],X9[4],Y9[4],X9[5],Y9[5],X9[6],Y9[6],X9[7],Y9[7],X9[8],Y9[8],X9[9],Y9[9],X9[10],Y9[10],X9[11],Y9[11],X9[12],Y9[12],X9[13],Y9[13],X9[14],Y9[14]
0315 REM "IOLIST FOR PO2 - Purchase order lines
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19],B[20],B[21],B[22],B[23],B[24],B[25],B[26],B[27]
0325 REM "IOLIST FOR FS1 - Sales order Header
0330 IOLIST C$(1),DEP_AMT
0335 REM "IOLIST FOR AR1 - Customer Masterfile
0340 IOLIST D$
0345 REM "IOLIST FOR FM0 - Location Masterfile
0350 IOLIST E$,E[0],E[1]
0373 IOLIST G2$
0375 REM "IOLISTS FOR AR3 - Salesperson
0380 IOLIST H$
0382 IOLIST H0$
0383 IOLIST H1$
0420 IOLIST L$,L[0],L[1]
0425 REM "IOLIST FOR FSH - O/P Division Information
0430 IOLIST D6$
0490 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0495 IOLIST X$(1),X[0],X[1],X[2],X[3],X[4],X[5],X[6],X[7],X[8],X[9],X[10],X[11],X[12],X[13],X[14],X[15],X[16],X[17],X[18],X[19],X[20],X[21],X[22],X[23],X[24],X[25],X[26],X[27],X[28],X[29],X[30],X[31],X[32],X[33],X[34],X[35],X[36],X[37],X[38],X[39],X[40]
0499 ! 
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="02O PO1...  03O PO2...  05O IC0...  06O PO3...  07O FM0...  08O FMP...  10O FS2...  11O POE...  13O ZZPARM  15O AR3...  16O AP4...  19O FS1... "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0535 B9=0; CALL "BC2VER",ERR=0536,"","",""; B9=1; REM "B9=1 means use Barcode module
0540 IF B9=1 THEN GOTO 0550 ELSE FIND (Z[13],DOM=0541,KEY="FORMATRR"+V0$(60,1)); GOTO 0550
0545 CALL "ZZPROM",".4",X3$,0,"Invalid Flexible Format Code: "+V0$(60,1),"","",0; GOTO 9900
0550 FIND (Z[13],KEY=X3$(9,3)+"F/M")P9$
0560 FIND (Z[13],KEY=X3$(9,3)+"A/R")P8$
0570 FIND (Z[13],KEY=X3$(9,3)+"AR1")P4$
0585 RT_PARM$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,RT_PARM$
0599 ! 
0600 REM "READ IN FORMATTING, SET W3 ETC.
0605 M0=14; REM "Number of fields on flexible format
0610 DIM X9[M0],Y9[M0],X9$(M0),Y8[3],Y8$(35)
0615 IF B9=1 THEN DIM D9$(11); D9$(1)="RR",D9$(11,1)=V0$(60,1); CALL "BC2FMZ",X3$,X4$,D9$,A0$,A0{ALL},X9{ALL},X9$,Q1$,Q{ALL},C0; IF C0=0 THEN Y8[0]=A0[0],Y8[1]=A0[2],Y8[2]=A0[4],Y8[3]=A0[5],Y9[32]=A0[6]; GOTO 0625 ELSE B9=0; GOTO 0540
0620 GOSUB SET_READONLY; READ (Z[13],KEY="FORMATRR"+V0$(60,1),DOM=*NEXT)IOL=0312,IOL=0313; GOSUB CLEAR_READONLY; GOTO 0625
0621 GOSUB CLEAR_READONLY; CALL "ZZPROM",".4",X3$,0,"Invalid Flexible Format Code: "+V0$(60,1),"","",0; GOTO 9900
0655 W3=Y8[1]
0660 DIM Y5$(W3),Y6$(W3)
0665 GOSUB 8650
0699 ! 
0700 REM "Open Printer
0730 CALL "ZZDAPP",X3$,"DZZFLESZZENTRZZCOMP"
0735 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0736,9930
0740 W0=60
0745 PMODE=0,X$=V3$,F_CODE$="RR"+DIM(8)+V0$(60,1); CALL "EF2PRM",ERR=0746,X3$,X4$,X$,P6$,F6$,F_CODE$,ITEM_PARM$; IF P6$(144,2)="JF" THEN JETFORM_MODE=1,JETFORM_DEV=W9,PMODE=JETFORM_MODE ELSE IF P6$(144,2)="UF" THEN UNFORM_MODE=2,UNFORM_DEV=W9,PMODE=UNFORM_MODE ! SSP 241023
0750 GOSUB 8100
0755 IF W6$<>"" THEN Y$="12O "+W6$+" "; CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1; W6=Z[12]; IF W6$(1,1)="N" THEN X$=FN%FID$(Z[12]); CALL "EF2PRM",ERR=0756,X3$,X4$,X$,P6$,F6$
0799 ! 
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840
0820 DIM U[1]; U=9,U0=1,U[0]=0,U[1]=9,T0=0,U0$="    ",T5$="                "; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0899 ! 
0900 REM " Position read"
0905 REM "set key file to read from"
0910 U1=Z[2]
0920 IF V0=1 THEN V2$=V2$(1,18)
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0959 ! 
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
0995 IF Q5=0 THEN IF CALLED$<>"Y" THEN GOSUB 6000 ! Alignment pattern
0999 ! 
1000 REM "Read next record
1005 U$=KEY(U1,END=5000); READ (U1)
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])<V2$(U3,U[U9]) THEN EXITTO 1000
1030 IF U$(U2,U[U9])>V2$(U3+U[U9],U[U9]) THEN IF U=1 THEN EXITTO 5000 ELSE EXITTO 1000
1040 NEXT U9
1099 ! 
1100 REM "Get record"
1110 U9$=U$
1120 ON V0 GOTO 1150,1150,1130,1140
1150 DIM A$(383); READ (Z[2],KEY=U$(1,9),ERR=1000,DOM=1000)IOL=0310
1160 IF Q5=1 THEN CALL "ZZDISP","A",A$(297,9),"P/O",X3$,"","",34,21,X4$; GOSUB 7550; GOTO 1000
1170 RR_NUM$=A$(362,5); IF STP(RR_NUM$,3)="" THEN GOSUB GET_RR_NUM
1199 ! 
1300 REM "Read other data
1305 P0=1,FIRST_PASS=1
1310 Y$="05C IC0...  06C PO3...  10C FS2...  11C POE...  05O AR1...  06O AP2...  10O FSH...  11O FSV...  "
1315 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
1317 XP=PRM('XI'); SET_PARAM 'XI'
1320 DIM C$(350); FIND (Z[19],KEY=A$(306,8),DOM=1323)IOL=0330
1321 SET_PARAM 'XI'=XP
1323 IF D6$(1,2)<>A$(297,2) THEN DIM D6$(402); FIND (Z[10],KEY=A$(297,2),DOM=1324)IOL=0430
1335 DIM D$(225); FIND (Z[5],KEY=A$(179,10),DOM=1336)IOL=0340
1338 IF P4$(64,1)<>"Y" OR P8$(109,1)<>"Y" THEN GOTO 1340 ELSE FIND (Z[11],KEY=A$(306,8),DOM=1339)X$; D$(56,87)=X$(54,87); IF POS(X9$(1,1)="N",1)<>0 AND X$(19,35)<>DIM(35) THEN D$(11,35)=X$(19,35)
1340 IF A$(173,1)="S" THEN C$(89,8)=A$(347,4)+A$(343,4),C$(28,15)=A$(328,15)
1350 DIM H$(315); FIND (Z[15],KEY=C$(89,4),DOM=1351)IOL=0380
1355 DIM H0$(315); FIND (Z[15],KEY=C$(93,4),DOM=1356)IOL=0382
1360 DIM G2$(426); FIND (Z[16],KEY=A$(7,10),DOM=1361)IOL=0373
1365 DIM H1$(52); FIND (Z[6],KEY=A$(210,2),DOM=1366)IOL=0383
1370 Y$="05C AR1...  06C AP2...  10C FSH...  11C FSV...  05O IC0...  06O PO3...  10O FS2...  11O POE...  "
1375 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
1380 GOSUB 7500
1399 ! 
1500 REM "Print Data
1515 P1=1,P2=1
1520 IF P1>P0 THEN GOTO 1700
1530 GOSUB 5400
1540 GOSUB 5500
1560 GOSUB 2000
1580 P1=P1+1
1590 GOTO 1520
1599 ! 
1900 REM "Accumulate Totals"
1990 T[T0,0]=T[T0,0]+1
1992 IF DO_2 THEN IF FIRST_PASS THEN FIRST_PASS=0,HOLD_W9=W9,W9=W6,HOLD_JF_MODE=JETFORM_MODE,JETFORM_MODE=0; GOTO 1380 ELSE W9=HOLD_W9,JETFORM_MODE=HOLD_JF_MODE
1993 GOSUB UPDATE_RR_NUM
1995 GOTO 1000
1999 ! 
2000 REM "Print a Receiving Report
2005 S0$="",S2$=""
2007 IF B9=1 THEN F5=0,F5$=""; GOSUB 8300; REM "To print headings and messages for this item
2010 GOSUB 2100
2020 GOSUB 2150
2030 GOSUB 2200
2040 GOSUB 2350
2050 GOSUB 2400
2070 IF POS("X"<>A$(297,9))=0 THEN RETURN 
2081 IF P0>1 AND P1=1 THEN GOSUB 8850
2085 GOSUB 2900
2090 RETURN 
2099 ! 
2100 REM "Print very top
2105 IF JETFORM_MODE THEN P$="" ELSE DIM P$(Y8[0]*W3)
2115 F5=1,F5$=RR_NUM$; GOSUB 8300 ! Whse receiving report number
2120 CALL "ZZDISP","AX",A$(297,9),"P/O",X3$,X$,"",0,0,X4$; P5$=X$
2125 F5=2,F5$=X$; GOSUB 8300 ! P/O number - formatted
2130 F5=3,F5$=A$(297,9); GOSUB 8300 ! P/O number - unformatted
2140 RETURN 
2145 ! 
2150 REM "Order header info
2155 CALL "ZZDISP","AX",A$(7,10),"A/P",X3$,X$,"",0,0,X4$
2160 F5=4,F5$=X$; GOSUB 8300 ! Vendor code
2165 F5=12,F5$=A$(248,12); GOSUB 8300 ! This job number
2180 F5=10,X$=STR(INT(TIM):"00")+STR(100*FPT(TIM):"00"); CALL "ZZDISP","TX",X$,"",X3$,F5$,"",0,0,X4$; X$=DAY,X$=X$(7,2)+X$(1,2)+X$(4,2); F5$=FNE$(X$)+" "+F5$; IF X9$(F5,1)="H" THEN F5$="Printed: "+F5$
2182 GOSUB 8300
2185 IF A$(367,1)<>" " AND X9$(11,1)=" " THEN F5=11,F5$="*REPRINT* ("+A$(367,1)+")"; GOSUB 8300 ELSE IF A$(367,1)<>" " AND X9$(11,1)="A" THEN F5=11,F5$="AMENDED ("+A$(367,1)+")"; GOSUB 8300 ! Reprint message
2190 RETURN 
2195 ! 
2200 ! Customer, Vendor
2250 F5=7,F5$=D$(11,35); GOSUB 8300 ! Customer name
2280 F5=5,F5$=A$(21,35); GOSUB 8300 ! Vendor name
2295 RETURN 
2299 ! 
2350 REM "Print bottom line
2355 CALL "ZZDISP","AX",A$(179,10),"A/R",X3$,X$,"",0,0,X4$
2360 F5=6,F5$=X$; GOSUB 8300 ! Customer code
2390 RETURN 
2399 ! 
2400 REM "Print line information
2405 I0=(Y8[2]-1)*W3,I1=0,S0$="",B1=0,B7=0,P7$="",B5=0,B6=0,B8=0,PO_LINE=0
2406 IF C1>2 THEN GOTO 2450 ELSE IF C1=1 AND LEN(P2$)=13 THEN GOTO 2450
2407 IF POS("X"<>A$(297,9))=0 THEN FOR I3=1 TO Y8[3]; GOSUB 2500; NEXT I3
2410 READ (Z[3],KEY=A$(297,9),DOM=2411)
2420 READ (Z[3],END=2490)IOL=0320
2425 IF B$(82,9)<>A$(297,9) THEN GOTO 2490
2427 IF POS(B$(10,1)="MS")<>0 THEN GOTO 2420 ! Don't output message and special lines
2430 IF C1<>2 THEN B1=0
2431 PO_LINE=PO_LINE+1
2432 GOSUB 7850
2434 REM "S2$ is flag to print comments only
2435 IF POS(B$(10,1)="CXI")<>0 AND S0$="" THEN IF B$(81,1)="Y" THEN S0$=B$(91,20)+"001",S8$=A$(297,8)+B$(7,3),S2$=""; GOSUB 8950 ELSE IF B$(81,1)="C" THEN S0$=B$(91,20),S2$="*"; GOSUB 8950
2438 IF I0+F4*W3>(Y8[2]+Y8[3]-1)*W3 THEN PO_LINE=0; GOSUB 4600
2440 GOSUB 2500
2445 GOTO 2420
2450 REM "Print lines from P2$ for spec instr."
2460 READ (Z[6],KEY=P2$(1,13),DOM=2461)IOL=0350
2465 READ (Z[3],KEY=E$(166,9)+E$(7,3),DOM=2466)IOL=0320
2466 READ (Z[3],KEY=A$(297,9)+E$(7,3),DOM=2475)IOL=0320
2470 B1=E[0]; GOSUB 8900; IF L$(20,1)="Y" THEN B1=B1/L[1] ELSE B1=B1/B[2]
2471 GOSUB 2500
2475 P2$=P2$(14)
2490 PO_LINE=0
2495 RETURN 
2499 ! 
2500 REM "Print one line from B$, I0 is assumed to be set
2503 F4=0,P3=0
2505 IF B$(10,1)="M" THEN F5=9,F5$=B$(28,40); GOSUB 8350; GOTO 2535 ! Description
2506 IF B[2]=0 THEN B[2]=1 ! Quantity per U/M
2508 GOSUB 8500; GOSUB 8900
2509 IF L$(21,1)="Y" OR (B$(10,1)="S" AND I$(61,1)="Y") THEN IF C1<2 THEN P3=B[4]; IF L$(21,1)="Y" THEN B[0]=B[4]
2510 REM "
2511 UM$=B$(77,4),UM0=B[2]
2512 F5=14,F5$=UM$; IF MID(X9$,F5,1)="Q" THEN F5$=FNS$(F5$)+"/"+FNR$(STR(UM0:M3$)); GOSUB 8350 ELSE GOSUB 8350 ! Unit of measure
2513 IF B1=0 THEN IF L$(20,1)="Y" THEN B1=B[11]/L[1] ELSE B1=B[11]/B[2]
2515 IF B1<>0 THEN IF FPT(B1)=0 THEN F5=13,F5$=STR(B1:M2$); GOSUB 8350 ELSE F5=13,F5$=STR(B1:M0$); GOSUB 8350 ! Quantity
2517 IF B$(10,1)="S" AND I$(61,1)="Y" THEN GOTO 2530
2520 IF L$(21,1)="Y" OR B[1]=0 THEN P3=B[0] ELSE IF L$(20,1)="Y" AND L[0]<>0 THEN P3=B[0]*B1/L[0] ELSE P3=B[0]*B1
2530 B7=B7+P3,B8=B8+B1
2533 F5=8,F5$=FNS$(B$(101,10)); GOSUB 8350 ! Item code
2590 I0=I0+F4*W3
2595 RETURN 
2599 ! 
2900 REM "Send P$ to printer
2901 JF_OPT$="N"; IF POS("J1"=V0$(59,12))<>0 THEN IF J1_PREV_PO_NUM$<>A$(297,9) THEN J1_PREV_PO_NUM$=A$(297,9) ELSE JF_OPT$="E"; REM "option to create one file only 
2904 ! IF P2>1 THEN F5=134,F5$="PAGE:"+STR(P2:"#0"); GOSUB 8300
2906 IF B9=1 AND JETFORM_MODE=0 AND UNFORM_MODE=0 THEN DIM Q[1]; CALL "BC2FMX",X3$,X4$,P$,O$,W9,Z[13],A0$,A0{ALL},R6$,Q0$,Q{ALL},Z0; IF Z0=1 THEN GOTO 5000 ELSE GOTO 2933; REM "USe barcode module to print  SSP 241023
2908 IF UNFORM_MODE THEN GOSUB IMAGE_LIBRARY_SETUP; CALL "EF2UNF",X3$,X4$,P6$,JF_OPT$,UNFORM_DEV,ITEM_PARM$,JOB_MODIFIER$,RET$; FOR I=0 TO INT(LEN(P$)/W3)-1; Y5$=P$(I*W3+1,W3); GOSUB 7300; NEXT I; GOTO 2930; REM "Open new item for jetform & print or print regular output, option to create one file only SSP 241023
2910 IF JETFORM_MODE THEN GOSUB IMAGE_LIBRARY_SETUP; CALL "EF2JET",X3$,X4$,P6$,JF_OPT$,JETFORM_DEV,ITEM_PARM$,JOB_MODIFIER$,RET$; PRINT (W9)P$, ELSE FOR I=0 TO INT(LEN(P$)/W3)-1; Y5$=P$(I*W3+1,W3); GOSUB 7300; NEXT I; REM "Open new item for jetform & print or print regular output, option to create one file only  
2933 IF P6$="" OR (DO_2 AND FIRST_PASS=0) OR (POS("J1"=V0$(59,12))<>0 AND JF_OPT$="E") THEN GOTO 2934 ELSE GOSUB 8060; CALL "EF2FAX",ERR=*NEXT,X3$,X4$,P6$,F6$,W9,X$,T$,PMODE,EM$; REM "Setup fax #s and Tag 2s, then call fax routine |skip if no params set or if w6$ set(print both printer & elect items) and we ar doing the printer  SSP 241023
2935 P2=P2+1; GOSUB 8650
2940 RETURN 
2949 ! 
4600 REM "Continue if overflow on lines
4605 GOSUB 8850
4610 GOSUB 2900
4650 GOSUB 2100
4655 GOSUB 2150
4660 GOSUB 2200
4665 GOSUB 2350
4670 I0=(Y8[2]-1)*W3,I1=0; GOSUB 8650
4675 PO_LINE=1; REM "Reset to 1, we already have a line we are doing
4690 RETURN 
4699 ! 
4800 JETFORM_PRINT:REM "For jetform mode add field nominated value with F5 as the field name and F5$ as the value; P_LINE[ F5 ] is the last po line that the field F5 was printed on (init'ed to 0). IF the current po line is more than 1 > that po line then we need to output blank fields until it is 1 less than the current po line. Ofcourse for the non line (header & total) sections the current po  line is 0. The current po line is in PO_LINE
4810 IF F5<>58 THEN IF PO_LINE>P_LINE[F5]+1 THEN P$=P$+"^field "+STP(P6$(146,3),1)+STR(F5:"000")+'LF'; P_LINE[F5]=P_LINE[F5]+1; GOTO 4810; REM "fld 58 only prints once should only be on line 1
4815 P$=P$+"^field "+STP(P6$(146,3),1)+STR(F5:"000")+'LF'+F5$+'LF'; P_LINE[F5]=P_LINE[F5]+1
4845 JETFORM_PRINT_END:RETURN 
4849 ! 
5000 REM "End of Print
5010 IF Q5=1 THEN GOTO 9930
5020 IF W8=0 THEN GOTO 5200
5200 REM "
5291 GOTO 9900
5295 ! 
5300 REM "Remove printed P/O's
5305 IF V8$="A" THEN GOTO 5330
5310 CALL "ZZPROM",".Y",X3$,Z,"Did all of the Warehouse Receiving Reports print correctly?","","",0
5320 ON Z GOTO 5330,5340
5330 GOSUB 5350; GOTO 5345
5345 RETURN 
5349 ! 
5350 REM "Do remove
5360 Q5=1; GOTO 0990
5390 RETURN 
5395 ! 
5400 REM "Set code C1 for type of P/O
5405 REM "0=1 of 1 p/o, 1=cover p/o for multiple p/os, 2=general inst, 3=print special instr. from current po3 record
5410 IF P0=1 THEN C1=0; GOTO 5445
5414 REM "P0 is > 1
5415 ON P1 GOTO 5445,5420,5430,5440
5420 C1=1; GOTO 5445
5440 C1=3
5445 RETURN 
5449 ! 
5500 REM "Get proper address, C1=1 taken care of in 2300's
5505 DIM E$(428)
5510 ON C1 GOTO 5515,5590,5520,5550
5515 IF LEN(P2$)=13 THEN GOTO 5550; REM "Only one P/O, but it is special ship
5520 READ (Z[6],KEY=A$(297,9)+"    ",DOM=5530)IOL=0350; GOTO 5590
5530 READ (Z[6],KEY=A$(297,8)+"     ",DOM=5540)IOL=0350; GOTO 5590
5535 REM "If no general inst., use customer address
5540 E$(16,30)=D$(165,20),E$(46,87)=D$(56,87); GOTO 5590
5550 REM "Read in special shipping record
5560 READ (Z[6],KEY=P2$(1,13),DOM=5580)IOL=0350
5570 GOTO 5590
5580 REM "Error"
5585 EXITTO 1580
5590 RETURN 
5600 REM "Get name to print on shipto
5610 E0$=D$(11,35)
5615 ON POS(E$(11,1)="CPD") GOTO 5645,5620,5625,5630
5620 FIND (Z[7],KEY=E$(11,1)+D$(1,10)+E$(12,4),DOM=5645)E1$; GOTO 5635
5625 FIND (Z[7],KEY=E$(11,1)+A$(7,10)+E$(12,4),DOM=5645)E1$; GOTO 5635
5630 FIND (Z[7],KEY=E$(11,1)+Y6$(1,10)+E$(12,4),DOM=5645)E1$; GOTO 5635
5635 E0$=E1$(16,35)
5645 RETURN 
5649 ! 
5800 REM "Print extended shipping lines
5810 FOR I=0 TO 3
5820 IF POS(" "<>MID(SHIP_TO$,402+I*30,30))>0 THEN IF JETFORM_MODE THEN F5$=SHIP_TO$(402+I*30,30); GOSUB JETFORM_PRINT ELSE P$(I0,30)=SHIP_TO$(402+I*30,30); I0=I0+W3
5830 NEXT I
5835 SHIP_TO$=""
5840 RETURN 
5849 ! 
5900 REM "Calculate T unit of measure
5920 IF (X9$(F5,1)<>"D" AND X9$(F5,1)<>"d") OR UM$<>"T   " OR UM0<>1000 THEN GOTO 5990
5940 PRECISION 5; F5$=STR(NUM(F5$)*.001:"#.00000"),UM$="EACH",UM0=1; PRECISION 2
5990 RETURN 
5999 ! 
6000 REM "Print alignment pattern?
6005 IF V8$="A" THEN GOTO 6090
6010 IF V3$(2,2)="A " THEN PRINT 'EA',
6012 J8=0; REM "Set flag
6015 CALL "ZZPROM","R4",X3$,Z,"","","",X0
6017 IF Z<2 AND V3$(2,2)="A " THEN PRINT 'BA',
6020 ON Z GOTO 6090,6021,9900
6025 J8=1; REM "Flag - we're doing an alignment pattern
6030 GOSUB 6100
6040 GOSUB 6200
6045 IF V3$(2,1)<>"T" THEN PRINTER$=FID(W9); CLOSE (W9); OPEN (W9)PRINTER$
6050 GOTO 6010
6090 RETURN 
6099 ! 
6100 REM "Setup test data"
6105 X$="X",X5$="I6XXXX"
6110 DIM A$(383,X$),B$(118,X$),B[27],C$(350,X$),D$(200,X$),E$(225,X$),F$(386,X$),F[17],G$(20,X$),G0$(20,X$),G1$(20,X$),H$(315,X$),H0$(315,X$),I$(159,X$),J$(134,X$),K$(90,X$),G2$(300,X$),H1$(40,X$)
6120 A$(204,6)=X5$,A$(192,6)=X5$,C$(16,6)=X5$,A$(224,6)=X5$,J$(21,3)="000",A$(242,6)=X5$,A$(291,6)=X5$,C$(55,6)=X5$,I$(103,4)="0999",A$(326,2)="99",I$(42,5)="99999",A$(190,1)="E",C$(70,6)=X5$
6125 I$(68,5)="YYYYY"
6130 DIM H1$(40,"X")
6140 DIM RR_NUM$(5,"X")
6190 RETURN 
6199 ! 
6200 REM " Print align. pattern
6210 S0$=""
6220 GOSUB 2000
6275 GOSUB 2900
6290 RETURN 
6299 ! 
6300 REM 
6310 IF MID(X9$,F5,1)="X" THEN GOTO 6340
6320 GOSUB 8350
6340 RETURN 
6349 ! 
6800 SET_READONLY:
6810 SET_PARAM 'XI'
6820 SET_READONLY_END:RETURN 
6825 ! 
6830 CLEAR_READONLY:
6835 SET_PARAM -'XI'
6840 CLEAR_READONLY_END:RETURN 
6845 ! 
7000 GET_RR_NUM:
7010 DIM OP_PARMS$(512); EXTRACT (Z[13],KEY=X3$(9,3)+"F/M")OP_PARMS$(1)
7015 RR_NUM=NUM(OP_PARMS$(326,5),ERR=*NEXT); GOTO 7020
7016 RR_NUM=1
7020 IF RR_NUM=0 THEN RR_NUM=1
7025 RR_NUM$=STR(RR_NUM:"00000")
7030 NEXT_RR_NUM=RR_NUM+1; IF NEXT_RR_NUM>99999 THEN NEXT_RR_NUM=1
7035 OP_PARMS$(326,5)=STR(NEXT_RR_NUM:"00000")
7040 WRITE (Z[13],KEY=X3$(9,3)+"F/M")OP_PARMS$
7045 RETURN 
7049 ! 
7300 REM "Output line Y5$ to output device
7310 W=W+1
7330 PRINT (W9)STP(Y5$,1); DIM Y5$(W3)
7395 RETURN 
7399 ! 
7400 REM "Read report selection parameters"
7405 CLOSE (14); OPEN (14)"ZZP"
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (14,KEY=X3$(1,8),DOM=7451)IOL=0490
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"   ")-1)
7482 IF V1$(1,3)="???" THEN V8$="A",V7$=V1$(4,2); IF V0$(71,1)="1" THEN V1$="PO1   "
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7486 CLOSE (14)
7488 IF POS(">"=V0$(59,12))=0 THEN W6$="" ELSE W6$=V0$(59,12),W6$=W6$(POS(">"=W6$)+1,2),DO_2=1
7490 RETURN 
7499 ! 
7500 REM "We don't need this for RR but might be referenced somewhere...Run through special shipping and calculate # of PO's needed (P0)
7504 DIM E[3]
7505 P0=1,P2$="",P3$="",P10$="",SHIP_POINTS$=""
7545 RETURN 
7549 ! 
7550 REM "increment reprint flag, user said printed correctly
7560 EXTRACT (Z[2],KEY=U$(1,9))IOL=0310
7570 IF A$(367,1)="9" THEN A$(367,1)="8" ! Modeled after packing list logic, FM2RBC line 10115
7575 A$(367,1)=STR(NUM(A$(367,1))+1:"0")
7580 WRITE (Z[2])IOL=0310
7590 RETURN 
7599 ! 
7600 UPDATE_RR_NUM:! Finished with this one, update RR to PO1
7610 EXTRACT (Z[2],KEY=U$(1,9))IOL=0310
7615 A$(362,5)=RR_NUM$
7620 WRITE (Z[2])IOL=0310
7645 RETURN 
7649 ! 
7650 REM "Fix city, state, zip in X5$ into X6$
7655 REM IF X5$(17,2)="  " THEN GOTO 07690
7660 X6$=FNS$(X5$(1,16))+", "+X5$(17,2)+"  "
7670 CALL "ZZDISP","AX",X5$(19,9),"ZIP",X3$,X$,"",0,0,X4$
7680 X6$=X6$+X$
7690 RETURN 
7699 ! 
7850 REM "Check if ext only
7854 DIM I$(61)
7855 IF B$(10,1)<>"S" THEN GOTO 7890
7860 READ (Z[8],KEY="X"+B$(101,10),DOM=7890)I$
7890 RETURN 
7899 ! 
8060 REM "Setup Fax String
8065 DIM X$(6*51),EM$(6*21)
8069 CALL "ZZDISP","AX",A$(179,10),"A/R",X3$,T$,"",0,0,X4$
8070 X$(1)="C"+D$(143,12),X$(27)="C:"+T$,EM$(1)="C"+A$(179,10); REM "Customer
8074 CALL "ZZDISP","AX",A$(7,10),"A/P",X3$,T$,"",0,0,X4$
8075 X$(52)="V"+G2$(133,12),X$(78)="V:"+T$,EM$(22,21)="V"+A$(7,10); REM "Vendor 
8077 IF LEN(X7$)<150 THEN DIM J0$(200); J0$(1)="P"+A$(7,10),J0$(138,12)=G2$(133,12) ELSE J0$=X7$; IF STP(J0$(138,12),3," ")="" THEN J0$(138,12)=G2$(133,12)
8078 CALL "ZZDISP","AX",J0$(2,10),"A/P",X3$,T$,"",0,0,X4$
8079 X$(103)="P"+J0$(138,12); IF POS(" "<>J0$(12,4))=0 THEN X$(129)="P:"+T$,EM$(43,21)="P"+J0$(2,10) ELSE X$(129)="P:"+T$+"-"+J0$(12,4),EM$(43,21)="P"+A$(7,10)+J0$(12,4); REM "Plant 
8080 IF LEN(E1$)<150 THEN DIM J0$(200); J0$(1)="C"+A$(179,10),J0$(138,12)=D$(143,12) ELSE J0$=E1$
8081 IF J0$(1,1)="D" THEN X$="Whse" ELSE IF J0$(1,1)="C" THEN CALL "ZZDISP","AX",J0$(2,10),"A/R",X3$,T$,"",0,0,X4$ ELSE CALL "ZZDISP","AX",J0$(2,10),"A/P",X3$,T$,"",0,0,X4$
8082 AA$=E1$; DIM E1$(200); GOSUB 5600; X$(154)="T"+E1$(138,12),E1$=AA$; IF POS(" "<>J0$(12,4))=0 THEN X$(180)="T:"+T$ ELSE X$(180)="T:"+T$+"-"+J0$(12,4),EM$(64,21)="T"+J0$(2,14); REM "Ship to 
8083 X$(205)="S"+H0$(127,12); X$(231,25)="S:"+A$(343,4)+"-"+H0$(5,30),EM$(85,21)="S"+A$(343,4); REM "Salesperson
8084 CALL "UPDPO3;GET_PO_SHIPLOC_LIST",A$(297,9),"D",WHSE_LIST$
8085 IF MID(WHSE_LIST$,1,1)="D" THEN DIM E1$(200); E$=DIM(10)+WHSE_LIST$; GOSUB 5600; X$(256)="W"+E1$(138,12),X$(282,25)="W:"+MID(WHSE_LIST$,2,4)+"-"+E1$(16,35),EM$(106,21)="W"+MID(WHSE_LIST$,2,4) ! Warehouse
8090 IF J8=1 THEN FOR J7=0 TO 4; X$(2+J7*51,25)=P6$(150,12),EM$(2+J7*21,20)="ALIGNMENTPATTERN"; NEXT J7; REM "If alignment pattern, replace with align pat from parameters
8093 T$=RR_NUM$; T$="RR:"+T$; REM "Tag 1 for fax
8095 RETURN 
8099 ! 
8100 REM "Set D1$=normal, D2$=double high printing
8101 REM "Assumes special mode 5 is double & special mode six is un-double
8105 REM "This program assumes that double high is on until turned off, changes will be needed if double high goes off auto. at end of a line see lines 740 & 9905
8110 D1$="",D2$=""
8111 RETURN 
8114 CLOSE (14); OPEN (14)"ZZPARM"
8115 READ (14,KEY="PRINTERS",DOM=8145)D1$,D2$
8120 P=POS(FID(W9)=D2$,2); IF P=0 THEN GOTO 8145
8125 READ (14,KEY="ptr"+D1$(INT(P/2)*8+9,8),DOM=8130)D3$; GOTO 8135
8130 D1$="",D2$=""; GOTO 8145
8135 D1$=D3$(282,16),D2$=D3$(266,16)
8140 D1$=FNS$(D1$),D2$=FNS$(D2$)
8145 RETURN 
8149 ! 
8150 REM "Return to normal
8155 IF D1$="" THEN GOTO *RETURN
8160 PRINT (W9)'BO',D1$,'EO',
8175 RETURN 
8199 ! 
8200 REM "Goto double high
8205 IF D2$="" THEN GOTO *RETURN
8210 PRINT (W9)'BO',D2$,'EO',
8225 RETURN 
8249 ! 
8300 REM "Place in page, F5 = field #, F5$=field info
8305 IF X9[F5]=0 THEN GOTO 8315 ELSE IF JETFORM_MODE THEN GOSUB JETFORM_PRINT; GOTO 8315
8306 IF B9=1 THEN DIM Q[1]; CALL "BC2FMY",X3$,X4$,A0$,A0{ALL},F5,F5$,0,0,P$,O$,"","",Q{ALL}; GOTO 8315; REM "Use barcode module
8310 P$(X9[F5]+Y9[F5]*W3,LEN(F5$))=F5$
8315 RETURN 
8320 REM "Offset lines by O0
8325 IF X9[F5]=0 THEN GOTO 8345 ELSE IF JETFORM_MODE THEN GOSUB JETFORM_PRINT; GOTO 8345
8326 IF B9=1 THEN DIM Q[1]; CALL "BC2FMY",X3$,X4$,A0$,A0{ALL},F5,F5$,0,O0,P$,O$,"","",Q{ALL}; GOTO 8345; REM "Use barcode module
8330 P$(X9[F5]+(Y9[F5]+O0)*W3,LEN(F5$))=F5$
8345 RETURN 
8349 ! 
8350 REM "F5=field#, F5$=field info. line number is used to index off of I0 set to first line in lines section - 1. line 1  on a line = first line.
8351 REM "Set F4 to highest line # printed
8355 IF X9[F5]=0 THEN GOTO 8395 ELSE IF JETFORM_MODE THEN GOSUB JETFORM_PRINT; F4=1; GOTO 8395
8357 REM IF I0+Y9(F5)*W3>(Y8(2)+Y8(3)-1)*W3 THEN GOSUB 04600
8358 IF B9=1 THEN DIM Q[1]; O0=I0/W3; CALL "BC2FMY",X3$,X4$,A0$,A0{ALL},F5,F5$,0,O0,P$,O$,"","",Q{ALL}; Y9[F5]=Q[0]; GOTO 8361; REM "Use barcode module
8360 P$(I0+X9[F5]+Y9[F5]*W3,LEN(F5$))=F5$
8365 IF Y9[F5]>F4 THEN F4=Y9[F5]
8395 RETURN 
8399 ! 
8500 REM "SDL= Second Description Line & Line item product codes
8504 DIM X$(187),X[40]; FIND (Z[5],KEY=B$(91,20),DOM=8505)IOL=0495
8505 DESC$=B$(28,40); IF V3$(2,1)="N" THEN DESC1=POS($A3$=DESC$); IF DESC1<>0 THEN DESC$(DESC1,1)=$9C$
8508 IF POS(B$(10,1)="IC")=0 THEN F5=9,F5$=DESC$; GOSUB 8350; GOTO 8525
8510 FIND (Z[5],KEY=B$(91,20),DOM=8525)X$
8515 F5=9,F5$=X$(21,40); GOSUB 8350
8545 RETURN 
8549 ! 
8650 REM "Reset line counters used by jetform for positioning line fields
8655 PO_LINE=0; DIM P_LINE[FN%NEA("X9",1)]
8695 RETURN 
8699 ! 
8850 REM "Print continued message
8855 ! F5=133,F5$="*Cont*"; GOSUB 8300
8895 RETURN 
8899 ! 
8900 REM "U/M
8910 IF B$(77,4)<>L$(4,4) THEN CLOSE (14); OPEN (14)"ZZPARM"; DIM L$(21),L[1]; READ (14,KEY="U/M"+B$(77,4),DOM=8911)IOL=0420
8920 IF L[1]=0 THEN L[1]=1
8940 RETURN 
8949 ! 
8950 ! check FS2 record, may have a from customer, set it
8955 IF LEN(S0$)<20 THEN GOTO *RETURN
8960 READ (Z[10],KEY=A$(297,8)+B$(7,3),DOM=*RETURN)FS2$
8965 IF STP(FS2$(161,10),3)>"" AND S0$(1,10)<>DIM(10) AND FS2$(161,10)<>S0$(1,10) THEN S0$(1,10)=FS2$(161,10)
8995 RETURN 
8999 ! 
9000 REM "ERROR PROCESSING
9002 IF %UNATTEND THEN PRINT @(0,10),'CL',"Unattended Error: Program PO2PRA: Eror ",STR(ERR),": Line ",STR(TCB(5)),
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End program
9905 IF FID(0)<>FID(W9) THEN CLOSE (W9); W9=-1; REM "to prevent formfeed action
9910 IF V3$(2,2)="A " THEN PRINT 'EA',
9915 IF V8$="A" THEN V3$(2,6)="AUTO"+V7$
9920 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9925 IF V8$="A" OR (Y4$<>X0$ AND V3$(2,1)<>"T") THEN IF CALLED$<>"Y" THEN GOSUB 5300
9930 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9935 CALL "ZZDAPP",X3$,"AALL"
9937 IF %GUI AND Y4$<>"" THEN RUN Y4$,ERR=*NEXT
9940 SETERR 9945; EXIT 
9945 IF %GUI THEN EXIT 
9950 IF Y4$<>"" THEN RUN Y4$,ERR=9953
9955 RUN "ZMENU"
9999 END 
11000 IMAGE_LIBRARY_SETUP:! If needed, setup parameters to add image library info to Jetform call
11005 IF %HAS_IMAGE_LIB THEN {
11020 %IL_ADD_DOC_TYPE$="AA"
11025 %IL_ADD_TF1$=A$(297,9) ! P/O number
11030 %IL_ADD_TF2$=RR_NUM$ ! Whse receiving report number
11035 IMG_SEQ$=A$(367,1); IF IMG_SEQ$=DIM(1) THEN IMG_SEQ$="0"
11040 %IL_ADD_IMAGE_PATH$=STP(FN%ZZDISP$(MID(A$,297,9),"P/O"))+"_"+RR_NUM$+"_"+STR(P2)+"_"+IMG_SEQ$
11045 %IL_FORMAT$=V0$(60,1)
11075  } ELSE {
11080 %IL_ADD_DOC_TYPE$="",%IL_ADD_TF1$="",%IL_ADD_TF2$="",%IL_ADD_IMAGE_PATH$="",%IL_FORMAT$=""
11085  }
11095 RETURN 
11099 ! ****************************************************************
56000 REM "239078-Receiving Report form, flexible format/printing programs
56002 REM "241023-Modify printing programs to use Unform                      
56003 REM "307480-DBD-378: Email to warehouse                                 
