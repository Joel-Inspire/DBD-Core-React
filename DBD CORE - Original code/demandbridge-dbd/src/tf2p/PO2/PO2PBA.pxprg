0010 REM "Consolidated Pick List <PO2PBA>
0035 REM "5.7 - 04/08/13 - 8.863333 - tma - SSP# 262397
0037 REM "262397-Proactive change, validate all form printing programs for   
0040 REM "Copyright 2013 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 SETERR 9000; SETESC 9300
0110 X0$="PO2PBA",X1$="Consolidated Pick List"
0120 DIM A$(115),A[3],B$(58),B[10],C$(128),C[20],D$(310),D[2],E$(214),F$(32),J$(46)
0140 Z0$="##,###,###",Z1$="###,###"
0210 T=1,T0=0,T1=9
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0310 IOLIST A$,A[0],A[1],A[2],A[3]
0325 REM "IOLIST FOR IC0 - Item Masterfile
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20]
0345 REM "IOLIST FOR FM0 - Warehouse Locations
0350 IOLIST E$
0380 IOLIST I$,I[0],I[1]
0385 IOLIST L$,L[0],L[1],L[2],L[3],L[4],L[5],L[6]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0507 REM "Slot 2 will be used for temp sort file created in 8000's
0510 Y$="01O POI...  03O IC0...  05O FM0...  06O PO3...  "; IF V1$(1,3)<>Y$(5,3) THEN Y$=Y$+"01O "+V1$+"  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0590 READ (Z[13],KEY=X3$(9,3)+"I/C")P0$
0600 REM "Set len of vend L0 and item  L1 codes
0610 DIM X5$(10,"X"); CALL "ZZDISP","AX",X5$,"A/P",X3$,X$,"",0,0,X4$; L0=LEN(X$)+2
0615 DIM X5$(20,"X"); CALL "ZZDISP","AX",X5$,"I/C",X3$,X$,"",0,0,X4$; L1=LEN(X$)+2
0700 REM "Open Printer
0720 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W0$,W1$,W0,W9,W2,W3,W4; ON W4 GOTO 0721,9920
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0810 ON V0 GOTO 0820,0820,0830,0840,0845
0820 DIM U[3]; U=34,U0=3,U[0]=0,U[1]=4,U[2]=10,U[3]=20,T0=3,V1=4,V2=10,V3=20,U0$="Y   ",T5$="S   S   AI/C    ",U5$="Location",U6$="Bin number",U7$="Item code",S9$="X",X1$=X1$+" by Location"; GOSUB 6900; GOTO 0850
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0880 IF V0$(74,1)="Y" THEN GOTO 8000
0900 REM " Position read"
0905 REM "set key file to read from"
0910 CALL "ZZFLES",X3$,Y1$,Y0$,"00O "+V1$+"  ",Z{ALL},Z0,0; IF Z0>0 THEN GOTO 9900 ELSE U1=Z[0]
0911 IF V0$(74,1)="Y" THEN READ (U1,KEY="",DOM=1000)
0915 IF V0=4 THEN V2$=V2$(1,10)+V2$(21,10)+V2$(11,10)+V2$(31,10)+V2$(41)
0930 IF LEN(V2$)>=U*2 THEN GOTO 0965
0934 REM "create default 'all' range key
0935 DIM V2$(U*2),V4$(U,"~"); U3=1
0940 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1]
0950 V2$(U3+U[U9],U[U9])=V4$(1,U[U9])
0955 NEXT U9
0964 REM "Get starting key from range key
0965 DIM V4$(U); U3=1,U4=1
0970 FOR U9=1 TO U0; U3=U3+U[U9-1]+U[U9-1],U4=U4+U[U9-1]
0975 V4$(U4,U[U9])=V2$(U3,U[U9])
0980 NEXT U9
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 EXTRACT (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record
1005 U$=KEY(U1,END=5000)
1007 IF V0$(74,1)="Y" AND Q5<>2 THEN READ (U1); SHIP$=U$(5,4),U$=U$(1,4)+U$(32,10)+U$(9,23); GOTO 1100
1008 REM "check key against range info"
1009 U3=1,U2=1
1010 FOR U9=1 TO U0; U2=U2+U[U9-1],U3=U3+U[U9-1]+U[U9-1]
1020 IF U$(U2,U[U9])>=V2$(U3,U[U9]) THEN GOTO 1030 ELSE EXTRACT (U1,KEY=U$(1,U2-1)+V2$(U3,U[U9]),DOM=1021)
1025 EXITTO 1000
1030 IF U$(U2,U[U9])<=V2$(U3+U[U9],U[U9]) THEN GOTO 1040 ELSE READ (U1,KEY=U$(1,U2-1)+$FF$,DOM=1032)
1035 EXITTO 1000
1040 NEXT U9
1100 REM "Get record"
1110 U9$=U$
1115 IF Q5=1 THEN GOSUB 7800; GOTO 1000
1120 ON V0 GOTO 1150,1150,1130,1140,1145
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1160 IF Q5=2 THEN GOSUB 8100; GOTO 1000; REM "Creating sort file by ship-to,ssp100218
1300 REM "Read other data
1310 DIM C$(LEN(C$)); FIND (Z[3],KEY=U$(15,20),DOM=1311)IOL=0330
1340 H8$=C$
1400 REM "Subtotals"
1415 GOSUB 3100
1420 ON T GOTO 1470,1430,1440,1450,1460
1430 GOSUB 6300
1440 GOSUB 6400
1450 GOSUB 6500
1490 T=0
1500 REM "Print Data
1505 IF W+2>W0 THEN GOSUB 6000
1510 CALL "ZZDISP","AX",A$(38,8),"O/P",X3$,X$,"",0,0,X4$; Y5$(16)=X$
1515 Y5$(27)=A$(46,3)
1520 Y5$(33)=A$(49,12)
1525 CALL "ZZDISP","AX",A$(99,9),"P/O",X3$,X$,"",0,0,X4$; Y5$(46)=X$
1530 Y5$(50)=" "+A$(61,9),Y5$(61)=A$(70,9)
1600 REM "Quantities
1609 REM "If partial case, add one & print asterisk
1610 IF FPT(A[0])=0 THEN Y5$(71)=STR(A[0]:Z0$) ELSE Y5$(71)=STR(INT(A[0])+1:Z0$)+"*"
1620 IF A[0]>1 THEN Y5$(83)=FNS$(A$(108,4))+"S OF" ELSE Y5$(83)=A$(108,4)+" OF"
1630 IF A[2]=0 THEN Y5$(92)=STR(A[1]:Z1$); GOTO 1660 ELSE Y5$(92)=STR(A[2]:Z1$)
1645 IF A[2]>1 THEN Y5$(101)=FNS$(A$(112,4))+"S OF" ELSE Y5$(101)=A$(112,4)+" OF"
1648 A[3]=A[1]*A[0]
1650 Y5$(110)=STR(A[1]:Z1$)
1660 A[3]=A[0]*A[1]; Y5$(121)=STR(A[3]:Z0$)
1690 GOSUB 7300
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+A[3]
1920 IF FPT(A[0])=0 THEN T[T0,2]=T[T0,2]+A[0] ELSE T[T0,2]=T[T0,2]+INT(A[0])+1 ! SSP#151258
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 T7$=T5$(T*4-3,4)
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "High order break"
3110 IF V1=0 THEN RETURN ELSE IF U1$=U9$(1,V1) THEN GOSUB 3200; RETURN 
3120 GOSUB 3220
3130 IF U1$="" THEN GOTO 3160
3150 T=1,T$=U5$,T6$=U1$; GOSUB 3050; GOSUB 7000
3155 IF U0$(1,1)="Y" AND T0$<>"END" THEN U1$=U9$(1,V1); GOSUB 6800; GOSUB 6000
3160 U1$=U9$(1,V1)
3180 RETURN 
3200 REM "Second highest order break"
3205 IF V0$(74,1)="Y" THEN BIN_SHIPTO$=SHIP$ ELSE BIN_SHIPTO$=U9$(V5,V2)
3210 IF V2=0 THEN RETURN ELSE IF U2$=BIN_SHIPTO$ THEN GOSUB 3300; RETURN 
3220 GOSUB 3320
3230 IF U2$="" THEN GOTO 3260
3250 T=2,T$=U6$,T6$=U2$; GOSUB 3050; GOSUB 7000
3255 IF U0$(2,1)="Y" THEN GOSUB 6000
3260 IF V0$(74,1)="Y" THEN U2$=SHIP$ ELSE U2$=U9$(V5,V2)
3280 RETURN 
3300 REM "Next to lowest order break"
3310 IF V3=0 THEN RETURN ELSE IF U3$=U9$(V6,V3) THEN GOSUB 3400; RETURN 
3320 GOSUB 3420
3330 IF U3$="" THEN GOTO 3360
3350 T=3,T$=U7$,T6$=U3$; GOSUB 3050; GOSUB 7000
3355 IF U0$(3,1)="Y" THEN GOSUB 6000
3360 U3$=U9$(V6,V3)
3380 RETURN 
3400 REM "lowest order break"
3410 IF V4=0 THEN RETURN ELSE IF U4$=U9$(V7,V4) THEN RETURN 
3430 IF U4$="" THEN GOTO 3460
3450 T=4,T$=U8$,T6$=U4$; GOSUB 3050; GOSUB 7000
3455 IF U0$(4,1)="Y" THEN GOSUB 6000
3460 U4$=U9$(V7,V4)
3480 RETURN 
5000 REM "End of Print
5005 IF Q5=2 THEN Q5=0,V1$="TMP"+FID(0); GOTO 0881
5010 IF Q5=1 THEN GOTO 5390
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; GOSUB 7000
5200 REM "
5290 GOTO 9900
5300 REM "Remove printed P/O's
5305 IF V3$(2,2)="A " THEN PRINT 'EA',
5310 CALL "ZZPROM",".Y",X3$,Z,"Remove the items printed on this pick list?","","",0
5320 ON Z GOTO 5330,5340
5330 GOSUB 5350; GOTO 5345
5345 RETURN 
5350 REM "Do remove
5354 PRINT @(0,19),'CE',
5355 PRINT @(10,19),"Removing Item:",
5360 Q5=1; GOTO 0900
5390 RETURN 
6000 REM "Page header
6010 GOSUB 6300
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W4$,W5$
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6110 Y5$(1)="Bin Number",Y5$(13)="Item Code   Description"
6112 IF V0$(74,1)="Y" THEN Y5$(1,12)=" Ship-to"
6115 Y5$(53)="Starting",Y5$(64)="Ending",Y5$(125)="Total"
6120 GOSUB 7300
6125 Y5$(16)="Order #",Y5$(26)="Line",Y5$(34)="Job No.",Y5$(46)="P/O No",Y5$(54)="Number",Y5$(64)="Number"
6130 Y5$(72)=T1$(1,18)+" Packaging "+T1$(1,18)
6135 Y5$(123)="Quantity"
6150 GOSUB 7300
6199 RETURN 
6300 REM "Break #2 header - Location
6315 DIM E0$(LEN(E$)); FIND (Z[5],KEY="D"+E0$(1,10)+U1$,DOM=6316)E0$
6350 W0$=U1$+" - "+FNS$(E0$(16,35)); IF POS(" "<>E0$(111,16))<>0 THEN W0$=W0$+" - "+FNS$(E0$(111,16))+", "+E0$(127,2)
6399 RETURN 
6400 REM "Bin number break
6405 IF W+3>W0 THEN GOSUB 6000
6410 IF V0$(74,1)="Y" THEN Y5$(1)=SHIP$ ELSE Y5$(1)=A$(5,10)
6495 RETURN 
6500 REM "Item number break
6505 IF W+2>W0 THEN GOSUB 6000
6515 CALL "ZZDISP","AX",C$(1,20),"I/C",X3$,X$,"",0,0,X4$; Y5$(13)=X$
6517 H8$=C$(1,82); GOSUB 7750
6520 Y5$(25)=H9$
6525 GOSUB 7300
6530 IF POS(" "<>A$(15,10))<>0 THEN CALL "ZZDISP","AX",A$(15,10),"A/R",X3$,X$,"",0,0,X4$; Y5$(4)=X$
6595 RETURN 
6900 REM "SORT
6905 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6940 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,X9
6990 RETURN 
7000 REM "Totals logic
7001 IF T0$="END" AND T=0 THEN GOTO 7090
7002 IF T<>T0 THEN GOTO 7100 ELSE IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7003 IF W+3>W0 THEN GOSUB 6000
7005 Y5$(71)=T1$(1,LEN(Z0$)),Y5$(121)=T1$(1,LEN(Z0$)); GOSUB 7300 ! SSP#151258
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 Y5$(1)="Total"
7020 Y5$(71)=FNQ$(T[T,2],Z0$),Y5$(121)=FNQ$(T[T,1],Z0$); GOSUB 7300 ! SSP#151258
7065 IF T$="END" THEN Y5$(121)=T2$(1,LEN(Z0$))
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed"+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600),T4=MOD(T3,60),T3=INT(T3/60)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")+":"+STR(T4:"00")
7260 Y5$(1)="Elapsed time"+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 W=W+1
7330 IF Y5$=Y6$ THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; DIM Y5$(W3)
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"  ")-1)
7485 FOR U7=1 TO LEN(V1$); IF V1$(U7,1)=" " THEN V1$(U7,1)="."; NEXT U7 ELSE NEXT U7 ! SSP#262397
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
7750 REM "Build desc from H8$
7755 IF POS("D"=V0$(59,12))>0 THEN H9$=H8$(21,40); RETURN 
7760 H9$=FNS$(H8$(64,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+"x"
7770 H9$=H9$+FNS$(H8$(72,8)); IF H9$<>"" THEN IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
7775 IF NUM(H8$(80,2))<>0 THEN H9$=H9$+H8$(80,2)+" Ply "
7780 H9$=H9$+FNS$(H8$(21,40)); H8$=""
7790 RETURN 
7800 REM "Remove the record
7805 PRINT @(25,19),'CL',FNS$(U$(5,10))," - ",FNS$(U$(15,10))," ",FNS$(U$(25,10)),
7820 REMOVE (Z[1],KEY=U$,DOM=7821)
7845 RETURN 
8000 REM "Create temp sort file with ship-to from general shipping, ssp100218
8010 Y$="Cons Pick List by Ship-to"
8020 CALL "ZZ2SRT",X3$,X4$,Z[13],Z[1],41,Y$,Z,"NN"
8025 IF Z=2 THEN GOTO 9920
8030 Y$="02O "+Y$,Y9$=Y$
8040 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
8050 IF Z=0 THEN Q5=2; GOTO 0900 ELSE GOTO 9920
8100 REM "Write records to sort file
8110 PRINT @(30),A$(1,4),"-",A$(15,10),"-",A$(25,10),
8120 DIM PO3$(100); FIND (Z[6],KEY=A$(38,8)+DIM(5),DOM=8121)PO3$
8130 WRITE (Z[2],KEY=A$(1,4)+PO3$(12,4)+A$(15,23)+A$(5,10))
8190 RETURN 
8900 REM "Function Statements
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8920 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
8930 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
8940 DEF FNC$(Z9$)=Z9$(1,POS(" "=Z9$+" ")-1)
8950 DEF FNQ$(Z9,Z9$)=STR(Z9:Z9$(1,POS(".00"=STR(Z9:Z9$)+".00")-1))
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "End program
9908 ! IF W8>0 THEN GOSUB 5300!SSP#218343 moved line, asked question before it printed report
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9915 IF W8>0 THEN GOSUB 5300 ! SSP#218343 moved line, asked question before it printed report   
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF NOT(NUL(Y4$)) THEN IF %GUI THEN CALL Y4$,ERR=*NEXT ELSE RUN Y4$,ERR=9931 ! SSP#218343
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU" ! SSP#218343
9999 END 
56000 REM "218343-When running the consolidated pick list, it boots you off   
56001 REM "262397-Proactive change, validate all form printing programs for   
