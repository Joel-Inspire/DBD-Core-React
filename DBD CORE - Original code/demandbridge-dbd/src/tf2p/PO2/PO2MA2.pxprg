0010 REM "Purchase Order Entry - Line entry overlay <PO2MA2>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 10/10/14 - 11.751111 - jvv - SSP# 273055
0037 REM "273055-Expand IC2 for 6 additional cost breaks, allow entry in GUI 
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0075 X0$="PO2MA2"
0080 L9=L0,L0=0; ON L9 GOTO 0081,3040,2000,2035,5700,5700
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0300 REM "IOLISTS
0309 REM "Change in special shipping also
0310 IOLIST A$(1,LEN(A$)),A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27]
0320 IOLIST B$
0330 IOLIST C$
0340 IOLIST D$
0341 IOLIST D1$,D[0],D[1],D[2]
0350 IOLIST E$
0360 IOLIST F$,F[0],F[1],F[2],F[3],F[4],F[5],F[6],F[7],F[8],F[9],F[10],F[11],F[12],F[13],F[14],F[15],F[16],F[17],F[18],F[19],F[20],F[21],F[22],F[23],F[24],F[25],F[26],F[27],F[28],F[29],F[30] ! WO273055
0370 IOLIST G$
0380 IOLIST H$,H[0],H[1],H[2],H[3],H[4],H[5],H[6],H[7],H[8],H[9],H[10],H[11],H[12],H[13],H[14],H[15],H[16],H[17],H[18]
0390 IOLIST I$,I[0],I[1],I[2],I[3],I[4],I[5],I[6],I[7],I[8],I[9],I[10],I[11],I[12],I[13],I[14],I[15],I[16],I[17],I[18],I[19],I[20],I[21],I[22],I[23],I[24],I[25],I[26],I[27],I[28],I[29],I[30]
0400 IOLIST U$,U[0],U[1]
0405 IOLIST V$,V[0],V[1]
0410 IOLIST L$,L[0],L[1],L[2],L[3],L[4],L[5],L[6],L[7],L[8],L[9],L[10],L[11],L[12],L[13],L[14],L[15],L[16],L[17],L[18],L[19],L[20],L[21]
0420 IOLIST J$,J[0],J[1],J[2],J[3],J[4],J[5],J[6]
1800 REM 
1810 GOTO 9900
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF INT(C0)=3 THEN GOSUB 7300 ELSE IF C0>3 OR X0<0 THEN X0=999
2029 REM IF A$(10,1)="I" AND POS(X3$(9,3)="015",3)>0 THEN GOTO 02031
2030 IF R0$="R" THEN IF X0<9 THEN X0=9
2040 IF X0>35 THEN GOTO 1800 ELSE IF X0>24 THEN GOTO 3000 ELSE ON X0 GOTO 2100,2700,2200,2800,2250,2300,2350,2400,2150,2600,2550,2500,2450,2950,2750,2650,3500,3550,3600,3950,3650,3700,3750,3800,3850,3900,1800 ! ssp 200130
2100 REM "Line type
2105 J9$="SMIN",X$="Inventory or customer inventory, Special, Non-stock,or Message"; REM " IF A$(7,3)="001" THEN LET J9$="C"+J9$,X$="CUSTOMER FORM, "+X$
2110 CALL "ZZENTR","SRU",A{ALL},A$,X4$,X3$,23,4+V0,10,1,C0,"","{3"+X$,J9$,"PO2MAB04","CSM","",""
2140 GOTO 2020
2150 REM "Sales order line
2155 IF A$(10,1)="M" OR B$(173,1)<>"C" THEN GOTO 2020
2160 CALL "ZZENTR","Z",A{ALL},A$,X4$,X3$,72,10+V0,11,3,C0,"","{3"+X$,"","PO2MAB06","","",""
2190 GOTO 2020
2200 REM "Special shipping
2201 IF B$(361,1)="Y" THEN A$(14,1)="N"; PRINT @(77,4+V0),A$(14,1); GOTO 2020; REM SSP 198988 Disallow Special Shipping if Customer PO Type Vendor
2205 IF A$(10,1)="M" THEN GOTO 2020
2207 IF A$(10,1)="I" AND A$(7,3)<>"001" THEN X$="Forms requiring specs may only be entered on line 1"
2210 BEFORE$=A$(14,1); CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,77,4+V0,14,1,C0,"","{3"+X$,"","PO2MAB08","","",""
2220 IF BEFORE$="Y" AND A$(14,1)="N" THEN GOSUB 8600
2240 GOTO 2020
2250 REM "Custom item number
2252 IF LEN(J3$)<10 THEN DIM J3$(20)
2253 J3$(11,10)=A$(101,10),J8$=A$(101,10),X$=""
2254 IF A$(10,1)="S" THEN GOTO 2900
2255 IF A$(10,1)="M" THEN GOTO 2020 ELSE B0$=A$(91,10)
2256 J$="SUX",J0$="",J2$="",J1$=""
2257 IF A$(10,1)="I" AND A$(81,1)="Y" AND FNS$(A$(91,10))>"" THEN J$="SUX",J0$="FM1",J2$=""; GOTO 2260
2258 IF A$(10,1)="I" THEN J$="AUX",J0$="IC0",J2$="ICE",J1$="IC2MAB",X$="Enter ? to lookup with Vendor's (supplier's) number"; GOTO 2260
2260 CALL "ZZENTR",J$,A{ALL},J8$,X4$,X3$,0,9+V0,1,10,C0,J2$,"{3"+X$,B0$,"PO2MAB10",J0$,J1$,""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2260,2261
2261 IF A$(10,1)="I" AND FNS$(A$(91,10))="" AND J8$="?         " THEN GOTO 4000
2262 IF C0>0 THEN A$(101,10)=J8$
2264 IF ABS(C0)=2 THEN GOTO 2020 ELSE IF A$(10,1)="I" AND FNS$(A$(101,10))="" THEN GOTO 2260
2265 IF POS(" "<>J3$)<>0 AND J3$=A$(91,20) THEN GOTO 2020
2268 IF R0$="" AND A$(81,1)="Y" AND POS(" "<>A$(91,10))<>0 THEN GOSUB 5000 ELSE IF A$(10,1)="I" THEN GOSUB 7900
2275 READ (Z[13],KEY="U/M"+A$(77,4),DOM=2276)IOL=0400
2290 GOTO 2020
2300 REM "Product code
2304 IF A$(10,1)="I" AND ABS(C0)<>2 THEN GOTO 2020
2305 J$="T"
2306 IF A$(10,1)="M" THEN GOTO 2020
2310 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,11,9+V0,25,3,C0,"","{3"+X$,J$,"PO2MAB12","FMPP","FM2SAA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2310,2311
2320 FIND (Z[4],KEY="T"+A$(25,3),DOM=2310)IOL=0340
2330 GOSUB 7650
2340 GOTO 2020
2350 REM "Description
2355 IF POS(A$(10,1)="MSN")=0 THEN GOTO 2020
2360 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,39,9+V0,28,40,C0,"","{3"+X$,"","PO2MAB14","","",""
2390 GOTO 2020
2400 REM "Starting number
2405 IF POS(A$(10,1)="MSN")<>0 THEN GOTO 2020
2407 IF FNS$(A$(91,10))>"" THEN IF H$(104,1)<>"Y" THEN PRINT @(19,10+V0),"(N/A)",; GOTO 2020
2410 CALL "ZZENTR","RZ",A{ALL},A$,X4$,X3$,19,10+V0,68,9,C0,"","{3"+X$,"","PO2MAB16","","",""
2440 GOTO 2020
2450 REM "Cost
2451 IF S6$(80,1)="Y" AND A[0]<>0 THEN GOTO 2020; REM " SSP# 142717
2452 HOLD_COST=A[0]; REM " SSP# 142717
2453 IF POS("PO2MAM"=U1$)=0 THEN IF S6$(69,1)="Y" THEN GOTO 2020; REM "WO113448, 138258
2454 IF A$(10,1)="M" THEN GOTO 2020
2455 IF A$(10,1)="S" THEN IF D1$(61,1)="Y" THEN GOTO 2020
2456 IF U$(21,1)="Y" THEN A[0]=0; PRINT @(36,13+V0),A[0]:M0$,; GOTO 2020
2460 PRECISION M6[1]; CALL "ZZENTR","NX&",A{ALL},A$,X4$,X3$,36,13+V0,0,M6[0],C0,M6$,"{3"+X$,"","PO2MAB18","","",""; PRECISION 2
2461 IF S6$(80,1)="W" AND A[0]<>HOLD_COST THEN CALL "ZZPROM",".4",X3$,0,"WARNING - You are about to change the vendor cost!","","",0; REM " ssp# 142717
2464 IF C0<>-2 AND D8$="*" THEN PRINT @(0,17),'CE',; D8$=""
2465 IF C0>0 THEN GOSUB 8000; GOSUB 6226
2466 HOLD_COST=0; REM "SSP# 142717
2490 GOTO 2020
2500 REM "Quantity
2505 IF A$(10,1)="M" THEN GOTO 2020
2506 IF A$(10,1)="S" THEN IF D1$(61,1)="Y" THEN GOTO 2020
2507 DIM Q[1]; IF U$(20,1)="Y" THEN Q[1]=A[11]/U[1] ELSE Q[1]=A[11]/A[2]
2508 IF R0$="" AND A$(10,1)="I" AND C0<>2 THEN GOSUB 7500
2509 F8$="*"; GOSUB 8500
2510 CALL "ZZENTR","NX.",Q{ALL},A$,X4$,X3$,21,13+V0,1,822,C0,M1$,"{3"+X$,"","PO2MAB20","","",""
2514 IF ABS(C0)>=2 AND D8$="*" THEN PRINT @(0,17),'CE',; D8$=""
2515 IF C0<0 THEN GOTO 2540
2520 IF U$(20,1)="Y" THEN A[11]=Q[1]*U[1] ELSE A[11]=Q[1]*A[2]
2524 IF A$(10,1)="I" THEN GOSUB 8400
2525 A[1]=A[11]-A[6]
2529 F9$="X"
2530 GOSUB 7950; GOSUB 8000; GOSUB 6226
2540 GOTO 2020
2550 REM "Unit of measure amount
2554 IF A$(10,1)="M" THEN GOTO 2020
2555 IF U$(20,1)="Y" THEN GOTO 2020 ELSE Q2=A[2]
2560 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,12,13+V0,2,501,C0,M2$,"{3"+X$,"","PO2MAB22","","",""
2565 IF C0>0 THEN GOSUB 8200; GOSUB 7950; GOSUB 6226
2590 GOTO 2020
2600 REM "Unit of measure per
2605 IF A$(10,1)="M" THEN GOTO 2020
2606 IF U$(20,1)<>"Y" THEN Q2=A[2] ELSE IF U[0]<>0 THEN Q2=U[0] ELSE Q2=U[1]
2610 CALL "ZZENTR","SUX",A{ALL},A$,X4$,X3$,7,13+V0,77,4,C0,"","{3"+X$,"","PO2MAB24","","",""
2615 REM IF C0<=0 THEN GOTO 2640
2620 READ (Z[13],KEY="U/M"+A$(77,4),DOM=2610)IOL=0400
2625 IF U$(20,1)="Y" THEN IF U[0]<>0 THEN A[2]=U[0] ELSE A[2]=U[1]
2626 IF A[2]=0 THEN A[2]=1
2630 IF C0>0 THEN GOSUB 8200; GOSUB 7950; GOSUB 6226
2640 GOTO 2020
2650 REM "Number of Labels
2655 IF C$(22,1)="N" OR A$(10,1)="M" THEN GOTO 2020
2660 CALL "ZZENTR","N@",A{ALL},A$,X4$,X3$,72,13+V0,3,400,C0,"###0","{3"+X$,"","PO2MAB26","","",""
2665 IF P4$(169,1)<>"Y" THEN GOTO 2020
2670 X$="Enter the carton label flexible fommat code to use"
2675 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,77,13+V0,1,1,C0,"","{3"+X$,"","PO2MAB28","","",""
2680 IF A$(1,1)<>" " THEN READ (Z[13],KEY="FORMATCL"+A$(1,1),DOM=2675)
2690 GOTO 2020
2700 REM "Specs required?
2705 IF A$(10,1)<>"I" OR A$(7,3)<>"001" THEN A$(81,1)="N"; PRINT @(49,4+V0),A$(81,1); GOTO 2020
2708 X$="NOTE: Only one Item with specs per Purchase Order and it must be line 001."
2710 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,49,4+V0,81,1,C0,"","{3"+X$,"","PO2MAB28","","",""
2740 GOTO 2020
2750 REM "Pack"
2755 IF A$(10,1)="M" THEN GOTO 2020
2760 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,63,13+V0,5,501,C0,M2$,"{3"+X$,"","PO2MAB26","","",""
2770 IF C0>0 THEN GOSUB 7950
2790 GOTO 2020
2800 REM "Customer number if S item
2802 IF LEN(J3$)<20 THEN DIM J3$(20)
2803 J3$(1,10)=A$(91,10)
2804 IF A$(10,1)="I" AND A$(81,1)="Y" AND P$(38,1)="Y" THEN GOTO 2807
2805 IF S6$(43,1)<>"Y" AND (A$(10,1)<>"I" OR C0<>2 OR P$(38,1)<>"Y") THEN GOTO 2020
2807 IF ABS(C0)<>2 AND POS(" "<>B$(179,10))>0 AND POS(" "<>A$(91,10))=0 THEN A$(91,10)=B$(179,10)
2810 CALL "ZZENTR","AXU",A{ALL},A$,X4$,X3$,15,6+V0,91,10,C0,"A/R","{3"+X$,"","PO2MAB13","AR1","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2810,2811
2813 IF POS(" "<>A$(91,10))=0 THEN IF S6$(43,1)="Y" THEN CALL "ZZPROM",".4",X3$,0,"You must enter a customer code, you may not enter a P/O for a stock item.","","",0; GOTO 2810
2815 IF POS(" "<>A$(91,10))=0 THEN PRINT @(34,6+V0),'CL',; IF P4$(188,1)<>"Y" THEN IF A$(81,1)<>"Y" THEN GOTO 2840 ELSE CALL "ZZPROM",".4",X3$,0,"Customer Code necessary since Specs Req is 'Y'","","",0; GOTO 2810
2820 IF ABS(C0)=2 OR ABS(C0)=4 THEN C0=0; GOTO 2020
2830 DIM E$(100); IF A$(91,10)<>S0$(1,10) THEN FIND (Z[5],KEY=A$(91,10),DOM=2810)IOL=0350
2831 IF A$(91,10)<>DIM(10) AND A[24]=0 THEN IF MID(E$,524,2)<>DIM(2) THEN A[24]=NUM(MID(E$,524,2),ERR=*NEXT) ! WO264206, default number of storage months from cust parm if customer specific item.
2835 PRINT @(34,6+V0),E$(11,35),
2840 GOTO 2020
2900 REM "Specials
2905 J9$=A$(101,10)
2910 CALL "ZZENTR","SUX",A{ALL},A$,X4$,X3$,0,9+V0,101,10,C0,"","{3"+X$,"X","PO2MAB11","FMPX","FS2MCA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2910,2911
2915 IF C0=-2 AND POS(" "<>J9$)=0 THEN GOTO 2940 ELSE IF POS(" "<>J9$)<>0 AND J9$=A$(101,10) THEN GOTO 2940
2920 FIND (Z[4],KEY="X"+A$(101,10),DOM=2910)IOL=0341
2925 GOSUB 8300
2940 GOTO 2020
2950 REM "Extension
2954 IF MID(U$,21,1)="Y" THEN GOTO 2956
2955 IF A$(10,1)<>"S" THEN GOTO 2020 ELSE IF D1$(61,1)<>"Y" THEN GOTO 2020
2960 CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,50,13+V0,4,621,C0,M0$,"{3"+X$,"","PO2MAB21","","",""
2990 GOTO 2020
3000 REM "Entry of received
3005 IF R0$<>"R" AND R0$<>"r" THEN GOTO 2020
3010 RUN "PO2MAR"
3035 REM "Return from PO2MAR overlay
3040 GOTO 2040
3500 REM "Selling U/M
3502 DIM V$(21),V[1]
3503 IF LEN(A$)<170 THEN A$=A$+DIM(170-LEN(A$)); REM "SSP128433
3505 IF A$(10,1)<>"I" OR P4$(213,1)<>"Y" THEN GOTO 2020 ELSE GOSUB 8350
3510 X$="Selling Unit of Measure"; CALL "ZZENTR","SUX",A{ALL},A$,X4$,X3$,7,13+V0,146,4,C0,"","{3"+X$,"","PO2MAB36","","",""
3520 IF POS(" "<>A$(146,4))=0 THEN GOTO 3535 ELSE DIM V[1]; FIND (Z[13],KEY="U/M"+A$(146,4),DOM=3510)IOL=0405; IF V$(21,1)="Y" THEN GOTO 3510
3525 IF C0<=0 THEN GOTO 3535 ELSE IF V$(20,1)="Y" THEN IF V[0]<>0 THEN A[20]=V[0] ELSE A[20]=V[1]
3530 PRINT @(12,16),A[20]:M2$,
3535 IF ABS(C0)=2 THEN PRINT @(0,16),'CL',@(1),"Cost:",; GOSUB 6200
3540 GOTO 2020
3550 REM "Selling Qty/Unit of Measure
3555 IF A$(10,1)<>"I" OR V$(20,1)="Y" OR P4$(213,1)<>"Y" THEN GOTO 2020
3558 IF POS(" "<>A$(146,4))=0 THEN A[20]=0,A[21]=0; PRINT @(12,13+V0),A[20]:M2$,@(36,13+V0),A[21]:M6$,; GOTO 2020
3560 X$="Selling Quantity per Unit of Measure"; CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,12,13+V0,20,501,C0,M2$,"{3"+X$,"","PO2MAB38","","",""
3570 IF A[20]=0 THEN A[20]=1; GOTO 3560
3590 GOTO 2020
3600 REM "Unit Selling Price
3605 IF A$(10,1)<>"I" OR P4$(213,1)<>"Y" THEN GOTO 2020
3608 IF POS(" "<>A$(146,4))=0 THEN A[20]=0,A[21]=0; PRINT @(12,13+V0),A[20]:M2$,@(36,13+V0),A[21]:M6$,; GOTO 2020
3610 X$="Unit Selling Price"; PRECISION M6[1]; CALL "ZZENTR","NX&",A{ALL},A$,X4$,X3$,36,13+V0,21,M6[0],C0,M6$,"{3"+X$,"","PO2MAB40","","",""; PRECISION 2
3640 GOTO 2020
3650 REM 
3655 IF A$(10,1)<>"I" OR P$(64,1)<>"Y" THEN GOTO 2020
3660 X$="Storage Expiration Date"; CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,53,14+V0,150,6,C0,"","{3"+X$,"","PO2MAB42","","",""
3690 GOTO 2020
3700 REM "Standard Cost U/M
3705 IF A$(10,1)<>"I" OR P$(64,1)<>"Y" THEN GOTO 2020 ELSE GOSUB 8360
3710 X$="Standard Cost Unit of Measure"; CALL "ZZENTR","SUX",A{ALL},A$,X4$,X3$,7,13+V0,164,4,C0,"","{3"+X$,"","PO2MAB44","","",""
3720 IF POS(" "<>A$(164,4))=0 THEN GOTO 3535 ELSE DIM V[1]; FIND (Z[13],KEY="U/M"+A$(164,4),DOM=3710)IOL=0405; IF V$(21,1)="Y" THEN GOTO 3710
3725 IF C0<=0 THEN GOTO 3735 ELSE IF V$(20,1)="Y" THEN IF V[0]<>0 THEN A[23]=V[0] ELSE A[23]=V[1]
3730 PRINT @(12,16),A[23]:M2$,
3735 IF ABS(C0)=2 THEN PRINT @(0,16),'CL',@(1),"Sell:",; GOSUB 8350
3740 GOTO 2020
3750 REM "Std Cost Qty/Unit of Measure
3755 IF A$(10,1)<>"I" OR V$(20,1)="Y" OR P$(64,1)<>"Y" THEN GOTO 2020
3758 IF POS(" "<>A$(164,4))=0 THEN A[22]=0,A[23]=0; PRINT @(12,13+V0),A[23]:M2$,@(36,13+V0),A[22]:M6$,; GOTO 2020
3760 X$="Standard Cost Quantity per Unit of Measure"; CALL "ZZENTR","NX",A{ALL},A$,X4$,X3$,12,13+V0,23,501,C0,M2$,"{3"+X$,"","PO2MAB46","","",""
3770 IF A[23]=0 THEN A[23]=1; GOTO 3760
3790 GOTO 2020
3800 REM "Standard Unit Cost
3805 IF A$(10,1)<>"I" OR P$(64,1)<>"Y" THEN GOTO 2020
3808 IF POS(" "<>A$(164,4))=0 THEN A[22]=0,A[23]=0; PRINT @(12,13+V0),A[23]:M2$,@(36,13+V0),A[22]:M6$,; GOTO 2020
3810 X$="Standard Unit Cost"; PRECISION M6[1]; CALL "ZZENTR","NX&",A{ALL},A$,X4$,X3$,36,13+V0,22,M6[0],C0,M6$,"{3"+X$,"","PO2MAB48","","",""; PRECISION 2
3840 GOTO 2020
3850 REM "Product Code
3855 IF A$(10,1)<>"I" OR P$(64,1)<>"Y" THEN GOTO 2020
3860 X$="Product Code Override"; CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,53,13+V0,156,3,C0,"","{3"+X$,"","PO2MAB50","","",""
3870 IF POS(" "<>A$(156,3))>0 THEN FIND (Z[4],KEY="T"+A$(156,3),DOM=3860)
3890 GOTO 2020
3900 REM "Commission Code
3905 IF A$(10,1)<>"I" OR P$(64,1)<>"Y" THEN GOTO 2020
3910 X$="Commission Code Override"; CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,62,13+V0,159,5,C0,"","{3"+X$,"","PO2MAB52","","",""
3920 IF POS(" "<>A$(159,5))>0 THEN CLOSE (14); OPEN (14)"AR4"+X3$(9,3); FIND (14,KEY=A$(159,5),DOM=3910)
3940 CLOSE (14); GOTO 2020
3950 ! Number Storage Months - SSP 200130
3955 X$="Enter Number Storage Months"
3960 CALL "ZZENTR","N@",A{ALL},A$,X4$,X3$,72,13+V0,24,200,C0,"#0","{3"+X$,"","PO2MAB26","","",""
3990 GOTO 2020
4000 REM "Supplier item number search
4010 DIM I2$(34); C7=4,I0=-1
4020 GOSUB 4320
4030 X$="",C0=ABS(C0),I0=I0+1; IF C0=2 THEN IF I0>0 THEN I0=I0-2
4040 IF ABS(C0)=4 THEN GOTO 4380 ELSE IF INT(C0)=3 OR ABS(C0)>3 OR I0<0 THEN I0=999; GOTO 4050
4050 ON I0 GOTO 4120,4060,4160,4200
4060 REM "Plant
4070 J1$="P"+A$(94,10)
4080 CALL "ZZENTR","SUZX",A{ALL},I2$,X4$,X3$,26,21,11,4,C0,"","{3"+X$,J1$,"PO2MAB74","FM0P","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 4080,4081
4090 IF I2$(11,4)="    " OR I2$(11,4)="0000" THEN GOTO 4110
4100 FIND (Z[3],KEY=J1$+I2$(11,4),DOM=4080)
4110 GOTO 4030
4120 REM "Vendor
4130 CALL "ZZENTR","AU",A{ALL},I2$,X4$,X3$,8,21,1,10,C0,"A/P","{3"+X$,"","PO2MAB76","AP4","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 4130,4131
4140 IF ABS(C0)<>4 THEN FIND (Z[7],KEY=I2$(1,10),DOM=4130)
4150 GOTO 4030
4160 REM "Supplier item number
4170 J1$=I2$(1,14)
4180 CALL "ZZENTR","S",A{ALL},I2$,X4$,X3$,49,21,15,20,C0,"","{3"+X$,J1$,"PO2MAB78","/FM2OBI","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 4180,4181
4190 GOTO 4030
4200 REM "Lookup Item number"
4210 Z$="12C PO7...  12O ICD...  "
4220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 4221,4410
4230 READ (Z[12],KEY=I2$,DOM=4231)
4240 I3$=KEY(Z[12],END=4270)
4250 IF I3$(1,LEN(I2$))<>I2$ THEN GOTO 4270
4260 J8$(1)=I3$(45)
4270 Z$="12C ICD...  12O PO7...  "
4280 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 4281,4410
4290 IF J8$(1)<>I3$(45) THEN GOTO 4160
4310 C7=3,C0=1; GOTO 2262
4320 REM "
4330 PRINT @(0,21),'CL','SB',@(0,21),"Vendor:",@(20,21),"Plant:",@(32,21),"Supplier Number:",'SF',
4340 CALL "ZZDISP","A",I2$(1,10),"A/P",X3$,"","",8,21,X4$
4350 PRINT @(26,21),I2$(11,4),@(49,21),I2$(15,20),
4360 RETURN 
4370 GOSUB 4320; GOTO 4050
4380 DIM I2$(LEN(I2$))
4390 IF POS(" "<>A$(101,10))<>0 THEN J8$=A$(101,10) ELSE DIM J8$(10)
4400 GOTO 4300
5000 REM "Do forms modify/setup
5002 IF FNS$(A$(101,10))="" THEN GOTO 5047
5005 DIM H[31],H$(448)
5010 FIND (Z[8],KEY=A$(91,20),DOM=5040)IOL=0380
5015 FIND (Z[10],KEY=A$(91,20),DOM=7850)IOL=0390
5020 GOSUB 7700
5022 IF FNS$(H$(82,10))>"" AND B$(7,10)<>H$(82,10) THEN GOSUB 5100
5023 CALL "ZZPROM","10",X3$,Z,"Modify item master before entering order data?","","",0; ON Z GOTO 5025,5030
5025 U1$="PO2MAB"+STR(LEN(A1$)+2:"00")+"!!"+A1$+U1$; GOTO 5065
5030 RETURN 
5040 CALL "ZZPROM","Y",X3$,Z,"Do you wish to set up this item?","","",0
5045 ON Z GOTO 5048,5047
5047 A$(101,10)=J8$; EXITTO 2250
5048 IF A$(81,1)<>"N" THEN GOSUB 5200
5050 REM "Run forms setup
5060 U1$="PO2MAB"+STR(LEN(A1$)+1:"00")+"!"+A1$+U1$
5065 U1$="FM2IAC20"+A$(91,10)+A$(101,10)+U1$
5070 U1$="FM2IAA"+"20"+A$(91,10)+A$(101,10)+U1$
5075 WRITE (Z[1],KEY=A1$)IOL=0310
5090 GOTO 9700
5100 REM "Change in vendors
5130 CALL "ZZDISP","AX",H$(82,10),"A/P",X3$,X$,"",0,0,X4$
5140 X$="NOTE: Previous Vendor was "+X$
5150 IF FNS$(H$(149,12))>"" THEN X$=X$+" - Previous Job#: "+FNS$(H$(149,12))
5160 CALL "ZZPROM",".4",X3$,0,X$,"","",0
5190 RETURN 
5200 REM "Transfer from another customer?"
5204 DIM N$(20),N0$(100),H$(448),H[31]
5205 GOSUB 5390
5206 REM "Customer Code
5207 C7=1,N0=1
5210 CALL "ZZENTR","AXU",A{ALL},N$,X4$,X3$,30,12,1,10,C0,"A/R","{3"+X$,"","FM2ODB03","AR1","",""; IF ABS(C0)>4 THEN GOSUB 6400; GOSUB 5390; ON C0 GOTO 5210,5211
5215 IF POS(" "<>N$(1,10))=0 THEN EXITTO 5050
5220 FIND (Z[5],KEY=N$(1,10),DOM=5210)N0$
5225 PRINT @(42,12),N0$(11,35),
5250 REM "Item code
5255 C0$=N$(1,10),C7=2,N0=1
5257 IF POS(" "<>N$(11,10))=0 THEN N$(11,10)=A$(101,10)
5260 CALL "ZZENTR","SUX",A{ALL},N$,X4$,X3$,30,13,11,10,C0,"","{3"+X$,C0$,"FM2ODB13","FM1","",""; IF ABS(C0)>4 THEN GOSUB 6400; GOSUB 5390; ON C0 GOTO 5260,5261
5265 IF C0=-2 THEN GOTO 5206 ELSE IF POS(" "<>N$(11,10))=0 THEN EXITTO 5050
5270 FIND (Z[8],KEY=N$,DOM=5260)IOL=0380
5275 PRINT @(42,13),H$(42,40),
5300 REM "Convert to new & write out
5310 H$(1,20)=A$(91,10)+A$(101,10),H$(109)="",H[6]=0,H[7]=0,H[8]=0,H[9]=0,H[10]=0,H[11]=0,H[12]=0,H[13]=0,H[14]=0,H[15]=0,H[16]=0,H[17]=0
5320 WRITE (Z[8],KEY=H$(1,20))IOL=0380
5330 GOSUB 5600
5350 GOTO 5050
5380 RETURN 
5390 PRINT @(0,11),'CE',@(0,12),"Copy this item from customer:",@(24,13),"item:",
5391 CALL "ZZDISP","A",N$(1,10),"A/R",X3$,"","",30,12,X4$
5392 PRINT @(42,12),N0$(11,35),@(24,13),"item:",@(30,13),N$(11,10),@(42,13),H$(42,40),
5395 RETURN 
5600 REM "Copy records for item copy, keep Z(1) open to write out stuff
5605 Z$="02C PO1...  03C FM0...  04C FMP...  05C AR1...  02O FMS...  03O FMU...  04O FMV...  05O FM2...  "
5610 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 5611,9900
5615 REM "Write out to FM2 file
5620 WRITE (Z[5],KEY=C$(21,3)+C$(24,16)+C$(40,2)+C$(11,10)+C$(1,10))
5625 FOR I=2 TO 4
5630 GOSUB 5645
5635 NEXT I
5637 Z$="02C FMS...  03C FMU...  04C FMV... 05C FM2...  02O PO1...  03O FM0...  04O FMP...  05O AR1...  "
5638 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 5639,9900
5640 RETURN 
5645 REM "Transfer records with key N$ in file Z(I) to key A$(91,10)+A$(101,10)
5650 EXTRACT (Z[I],KEY=N$,DOM=5651)
5655 K$=KEY(Z[I],END=5690)
5660 IF K$(1,LEN(N$))<>N$ THEN GOTO 5690
5665 READ RECORD (Z[I],END=5690)A8$
5670 A8$(1,20)=A$(91,10)+A$(101,10)
5675 WRITE RECORD (Z[I],KEY=A8$(1,LEN(K$)))A8$
5680 READ (Z[I],KEY=K$,END=5690)
5685 GOTO 5655
5690 RETURN 
5700 REM "Return from forms setup, set X0 and resume processing,L9=4 means new item, L9=5 means existing item
5710 X0=4,C0=0
5715 GOSUB 7700
5717 IF L9=4 THEN GOSUB 7860
5720 GOSUB 6200
5745 GOTO 2020
6000 REM "BACKGROUND
6005 DIM U[1],H[18]
6010 CALL "PO2MA1",X3$,X4$,6000,N0,A$,A{ALL},B$,E$,U$,H$,U[1],H[2],E2$,E4$,R0$
6190 RETURN 
6200 REM "DISPLAY DATA
6225 IF A$(10,1)="S" THEN IF D1$(61,1)="Y" THEN R0$=R0$+"*"
6230 CALL "PO2MA1",X3$,X4$,6200,N0,A$,A{ALL},B$,E$,U$,H$,U[1],H[2],E2$,E4$,R0$
6290 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6410 IF A$(10,1)="S" THEN IF D1$(61,1)="Y" THEN R0$=R0$+"*"
6420 CALL "PO2MA1",X3$,X4$,6400,N0,A$,A{ALL},B$,E$,U$,H$,U[1],H[2],E2$,E4$,R0$
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6460 CALL "PO2MA1",X3$,X4$,6450,N0,A$,A{ALL},B$,E$,U$,H$,U[1],H[2],E2$,E4$,R0$
6462 GOSUB 7600
6464 PRINT @(10,4+V0),A$(7,3),
6490 RETURN 
7300 REM 
7310 ON FPT(C0)*10 GOTO 7390,7325,7330,7390
7325 IF V8$="" THEN X0=X0+4; GOTO 7390 ELSE X0=NUM(V8$(POS(STR(X0:"00")<=V8$,2),2)); GOTO 7390
7330 IF V8$="" THEN X0=X0-6; GOTO 7335 ELSE X0=POS(STR(X0-1:"00")<=V8$,2)-2; IF X0<0 THEN X0=0; GOTO 7390 ELSE X0=NUM(V8$(X0,2)); GOTO 7390
7335 IF X0<0 THEN X0=0; GOTO 7390
7390 C0=3; RETURN 
7500 REM "Display quantity
7504 IF P$(46,1)="Y" AND POS(" "<>I$(168,20))<>0 THEN RETURN 
7505 D8$=""
7506 IF Q0=0 THEN Q0=1
7507 IF I[14]=0 THEN I[14]=1
7508 D2=A[2]/I[14]; IF D2=0 THEN D2=1
7510 IF F[1]=0 THEN GOTO 7590 ELSE D8$="*"
7515 PRINT @(1,18),'SB',"Base cost",@(18,18),"Quantity",@(36),"Cost",'SF',@(1,19),F[0]*D2:M0$,
7520 FOR I=1 TO 5 STEP 2
7525 IF F[I]=0 THEN EXITTO 7590 ELSE I0=INT(I/2)+19
7530 IF U$(20,1)<>"Y" THEN PRINT @(15,I0),F[I]/F[14]:M1$,@(31,I0),F[I+1]*D2:M0$, ELSE PRINT @(15,I0),F[I]/Q0:M1$,@(31,I0),F[I+1]*D2:M0$, ! SSP219166 jdf
7535 NEXT I
7537 IF F[7]=0 THEN GOTO 7590 ELSE PRINT @(53,18),'SB',"Quantity",@(71,18),"Cost",'SF',
7540 FOR I=7 TO 11 STEP 2
7545 IF F[I]=0 THEN EXITTO 7590 ELSE I0=INT(I/2)+16
7550 IF U$(20,1)<>"Y" THEN PRINT @(50,I0),F[I]/F[14]:M1$,@(66,I0),F[I+1]*D2:M0$, ELSE PRINT @(50,I0),F[I]/Q0:M1$,@(66,I0),F[I+1]*D2:M0$, ! SSP219166 jdf
7555 NEXT I
7590 RETURN 
7600 REM "Print P/O stuff
7610 CALL "ZZDISP","A",B$(7,10),"A/P",X3$,"","",17,4,X4$
7615 PRINT @(5,5),G$(11,35),@(61,4),B$(17,4),@(44,5),B$(21,35),
7640 RETURN 
7650 REM "Set defaults based on product code"
7655 DIM D$(50); FIND (Z[4],KEY="T"+A$(25,3),DOM=7646)IOL=0340
7660 IF C$(22,1)="N" THEN A[3]=0; PRINT @(55,13+V0),A[3]:M2$,
7670 IF C$(21,1)="N" THEN A$(81,1)="N"; PRINT @(72,10+V0),A$(81,1),
7680 A$(156,3)=A$(25,3),A$(159,5)=D$(38,5)
7690 RETURN 
7700 REM "Default based on forms info
7705 FIND (Z[8],KEY=A$(91,20),DOM=2260)IOL=0380
7710 A$(25,3)=H$(21,3); GOSUB 7650
7720 A$(28,40)=H$(42,40); GOSUB 7750
7725 A$(77,4)=H$(96,4),A[0]=H[1],A[2]=H[2],A[5]=H[0],A$(15,2)=H$(40,2),A$(17,8)=H$(24,8),A$(111,8)=H$(32,8)
7727 A$(146,4)=H$(100,4),A[20]=H[4],A[21]=H[3]; GOSUB 8270
7728 A$(156,3)=A$(25,3),A$(159,5)=D$(38,5)
7730 GOSUB 6225
7740 RETURN 
7750 REM "Figure starting number A$(68 ,9) from  H$(140,9)
7755 REM "Assumes H$(140,9) is right justified!
7758 IF H$(104,1)<>"Y" THEN RETURN 
7760 Z8=POS(" "<>H$(140,9)); IF Z8=0 THEN GOTO 7785
7765 DIM Z9$(10-Z8,"0")
7770 A$(68,9)=STR(NUM(H$(140,9),ERR=7775)+1:Z9$); GOTO 7780
7775 SETERR 9000; A$(68,9)=H$(140,9)
7780 IF A$(68+8,1)=" " THEN A$(68,9)=" "+A$(68,8); GOTO 7780
7790 RETURN 
7800 REM "Default and display for new item
7810 A$(14,1)="N",A$(81,1)="N"
7815 PRINT @(77,4+V0),A$(14,1),@(72,10+V0),A$(81,1),
7840 RETURN 
7850 REM "No IC0 record for Item
7852 GOSUB 7855; GOTO 5016
7855 CALL "ZZPROM",".Y",X3$,Z,"No inventory record for this item exists! Create it?","","",0; ON Z GOTO 7856,5047
7860 DIM I$(187),I[30]
7865 I$(1,20)=H$(1,20),I[14]=H[4],I$(120,4)=H$(100,4),I[15]=H[2],I$(124,4)=H$(96,4),I[17]=H[1],I[18]=H[1]
7870 WRITE (Z[10],KEY=I$(1,20))IOL=0390
7880 CALL "FM2UTI",X3$,I$(1,20),"F"
7890 RETURN 
7900 REM "Set defaults for stock item
7905 FIND (Z[10],KEY=A$(91,20),DOM=7906)IOL=0390; GOTO 7910
7906 A$(101,10)=STR(NUM(A$(101,10),ERR=7907)); FIND (Z[10],KEY=A$(91,20),DOM=7907)IOL=0390; GOTO 7910
7907 X0=X0-1,A$(101,10)=""; GOTO 7990
7910 A$(25,3)=I$(61,3),A$(28,40)=I$(21,40),A$(77,4)=I$(124,4),A[0]=I[17],A[2]=I[15],A[5]=I[19],A$(15,2)=I$(80,2),A$(17,8)=I$(64,8),A$(111,8)=I$(72,8)
7912 GOSUB 8250
7915 GOSUB 7650
7920 GOSUB 8100
7935 GOSUB 6225
7940 X0=X0+4
7950 REM "Figure carton labels
7955 IF D$(22,1)="N" THEN A[3]=0; GOTO 7990
7960 IF A[11]=0 OR A[5]=0 THEN A[3]=0 ELSE A[3]=INT(A[11]/A[5]*1.1+.9); IF A[3]<0 THEN A[3]=0
7980 PRINT @(72,13+V0),A[3]:"###0",
7990 RETURN 
8000 REM "Figure extension
8002 IF D1$(61,1)="Y" OR U$(21,1)="Y" THEN RETURN 
8003 REM "Figure price only if F9$="X"
8004 IF F9$="X" THEN F9$=""; IF P$(46,1)="Y" AND POS(" "<>I$(168,20))<>0 THEN GOSUB 8500
8005 IF U$(20,1)="Y" THEN Q0=U[1] ELSE Q0=A[2]
8006 IF Q0=0 THEN Q0=1
8010 A[4]=A[11]/Q0*A[0]
8020 IF U[0]<>0 THEN A[4]=A[4]/U[0]
8045 RETURN 
8100 REM "Assign inv values to cust job rec, read in inv-vendor stuff
8110 DIM H$(126); H$(24,18)=I$(64,18),H$(96,4)=I$(124,4),H[2]=I[15],H[0]=I[19]
8115 IF I9=1 THEN I9=0; GOTO 8140
8120 FIND (Z[6],KEY=A$(91,20)+B$(7,14),DOM=8140)IOL=0360
8125 A$(77,4)=F$(55,4),A[0]=F[0],H$(96,4)=F$(55,4),A[2]=F[14],H[2]=F[14],H[0]=F[16],A[5]=F[16]
8126 I[14]=F[14],A$(77,4)=F$(55,4)
8130 IF A[0]=0 THEN A[0]=F[13]
8140 RETURN 
8200 REM "Fix cost if UM changes
8205 IF Q2=0 THEN Q2=1
8220 CALL "FM2EXT",Z[13],1,A$(77,4),A[2],A[1],O0,A[4],M6[1]; A[0]=O0
8225 CALL "FM2EXT",Z[13],1,A$(77,4),A[2],A[7],O0,A[14],M6[1]; A[8]=O0; CALL "FM2EXT",Z[13],1,A$(77,4),A[2],A[10],O0,A[16],M6[1]; A[17]=O0
8230 GOSUB 8000
8231 IF Q0=0 THEN Q0=1
8232 IF R0$="" THEN RETURN 
8233 IF A[17]=0 THEN A[17]=1
8234 IF A[7]=0 THEN A[7]=1
8235 A[14]=A[7]/Q0*A[8],A[16]=A[10]/Q0*A[17]
8236 IF U[0]<>0 THEN A[14]=A[14]/U[0],A[16]=A[16]/U[0]
8240 RETURN 
8250 REM "Default Selling Price
8252 IF LEN(A$)<170 THEN A$=A$+DIM(170-LEN(A$)); REM "SSP128433 & 141145
8255 IF P4$(213,1)<>"Y" THEN GOTO 8285
8257 IF P4$(224,1)="Y" OR (P4$(224,1)="C" AND POS(" "<>I$(1,10))>0) THEN GOTO 8258 ELSE GOTO 8290
8260 A$(146,4)=I$(120,4),A[21]=I[0],A[20]=I[14]
8270 DIM V[1]; FIND (Z[13],KEY="U/M"+A$(146,4),DOM=8271)IOL=0405; GOTO 8280
8275 A$(146,4)="M",A[21]=A[21]*A[20]/1000; GOTO 8270
8280 IF V$(21,1)="Y" THEN GOTO 8275
8285 RETURN 
8290 A$(146,4)="",A[21]=0,A[20]=0
8295 RETURN 
8300 REM "Default based on special
8310 A$(28,40)=D1$(12,40),A$(25,3)=D1$(52,3),A$(77,4)=D1$(55,4),A[2]=D[0]
8312 IF D1$(61,1)="Y" THEN A[4]=D[2] ELSE A[0]=D[2]
8315 READ (Z[13],KEY="U/M"+A$(77,4),DOM=8316)IOL=0400
8335 GOSUB 6200
8340 RETURN 
8350 REM "Display Sell Line
8355 PRINT @(0,16),'CL',@(1),"Sell:",@(7),A$(146,4),@(12),A[20]:M2$,@(36),A[21]:M6$,@(65),"Months:"; IF P$(64,1)="Y" THEN PRINT @(48),"SED:",@(53),FND$(A$(150,6)),
8358 RETURN 
8360 REM "Display Standard Cost Line
8365 PRINT @(0,16),'CL',@(1),"StdC:",@(7),A$(164,4),@(12),A[23]:M2$,@(36),A[22]:M6$,@(49),"PC:",@(53),A$(156,3),@(58),"CC:",@(62),A$(159,5),
8370 RETURN 
8375 REM "Display Cost Line
8380 PRINT @(0,16),'CL',@(1),"Cost:",@(7),A$(77,4),@(12),A[2]:M2$,@(21),A[1]:M1$,@(36),A[0]:M6$,@(63),A[5]:M2$,@(72),A[3]:"###0",
8385 GOSUB 8000
8390 RETURN 
8400 REM "Set cost based on quantity
8402 REM IF U$(20,1)<>"Y" THEN LET D2=1; GOTO 8405
8403 IF I[14]=0 THEN I[14]=1
8404 D2=A[2]/I[14]; IF D2=0 THEN D2=1
8405 IF F[0]<>0 THEN PRECISION M6[1]; A[0]=F[0]*D2
8408 IF F[1]=0 THEN GOTO 8430 ! WO273055
8410 DIM C[12,1] ! WO 273055
8412 FOR I=1 TO 6; C[I,0]=F[1+(I-1)*2],C[I,1]=F[2+(I-1)*2]; NEXT I
8415 FOR I=7 TO 12; C[I,0]=F[1+(I+1)*2],C[I,1]=F[2+(I+1)*2]; NEXT I ! WO273055, breaks 7 through 12 are in IC2[17] through [28]
8420 FOR I=1 TO 12 ! WO273055, additional 6 cost breaks
8422 IF ABS(A[11])<C[I,0] THEN EXITTO 8430
8426 IF C[I,0]<>0 THEN PRECISION M6[1]; A[0]=C[I,1]*D2
8428 NEXT I
8430 PRINT @(36,16),A[0]:M6$,
8435 PRECISION 2
8440 RETURN 
8500 REM "Feature Driven Pricing
8501 REM "IF F8$="*" THEN DON'T SET THE PRICE
8505 IF A$(10,1)<>"I" OR POS(" "<>I$(168,20))=0 THEN RETURN 
8510 O0$=I$(82,10)+I$(168,20)
8520 CALL "FM2OB9",X3$,O0$,A$(77,4),A[2],A$(77,4),A[2],A[1],O0,O1,M6[1]
8525 O3=0; IF F8$<>"*" THEN A[0]=O0
8530 O2=NUM(B$(174,5),ERR=8544)
8535 IF O2<>0 THEN PRECISION M6[1]; O3=O0-O0*O2/100; IF F8$<>"*" THEN A[0]=A[0]-A[0]*O2/100
8538 PRECISION 2; IF R0$<>"" THEN GOTO 8544
8539 IF O3=0 THEN PRINT @(19,18),'CE',; GOTO 8544
8540 PRINT @(21,18),"FDP unit cost: ",O0:M6$,@(19,19),"Discounted cost: ",O3:M6$,
8544 F8$=""
8545 RETURN 
8600 REM "Remove any special shipping lines
8610 READ (Z[15],KEY=B$(297,9)+A$(7,3),DOM=8611)
8620 SPEC_SHIP_KEY$=KEY(Z[15],END=8640)
8630 IF SPEC_SHIP_KEY$(1,12)=B$(297,9)+A$(7,3) THEN REMOVE (Z[15],KEY=SPEC_SHIP_KEY$); GOTO 8620
8640 RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,"PO2MA2",Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9705 REM "For call from IC2MAI to PO2MAA
9706 IF LEN(Q1$)=12 THEN PRINT @(0,3),'CE',
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9714 IF R0$<>"" THEN GOSUB 8700
9715 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9730 RUN U1$(1,6)
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9940 L0=1,X0$="PO2MAB"
9950 RUN "PO2MAB"
9999 END 
38055 IF A$(10,1)<>"I" OR P4$(213,1)<>"Y" THEN GOTO 2020
56000 REM "198988-Manage Customer Supplied Inventory. Item Setup, Purch ord 
56001 ! "200130-Storage Expiration Date (SED) needs to be entered at the    
56003 REM "219166-Issue with the Quantity break pricing that is used in       
56004 REM "264206-Customer default parameter; Number of months for lot storage
56005 REM "273055-Expand IC2 for 6 additional cost breaks, allow entry in GUI 
