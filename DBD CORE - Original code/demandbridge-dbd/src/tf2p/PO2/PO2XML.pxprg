0010 REM "Fill in XML Template for P/O <PO2XML>"
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 03/17/22 - 0.900476 - crg - SSP# 307397
0037 REM "307397-DBD-255 - Impending Whse Receipt notification - 943 XML     
0040 REM "Copyright 2022 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 SETERR 0100; ENTER Q0$
0095 BEGIN 
0100 SETERR 9000
0110 X0$="PO2XML",X1$="Setup XML file for P/O"
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0500 REM "Open files
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O PO6... 02O PO1... 03O ECY... 04O ECA... 05O AP4... 13O ZZPARM"
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0590 DIM XMLOUT$(40)
0591 ! For testing
0592 READ (Z[13],KEY=X3$(9,3)+"XMLOUT",DOM=9900)XMLOUT$
0599 ! **************************************************
0600 ! Read in Server info and buyer info
0610 DIM ECY$:IOL(Z[3]); READ DATA FROM DIM(LEN(ECY$)),REC=ECY$ TO IOL=IOL(ECY$); READ (Z[3],KEY=XMLOUT$(25,15),DOM=*NEXT,REC=ECY$)
0620 DIM ECA$:IOL(Z[4]); READ DATA FROM DIM(LEN(ECA$)),REC=ECA$ TO IOL=IOL(ECA$); READ (Z[4],KEY=XMLOUT$(10,15),DOM=*NEXT,REC=ECA$)
0699 ! *************************************************
0990 READ (Z[1],KEY="V"+X0$,DOM=*NEXT)
1000 PROCESS_V_KEYS:! Process V records that match this program
1010 KEY$=KEY(Z[1],END=9900); IF MID(KEY$,2,6)<>X0$ THEN GOTO 9900 ELSE READ (Z[1])
1020 PO_NUM$=MID(KEY$,10,9)
1030 DIM PO1$:IOL(Z[2]); READ DATA FROM DIM(LEN(PO1$)),REC=QSD$ TO IOL=IOL(PO1$); EXTRACT (Z[2],KEY=PO_NUM$,DOM=END_OF_PROCESSING,REC=PO1$,BSY=GET_NEXT_PO6) ! If BSY then skip for now, SSP245945, BSY was going to END_OF_PROCESSING, changed to GET_NEXT_PO6, it was removing the record from PO6, we only want to skip it for now and try again next time in here.
1035 DIM AP4$(426); FIND (Z[5],KEY=PO1.VEND_DIV$+PO1.VEND_CODE$,DOM=*NEXT)AP4$(1); WEB_ADDR$=STP(AP4$(324,40)) ! SSP234945
1050 CREATE_OUTPUT_FILE:
1051 IF MID(KEY$,1,1)="W" THEN DOC_TYPE$="943",WHSE_CODE$=MID(KEY$,19,4) ELSE DOC_TYPE$="850" ! DBD-255
1055 OUT$=STP(ECY.SERVER_DIRECTORY$)+DLM+STP(SUB(ECY.FILE_MASK$,"*",DOC_TYPE$+"_"+PO_NUM$),3),OUT_TMP$=OUT$+".tmp"; REM " ssp199338,DBD-255
1060 ERASE OUT_TMP$,ERR=*NEXT
1065 SERIAL OUT_TMP$,ERR=CREATE_OUT_FILE_ERROR
1070 OUTPUT=HFN; OPEN LOCK (OUTPUT)OUT_TMP$
1200 ! Add entries to output file
1210 PRINT (OUTPUT)"basic=EC3PO1"
1220 PRINT (OUTPUT)"user="+STP(XMLOUT$(10,15))
1225 PRINT (OUTPUT)"password="+STP(ECA.BUYER_PASSWORD$)
1228 IF MID(KEY$,1,1)="V" THEN {! DBD-255
1230 PRINT (OUTPUT)"template="+WEB_ADDR$+"_template" ! SSP234945, make consistant with other files, use vendor web address field PRINT (OUTPUT)"template=850_"+STP(FN%ZZDISP$(PO1.VEND_DIV$+PO1.VEND_CODE$,"A/P"),3)+"_template"; REM " ssp199338
1231  } ELSE {
1232 PRINT (OUTPUT)"template=943_flowpoint_template"; PRINT (OUTPUT)"doc_type=943"; PRINT (OUTPUT)"whse_code="+WHSE_CODE$ ! DBD-255
1233  }
1235 PRINT (OUTPUT)"po="+PO1.PO_DIV$+PO1.PO_NUM$
1240 PRINT (OUTPUT)"xml_to_vendor=Y"
1250 PRINT (OUTPUT)"no_header=Y"
1255 PRINT (OUTPUT)"remove_file=Y"
1290 CLOSE (OUTPUT)
1295 RENAME OUT_TMP$,OUT$,ERR=*NEXT
1480 END_OF_PROCESSING:
1490 REMOVE (Z[1],KEY=KEY$,DOM=*NEXT)
1494 GET_NEXT_PO6:! SSP245945
1495 GOTO PROCESS_V_KEYS
1499 ! *******************************************
2990 GOTO 9900
2999 ! *********************************************
8000 ! Error handling
8005 CREATE_OUT_FILE_ERROR:IF ERR=12 THEN GOTO CREATE_OUTPUT_FILE ELSE GOTO END_OF_PROCESSING ! if err 12 then retry erase, else skip it
8915 FNT:DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8950 ! FND$ - Standard date routine
8955 FND:DEF FND$(LOCAL DATE_IN$, LOCAL SPECIAL_FORMAT$)
8960 LOCAL DATE_OUT$
8965 IF NUL(DATE_IN$) THEN DATE_OUT$="" ELSE IF NOT(NUL(SPECIAL_FORMAT$)) THEN DATE_OUT$=FN%FMT_TFDATE$(DATE_IN$,SPECIAL_FORMAT$) ELSE CALL "ZZDISP","DX",DATE_IN$,"",X3$,DATE_OUT$,"",0,0,X4$
8970 RETURN DATE_OUT$
8975 END DEF
9000 REM "ERROR PROCESSING                                                 
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL                                
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 SETERR 9940
9930 EXIT 
9940 RUN "ZZ2PGC"
9999 END 
56002 REM "199338-SENDING ORDER TO BROKER FORMS                               
56004 REM "245945-BCT PO's that print and also go out XML, programs are both  
56006 REM "234945-Flowpoint - change how template name constructed
56007 REM "307397-DBD-255 - Impending Whse Receipt notification - 943 XML     
