0010 REM "Purchase order receiving <PO2MAG>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 05/17/22 - 11.064884 - crg - SSP# 307403
0037 REM "307403-DBD-265: AP Invoice and PO Receiving parallel files         
0040 REM "Copyright 2022 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="PO2MAG",X1$="Purchase Order Receiving",K9$="",K9=0
0120 DIM Z0$(80,"-"),C0$(12),L[21]
0130 K0=6,K1=1
0135 C9=-1
0140 M0$="###,###.00-",M1$="#######.00-"
0145 GOSUB 8950
0175 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0200 REM "
0230 IF Q1$<>"" THEN X2=-1; PRINT @(0,7),'CE',
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$
0312 IOLIST I$
0320 IOLIST B$
0330 IOLIST J$,J[0],J[1],J[2],J[3],J[4],J[5],J[6],J[7],J[8],J[9],J[10],J[11],J[12]
0340 IOLIST L$,L[0],L[1],L[2],L[3],L[4],L[5],L[6],L[7],L[8],L[9],L[10],L[11],L[12],L[13],L[14],L[15],L[16],L[17],L[18],L[19],L[20],L[21],L[22],L[23],L[24],L[25],L[26],L[27]
0350 IOLIST M$,M[0],M[1],M[2],M[3],M[4],M[5],M[6],M[7],M[8],M[9],M[10],M[11],M[12],M[13],M[14],M[15],M[16],M[17],M[18],M[19],M[20],M[21],M[22],M[23],M[24],M[25],M[26],M[27],M[28],M[29]
0360 IOLIST U$,U[0],U[1]
0380 IOLIST H$,H[0],H[1],H[2],H[3],H[4],H[5],H[6],H[7],H[8],H[9],H[10],H[11],H[12],H[13]
0410 IOLIST K$,K[0],K[1],K[2],K[3],K[4],K[5],K[6],K[7],K[8],K[9],K[10],K[11],K[12],K[13]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X PO1...  02O AP4...  03O PO7...  04O PO2...  05O POA...  06O APD...  07O FS2...  08O FS1...  09O AP2...  11O API... 12O FMN...  13O ZZPARM  14O FTK...  15O ZY9     16O FTO...  17OSPO1...  "; REM "SSP# 131719
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0570 READ (Z[13],KEY=X3$(9,3)+"F/M")P8$
0580 READ (Z[13],KEY=X3$(9,3)+"I/C")P9$
0590 READ (Z[1],KEY=K9$,DOM=0591)
0595 DIM SBO$(255); READ (Z[13],KEY="osec"+X3$(40,3),DOM=0596)SBO$; REM "WO113448
0600 REM "
0620 GOSUB 6000
0800 REM 
0900 GOSUB 7400
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(200),A7$(33),H[13]
1035 DIM A$(383),A9$(19),I9$(50)
1045 A$(1,0+K9)=K9$+A1$
1049 IF Q1$="*" THEN Q1$=""
1050 FRT_FLAG$=""; REM "PO7 FLAG
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9700
1140 ON X0 GOTO 1200,1250,1600
1200 REM "P/O number
1205 IF A1$<>"" THEN A9$(1,9)=A1$; CALL "ZZDISP","A",A9$(1,9),"P/O",X3$,"","",39,3+V0,X4$; GOTO 1220
1207 X$="To enter P/O code suffix, press right arrow key. F10- Lookup by sales order num." ! 227610
1210 CALL "ZZENTR","AXUX",A{ALL},A9$,X4$,X3$,39,3+V0,1,9,C0,"P/O","{1"+X$,"","PO2MAG00","PO1","",""; IF C0=-7 THEN GOSUB LOOKUP_F10; END_IF ; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211 ! 227610
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9700,6970
1230 GOTO 1500
1240 GOTO 1120
1250 REM "Invoice number
1252 GOSUB 8550; REM "Display notes
1253 IF SBO$(69,1)="Y" THEN GOTO 1290; REM "WO113448
1254 IF A$(361,1)="Y" THEN GOTO 1290; REM "SSP 198988 Disallow access to Entering Vendor Invoice if Customer Supplier Vendor
1255 GOSUB 7500; ON A9 GOTO 1256,1650
1259 A8$=A9$(1,9),X$="Leave blank to post receipt without vendor invoice"
1260 CALL "ZZENTR","SXU",A{ALL},A9$,X4$,X3$,17,7+V0,10,10,C0,"P/O","{2"+X$,A8$,"PO2MAG02","PO7","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1260,1261
1262 IF ABS(C0)=2 AND LEN(A1$)>0 THEN A9$(1)="",A1$=A1$(1,0); PRINT 'CF',
1265 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1266,6800,9790
1266 IF ABS(C0)=2 AND A9$(9,1)<>" " THEN GOTO 1258
1270 I9=0; READ (Z[5],KEY=A9$(10,10),DOM=1271)
1272 I$=KEY(Z[5],END=1275); IF I$(1,10)<>A9$(10,10) THEN GOTO 1275
1273 DIM I7$(62); FIND (Z[3],KEY=I$(11,9)+I$(1,10)+I$(20,4),DOM=1274)I7$; I9=1
1274 GOTO 1290
1275 F0=1; DIM A7$(33); A7$(1)=X3$(15,6); GOSUB 7700
1280 F0=0
1285 FIND (Z[6],KEY=A7$(7,6)+A$(7,10)+A9$(10,10),DOM=1635); GOTO 1635
1287 CALL "ZZPROM",".4",X3$,Z,"This invoice exists already in the A/P invoice entry file","","",0; X0=0; GOTO 1120
1290 GOTO 1120
1500 REM "
1505 Z9=-1
1520 READ (Z[1],KEY=A9$(1,9),DOM=1210)IOL=0310
1521 Z9=0
1522 GOSUB 8700; REM SSP 189962
1524 IF P8$(325,1)="Y" THEN GOSUB CHECK_AR_MESSAGE ! SSP239615 jdf
1526 IF A$(351,1)="B" AND P8$(255,1)="Y" THEN CALL "ZZPROM","Y",X3$,Z,"This PO is bill as shipped.  Continue?","","",0; ON Z GOTO 1527,1000
1527 IF X3$(145,1)="1" AND A$(351,1)="B" AND P8$(280,1)="Y" THEN GOSUB 8100
1528 IF X3$(145,1)="1" THEN SRC$="W"; GOSUB 8200; REM "SSP125960 SSP 262321
1529 IF X3$(145,1)="1" THEN SRC$="I"; GOSUB 8200; REM " SSP# 149136 SSP 262321 
1530 FIND (Z[2],KEY=A$(7+K9,10),DOM=1531)IOL=0320; GOSUB 6200
1535 DIM I9$(50); FIND (Z[9],KEY=A$(210,2),DOM=1536)I9$
1539 IF Q1$<>"" THEN GOTO 1546
1545 REM CALL "PO2MAH",X3$,A$(173,1),A1$,"-1"
1548 IF (P8$(156,1)<>"Y" OR A9$(9,1)=" ") AND SBO$(79,1)<>"Y" THEN GOTO 1580; REM " SSP# 142625
1550 DIM M[29]; READ (Z[7],KEY=A9$(1,8),DOM=1551)
1552 READ (Z[7],END=1580)IOL=0350
1553 IF SBO$(79,1)<>"Y" THEN GOTO 1560; REM " SSP# 142625
1554 IF A$(351,1)<>"B" THEN GOTO 1560; REM " SSP# 142625 ONLY CHECK B'S
1557 IF M$(147,8)<>A9$(1,8) THEN GOTO 1580 ELSE IF M$(9,1)<>A9$(9,1) OR M[1]<>0 OR M[3]<>0 OR POS(M$(155,1)="MS")>0 THEN GOTO 1560
1558 IF M[4]=0 THEN X$="Line "+M$(6,3)+"-"+FNS$(M$(19,10))+"/"+FNS$(M$(50,40))+" has 0 cost and 0 sell " ELSE X$="Line "+M$(6,3)+"-"+FNS$(M$(19,10))+"/"+FNS$(M$(50,40))+" has 0 cost "; REM " ssp# 142625
1559 CALL "ZZPROM","99",X3$,0,X$,"","",0; X$=""; GOTO 6500; REM " SSP$ 142625
1560 IF M$(147,8)<>A9$(1,8) THEN GOTO 1580 ELSE IF M$(9,1)<>A9$(9,1) OR M[4]<>0 OR M[6]<>0 OR POS(M$(155,1)="MS")>0 THEN GOTO 1552
1561 IF P8$(156,1)<>"Y" THEN GOTO 1580
1565 X$="Line "+M$(6,3)+"-"+FNS$(M$(19,10))+"/"+FNS$(M$(50,40))+" has 0 sell price"
1570 IF SBO$(79,1)="Y" AND A$(351,1)="B" THEN CALL "ZZPROM","99",X3$,0,X$,"","",0; X$=""; IF SBO$(79,1)="Y" AND A$(351,1)="B" THEN GOTO 6500; REM " SSP# 142625
1571 CALL "ZZPROM",".4",X3$,0,X$,"","",0; X$=""; REM " SSP# 142625
1575 GOTO 1552
1580 GOTO 1240
1600 REM "See if new or not
1610 READ (Z[3],KEY=A9$,DOM=1611)A6$; A7$(1)=A6$(39,23)
1615 K$=KEY(Z[3],END=1616); READ (Z[3],KEY=K$)PO7$; FRT_FLAG$=PO7$(94,1)
1616 IF MID(K$,1,19)=A9$ THEN GOSUB 7600; GOTO 1800
1620 REM "LET F0=1; DIM A7$(33); LET A7$(1)=X3$(15,6); GOSUB 7700
1623 REM "LET F0=0
1625 REM "FIND (Z(6),KEY=A7$(7,6)+A$(7,10)+A9$(10,10),DOM=1635) 
1630 REM "CALL "ZZPROM",".4",X3$,Z,"THIS INVOICE EXISTS ALREADY IN THE A/P INVOICE ENTRY FILE","","",0; LET X0=0; GOTO 1120
1635 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1640
1640 IF NEW_FTK$="Y" THEN REMOVE (Z[14],KEY=A9$(1,8)) ! GOTO 1000 ELSE IF NEW_FTO$="Y" THEN REMOVE (Z[16],KEY=A9$(1,9)); GOTO 1000 ELSE GOTO 1000; REM "SSP123659, if you have the parm set to skip vendor invoice number entry and you enter the PO number and then don't add it, the FTK record is already created so we need to remove it before going back to enter the next PO number, also SSP125960 SSP 270181
1645 REMOVE (Z[16],KEY=A9$(1,9),DOM=*NEXT) ! SSP 270181
1648 GOTO 1000 ! SSP 270181
1650 DIM A7$(33); IF I9=1 THEN GOSUB 7800; GOTO 2000 ELSE A7$(1)=X3$(15,6); GOSUB 7700; GOTO 2000
1800 REM "Already on file
1819 IF %HAS_IMAGE_LIB THEN %IL_IMAGE_TYPE$="PO",%IL_IMAGE_KEY$=A9$(1,9)
1820 CALL "ZZPROM","X0PO2MAG",X3$,Z,"","","",0; %IL_IMAGE_TYPE$="",%IL_IMAGE_KEY$=""; ON Z GOTO 2000,6700,8000,6500
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1,C9=1
2020 SETERR 9000; X$="",Q$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF C0>3 OR X0<0 THEN GOTO 1800
2040 ON X0 GOTO 2100,2150,2200,2250,2300,1800
2100 REM "Receipt date
2105 N$=A7$(1,6)
2110 CALL "ZZENTR","DX",A{ALL},A7$,X4$,X3$,59,7+V0,1,6,C0,"",X$,"","PO2MAG04","","",""
2111 IF STP(A7$(1,6))="" THEN GOTO 2110 ! [SSP-209199]
2112 IF N$<>A7$(1,6) THEN CALL "IC2LAC",X3$,X4$,"","!"+A7$(1,6)+A9$(1,9)
2115 IF C0>=1 THEN GOSUB 7700
2140 GOTO 2020
2150 REM "Fiscal Year Ending
2155 J1$=X3$(9,3)+"G/LYE"
2160 CALL "ZZENTR","ZXUX",A{ALL},A7$,X4$,X3$,17,8+V0,7,4,C0,"","{1"+X$,J1$,"PO2MAG06","GLYE","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2160,2161
2170 FIND (Z[13],KEY=X3$(9,3)+"G/LYE"+A7$(7,4),DOM=2160)P1$
2173 IF POS(P1$(99,1)="CR")<>0 THEN CALL "ZZPROM",".4",X3$,Z,"Closed Years must be reopened by System Administrator prior to posting.","","",0; GOTO 2160
2190 GOTO 2020
2200 REM "Accounting Period
2210 CALL "ZZENTR","ZUX",A{ALL},A7$,X4$,X3$,59,8+V0,11,2,C0,"","{2"+X$,"","PO2MAG082","","",""
2217 IF A7$(11,2)<"01" OR A7$(11,2)>P1$(13,2) THEN GOTO 2210
2220 GOSUB 6245
2221 FLAG$=""
2223 IF C0<>0 THEN CALL "AR2DTE",X3$,X4$,A7$(7,6),FLAG$
2224 IF FLAG$="*" THEN X0=X0-2; REM GOTO 02200
2240 GOTO 2020
2250 REM "Freight amount
2255 IF POS(" "<>A9$(10,10))=0 THEN GOTO 2020
2256 DIM Q[1]; Q[1]=NUM(A7$(13,11),ERR=2257)
2260 CALL "ZZENTR","N",Q{ALL},A7$,X4$,X3$,17,9+V0,1,621,C0,M0$,"{2"+X$,"","PO2MAG083","","",""
2265 A7$(13,11)=STR(Q[1]:"-#######.00",ERR=2260)
2290 GOTO 2020
2300 REM "Prepayments - Invoice no
2305 IF A9=0 THEN GOTO 2020
2310 CALL "ZZENTR","SU",Q{ALL},A7$,X4$,X3$,59,9+V0,24,10,C0,"",X$,"","PO2MAG06","","",""
2340 GOTO 2020
3000 CHECK_AR_MESSAGE:! Check for customer entry in "AS5" SSP239615 jdf
3010 IF NUL(A$(179,10)) THEN RETURN ! SSP239615 jdf
3020 CALL "AR2MAG",X3$,X4$,A$(179,10),13,0,X; IF X>0 THEN GOSUB 6400 ! SSP239615 jdf
3060 RETURN ! SSP239615 jdf
6000 REM "BACKGROUND
6004 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,-1
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(31,3+V0),"P/O no:",@(9,4+V0),"Vendor:",@(49,4+V0),"Plant code:",@(0,6+V0),Z0$,
6070 PRINT @(1,7+V0),"Vendor invoice:",@(45,7+V0),"Receipt date:",@(4,8+V0),"Fiscal year:",@(40,8+V0),"Accounting period:",@(8,9+V0),"Freight:",'CL','SB',@(0,10+V0),Z0$,
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 CALL "ZZDISP","A",A$(7+K9,10),"A/P",X3$,"","",17,4+V0,X4$
6224 PRINT @(5,5+V0),B$(11,35),@(61,4+V0),A$(17+K9,4),@(44,5+V0),A$(21+K9,35),
6230 PRINT @(17,7+V0),A9$(10,10),
6232 CALL "ZZDISP","D",A7$(1,6),"",X3$,"","",59,7+V0,X4$
6240 PRINT @(17,8+V0),A7$(7,4),@(17,9+V0),A7$(13,11),@(59,8+V0),A7$(11,2),
6245 IF A7$(11,2)<>"  " THEN PRINT @(62,8+V0),"ending",; CALL "ZZDISP","D",P1$(15+6*NUM(A7$(11,2)),6),"",X3$,"","",69,8+V0,X4$
6300 PRINT @(5,11),B$(11,35),@(5,12),B$(46,30),@(5,13),B$(76,30),@(5,14),B$(106,16)," ",B$(122,2),
6310 CALL "ZZDISP","A",B$(124,9),"ZIP",X3$,"","",26,14,X4$
6320 PRINT @(44,11),A$(21,35),@(44,12),A$(86,30),@(44,13),A$(116,30),@(44,14),A$(146,16)," ",A$(162,2),
6330 CALL "ZZDISP","A",A$(164,9),"ZIP",X3$,"","",65,14,X4$
6335 IF POS(" "<>A$(56,30))>0 THEN PRINT @(44,15),"ATTN: ",A$(56,30),
6340 IF POS(" "<>A$(297,9))>0 THEN PRINT @(5,16),"TERMS: (",A$(210,2),") ",I9$(3,30)
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6460 CALL "ZZDISP","A",A9$(1,9),"P/O",X3$,"","",39,3+V0,X4$
6490 RETURN 
6500 REM "DELETE
6502 REMOVE_FTO=1 ! SSP232394 jdf
6510 READ (Z[3],KEY=A9$,DOM=6511)
6515 DIM J[12]
6520 K$=KEY(Z[3],END=6550)
6525 IF K$(1,LEN(A9$))<>A9$ THEN GOTO 6550
6530 READ (Z[3],KEY=K$)IOL=0330
6531 FROM$="6500"; REM "DON'T DELETE H[8] IF FROM HERE
6535 IF K$(23,1)=" " AND K$(20,3)<>"   " THEN GOSUB 8400
6537 IF J$(66,3)<>"   " THEN CALL "FM2ICC",X3$,X4$,"PO2MAG",-1,J$,J{ALL},"",0; REM "WO111482, Inventory costing, delete FTF record that goes with this PO7 record
6540 REMOVE (Z[3],KEY=K$)
6544 REMOVE (Z[5],KEY=K$(10,10)+K$(1,9)+K$(20,4),DOM=6545); REM " SSP# 137671
6545 GOTO 6520
6550 REMOVE (Z[14],KEY=A9$(1,8),DOM=6551); REM "WO111482
6551 FTO_P$=""; FTO_P$=KEP(Z[3],ERR=*NEXT) ! SSP232394 jdf
6552 FTO_N$=""; FTO_N$=KEY(Z[3],ERR=*NEXT) ! SSP232394 jdf
6553 IF MID(FTO_P$,1,9)=A9$(1,9) THEN REMOVE_FTO=0 ! SSP232394 jdf
6554 IF MID(FTO_N$,1,9)=A9$(1,9) THEN REMOVE_FTO=0 ! SSP232394 jdf
6555 IF REMOVE_FTO THEN REMOVE (Z[16],KEY=A9$(1,9),DOM=6556); REM "SSP125960   ! SSP232394 jdf
6557 FRT_FLAG$="",A1$=""; REM "SET FLAG IN PO7 TO N SSP 269538
6560 IF POS(" "<>A9$(10,10))<>0 AND A$(351,1)="B" OR A$(173,1)<>"C" THEN U1$="AP2DAA54"+A9$+A$(7,10)+A7$(1,12)+A$(179,2)+A7$(24,10)+"D"+X0$+"01*"+U1$ ELSE IF POS(" "<>A9$(10,10))<>0 AND A$(351,1)<>"B" THEN U1$="AP2DAA54"+A9$+A$(7,10)+A7$(1,12)+A$(179,2)+A7$(24,10)+"D"+"AR2EAA41"+A7$(7,6)+A9$(1,8)+A7$(1,6)+A7$(13,11)+A9$(10,10)+X0$+"01*"+U1$ END_IF ; GOTO 9700
6575 IF POS(" "<>A9$(10,10))<>0 THEN GOTO 9700
6590 GOTO 1000
6600 REM "ALT KEY DATA READS
6610 READ (Z[1],KEY=A9$(1,9),DOM=6611)IOL=0310
6615 FIND (Z[2],KEY=A$(7+K9,10),DOM=6616)IOL=0320
6620 DIM A6$(62); READ (Z[3],KEY=K0$,DOM=6621)A6$(1)
6622 DIM I9$(50); READ (Z[9],KEY=A$(210,2),DOM=6623)I9$
6625 A7$(1)=A6$(39)
6630 FIND (Z[13],KEY=X3$(9,3)+"G/LYE"+A7$(7,4),DOM=6631)P1$
6680 GOSUB 6200
6690 RETURN 
6700 REM "ADD ENTRY
6702 REM " READ (Z[8],KEY=A9$(1,8))FS1$;REM " SSP# 129955
6703 IF P8$(297,1)<>"Y" THEN GOTO 6709; REM "SSP# 129955
6705 CST_FLAG$=""; REM " SSP# 129955
6707 CALL "PO2CST",A$,X3$,X4$,J$,A9$(1,8),CST_FLAG$,"CHECK","PO2MAG",A{ALL},J{ALL},EXTRA_NUM2; REM " SSP# 129955
6709 REM " Back out PO stuff before going to lines, it will be put back later in PO2MAG ;REM " SSP# 129955
6710 FLAG$=""
6715 CALL "AR2DTE",X3$,X4$,A7$(7,6),FLAG$
6720 IF FLAG$="*" THEN GOTO 9900
6725 CALL "PO2MAH",X3$,A$(173,1),A9$(1,9),"-1"
6730 IF POS(" "<>A9$(10,10))=0 THEN Q2$="r" ELSE Q2$="R"
6740 U2$=A9$(1,9)+Q2$+A9$(10,10)+A7$,U2$="PO2MAB"+STR(LEN(U2$):"00")+U2$
6742 IF Q2$="R" THEN U2$=U2$+"AP2DAA55"+A9$+A$(7,10)+A7$(1,12)+A$(179,2)+A7$(24,10)+A$(210,2)
6743 IF POS(CST_FLAG$="PY")>0 AND SBO$(69,1)<>"Y" THEN U2$=U2$+"AR2EAS08"+A9$(1,8); GOTO 6750; REM " SSP# 129955
6745 IF SBO$(69,1)<>"Y" AND A$(173,1)="C" AND POS(A$(351,1)="BS")=0 THEN U2$=U2$+"AR2EAS08"+A9$(1,8)+"AR2EAA41"+A7$(7,6)+A9$(1,8)+A7$(1,6)+A7$(13,11)+A9$(10,10)
6750 U1$=U2$+X0$+"01*"+U1$
6755 PRINT @(0,7+V0),'CE',
6775 GOSUB 7900
6780 GOTO 9700
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[3],END=6895)
6820 IF A1$="" THEN GOTO 6800 ELSE IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6825 A$(1,LEN(A1$))=A1$
6826 READ (Z(1),KEY=A1$(1,9),DOM=*NEXT)IOL=0310 ! SSP#236902
6830 K$=A1$,A9$=A1$(1,19),X$="",FRT_FLAG$="Y"; GOSUB 7600; GOTO 1800 ! SSP232595 jdf             
6840 GOTO 6890
6850 REM "PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 K0$=A1$,A9$=A1$(1,19),A1$=A9$(1,9); GOSUB 6600; READ (Z[3],KEY=K0$,DOM=1800); GOTO 1800
6895 PRINT 'RB',; READ (Z[3],KEY=K9$,DOM=1140); GOTO 1140
7400 REM "Audit already run
7410 FIND (Z[3],KEY="",DOM=7490)V0$,K0$
7420 IF K0$>"" THEN CALL "ZZPROM","X1AUDITS",X3$,S3,"","","",0; ON S3 GOTO 9900,7490
7430 EXTRACT (Z[3],KEY="",ERR=9800)
7445 CALL "ZZPROM","X2AUDITS",X3$,S3,Q$,"","",0; ON S3 GOTO 9900,7446
7450 REMOVE (Z[3],KEY="")
7490 RETURN 
7500 REM "Look for pre-payment invoice
7510 A9=0; DIM K[14]; FIND (Z[11],KEY=A$(7,10)+"PP"+A9$(4,5)+"   ",DOM=7590)IOL=0410
7512 PRINT @(0,20),'CE',"Prepayment available for this order ("+FNS$(K$(11,10))+" "+STR(K[13]:"-#####.00")+")"
7513 Q$="Match this invoice against this pre-payment?"
7515 CALL "ZZPROM",".Y",X3$,S3,Q$,"","",0; ON S3 GOTO 7516,7595
7540 A9$(10,10)=K$(11,10)
7545 PRINT @(0,20),'CL',
7550 PRINT @(32,9+V0),'SB',"Invoice no. (for prepmts):",'SF',
7560 F0=1; DIM A7$(33); A7$(1)=X3$(15,6); GOSUB 7700
7565 A9=1; GOSUB 6230
7595 RETURN 
7600 REM "Set values for on file entry
7610 READ (Z[3],KEY=K$,DOM=1611)A6$; A7$(1)=A6$(39,23)
7619 REM "Back out header entry that now carries freight amount.
7620 REMOVE (Z[3],KEY=K$,DOM=7621)
7622 REMOVE (Z[5],KEY=K$(10,10)+K$(1,9)+K$(20,4),DOM=7623)
7624 X0=-1; GOSUB 7930
7630 GOSUB 6630
7640 RETURN 
7700 REM "Default accounting period based on date
7701 IF P8$(221,1)="Y" THEN GOTO 8050
7705 IF POS(" "<>A7$(1,6))=0 THEN GOTO 7790
7710 D1$=X3$(9,3)+"G/LYE"
7715 EXTRACT (Z[13],KEY=D1$,DOM=7716)P1$; GOTO 7720
7720 READ (Z[13],END=7770)P1$
7725 IF P1$(1,8)<>D1$(1,8) THEN GOTO 7770
7730 IF P1$(87,6)<A7$(1,6) THEN GOTO 7720
7740 FOR I=1 TO NUM(P1$(13,2))
7745 IF A7$(1,6)<=P1$(15+I*6,6) THEN A7$(7,6)=P1$(9,4)+STR(I:"00"); EXITTO 7755
7750 NEXT I
7775 DIM P1$(105); READ (Z[13],KEY=D1$+A7$(7,4),DOM=7776)P1$
7780 IF F0<>1 THEN GOSUB 6240
7790 RETURN 
7800 REM "Default if I9=1, A/P invoice already exists
7810 A7$(1)=I7$(39,12)
7825 FIND (Z[13],KEY=X3$(9,3)+"G/LYE"+A7$(7,4),DOM=7826)P1$
7830 GOSUB 6232
7840 RETURN 
7900 REM "Create Freight Header 5/21/90
7910 DIM X$(130); X$(1)=A9$,X$(39,23)=A7$(1,23),X0=1
7915 READ (Z(3),KEY=X$(1,23),DOM=*NEXT)X$; GOTO 7925 ! SSP#210034
7917 X$(96,14)=MID(A$,7,14),X$(110,10)=MID(A$,179,10) ! 307403-DBD-265
7920 WRITE (Z[3],KEY=X$(1,23))X$
7925 WRITE (Z[5],KEY=X$(10,10)+X$(1,9)+X$(20,4))
7928 GOSUB UPD_PRD ! SSP 307228
7930 IF A9$(9,1)=" " THEN GOTO 7990
7935 REM "Adjust Sales Order Header
7940 EXTRACT (Z[8],KEY=A9$(1,8),DOM=7990)IOL=0380
7943 IF LEN(X$)<>0 THEN FRT_FLAG$=X$(94,1); REM " FLAG FROM PO7
7949 IF FRT_FLAG$="Y" AND X0<0 THEN H[8]=H[8]+NUM(A7$(13,11))*X0; GOTO 7952; REM " CHANGE PO7 FLAG
7951 IF FRT_FLAG$<>"Y" AND X0>0 THEN H[8]=H[8]+NUM(A7$(13,11))*X0; FRT_FLAG$="Y"; REM " SSP# 131719
7952 IF LEN(X$)<>0 THEN X$(94,1)=FRT_FLAG$; WRITE (Z[3],KEY=X$(1,23))X$
7960 WRITE (Z[8])IOL=0380
7990 RETURN 
8000 REM "New Exit option
8020 GOSUB 7900
8030 GOTO 1640
8050 REM "Set Fiscal Year/AP from A/R Parameters
8060 READ (Z[13],KEY=X3$(9,3)+"A/R",DOM=8090)D1$
8070 A7$(7,6)=D1$(7,6),D1$=X3$(9,3)+"G/LYE"
8080 GOTO 7775
8090 RETURN 
8100 REM "For Inventory Costing, if they use batch processing and this is a B type order and the flag is set in FS1 then we will check to see if this order number is already being received in another batch, if so display a message with the batch number saying they can't access this order for PO receiving until the other batch is updated, WO111482
8103 NEW_FTK$="N"
8105 DIM FS1$(350); READ (Z[8],KEY=A9$(1,8),DOM=8145)FS1$
8110 IF FS1$(240,1)<>"Y" THEN GOTO 8145
8115 DIM FTK$(60); READ (Z[14],KEY=A9$(1,8),DOM=8120)FTK$; IF FTK$(9,4)=X3$(174,4) THEN GOTO 8145 ELSE GOSUB 8150; IF VALID_BATCH$="Y" THEN CALL "ZZPROM",".4",X3$,0,"Access Denied! This B type order being received in batch "+FTK$(9,4)+" by "+FTK$(13,3),"","",0; EXITTO 1000
8120 FTK$(1,15)=A9$(1,8)+X3$(174,4)+X3$(40,3); WRITE (Z[14],KEY=A9$(1,8),DOM=8115)FTK$; NEW_FTK$="Y"
8145 RETURN 
8150 REM "Check for batch record to make sure FTK record is valid, SSP123659
8155 VALID_BATCH$="N"
8160 READ (Z[15],KEY=FTK$(9,4),DOM=8161)
8165 READ (Z[15],END=8170)ZY9$; IF ZY9$(1,4)=FTK$(9,4) AND ZY9$(6,6)="PO2MAG" THEN VALID_BATCH$="Y"; GOTO 8195 ELSE GOTO 8165
8170 REMOVE (Z[14],KEY=A9$(1,8)); REM "Bogus FTK record, remove and return which will write out the new one and go on
8195 RETURN 
8200 REM "For batch processing, we will write out a record to FTO file when a PO is being received, if another user attempts to receive in another batch they will receive a message and will not be allowed access, similar to FTK file for Inventory Costing, SSP125960
8202 CALL "UPDFTO;CHECK_PO_LOCK",X3$,X4$,SRC$,NEW_FTO$,A9$(1,9),FTO_REC$,BATCH_EXISTS$,"Y"; IF BATCH_EXISTS$="Y" THEN EXITTO 1000 ELSE RETURN ! SSP 262321
8245 RETURN 
8400 REM "Remove PO7 record, and update buckets
8405 DIM L[27]
8410 L0$=J$(1,9)+J$(20,3)
8415 EXTRACT (Z[4],KEY=L0$,DOM=8440)IOL=0340
8417 IF P9$(44,1)="Y" AND MID(J$,95,1)="Y" THEN X$=L0$; CALL "IC2LAC",X3$,X4$,"",X$ ! SSP 239950
8420 L[6]=L[6]-J[0],L[7]=0,L[8]=0,L[14]=0,L[13]=L[13]-J[2],L[12]=0,L[9]=L[9]-J[4],L[10]=0,L[16]=0,L[15]=L[15]-J[6],L[17]=0
8421 IF L[6]<0 THEN L[6]=0
8422 IF L[13]<0 THEN L[13]=0
8423 IF L[9]<0 THEN L[9]=0
8425 IF L[15]<0 THEN L[15]=0
8426 L[1]=L[11]-L[6]
8427 L$(2,1)=CST_FLAG$; CST_FLAG$=""; REM " SSP# 129955
8428 L$(6,1)=""
8429 IF POS(L$(2,1)="LY")<>0 THEN L$(2,1)="P"; REM " SSP# 129955
8430 WRITE (Z[4],KEY=L0$)IOL=0340
8450 REM "Remove FS2
8455 DIM U$(21),U[2]; FIND (Z[13],KEY="U/M"+L$(77,4),DOM=8456)IOL=0360
8460 DIM M[29]
8470 EXTRACT (Z[7],KEY=J$(1,8)+J$(20,3),DOM=8525)IOL=0350
8480 M[7]=M[7]-J[0],M[19]=M[19]-J[2],M[13]=L[19],M[8]=M[8]-J[3]; IF M[8]<0 THEN M[8]=0
8490 IF M[19]=0 OR U$(21,1)="Y" THEN M[15]=0 ELSE GOSUB 8600
8495 IF M$(4,1)<>"Y" THEN IF M[15]<>M[1] THEN M[18]=M[15]/M[1]*M[4]
8500 IF LEN(M$)<204 THEN M$=M$+" "; GOTO 8500
8510 M$(171,12)=L$(119,12),M$(183,6)=L$(131,6),M$(189,9)=L$(137,9),M$(198,7)=STR(L[18]:"####.00")
8515 M$(3,1)="N"
8520 WRITE (Z[7],KEY=J$(1,8)+J$(20,3))IOL=0350
8525 REM " REMOVE FREIGHT FROM FS1 RECORD SSP# 131719
8527 X0=-1
8530 IF FROM$<>"6500" THEN GOSUB 7935
8540 RETURN 
8550 REM "Display notes
8555 FIND (Z[12],KEY=A9$(1,8),DOM=8585)N$
8560 PRINT @(0,10+V0),'CE',
8564 CALL "ZZGRPH",X3$,"B",X{ALL},"",13,10,0,0,0,12,54,0,0,0
8565 FOR I=0 TO 9; PRINT @(15,I+11),N$(9+I*50,50),; NEXT I
8570 CALL "ZZPROM","4",X3$,Z,"","","",0
8571 IF MID(P8$,336,1)="Y" THEN PERFORM "FM2NPA;FORCE_USER_ACK" ! 266632
8575 PRINT @(0,10+V0),'CE',@(0,10+V0),'SB',Z0$,'SF',
8585 N$=""
8590 RETURN 
8600 REM "Figure average cost from exten & quantity
8610 IF U$(20,1)="Y" THEN Q0=U[1] ELSE Q0=L[2]
8620 IF Q0=0 THEN Q0=1
8630 Q5=M[7]/Q0; IF Q5=0 THEN M[15]=0; GOTO 8690
8640 IF U[0]=0 THEN Q6=M[19] ELSE Q6=M[19]*U[0]
8650 M[15]=Q6/Q5
8690 RETURN 
8700 REM "Check if suspended order, if so PO cannot be PO received
8750 DIM SFS1$(350); READ (Z[8],KEY=A9$(1,8),DOM=8799)SFS1$
8760 IF SFS1$(314,1)="Y" THEN CALL "ZZPROM",".4",X3$,0,"Access Denied! This PO is part of a suspended order","","",0; EXITTO 1000
8799 RETURN 
8950 REM "Program flow logic
8960 DIM U0$(6); U0$(1)=FID(0),U0$=U0$+"PF"
8970 CLOSE (14); OPEN (14)"ZZPARM"; FIND (14,KEY=U0$,DOM=8990)U0$,U1$
8975 IF U1$(1,6)<>X0$ THEN Q1$=""; GOTO 8990
8980 Q1=NUM(U1$(7,2)),Q1$=U1$(9,Q1),U1$=U1$(Q1+9)
8985 IF LEN(Q1$)<=9 THEN F9$="" ELSE IF Q1$(10)="*" THEN Q1$=Q1$(1,9),F9$="*"
8990 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9730 RUN U1$(1,6)
9790 IF NEW_FTK$="Y" THEN REMOVE (Z[14],KEY=A9$(1,8),DOM=9791); REM "SSP123659, if we've just written a new FTK record but then hit F4 to get out before writing any PO receiving info, then we need to remove the FTK record, changed line 1265 from going to 9800 to 9790 since there were other places in the program going to 9800 in which case we may not want to remove the FTK record even if it's new
9791 READ (Z(3),KEY=A9$(1,9),DOM=*NEXT) ! SSP#275406
9792 PO7_KEY$=KEY(Z(3),END=*NEXT); IF PO7_KEY$(1,9)=A9$(1,9) THEN GOTO 9800 ! SSP#275406
9794 REMOVE (Z[16],KEY=A9$(1,9),DOM=*NEXT); REM "SSP125960 SSP 270181!SSP#275406
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
10000 REM "F10 - Lookup by sales order number - 227610
10005 LOOKUP_F10:
10006 IF TCB(88)=0 THEN MSGBOX "You must be using Windx to access this feature!",MSG("WARNING"); GOTO LOOKUP_F10_DONE
10010 RETVAL$=""
10020 DIM INPUTVAL$(9); PRINT @(23,20),'CE',"Sales Order Number:",; CALL "ZZENTR","AUX",A{ALL},INPUTVAL$,X4$,X3$,44,20,1,8,C0,"O/P","{1Enter Sales Order number -or- Blank for all, then hit Return.","PO2MAG02","FS1","",""; IF ABS(C0)=4 THEN GOSUB 6400; GOTO 10040
10030 %QRY_ORDER$=STP(INPUTVAL$); PROCESS "QRY_FS2.2","../OPG/OP.EN",RETVAL$; A9$(1,9)=RETVAL$; C0=1
10040 %QRY_ORDER$=""; A9$(1,9)=STP(RETVAL$); C0=1
10045 LOOKUP_F10_DONE:GOSUB 6400
10090 RETURN 
10099 ! 
11000 UPD_PRD:! SSP 307228
11010 IF STP(A9$,2)="" THEN RETURN 
11012 IOLIST JH$,JH[0],JH[1],JH[2],JH[3],JH[4],JH[5],JH[6],JH[7],JH[8],JH[9],JH[10],JH[11],JH[12]
11015 DIM HLD_KY$(23); HLD_KY$(1,19)=A9$(1,19); DIM JH$(130),JH[12]
11020 READ (Z[3],KEY=HLD_KY$,DOM=11090)IOL=11012; HLD_YR$=JH$(45,4),HLD_PRD$=JH$(49,2)
11025 READ_NEXT:
11030 K3$=KEY(Z[3],END=11090); IF K3$(1,19)<>HLD_KY$(1,19) THEN GOTO 11090
11040 READ (Z[3],KEY=K3$)IOL=11012
11050 IF JH$(45,4)=HLD_YR$ AND JH$(49,2)=HLD_PRD$ THEN GOTO READ_NEXT
11060 JH$(45,4)=HLD_YR$,JH$(49,2)=HLD_PRD$
11065 WRITE (Z[3],KEY=K3$)IOL=11012; GOTO READ_NEXT
11090 RETURN 
56002 REM "180032-Wireless Warehousing                                        
56003 REM "198988-Manage Customer Supplied Inventory. Item Setup, Purch ord      
56004 REM "209199-ICF table column name mismatch between Oracle and TopForm   
56005 REM "210034-Governor invoice importing issues                           
56006 REM "227610-New option in PO Receiving, GUI lookup by sales order num.  
56007 REM "232394-FTO record is being removed if you have multiple receivers  
56008 REM "232595-Freight is being retained in the FS1 file if you delete     
56009 REM "236902-PO2MAG-PO recvg - If you use your down arrow key to bring up
56010 REM "239615-Display Customer Invoice Inquiry Message in PO Receiving.   
56012 REM "239950-P/O Receiving-Multiple Vendor bill and Receivings in batch  
56013 REM "262321-WMS:  While in WMS PO receiving - able to delete po  - stock
56014 REM "269538-When Stock PO is deleted, FTO not being removed
56015 REM "270181-Delete order from PO receiving, then check to verify by     
56016 REM "275406-Issue with receiving the same PO in two different batches   
56017 REM "307228-Period change in PO receiving header not updated to lines   
56018 REM "307403-DBD-265: AP Invoice and PO Receiving parallel files         
