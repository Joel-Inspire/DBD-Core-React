0010 REM "Purchase Order Receiving Report <PO2RRA>
0035 REM "5.7 - 03/13/19 - 17.251944 - tma - SSP# 306844
0037 REM "306844-PE(047, PO2RRA-7480) when in PO rec'g.                      
0040 REM "Copyright 2019 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0100 SETERR 9000; SETESC 9300
0110 X0$="PO2RRA",X1$="Purchase Order Receiving Report"
0120 DIM A$(130),A[12],C$(314),D$(310),D[2],E$(145),E[19],F$(224),F[2]
0140 Z0$="##,###,###.00-",Z1$="###.00-",Z2$="##,##0-",Z3$="###,###.00#"
0210 T=1,T0=0,T1=2
0220 W3=131
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3),W[4]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0260 CALL "ZZRPTP",X3$,X0$,X1$,V9$,V0,Q0$,Q1$,Q2$,Q3$,Q4$,Q5$,Q6$
0300 REM "I/O lists
0305 REM "IOLIST FOR PO7 - Purchase order receiving information
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12]
0325 REM "IOLIST FOR PO1 - Purchase order header
0330 IOLIST C$
0335 REM "IOLIST FOR AP4 - Vendor Masterfile
0340 IOLIST D$,D[0],D[1],D[2]
0345 REM "IOLIST FOR PO2 - Purchase order lines
0350 IOLIST E$,E[0],E[1],E[2],E[3],E[4],E[5],E[6],E[7],E[8],E[9],E[10],E[11],E[12],E[13],E[14],E[15],E[16],E[17],E[18],E[19]
0355 REM "IOLIST FOR PO3 - Shipping Information
0360 IOLIST F$,F[0],F[1],F[2]
0370 IOLIST G$
0380 IOLIST I$,I[0],I[1]; REM "SSP38933
0400 IOLIST FTF$,FTF[0],FTF[1],FTF[2],FTF[3],FTF[4],FTF[5],FTF[6],FTF[7],FTF[8],FTF[9],FTF[10],FTF[11],FTF[12],FTF[13],FTF[14],FTF[15]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,IOL=0425
0425 IOLIST V$,V[0],V[1]
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01L PO7...  03O PO1...  04O AP4...  05O PO2...  06O PO3...  07O FMN...  08O FTF...  09O FS1...  10O POH...  11O APD...  13O ZZPARM  "; IF V1$(1,3)<>Y$(5,3) THEN Y$=Y$+"02O "+V1$+"  " ! SSP#269827
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 DIM P0$(100); READ (Z[13],KEY=X3$(9,3)+"I/C",DOM=0531)P0$
0605 N0$="PO2RUA"; GOSUB 6900
0620 REM "Repository Stuff
0630 R6$="N"; IF V3$(2,1)<>"T" THEN CALL "ZR2DGP",ERR=0631,X3$,X4$,"POR_JRNL  ",R6$
0635 IF R6$(1,1)="Y" THEN T9$=X3$(21,6)+X3$(178,6); CALL "ZR2DND",ERR=0636,X3$,X4$,T9$,"",R6$; REM "Signal start of a new document, the key is system date+audit control #
0720 REM 
0800 REM "Alternate Sort Info & total dim"
0810 DIM U[10,2],U0$(10),Y[5,1]
0820 CALL "ZZRSEQ",X3$,X0$,V1$,U,U0,U1,U{ALL},Y{ALL},T0,V0,U0$,T5$,U5$,S9$,X9; ON X9 GOTO 0821,9920
0825 IF V0>1 THEN GOSUB 6950
0850 DIM T[T0,T1]; U5$(1,20)=Q4$
0890 REM "Open Printer
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0898 IF %HAS_IMAGE_LIB THEN CALL "ILGAUD",ERR=*NEXT,X3$,X4$,"R8" ! 174140
0900 REM "Check for restart record"
0905 READ (Z[1],KEY="",DOM=0950)
0910 IF K0$>"" THEN X3$(184,1)="1"
0915 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W4,W4$,W5$; ON W5 GOTO 0916,9900
0920 CALL "ZZREST",X3$,Z[13],Z[1],W9,W3,W,V0$,K0$,K1$,K0,K1
0925 GOSUB 6000
0950 REM " Position read"
0970 IF V2$="" THEN GOTO 0990 ELSE DIM V4$(U); U9=1; FOR X=1 TO U0; V4$(U[X,0],U[X,1])=V2$(U9,U[X,1]); U9=U9+2*U[X,1]; NEXT X
0985 V4$(U,1)=CHR(ASC(V4$(U,1))-1)
0990 READ (U1,KEY=V4$,DOM=0991)
1000 REM "Read next record and check range
1005 U$=KEY(U1,END=5000); IF V2$="" THEN GOTO 1100
1010 FOR X=1 TO U0; U2=U[X,0],U3=U[X,0]*2-1,U4=U[X,1]
1020 IF U$(U2,U4)<V2$(U3,U4) THEN READ (U1); EXITTO 1000
1030 IF U$(U2,U4)>V2$(U3+U4,U4) THEN IF U=1 THEN EXITTO 5000 ELSE READ (U1); EXITTO 1000
1040 NEXT X
1100 REM "Get record"
1105 U8$=U9$; IF U8$="" THEN U8$=U$
1110 U9$=U$; IF V0<2 THEN GOTO 1150 ELSE U$="",X=0
1120 IF Y[X,0]>0 THEN U$=U$+U9$(Y[X,0],Y[X,1]),X=X+1; GOTO 1120
1130 READ (U1)
1150 READ (Z[1],KEY=U$,DOM=1000)IOL=0310
1200 REM "Reporting criteria
1330 DIM C$(LEN(C$)); FIND (Z[3],DOM=1331,KEY=A$(1,9))IOL=0330
1340 DIM D$(LEN(D$)); FIND (Z[4],DOM=*NEXT,KEY=C$(7,10))IOL=0340; GOTO 1350
1345 D$(11,35)="*** INVALID VENDOR ***",FLAG=1
1350 DIM E$(LEN(E$)); FIND (Z[5],DOM=1351,KEY=A$(1,9)+A$(20,3))IOL=0350
1360 DIM F$(LEN(F$))
1362 IF C$(173,1)="S" THEN F$(166,9)=A$(1,9) ELSE F$(166,8)=A$(1,8)
1363 IF A$(23,1)<>" " THEN F$(7,3)=A$(20,3)
1364 F$(10,1)=A$(23,1)
1365 FIND (Z[6],KEY=F$(166,9)+F$(7,4),DOM=1366)IOL=0360
1370 INV_FLG$="*"; FIND (Z[11],KEY=A$(45,6)+C$(7,10)+A$(10,10),DOM=*NEXT); INV_FLG$="" ! SSP#269827
1400 REM "Subtotals"
1410 IF U2$="" THEN GOTO 1435 ELSE FOR T=1 TO U0; IF U9$(U[T,0],U[T,1])<>U2$(U[T,0],U[T,1]) THEN EXITTO 1420 ELSE NEXT T; GOTO 1480
1425 GOSUB 3100
1430 ON T+1 GOTO 1470,1435,1445,1455
1435 GOSUB 6300
1490 T=0,U2$=U9$
1500 IF U$(20,3)="   " THEN GOTO 1000
1505 IF A$(23,1)<>" " THEN GOSUB 2800; GOTO 1000
1510 IF W+3>W0 THEN GOSUB 6000
1512 DIM H8$(82); H8$(21,40)=E$(28,40),H8$(64,18)=E$(17,8)+E$(111,8)+E$(15,2); GOSUB 7700
1515 Y5$(4)=A$(20,3),Y5$(10)=H9$; GOSUB 7300
1519 H7$=""
1520 IF E[18]<>0 THEN H7$="Weight: "+STR(E[18]:Z1$)+"  "
1523 IF POS(" "<>E$(119,12))<>0 THEN H7$=H7$+"Job: "+FNS$(E$(119,12))+"  "+FND$(E$(131,6))+"  "
1525 IF POS(" "<>E$(137,9))<>0 THEN H7$=H7$+"Ending number: "+E$(137,9)+"  "
1526 DIM I$(20),I[1]; FIND (Z[13],KEY="U/M"+E$(77,4),DOM=1527)IOL=0380; REM "SSP38933
1527 IF I$(20,1)="Y" THEN QTYUM=I[0] ELSE QTYUM=E[2]; REM "SSP38933
1528 IF QTYUM=0 THEN QTYUM=1; REM "SSP38933
1530 Y5$(10)=H7$,Y5$(105)="Complete?  "+E$(6,1); GOSUB 7300
1531 IF E$(14,1)<>"Y" THEN Y5$(8)=F$(11,1)+"-"+F$(12,4)
1532 Q3=A[3]; CALL "FM2EXT",ERR=1533,Z[13],2,E$(77,4),E[2],Q3,0,0,0
1533 Q1=A[0]; CALL "FM2EXT",ERR=1534,Z[13],2,E$(77,4),E[2],Q1,0,0,0
1534 Q2=A[4]; CALL "FM2EXT",ERR=1535,Z[13],2,E$(77,4),E[2],Q2,0,0,0
1535 Y5$(15)=E$(77,4),Y5$(21)=STR(E[2]:Z2$),Y5$(29)=FNQ$(Q1,Z0$),Y5$(44)=STR(A[1]:Z3$),Y5$(56)=STR(A[2]:Z0$),Y5$(74)=FNQ$(Q3,Z0$),Y5$(91)=FNQ$(Q2,Z0$),Y5$(106)=STR(A[5]:Z3$),Y5$(118)=STR(A[6]:Z0$); REM "SSP38933
1540 GOSUB 7300
1550 IF A$(66,3)<>"   " THEN GOSUB 2900; REM "Print inventory costing records
1900 REM "Accumulate Totals"
1910 T[T0,1]=T[T0,1]+A[2],T[T0,2]=T[T0,2]+A[6]
1915 TOT_QTY_REC=TOT_QTY_REC+A[0],TOT_QTY_INV=TOT_QTY_INV+A[4]; REM "WO115199
1990 T[T0,0]=T[T0,0]+1
1995 GOTO 1000
2800 REM "Print shipping instructions
2805 IF W+2>W0 THEN GOSUB 6000
2810 IF F$(12,4)="    " THEN Y5$(8)="Customer's location"; GOTO 2825
2815 Y5$(8)=F$(11,1)+"-"+F$(12,4)+" "+FNS$(F$(106,16))+", "+F$(122,2)
2825 Y5$(29)=FNQ$(A[0],Z0$)
2830 GOSUB 7300
2840 RETURN 
2890 RETURN 
2900 REM "Print inventory costing transaction record(s)
2905 IF W+2>W0 THEN GOSUB 6000
2910 DIM FTF$(150),FTF[15],FS1$(350)
2915 READ (Z[9],KEY=A$(1,8),DOM=2990)FS1$
2920 KEY$=A$(1,8)+FS1$(237,3)
2925 READ (Z[8],KEY=KEY$,DOM=2926)
2930 K$=KEY(Z[8],END=2990); READ (Z[8],KEY=K$)IOL=0400
2935 IF FTF$(1,11)<>KEY$ THEN GOTO 2990
2940 IF FTF$(15,3)<>A$(66,3) THEN GOTO 2930
2945 Y5$(5)="IC",Y5$(8)=FTF$(12,3); REM "Line number
2950 Y5$(13)="Est Cost: "+STR(FTF[9]:Z0$); REM "Estimated cost in inventory
2955 Y5$(43)="Act Cost: "+STR(FTF[10]:Z0$); REM "Actual cost in inventory
2960 Y5$(73)="Act in Prog: "+STR(FTF[11]:Z0$); REM "Actual cost in progress
2965 Y5$(103)="Unallocated: "+STR(FTF[12]:Z0$); REM "Unallocated cost-inventory gone
2970 GOSUB 7300
2975 GOTO 2930
2990 RETURN 
3000 REM "Subtotaling Routines"
3050 REM "Add to T$ for total title, T & T5 should be set
3055 IF T=1 THEN GOSUB 3500; GOTO 3090
3060 IF T7$(1,1)=" " THEN RETURN 
3070 IF T7$(1,1)<>"S" THEN CALL "ZZDISP",T7$(1,1)+"X",T6$,T7$(2,3),X3$,T6$,"",0,0,X4$
3080 IF T$="" THEN T$=T6$ ELSE T$=T$+" "+T6$
3090 RETURN 
3100 REM "Subtotal Logic
3110 FOR T=U0 TO T STEP -1
3130 T$=FNS$(U5$(T*20+1,20)),T6$=U2$(U[T,0],U[T,1]),T7$=T5$(T*4+1,4); GOSUB 3050; IF POS(U0$(T+1,1)="23")>0 THEN GOSUB 7000 ELSE GOSUB 7100
3150 IF POS(U0$(T+1,1)="13")>0 THEN GOSUB 6000
3180 NEXT T
3195 RETURN 
3280 RETURN 
3380 RETURN 
3480 RETURN 
3500 REM "Setup title for P/O and A/P invoice number
3505 IF V$(72,1)="Y" THEN GOSUB 7800
3510 CALL "ZZDISP","AX",U2$(1,9),"P/O",X3$,T6$,"",0,0,X4$
3520 T$="P/O: "+FNS$(T6$+"  A/P invoice: "+U2$(10,10))
3540 RETURN 
5000 REM "End of Print
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5050 T=0; GOSUB 3100
5200 REM "
5210 IF DATEFLG=1 THEN J$="An Invalid Trans date or Fiscal YR/Acct Per has been detected, no update allowed"; CALL "ZZPROM",".4",X3$,Z,J$,"","",0; GOTO 9900
5220 READ (Z(10),KEY="",DOM=*NEXT)
5230 POH$=KEY(Z(10),END=*NEXT); GOTO 5290
5240 LOTFLG=1
5290 IF P0$(44,1)="Y" AND LOTFLG=0 THEN Y4$="PO2RRB" ELSE IF V3$(2,1)<>"T" THEN Y4$="PO2RUA" ELSE Y4$=""
5295 GOTO 9900
6000 REM "Page header
6035 IF T0$="END" THEN W0$=""
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W3$,W3,W,W9,W8,W0,W5,W2,W8$+"R",R6$; IF T0$="END" THEN GOTO 6060 ELSE ON W5 GOTO 6041,9910; REM "SSP113796, changed W9$ to W3$
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6115 Y5$(18)="Unit of",Y5$(29)="--------------- Received ----------------",Y5$(75)="Quantity",Y5$(91)="---------------- Invoiced ---------------"
6120 GOSUB 7300
6125 Y5$(18)="measure",Y5$(32)="Quantity",Y5$(44)="Unit cost",Y5$(59)="Extension",Y5$(74)="backordered",Y5$(94)="Quantity",Y5$(106)="Unit cost",Y5$(121)="Extension"
6130 GOSUB 7300
6195 IF FID(W9)=FID(0) THEN PRINT 'SF',
6199 RETURN 
6300 REM "Break header #2
6310 IF W+2>W0 THEN GOSUB 6000
6315 Y5$(1)="P/O number:"
6318 CALL "ZZDISP","ASX",A$(1,9),"P/O",X3$,Y5$,"",13,0,X4$
6320 Y5$(29)="Vendor:"
6323 CALL "ZZDISP","ASX",C$(7,10),"A/P",X3$,Y5$,"",37,0,X4$
6325 Y5$(49)=D$(11,35),Y5$(90)="Invoice number:",Y5$(106)=A$(10,10)+INV_FLG$ ! SSP#269827
6330 GOSUB 7300
6333 Y5$(2)="SLS: "+C$(343,4) ! WO294656, output salesperson code
6334 Y5$(12)="Date: " ! WO294656, move data to the right
6335 CALL "ZZDISP","DSX",A$(39,6),"",X3$,Y5$,"",18,0,X4$ ! WO294656, move to right
6340 Y5$(30)="FY/AP: "+A$(45,4)+"/"+A$(49,2)
6341 Y5$(48)="CSR: "+C$(347,4) ! WO294656, output CSR
6342 BADDATE$=""; IF A$(39,6)=DIM(6) OR POS(" "=A$(45,6))<>0 THEN DATEFLG=1,BADDATE$="****BAD DATE ****" ! SSP#258263
6344 IF BADDATE$<>"" THEN Y5$(114)=BADDATE$; GOSUB 7300
6345 N0=0,N0=NUM(A$(51,11),ERR=6347)
6346 IF N0<>0 THEN Y5$(109)="Freight:",Y5$(118)=STR(N0:Z0$),T[T0,2]=T[T0,2]+N0
6350 GOSUB 7300
6390 RETURN 
6900 REM "Auto run update
6905 IF N0$="" OR V3$(1,1)<>"I" THEN GOTO 6940
6910 EXTRACT (Z[1],KEY="",DOM=6990)V0$,K0$
6915 IF K0$>"" THEN GOTO 6990 ELSE IF POS("G"=FID(0))=1 THEN GOTO 6940
6930 Q$="+ ("+V0$(20,4)+"."+V0$(24,2)+")"
6935 CALL "ZZPROM","X0AUDITS",X3$,S3,Q$,"","",0; ON S3 GOTO 6940,5200,9900
6940 REMOVE (Z[1],KEY="",ERR=*NEXT)
6945 RETURN 
6955 IF S9$="X" THEN GOTO 6990 ELSE S0=Z[1],S1=Z[2]
6970 CALL "ZZSORT",X3$,"",V1$,S9$,"","",0,0,0,S0,S1,S9; ON S9 GOTO 6971,9920
6990 RETURN 
7000 REM "Totals logic
7001 IF T>0 THEN IF T[T,0]<2 THEN IF T=2 THEN GOTO 7100 ELSE GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7004 IF POS(X3$(9,3)="498",3)>0 THEN Y5$(29)=T1$(1,14); REM "WO115199
7005 Y5$(56)=T1$(1,14)
7006 IF POS(X3$(9,3)="498",3)>0 THEN Y5$(91)=T1$(1,14); REM "WO115199
7007 Y5$(118)=T1$(1,14); GOSUB 7300
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" "+Q2$+" ("+STR(T[T,0]:"0")+"):"
7020 IF POS(X3$(9,3)="498",3)>0 THEN Y5$(32)=STR(TOT_QTY_REC:Z3$); REM "WO115199
7025 Y5$(56)=STR(T[T,1]:Z0$)
7030 IF POS(X3$(9,3)="498",3)>0 THEN Y5$(94)=STR(TOT_QTY_INV:Z3$); REM "WO115199
7035 Y5$(118)=STR(T[T,2]:Z0$); GOSUB 7300
7065 IF T$="END" THEN Y5$(56)=T2$(1,14),Y5$(118)=T2$(1,14); GOSUB 7300
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)=Q1$+": "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 T4$=STR(INT(T2):"##0")
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+":"+STR(T3:"00")
7260 Y5$(1)=Q3$+": "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1
7324 IF R6$(1,1)="Y" THEN CALL "ZR2DNL",ERR=7327,X3$,X4$,Y5$,R6$; REM " Send line to repository
7325 IF %IL_PDF THEN PRINT (%IL_PDF,ERR=*NEXT)Y5$ ! 174140
7330 IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     13O ZZPARM  "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7435 DIM V[1]
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V$(71,1)),W3$=STP(V$(19,40),1) ! SSP#306844
7485 FOR U1=1 TO LEN(V1$); IF V1$(U1,1)=" " THEN V1$(U1,1)="."; NEXT U1 ELSE NEXT U1
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
7600 REM "Handle V2$<U*2 length
7610 DIM V2$(U*2),V4$(U,"~"); U3=1
7615 FOR U9=1 TO U0; U3=U3+U[U9-1,1]+U[U9-1,1]
7620 V2$(U3+U[U9,1],U[U9,1])=V4$(1,U[U9,1])
7630 NEXT U9
7640 RETURN 
7750 REM "Build desc from H8$
7752 CALL "ZZDISP","AX",E$(101,10),"I/C",X3$,H9$,"",0,0,X4$
7755 H9$=FNS$(H9$)+" "; GOTO 7780
7760 H9$=FNS$(H8$(64,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+"x"
7770 H9$=H9$+FNS$(H8$(72,8)); IF H9$<>"" THEN IF POS(" "<>H8$(72,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
7775 IF NUM(H8$(80,2))<>0 THEN H9$=H9$+H8$(80,2)+" Ply "
7780 H9$=H9$+FNS$(H8$(21,40)); H8$=""
7790 RETURN 
7800 REM "Print Order Notepad
7810 READ (Z[7],KEY=U8$(1,8),DOM=7890)IOL=0370
7815 GOSUB 7300
7820 FOR I=0 TO 9
7830 IF POS(" "<>G$(9+I*50,50))=0 THEN GOTO 7880
7840 IF W+2>W0 THEN GOSUB 6000
7850 Y5$(23)=G$(9+I*50,50); GOSUB 7300
7880 NEXT I
7890 RETURN 
8905 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
8910 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8940 DEF FNS$(Z$)=Z$(1,POS("   "=Z$+"   ")-1)
8950 DEF FNQ$(Z9,Z9$)=STR(Z9:Z9$(1,POS(".00"=STR(Z9:Z9$)+".00")-1)+"-") ! SSP#233616
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 IF Y5=68 THEN RETRY ELSE ON C9 GOTO 1140,2040
9900 REM "End program
9905 IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9922 IF FLAG THEN X$="INVALID VENDOR: Delete PO Recieving, PO and re-enter under correct vendor"; CALL "ZZPROM",".4",X3$,Z,X$,"","",0; GOTO 9940 ! SSP#276949
9925 ! IF %GUI THEN EXIT 
9928 WAIT 5 ! SSP#220341 needs to be here see notes on 220341
9930 IF NOT(NUL(Y4$)) THEN IF %GUI THEN CALL Y4$,ERR=9931 ELSE RUN Y4$,ERR=9931
9940 IF %GUI THEN EXIT ELSE RUN "ZMENU"
9999 END 
56000 REM "178639-PO Receiving report will allow update without transaction   
56001 REM "191391-When no lots PDF printing gets error on second report
56002 REM "220341-In image library, the PO lot receiving report is being saved
56003 REM "233616-PO receiving report does not show negative quantities on    
56004 REM "258263-GUI:  In po receiving you can put in a blank receiving date 
56005 REM "276949-Can enter an order for a vendor that has been deleted.      
56006 REM "294656-Modify PO Receiving Report with salesperson code and CSR.   
56007 REM "306844-PE(047, PO2RRA-7480) when in PO rec'g.                      
