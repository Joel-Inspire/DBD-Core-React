0010 REM "Get data for all files into one file <ZZDATA>
0035 REM "3.7 - 03/02/94 - 16.63 - jsc
0040 BEGIN ; PRINT 'CS',"DATA FILE ROUTINE - ZZDATA",'LF','LF',
0041 REM "        Licensed Software - All Rights Reserved.
0045 INPUT "DO YOU WISH TO COLLECT OR EXTRACT? (C-E-END): ",Q$; IF Q$="END" THEN GOTO 9900 ELSE IF Q$="E" THEN GOTO 0050 ELSE IF Q$="C" THEN GOTO 0100 ELSE GOTO 0045
0050 BEGIN 
0055 DIM Z$(20,$00$)
0060 OPEN (7)"TMPDATA"
0070 GOTO 3000
0100 BEGIN 
0110 M0$="###,##0"
0120 DIM Z$(20,$00$)
0210 PRINT 'CS'
0410 OPEN (1)"D0"
0420 Q$="TMPDATA"; ERASE Q$,ERR=0421; GOTO 0420
0460 OPEN (6)"ZZPARM"; READ (6,KEY=FID(0)+"      ")X3$
0510 INVOKE "cp /dev/null UTILS/TMPDATA"
0515 OPEN (7)"TMPDATA"
0520 DIM T0$(512); WRITE RECORD (7)T0$
1020 READ RECORD (1,END=5000)A$
1025 F$=FNZ$(A$(3))
1027 F0=F0+1
1030 CLOSE (2); OPEN (2)F$
1035 F9$=FID(2)
1040 IF POS(F9$(10,1)=$0A$)>0 THEN GOTO 1000
1050 CALL "ZZINFO",2,0,X3$,R0,R1,K,R,0,0,0,""
1060 IF K=0 THEN R0=R1
1200 REM 
1220 PRINT @(0,3),'LD',@(0,18),"PROCESSING FILE ",F$,
2000 REM "PROCESS FILE F$
2100 REM "WRITE HEADER
2120 DIM H0$(64); H0$(1,22)=FID(2),H0$(1,3)=$FFFFFF$
2130 H0$(23,3)=BIN(R0,3)
2140 R2=0,K$=""
2170 Q$=H0$; GOSUB 8000
2500 REM "PROCESS RECORDS IN FILE, ADD TO DATA FILE
2510 IF K=0 THEN READ RECORD (2,IND=0)B$; GOTO 2530
2520 IF K>0 THEN K$=KEY(2,END=2800)
2525 READ RECORD (2,END=2800)B$
2530 R2=R2+1
2540 IF K>0 THEN Q$=K$; GOSUB 8000
2550 Q$=FNZ$(B$); GOSUB 8000
2560 REM PRINT @(50),"RECORD: ",R2:M0$,
2590 GOTO 2520
2800 REM 
2802 PRINT @(50),"RECORD: ",R2:M0$,
2805 IF R0=0 AND R2=0 THEN PRINT @(50),"EMPTY",
2810 IF R2<>R0 THEN PRINT 'RB',"FILE ",F9$(4,6)+F9$(21,2)+" HAS ONLY ",R2," RECORDS, NOT ",R0,; INPUT *
2840 Q$="END OF "+F$; GOSUB 8000
2990 GOTO 1000
3000 REM "Process file and unpack
3002 DIM S$(20)
3003 ESCAPE 
3005 REM "M0=MODE (0=READ ONLY, M1=RESET ACTUAL DATA FILES USING DATA)
3006 M0=0; INPUT 'RB',"DO YOU WISH TO RESTORE DATA? NO RESPONSE JUST LOOKS AT DATA (YES-NO): ",Q$; IF Q$="YES" THEN M0=1 ELSE IF Q$<>"NO" THEN GOTO 3006
3010 M0$="###,##0"
3015 READ RECORD (7,IND=0)T0$
3020 GOSUB 8100; H0$=Q$
3030 IF LEN(Q$)=64 THEN IF Q$(1,3)=$FFFFFF$ THEN GOTO 3200
3200 REM "NEW FILE - DEFINE FILE
3210 REM "12,3=NUMBER OF RECORDS
3211 REM "15,2=RECORD SIZE
3212 REM "10,1 =KEY SIZE
3220 PRINT @(0,3),'LD',@(0,18),"FILE ",Q$(4,6)+Q$(21,2)+", ",DEC(Q$(23,3))," RECORDS IN USE",
3225 R2=DEC(Q$(23,3)),K=ASC(Q$(10)),F$=FNZ$(H0$(4,6)+H0$(21,2))
3230 IF M0=0 THEN GOTO 3250 ELSE ERASE F$,ERR=3231; GOTO 3230
3235 IF ERR<>12 THEN ESCAPE 
3240 Q$=H0$(1,22),Q$(1,3)=$000000$; FILE Q$
3245 CLOSE (2); OPEN (2)F$
3250 REM "PROCESS FILE
3260 IF R2>0 THEN GOSUB 3400
3264 REM "PREMATURE END OF FILE (E0=1)
3265 IF E0=1 THEN GOTO 3275
3270 GOSUB 8100; IF Q$<>"END OF "+FNS$(H0$(4,6)) THEN ESCAPE 
3280 GOTO 3020
3400 REM "PROCESS FILE
3405 R0=0,E0=0
3410 IF K>0 THEN GOSUB 8100; K$=Q$
3412 GOSUB 8100
3415 R0=R0+1
3420 REM PRINT @(50),"RECORD: ",R0:M0$,
3430 IF M0=1 THEN IF K=0 THEN WRITE RECORD (2,IND=R0-1)Q$ ELSE WRITE RECORD (2,KEY=K$)Q$
3445 IF POS("END OF "=Q$)=1 THEN E0=1; GOTO 3485
3450 IF R0<R2 THEN GOTO 3410
3485 PRINT @(50),'CL'
3490 RETURN 
5000 REM "DONE WITH FILES
5020 IF LEN(B0$)>512 THEN WRITE RECORD (7)B0$(1,512); B0=B0+1,B0$=B0$(513); GOTO 5020
5025 IF B0$>"" THEN WRITE RECORD (7)B0$; B0$=""; B0=B0+1
5050 REM "SET FILE HEADER DATA
5060 READ RECORD (7,IND=0)T0$
5065 T0$(1,3)=BIN(B0,3),T0$(4,6)=B1$
5090 WRITE RECORD (7,IND=0)T0$
5095 ESCAPE 
8000 REM "Write data
8010 L=LEN(Q$); Q$=BIN(L,3)+Q$
8020 B0$=B0$+Q$
8025 IF LEN(B0$)<512 THEN GOTO 8090
8040 WRITE RECORD (7)B0$(1,512); B0=B0+1,B1$=CRC(B1$+B0$(1,512))
8045 B0$=B0$(513)
8050 GOTO 8025
8090 RETURN 
8100 REM "Get next data from file
8120 IF LEN(B0$)<3 THEN READ RECORD (7)Q$; B0$=B0$+Q$
8125 L=DEC(B0$(1,3)),B0$=B0$(4)
8130 IF LEN(B0$)<L THEN READ RECORD (7)Q$; B0$=B0$+Q$; GOTO 8130
8140 Q$=B0$(1,L),B0$=B0$(L+1)
8190 RETURN 
8200 REM "VERIFY CHECKSUM
8202 B1$=""
8210 B0=0; READ RECORD (7,IND=B0)T0$
8220 READ RECORD (7,END=8250)B0$
8225 B0=B0+1; B1$=CRC(B1$+B0$)
8240 GOTO 8220
8250 REM "
8290 ESCAPE 
8295 RETURN 
8910 DEF FNZ$(Z9$)=Z9$(1,POS(Z$=Z9$+Z$)-1)
8915 DEF FNS$(Z9$)=Z9$(1,POS(S$=Z9$+S$)-1)
9999 END 
