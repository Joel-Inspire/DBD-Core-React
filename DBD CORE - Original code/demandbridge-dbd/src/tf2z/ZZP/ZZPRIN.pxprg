0010 REM "<ZZPRIN> Report Output Device Setup
0035 REM "5.7 - 01/21/14 - 8.685555 - tma - SSP# 267933
0036 REM "Added line 5106
0037 REM "267933-Issue printing customer detail listing to Unform PDF. Get a 
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "X0$ - Calling program
0052 REM "X3$ - System parameters
0054 REM "Y0$ - Command
0056 REM "F0$ - Device name input
0058 REM "F[ALL] - File channels from ZZFLES
0060 REM "H0$ -
0062 REM "H1$ -
0064 REM "H0  - Lines per page or screen
0066 REM "H1  - Output device channel number
0068 REM "H2  -
0070 REM "H3  - Columns on output device
0072 REM "H4  -
0100 SETERR ERROR_PROCESSING; SETESC 9200
0200 DIM F$(12),S$(60)
0250 %FF_ALWAYS=0 ! 172919 clear flag
0300 DEF FNS$(Z3$)=Z3$(1,POS(S$=Z3$+S$)-1)
0370 IOLIST MODE$:[LEN(1)],ID$:[LEN(6)],STATUS$:[LEN(1)],VERT$:[LEN(1)],HORZ$:[LEN(1)],AUX$:[LEN(4)],LINES$:[LEN(2)],FRM_CDE$:[LEN(1)],DESC$:[LEN(47)],JCARD$:[LEN(60)],JCARD_UNLCK$:[LEN(1)]
0380 DIM PTR_CNFG$:IOL=0370
0410 REM IOLIST X3$,X4$,V1$,V3$,V2$,V0$
1020 ENTER X0$,X3$,Y0$,F0$,F{ALL},H0$,H1$,H0,H1,H2,H3,H4
1021 IF LEN(F0$)<60 THEN F0$=F0$+S$(1,60-LEN(F0$))
1022 PTR_CNFG$=F0$
1023 %PRINT_OPT$=STP(MID(F0$,65,60)) ! 171415 Save info for print drivers to use
1025 H4=0; DIM Z[NUM(X3$(60,3))]
1040 IF PTR_CNFG.ID$=MID(S$,1,6) OR PTR_CNFG.ID$="" THEN {
1041 GOSUB INITIALIZE_PRINTER
1042  } ELSE {
1043 IF MID(PTR_CNFG.ID$,1,1)="U" OR PTR_CNFG.ID$="" THEN GOSUB DEFAULT_PRINTER
1044  }
1045 IF POS(MID(PTR_CNFG.ID$,1,2)="T A ")<>0 THEN F1$=FID(0) ELSE F1$=PTR_CNFG.ID$
1050 IF X0$="ZZSCRN" OR X0$="ZZLIST" AND H3>80 THEN PTR_CNFG.HORZ$="C"
1110 IF MID(F1$,1,2)="SP" OR MID(F1$,1,2)="LP" OR POS(MID(F1$,1,1)="FLMNOPQR")<>0 THEN H2=0; GOTO 2000 ! 146676;REM" Took the Y out of the FLMNOPQRY since too many uses had Y terminals and this was causing a problem with the view to screen ref#152836,152810
1116 IF MID(PTR_CNFG.ID$,1,1)="A" THEN H2=2; GOTO 2000
1130 REM "IF POS("H"=H0$)<>0 THEN H4=2; GOTO PROGRAM_EXIT *SSP#: 152473
1140 IF MID(PTR_CNFG.ID$,1,1)="*" THEN {
1150 CALL "ZA2BBU",X3$,H0$,S0$,0,PTR_CNFG$,"C",F1$,0,0,H4
1160 IF H4>0 OR POS(MID(S0$,264,1)="* ")=0 THEN GOTO PROGRAM_EXIT
1161  }
1165 IF UCS(STP(MID(F0$,65,60),1))="NOSTOP" THEN H0=99999999999999,H2=1; GOTO 2000
1170 H0=22,H2=1; GOTO 2000
2000 REM "Open Files
2020 GOSUB OPEN_REPORT_PRINT_FILE
2025 REM READ (F[0],KEY=X3$(1,8),DOM=*NEXT) IOL=00420
2030 REM LET X$=PTR_CNFG$, F_CODE$="JETREPORT ; CALL "EF2PRM",ERR=01023,X3$,X4$,X$,P6$,F6$,F_CODE$,FORM_PARM$; IF MID(P6$,144,2)="JF" THEN LET JETFORM_MODE=1, JETFORM_DEV=H2; IF JETFORM_MODE =0 THEN GOTO 2040
2032 REM CALL "EF2JET",X3$,X4$,P6$,"N",JETFORM_DEV,FORM_PARM$,JETFORM_MODIFIER$,RET$
2038 REM GOTO WHAT IS NEXT IF THIS IS JETFORM.
2040 IF MID(PTR_CNFG.ID$,1,1)<>"*" THEN IF H2=1 THEN GOSUB TERMINAL_PRINT_SETUP ELSE GOSUB 5000
2050 IF NOT(%GUI) AND POS("G"=FID(0))=0 AND FID(H1)<>FID(0) AND POS("I"=H0$)=0 THEN CALL "ZZPROM","X0ZZPRIN",X3$,0,"","","",22
2080 IF H0*H3>65000 THEN DIM H1$(65*H3) ELSE DIM H1$(H0*H3)
2090 GOTO PROGRAM_EXIT
3000 TERMINAL_PRINT_SETUP:
3005 PTR_CNFG$(28,3)="080"
3010 IF H3<80 THEN RETURN ELSE REM " IF H3>131 THEN GOTO 03050
3020 PRINT (H1,ERR=3021)'CS','WINDOW'(0,0,132,25),'CP','CS',; PTR_CNFG$(28,3)="132"; GOTO 3030
3021 PRINT (H1,ERR=3050)'CS','T2','CS',
3025 PTR_CNFG$(28,3)="132"
3030 RETURN 
3050 H0$=H0$+"V"
3060 DIM H1$(H0*H3)
3090 RETURN 
5000 SETUP_PRINTER:
5002 H0=NUM(PTR_CNFG.LINES$,ERR=*NEXT)-8
5003 IF H0<0 THEN H0=58
5005 DIM Z[NUM(X3$(60,3))],P9$(40); Z$="01O ZZPARM  ",V1$="",V0$=Y0$,P0$=""
5007 CALL "ZZFLES",X3$,V1$,V0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO *NEXT,5090
5010 READ (Z[1],KEY="PRINTERS",DOM=END_SETUP_PRINTER,ERR=*NEXT)P1$,P2$,P9$,P3$ ! P1$ = PRINTER NAMES, P2$=PRINTER DEVICE LIST
5012 P=POS(F1$(1,2)=P2$,2); IF P=0 THEN GOTO END_SETUP_PRINTER ! Find offset of ptr fid in dev list
5015 READ (Z[1],KEY="ptr"+P1$(INT(P/2)*8+9,8),DOM=END_SETUP_PRINTER)P$; PTR_CNFG$(20,8)=P$(4,8); IF P$(4,8)="FFALWAYS" THEN %FF_ALWAYS=1 ! 172919 set flag
5016 GOSUB SPECIAL_FORMS
5020 IF MID(P$,12,1)<>"*" THEN GOSUB LOAD_MNEMONICS
5045 IF PTR_CNFG.AUX$<>"    " THEN {
5046 FOR I=1 TO 4 ! special print mnemonics are entered by number in output parameter prg. 
5055 IF MID(PTR_CNFG.AUX$,I,1)<>" " THEN P0$+=$27$+PTR_CNFG.AUX$(I,1)+"S"+$272C$ ! build aux mnemonics  '1S','2S','3S','4S','5S', from numbers in AUX$
5060 NEXT I
5061  }
5070 IF MID(P$,12,1)="*" THEN GOSUB LOAD_MNEMONICS
5105 SETERR 5125
5109 IF P0$="" THEN GOTO 5185
5110 CMDSTR$="PRINT (H1)"+P0$ ! Print AUX Mnemonic list
5120 EXECUTE CMDSTR$
5124 GOTO 5180
5125 X$="+ ("+F1$+")"; CALL "ZZPROM","X4ZZPRIN",X3$,X4,X$,"","",0; IF X4=0 THEN RETRY ELSE IF X4=2 THEN GOTO 5630
5130 SETERR ERROR_PROCESSING; EXITTO 5635
5185 SETERR ERROR_PROCESSING
5190 END_SETUP_PRINTER:RETURN 
5200 SPECIAL_FORMS:
5210 IF PTR_CNFG.FRM_CDE$<>"Z" THEN IF PTR_CNFG.FRM_CDE$=MID(P9$,INT(P/2)+1,1) OR %GUI THEN GOTO END_SPECIAL_FORMS ! SSP#230500
5215 DIM D$(50); D$(6,30)="Special Forms"
5220 READ (Z[1],KEY="FORM"+PTR_CNFG.FRM_CDE$,DOM=*NEXT)D$
5225 IF POS("G"=FID(0))=1 OR PTR_CNFG.MODE$="Q" THEN GOTO 5265
5230 X$="+"+FNS$(D$(6,30)); CALL "ZZPROM","X1ZZPRIN",X3$,0,X$,"","",21
5235 M$="5"; IF PTR_CNFG.MODE$="Q" THEN M$="X2ZZPRIN"
5240 CALL "ZZPROM",M$,X3$,A,"","","",0
5255 PRINT @(0,21),'CL',
5260 ON A GOTO 5265,5270,5280
5265 P9$(INT(P/2)+1,1)=PTR_CNFG.FRM_CDE$
5266 WRITE (Z[1],KEY="PRINTERS")P1$,P2$,P9$,P3$
5267 GOTO END_SPECIAL_FORMS
5270 REM "END
5272 H4=1,H1=-1,F$="13C "+F1$; CALL "ZZFLES",X3$,Y1$,Y0$,F$,F{ALL},Z0,Z1; EXITTO PROGRAM_EXIT
5280 GOTO PRINT_QUEUE
5290 END_SPECIAL_FORMS:RETURN 
5300 CONVERT_SPECIAL_PRINTER_EFFECTS:! /DATA SOUTH S/B DEAD CODE
5305 P=POS("["=P0$); IF P=0 THEN RETURN ELSE P1=POS("]"=P0$(P)); IF P1=0 THEN RETURN ELSE P1=P1-1
5310 Q$=P0$(P+1,P1-1); CLOSE (14); OPEN (14)"ZT0"
5312 Q$=Q$+"  ",Q$=Q$(1,2)
5315 FIND (14,KEY=P$(4,8)+Q$,DOM=5350)Q$,Q0,Q1
5320 X$=FNS$(Q$(31,58))
5340 P0$=P0$(1,P-1)+X$+P0$(P+P1+1)
5345 GOTO 5305
5350 PRINT @(0,20),'CE',"TRYING TO FIND PRINTER ",P$(4,8)," EFFECT ",Q$,". ",P1,; INPUT *
5355 END_CONVERT_SPECIAL_PRINTER_EFFECTS:RETURN 
5500 CHECK_PRINTER_FREE:
5505 IF MID(F1$,1,2)=FID(0) THEN RETURN 
5506 CLOSE (14); OPEN (14,ERR=5600)F1$(1,2); X$=FID(14); CLOSE (14); IF LEN(X$)<>2 THEN Q=12; GOTO 5602
5510 A$=TSK(0,ERR=5600)
5520 IF POS(MID(F1$,1,2)+"0"=A$,3)>0 THEN F1$(1)=F1$(1,2); RETURN ; REM "Printer is available !SSP#267933
5535 IF POS(MID(F1$,1,2)=MID(Y0$,11),12)>0 THEN RETURN 
5540 IF PTR_CNFG.MODE$="Q" OR POS("G"=FID(0))=1 THEN GOTO *NEXT ELSE GOTO 5600
5550 WAIT 4
5580 GOTO 5510
5600 Q=ERR; IF MID(X3$,77,1)="U" THEN CLOSE (14); OPEN (14,ERR=*NEXT)"ZZ9"; READ (14,KEY=F1$(1,2),ERR=5601)X$; X$="+("+X$(1,2)+", "+FNS$(X$(3,30))+")"; GOTO 5605
5602 X$="+"+FNS$(F1$); IF Q=12 THEN X$=X$+" (Invalid Device Name!)"
5605 CALL "ZZPROM","X3ZZPRIN",X3$,X4,X$,"","",0
5620 ON X4 GOTO CHECK_PRINTER_FREE,5630,5635
5630 H4=1; EXITTO PROGRAM_EXIT
5635 Z$=STR(F0:"00")+"CU"+F1$(1,2)+"      "; CALL "ZZFLES",X3$,V1$,V0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 5636,5636
5638 IF %GUI THEN GOSUB SELECT_ALT_PRINTER; GOTO 5660
5640 CALL "ZZRPTO",X3$,X4$,Y0$,PTR_CNFG$,F{ALL},4,C0,X0
5650 PRINT @(0,3),'CE',
5660 IF PTR_CNFG$="" THEN {
5661 GOSUB INITIALIZE_PRINTER
5662 F1$="LP"
5663  } ELSE {
5664 IF POS(MID(PTR_CNFG.ID$,1,1)="TA")<>0 THEN F1$=FID(0) ELSE F1$=PTR_CNFG.ID$
5665  }
5666 RESET ; GOTO 1025
5670 GOTO CHECK_PRINTER_FREE
5700 LOAD_MNEMONICS:REM Load Printer Configuration Mnemonics, WAS ESCAPE SEQUENCES
5711 P0$+=$27$+STR(POS(PTR_CNFG.VERT$="NC12"))+"Y"+$272C$
5721 P0$+=$27$+STR(POS(PTR_CNFG.HORZ$="NC12"))+"X"+$272C$
5730 END_LOAD_MNEMONICS:RETURN 
6000 OPEN_REPORT_PRINT_FILE:! 
6010 CALL "ZZFLES",X3$,Y1$,Y0$,"00O ZZP...  ",F{ALL},Z1,0; ON Z1 GOTO 6011,6012
6015 IF H1>0 THEN CLOSE (H1) ELSE IF F[0]=0 THEN GOTO 6050
6017 REM "Skip this to prevent 'empty' files being generated when printing to files or reform IF MID(PTR_CNFG.ID$,1,1)<>"*" THEN GOSUB CHECK_PRINTER_FREE
6020 F[F0]=F[0]; IF H1>0 THEN F[F0]=H1 ELSE H1=F[F0]
6030 IF MID(PTR_CNFG.ID$,1,1)="*" THEN S3=NUM(PTR_CNFG$(61,4)); IF S3=0 THEN S3=512
6032 S3=512
6035 CLOSE (F[F0]); IF MID(PTR_CNFG.ID$,1,1)<>"*" THEN OPEN (F[F0],ERR=6049)FNS$(F1$) ELSE IF MID(PTR_CNFG$,60,1)="*" THEN OPEN (F[F0],ERR=6050,ISZ=S3)FNS$(F1$) ELSE OPEN LOCK (F[F0])FNS$(F1$) ! 148605, Open lock to be able to write out
6045 GOTO 6090
6049 IF MID(PTR_CNFG.ID$,1,1)<>"*" THEN GOSUB CHECK_PRINTER_FREE; GOTO 6030; REM "New logic, don't try this extra open/close unless we are going to have an error
6050 IF ERR=14 THEN IF POS("G"=FID(0))=1 THEN WAIT 4; GOTO 6035
6060 FOR F0=1 TO NUM(X3$(60,3))
6061 IF F0=14 THEN GOTO 6070; REM "WO120202, never use channel 14
6065 F$=FID(F0,ERR=*NEXT); GOTO 6070
6066 EXITTO 6080
6070 NEXT F0
6071 F0=29000; GOTO 6080; REM "WO120202, if we're here then we didn't find and open channel to use so use 29000, not used for anything else
6075 CALL "ZZPROM","X9ZZPRIN",X3$,0,"","","",0
6077 H4=2; EXITTO PROGRAM_EXIT
6085 OPEN (F0,ERR=*NEXT)FNS$(F1$); GOTO 6087
6086 H4=2; EXITTO PROGRAM_EXIT
6087 H1=F0
6088 REM "Reset Y0$"
6089 DIM Q[NUM(X3$(60,3))]; Y2$=""; CALL "ZZFLES",X3$,Y1$,Y2$,"00O ZZP...  ",Q{ALL},Z0,Z1; Y0$=Y2$
6090 REM "
6093 IF MID(PTR_CNFG.ID$,1,1)="A" THEN PRINT (F[F0],ERR=6075)'BA',
6095 RETURN 
7000 PRINT_QUEUE:
7001 REM "Set Z2 to point to ZZQ file
7004 Z$="02O ZZQ...  "
7005 CALL "ZZFLES",X3$,V1$,V0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO *NEXT,7090
7006 Z2=Z[2]
7010 DIM F9$(6); F9$(1)=F1$
7020 READ RECORD (Z2,KEY=F9$,DOM=7021)A$; GOTO 7050
7022 GOTO 7090
7060 REMOVE (Z2,KEY=F9$,DOM=7061)
7070 K$=A$(1,19),K$(7,1)=CHR(ASC(K$(7,1))+90)
7080 WRITE RECORD (Z2,KEY=K$,DOM=7081)A$; GOTO 7090
7085 K$(19,1)=CHR(ASC(K$(19,1))+1); GOTO 7080
7090 H4=2; GOTO PROGRAM_EXIT
7095 END_PRINT_QUEUE:RETURN 
7500 INITIALIZE_PRINTER:
7510 PTR_CNFG.MODE$="I"
7520 GOSUB DEFAULT_PRINTER
7530 PTR_CNFG.STATUS$="N"
7540 PTR_CNFG.VERT$="N"
7550 PTR_CNFG.HORZ$="N"
7560 PTR_CNFG.AUX$="    "
7570 PTR_CNFG.LINES$="66"
7580 PTR_CNFG.FRM_CDE$=" "
7590 END_INITIALIZE_PRINTER:RETURN 
7600 DEFAULT_PRINTER:
7610 PTR_CNFG.ID$=MID(X3$,172,2)+"    "
7620 RETURN 
9000 ERROR_PROCESSING:
9010 Y5=ERR,Y6=TCB(5)
9020 IF ERR=66 THEN EXIT ERR
9040 CALL "ZZERRM",Y8$,"ZZPRIN",Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,PROGRAM_EXIT,9070,9090
9060 RETRY 
9070 SETERR 9080
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9110 EXIT ERR
9280 RETURN 
9300 SETESC 9350
9310 SETERR 9350
9315 IF MID(X3$,47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR ERROR_PROCESSING; RETURN 
9900 PROGRAM_EXIT:
9910 F0$=PTR_CNFG$
9920 EXIT 
9999 END 
10000 ! 
10005 SELECT_ALT_PRINTER:
10010 PTR_CNFG_MODE$=PTR_CNFG$(1,1)
10011 RPT_OUTDEV$=PTR_CNFG$(2,2)
10012 RPT_VERT$=PTR_CNFG$(9,1)
10013 RPT_HORIZ$=PTR_CNFG$(10,1)
10014 RPT_AUX1$=PTR_CNFG$(11,1)
10015 RPT_AUX2$=PTR_CNFG$(12,1)
10016 RPT_AUX3$=PTR_CNFG$(13,1)
10017 RPT_AUX4$=PTR_CNFG$(14,1)
10018 RPT_LINES$=PTR_CNFG$(15,2)
10019 RPT_FORMCODE$=PTR_CNFG$(17,1)
10020 RPT_JOBCARD$=PTR_CNFG$(65,60)
10021 RPT_JOBCARD_FLG$=PTR_CNFG$(125,1)
10029 ! 
10030 REPLACEMENT_SCRN$="ZGXOPO"
10035 REPLACEMENT_LIB$="../ZGX/ZG.EN"
10040 PERFORM "*winproc;overlay_screen"
10049 ! 
10050 PTR_CNFG$(1,1)=PTR_CNFG_MODE$
10051 PTR_CNFG$(2,2)=RPT_OUTDEV$
10052 PTR_CNFG$(9,1)=RPT_VERT$
10053 PTR_CNFG$(10,1)=RPT_HORIZ$
10054 PTR_CNFG$(11,1)=RPT_AUX1$
10055 PTR_CNFG$(12,1)=RPT_AUX2$
10056 PTR_CNFG$(13,1)=RPT_AUX3$
10057 PTR_CNFG$(14,1)=RPT_AUX4$
10058 PTR_CNFG$(15,2)=RPT_LINES$
10059 PTR_CNFG$(17,1)=RPT_FORMCODE$
10060 PTR_CNFG$(65,60)=RPT_JOBCARD$
10061 PTR_CNFG$(125,1)=RPT_JOBCARD_FLG$
10095 RETURN 
10099 ! 
56001 REM "267933-Issue printing customer detail listing to Unform PDF. Get a 
