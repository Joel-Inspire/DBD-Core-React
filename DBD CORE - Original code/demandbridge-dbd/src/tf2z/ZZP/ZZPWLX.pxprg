0010 ! 
0035 REM "5.0 - 12/13/01 - 18.483055 - plh - SSP# 142887
0040 REM "Copyright 2001 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0100 ! ^100
0101 UPDATE_TFWINAPI:ESCAPE 
0110 ! GET WINDX DIRECTORY
0120 CALL "[WDX]*windx.utl;GET_WINDX",W_DIR$
0130 W_DIR$=MID(W_DIR$,1,POS("/\":W_DIR$,-1)-1)
0140 ! 
0150 ! Chk version of topform's pvx/com windows interface
0160 TRN_PRG$="tfwinapi"
0170 REM_DIR$=%WDX$+W_DIR$+"\"+TRN_PRG$
0180 IF FNIS_VALID(REM_DIR$) THEN {
0190 ! 
0200 ! test for version by date
0210 REM_DT$=FNGETCREATEDATE$(REM_DIR$)
0220 LOC_DT$=FNGETCREATEDATE$(TRN_PRG$)
0230 IF FNIS_1STDATE_LE(LOC_DT$,REM_DT$) THEN GOTO 0280
0240 ERASE REM_DIR$,ERR=*NEXT
0250  }
0260 CALL PGN+";MOVE_FILES",W_DIR$,TRN_PRG$,RETCDE
0270 ! 
0271 RETURN 
0272 ! 
0273 UPDATE_WORDLINK:
0275 GOSUB UPDATE_TFWINAPI
0280 ! 
0281 ! Get program folder path and set TopForm's WordLink path
0290 CALL %WDX$+"tfwinapi;GETWORDVERSION",WRDVER$
0300 WRD_VER$=STP(MID(WRDVER$,LEN(WRDVER$),1),2)
0310 CSIDL_PROGRAM_FILES=38
0320 CALL %WDX$+"tfwinapi;getfolderpath",CSIDL_PROGRAM_FILES,PRGPATH$,WINVER$
0330 CSIDL_WINDOWS=36
0340 CALL %WDX$+"tfwinapi;getfolderpath",CSIDL_WINDOWS,WINPATH$,WINVER$
0350 IF PRGPATH$="" THEN PRGPATH$="C:\Program Files"
0360 W_DIR$=PRGPATH$+"\TopForm\WordLink"
0370 TRN_PRG$="WordLink.exe"
0380 ! 
0390 ! if no "\TopForm\WordLink" directory, go straight to install package
0400 IF NOT(FNIS_VALID(%WDX$+W_DIR$)) THEN DOINSTALL=1; GOTO 0610
0410 ! 
0420 ! check for file's presence
0430 REM_DIR$=%WDX$+W_DIR$+"\"+TRN_PRG$
0440 IF FNIS_VALID(REM_DIR$) THEN {
0450 ! check for latest version
0460 CALL %WDX$+"tfwinapi;GETFILEVERSION",W_DIR$+"\"+TRN_PRG$,VERNO$,ERRNO
0470 IF VERNO$>="2.0.0.1" THEN GOTO *RETURN
0480  }
0490 ! 
0500 REM_DT$=FNGETCREATEDATE$(REM_DIR$)
0510 LOC_DT$=FNGETCREATEDATE$(TRN_PRG$)
0520 ! 
0530 ! check for install package stamp and just replace exe if there
0540 IF FNIS_VALID(%WDX$+W_DIR$+"\ST6UNST.LOG") THEN {
0550 IF FNIS_1STDATE_LE(LOC_DT$,REM_DT$) THEN {
0560 CALL PGN+";MOVE_FILES",W_DIR$,TRN_PRG$,RETCDE
0570 GOTO *RETURN
0580  } ! endif date compare
0590  } ! endif vbuninstall chk
0600 ! 
0610 ! Appliction not installed so install it in temp directory
0620 W_DIR$=WINPATH$+"\Temp"
0630 IF FNIS_VALID(%WDX$+W_DIR$+"\WordLink.CAB") AND FNIS_VALID(%WDX$+W_DIR$+"\SETUP.LST") AND FNIS_VALID(%WDX$+W_DIR$+"\Setup.exe") THEN GOTO 0820
0640 ! 
0650 MSGBOX "Do you wish to continue?","WordLink Installation Required!","YESNO",RETCDE$
0660 IF RETCDE$="NO" THEN GOTO *RETURN
0670 ERASE %WDX$+W_DIR$+"\Setup.LST",ERR=*NEXT
0680 ERASE %WDX$+W_DIR$+"\Setup.exe",ERR=*NEXT
0690 ERASE %WDX$+W_DIR$+"\WordLink.CAB",ERR=*NEXT
0700 ! 
0710 TRN_PRG$="Wordlink.CAB"
0720 CALL PGN+";MOVE_FILES",W_DIR$,TRN_PRG$,RETCDE
0730 ! 
0740 ! move VB Setup Program - version chk not required
0750 TRN_PRG$="setup.exe"
0760 CALL PGN+";MOVE_FILES",W_DIR$,TRN_PRG$,RETCDE
0770 ! 
0780 ! move Setup list
0790 TRN_PRG$="SETUP.LST"
0800 CALL PGN+";MOVE_FILES",W_DIR$,TRN_PRG$,RETCDE
0810 ! 
0820 ! Execute setup program
0830 CALL %WDX$+"tfwinapi;RUNPROGRAMANDWAIT",W_DIR$+"\"+"setup.exe"
0840 WAIT 1
0841 EXIT 
0850 ! 
0860 MOVE_FILES:
0870 ENTER SRCHPATH$,PRGFLE$,RESULT
0880 RESULT=0
0890 FULLFILENAME$=SRCHPATH$+"\"+PRGFLE$
0900 CALL "ZZ2TRN",PRGFLE$,FULLFILENAME$,RESULT
0910 EXIT 
0920 ! 
9999 END 
64000 ! ^ 64000
64010 DEF FNIS_VALID(DPATH$)
64020 LOCAL _DIR
64030 _DIR=UNT
64040 OPEN (_DIR,ERR=*NEXT,ISZ=1)DPATH$; CLOSE (_DIR); RETURN 1
64050 RETURN 0
64060 END DEF
64070 ! 
64080 DEF FNGETCREATEDATE$(AFULLNAME$)
64090 LOCAL _DIR,FCDT$
64100 _DIR=UNT
64110 OPEN (_DIR,ERR=64150,ISZ=1)AFULLNAME$
64120 FCDT$=FIN(_DIR,ERR=*NEXT)
64130 FCDT$=MID(FCDT$,13,4,ERR=*NEXT)
64140 CLOSE (_DIR)
64150 FCDT$=DTE(JUL(1970,1,1)+DEC($00$+FCDT$)/86400)
64160 RETURN FCDT$
64170 END DEF
64180 ! 
64190 DEF FNIS_1STDATE_LE(DT1$,DT2$)
64200 LOCAL _DT1,_DT2
64210 _DT1=JUL(DT1$,ERR=64240)
64220 _DT2=JUL(DT2$,ERR=64240)
64230 IF _DT1<=_DT2 THEN RETURN 1
64240 RETURN 0
64250 END DEF
64260 ! 
