0010 ! <ZZ2TRN> Server file Transfer to Remote Windx Machine
0035 REM "5.0 - 11/20/01 - 18.120833 - plh - SSP# 142887
0040 REM "Copyright 2001 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0100 ! ^ 100
0110 ENTER LOC_FILE$,REM_FILE$,RESULT
0111 HAS_STATUSBAR=POS("S"=_PANEL_ATR$)
0120 RESULT=0
0130 IF REM_FILE$="" OR LOC_FILE$="" THEN EXIT 
0131 IF %TERM_TYPE$="" OR POS("WINDX"=%TERM_TYPE$)>0 THEN ISTHINCLIENT=1 ELSE ISTHINCLIENT=0
0140 IF HAS_STATUSBAR THEN PRINT 'MESSAGE'("Transfering "+LOC_FILE$,1)
0150 X$=MSE
0160 IF X$="" THEN EXIT 
0170 WINDX$=""; IF POS("[WDX]"=UCS(REM_FILE$))=0 THEN WINDX$="[wdx]"
0180 IF X$(1,1)<>$FF$ AND X$(22,1)<>$FF$ THEN GOTO SERVER_FILE
0190 EXIT 
1000 ! ^1000
1010 SERVER_FILE:
1020 _SRV=UNT
1030 CLOSE (_SRV); OPEN (_SRV,ISZ=1024,ERR=1160)LOC_FILE$
1070 IF TCB(82)>0 THEN {
1080 SRC_PATH$=PTH(_SRV)
1090 CALL WINDX$+"tfwinapi;getcomputername",PCNAME$
1100 XCPY$="xcopy "+SRC_PATH$+" \\"+PCNAME$+"\"+REM_FILE$+" /q /s /y /z"
1110 INVOKE WAIT XCPY$
1120  } ELSE {
1130 GOTO REM_FILE
1140  }
1150 EXIT 
1160 MSGBOX "Unable to open "+LOC_FILE$+" for transfer"; EXIT 
1170 ! !^100
1180 REM_FILE:
1190 _PC=UNT
1200 CLOSE (_PC); OPEN PURGE (_PC,ISZ=1,ERR=1210)WINDX$+REM_FILE$; GOTO COPY_FILE
1210 IF ERR<>12 THEN GOTO 1280
1220 SETERR 1240
1221 P=POS("[WDX]"=UCS(REM_FILE$))+5
1222 IF WINDX$="" AND P>0 THEN TMP_FILE$=MID(REM_FILE$,P) ELSE TMP_FILE$=REM_FILE$
1230 EXECUTE "[WDX]SERIAL "+QUO+TMP_FILE$+QUO
1240 SETERR 0000
1250 CLOSE (_PC); OPEN PURGE (_PC,ISZ=1,ERR=1260)WINDX$+REM_FILE$; GOTO COPY_FILE
1260 CLOSE (_SRV,ERR=*NEXT)
1270 EXIT 
1280 ! !^100
1290 COPY_FILE:
1300 BYT_CNT=0,IND_CNT=0
1310 LOC_SIZE$=FIN(_SRV)
1320 LOC_SIZE=DEC(MID(LOC_SIZE$,1,4))
1330 CHUNK=LOC_SIZE/20
1340 PCNT=CHUNK
1350 IF NOT(HAS_STATUSBAR) AND ISTHINCLIENT THEN CALL "*progbar;init","Copying "+LOC_FILE$,40
1360 ! !^100
1370 COPY_REC:
1380 READ RECORD (_SRV,IND=IND_CNT,END=FIN_COPY)R$
1390 WRITE RECORD (_PC,IND=BYT_CNT)R$
1400 IND_CNT=IND_CNT+1,BYT_CNT=BYT_CNT+LEN(R$)
1410 IF BYT_CNT>PCNT THEN {
1420 IF HAS_STATUSBAR THEN {
1430 PRINT 'MESSAGE'("Transfering "+LOC_FILE$+" "+STR(BYT_CNT,"#00000000"),1)
1440  } ELSE {
1460 IF ISTHINCLIENT THEN CALL "*progbar;update_percent",ERR=*NEXT,"Copying "+LOC_FILE$,INT(BYT_CNT/LOC_SIZE*100)
1470  }
1480 PCNT=BYT_CNT+CHUNK
1490  }
1500 GOTO COPY_REC
1510 ! !^100
1520 FIN_COPY:
1530 CLOSE (_SRV); CLOSE (_PC)
1540 IF HAS_STATUSBAR THEN {
1550 PRINT 'MESSAGE'("Transfering "+LOC_FILE$+" Complete",1)
1560  } ELSE {
1570 IF ISTHINCLIENT THEN CALL "*progbar;wrap_up"
1580 RESULT=1
9000 ! ^9000
9010 EXIT 
9999 END 
