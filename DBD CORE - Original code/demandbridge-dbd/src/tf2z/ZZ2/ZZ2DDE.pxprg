0010 REM "Export data dictionary <ZZ2DDE>
0035 REM "5.7 - 05/27/21 - 16.145876 - dmm - SSP# 280766
0037 REM "280766-Modify ZZ2DDE program to export schema doc that contains    
0040 REM "Copyright 2021 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER OUTPUT
0100 SETERR 9000
0110 X0$="ZZ2DDE",X1$="Export data dictionary"
0120 IF NOT(OUTPUT) THEN GOSUB OPEN_TEXT_FILE
0130 PRINT 'CS',@(0,3),"Generating Schema info in ",SUB(PTH(OUTPUT),"[wdx]","")
0150 DDE=HFN; OPEN (DDE)"providex.dde"
0170 DDF=HFN; OPEN (DDF)"providex.ddf"
0199 ! 
0200 READ_RECORD:
0210 READ (DDF,KNO=1,END=9900)IOL=IOL_DDF
0212 PRINT (OUTPUT)"Table : ",IOL=IOL_DDF_OUT
0213 PRINT @(5,5),'CL',IOL=IOL_DDF_OUT
0220 GOSUB LIST_FIELDS
0225 GOSUB LIST_KEYS
0230 PRINT (OUTPUT)
1990 GOTO READ_RECORD
1999 ! 
6000 REM "List fields in file
6005 LIST_FIELDS:
6007 KEYDDF$=KEC(DDF,KNO=0)
6010 READ (DDE,KEY=KEYDDF$,DOM=*NEXT)
6015 OFFSET$="1"
6020 KEYDDE$=KEY(DDE,END=*RETURN); READ (DDE,KEY=KEYDDE$)IOL=IOL_DDE
6030 IF KEYDDE$(1,6)<>KEYDDF$ THEN GOTO *RETURN
6040 IF TYPE$="N" THEN OFFSET$=""
6050 PRINT (OUTPUT)IOL=IOL_DDE_OUT
6060 IF TYPE$<>"N" THEN OFFSET$=STR(NUM(OFFSET$)+NUM(LEN$))
6080 GOTO 6020
6095 RETURN 
6099 ! 
6100 REM "List file keys
6105 LIST_KEYS:
6110 KEYFLD$="",K=0; CALL "DBSQLGEN",KEYFLD$,ERRMSG$,TABLE_NAME$,"*providex.ddf","","KEY"
6115 PRINT (OUTPUT)"Keys :"
6120 NEXTKEY=POS(SEP=KEYFLD$); IF NEXTKEY>0 THEN K++; NEXTKEY$=MID(KEYFLD$,1,NEXTKEY-1); KEYFLD$=MID(KEYFLD$,NEXTKEY+1) ELSE GOTO *RETURN
6140 PRINT (OUTPUT)TBL(K>1,"Primary Key","Alternate Key "+STR(K-1)),": ",STP(STP(NEXTKEY$,2),1,",")
6150 GOTO 6120
6195 RETURN 
6199 ! 
7000 OPEN_TEXT_FILE:
7001 ! TXTFL$="[wdx]C:\temp\dbschema.txt"
7002 TXTFL$="/tmp/dmm/dbschema.txt"
7005 SERIAL TXTFL$,ERR=*PROCEED; OPEN PURGE (HFN)TXTFL$; OUTPUT=LFO
7010 FILE_OPENED=1
7045 RETURN 
7099 ! 
8000 ! 8000 - iolists
8010 IOL_DDF:IOLIST TABLE_NAME$,PATH$,LST_UPD$,FILE_PROC$,NOT_USED1$,NOT_USED2$,NOT_USED3$,NOT_USED4$,NOT_USED5$,ALT_IOLIST$,SEP$,RFU$
8020 IOL_DDE:IOLIST IOLOBJ$,NAME$,DESC$,TYPE$,LEN$,OCC$,FORMAT$,VALID$,PRINT$,HELP$,NOTES$,KEYDEF$,QUERY$,CLASS$,FLAGS$,DFLT$,ALT_NAME$,USER_TAG$
8030 IOL_DDF_OUT:IOLIST TABLE_NAME$
8040 IOL_DDE_OUT:IOLIST TABLE_NAME$,"|",NAME$,"|",TBL(TYPE$="N",TBL(CLASS$="DATE-KKMMDD","CHAR","DATE"),"NUMBER"),"|",OFFSET$,"|",LEN$,"|",NOTES$,"|"
8999 ! 
9000 REM "ERROR PROCESSING
9005 PRINT "err: ",ERR," line: ",TCB(5),; INPUT *
9900 REM "END PROGRAM
9905 IF FILE_OPENED THEN CLOSE (OUTPUT); OUTPUT=0
9910 INPUT @(0,20),"Process complete <ret> to continue",*
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "280766-Modify ZZ2DDE program to export schema doc that contains    
