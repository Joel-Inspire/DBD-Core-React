0010 ! ZZ2RPA - Reports and printing functions
0035 REM "5.7 - 08/19/10 - 9.76 - crg - SSP# 197952
0037 REM "197952-Streamline processing by user - Chained journal updates     
0040 REM "Copyright 2010 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0060 ! 
0600 DROP_BOX_LOAD:! Build list of print selections and load into drop box
0602 ENTER LIST_CONTROL,PGM$,START$,END$,OTHER$,ERR=*NEXT
0605 LOCAL X,X3$,X4$,Y1$,Y0$,Z$,Z0,Z1
0610 CALL "ZZCOMP","","","",X3$,X4$,"",-1,0,0; DIM BDL_Z[NUM(X3$(60,3))]
0615 Z$="01O ZZS... "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,BDL_Z{ALL},Z0,Z1
0620 DROP_BOX LOAD LIST_CONTROL,"",ERR=*NEXT
0625 READ (BDL_Z[1],KEY=PGM$,DOM=*NEXT)
0630 DBL_LOAD_NEXT:
0635 ZZSK$=KEY(BDL_Z[1],END=DBL_LOAD_DONE); IF MID(ZZSK$,1,LEN(PGM$))<>PGM$ THEN GOTO DBL_LOAD_DONE
0640 DIM ZZS$(72); READ (BDL_Z[1],KEY=ZZSK$)ZZS$
0650 DROP_BOX LOAD LIST_CONTROL,++DB_INDEX,MID(ZZS$,7,12),ERR=*NEXT
0670 GOTO DBL_LOAD_NEXT
0685 DBL_LOAD_DONE:
0690 CALL "ZZFLES",X3$,Y1$,Y0$,"END",BDL_Z{ALL},0,0
0695 EXIT 
0699 ! 
0700 DROP_BOX_READ:! Given LIST_CONTROL, return CODE$ and DESC$
0710 ENTER LIST_CONTROL,PGM$,CODE$,DESC$
0720 LOCAL X,X3$,X4$,Y1$,Y0$,Z$,Z0,Z1
0730 CALL "ZZCOMP","","","",X3$,X4$,"",-1,0,0; DIM BDL_Z[NUM(X3$(60,3))]
0740 Z$="01O ZZS... "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,BDL_Z{ALL},Z0,Z1
0750 DROP_BOX READ LIST_CONTROL,CODE$,ERR=*NEXT
0760 DIM ZZS$(72); READ (BDL_Z[1],KEY=PGM$+CODE$,DOM=*NEXT)ZZS$
0770 DESC$=STP(MID(ZZS$,19,40),2)
0780 DBR_DONE:
0790 CALL "ZZFLES",X3$,Y1$,Y0$,"END",BDL_Z{ALL},0,0
0795 RETURN 
0799 ! 
0800 DROP_BOX_WRITE:! Given LIST_CONTROL and CODE$, set selection entry and return DESC$
0810 ENTER LIST_CONTROL,PGM$,CODE$,DESC$
0820 LOCAL X,X3$,X4$,Y1$,Y0$,Z$,Z0,Z1
0830 CALL "ZZCOMP","","","",X3$,X4$,"",-1,0,0; DIM BDL_Z[NUM(X3$(60,3))]
0840 Z$="01O ZZS... "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,BDL_Z{ALL},Z0,Z1
0850 DIM ZZS$(72); READ (BDL_Z[1],KEY=PGM$+CODE$,DOM=*NEXT)ZZS$
0860 DESC$=STP(MID(ZZS$,19,40),2)
0870 DROP_BOX WRITE LIST_CONTROL,CODE$,ERR=*NEXT
0880 DBW_DONE:
0890 CALL "ZZFLES",X3$,Y1$,Y0$,"END",BDL_Z{ALL},0,0
0895 EXIT 
0899 ! 
1000 PREP_PRINT_REPORT:! Prepare ZZP entry using ZZS record
1010 ENTER PGM$,CODE$,SORTFILE$,ERR=*NEXT
1020 LOCAL X,X3$,X4$,Y1$,Y0$,Z$,Z0,Z1
1025 ZZS_IOL1:IOLIST A$
1030 ZZS_IOL2:IOLIST R0$,W$,W[0],W[1],W[2],W[3],W[4]
1035 DIM W[4]
1040 CALL "ZZCOMP","","","",X3$,X4$,"",-1,0,0; DIM BDL_Z[NUM(X3$(60,3))]; INDEX=0,DB_INDEX=0
1045 Z$="01O ZZS...  02O ZZP...  "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,BDL_Z{ALL},Z0,Z1
1050 READ (BDL_Z[1],KEY=PAD(PGM$,6)+PAD(CODE$,12),DOM=ERR_NO_ZZS)IOL=ZZS_IOL1,IOL=ZZS_IOL2
1055 IF NUL(SORTFILE$) THEN GOSUB LOOKUP_SORT_SEQUENCE
1060 WRITE (BDL_Z[2],KEY=X3$(1,8))X3$,X4$,SORTFILE$,R0$,W$,A$
1080 PR_DONE:
1085 CALL "ZZFLES",X3$,Y1$,Y0$,"END",BDL_Z{ALL},0,0
1090 EXIT 
1095 ! 
1096 ERR_NO_ZZS: MSGBOX "ZZS entry not found: "+PGM$+" - "+CODE$; EXIT 
1099 ! 
1200 LOOKUP_SORT_SEQUENCE:! Given program name, call ZZRSEQ and get sort sequence and sortfile
1295 RETURN 
1299 ! 
1300 CHECK_BATCH:! Check if given batch exists
1310 ENTER BATCH$,FOUND,ERR=*NEXT
1320 LOCAL X,X3$,X4$,Y1$,Y0$,Z$,Z0,Z1
1340 CALL "ZZCOMP","","","",X3$,X4$,"",-1,0,0; DIM BDL_Z[NUM(X3$(60,3))]
1345 Z$="01O ZY9  "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,BDL_Z{ALL},Z0,Z1
1350 FOUND=0; READ (BDL_Z[1],KEY=PAD(BATCH$,5),DOM=*NEXT); FOUND=1
1380 FB_DONE:
1385 CALL "ZZFLES",X3$,Y1$,Y0$,"END",BDL_Z{ALL},0,0
1390 EXIT 
1395 ! 
1399 ! 
9999 END 
56001 REM "197952-Streamline processing by user - Chained journal updates     
