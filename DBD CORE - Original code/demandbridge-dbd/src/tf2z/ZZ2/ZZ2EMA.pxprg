0010 REM "Email Address Setup <ZZ2EMA>
0020 SETESC 9300; SETERR 9000
0035 REM "5.4 - 11/23/05 - 10.559444 - jme - SSP# 186852
0037 REM "186852-Email Order Shipping Confirmations                          
0040 REM "Copyright 2005 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="ZZ2EMA",X1$="Email Address Setup"
0120 DIM Z0$(80,"-")
0130 K0=1,K1=1
0135 C9=-1
0200 RUN "DTGEMA" ! new version
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 K9$="H",K9=LEN(K9$)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1,LEN(A$))
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X EML...  02O AP4...  03O FM0... 04O AR1... 13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0620 GOSUB 6000
0690 READ (Z[1],KEY=K9$,DOM=0691)
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM AP4$(427),FM0$(600)
1035 DIM A$(512)
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1250,1300,1350,1500
1200 REM "Email Type
1202 PRINT @(0,15),'CL','CF'
1205 ! Now entering either VN or CS A$(6,2)="VN"; PRINT @(15,3),A$(6,2); GOTO 1220 ! for ssp#174532 set to vendor, will set later
1209 X$="VN for Vend email, CS for CSR email, SH for Shipment File, CR for Cust Reports"
1210 CALL "ZZENTR","SUX",A{ALL},A$,X4$,X3$,15,3,6,2,C0,"","{1"+X$,"","ZZ2EMA00","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1211 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9800
1215 GOSUB CHECK_EMAIL_TYPE; IF NOT(OK) THEN GOTO 1210
1220 GOSUB DISPLAY_EMAIL_TYPE
1240 GOTO 1120
1250 ! TopForm Reference
1260 GOSUB ENTER_TOPFORM_REF_1
1290 GOTO 1120
1300 ! Second TopForm Reference if needed
1310 GOSUB ENTER_TOPFORM_REF_2
1340 GOTO 1120
1350 ! Sequence number
1360 CALL "ZZENTR","ZUX",A{ALL},A$,X4$,X3$,15,6,28,3,C0,"","{1"+X$,"","ZZ2EMA04","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1360,1361
1390 GOTO 1120
1500 REM "
1505 Z9=-1
1520 READ (Z[1],KEY=A$(6,25),KNO=1,DOM=1600)IOL=0310
1521 A1$=A$(1,5)
1525 Z9=0
1530 GOSUB 5800
1590 GOTO 1700
1600 REM "
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630
1630 GOTO 1000
1650 GOSUB GET_NEXT_EMAIL_KEY
1655 IF A$(6,2)="VN" THEN A$(31,10)="PROOF" ELSE IF A$(6,2)="CS" THEN A$(31,10)="RFQ"; PRINT @(15,8),A$(31,10), ! for now the only one allowed
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1820 CALL "ZZPROM",STR(6+C9),X3$,Z,"","","V",X0; ON Z GOTO 1850,4000,1630,6700
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310
1865 C9=1
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF C0>3 OR X0<0 THEN GOTO 1800
2040 ON X0 GOTO 2100,2150,2200,2250,2300,2350,2400,2450,2500,2550,2600,2650,2700,1800
2100 REM "Address Type
2105 IF A$(6,2)="CR" THEN X$="Enter Program Name" ELSE IF A$(6,2)="VN" THEN X$="Enter Data Type" ELSE GOTO 2020
2107 PRINT @(5,8),'CL','EB',X$(7),":",'SF'
2110 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,19,8,31,10,C0,"","{3"+X$,"","ZZ2EMA02","","",""
2120 IF A$(6,2)="CR" AND POS(A$(31,6)="      FM2CEAFM2CIAFM2TBA",6)=0 THEN GOTO 2100
2140 GOTO 2020
2150 REM "Email Address
2160 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,19,9,41,60,C0,"","{3"+X$,"","ZZ2EMA04","","",""
2190 GOTO 2020
2200 REM transfer type (email,ftp, webec)
2205 IF POS(A$(6,2)="SHCR",2)=0 THEN A$(101,156)=DIM(156); GOTO 2020
2207 X$="E-Email, F-FTP, W-Webec"
2210 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,19,10,101,1,C0,"","{3"+X$,"","ZZ2EMA05","","",""
2220 IF POS(A$(101,1)="EFW ")=0 THEN GOTO 2200
2240 GOTO 2020
2250 REM ' FTP LOGIN
2255 IF A$(101,1)<>"F" THEN GOTO 2020
2260 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,19,11,102,15,C0,"","{3"+X$,"","ZZ2EMA04","","",""
2290 GOTO 2020
2300 REM ' FTP PASSWORD
2305 IF A$(101,1)<>"F" THEN GOTO 2020
2310 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,19,12,117,20,C0,"","{3"+X$,"","ZZ2EMA04","","",""
2340 GOTO 2020
2400 REM send from directory
2402 IF POS(A$(6,2)="SHCR",2)=0 THEN GOTO 2020
2410 BASE$=CVS(PTH(Z[1]),2); XD=POS("EML"=BASE$); BASE$=BASE$(1,XD-1)
2415 IF POS("\"=BASE$)<>0 THEN SEP$="\" ELSE SEP$="/"
2420 DIR$=BASE$+A$(6,2); DIRECTORY DIR$,ERR=*NEXT
2425 DIR$=DIR$+SEP$+CVS(A$(8,20),2); DIRECTORY DIR$,ERR=*NEXT
2427 DIR$=DIR$+SEP$+A$(28,3); DIRECTORY DIR$,ERR=*NEXT
2430 A$(137,60)=PAD(DIR$,60)
2435 PRINT @(19,13),A$(137,60)
2440 GOTO 2020
2450 REM SEND TO DIRECTORY (FTP)
2455 IF A$(101,1)<>"F" THEN GOTO 2020
2460 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,19,14,197,60,C0,"","{3"+X$,"","ZZ2EMA04","","",""
2490 GOTO 2020
2500 REM Summary count UOM
2502 IF A$(6,2)<>"SH" THEN GOTO 2020
2505 PRINT @(0,15),'CL','SB',"Summary Count UOM:",'SF'
2510 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,19,15,257,4,C0,"","{3"+X$,"","ZZ2EMA04","","",""
2540 GOTO 2020
2550 REM ' email from
2555 IF A$(101,1)<>"E" THEN GOTO 2020
2560 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,19,16,311,60,C0,"","{3"+X$,"","ZZ2EMA04","","",""
2565 IF STP(A$(311,60),3," ")="" THEN GOTO 2550
2590 GOTO 2020
2600 REM ' report title/SUBJECT
2605 IF POS(A$(101,1)="EW")=0 THEN GOTO 2020
2607 IF A$(101,1)="E" THEN X$="Enter Email Subject" ELSE IF A$(101,1)="W" THEN X$="Enter Report Title" ELSE X$=""
2610 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,19,17,261,50,C0,"","{3"+X$,"","ZZ2EMA04","","",""
2615 IF STP(A$(261,50),3," ")="" THEN GOTO 2610
2640 GOTO 2020
2650 REM ' Report Selection ID
2655 IF POS(A$(6,2)="CR")=0 THEN GOTO 2020
2657 X$="Enter a Valid Report Selection ID"
2660 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,19,18,371,12,C0,"","{3"+X$,"","ZZ2EMA04","","",""
2680 GOTO 2020
2700 REM 'last process date
2710 CALL "ZZENTR","D",A{ALL},A$,X4$,X3$,19,19,383,6,C0,"","{3"+X$,"","ZZ2EMA04","","",""
2740 GOTO 2020
4000 REM REMOVE
4010 REMOVE (Z[1],KEY=A1$)
4040 GOTO 1200
5800 REM "Alternate read
5805 SWITCH A$(6,2)
5810 CASE "VN"; DIM AP4$(426),FM0$(600); FIND (Z[2],KEY=A$(8,2),DOM=*PROCEED)AP4$(1); FIND (Z[3],KEY="P"+A$(8,14),DOM=*PROCEED)FM0$(1)
5840 BREAK
5880 END SWITCH 
5895 RETURN 
5899 ! ************************************
6000 REM "BACKGROUND
6010 PRINT @(0,4),'CL',
6015 PRINT (0,ERR=*NEXT)'SB',
6050 PRINT @(3,3),"      Type:",@(5,6),"Sequence:",@(0,7),Z0$,
6060 PRINT @(5,8),"Program name:",@(3,9),"Ftp/Email Addr:",
6065 PRINT @(4,10),"Transfer Type:"
6070 PRINT @(8,11),"Ftp Login:"
6075 PRINT @(5,12),"Ftp Password:"
6080 PRINT @(0,13),"Send Fr Directory:"
6085 PRINT @(0,14),"Send To Directory:"
6090 PRINT @(7,16),"Email From:"
6092 PRINT @(4,17),"Title/Subject:"
6093 PRINT @(4,18),"Rpt Selection:"
6094 PRINT @(2,19),"Last Process Dt:"
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 PRINT @(19,8),A$(31,10),@(19,9),A$(41,60),@(19,10),A$(101,1),@(19,11),A$(102,15),@(19,12),@(19,13),A$(137,60),@(19,14),A$(197,60),@(19,16),A$(311,60),@(19,17),A$(261,50),@(19,18),A$(371,12)
6225 IF A$(6,2)="SH" THEN PRINT @(0,15),'CL','SB',"Summary Count UOM:",'SF',@(19,15),A$(257,4)
6230 CALL "ZZDISP","D",A$(383,6),"",X3$,"","",19,19,X4$
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 PRINT @(15,3),A$(6,2),
6465 GOSUB DISPLAY_EMAIL_TYPE
6470 GOSUB DISPLAY_TOPFORM_REF; GOSUB DISPLAY_TOPFORM_REF_2
6485 PRINT @(15,6),A$(28,3),
6487 IF A$(6,2)="SH" THEN PRINT @(0,15),'CL','SB',"Summary Count UOM:",'SF',@(19,15),A$(257,4)
6490 RETURN 
6500 CREATE_DIR:
6502 WKDIR$=""
6505 TMPDIR$=DIR$
6510 XU=POS("/"=TMPDIR$),XW=POS("\"=TMPDIR$); IF XU=0 AND XW=0 THEN XU=1; TMPDIR$="/usr/lib/pvx/"+TMPDIR$
6590 RETURN 
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY
6705 IF Z9=0 THEN GOTO 6790
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 A$(1,LEN(A1$))=A1$
6825 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
6902 V9$="2603  2605  "
7500 DISPLAY_EMAIL_TYPE:! Display Email Type
7510 DIM EMAIL_TYPE$(20); TYPE_TAG$="",TYPE_TAG_2$=""
7515 SWITCH A$(6,2)
7520 CASE "VN"; EMAIL_TYPE$(1)="Vendor",TYPE_TAG$="Vendor",TYPE_TAG_2$="Plant"; BREAK
7522 CASE "CS"; EMAIL_TYPE$(1)="Customer Service Rep",TYPE_TAG$="CSR",TYPE_TAG_2$=""; BREAK
7524 CASE "SH"; EMAIL_TYPE$(1)="Warehouse Location",TYPE_TAG$="Location",TYPE_TAG_2$=""; BREAK
7526 CASE "CR"; EMAIL_TYPE$(1)="Customer Number",TYPE_TAG$="Customer",TYPE_TAG_2$=""; BREAK
7535 END SWITCH 
7540 PRINT @(18,3),EMAIL_TYPE$,
7541 PRINT (0,ERR=*NEXT)@(0,4),'SB',
7542 IF NUL(TYPE_TAG$) THEN PRINT @(0,4),'CL', ELSE PRINT @(0,4),PAD(TYPE_TAG$,13,0)+":",
7543 IF TYPE_TAG_2$="" THEN PRINT @(0,5),'CL', ELSE PRINT @(0,5),PAD(TYPE_TAG_2$,13,0)+":",
7544 PRINT (0,ERR=*NEXT)'SF',
7545 RETURN 
7549 ! **********************************************************
7550 CHECK_EMAIL_TYPE:! Check email type field and set OK 
7555 OK=SGN(POS(A$(6,2)="VNCSSHCR",2))
7595 RETURN 
7599 ! ***************************************************
7600 ENTER_TOPFORM_REF_1:! Based on email type enter the correct TopForm Reference field
7605 SWITCH A$(6,2)
7610 CASE "VN"
7615 CALL "ZZENTR","AUX",A{ALL},A$,X4$,X3$,15,4,8,10,C0,"A/P","{1"+X$,"","ZZ2EMA02","AP4","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 7615,7616
7616 IF ABS(C0)=4 THEN GOTO 9900
7617 DIM AP4$(426); FIND (Z[2],KEY=A$(8,10),DOM=7615)AP4$(1)
7619 BREAK
7620 CASE "CS"
7625 CALL "ZZENTR","SUX",A{ALL},A$,X4$,X3$,15,4,8,4,C0,"","{1"+X$,"","ZZ2MA03","","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 7625,7626
7626 IF ABS(C0)=4 THEN GOTO 9900
7629 BREAK
7630 CASE "SH"
7635 CALL "ZZENTR","SZUX",A{ALL},A$,X4$,X3$,15,4,8,4,C0,"","{1"+X$,"","IC2MAC02","FM0D","X",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 7635,7636
7636 IF ABS(C0)=4 THEN GOTO 9900
7639 BREAK
7640 CASE "CR"
7645 CALL "ZZENTR","AUX",A{ALL},A$,X4$,X3$,15,4,8,10,C0,"A/R","{1"+X$,"","ZZ2EMA02","AR1","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 7645,7646
7646 IF ABS(C0)=4 THEN GOTO 9900
7648 DIM AR1$(600); FIND (Z[4],KEY=A$(8,10),DOM=7645)AR1$(1)
7649 BREAK
7690 END SWITCH 
7693 GOSUB DISPLAY_TOPFORM_REF
7695 RETURN 
7699 ! **********************************************
7700 DISPLAY_TOPFORM_REF:! Based on email type and already having the correct referenced record, display it
7705 DIM TF_REF$(40)
7710 SWITCH A$(6,2)
7715 CASE "VN"; CALL "ZZDISP","A",A$(8,10),"A/P",X3$,"","",15,4,X4$; PRINT @(30,4),'CL',MID(AP4$,11,35),; BREAK
7720 CASE "CS"; PRINT @(15,4),A$(8,4),@(30,4),'CL',; BREAK
7725 CASE "SH"; PRINT @(15,4),A$(8,4),@(30,4),'CL',; BREAK
7730 CASE "CR"; CALL "ZZDISP","A",A$(8,10),"A/R",X3$,"","",15,4,X4$; PRINT @(30,4),'CL',AR1$(11,35); BREAK
7740 END SWITCH 
7745 RETURN 
7749 ! **************************************************
7750 GET_NEXT_EMAIL_KEY:! find last key used and get next one
7755 PREV_ADDR$=KEL(Z[1],KNO=0,END=*PROCEED); IF PREV_ADDR$="" THEN A$(1,5)=DIM(5,$00$) ELSE A$(1,5)=PREV_ADDR$
7775 A$(1,5)=FN%NEXT_SEQ$(A$(1,5),5); WRITE (Z[1],KEY=A$(1,5),DOM=*SAME)IOL=0310
7780 A1$=A$(1,5)
7795 RETURN 
7799 ! ****************************************************
7800 ENTER_TOPFORM_REF_2:! Based on email type enter the correct TopForm Reference field
7805 SWITCH A$(6,2)
7810 CASE "VN"
7814 J9$="P"+A$(8,10)
7815 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,15,5,18,4,C0,"","{1"+X$,J9$,"ZZ2EMA14","FM0P","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 7815,7816
7816 DIM FM0$(600); IF NOT(NUL(A$(18,4))) THEN FIND (Z[3],KEY="P"+A$(8,14),DOM=7815)FM0$
7819 BREAK
7820 CASE "CS"; BREAK
7822 CASE "SH"; BREAK
7824 CASE "CR"; BREAK
7890 END SWITCH 
7893 GOSUB DISPLAY_TOPFORM_REF_2
7895 RETURN 
7899 ! **************************************************
7900 DISPLAY_TOPFORM_REF_2:! Based on email type and already having the correct referenced record, display 2ND key if needed
7905 DIM TF_REF2$(40)
7910 SWITCH A$(6,2)
7915 CASE "VN"; PRINT @(15,5),A$(18,4),@(30,5),FM0$(16,35),; BREAK
7920 CASE "CS"; PRINT @(0,5),'CL',; BREAK
7940 END SWITCH 
7945 RETURN 
7949 ! ***************************************************
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "186852-Email Order Shipping Confirmations                          
