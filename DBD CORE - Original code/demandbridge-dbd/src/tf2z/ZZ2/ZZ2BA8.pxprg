0010 REM "<ZZ2BA8> Level 8 Merge Program Code to Generated Prog
0030 REM "(C) 1985,1986 Basic Ideas, Inc; Atlanta, Ga.
0035 REM "This software contains trade secrets and proprietary information"
1000 L$="9999 END"
1010 ENTER TARGET_FILE$,S0,S1,Q0,A0$
1020 SETERR 9000
1030 ! 
1040 SWITCH A0$
1050 CASE "REM ONLY"
1060 A0=1,A0$=""
1070 BREAK
1080 CASE "REM OUT"
1090 A0=2,A0$=""
1100 BREAK
1110 CASE "QUIET"
1120 A0$="",A0=-1
1130 BREAK
1140 DEFAULT 
1150 ! Data in A0$ may be program lines to merge.
1151 A0=0
1160 END SWITCH 
1170 ! 
1180 ! CONVERT PRG FILE TO TEXT
1190 WORK_FILE$=TARGET_FILE$+".txt"
1200 ERASE WORK_FILE$,ERR=*NEXT
1210 SERIAL WORK_FILE$
1220 CALL "*PG.CNV",TARGET_FILE$,WORK_FILE$
1230 ! 
1240 ! OPEN TEXT FILE
1250 IF Q0=0 THEN Q0=UNT ELSE CLOSE (Q0,ERR=*NEXT)
1260 OPEN (Q0,ERR=9800)WORK_FILE$
1270 IF A0<>-1 THEN PRINT @(10,7),TARGET_FILE$
1280 ! 
1290 ! READ TEXT FILE
1300 REPEAT 
1310 READ RECORD (Q0,END=1570)A$
1320 P=POS(" ?":A$); IF P=0 THEN GOTO 1550
1330 IF LEN(A$)<5 THEN GOTO 1550
1340 IF POS(L$=A$)=1 THEN GOTO 1550
1350 IF NUM(A$(1,P))<S0 THEN GOTO 1550
1360 IF NUM(A$(1,P))>S1 THEN GOTO 1570
1370 GOSUB UPDATE_ACTIVITY_MONITOR
1380 ! 
1390 SWITCH A0
1400 CASE 0
1410 CASE -1
1420 GOSUB BUILD_LINE_OUT
1430 BREAK
1440 CASE 1
1450 IF MID(A$,P+1,1)="!" OR MID(A$,P+1,3)="REM" THEN GOSUB BUILD_LINE_OUT
1460 BREAK
1470 CASE 2
1480 LINENUM=0
1490 LINENUM=NUM(MID(A$,1,P),ERR=*NEXT)
1500 IF LINENUM>0 AND LINENUM<100 OR MID(A$,P+1,1)<>"!" AND MID(A$,P+1,3)<>"REM" THEN {
1510 GOSUB BUILD_LINE_OUT
1520  }
1530 BREAK
1540 END SWITCH 
1550 ! 
1560 GOTO 1580
1570 EXITTO 1590
1580 UNTIL A$=""
1590 ! 
5000 ! NORMAL END OF LOOP
5010 CLOSE (Q0)
5020 ERASE WORK_FILE$,ERR=*NEXT
5030 GOTO 9900
5040 ! 
6000 REM ACTIVITY INDICATOR
6210 UPDATE_ACTIVITY_MONITOR:
6220 IF A0<>-1 THEN PRINT ".",
6290 RETURN 
7000 ! 
7001 BUILD_LINE_OUT:
7010 CLINE$=CPL(A$)
7020 LINELEN=LEN(CLINE$)
7030 CLINE$=$000000$+CLINE$
7040 CLINE$(1,3)=BIN(LINELEN,3)
7050 A0$+=CLINE$
7060 RETURN 
9000 REM "ERROR
9020 EXIT ERR
9910 IF A0<>-1 THEN PRINT @(0,7),'CE',
9930 CLOSE (Q0); ERASE WORK_FILE$,ERR=*NEXT
9940 EXIT 
9999 END 
