0010 REM "RUN Job Stream <ZZ2JS1>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 06/25/14 - 9.638055 - dmm - SSP# 270887
0037 REM "270887-Job Stream version of FM2UT9 dies which causes issues with  
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0102 %IN_JOBSTREAM=1 ! SSP154913, Set global, need in ZZFLES if err
0104 IF NAR>=1 THEN JOB_ID$=ARG(1)
0105 IF X3$="" THEN GOSUB 7500
0110 X0$="ZZ2JS1",X1$="Run Job Stream"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 IF BKG=0 THEN PRINT @(0,3),'CE',@(10,10),"Running Job Stream ",Q1$,
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$
0320 IOLIST B$
0500 REM "FILES
0510 OPEN (1)"ZZPARM"
0535 READ (1,KEY=X3$(9,3)+"JS"+JOB_ID$)JS$
0600 REM "
0610 IF BKG=0 THEN PRINT @(29,12),JS$(9,30),
0640 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0800 REM "Open log file, if needed erase first
0805 LOG_FILE$="JS"+JS$(6,3)+".log"
0810 IF JS$(159,1)="Y" THEN ERASE LOG_FILE$,ERR=*NEXT; GOTO 0810
0815 LOG=HFN; CLOSE (LOG); OPEN LOCK (LOG,ERR=*NEXT)LOG_FILE$; GOTO 0820
0816 SERIAL LOG_FILE$,0,0; GOTO 0815; REM "create file if needed
0825 LOG_FILE_OPEN=1
0829 IF JS$(159,1)<>"Y" THEN MESS$=""; GOSUB 7550; REM " Put blank line before starting to help seperate from other entries if not clearing each time
0830 MESS$="STARTING|Job Stream|"+JS$(6,3)+"|"+STP(JS$(9,30),1)+"|"; GOSUB 7550
1000 REM "BEGIN MAIN PROCESS
1005 GOSUB 7600; REM "do pre job command
1010 REM "Process the job streams
1020 FOR I=1 TO 10
1025 INDEX=132+I*28
1030 IF STP(JS$(INDEX,8),3," ")="" THEN GOTO 1080
1035 PROG$=STP(JS$(INDEX,8),1),PARM$=STP(JS$(INDEX+8,20),1),PARM_HOLD$=PARM$
1040 MESS$="STARTING|"+PROG$+"|"+PARM$+"|"; GOSUB 7550
1042 %IN_JOBSTREAM=1 ! SSP154913, Set global, need in ZZFLES if err
1045 CALL PROG$,ERR=1046,X3$,X4$,"JS",PARM$; PARM$=PARM$+" "; GOTO 1047
1046 MESS$="ERROR|"+STR(ERR)+"|CALLING|"+PROG$+"|"+PARM_HOLD$+"|"; GOSUB 7550; GOTO 1050
1047 IF PARM$(1,1)=$FF$ THEN MESS$="ERROR MESSAGE|"+PARM$(2)+"|BY|"+PROG$+"|"+PARM_HOLD$+"|" ELSE MESS$="FINISHED|"+PROG$+"|"+PARM_HOLD$+"|"
1048 GOSUB 7550; REM "MESS$ set in 1047
1080 NEXT I
1085 GOSUB 7650
1090 MESS$="FINISHED|Job Stream|"+JS$(6,3)+"|"+STP(JS$(9,30),1)+"|"; GOSUB 7550
1091 IF JS$(159,1)<>"Y" THEN MESS$=""; GOSUB 7550; REM "If not restarting log each time, put in blank line for readability
1095 GOTO 9900
7500 REM "Being called from the command line setup X3$ from TFE info etc
7503 REM "2ND ARGUMENT PASSED IS THE environment id, if not passed, then use JS+Job stream # (ie JS001)
7504 IF NAR>=2 THEN Q1$=ARG(2) ELSE Q1$="JS"+JOB_ID$ END_IF 
7505 CLOSE (13); OPEN (13,ERR=9800)"ZZPARM"; READ RECORD (13,KEY="TFE"+Q1$+DIM(3),DOM=9800)ENV$
7508 ENV$(1,8)=FID(0)+DIM(6)
7510 WRITE RECORD (13,KEY=FID(0)+DIM(6))ENV$
7530 READ (13,KEY=FID(0)+DIM(6))X3$,*,X4$
7535 %X3$=X3$,%X4$=X4$,%X3_SET=1 ! SSP270887
7540 LEAVE_VIA_REL=1; REM "We need to release when we leave
7545 RETURN 
7550 REM "write MESS$ to log file (open on 2)
7553 IF LOG_FILE_OPEN THEN GOTO 7554 ELSE GOTO 7595
7555 PRECISION 14; OUT$=DTE(0:"%D-%Ml-%Y %h:%m:%s")+"|"+MESS$; PRECISION 2
7560 PRINT (LOG)OUT$
7570 CLOSE (LOG); OPEN LOCK (LOG,ERR=*NEXT)LOG_FILE$; GOTO 7595 ! Close then open log file each time so messages will always write
7575 LOG_FILE_OPEN=0
7595 RETURN 
7600 REM "Process pre-job command
7605 IF STP(JS$(39,60),3," ")="" THEN GOTO 7645
7608 COMM$=STP(JS$(39,60),1)
7610 MESS$="STARTING|PRE-JOB COMMAND|"+COMM$+"|"; GOSUB 7550
7620 INVOKE COMM$
7625 MESS$="FINISHED|PRE-JOB COMMAND|"+COMM$+"|"; GOSUB 7550
7645 RETURN 
7650 REM "Process post job command
7655 IF STP(JS$(99,60),3," ")="" THEN GOTO 7695
7658 COMM$=STP(JS$(99,60),1)
7660 MESS$="STARTING|POSTJOB COMMAND|"+COMM$+"|"; GOSUB 7550
7670 INVOKE COMM$
7675 MESS$="FINISHED|POSTJOB COMMAND|"+COMM$+"|"; GOSUB 7550
7695 RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9800 REM "RELEASE IF NEEDED
9805 RELEASE 
9900 REM "END PROGRAM
9903 %IN_JOBSTREAM=0
9904 MESS$="Leaving Job Stream|"+JS$(6,3)+"|"+STP(JS$(9,30),1)+"|"; GOSUB 7550
9905 IF LEAVE_VIA_REL THEN GOTO 9800
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "270887-Job Stream version of FM2UT9 dies which causes issues with  
