0010 REM "Scheduled Email Processing <ZZ2SEP>                                     
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 05/13/19 - 11.574444 - dmm - SSP# 307154
0037 REM "307154-DBSPT-61747; Modify Auto Reorder Notice Email program with  
0040 REM "Copyright 2019 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="ZZ2SEP",X1$="Scheduled Email Processing"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "                                                                 
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0310 IOLIST FT4$,FT4[0],FT4[1],FT4[2],FT4[3],FT4[4],FT4[5],FT4[6],FT4[7],FT4[8],FT4[9],FT4[10],FT4[11],FT4[12],FT4[13],FT4[14],FT4[15],FT4[16],FT4[17],FT4[18],FT4[19],FT4[20],FT4[21],FT4[22],FT4[23],FT4[24],FT4[25],FT4[26],FT4[27],FT4[28],FT4[29]
0500 REM "FILES                                                            
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O EMS...  02O EMR...  03O EML...  04OSEMS...  05O FM1...  06O IC0...  07O AR1...  08O FT3...  09O FT4...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0595 DIM EML_P$(512); READ (Z[13],KEY=X3$(9,3)+"EML",DOM=*NEXT)EML_P$(1)
0600 REM "                                                                 
0610 IF Q1$="" THEN GOSUB 6000
0640 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0900 ! 
0905 DIM EMS[1]
0915 TODAY$=DTE(JUL(0,0,0):"YYYYMMDD"),TODAY$=CHR(NUM(TODAY$(1,3))-125)+TODAY$(4)
0920 CALL "UPDEMS;READ_BY_KEY",Z[1],1,"",EMS$,EMS{ALL},FOUND ! ssp#246635
0999 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
1000 REM "BEGIN MAIN PROCESS                                               
1010 CALL "UPDEMS;READ_NEXT",Z[1],1,EMS$,EMS{ALL},FOUND
1020 WHILE FOUND AND MID(EMS$,52,6)<=TODAY$
1025 IF MID(EMS$,52,6)=DIM(6) THEN GOTO 1085
1030 GOSUB SEND_EMAIL
1035 GOSUB LOG_EMAIL
1085 CALL "UPDEMS;READ_NEXT",Z[1],1,EMS$,EMS{ALL},FOUND
1090 WEND 
1095 GOTO 9900
1099 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
1600 GOTO 1000
2000 SEND_EMAIL:! Given EMS$ send Email with CC to CSR, if Email address is blank, just send to CSR
2005 ! Get CSR Email
2010 DIM EML$:IOL(Z[3]); READ DATA FROM DIM(LEN(EML$)),REC=EML$ TO IOL=IOL(EML$)
2011 FIND (Z[3],KNO=2,KEY="CS"+PAD(EMS$(158,4),20)+PAD("RFQ",10),DOM=*NEXT,REC=EML$)
2020 IF NUL(EMS$(58,100)) THEN TO_ADDRESS$=STP(EML.EMAIL_ADDRESS$),CC_ADDRESS$="",BCC_ADDRESS$="" ELSE TO_ADDRESS$=STP(EMS$(58,100)),CC_ADDRESS$=STP(EML.EMAIL_ADDRESS$),BCC_ADDRESS$=""
2025 GOSUB GET_DATA_FROM_FILES
2050 REPLY_ADDRESS$=FROM_ADDRESS$
2055 SMTP_SERVER$=STP(EML_P$(7,60),3," ")
2065 ERASEIT=1,BODYENCODING$="text/html"
2070 CALL "*web/email",ERR=*NEXT,FROM_ADDRESS$,REPLY_ADDRESS$,TO_ADDRESS$,CC_ADDRESS$,BCC_ADDRESS$,SUBJECT$,MESSAGE$,ATTACHMENT$,"N",SMTP_SERVER$,SERVERTIMEOUT,LINEWRAPSAT,BODYENCODING$,ERASEIT,ERRORMESG$ ! 272475 - Handle errors, just ignore for now
2095 RETURN 
2099 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
2100 GET_DATA_FROM_FILES:! Retrieve data from files
2105 FORMAT_NAME$=STP(EMS$(162,10))
2110 FILE_NAME$="arn_from_address_"+FORMAT_NAME$+".txt"; GOSUB GET_FILE_DATA; FROM_ADDRESS$=SUB(SUB(FILE_DATA$,$0D$,""),$0A$,"")
2115 FILE_NAME$="arn_subject_"+FORMAT_NAME$+".txt"; GOSUB GET_FILE_DATA; SUBJECT$=SUB(SUB(FILE_DATA$,$0D$,""),$0A$,"")
2120 FILE_NAME$="arn_message_"+FORMAT_NAME$+".txt"; GOSUB GET_FILE_DATA; MESSAGE$=FILE_DATA$
2125 FILE_NAME$="arn_bcc_address_"+FORMAT_NAME$+".txt"; GOSUB GET_FILE_DATA; BCC_ADDRESS$=SUB(SUB(FILE_DATA$,$0D$,""),$0A$,"") ! SSP 222830
2195 RETURN 
2199 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
2200 GET_FILE_DATA:! Given FILE_NAME$ put data into FILE_DATA$
2205 FILE_DATA$=""
2210 FILE_CHAN=HFN; OPEN (FILE_CHAN,ERR=*RETURN)"email"+DLM+FILE_NAME$
2215 SELECT LINE$ FROM FILE_CHAN
2220 FILE_DATA$+=LINE$+$0D0A$
2230 NEXT RECORD 
2290 CLOSE (FILE_CHAN)
2292 GOSUB PROCESS_TAGS
2295 RETURN 
2299 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
3000 LOG_EMAIL:! move  to log file
3010 DIM EMR$(248); EMR$(1)=EMS$
3015 EMR$(172,6)=TODAY$
3020 WRITE (Z[2],KEY=EMR$(1,10))EMR$
3025 REMOVE (Z[4],KEY=EMS$(1,10),DOM=*NEXT) ! remove from scheduled
3095 RETURN 
3099 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
4000 PROCESS_TAGS:
4010 LINE$=""
4020 LINE$=FILE_DATA$
4030 INDEX=POS("?tf?"=LINE$); IF INDEX=0 THEN GOTO 4495
4040 END_INDEX=POS("?"=LINE$(INDEX+4)),TAG$=LINE$(INDEX+4,END_INDEX-1),LINE1$=LINE$(1,INDEX-1),LINE2$=LINE$(INDEX+END_INDEX+4)
4050 SWITCH LCS(TAG$)
4060 CASE "item_code"
4070 TAG_VALUE$=STP(MID(EMS$,42,10),2); BREAK
4080 CASE "cust_code"
4090 TAG_VALUE$=""; CALL "ZZDISP","AX",MID(EMS$,22,10),"A/R",X3$,TAG_VALUE$,"",0,0,X4$; BREAK
4100 CASE "order_number"
4110 TAG_VALUE$=""; CALL "ZZDISP","AX",MID(EMS$,11,8),"O/P",X3$,TAG_VALUE$,"",0,0,X4$; BREAK
4120 CASE "item_desc" ! WO 235077
4130 TAG_VALUE$=""; GOSUB GET_ITEM_INFO; TAG_VALUE$=ITEM_DESC$; BREAK ! WO 235077
4140 CASE "cust_name"
4150 TAG_VALUE$=""; GOSUB GET_CUST_NAME; TAG_VALUE$=CUST_NAME$; BREAK ! WO307154
4160 CASE "order_date"
4170 TAG_VALUE$=""; GOSUB GET_ORDER_DATE; TAG_VALUE$=ORDER_DATE$; BREAK ! WO307154
4180 CASE "order_quantity"
4190 TAG_VALUE$=""; GOSUB GET_ORDER_QUANTITY; TAG_VALUE$=ORDER_QUANTITY$; BREAK ! WO307154
4390 DEFAULT 
4395 TAG_VALUE$=""; BREAK
4400 END SWITCH 
4410 LINE$=LINE1$+TAG_VALUE$+LINE2$
4450 GOTO 4030
4495 FILE_DATA$=LINE$
4499 RETURN 
5000 REM "EOJ                                                              
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND                                                       
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"Processing Scheduled Emails"
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
8000 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~
8002 ! WO 235077 - Get item information
8005 GET_ITEM_INFO:
8010 ITEM_DESC$=""
8015 IF NUL(MID(EMS$,32,20)) THEN GOTO *RETURN ELSE IF NUL(MID(EMS$,32,10)) THEN GOTO READ_IC0
8020 READ_FM1:
8030 READ (Z[5],KEY=MID(EMS$,32,20),DOM=READ_IC0)FM1$
8040 ITEM_DESC$=STP(MID(FM1$,42,40),2); GOTO DONE_GII
8050 READ_IC0:
8060 READ (Z[6],KEY=MID(EMS$,32,20),DOM=DONE_GII)IC0$
8070 ITEM_DESC$=STP(MID(IC0$,21,40),2); GOTO DONE_GII
8090 DONE_GII:
8095 RETURN 
8099 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~
8100 GET_CUST_NAME:! WO307154
8105 CUST_NAME$=""
8110 IF NUL(MID(EMS$,22,10)) THEN GOTO *RETURN
8115 DIM AR1$(600); FIND (Z[7],KEY=EMS$(22,10),DOM=*RETURN)AR1$(1)
8120 CUST_NAME$=AR1$(11,35)
8145 RETURN 
8149 ! 
8150 GET_ORDER_DATE:! WO307154
8155 ORDER_DATE$=""
8160 IF NUL(MID(EMS$,11,8)) THEN GOTO *RETURN
8165 DIM FT3$(606); FIND (Z[8],KEY=EMS$(11,8),DOM=*RETURN)FT3$(1)
8170 CALL "ZZDISP","DX",FT3$(16,6),"",X3$,ORDER_DATE$,"",0,0,X4$
8195 RETURN 
8199 ! 
8200 GET_ORDER_QUANTITY:! WO307154
8205 ORDER_QUANTITY$=""
8210 IF NUL(MID(EMS$,11,8)) OR NUL(MID(EMS$,19,3)) THEN GOTO *RETURN
8215 DIM FT4$(356),FT4[29]; FIND (Z[9],KEY=EMS$(11,8)+EMS$(19,3),DOM=*RETURN)IOL=0310
8220 ORDER_QUANTITY$=STR(FT4[10]:"#,###,##0")
8245 RETURN 
8249 ! 
9000 REM "ERROR PROCESSING                                                 
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL                                
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC                                       
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM                                      
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "191992-Generate welcome email during order entry and flag items   
56001 REM "222830-Reorder notice email feature enhancement: support for BCC.  
56002 REM "235077-DT9-Add logic to output the item's description on the ARN.  
56003 REM "246635-[DT12] Issue with ZZ2SEP, Sceduled email processing. Emails 
56004 REM "272475-Error in unattended printing concerning the automatic       
56006 REM "307154-DBSPT-61747; Modify Auto Reorder Notice Email program with  
