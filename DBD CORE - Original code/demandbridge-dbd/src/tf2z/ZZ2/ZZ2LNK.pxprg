0010 ! <ZZ2LNK> Display Providex Link Files
0020 ! 129412, 8/6/2001  plh
0030 ! 
0035 REM "5.5 - 02/26/07 - 16.276388 - crg - SSP# 205460
0037 REM "205460-Oracle - FIN(CHANNEL,"NUMREC") changed to use FN%FIN$()     
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! 
0060 WINDX_FLAG=TCB(88)
0100 ! ^100
0110 DIM ALINE$(28,"-")
0120 PRINT 'DIALOGUE'(5,5,72,12,"LINK FILE SETTINGS",OPT="X")
0130 PRINT 'CS','SB'
0140 PRINT @(25,0),"Enter to Select File"
0141 PRINT @(25,2),"Arrows to Navigate Files"
0150 IF WINDX_FLAG=0 THEN PRINT @(25,3),"F4 to Quit" ELSE PRINT @(25,3),"Alt+F4 to Quit"
0160 PRINT @(1,5),ALINE$,"File Contents",ALINE$,'EB'
0170 FHDLE=FN%GET_FILES_AS_OPEN_FILE(%DATAPATH$,"*","N")
0180 FCNT$=FN%FIN$(FHDLE,"NUMREC")
0190 FCNT=NUM(FCNT$)
0191 ORIG_3D_STATE=PRM('3D')
0200 IF WINDX_FLAG>0 THEN PRINT '2D'
0211 PRINT @(1,0),"LINK FILE:"
0220 VARLIST_BOX 31999,@(14,0,10,5),OPT="s"
0230 VARLIST_BOX DISABLE 31999
0280 ACNT=0
0290 FOR X=1 TO FCNT
0300 READ (FHDLE,RNO=X,ERR=0460)
0310 FILENAME$=KEY(FHDLE,ERR=0460)
0320 IS_LNK=0
0330 IF LEN(FILENAME$)=2 THEN {
0340 CALL "ZZ2FNC;CHECK_LINK_NAME",FILENAME$,IS_LNK
0350  } ELSE {
0360 IS_LNK=0
0370  }
0380 IF IS_LNK=1 THEN {
0390 ACNT+=1
0410 VARLIST_BOX LOAD 31999,0,FILENAME$
0441  }
0450 NEXT 
0460 CLOSE (FHDLE,ERR=*NEXT)
0480 VARLIST_BOX ENABLE 31999
0490 VARLIST_BOX AUTO 31999
0570 DIM LNK_FILE$(2," ")
0580 DIM CMD$(60," ")
0590 DIM DEV$(12," ")
0600 APOS=1,OFFSET=0
0610 WHILE CTL<>4
0630 SET_FOCUS 31999
0640 PRINT 'CH'
0650 OBTAIN A$
0790 K=CTL(0,A$)
0800 IF K=-1999 OR K=4 THEN EXITTO 1050
0890 VARLIST_BOX READ 31999,LNK_FILE$,MODE$
0891 SHOW_FILE=0
0900 IF WINDX_FLAG=0 THEN {
0910 IF MODE$=$00$ THEN SHOW_FILE=1
0920  } ELSE {
0930 IF MODE$=$02$ OR MODE$=$0D$ THEN SHOW_FILE=1
0931  }
0940 IF SHOW_FILE>0 THEN GOSUB DISPLAY_FILE
1040 WEND 
1050 ! need to clear last ctl if user terminated from control box
1060 PRINT 'POP'
1070 IF ORIG_3D_STATE>0 THEN PRINT '3D'
1080 GOTO EXIT_ZZ2LNK
1090 DISPLAY_FILE:
1100 LF_HDLE=UNT
1110 OPEN (LF_HDLE,ISZ=256,ERR=1185)HWD+DLM+"nomads"+DLM+LNK_FILE$
1120 READ (LF_HDLE)A$
1130 CMD$=MID(A$,9,60)
1140 DEV$=MID(A$,69,12)
1150 PRINT @(1,6),'CF','SF'
1160 PRINT @(1,6),"Link File: "+LNK_FILE$
1170 PRINT @(1,7),"Cmd Line:  "+CMD$
1180 PRINT @(1,8),"Dev File:  "+DEV$,'EF'
1190 RETURN 
1200 EXIT_ZZ2LNK:
1210 END 
9999 END 
56002 REM "205460-Oracle - FIN(CHANNEL,"NUMREC") changed to use FN%FIN$()     
