0010 REM "PRINTER STATUS <ZZ2PST>
0020 SETESC 9300; SETERR 9000
0035 REM "5.1 - 10/31/02 - 17.253888 - kmc - SSP# 154750
0040 REM "Copyright 2002 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="ZZ2PST",X1$="Printer Status"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST A$
0320 IOLIST B$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O ZZ9...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0610 GOSUB 6000
0700 REM "Determine machine type and set Q_STAT$ to command to use
0705 SSTR$=SYS,Q_STAT$=""
0710 IF SSTR$="UNIX-AIX" THEN Q_STAT$="/usr/bin/enq -e -As -P {} | grep -v Queue | grep -v \- | cut -c15-23",E_COMM$="enable {}",D_COMM$="disable {}"; GOTO 0730
0730 REM "Resume here after setting Q_STAT$
1000 REM "Get list of printers
1005 DIM DEV$[500] ! SSP#151915
1009 REM "PRINT_START=first printer, PRINT_END=last printer
1010 I=0,PRINT_START=0,PRINT_END=0,PLIST$=""
1014 READ (Z[1],KEY="",DOM=*NEXT)
1015 READ (Z[1],END=1050)IOL=0310
1025 IF A$(43,1)<>"2" THEN GOTO 1015 ELSE IF PRINT_START=0 THEN PRINT_START=1,PRINT_END=1,DEV$[1]=A$,PLIST$=PLIST$+A$(1,2) ELSE PRINT_END+=1,DEV$[PRINT_END]=A$,PLIST$=PLIST$+A$(1,2)
1045 I=I+1; GOTO 1015
1100 REM "Display printer info
1105 FIRST_LINE=4,LAST_LINE=20,LAST_COL=1,INDEX=PRINT_START,CURR_LINE=FIRST_LINE,CURR_COL=1,COL_WIDTH=39,AT_END=0; GOSUB 7500
1110 IF INDEX>PRINT_END THEN GOTO 1150 ELSE IF CURR_LINE>LAST_LINE THEN IF CURR_COL<LAST_COL THEN CURR_COL=CURR_COL+1,CURR_LINE=FIRST_LINE ELSE GOSUB 7500
1112 SPOS=(CURR_COL-1)*COL_WIDTH
1115 GOSUB 7550; GOSUB 7600; GOSUB 7650; REM "Determine device in DEVICE$,and P_SPOOL and status in STATUS$, and STAT_ATTN, and DESC$
1119 PRINT @(0,20)," ",@(0+SPOS,CURR_LINE),DIM(79),@(1+SPOS,CURR_LINE),DEV$[INDEX](1,2),
1120 IF STAT_ATTN THEN PRINT @(6+SPOS,CURR_LINE),'BR',STATUS$,'ER', ELSE PRINT @(6+SPOS,CURR_LINE),STATUS$,
1125 PRINT @(15+SPOS,CURR_LINE),DESC$,
1130 IF SPOOLED=4 THEN PRINT @(44+SPOS,CURR_LINE),'SB',"FILE",'SF', ELSE IF SPOOLED=3 THEN PRINT @(44+SPOS,CURR_LINE),'SB',"HTML",'SF', ELSE IF SPOOLED=2 THEN PRINT @(44+SPOS,CURR_LINE),'SB',"Samba",'SF', ELSE IF SPOOLED=1 THEN PRINT @(44+SPOS,CURR_LINE),'SB',"Spool",'SF', ELSE PRINT @(44+SPOS,CURR_LINE),'SB',"Dev",'SF',
1134 PRINT @(50+SPOS,CURR_LINE),'SB',DEVICE$,'SF',
1145 CURR_LINE=CURR_LINE+1,INDEX=INDEX+1; GOTO 1110
1150 AT_END=1; GOSUB 7500; INDEX=PRINT_START; GOTO 1112
1900 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "Display TAG$ etc
6455 PRINT @(19-LEN(TAG$),21),'SB',TAG$,'SF',
6495 RETURN 
7500 REM "Start a new page
7505 IF AT_END OR CURR_LINE>FIRST_LINE THEN AT_END=0; CALL "ZZPROM","X0ZZ2PST",X3$,Z,"","","",0; ON Z GOTO 7515,9900,7506,7507,7508,7509,7515 ELSE GOTO 7515
7506 TASK$="E"; GOSUB 7800; GOTO 7515; REM "Enable a printer
7507 TASK$="D"; GOSUB 7800; GOTO 7515; REM "Disable a printer
7508 INDEX=PRINT_START; GOTO 7515; REM "Redisplay
7509 GOSUB 7700; REM "Start Display at; goto 7515
7515 CURR_LINE=FIRST_LINE,CURR_COL=1
7520 PRINT @(0,FIRST_LINE),'CE',
7523 FOR II=1 TO LAST_COL
7524 SPOS=(II-1)*COL_WIDTH
7525 PRINT @(0+SPOS,3),'SB',"Name",@(6+SPOS,3),"Status",@(15+SPOS,3),"Description",@(44+SPOS,3),"Type",@(50+SPOS,3),"Device",'SF',
7526 NEXT II
7549 RETURN 
7550 REM "Return device in DEVICE$, uses SPOOLED, MAX length 16
7553 DIM DEVICE$(30); DEVICE$(1)="Unknown",P_SPOOL=0
7555 IF STP(DEV$[INDEX](233,12))="tf_tofile" THEN SPOOLED=4,P_SPOOL=0,DEVICE$(1)=DEV$[INDEX](113,60); GOTO 7590
7556 IF STP(DEV$[INDEX](233,12))="tf_tohtml" THEN SPOOLED=3,P_SPOOL=0,DEVICE$(1)=DEV$[INDEX](113,60); GOTO 7590
7557 IF DEV$[INDEX](52,1)="Y" THEN SPOOLED=2,P_SPOOL=0,DEVICE$(1)=DEV$[INDEX](113,60); GOTO 7590
7558 IF DEV$[INDEX](173,1)=">" AND POS("lp"=DEV$[INDEX](173,60))<>0 THEN SPOOLED=1,P_SPOOL=1 ELSE SPOOLED=0,P_SPOOL=0
7560 IF SPOOLED=0 THEN IF DEV$[INDEX](233,12)<>DIM(12) THEN DEVICE$(1)=DEV$[INDEX](233,12),P_SPOOL=0; GOTO 7590 ELSE DEVICE$(1)=DEV$[INDEX](173,60),P_SPOOL=0; GOTO 7590
7565 TMP$=DEV$[INDEX](173,60)
7566 IF TMP$(1,3)<>">lp" AND TMP$(1,4)<>"> lp" THEN GOTO 7570; REM "If lp then 7567-7569 determine printer name
7567 P_SPOOL=1,TMPP=POS("-d"=TMP$); IF TMPP=0 THEN TMPP=POS("-P"=TMP$) END_IF ; IF TMPP=0 THEN DEVICE$=TMP$; GOTO 7590; REM "If can't find -d or -P then give up
7568 TMP$=STP(TMP$(TMPP+2),2); TMPP=POS(" "=TMP$); IF TMPP>1 THEN DEVICE$(1)=TMP$(1,TMPP-1); GOTO 7590 ELSE DEVICE$(1)=TMP$; GOTO 7590; REM "strip off up to -d or -P and then get next 'word' and leave
7570 IF POS("FX2.ss"=TMP$)<>0 THEN DEVICE$(1)="VSI-Fax"; GOTO 7590
7572 IF POS("rfgrab"=TMP$)<>0 THEN DEVICE$(1)="Reform Test"; GOTO 7590
7574 IF POS("rfserver"=TMP$)<>0 THEN TMPP=POS("-f"=TMP$); IF TMPP=0 THEN DEVICE$(1)="Reform Server"; GOTO 7590 ELSE DEVICE$(1)="Reform:"+STP(TMP$(TMPP+2),2); GOTO 7590
7576 TMPP=POS("tf2z/USR/file_out"=TMP$); IF TMPP<>0 THEN DEVICE$(1)="Disk:"+STP(TMP$(TMPP+18),2); GOTO 7590
7589 DEVICE$(1)=TMP$; REM "If all else fails,just show it
7599 RETURN 
7600 REM "Return STATUS$
7605 DIM STATUS$(8); STAT_ATTN=0
7606 PRINT @(1,21),"Checking status for: "+DEV$[INDEX](1,2)+"-"+DEV$[INDEX](3,40)
7607 IF STP(DEV$[INDEX](233,12))="tf_tohtml" OR STP(DEV$[INDEX](233,12))="tf_tofile" THEN STATUS$(1)="READY"; GOTO 7645 ! 154750                                                                  
7610 IF P_SPOOL THEN GOTO 7615; REM "If spooled determine status another way, else just check device to see if locked
7612 STATUS$(1)="READY"; CLOSE (14); OPEN (14,ERR=7613)DEV$[INDEX](1,2); CLOSE (14); GOTO 7645
7613 ENUM=ERR; IF ENUM=0 THEN STATUS$(1)="BUSY"; GOTO 7645 ELSE STATUS$(1)="ERR:"+STR(ENUM),STAT_ATTN=1; GOTO 7645
7615 REM "Check status of spooled device, via the spooler
7620 P0=POS("{}"=Q_STAT$); IF P0=0 THEN GOTO 7640 ELSE COMM$=Q_STAT$(1,P0-1)+STP(DEVICE$,1)+" "+Q_STAT$(P0+2)
7625 SLOT=UNT; OPEN (SLOT,ERR=7640)"< "+COMM$; READ (SLOT)STATUS$; CLOSE (SLOT)
7630 IF STATUS$(1,4)="DOWN" THEN STAT_ATTN=1
7632 IF POS("Invalid"=STATUS$)<>0 THEN STATUS$="BAD NAME",STAT_ATTN=1
7639 IF LEN(STATUS$)>8 THEN STATU$=STATUS$(1,8)
7645 PRINT @(0,21),'CL',
7649 RETURN 
7650 REM "Set DESC$ from ZZ9
7655 DIM DESC$(28),ZZ9$(44)
7670 DESC$(1)=DEV$[INDEX](3,40)
7699 RETURN 
7700 REM "Start Display at
7705 TAG$="Start Display At:"
7710 GOSUB 7750
7715 IF P_INDEX<>0 THEN INDEX=P_INDEX
7749 RETURN 
7750 REM "Given TAG$, get a printer name and set P_INDEX. if P_INDEX is 0, then none chosen
7755 GOSUB 6450; P_INDEX=0
7758 DIM A[1],A$(2)
7760 CALL "ZZENTR","SUX",A{ALL},A$,X4$,X3$,20,21,1,2,C0,"","{3"+X$,"","ZZ2PST00","ZZ9","ZZ2PEA",""; IF ABS(C0)>4 THEN GOSUB 6400; GOSUB 6450; ON C0 GOTO 7760,7761
7765 IF A$(1,2)=DIM(2) THEN GOTO 7795 ELSE P0=POS(A$=PLIST$,2); IF P0=0 THEN GOTO 7760 ELSE P_INDEX=(P0-1)/2+PRINT_START
7795 PRINT @(0,20),'CE',
7799 RETURN 
7800 REM "Based on TASK$ (E=enable, D=disable), choose printer and do task, if no printer specified, then ok.
7805 IF TASK$="E" THEN TAG$="Enable Printer:" ELSE IF TASK$="D" THEN TAG$="Disable Printer:" ELSE GOTO 7849
7810 GOSUB 7750; IF P_INDEX=0 THEN GOTO 7845
7815 INDEX=P_INDEX; GOSUB 7550; REM "Set index and get device name & type
7820 IF P_SPOOL THEN GOTO 7821 ELSE TMP$="Printer "+A$+" is not a spooled printer"; CALL "ZZPROM",".4",X3$,Z,TMP$,"","",0; GOTO 7810
7825 IF TASK$="E" THEN COMM$=E_COMM$ ELSE COMM$=D_COMM$
7830 P0=POS("{}"=COMM$); IF P0<>0 THEN COMM$=COMM$(1,P0-1)+STP(DEVICE$,1)+" "+COMM$(P0+2)
7835 INVOKE COMM$
7849 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F$
8115 PRINT @(0,7),"There are "+STR(T)+" records to process"
8129 REM "Set T0, we make sure T0 is > 1, because later on we MOD and look for avalue of 1. IF T0 is 1, then nothing would get reported. We look for a result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T0'th record to get the first report
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,C
8195 RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9515 GOSUB 6450; GOTO 7760
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
