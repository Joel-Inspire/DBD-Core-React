0010 REM "Open/Write/Close Log File <ZZ2LOG>                                     
0035 REM "5.7 - 10/20/14 - 15.1925 - dmm - SSP# 270466
0037 REM "270466-Unattended printing log gets to the maximum size and stops  
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0190 ! 
0500 INIT:ENTER LOG_LEVEL
0510 %LOG_DEBUG=-1 ! 245262
0520 %LOG_INFO=1
0530 %LOG_WARN=2
0540 %LOG_ERROR=3
0550 %LOG_LEVEL=LOG_LEVEL
0690 EXIT 
0695 ! 
1000 OPEN_LOG:ENTER X3$,X4$,LOG_FILE$,OPTIONS$,LOG_CHANNEL
1005 SETERR OPEN_ERROR
1010 LOG_DIR$=%DATAPATH$+DLM+"log"+DLM
1020 LOG_CHAN=HFN; OPEN (LOG_CHAN,ERR=*NEXT)LOG_DIR$; CLOSE (LOG_CHAN); GOTO DIR_EXISTS
1025 DIRECTORY LOG_DIR$
1040 DIR_EXISTS:
1050 IF POS("N"=OPTIONS$)>0 THEN ERASE LOG_DIR$+LOG_FILE$,ERR=*NEXT
1060 IF POS("N"=OPTIONS$)>0 THEN GOTO CREATE_NEW
1070 IF POS("A"=OPTIONS$)>0 THEN TEST_OPEN=HFN; OPEN (TEST_OPEN,ERR=CREATE_NEW)LOG_DIR$+LOG_FILE$; CLOSE (TEST_OPEN); GOTO OPEN_FILE ! If append option, see if we have the file already, if we can open it, then close it and go to the open logic, if not then assume we need to create it by going to the create logic.
1100 CREATE_NEW:
1110 SERIAL LOG_DIR$+LOG_FILE$
1200 OPEN_FILE:
1210 LOG_CHANNEL=HFN; OPEN LOCK (LOG_CHANNEL)LOG_DIR$+LOG_FILE$
1390 EXIT 
1400 OPEN_ERROR:
1490 EXIT ERR
1495 ! 
2000 LOG_MSG:ENTER X3$,X4$,LOG_CHANNEL,LOG_MSG$,LOG_LEVEL
2005 SETERR LOG_MSG_ERROR
2030 BEG_MSG$=DTE(0:"%Y-%Mz-%Dz|%Hz:%mz:%sz")+"|"+X3$(40,3)+"|"+FID(0)+"|"+WHO+"|"
2040 PRINT (LOG_CHANNEL)BEG_MSG$+LOG_MSG$
2390 EXIT 
2400 LOG_MSG_ERROR:
2410 ERRNO=ERR; IF ERRNO=278 AND FIN(LOG_CHANNEL,"FILELENGTH")>="2147483648" THEN LOG_CHANNEL=FN%TEXT_FILE_ROTATE(LOG_CHANNEL); RETRY ! SSP270466, file has hit limit
2490 EXIT ERR
2495 ! 
5000 CLOSE_LOG:ENTER X3$,X4$,LOG_CHANNEL
5020 CLOSE (LOG_CHANNEL,ERR=*NEXT)
5490 EXIT 
5495 ! 
9999 END 
