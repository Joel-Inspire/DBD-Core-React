0010 ! ZZ2WDX - Windx and GUI related functions
0020 ! "5.1 - 07/17/03 - 11.532222 - jir
0030 ! "Copyright 2003 Computer Software Inc.; Norcross, Georgia
0035 REM "5.7 - 03/16/18 - 13.020833 - crg - SSP# 263584
0037 REM "263584-Providex version upgrade from Pvx 7.1 to PxPlus 2017        
0040 REM "Copyright 2018 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0080 IF TCB(88)=0 THEN MSGBOX "You must be using Windx to access this program"; IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0099 ! 
0100 REM "Update Windx installation with application files
0105 ENTER *,ERR=*NEXT
0107 INI$=""; GOSUB GET_INI_INFO; CALL "[wdx]*inifile;read",ERR=*NEXT,INI$,"DB Distributor","SuppressFileCheck","",VAL$; IF POS(UCS(VAL$)="1Y")>0 THEN GOTO 0111
0110 GOSUB CHECK_WINDX_FILES
0111 %ZZ2WDX_SYNC_FILES=1
0115 SETERR 0120; EXIT 
0120 RUN "ZMENU"
0199 END 
0999 ! 
1000 REM "Initialize GUI environment when launching from CHUI mode
1005 GUI_INIT:
1010 IF NOT(NUL(%SV_DBD_SFONT$)) THEN %SV_DBD_SFONT++; GOTO 1995
1020 %SV_DBD_SFONT$=FIN(0,"STDFONT")
1050 IF NOT(%GUI) THEN CHUI_MODE=1; %GUI=1 ! PRINT 'SHOW'(-1) ! 263584 
1200 GOSUB GET_FONT_INFO
1240 PRINT (0)'OPTION'("STDFONT",FONTNAME$+",-13"),'ER',
1995 EXIT 
1999 ! 
2000 REM "Reset environment before leaving GUI panel and returning to CHUI mode
2005 GUI_WRAPUP:
2010 IF %SV_DBD_SFONT>0 THEN %SV_DBD_SFONT--; GOTO 2995
2030 IF NUL(%SV_DBD_SFONT$) THEN GOTO 2995
2040 PRINT (0)'OPTION'("STDFONT",%SV_DBD_SFONT$),'DF',
2050 IF CHUI_MODE THEN CHUI_MODE=0; %GUI=0; PRINT 'SHOW'(3)
2100 %SV_DBD_SFONT$="",%SV_DBD_SFONT=0
2995 EXIT 
2999 ! 
5000 CHECK_WINDX_FILES:
5005 PRINT 'WINDOW'(5,5,35,5,"Checking Files",'CS'+'_WHITE'+'BLACK'),'SF',@(1,0),'CS',"Checking...."
5010 GOSUB CHECK_VISUAL_EFFECT; IF %RB_TIP$<>"0" AND %RB_TIP$<>"" THEN RB_COLOR$=%RB_TIP$ ! GOSUB SEL_TIP_COLOR
5020 IF DEC(MID(MSE,22,1))>3 THEN WINDX=1,WDX$="[wdx]" ELSE WINDX=0,WDX$=""
5030 PRINT @(1,0),'CS',"Find Windx Location",
5040 CALL WDX$+"*windx.utl;get_windx",LOCATION$
5042 XX=POS("\"=LOCATION$,-1); IF XX<>0 THEN LOCATION$=LOCATION$(1,XX-1) ! First time, one level
5044 XX=POS("\"=LOCATION$,-1); IF XX<>0 THEN LOCATION$=LOCATION$(1,XX-1) ! Second time, two levels up
5049 ! 
5050 CN12=HFN; OPEN (CN12)"../lib/_bmp/dbsync.txt"; READ (CN12)HED$
5060 CN13=HFN; OPEN (CN13,ERR=*NEXT)"[wdx]"+LOCATION$+"/lib/_bmp/dbsync.txt"; READ (CN13)HED1$
5070 CLOSE (CN13),(CN12)
5075 IF HED$<=HED1$ THEN NO_COPY=1; GOTO DONE_CHECK_WINDX_FILES
5079 ! 
5099 ! 
5100 DIRECTORY "[WDX]"+LOCATION$+"\windx",ERR=*NEXT
5102 DIRECTORY "[WDX]"+LOCATION$+"\windx\topform",ERR=*NEXT
5104 DIRECTORY "[WDX]"+LOCATION$+"\lib",ERR=*NEXT
5106 DIRECTORY "[WDX]"+LOCATION$+"\lib\_bmp",ERR=*NEXT
5108 DIRECTORY "[WDX]"+LOCATION$+"\lib\_ext",ERR=*NEXT
5110 DIRECTORY "[WDX]"+LOCATION$+"\lib\_ext\system",ERR=*NEXT
5112 WINDXPLUGIN$=LOCATION$
5114 TOPFORMDIR$=LOCATION$+"\windx\topform"
5116 WORDDIR$=LOCATION$+"\lib"
5118 BMPDIR$=LOCATION$+"\lib\_bmp"
5120 MSGBOXDIR$=LOCATION$+"\lib\_ext\system"
5129 ! 
5130 SERVERDIR$="../lib/*bmp"
5140 SELECT NAME$ FROM SERVERDIR$ WHERE LEN(NAME$)>2
5150 PRINT @(1,0),'CS',"Copy File:"+NAME$,
5200 SWITCH NAME$
5210 ! WINDXPLUGIN$
5220 CASE "TFWORDLINK","db_logo.ico"
5230 WHERE$=WINDXPLUGIN$
5240 BREAK
5250 ! TOPFORMDIR$
5260 CASE "db_logo.bmp","il_logo.htm","il_logo.jpg","nofound.bmp"
5270 WHERE$=TOPFORMDIR$
5280 BREAK
5290 ! WORDDIR$
5300 CASE "_CMDWORD","_OCX"
5310 WHERE$=WORDDIR$
5320 BREAK
5330 ! MSGBOXDIR$
5340 CASE "msgbox.gui"
5350 WHERE$=MSGBOXDIR$
5360 BREAK
5369 ! BMPDIR$
5370 DEFAULT 
5380 WHERE$=BMPDIR$
5389 ! 
5390 END SWITCH 
5400 GOSUB TRANSFER_FILE
5410 PRINT " Done"
5420 NEXT RECORD 
5450 DONE_CHECK_WINDX_FILES:
5480 PRINT (0,ERR=*NEXT)'POP',
5495 RETURN 
5499 ! 
5600 CHECK_VISUAL_EFFECT:
5610 _ZZPA=HFN; OPEN (_ZZPA)"ZZPARM"
5620 READ (_ZZPA,KEY="*"+%X3_OP_ID$+"visual*",DOM=*NEXT)%NOMAD_VISUAL_OVERRIDE,%NOMAD_OVERRIDE_FONT$,%RB_TIP$,%BCK_COLOR$,NOMAD_ACTV_FOLDER_COLORS$,%TAB_TEXT_COLOR$,BTN_FOCUS_COLOR$,BTN_OVER_COLOR$,BTN_FACE_COLOR$,%TAB_ALIGNMENT$,%PICTURE$,%PLAY_SHOW$
5630 IF NOMAD_ACTV_FOLDER_COLORS$<>"" THEN %NOMAD_ACTV_FOLDER_COLORS$=NOMAD_ACTV_FOLDER_COLORS$ ! ssp 229498
5640 IF %NOMAD_VISUAL_OVERRIDE=4 THEN EXECUTE "[WDX]SET_PARAM 'MX'=1" ELSE EXECUTE "[WDX]SET_PARAM 'MX'=0"
5650 IF NOT(NUL(BTN_FOCUS_COLOR$)) THEN PRINT 'OPTION'("BtnFocusHilight",BTN_FOCUS_COLOR$),
5660 IF NOT(NUL(BTN_OVER_COLOR$)) THEN PRINT 'OPTION'("BtnHoverHilight",BTN_OVER_COLOR$),
5670 IF NOT(NUL(BTN_FACE_COLOR$)) THEN PRINT 'OPTION'("BtnfaceClr",BTN_FACE_COLOR$),
5680 CLOSE (_ZZPA)
5695 RETURN 
5699 ! 
5700 TRANSFER_FILE:! Transfer IMAGE_SRC$ to IMAGE_DEST$
5710 IMAGE_SRC$=SERVERDIR$+"/"+NAME$
5720 IMAGE_DEST$=WDX$+WHERE$+"\"+NAME$
5730 IF NUL(IMAGE_SRC$) THEN GOTO TRANSFER_FILE_END ELSE IMG_SRC=HFN; OPEN (IMG_SRC,ISZ=512)IMAGE_SRC$
5740 ERASE IMAGE_DEST$,ERR=*PROCEED; SERIAL IMAGE_DEST$,ERR=*NEXT
5750 IMG_DEST=HFN; OPEN (IMG_DEST,ISZ=1,ERR=*NEXT)IMAGE_DEST$
5770 TRANSFER_BEGIN:
5775 READ RECORD (IMG_SRC,END=FINISH_TRANSFER)IMG$
5780 WRITE RECORD (IMG_DEST)IMG$; GOTO 5775
5785 FINISH_TRANSFER:CLOSE (IMG_DEST); CLOSE (IMG_SRC)
5790 TRANSFER_FILE_END:
5795 RETURN 
5799 ! 
8000 REM "Get client side information by querying Windx plugin
8005 GET_INI_INFO:
8010 REM "Get location of pvx.ini file used by Windx
8020 CALL "[wdx]*windx.utl;get_val","ARG(-1)",INI$
8045 RETURN 
8049 ! 
8050 REM "Get values of Font and size
8055 GET_FONT_INFO:
8060 IF NUL(INI$) THEN GOSUB GET_INI_INFO
8065 CALL "[wdx]*inifile;read",ERR=*NEXT,INI$,"Font","Name","Fixedsys",FONTNAME$,256
8070 CALL "[wdx]*inifile;read",ERR=*NEXT,INI$,"Font","Points","9",FONTSIZE$,256
8095 RETURN 
8099 ! 
9999 END 
56001 REM "238083-Ability to update Windx files from server in CHUI
56002 REM "243030-Panel issues when GUI panels are executed via CHUI-font size    
