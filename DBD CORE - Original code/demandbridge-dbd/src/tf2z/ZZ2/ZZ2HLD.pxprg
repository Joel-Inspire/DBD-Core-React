0010 REM "Hold files for processing - <ZZ2HLD>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 05/26/10 - 15.812222 - crg - SSP# 238788
0037 REM "238788-Issue with renaming files in Providex across filesystems    
0040 REM "Copyright 2010 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
1000 SEND_FULL_DIR:
1010 ENTER X3$,X4$,DT0$,DIRECTORY$
1020 FROM_DIR$=DIRECTORY$
1030 MSG$="ZZ2HLD|DIR|"+FROM_DIR$; MX=FN%TOD_LOG("INFO",MSG$,1,0)
1040 DEST_DIR$=STP(STP(MID(DT0$,62,60),2),1,DLM); IF NUL(DEST_DIR$) THEN GOTO ERR_DIR
1050 MSG$="ZZ2HLD|HOLDDIR|"+DEST_DIR$; MX=FN%TOD_LOG("INFO",MSG$,1,0)
1060 CMD_CHAN=UNT; OPEN (CMD_CHAN,ERR=*RETURN)FROM_DIR$
1070 NEXT_FILE:
1080 READ (CMD_CHAN,END=DIR_DONE)NEXT_FILE$; IF NEXT_FILE$="." OR NEXT_FILE$=".." THEN GOTO *SAME
1090 SOURCE_FILE$=FROM_DIR$+DLM+NEXT_FILE$
1100 MSG$="ZZ2HLD|SOURCEFILE|"+NEXT_FILE$; MX=FN%TOD_LOG("INFO",MSG$,1,0)
1110 SUCCESS$="N" ! 238788
1112 RENAME SOURCE_FILE$ TO DEST_DIR$+DLM+NEXT_FILE$,ERR=*NEXT; SUCCESS$="Y (RENAME)"; GOTO MOVE_DONE ! 238788
1114 INVOKE "mv -f "+SOURCE_FILE$+" "+DEST_DIR$+DLM+NEXT_FILE$,ERR=*NEXT; SUCCESS$="Y (UNIX mv)"; GOTO MOVE_DONE ! 238788
1119 MOVE_DONE:! 238788
1120 MSG$="ZZ2HLD|FILE MOVE|SUCCESS|"+SUCCESS$; MX=FN%TOD_LOG("INFO",MSG$,1,0)
1150 GOTO NEXT_FILE
1160 DIR_DONE:
1170 CLOSE (CMD_CHAN)
1180 MSG$="ZZ2HLD|DONE|"; MX=FN%TOD_LOG("INFO",MSG$,1,0)
1190 EXIT 
1200 ! 
1210 ERR_DIR:
1220 MSG$="ZZ2HLD|HOLDDIR|ERROR - NO DIR SPECIFIED"; MX=FN%TOD_LOG("WARN",MSG$,1,0)
1230 EXIT 
9999 END 
56001 REM "238788-Issue with renaming files in Providex across filesystems    
