0010 REM + PO6 Type L Conversion    <PO6CON>
0035 REM "5.5 - 04/05/07 - 9.860833 - mhe - SSP# 204694
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM + Begin Program Notes
0052 REM "The purpose of this program is to read through the PO6 file
0053 REM "and check to see if the type 'L' records have the pick
0054 REM "priority in them. If the records do not, then the default
0055 REM "pick priority value of '  ' (2 spaces) is placed after the
0056 REM "warehouse code.
0068 REM "
0070 REM "Previous record layout:
0072 REM "  PO6 type, warehouse, order number, order line number
0075 REM "
0080 REM "New record layout:
0082 REM "  PO6 type, warehouse, pick priority, order number, order line number
0099 REM + End Program Notes
0100 REM + Begin Program Initialization
0110 CLEAR 
0130 ENTER X3$,X4$,Q0$,Q1$,ERR=*NEXT
0140 SETERR ERROR_PROCESSING
0150 SETESC ESCAPE_PROCESSING
0200 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0 ! call ZZCOMP
0299 REM + End Program Initialization
0300 REM + Begin String and Variable Declaration
0305 X0$=PGN
0310 X1$="PO6 Type L Conversion"
0320 DIM Z[255]
0325 PICK_PRIORITY$="  " ! set the pick priority default value
0499 REM + End String and Variable Declaration
0500 REM + Begin Open Files
0510 Z$=""
0520 Z$+="01L PO6...  " ! PO6 File
0580 GOSUB OPEN_FILES ! open the files
0590 PO6_LFN=Z[1] ! set the PO6 lfn
0799 REM + End Open Files
0800 REM + Begin IOLISTs
0805 IOLIST PO6$ ! PO6 record
0999 REM + End IOLISTs
1000 REM + Begin Program Main Routine
1005 GOSUB DISPLAY_INFO ! display splash screen
1010 PO6_KEY$="L"
1012 READ (PO6_LFN,KEY=PO6_KEY$,ERR=ERROR_PROCESSING,DOM=*NEXT) ! read PO6
1020 REPEAT 
1025 PO6_KEY$=KEY(PO6_LFN,END=1295)
1030 READ (PO6_LFN,KEY=PO6_KEY$,ERR=ERROR_PROCESSING,DOM=*NEXT) ! read PO6
1040 IF PO6_KEY$(1,1)="L" THEN {! if a type 'L' record is found
1050 IF LEN(PO6_KEY$)<>18 THEN {! if the key is the old format
1060 NEW_PO6_KEY$=PO6_KEY$(1,5)+PICK_PRIORITY$+PO6_KEY$(6) ! build the new key
1070 WRITE (PO6_LFN,KEY=NEW_PO6_KEY$,ERR=ERROR_PROCESSING) ! write the record
1080 REMOVE (PO6_LFN,KEY=PO6_KEY$,ERR=ERROR_PROCESSING) ! remove the old record
1150  }
1160  }
1170 UNTIL PO6_KEY$(1,1)<>"L" ! loop until no more type 'L' records
1295 GOTO END_PROGRAM ! end
3000 DISPLAY_INFO:
3010 PRINT 'CS'
3015 PRINT @(1,2),"PO6 Type L Conversion"
3020 PRINT @(1,4),"The purpose of this program is to read through the PO6 file"
3025 PRINT @(1,5),"and check to see if the type 'L' records have the pick"
3030 PRINT @(1,6),"priority in them. If the records do not, then the default"
3035 PRINT @(1,7),"pick priority value of '  ' (2 spaces) is placed after the"
3040 PRINT @(1,8),"warehouse code."
3045 PRINT @(1,10),"Previous record layout:"
3050 PRINT @(1,11)," PO6 type, warehouse, order number, order line number"
3055 PRINT @(1,13),"New record layout:"
3060 PRINT @(1,14)," PO6 type, warehouse, pick priority, order number, order line number"
3095 RETURN 
9000 ERROR_PROCESSING:
9001 REM "ERROR PROCESSING Routine
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,EXIT_PROGRAM,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR ERROR_PROCESSING; RETRY 
9090 SETERR 0000; RETRY 
9100 TRANSFER_CONTROL:
9101 REM " TRANSFER CONTROL
9120 ON Y8 GOTO END_PROGRAM,0990,9920
9180 GOTO 0990
9190 GOTO EXIT_PROGRAM
9300 ESCAPE_PROCESSING:
9301 REM "Escape Processing Routine
9305 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR ERROR_PROCESSING; SETESC ESCAPE_PROCESSING; RETURN 
9500 CTRL_LOGIC:
9501 REM "Control Logic
9510 SETERR ERROR_PROCESSING; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9740 RETURN 
9750 OPEN_FILES:
9751 REM "Open Files Routine
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9790 RETURN 
9800 EXIT_PROGRAM:
9801 REM "EXIT PROGRAM
9900 END_PROGRAM:
9901 REM "END PROGRAM
9905 Y8=2
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM + Modification History
56001 REM " 02/09/2007 : MHE : Program created for SSP 204694
56002 REM "204694-Program Exception (047, FM2RBC-0996) when in Unattended     
