0010 REM "Check director files against Master file definitions <UT2MFC>
0020 SETESC 9180; SETERR 9000
0030 ! 
0035 REM "5.5 - 02/26/07 - 16.315 - crg - SSP# 205460
0037 REM "205460-Oracle - FIN(CHANNEL,"NUMREC") changed to use FN%FIN$()     
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "        Licensed Software - All Rights Reserved.
0060 CLEAR ; SETERR 0070; ENTER X3$,X4$,Q0$,Q1$
0070 SETERR 9000
0080 X0$="UT2MFC",X1$="Check directory files against Master file definitions <UT2MFC>"
0090 K0=20,K1=1
0100 C9=-1
0110 COL=(80-LEN(X1$))/2
0120 DIM S$(COL," ")
0130 TYPE$="ISD"
0140 DOMAX=0
0300 ! ^ 300 "IOLISTS
0310 IOLIST KEY$:[LEN(6)],DESC$:[LEN(39)],AZERO$:[LEN(1)],FILETYPE$:[LEN(1)],MKL$:[LEN(2)],MDL$:[LEN(4)],MRN$:[LEN(4)],SOURCE$:[LEN(6)],UNK_NUM1$:[LEN(3)],UNK_NUM2$:[LEN(3)]
0320 IOLIST FILENAME$:[LEN(6)],ERR_DESC$:[LEN(20)],DESC$:[LEN(39)],FILETYPE$:[LEN(1)],MKL$:[LEN(2)],MDL$:[LEN(4)],MRN$:[LEN(4)],ZFILETYPE$:[LEN(1)],ZMKL$:[LEN(2)],ZMDL$:[LEN(4)],ZMRN$:[LEN(4)],SOURCE$:[LEN(6)],UNK_NUM1$:[LEN(3)],UNK_NUM2$:[LEN(3)]
0330 IOLIST FILENAME$:[LEN(6)],SP1$:[LEN(2)],EDESC$:[LEN(20)],SP2$:[LEN(2)],DESC$:[LEN(20)],SP3$:[LEN(2)],FILETYPE$:[LEN(2)],SP4$:[LEN(2)],MKL$:[LEN(2)],SP5$:[LEN(2)],MDL$:[LEN(4)],SP6$:[LEN(2)],MRN$:[LEN(4)]
0340 IOLIST FILENAME$:[LEN(6)],SP1$:[LEN(2)],EDESC$:[LEN(20)],SP2$:[LEN(2)],DESC$:[LEN(20)],FILETYPE$:[LEN(4)],SP4$:[LEN(1)],MKL$:[LEN(3)],SP5$:[LEN(2)],MDL$:[LEN(4)],SP6$:[LEN(2)],MRN$:[LEN(4)]
0500 ! ^ 500 " open FILES
0510 H_ZZE=UNT
0520 OPEN (H_ZZE,ERR=*NEXT)"ZZE"+%LIC_COMP$; GOTO 0550
0530 OPEN (H_ZZE,ERR=*NEXT)"ZZE"; GOTO 0550
0540 PRINT "COULD NOT OPEN DISK MASTER FILE"; GOTO 9900
0600 ! ^ 600 DISPLAY PANEL
0610 GOSUB DISPLAY_DIALOG_6800
0620 GOSUB GET_DATA_6900 ! get DIRPATH$ and FILE_MASK$ values
0630 MSGBOX "Check Maximum Record Length?","Config File Check","YESNOCANCEL,2",MSG_ANS$
0640 IF MSG_ANS$="YES" THEN DOMAX=1 ELSE DOMAX=0
0650 IF MSG_ANS$="CANCEL" THEN GOTO 5000
0700 ! ^ 700
0710 FHDLE_IN=FN%GET_FILES_AS_OPEN_FILE(DIRPATH$,FILE_MASK$,"N")
0720 IF FHDLE_IN=0 THEN {
0730 PRINT "NO FILES TO CHECK"
0740 GOTO 9900
0750  }
0760 FHDLE_OUT=UNT
0770 OPEN (FHDLE_OUT,IOL=0320)"*Memory*"
0780  }
0900 ! ^ 900 Position Read
0910 READ (FHDLE_IN,IND=0,ERR=*NEXT)
1000 ! ^ 1000  MAIN
1010 MAIN_1000:
1100 ! ^ 1100 Read Next File from Memory file
1110 READ (FHDLE_IN,END=MAIN_EXIT)K$
1120 GOSUB GET_FILE_PARAMETERS_6000
1130 IF FTYPE>3 THEN GOTO MAIN_1000
1150 ! ^ 50 Get Disk Master File Information
1160 READ (H_ZZE,KEY=MID(K$,1,3)+"   ",DOM=*NEXT)IOL=0310; GOTO FILE_OPENED_1300
1170 ! Clear workspace vars
1180 ! DIM OUTREC$:IOL=0320
1190 ! ERROR_DESC$="Missing"
1200 ! GOSUB LOAD_OUTPUT_RECORD_6500
1210 ! GOSUB WRITE_RECORD_6700
1220 ! PRINT @(10,13),"File       ",'CE',K$,@(35,13),"ERRORS: ",ERROR_DESC$
1230 GOTO MAIN_LOOP
1300 ! ^ 100
1310 FILE_OPENED_1300:
1320 ! Clear workspace vars
1330 DIM OUTREC$:IOL=0320
1340 GOSUB COMPARE_PARAMETERS_6300
1350 PRINT @(10,13),'CL',"File       ",'CE',K$,@(30,13),"ERRORS: ",ERROR_DESC$
1360 IF POS(" "<ERROR_DESC$)>0 THEN {
1370 GOSUB LOAD_OUTPUT_RECORD_6500
1380 GOSUB WRITE_RECORD_6700
1390  }
1400 MAIN_LOOP:
1410 GOTO MAIN_1000
1420 MAIN_EXIT:
1430 CLOSE (FHDLE_IN,ERR=*NEXT)
2000 ! ^ 2000 DISPLAY - File Errs are in memory file
2010 DIM UNDERLINE$(79,"-")
2020 DOLIST=0
2030 GOSUB LOAD_TITLE_LINE1
2040 GOSUB LOAD_TITLE_LINE2
2050 FCNT$=FN%FIN$(FHDLE_OUT,"NUMREC")
2060 FCNT=NUM(FCNT$,ERR=*NEXT)
2070 IF FCNT=0 THEN GOTO 5000
2080 READ (FHDLE_OUT,IND=0,ERR=*NEXT)
2090 PRINT 'CS'
2100 REC_CNT=5
2110 OFFSET=5
2120 FOR X=1 TO FCNT STEP REC_CNT
2130 PRINT @(COL,1),'SB',X1$
2140 PRINT @(1,2),TLINE1$
2150 PRINT @(1,3),TLINE2$
2160 PRINT @(1,4),UNDERLINE$
2170 FOR X1=1 TO REC_CNT
2180 READ (FHDLE_OUT,END=*NEXT)IOL=0320
2190 GOSUB LOAD_PRTLINE1
2200 PRINT @(1,OFFSET+X1*3-2),'SF',PRTLINE$
2210 GOSUB LOAD_PRTLINE2
2220 PRINT @(1,OFFSET+X1*3-1),PRTLINE$
2230 PRINT @(1,OFFSET+X1*3),UNDERLINE$
2240 NEXT X1
2250 PRINT "<<Any Key to Continue, F2 to List, F4 to Quit >>"
2260 IF DOLIST=0 THEN {
2270 OBTAIN RET$
2280 IF CTL=4 THEN EXITTO 5000
2290 IF CTL=2 THEN DOLIST=1
2300  }
2310 PRINT 'CF'
2320 NEXT X
3000 ! ^ 3000 - Text File
3010 TEXTFILE$="tmpFChk.txt"
3020 PRINT @(10,13),"Creating Text File "+TEXTFILE$
3030 ERASE DIRPATH$+DLM+TEXTFILE$,ERR=*NEXT; GOTO *SAME
3040 SERIAL DIRPATH$+DLM+TEXTFILE$
3050 READ (FHDLE_OUT,IND=0,ERR=*NEXT)
3060 H_PRT=UNT
3070 OPEN PURGE (H_PRT,ERR=5000)DIRPATH$+DLM+TEXTFILE$
3080 REC_CNT=20
3090 FOR X=1 TO FCNT STEP REC_CNT
3100 PRINT (H_PRT)S$,X1$
3110 PRINT (H_PRT)TLINE1$
3120 PRINT (H_PRT)TLINE2$
3130 PRINT (H_PRT)UNDERLINE$
3140 FOR X1=1 TO REC_CNT
3150 READ (FHDLE_OUT,END=PRT_EXIT)IOL=0320
3160 GOSUB LOAD_PRTLINE1
3170 PRINT (H_PRT)PRTLINE$
3180 GOSUB LOAD_PRTLINE2
3190 PRINT (H_PRT)PRTLINE$
3200 PRINT (H_PRT)UNDERLINE$
3210 NEXT X1
3220 PRINT (H_PRT)'FF'
3230 NEXT X
3240 PRT_EXIT:
3250 CLOSE (H_PRT,ERR=*NEXT)
5000 ! ^ 5000 EOJ
5010 CLOSE (FHDLE_OUT,ERR=*NEXT)
5020 GOTO 9900
6000 ! ^ 6000
6010 GET_FILE_PARAMETERS_6000:
6020 LOCAL H_TMP
6030 USEDREC=0,MAXREC=0,KEYSIZE=0,BYTES=0
6040 H_TMP=UNT
6050 CLOSE (H_TMP,ERR=*NEXT)
6060 OPEN (H_TMP,ERR=GET_FILE_PARAMETERS_6000_EXIT)K$
6070 F$=FID(H_TMP,ERR=GET_FILE_PARAMETERS_6000_EXIT)
6080 IF LEN(F$)=2 THEN FTYPE=13 ELSE FTYPE=ASC(MID(F$,10,1))
6090 IF FTYPE>3 THEN GOTO GET_FILE_PARAMETERS_6000_EXIT
6100 USEDREC=NUM(FN%FIN$(H_TMP,"NUMREC"),ERR=*NEXT)
6110 MAXREC=DEC(MID(F$,12,3))
6120 KEYSIZE=ASC(F$(11,1))
6130 BYTES=DEC(F$(15,2))
6140 GET_FILE_PARAMETERS_6000_EXIT:
6150 CLOSE (H_TMP,ERR=*NEXT)
6160 RETURN 
6200 ! ^ 100
6210 COMPARE_PARAMETERS_6300:
6220 ! IN DECLINING ORDER OF SEVERITY
6230 ERROR_DESC$=""
6240 IF FTYPE<0 OR FTYPE>2 THEN GOTO 6550
6250 IF FTYPE=1 AND FILETYPE$="T" THEN {
6260 FTYPE$="T"
6270 GOTO 6340
6280  }
6290 DIM TLETTER$(1," ")
6300 TLETTER$=TYPE$(FTYPE+1,1)
6310 IF TLETTER$=UCS(FILETYPE$) THEN GOTO 6340
6320 IF FTYPE=2 AND UCS(FILETYPE$)="S" THEN GOTO 6340
6330 ERROR_DESC$="FILE TYPE, "
6340 IF KEYSIZE<>NUM(MKL$,ERR=*NEXT) THEN ERROR_DESC$+="KEY SIZE, "
6350 IF UCS(FLILETYPE$)<>"S" THEN {
6360 IF DOMAX=1 AND MAXREC>NUM(MRN$,ERR=*NEXT) AND MAXREC<>0 THEN ERROR_DESC$+="REC CNT, "
6370 IF BYTES<>NUM(MDL$,ERR=*NEXT) THEN ERROR_DESC$+="DATA SIZE, "
6380 IF FTYPE=0 THEN FTYPE$="I" ELSE FTYPE$="D"
6390  } }
6400 IF UCS(FILETYPE$)="S" THEN {
6410 IF BYTES=0 THEN GOTO 6540
6420 ! IF UCS(FILETYPE$)="S" AND POS("9"<MDL$)>0 THEN {
6430 ! DIM REF_FILE$:IOL=0310
6440 ! REF_KEY$=PAD(MDL$,6,1," ")
6450 ! READ (H_ZZE,KEY=REF_KEY$,ERR=6500,DOM=6500)REF_FILE$
6460 ! IF POS("0"<REF_FILE.MDL$)>0 THEN {
6470 ! REF_NUM=0
6480 ! REF_NUM=NUM(REF_FILE.MDL$,ERR=*NEXT)
6490 ! IF REF_NUM>0 AND BYTES<>REF_NUM THEN GOTO 6500
6500 ! } }
6510 ! GOTO 6510
6520 FTYPE$="S"
6530 ERROR_DESC$+="DATA SIZE, "
6540  }
6550 RETURN 
6600 ! ^ 100
6610 LOAD_OUTPUT_RECORD_6500:
6620 OUTREC.FILENAME$=K$
6630 OUTREC.ERR_DESC$=ERROR_DESC$
6640 OUTREC.FILETYPE$=FTYPE$
6650 OUTREC.MKL$=PAD(STR(KEYSIZE),2,0," ")
6660 OUTREC.MDL$=PAD(STR(BYTES),4,0," ")
6670 OUTREC.MRN$=PAD(STR(MAXREC),4,0," ")
6680 OUTREC.ZFILETYPE$=FILETYPE$
6690 IF MKL$<>"" THEN OUTREC.ZMKL$=PAD(STR(NUM(MKL$,ERR=*NEXT)),2,0," "); GOTO 6710
6700 OUTREC.ZMKL$=MKL$
6710 IF FTYPE<>1 THEN {
6720 IF MDL$<>"" THEN OUTREC.ZMDL$=PAD(STR(NUM(MDL$,ERR=*NEXT)),4,0," "); GOTO 6740
6730 OUTREC.ZMDL$=MDL$
6740 IF MRN$<>"" THEN OUTREC.ZMRN$=PAD(STR(NUM(MRN$,ERR=*NEXT)),4,0," "); GOTO 6760
6750 OUTREC.ZMRN$=MRN$
6760  }
6770 RETURN 
6800 ! ^ 100
6810 WRITE_RECORD_6700:
6820 WRITE (FHDLE_OUT)OUTREC$
6830 RETURN 
6900 ! ^ 100
6910 DISPLAY_DIALOG_6800:
6920 WDW_TITLE$=X1$,PATH$="*.*",DIR$=%DATAPATH$
6930 GET_FILE_BOX PATH$,DIR$,WDW_TITLE$
6940 IF NUL(PATH$) THEN GOTO 9900
6950 P=POS(DIR$=PATH$,1,0)
6960 IF P>1 THEN PATH$=MID(PATH$,POS(DIR$=PATH$,-1,1))
6970 P1=POS(DLM=PATH$,-1,1)
6980 DIRPATH$=MID(PATH$,1,P1-1)
6990 RETURN 
7000 ! ^ 100
7010 GET_DATA_6900:
7020 D_HDLE=HFN
7030 OPEN (D_HDLE,ERR=*NEXT)DIRPATH$; CLOSE (D_HDLE); GOTO 7050
7040 ESCAPE 
7050 PRINT 'CS',
7060 PRINT @(COL,1),X1$
7070 RETURN 
7100 ! ^ 100
7110 LOAD_PRTLINE1:
7120 DIM PRTLINE$:IOL=0330
7130 PRTLINE.FILENAME$=FILENAME$
7140 PRTLINE.EDESC$=ERR_DESC$
7150 PRTLINE.DESC$=MID(DESC$,1,20)
7160 PRTLINE.FILETYPE$=FILETYPE$
7170 PRTLINE.MKL$=MKL$
7180 PRTLINE.MDL$=MDL$
7190 PRTLINE.MRN$=MRN$
7200 RETURN 
7300 ! ^ 100
7310 LOAD_PRTLINE2:
7320 DIM PRTLINE$:IOL=0330
7330 PRTLINE.FILENAME$="ZZE"
7340 PRTLINE.EDESC$=""
7350 PRTLINE.DESC$=SOURCE$
7360 PRTLINE.FILETYPE$=ZFILETYPE$
7370 PRTLINE.MKL$=ZMKL$
7380 PRTLINE.MDL$=ZMDL$
7390 PRTLINE.MRN$=ZMRN$
7400 RETURN 
7500 ! ^ 100
7510 LOAD_TITLE_LINE1:
7520 DIM TLINE1$:IOL=0340
7530 TLINE1.FILENAME$="File"
7540 TLINE1.EDESC$=""
7550 TLINE1.DESC$=""
7560 TLINE1.FILETYPE$="File"
7570 TLINE1.MKL$="Key"
7580 TLINE1.MDL$="Byte"
7590 TLINE1.MRN$="Max"
7600 RETURN 
7610 LOAD_TITLE_LINE2:
7620 DIM TLINE2$:IOL=0340
7630 TLINE2.FILENAME$="Name"
7640 TLINE2.EDESC$="Errors Found"
7650 TLINE2.DESC$="Description"
7660 TLINE2.FILETYPE$="Type"
7670 TLINE2.MKL$="Sz"
7680 TLINE2.MDL$="/Rec"
7690 TLINE2.MRN$="Cnt"
7700 RETURN 
8000 ! ^ 8000 - Functions
8010 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 ! ^ 9000 "ERROR PROCESSING
9010 ESCAPE 
9900 ! ^ 9900 "END PROGRAM
9910 CLOSE (H_ZZE,ERR=*NEXT)
9920 CLOSE (FHDLE_IN,ERR=*NEXT)
9930 CLOSE (FHDLE_OUT,ERR=*NEXT)
9940 CLOSE (H_PRT,ERR=*NEXT)
9950 SETERR 9960; Q1$=A1$; EXIT 
9960 SETESC 9210
9970 END 
9999 END 
56002 REM "205460-Oracle - FIN(CHANNEL,"NUMREC") changed to use FN%FIN$()     
