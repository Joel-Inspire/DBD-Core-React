0010 REM "Update TopForm Program <UT2UTP>"
0020 SETESC 9300; SETERR 9000
0035 REM "4.0 - 07/23/96 - 22.46 - kmc
0040 REM "Copyright 1996 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0110 X0$="UT2UTP",X1$="Update TopForm Progam",K9$="",K9=0
0120 DIM Z0$(80,"-"),S0$(80),W[10]
0130 K0=2,K1=1
0135 C9=-1,V9=-2
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(65,1)>"2" THEN PRECISION NUM(X3$(65,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1)
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O ZW2... 03O ZW5...  04O ZZ9...  13O ZZPARM  "
0520 GOSUB 9750; ON Z0 GOTO 0521,9900
0600 REM "
0680 GOSUB 6000
1000 REM "
1030 DIM B0$(200),C$(200)
1035 DIM A$(82)
1040 A$(1)=A1$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 SETERR 9000; C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1450,1200,1250,1350,1400,1300,1500
1200 REM "Source Directory A$(1,30)
1202 A$(1,30)="dev",A$(31,30)="rel"
1205 IF C0<>2 AND POS(" "<>A$(1,30))<>0 THEN PRINT @(23,3),A$(1,30),; GOTO 1240
1210 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,23,3,1,30,C0,"","{1"+X$,"","UT2UTP"+"00","","","  H"; IF C0=6 THEN GOSUB 6400; C0=1
1212 IF INT(ABS(C0))=3 THEN C0=-ABS(C0); READ (Z[1],KEY=A$(1,2),DOM=1213)
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9800,6970
1240 GOTO 1120
1250 REM "Destination Directory A$(31,30)
1260 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,23,4,31,30,C0,"","{3"+X$,"","UT2UT"+"P02","","",""; IF C0=6 THEN GOSUB 6400; C0=1
1290 GOTO 1120
1300 REM "Program Name A$(61,8)
1310 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,23,7,61,8,C0,"","{3"+X$,"","UT2UTP"+"04","","",""; IF C0=6 THEN GOSUB 6400; C0=1
1312 IF A$(63,1)="2" THEN J$=A$(61,8); CALL "YUCASE",X3$,J$,J0$; A$(61,8)=J0$; PRINT @(23,7),A$(61,8),
1315 P5$=""; PRINT @(32,7),'CL',
1320 IF POS(" "<>A$(61,8))<>0 THEN CLOSE (14); CALL "ZZXPRG",P5$,14,A$(61,6),0,0
1324 IF LEN(P5$)>35 THEN P5$=P5$(1,35)
1325 PRINT @(32,7),P5$,
1340 GOTO 1120
1350 REM "W/O or SSP #:
1355 X$="To be used as a reference in the tflog"
1360 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,23,5,69,6,C0,"","{3"+X$,"","UT2U"+"TP06","ZW2","ZW2ZZZ",""; IF C0=6 THEN GOSUB 6400; C0=1
1365 IF POS(" "<>A$(69,6))=0 THEN DIM B0$(200); GOTO 1375
1370 FIND (Z[2],KEY=A$(69,6),DOM=1360)B0$
1375 PRINT @(31,5),B0$(53,45),
1390 GOTO 1120
1400 REM "OLD #
1405 X$="To be used as a reference in the tflog"
1410 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,23,6,75,5,C0,"","{3"+X$,"","UT2U"+"TP08","ZW5","ZW2DAA",""; IF C0=6 THEN GOSUB 6400; C0=1
1415 IF POS(" "<>A$(75,5))=0 THEN DIM C$(200); GOTO 1425
1420 FIND (Z[3],KEY=A$(75,5),DOM=1410)C$
1425 PRINT @(31,6),C$(80,45),
1440 GOTO 1120
1450 REM "Printer
1455 IF A$(80,2)="  " THEN A$(80,2)="LP"
1460 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,70,3,80,2,C0,"","{3"+X$,"","UT2UT"+"P22","","",""
1470 DIM C1$(52); FIND (Z[4],KEY=A$(80,2),DOM=1471)C1$
1480 IF C1$(44,8)="HPLASER " THEN A$(82,1)="Y" ELSE A$(82,1)=""
1490 GOTO 1120
1500 REM "
1505 CALL "ZZPROM","10",X3$,Z,"","","",0
1510 IF Z=0 THEN X0=4; GOTO 1140
2000 REM "Display work to do & get approval
2002 A1$=A$(1,60)
2003 PRINT @(0,9),'CE',
2005 GOSUB 8000; REM "Get path from source DIR to program in P0$
2010 REM "P1$ is source file, P2$ is destination, & P3$ is backup file
2015 P1$=FNS$(A$(1,30))+"/"+P0$+"/"+FNS$(A$(61,8))
2016 P2$=FNS$(A$(31,30))+"/"+P0$+"/"+FNS$(A$(61,8))
2017 P3$=FNS$(A$(31,30))+"/"+"archive"+"/"+FNS$(A$(61,8))+"."+X3$(21,6)+"."+STR(INT(TIM):"00")+STR(FPT(TIM)*100:"00")
2020 L1$="Print changes from "+P2$+" to "+P1$+" on LP."; PRINT @(0,9),L1$,
2022 L2$="Archive "+P2$+" to "+P3$+"."; PRINT @(0,10),L2$,
2024 L3$="Copy "+P1$+" to "+P2$+"."; PRINT @(0,11),L3$,
2025 L7=0; IF POS(" "<>A$(69,6))<>0 THEN L4$="Add lines to archive in "+P2$+" for SSP# "+A$(69,6)+"."; PRINT @(0,12+L7),L4$,; L7=L7+1 ELSE L4$=""
2026 IF POS(" "<>A$(75,5))<>0 THEN L5$="Add lines to archive in "+P2$+" for OLD# "+A$(75,5)+"."; PRINT @(0,12+L7),L5$,; L7=L7+1 ELSE L5$=""
2029 CALL "ZZPROM",".10",X3$,Z,"Is this what you wish to do?","","",0
2030 IF Z=1 THEN PRINT @(0,9),'CE',; X0=4; GOTO 1140
2050 M$=L1$; GOSUB 2100; REM "Print rev listing
2052 M$=L2$; GOSUB 2150; REM "Archive file
2054 M$=L3$; GOSUB 2200; REM "Copy new file into release
2056 IF L4$<>"" THEN M$=L4$; GOSUB 2250; REM "Update archives with SSP#
2058 IF L5$<>"" THEN M$=L5$; GOSUB 2300; REM "update archives with OLD#
2095 X0=5; GOTO 1140
2100 REM "Print out rev listing
2110 V0$=":"+FNS$(A$(1,30))+"/"+P0$,V1$=":"+FNS$(A$(31,30))+"/"+P0$,F0$=FNS$(A$(61,8))
2111 W0$="",V2$=A$(80,3)
2112 IF POS(" "<>A$(69,6))<>0 THEN W0$=W0$+" SSP# "+A$(69,6)
2113 IF POS(" "<>A$(75,5))<>0 THEN W0$=W0$+" OLD# "+A$(75,5)
2114 IF P3$<>"" THEN W0$=W0$+" archive should be "+P3$
2119 CLOSE (10)
2120 CALL "UT2REV",X3$,V0$,V1$,F0$,10,W0$,W1$,W2$,W3$,W{ALL},V2$
2125 CLOSE (10)
2130 GOSUB 8100; REM "M$ set in call
2140 CALL "ZZPROM",".5",X3$,Z,"Rev Listing complete","","",0
2141 IF Z=1 THEN EXITTO 2090; REM "Skip rest, and await further instructions
2145 RETURN 
2150 REM "Archive the old file
2155 PRINT @(0,9),'CE',@(15,15),L2$,
2160 C0$="cp "+P2$+" "+P3$
2165 INVOKE C0$
2180 GOSUB 8100; REM "M$ set before Gosub
2195 RETURN 
2200 REM 'COPY FILE INTO RELEASE
2205 PRINT @(0,9),'CE',@(15,15),L3$,
2210 C0$="cp "+P1$+" "+P2$
2230 INVOKE C0$
2240 GOSUB 8100; REM "M$ set before gosub here
2245 RETURN 
2250 REM "Update archive notes with SSP#
2255 PRINT @(0,9),'CE',@(15,15),L4$,
2260 M$="SSP# "+A$(69,6)+"  "+B0$(53,60)
2265 GOSUB 8100
2295 RETURN 
2300 REM "Update archive notes with OLD#
2305 PRINT @(0,9),'CE',@(15,15),L5$,
2310 M$="OLD# "+A$(75,5)+"  "+C$(80,60)
2315 GOSUB 8100
2345 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(5,3),"Source Directory:",@(60),"Printer:",@(0,4),"Destination "+"Directory:",@(9,7),"Program Name:",@(11,5),"W/O, SSP #:",@(16,6),"OLD"+" #:",@(0,8),Z0$,
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6390 RETURN 
6400 REM "WHOLE SCREEN
6402 IF C9=0 THEN IF ABS(C0)=17 THEN GOTO 6970
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 PRINT @(23,3),A$(1,30),@(23,4),A$(31,30),@(23,7),A$(61,8),@(23,5),A$(69,6),@(31,5),B0$(53,45),@(23,6),A$(75,5),@(31,6),C$(80,45),
6490 RETURN 
8000 REM "Get path from A$(1,30) DIR to A$(61,8) program
8005 J0$=FNS$(A$(1,30)),J1$=FNS$(A$(61,8)),J3$="TMP"+FID(0)
8010 J2$="bbfind "+J1$+" "+J0$+" > /usr/lib/basic8/UTILS/"+J3$
8015 PRINT @(15,20),"Searching for the program, one moment please..."
8020 ERASE J3$,ERR=8021; GOTO 8020
8025 INVOKE J2$
8030 CLOSE (14,ERR=8031)
8035 OPEN (14)J3$
8036 PRINT @(0,20),'CL',
8038 REM "Read in path, we assume we will get 1 or 0 (not found). If more t"+"han one, then we always get first one, give warning of more
8039 B$=""
8040 READ RECORD (14,END=8041)B$
8045 IF POS($00$<>B$)=0 OR LEN(B$)=0 THEN J4$="Program "+J1$+" not found in source DIRectory "+J0$+"!!"; CALL "ZZPROM",".4",X3$,0,J4$,"","",0; EXITTO 1100
8050 REM "Check to see if more than one entry in B$ by looking for program "+"name more than once.
8051 IF POS(J1$=B$(POS(J1$=B$)+LEN(J1$)))=0 THEN GOTO 8055; REM "check B$ after the first occurance of J1$ in B$
8052 J4$="WARNING - More than one program "+J1$+" exists in source DIRector"+"y "+J0$+"!!"
8053 CALL "ZZPROM",".4",X3$,0,J4$,"","",0
8060 REM "B$ should have source DIR on front and program name on back, remo"+"ve those, leaving just the path between intact.
8065 P0$=B$(1,POS(J1$=B$+J1$)-2); REM "Remove trailing '/' alsoe
8070 IF P0$(1,LEN(J0$))=J0$ THEN P0$=P0$(LEN(J0$)+2); REM "Remove leading "/" also
8090 PRINT @(0,20),'CE',
8095 RETURN 
8100 REM "Print a message (M$) to the archive log
8105 M0$=FND$(X3$(21,6))+" "+STR(TIM:"00.00")
8109 M8$=FNS$(A$(31,30))+"/"+"archive"+"/"+"tflog"
8110 M9$="echo "+QUO+M0$+QUO+" "+A$(61,8)+" $LOGNAME "+QUO+M$+QUO+" >>"+M8$
8125 INVOKE M9$
8145 RETURN 
8905 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
8910 DEF FND$(Z9$)=Z9$(3,2)+"/"+Z9$(5,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8920 DEF FNB$(Z9$)=X3$(214-6*NUM(Z9$),1)
8925 DEF FNS$(Z9$)=Z9$(1,POS(S0$=Z9$+S0$)-1)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9120 ON Y8 GOTO 9900,0990,9920
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9740 RETURN 
9750 REM "FILES
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9790 RETURN 
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9905 Y8=2
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
