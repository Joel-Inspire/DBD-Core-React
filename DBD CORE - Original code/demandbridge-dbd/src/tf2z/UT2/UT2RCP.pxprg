0010 REM UT2RCP - Release program compare
0090 BEGIN 
0510 OPEN (1)"ZWL"; OPEN (2)"ZWV"; OPEN (3)"ZV0"
1000 INPUT (0,LEN=10)"Enter Release Number: ",REL$
1010 IF REL$="" THEN BEGIN ; RUN "ZMENU"
1020 REL$=PAD(REL$,10)
1100 READ (2,KEY=REL$,KNO=1,DOM=*NEXT)
1200 KY2$=KEY(2,END=1900); IF KY2$(1,10)<>REL$ THEN GOTO 1900
1300 READ (2,KEY=KY2$)A2$
1310 CALLNUM$=A2$(1,6)
1320 GOSUB COMPARE_ISSUE
1500 GOTO 1200
1900 BEGIN ; RUN "ZMENU"
15000 COMPARE_ISSUE:
15030 SETERR 17845
15040 IOL_ZWL:IOLIST ZWL$
15060 READ (1,KEY="",ERR=*NEXT)
15070 NEXT_ZWL:
15080 COUNT=0,SEQ=0
15090 READ (1,END=DONE_ZWL)IOL=IOL_ZWL
15100 IF STP(ZWL$(12,6))<>CALLNUM$ THEN GOTO NEXT_ZWL
15110 ! 
15120 INFO$="Program compare utility"
15130 WK.LFN=HFN,PR.LFN=0
15140 WRK.1$="",WRK.2$=""
15150 X$=FIB(0),SV_MODE$=X$(21,3); PRINT 'CS' ! 'RM','_BLACK','WHITE','SN','DF','CS',
15160 SV_SD=PRM('SD'); SET_PARAM -'SD'
15170 ! 100 - First program definition
15180 ! IF LAST.1$<>"" THEN RESP$=LAST.1$
15190 QUEST$="Original program file/directory:"; GOSUB 17290
15200 IF RESP$="" THEN GOTO 17740 ELSE LAST.1$=RESP$
15210 OPEN INPUT (WK.LFN,ERR=15250)RESP$; A$=FIB(WK.LFN),PTH.1$=PTH(WK.LFN)
15220 CLOSE (WK.LFN); IF A$(19,1)="D" THEN GOTO 15400 ELSE IF A$(19,1)<>"P" THEN GOTO 15260
15230 OPEN INPUT (WK.LFN,ISZ=256,ERR=15250)RESP$; READ RECORD (WK.LFN)X$; CLOSE (WK.LFN); IF LEN(X$)<>256 THEN GOTO 15260
15240 INFO$="Original program file:"+PTH.1$; GOTO 15280
15250 INFO$=MSG(RET); PRINT 'RB',; GOTO NEXT_ZWL
15260 INFO$="'"+RESP$+"' is not a program file"; PRINT 'RB',; GOTO 15170
15270 INFO$="'"+RESP$+"' is password protected"; PRINT 'RB',; GOTO 15170
15280 ! 300 - Second program definition
15290 RESP$=PTH.1$
15300 QUEST$="Revised program file:"; GOSUB 17290
15310 IF RESP$="" THEN GOTO 15070 ELSE LAST.2$=RESP$
15320 IF LEN(RESP$)=2 THEN IF LEN(PTH.1$)>2 AND RESP$(2,1)=":" AND UCS(RESP$(1,1))>="A" AND UCS(RESP$(1,1))<="Z" THEN RESP$=RESP$(1,1)+PTH.1$(2)
15330 OPEN INPUT (WK.LFN,ERR=15370)RESP$; A$=FIB(WK.LFN),PTH.2$=PTH(WK.LFN)
15340 CLOSE (WK.LFN); IF A$(19,1)<>"P" THEN GOTO 15380
15350 OPEN INPUT (WK.LFN,ISZ=256,ERR=15370)RESP$; READ RECORD (WK.LFN)X$; CLOSE (WK.LFN); IF LEN(X$)<>256 THEN GOTO 15380
15360 FL.TYP$="P"; GOTO 15520
15370 INFO$=MSG(RET); PRINT 'RB',; GOTO 15280
15380 INFO$="'"+RESP$+"' is not a program file"; PRINT 'RB',; GOTO 15280
15390 INFO$="'"+RESP$+"' is password protected"; PRINT 'RB',; GOTO 15280
15400 ! 500 - Second program definition
15410 IF LAST.2$<>"" THEN RESP$=LAST.2$
15420 INFO$="Directory containing original programs:"+PTH.1$
15430 QUEST$="Revised program Directory:"; GOSUB 17290
15440 IF LEN(RESP$)=2 THEN IF LEN(PTH.1$)>2 AND RESP$(2,1)=":" AND UCS(RESP$(1,1))>="A" AND UCS(RESP$(1,1))<="Z" THEN RESP$=RESP$(1,1)+PTH.1$(2)
15450 IF RESP$="" THEN GOTO 15070 ELSE LAST.2$=RESP$
15460 OPEN INPUT (WK.LFN,ERR=15490)RESP$; A$=FIB(WK.LFN),PTH.2$=PTH(WK.LFN)
15470 CLOSE (WK.LFN); IF A$(19,1)<>"D" THEN GOTO 15500
15480 FL.TYP$="D"; GOTO 15510
15490 INFO$=MSG(RET); PRINT 'RB',; GOTO 15400
15500 INFO$="'"+RESP$+"' is not a directory"; PRINT 'RB',; GOTO 15400
15510 GOSUB GET_FILES; IF PG.MSK$="" THEN GOTO 15400
15520 ! 600 - Changes only flag
15530 INFO$="Print only the lines which have been changed"
15540 OPTS$="Yes No"
15550 ! GOSUB 5000
15560 CHG.FLG$="Y"
15570 ! 
15580 INFO$="Output results to"
15590 OPTS$="Screen Printer"
15600 ! GOSUB 5000
15610 PRT_OPT$="P"; IF PRT_OPT$="Q" THEN GOTO 17740
15620 ! 
15630 PR.LFT=0,PR.PAGE=0
15640 PR.TTL$="Program comparision for "+WHO
15650 IF PRT_OPT$="S" THEN PR.LFN=HFN; OPEN (PR.LFN)FID(0); GOTO 15690
15660 PR.LFN=HFN; XX$="/tmp/"+CALLNUM$+".TXT" ! ERASE __XX$,ERR=*NEXT ! CALL "*pr.opn",PR.LFN,20
15670 SERIAL XX$,ERR=*PROCEED; OPEN LOCK (PR.LFN)XX$
15680 ON CTL GOTO 15690,15520,15660,15660,17740
15690 MX_COL=MXC(PR.LFN); IF MX_COL<1 THEN MX_COL=79 ! 
15700 MX_LINE=999999
15710 MX_MID=MAX(31,INT((MX_COL-6)/2)-1) ! 
15720 ! 800 - Compare
15730 TTL.1$=PTH.1$,TTL.2$=PTH.2$
15740 IF FL.TYP$="P" THEN GOSUB 16010; GOTO 15990
15750 ! 
15760 IF FL_LST=0 THEN GOTO 15820
15770 ! 
15780 F$=KEY(FL_LST,END=*NEXT); READ (FL_LST); F$=F$(1,LEN(F$)-1); GOTO 15840
15790 X$=PTH(FL_LST); CLOSE (FL_LST); IF POS("*memory*"=LCS(X$))=0 THEN ERASE X$,ERR=*NEXT
15800 GOTO 15990
15810 ! 
15820 X=POS(SEP=FL.LST$); IF X=0 THEN GOTO 15990
15830 F$=FL.LST$(1,X-2),FL.LST$=FL.LST$(X+1)
15840 X=HFN; OPEN INPUT (X,ERR=15760)TTL.1$+DLM+F$; X$=FIB(X); PTH.1$=PTH(X); CLOSE (X)
15850 IF X$(19,1)<>"P" THEN GOTO 15760
15860 OPEN INPUT (X,ISZ=256,ERR=15760)TTL.1$+DLM+F$; READ RECORD (X)X$; CLOSE (X); IF LEN(X$)<>256 THEN GOTO 15760 ELSE IF X$(1,1)+X$(5,4)<>"[prg]" THEN GOTO 15760
15870 PTH.1X$=X$
15880 COMP$="",TTL.3$=""; IF FID(0)=FID(PR.LFN) THEN PR.LFT=0,PR.PAGE=0
15890 X=HFN; OPEN INPUT (X,ERR=15940)TTL.2$+DLM+F$; X$=FIB(X); PTH.2$=PTH(X); CLOSE (X)
15900 IF X$(19,1)<>"P" THEN GOTO 15760
15910 OPEN INPUT (X,ISZ=256,ERR=15760)TTL.2$+DLM+F$; READ RECORD (X)X$; CLOSE (X); IF LEN(X$)<>256 THEN GOTO 15760 ELSE IF X$(1,1)+X$(5,4)<>"[prg]" THEN GOTO 15760
15920 IF PTH.1X$=X$ THEN GOTO 15760
15930 GOSUB 16010; GOTO 15760
15940 IF PR.LFT<1 THEN GOSUB 16710; GOTO 15960
15950 PRINT (PR.LFN)DIM(MX_COL,"-"); PR.LFT=PR.LFT-1
15960 PRINT (PR.LFN)"Cannot find:",TTL.2$+DLM+F$; PR.LFT=PR.LFT-1
15970 IF FID(0)=FID(PR.LFN) THEN INFO$="Finished comparing these two programs.  Continue?",OPTS$="Continue"; GOSUB 16890; IF OPT$="Q" THEN GOTO 17740
15980 GOTO 15760
15990 CALL "*pr.cls",PR.LFN; PR.LFN=0
16000 GOTO 17740
16010 ! 1000 - Convert the files
16020 PRINT @(0,2),'CE','SB','CYAN',"Comparing:",@(0,4),"Original:",@(16,4),PTH.1$,@(0,6),"Revised:",@(16,6),PTH.2$,'RM',
16030 TITLE1$=PTH.1$; IF LEN(TITLE1$)>MX_MID THEN TITLE1$=TITLE1$(1,2)+"~~"+TITLE1$(LEN(TITLE1$)-MX_MID+5)
16040 TITLE2$=PTH.2$; IF LEN(TITLE2$)>MX_MID THEN TITLE2$=TITLE2$(1,2)+"~~"+TITLE2$(LEN(TITLE2$)-MX_MID+5)
16050 ERR_MSG$=""
16060 COMP$=PAD(TITLE1$,MX_MID,2)+" Line#  "+PAD(TITLE2$,MX_MID,2),TTL.3$=COMP$
16070 CALL "*pg.cnv",ERR=16080,PTH.1$,WRK.1$; GOTO 16090
16080 ERR_MSG$=PTH.1$+" is password protected"; PRINT @(0,20),ERR_MSG$,'CL',; GOTO 16330
16090 CALL "*pg.cnv",ERR=16100,PTH.2$,WRK.2$; GOTO 16110
16100 ERR_MSG$=PTH.2$+" is password protected"; PRINT @(0,20),ERR_MSG$,'CL',; GOTO 16330
16110 LFN.1=HFN; OPEN (LFN.1)WRK.1$
16120 LFN.2=HFN; OPEN (LFN.2)WRK.2$
16130 L.1$="",L.2$=""
16140 NO.ADD=0,NO.CHG=0,NO.DEL=0
16150 ! 2000 - Compare records
16160 IF L.1$="" THEN READ RECORD (LFN.1,END=16200)L.1$
16170 L1=NUM(L.1$(1,5),ERR=16180); GOTO 16210
16180 L1=NUM(L.1$(1,4),ERR=*NEXT); GOTO 16210
16190 MSGBOX "Cannot compare programs without line numbers",PTH.1$; GOTO 16330
16200 L1=65000
16210 IF L.2$="" THEN READ RECORD (LFN.2,END=16240)L.2$
16220 L2=NUM(L.2$(1,5),ERR=16230); GOTO 16250
16230 L2=NUM(L.2$(1,4)); GOTO 16250
16240 L2=65000
16250 IF L1+L2=130000 THEN GOTO 16330
16260 IF FID(0)=FID(PR.LFN) AND NO.ADD+NO.DEL+NO.CHG+(CHG.FLG$="N")<>0 THEN GOTO 16280
16270 PRINT @(10,4),L1:"#0000",@(10,6),L2:"#0000",
16280 IF L1<L2 THEN GOTO 16310
16290 IF L1>L2 THEN GOTO 16320
16300 REC.1$=L.1$,REC.2$=L.2$,L.1$="",L.2$=""; GOSUB 16470; GOTO 16150
16310 REC.1$=L.1$,REC.2$="",L.1$=""; GOSUB 16470; GOTO 16150
16320 REC.1$="",REC.2$=L.2$,L.2$=""; GOSUB 16470; GOTO 16150
16330 ! 2900 - End of report
16340 IF LFN.1<>0 THEN CLOSE (LFN.1)
16350 IF LFN.2<>0 THEN CLOSE (LFN.2)
16360 IF WRK.1$<>"" THEN ERASE WRK.1$,ERR=16370; WRK.1$=""
16370 IF WRK.2$<>"" THEN ERASE WRK.2$,ERR=16380; WRK.2$=""
16380 ! 
16390 IF NO.ADD+NO.DEL+NO.CHG=0 AND ERR_MSG$="" AND FL.TYP$<>"P" THEN GOTO 16460
16400 IF PR.LFT<1 THEN GOSUB 16710
16410 PR.LFT=PR.LFT-1
16420 IF ERR_MSG$<>"" THEN PRINT (PR.LFN)ERR_MSG$; ERR_MSG$=""; GOTO 16450
16430 IF NO.ADD+NO.DEL+NO.CHG=0 THEN PRINT (PR.LFN)" No differences between:",PTH.1$," and ",PTH.2$; GOTO 16450
16440 PRINT (PR.LFN)NO.ADD," additions,",NO.DEL," deletions,",NO.CHG," changes"
16450 IF FID(0)=FID(PR.LFN) THEN INFO$="Finished comparing these two programs.  Continue?",OPTS$=""; GOSUB 16890; IF OPT$="Q" THEN EXITTO 17740
16460 RETURN 
16470 ! 4000 - Output line
16480 IF REC.1$=REC.2$ THEN CD$="" ELSE IF REC.1$="" THEN CD$="Add",NO.ADD=NO.ADD+1 ELSE IF REC.2$="" THEN CD$="Del",NO.DEL=NO.DEL+1 ELSE CD$="Chg",NO.CHG=NO.CHG+1
16490 IF CD$="" AND CHG.FLG$="Y" THEN RETURN 
16500 O1$=REC.1$,O2$=REC.2$
16510 LNUM$=""
16520 IF O1$<>"" THEN LNUM$=O1$(1,POS(" "=O1$+" ")-1)
16530 X=LEN(LNUM$); IF X<4 OR X>5 THEN IF O2$<>"" THEN LNUM$=O2$(1,POS(" "=O2$+" ")-1)
16540 IF O1$<>"" THEN O1$=O1$(LEN(LNUM$)+2)
16550 IF O2$<>"" THEN O2$=O2$(LEN(LNUM$)+2)
16560 LNUM$=PAD(LNUM$,5,0),O1$=" "+O1$,O2$=" "+O2$
16570 IF PR.LFT<1 THEN GOSUB 16710 ELSE IF COMP$<>"" THEN GOSUB SUB_HEADING
16580 PR.LFT=PR.LFT-1
16590 IF LEN(O1$)<=MX_MID THEN LINE$=LINE$+O1$+DIM(MX_MID+1)+LNUM$; PRINT (PR.LFN)O1$,@(MX_MID+1),LNUM$,; O1$=""; GOTO 16620
16600 PRINT (PR.LFN)O1$(1,MX_MID),@(MX_MID+1),LNUM$,; LINE$=LINE$+O1$(1,MX_MID)+" "+LNUM$
16610 O1$=":"+O1$(MX_MID+1)
16620 LNUM$=""
16630 IF LEN(O2$)<=MX_MID THEN LINE$=LINE$+DIM(MX_MID+8)+O2$+$0A$; PRINT (PR.LFN)@(MX_MID+8),O2$; O2$=""; GOTO 16660
16640 PRINT (PR.LFN)@(MX_MID+8),O2$(1,MX_MID); LINE$=LINE$+" "+O2$(1,MX_MID)+$0A$
16650 O2$=":"+O2$(MX_MID+1)
16660 IF O2$+O1$<>"" THEN GOTO 16570
16670 PRINT (PR.LFN)'LF',; PR.LFT=PR.LFT-1
16680 IF CD$="" THEN RETURN 
16690 IF FID(0)<>FID(PR.LFN) THEN PRINT @(0,20),NO.ADD," Additions, ",NO.DEL," deletions, ",NO.CHG,", changes",
16700 RETURN 
16710 ! 4500 - Print page header
16720 PR.PAGE=PR.PAGE+1
16730 IF PR.PAGE<=1 THEN GOTO 16760
16740 IF FID(0)<>FID(PR.LFN) THEN GOTO 16760
16750 INFO$="More output to follow.  Continue?",OPTS$="Continue"; GOSUB 16890; IF OPT$="Q" THEN EXITTO 17740
16760 IF FID(0)=FID(PR.LFN) THEN PRINT (PR.LFN)'CS' ! ELSE PRINT (PR.LFN)'FF',
16770 IF NOT(HEADER_DONE) THEN HEADER_DONE=1; PRINT (PR.LFN)'LF',DAY,INT(TIM):"##0:",FPT(TIM)*60:"00",@(INT((MX_COL+1-LEN(PR.TTL$))/2)),PR.TTL$,'LF',DIM(MX_COL,"-")
16780 PRINT (PR.LFN)'LF',TTL.3$,'LF',DIM(MX_COL,"-")
16790 PR.LFT=MX_LINE,COMP$=""
16800 HEADER$=TTL.3$
16810 RETURN 
16820 ! 4600 - Sub Heading
16830 SUB_HEADING:
16840 IF PR.LFT<3 THEN COMP$=""; GOSUB 16710; GOTO 16880
16850 PRINT (PR.LFN)'LF',COMP$
16860 PRINT (PR.LFN)DIM(MX_COL,"-")
16870 PR.LFT=PR.LFT-3,COMP$=""
16880 RETURN 
16890 ! 5000 - Display title/options
16900 X$=" "+PAD(OPTS$+" Quit",79)
16910 PRINT @(0,0),'RM','BR',PAD(INFO$,80),@(0,1),X$,'RM',
16920 O=0; IF DEF_OPT$<>"" THEN O=POS(" "+DEF_OPT$=X$),DEF_OPT$=""
16930 IF O=0 THEN O=1
16940 OPT$=X$(O+1,1),L=POS(" "=X$(O+1))+1
16950 MSE_DOWN=0
16960 ! 5100 - Get option
16970 PRINT 'YELLOW',@(O-1,1),X$(O,L),'RM',
16980 COUNT+=1; OBTAIN (0,SIZ=1,HLP=X$(O+1,1))@(O-1,1),'BI',O$,'EI'
16990 ANT=CTL
17000 ! IF CTL=5 THEN GOSUB CLIP
17010 IF CTL=-1080 THEN GOTO 17250
17020 PRINT 'BR',@(O-1,1),X$(O,L),'RM',
17030 IF CTL>=0 THEN ON CTL GOTO 17220,17190,17150,17110,17110,17120
17040 IF CTL=-1081 THEN IF MSE_DOWN=0 THEN GOTO 16960 ELSE GOTO 17220
17050 IF CTL>-1000 THEN CALL "*control"; GOTO 16960
17060 IF CTL=-1003 OR CTL=-1004 OR CTL=-1016 THEN GOTO 17190
17070 IF CTL=-1005 OR CTL=-1015 THEN GOTO 17150
17080 IF CTL=-1010 THEN O=1; GOTO 17180
17090 IF CTL=-1018 THEN GOTO 17210
17100 PRINT 'RB',; GOTO 16960
17110 O$="Q"
17120 IF O$=" " THEN GOTO 17150 ELSE O$=UCS(O$)
17130 P=POS(" "+O$=X$); IF P=0 THEN PRINT 'RB',; GOTO 16960
17140 OPT$=O$,O=P,L=POS(" "=X$(O+1))+1; GOTO 17220
17150 ! 5300 - Forward
17160 OPT$=X$(O+L,1),O=O+L-1
17170 IF OPT$=" " THEN O=1,OPT$=X$(2,1)
17180 L=POS(" "=X$(O+1))+1; GOTO 16960
17190 ! 5350 - Backward
17200 O=POS(" "=X$(1,O-1),-1); IF O<>0 THEN L=POS(" "=X$(O+1))+1,OPT$=X$(O+1,1); GOTO 16960
17210 O=LEN(STP(X$,2)); GOTO 17200
17220 ! 5400 - Return - selection made
17230 PRINT 'GREEN',@(O-1,1),X$(O,L),'RM'
17240 IF OPT$="Q" THEN EXITTO 17740 ELSE RETURN 
17250 ! 5450 - Mouse follow logic
17260 MSE_DOWN=1,O$=MSE; O__C=DEC(O$(2,1))+1,O1=MAX(1,POS(" "=STP(X$(1,O__C),1),-1))
17270 IF O=O1 THEN GOTO 16980 ELSE PRINT 'BR',@(O-1,1),X$(O,L),'RM',; O=O1,OPT$=X$(O+1,1)
17280 IF OPT$=" " THEN GOTO 17210 ELSE GOTO 17180
17290 ! 5500 - Get input
17300 PRINT @(0,0),'RM','BR',PAD(INFO$,80),@(0,1),PAD(QUEST$,80),'RM',
17310 COUNT+=1
17320 IF COUNT=1 THEN RESP$=ZWL$(1,8) ! INPUT EDIT @(LEN(QUEST$),1),'YELLOW',RESP$,'RM'
17330 IF COUNT=2 THEN RESP$=SUB(RESP$,"/pvx/","/pvx/rel/"); COUNT=0
17340 IF CTL<>0 THEN EXITTO 17740 ELSE RESP$=STP(RESP$,2)
17350 RETURN 
17360 ! 7000 - Error logic
17370 PRINT IOL=17720,MSG(RET),; GOTO 15170
17380 PRINT IOL=17730,MSG(RET),; GOTO 15280
17390 PRINT IOL=17720,MSG(RET),; GOTO 15245
17400 PRINT IOL=17720,MSG(RET),; GOTO 15365
17410 ! 7800
17420 GET_FILES:
17430 DR$=PTH.1$,PG.MSK$="*",FL.LST$="",FL_LST=0,FL_TMP=0
17440 CALL "*gflst",DR$,PG.MSK$,FL.LST$,"N"
17450 IF CTL=4 AND PG.MSK$<>"" THEN GOTO 17470
17460 IF CTL>0 AND CTL<>2 AND CTL<>100 THEN EXITTO 17740
17470 IF PG.MSK$="" THEN GOTO 17580
17480 PRINT @(29,12),"(Scanning directory)",
17490 DR$=PTH.1$,FL.LST$=""; CALL "*file.lst;list_to_file",FL.LST$,DR$,PG.MSK$,"P"
17500 DR$=PTH.2$,FL.TMP$=""; CALL "*file.lst;list_to_file",FL.TMP$,DR$,PG.MSK$,"P"
17510 IF POS("FILNUM="=FL.TMP$) THEN GOTO 17600
17520 ! 
17530 X=POS(SEP=FL.TMP$); IF X=0 THEN GOTO 17560
17540 X$=FL.TMP$(1,X),FL.TMP$=FL.TMP$(X+1),X=POS(SEP+X$=SEP+FL.LST$); IF X=0 THEN FL.LST$=FL.LST$+X$
17550 GOTO 17530
17560 FL.LST$=SRT(FL.LST$)
17570 PRINT @(29,12),'CL',
17580 RETURN 
17600 ! ^100
17610 X=POS("FILNUM="=FL.LST$); IF X=0 THEN GOTO 17520 ELSE FL_LST=NUM(FL.LST$(8))
17620 X=POS("FILNUM="=FL.TMP$); IF X=0 THEN GOTO 17520 ELSE FL_TMP=NUM(FL.TMP$(8))
17630 READ (FL_TMP,KEY="",ERR=*NEXT)
17640 READ (FL_TMP,END=17670)X$
17650 WRITE (FL_LST,KEY=X$,DOM=*NEXT)X$
17660 GOTO 17640
17670 ! 
17680 X$=PTH(FL_TMP); CLOSE (FL_TMP); IF POS("*memory*"=LCS(X$))=0 THEN ERASE X$,ERR=*NEXT
17690 READ (FL_LST,KEY="",ERR=*NEXT)
17700 RETURN 
17710 ! 8000 - Iolists
17720 IOLIST @(0,20),'RB','CL'
17730 IOLIST @(0,20),'CL'
17740 ! 9000 - Wrapup
17750 PRINT @(0,22),'CL',"End of comparision",'LF','MODE'(SV_MODE$),'DF','CE',
17760 SET_PARAM 'SD'=SV_SD
17770 IF PR.LFN<>0 THEN CALL "*pr.abt",PR.LFN
17780 CLOSE (WK.LFN)
17790 IF LFN.1<>0 THEN CLOSE (LFN.1)
17800 IF LFN.2<>0 THEN CLOSE (LFN.2)
17810 IF WRK.1$<>"" THEN ERASE WRK.1$
17820 IF WRK.2$<>"" THEN ERASE WRK.2$
17827 GOSUB UPDATE_REC ! update to file ZV0
17830 GOTO NEXT_ZWL
17840 DONE_ZWL:
19090 RETURN 
20000 UPDATE_REC:
20005 PRINT @(0,20),"UPDATING TO FILE ZV0...",
20010 PROG$=LAST.1$,SEQ=0
20050 TXT_FL=HFN; OPEN LOCK (TXT_FL,OPT="TEXT")XX$
20100 READ (TXT_FL,END=20500)J$
20110 SEQ=SEQ+1; DIM ZV0$(1532)
20120 ZV0$(1,6)=CALLNUM$,ZV0$(17,6)=STR(SEQ:"000000")
20130 ZV0$(7,10)=PAD(PROG$,10)
20140 ZV0$(23,LEN(J$))=J$
20150 ZV0$(1023,10)=PAD(REL$,10)
20200 WRITE (3,KEY=ZV0$(1,22))ZV0$
20300 GOTO 20100
20500 CLOSE (TXT_FL); ERASE XX$
20600 RETURN 
