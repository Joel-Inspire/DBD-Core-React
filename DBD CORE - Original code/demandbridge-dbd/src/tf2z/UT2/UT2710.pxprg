0010 REM "DB Distributor Release 7.1 Update <UT2710>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 02/28/22 - 16.232118 - jvv - SSP# 307387
0037 REM "307387-EQLink DBD Bridge to Qnet                                   
0038 ! USE UT2710 AS OF 02/01/2013 INSTEAD OF UT2700
0040 REM "Copyright 2022 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="UT2710",X1$="DB Distributor Release 7.1 Update"
0120 DIM Z0$(80,"-"),S$(40)
0130 K0=20,K1=1
0135 C9=-1
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0509 REM "Stay away from slot Z(11)
0510 Z$="12O ZZE  13O ZZPARM  "
0520 GOSUB 9750
0600 REM "
0610 GOSUB 6000
0640 IF Q1$<>"*" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
1000 REM 
1005 PRINT @(0,4),'CE',
1006 GOSUB CREATE_HELP_FILE
1008 GOSUB MERGE_LIBRARY
1010 GOSUB 4000; GOSUB DO_ZC0_UPDATE
1012 PERFORM "ZZGFLS;DO_ZC1_UPDATE" ! 237322
1013 PERFORM "ZZ2EXP;UPGRADE_PARAMS" ! 269059
1090 ! 
1100 H1$="IM3 Import Device file to 512, string to 360" ! WO260723
1110 F$="IM3...",F1$="O"; GOSUB 7500; GOSUB 7900
1120 DIM A[4]; A[0]=K,A[1]=512,A[2]=R1,A[3]=D,A[4]=360; GOSUB 8000
1125 ! 
1130 H1$="Monthly Disbursements Header to 384,string to 110"; REM " SSP#189432 
1140 F$="APM...",F1$="O"; GOSUB 7500; GOSUB 7900
1150 DIM A[4]; A[0]=K,A[1]=384,A[2]=R1,A[3]=D,A[4]=110; GOSUB 8000
1155 ! 
1160 H1$="Supplier Price List to 512, string to 115" ! WO273055
1170 F$="IC2...",F1$="O"; GOSUB 7500; GOSUB 7900
1180 DIM A[4]; A[0]=K,A[1]=512,A[2]=R1,A[3]=D,A[4]=115; GOSUB 8000
1185 ! 
1190 H1$="Credit Card Information to 256, string to 255" ! SSP229993
1200 F$="ARZ...",F1$="O"; GOSUB 7500; GOSUB 7900
1210 DIM A[4]; A[0]=K,A[1]=256,A[2]=R1,A[3]=D,A[4]=255; GOSUB 8000
1220 H1$="EmailNotFax to 640, string to 422" ! SSP298391
1225 F$="EF3...",F1$="O"; GOSUB 7500; GOSUB 7900
1230 DIM A[4]; A[0]=K,A[1]=640,A[2]=R1,A[3]=D,A[4]=422; GOSUB 8000
1232 H1$="FS6_OrderHis to 434, string to 163" ! SSP307357
1234 F$="FS6...",F1$="O"; GOSUB 7500; GOSUB 7900
1235 DIM A[4]; A[0]=K,A[1]=434,A[2]=R1,A[3]=D,A[4]=163; GOSUB 8000
1240 ! 
1242 H1$="ZZ9_Device to 384, string to 380" ! SSP307384
1244 F$="ZZ9...",F1$="O"; GOSUB 7500; GOSUB 7900
1245 DIM A[4]; A[0]=K,A[1]=384,A[2]=R1,A[3]=D,A[4]=380; GOSUB 8000
2000 REM "Erase files
2090 ! 
3000 REM "Set Parameter Records
3020 REM "BATCH record
3040 GOSUB 4200
3050 GOSUB CREATE_EF_PARMS ! SSP 298391
3090 ! 
3500 REM "Rebuilds-
3564 CALL "EF2UNF;SETUP_GRAPHICS",ERR=*NEXT,X3$,X4$,"",ERRMSG$ ! SSP 245093
3566 CALL "UPDFS6;ADD_FS6_LINE_NUM",ERR=*NEXT,X3$,X4$,"",ERRMSG$ ! SSP 307357
3568 GOSUB UPD_GUI_VIEWER ! SSP 302865
3570 CALL "ZZ2NST",X3$,X4$,"FM1","B","Budget",ANSWER$,"N" ! SSP275493
3575 CALL "ZZ2NST",X3$,X4$,"FM1","F","Freight",ANSWER$,"N" ! SSP275493
3580 CALL "ZZ2NST",X3$,X4$,"FM1","V","Value",ANSWER$,"N" ! SSP275493
3585 PERFORM "ZYGZY8;MERGE_COUNTRY_FILE" ! 307210
3595 ! 
3900 REM "Call WebEC update routine
3910 CALL "UT26EC",X3$,X4$,"!","*"
3915 CALL "ZPMCON",ERR=*NEXT,X3$,X4$,"","*"; REM SSP 303058
3920 ! 
3925 REM "Call UT2REL to setup certain parameters and files
3930 CALL "UT2REL",ERR=*NEXT,X3$,X4$,"","*"; REM SSP 307177
3985 ! 
3990 GOTO 5000
3995 ! 
4000 REM "Define missing files F5$=Files to check, G5$=use number of records from this exisiting file or if number use it.
4010 F5$="RPK...SV0...SME...ATI...FV4...FV5...FV6...FV7...XZU...ZY8...FV8...DRT...ZYR..." ! SSP 276772SSP 303779 307244 307387
4020 G5$="020   020   020   020   020   020   020   020   020   020   020   020   020   " ! SSP 276772SSP 303779 307244 307387     
4030 IF F5$="" THEN GOTO 4190 ELSE F4$=F5$(1,6),F5$=F5$(7),G4$=G5$(1,6),G5$=G5$(7)
4035 ZZE_KEY$=F4$; IF F4$(4,3)="..." THEN ZZE_KEY$=F4$(1,3)+DIM(3)
4040 FIND (Z[12],KEY=ZZE_KEY$)A$
4045 FILE$=F4$; IF F4$(4,3)="..." THEN FILE$=F4$(1,3)+X3$(9,3)
4050 CLOSE (14); OPEN (14,ERR=4051)FILE$; CLOSE (14); GOTO 4180
4055 R1=NUM(G4$,ERR=4056),D=0; GOTO 4080
4060 F$=G4$; F1$="O"; GOSUB 7500
4070 GOSUB 7900; F1$="C"; GOSUB 7500
4080 DIM A[4]; A[2]=R1,A[3]=D,Q$=FILE$
4090 IF A$(47,1)="S" THEN T0=3,A[0]=NUM(A$(48,2)) ELSE IF A$(47,1)="D" THEN T0=2,A[0]=NUM(A$(48,2)),A[1]=NUM(A$(50,4)) ELSE IF A$(47,1)="I" THEN T0=0,A[1]=NUM(A$(50,4)) ELSE ESCAPE 
4095 IF Q$="ZZWSEC" THEN A[0]=127 ! 3 character key length
4100 GOSUB 7650
4105 H1$="Defined file "+Q$; GOSUB 8800
4110 IF T0<>0 THEN GOTO 4180
4120 F$=F4$; F1$="O"; GOSUB 7500
4130 CALL "ZZINIT",STR(F:"00")
4140 F1$="C"; GOSUB 7500
4180 GOTO 4030
4190 RETURN 
4195 ! 
4200 REM "Create BATCH control record in ZZPARM
4210 READ (Z[13],KEY="CMP"+X3$(9,3),DOM=4290)X$; IF X$(194,1)<>"1" THEN GOTO 4290
4215 PRINT @(0,4),'CE',@(10,10),"Rebuilding Batch Control Record",
4220 Q$=""; READ (Z[12],KEY="",DOM=4221)
4230 READ (Z[12],END=4275)X$
4240 IF X$(46,1)<>"1" THEN GOTO 4230
4245 PRINT @(45,10),X$(1,3),
4250 IF X$(4,3)="   " THEN X$(4,3)="..."
4260 Q$=Q$+X$(1,6)+"  "
4270 GOTO 4230
4275 PRINT @(0,4),'CE',
4280 Q$="BATCH"+Q$
4285 WRITE (Z[13],KEY=Q$(1,5))Q$
4290 RETURN 
4295 ! 
4300 DO_ZC0_UPDATE:! update company specific ZCO records from the release ZC0 data in tf2z/USR/ZC0
4305 ZC0_SLOT=HFN; OPEN (ZC0_SLOT)"ZC0"; F=ZC0_SLOT; GOSUB 8100
4310 ZC0C_SLOT=HFN; OPEN (ZC0C_SLOT,ERR=4311)"ZC0"+X3$(9,3); GOTO 4315
4311 KEYED "ZC0"+X3$(9,3),40,[1:21:20]+[1:1:20],,512; GOTO 4310
4315 SELECT ZC0$ FROM (ZC0_SLOT)
4318 C=C+1; IF MOD(C,100)=0 THEN GOSUB 8150
4320 FIND (ZC0C_SLOT,KEY=ZC0$(1,40),DOM=UPDATE_ZC0C)ZC0C$
4325 IF ZC0C$(76,1)="Y" THEN CONTINUE ! if don't overwrite flag is set, then skip it
4330 UPDATE_ZC0C:! if here, then record missing or can be overwritten
4340 WRITE (ZC0C_SLOT,KEY=ZC0$(1,40))ZC0$
4360 NEXT RECORD 
4395 RETURN 
4399 ! 
5000 REM "EOJ
5010 PRINT @(0,3),'CE',@(0,10),"The automated portion of your DB Distributor Release 7.1 installation is now    complete.  Contact DB Support for further instructions at 770 239-7777."
5020 IF Q1$<>"*" THEN CALL "ZZPROM",".4",X3$,Z,"","","",0
5040 GOTO 9900
5090 ! 
6000 REM "Bkg
6020 PRINT 'SB',@(0,3),"The DB Distributor Release 7.1 updates will be performed automatically by this  program.",@(0,6),"After answering YES to the Proceed question below, new files will be defined and",@(0,7),"some of your data will be adjusted to be compatible with DB Distributor Release 7.1.",@(0,10),"Also, various sort file rebuilds will be run automatically.",@(0,12),"Effective Release Date is 02/01/2013."
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6195 ! 
7500 REM "Open/Close Files
7505 Z$=""
7510 FOR I=1 TO LEN(F$) STEP 6
7515 Z$=Z$+STR(I:"00")+F1$+" "+F$(1,6)+"  "
7520 NEXT I
7525 GOSUB 9750
7530 F=Z[1]
7540 RETURN 
7590 ! 
7600 REM "CREATE TEMP FILE
7610 Q$="-T"+FID(0)
7615 MX=FN%ERASE(Q$,ERR=7616); GOTO 7615 ! 246342
7625 IF ERR<>12 THEN GOTO 9000
7630 GOSUB 7650
7635 Z$="11O "+Q$; GOSUB 9750
7640 RETURN 
7650 REM DEFINE FILE
7660 IF T0=0 THEN INDEXED Q$,A[2],A[1],A[3],0 ELSE IF T0=2 THEN DIRECT Q$,A[0],0,-A[1],A[3],0 ELSE IF T0=3 THEN SORT Q$,A[0],0,A[3],0 ELSE ESCAPE ! SSP#254684
7690 RETURN 
7695 ! 
7700 REM "EXPAND FILE -NOTE: Cannot expand strings of Indexed files!!!!
7705 C=0
7707 IF T0<>0 THEN EXTRACT (F,KEY="",DOM=7708) ELSE EXTRACT (F,IND=0)
7710 IF T0<>0 THEN K$=KEY(F,END=7780) ELSE SETERR 7790; I0=IND(F)
7715 READ RECORD (F,END=7780)A$
7718 REM LET C=C+1; IF MOD(C,100)=0 THEN LET H1$="Record: "+STR(C); GOSUB 08800
7719 C=C+1; IF MOD(C,T2)=1 THEN GOSUB 8150
7720 IF A[4]=0 OR T=0 THEN GOTO 7735 ELSE P0=POS($8A$=A$); IF P0=0 THEN P0=LEN(A$)+1,A$=A$+$8A$
7721 IF P0-1>A[4] THEN REM CALL "ZZPROM",".5",X3$,A,"String will be truncated!","","",0; ON A GOTO 07722,07900
7722 DIM S$(A[4]); S$(1)=A$(1,P0-1),A$=S$+A$(P0)
7730 REM IF LEN(A$)>A[1] THEN IF POS($00$<>A$(A[1]))>0 THEN CALL "ZZPROM",".5",X3$,A,"Record will be truncated!","","",0; ON A GOTO 07731,07900
7735 IF LEN(A$)>A[1] THEN A$=A$(1,A[1])
7750 IF T0=0 THEN WRITE RECORD (Z[11])A$ ELSE WRITE RECORD (Z[11],KEY=K$)A$
7760 GOTO 7710
7790 SETERR 9000
7795 RETURN 
7799 ! 
7800 REM "RENAME FILES
7810 MX=FN%ERASE("TX"+FID(0),ERR=7811); GOTO 7810 ! 246342
7815 IF ERR<>12 THEN GOTO 9000
7820 Z$="01C "+F$(1,6)+"  "; GOSUB 9750; CLOSE (Z[1],ERR=7821)
7830 MX=FN%RENAME(F9$,"TX"+FID(0)) ! 246342
7840 TMP=Z[11]; Z$="11C "+Q$+"  "; GOSUB 9750; CLOSE (TMP,ERR=7841)
7845 MX=FN%RENAME(Q$,F9$) ! 246342
7850 MX=FN%ERASE("TX"+FID(0)) ! 246342
7860 H2$="*Finished*"; GOSUB 8800
7890 RETURN 
7895 ! 
7900 REM 
7910 CALL "ZZINFO",F,X0,X3$,R0,R1,K,B,D,T0,S0,F9$
7940 RETURN 
7990 ! 
8000 REM 
8005 GOSUB 8100
8010 H2$="*Starting*"; GOSUB 8800
8040 GOSUB 7600
8050 GOSUB 7700
8060 IF K=0 THEN GOSUB 8200
8070 GOSUB 7800
8090 RETURN 
8095 ! 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T2 to the reporting interval (report every T2 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",F,T9,X3$,T,T2,K,B,D,S0,S1,F$
8115 H2$=STR(T)+" # of records to do "; GOSUB 8800
8129 REM "Set T2, we make sure T2 is > 1, because later on we MOD and look for avalue of 1. IF T2 is 1, then nothing would get reported. We look fora result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T2'th record to get the first report
8130 T2=INT(T*.02); IF T2<1 THEN T2=2
8135 C=0,T1=0
8140 PRINT @(0,4),'CL',
8145 RETURN 
8149 ! 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",4,10,50,T1,T,C
8195 RETURN 
8199 ! 
8200 REM "INDEXED FILES
8210 DIM Y[6]; EXTRACT (Z[11],IND=0)IOL=0290
8220 Y[3]=A[2]-1
8230 WRITE (Z[11],IND=0)IOL=0290
8290 RETURN 
8299 ! 
8600 REM "Expand String-F=file slot, F0$=key prefix, F0=string length
8605 C=0,H2$="*STARTING*"; GOSUB 8800
8610 READ (F,KEY=F0$,DOM=8611)
8620 K$=KEY(F,END=8690)
8630 IF K$(1,LEN(F0$))<>F0$ THEN GOTO 8690
8640 EXTRACT RECORD (F,KEY=K$)A$
8650 P0=POS($8A$=A$); IF P0=0 THEN ESCAPE 
8660 DIM A1$(F0); A1$(1)=A$(1,P0-1),A1$=A1$+A$(P0),A1$=A1$(1,LEN(A$))
8665 C=C+1; IF MOD(C,100)=0 THEN H2$="Record: "+STR(C); GOSUB 8800
8670 WRITE RECORD (F,KEY=K$)A1$
8680 GOTO 8620
8690 H2$="*FINISHED*"; GOSUB 8800
8695 RETURN 
8699 ! 
8800 REM "Display progress  - Eventually write a log file
8810 L=L+1; IF L>15 THEN PRINT @(0,5),'LD',@(0,20),; L=15 ELSE PRINT @(0,L+5),
8815 X$=STR(INT(TIM):"00")+STR(INT(FPT(TIM)*100):"00")
8820 PRINT DAY," @ ",FNT$(X$)," ",H1$," ",H2$
8830 H2$=""
8840 RETURN 
8890 ! 
8900 REM "Special handling for FM4"
8902 DIM B[15]
8905 IOLIST B$,B1$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15]
8910 READ (Z[1],KEY="",DOM=8911)
8920 EXTRACT (Z[1],END=8940)IOL=8905
8930 B1$=""; WRITE (Z[1])IOL=8905; GOTO 8920
8940 RETURN 
8990 ! 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; RETRY 
9750 REM 
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9770 ON Z0 GOTO 9771,9900
9790 RETURN 
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
10000 MERGE_LIBRARY:! ssp 201126
10001 IF %C$="500" THEN GOTO *RETURN ! Do not touch msg libs in dev
10002 PRINT @(0,3),'CL',
10005 PRINT @(0,4),"Reading msglib.en",
10010 _EN=HFN; OPEN (_EN,ERR=NO_EN)"*tfmsglib.en"
10020 _CA=HFN; OPEN (_CA,ERR=*NEXT)"*tfmsglib.ca"
10030 _UK=HFN; OPEN (_UK,ERR=*NEXT)"*tfmsglib.uk" ! 213071 was opening wrong channel _CA
10035 READ_NEXT:
10040 _K_EN$=KEY(_EN,END=END_MERGE_LIB); READ RECORD (_EN)REC$
10050 WRITE RECORD (_CA,KEY=_K_EN$,DOM=*NEXT,ERR=*NEXT)REC$
10060 WRITE RECORD (_UK,KEY=_K_EN$,DOM=*NEXT,ERR=*NEXT)REC$
10100 GOTO READ_NEXT
10110 NO_EN:PRINT @(0,4),"*msglib.en - missing"
10120 END_MERGE_LIB:
10130 RETURN 
10190 ! 
11000 CREATE_HELP_FILE:
11010 ! Create Help File for GUI
11020 WRITE (Z(13),KEY="HLPA/R")"A/R","\pvx\tf2g\hlp\AccountsReceivable\!SSL!\Microsoft_HTML_Help\Accounts_Receivable.chm"
11030 WRITE (Z(13),KEY="HLPA/P")"A/P","\pvx\tf2g\hlp\AccountsPayable\!SSL!\Microsoft_HTML_Help\Accounts_Payable.chm"
11040 WRITE (Z(13),KEY="HLPG/L")"G/L","\pvx\tf2g\hlp\GeneralLedger\!SSL!\Microsoft_HTML_Help\General_Ledger.chm"
11050 WRITE (Z(13),KEY="HLPZPL")"Z/P","\pvx\tf2g\hlp\ContactManagement\!SSL!\Microsoft_HTML_Help\COntact_Management.chm"
11060 RETURN 
11090 ! 
11200 CREATE_EF_PARMS:! ssp 298391
11210 READ (Z[13],KEY=X3$(9,3)+"E/F",DOM=*NEXT)*; RETURN 
11230 PERFORM "EFGMPA;SET_DEFAULTS"
11240 PROCESS "EFGMPA","../PMG/PM.EN"
11250 RETURN 
11290 ! 
13000 UPD_GUI_VIEWER:
13010 VKY$="P_",VW$="tf_toviewer"; DIM ZZ9$(255),R_ZZ9$(255),LNK_REC$(256)
13015 _FIL_NO=HFN; OPEN (_FIL_NO,ERR=END_GUI_VIEWER)"ZZ9"+%C$
13020 FIND (_FIL_NO,KEY=VKY$,DOM=ADD_NEW)ZZ9$
13025 IF STP(MID(ZZ9$,233,22),2)=VW$ THEN GOTO *NEXT ELSE GOTO END_GUI_VIEWER
13028 GOSUB FIND_LNK_NAME
13030 HAS_UF=0; IF (FN%HAS_MODULE("UF")) THEN HAS_UF=1
13040 R_ZZ9$(43,1)="2",R_ZZ9$(44,8)="NOHEX",R_ZZ9$(52,1)="Y",R_ZZ9$(173,60)="/tmp"
13045 IF HAS_UF THEN R_ZZ9$(233,22)="tf_tounform",R_ZZ9$(53,60)="[wdx]&TEMP\&TITLE_NS_&BREAK_&SEQ.pdf",R_ZZ9$(113,60)="-dbdshow" ELSE R_ZZ9$(233,22)="tf_topdf",R_ZZ9$(53,60)="[wdx]&TEMP\&TITLE_NS_&BREAK_&SEQ.pdf",R_ZZ9$(113,60)="SHOW;"
13050 EXTRACT (_FIL_NO,KEY=VKY$)ZZ9$
13060 ZZ9$(43)=R_ZZ9$(43) ! Device Driver
13070 WRITE (_FIL_NO,KEY=VKY$)ZZ9$
13100 UPD_LNK_FLE:
13105 READ RECORD (LNK_FLE,IND=0,ERR=*NEXT)LNK_REC$; GOTO 13140
13125 LNK_REC$(1,4)=TSK(-1)
13130 LNK_REC$(5,4)="dev]"
13140 LNK_REC$(9,60)=ZZ9$(173,60)
13155 LNK_REC$(69,12)=ZZ9$(233,12)
13160 WRITE RECORD (LNK_FLE,IND=0)PAD(LNK_REC$,256)
13170 ! 
13180 END_GUI_VIEWER:
13185 CLOSE (_FIL_NO,ERR=*NEXT)
13188 CLOSE (LNK_FLE,ERR=*NEXT)
13190 RETURN 
13199 ! 
13200 FIND_LNK_NAME:
13205 LNK_FLE=HFN; OPEN PURGE (LNK_FLE,ISZ=256,ERR=CREATE_LNK_FILE)VKY$; GOTO FIND_LNK_EXIT
13210 ! EAD RECORD (LNK_FLE,IND=0,ERR=*NEXT)LNK_REC$; GOTO FIND_LNK_EXIT
13220 CREATE_LNK_FILE:
13230 CLOSE (LNK_FLE,ERR=*NEXT)
13240 ERASE VKY$,ERR=*NEXT; GOTO *SAME
13250 SERIAL VKY$,ERR=*NEXT
13260 GOTO FIND_LNK_NAME
13290 FIND_LNK_EXIT:
13295 RETURN 
13299 ! 
13300 ADD_NEW:
13310 ZZ9$(1,2)=VKY$,ZZ9$(233,22)=VW$
13315 ZZ9$(3,40)="New PVX to Viewer"
13320 WRITE (_FIL_NO,KEY=VKY$)ZZ9$
13330 GOTO 13020
13390 ! 
56000 REM "261436-Change graphical version release number from 7.0 to 7.1.    
56001 REM "260723-Import Module/Cash Receipts GW/CIG Plus; Allow selection of 
56002 REM "261434-DBRS Development - Next Phase - Ability to schedule BIRT rpt
56003 REM "269059-Auto expand allows an exit in update                        
56004 REM "273055-Expand IC2 for 6 additional cost breaks, allow entry in GUI 
56006 REM "229993-PCI/PA-DSS New CC Module                                    
56008 REM "275379-Freight Discounts entered by customer.  Need this by cust   
56009 REM "276772-Multiple Finder's Fees per Customer/Item/Order Line.        
56010 REM "298391-Ability to send E/F emails of Invoices after SJ Update      
56011 REM "302865-PXPlus2017.  Report Viewer does not appear to be compatible.
56012 REM "303058-Support for Data Replication of UOM's.                      
56013 REM "303779-File not found XZU when running UT2710.                     
56014 REM "307177-Release utility to check files and parameter setups         
56015 REM "307210-International shipping address - add country code           
56016 REM "307357-Update FS6 to allow for more line numbers                   
56017 REM "307384-DBSPT-127145: Expand queue path in Device Setup             
56018 REM "307387-EQLink DBD Bridge to Qnet                                   
