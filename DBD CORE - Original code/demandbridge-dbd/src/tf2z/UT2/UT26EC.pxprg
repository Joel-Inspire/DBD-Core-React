0010 REM "DB Distributor eCommerce 5.6 Update <UT26EC>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 04/14/22 - 16.523558 - dmm - SSP# 307309
0037 REM "307309-DBD-113 - Authorize.Net support                             
0040 REM "Copyright 2022 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! This program is a replacement for UT25EC as of release 5.6, it is run from UT2560 (SSP224200).
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0105 IF NOT(FN%HAS_MODULE("EC")) THEN GOTO 9930 ! 248802
0110 X0$="UT26EC",X1$="DB Distributor eCommerce 5.6 Update"
0120 DIM Z0$(80,"-"),S$(40)
0130 K0=20,K1=1
0135 C9=-1
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0200 ! 
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0509 REM "Stay away from slot Z(11)
0510 Z$="12O ZZE  13O ZZPARM  "
0520 GOSUB 9750
0600 ! 
0610 GOSUB 6000
0660 PRINT @(0,4),'CE',
1000 ! 
1010 GOSUB 4000
1011 CLOSE (100); OPEN (100)"ZZPARM"; PARMSFOUND$="N"; FIND (100,KEY=X3$(9,3)+"E/C",DOM=9900); PARMSFOUND$="Y"; CLOSE (100)
1015 ! 
1020 IF Q1$<>"*" THEN CALL "ZZPROM",".Y",X3$,Z,"Upgrade existing file specifications?","","",0; ON Z GOTO 1021,1800
1021 ! 
1120 H1$="Customer Primary Functions file to 512, string to 510" ! WO224138
1130 F$="EEC...",F1$="O"; GOSUB 7500; GOSUB 7900 ! WO224138
1140 DIM A[4]; A[0]=K,A[1]=512,A[2]=R1,A[3]=D,A[4]=510; GOSUB 8000 ! WO224138
1149 ! 
1150 H1$="EC Server file to 1024, string to 976" ! SSP242458
1160 F$="ECY...",F1$="O"; GOSUB 7500; GOSUB 7900 ! SSP 242458
1170 DIM A[4]; A[0]=K,A[1]=1024,A[2]=R1,A[3]=D,A[4]=976; GOSUB 8000 ! SSP242458
1179 ! 
1180 H1$=" Order 3rd Party Information file to 576, string to 572" ! SSP307358-DBD-185-DBSPT-115417
1190 F$="EEE...",F1$="O"; GOSUB 7500; GOSUB 7900 ! SSP307358
1200 DIM A[4]; A[0]=K,A[1]=576,A[2]=R1,A[3]=D,A[4]=572; GOSUB 8000 ! SSP307358
1209 ! 
1210 H1$="Worksheet 3rd Party Information file to 576, string to 574" ! SSP307358-DBD-185-DBSPT-115417
1220 F$="EEP...",F1$="O"; GOSUB 7500; GOSUB 7900 ! SSP307358
1230 DIM A[4]; A[0]=K,A[1]=576,A[2]=R1,A[3]=D,A[4]=574; GOSUB 8000 ! SSP307358
1239 ! 
1240 H1$="Credit Card Info File to 576, string to 476" ! SSP307309-DBD-113
1250 F$="EDD...",F1$="O"; GOSUB 7500; GOSUB 7900 ! SSP307309
1260 DIM A[4]; A[0]=K,A[1]=576,A[2]=R1,A[3]=D,A[4]=476; GOSUB 8000 ! SSP307309
1269 ! 
1795 ! 
1800 REM "
1810 IF Q1$<>"*" THEN CALL "ZZPROM",".Y",X3$,Z,"Run special processing updates?","","",0; ON Z GOTO 1811,5000
1990 ! 
1995 PRINT @(0,19),'CE',
1998 CLOSE (100); CLOSE (101)
1999 ! 
2000 REM "update files with standard settings
2010 CLOSE (100); OPEN (100)"ZZPARM"
2020 DIM ECPARM$(1275); EXTRACT (100,KEY=X3$(9,3)+"E/C",DOM=2021)ECPARM$
2030 REM "set order status keyword search values
2031 IF NOT(NUL(ECPARM$(843,10))) THEN GOTO 2042 ! If already filled in, don't overwrite
2032 ECPARM$(843,10)="OSKW_OTYPE"
2033 ECPARM$(853,10)="OSKW_CUSPO"
2034 ECPARM$(863,10)="OSKW_ITEM"
2035 ECPARM$(873,10)="OSKW_SHPTO"
2036 ECPARM$(883,10)="OSKW_CITY"
2037 ECPARM$(893,10)="OSKW_STATE"
2038 ECPARM$(903,10)="OSKW_ZIP"
2039 ECPARM$(913,10)="OSKW_BYDAT"
2040 ECPARM$(923,10)="OSKW_ILINE"
2042 IF ECPARM$(803,20)=DIM(20) THEN ECPARM$(803,20)="Warehouse Release"
2044 IF ECPARM$(823,20)=DIM(20) THEN ECPARM$(823,20)="Reorder"
2046 IF ECPARM$(797,3)=DIM(3) THEN ECPARM$(797,3)="090"
2047 IF ECPARM$(800,3)=DIM(3) THEN ECPARM$(800,3)="045"
2055 WRITE (100,KEY=X3$(9,3)+"E/C")ECPARM$
2099 ! 
2100 REM "update ECU... with User defined sort definitions
2105 CLOSE (100); CLOSE (101); CLOSE (200); CLOSE (201)
2110 OPEN (100,ERR=*NEXT)"tf2w/EC3/ECU000"; GOTO 2120
2115 OPEN (100,ERR=DONE_WITH_ECU)"ECU000"
2120 OPEN (101,ERR=DONE_WITH_ECU)"ECU"+X3$(9,3)
2125 OPEN (200,ERR=*NEXT)"tf2w/EC3/ECV000"; GOTO 2135
2130 OPEN (200,ERR=DONE_WITH_ECU)"ECV000"
2135 OPEN (201,ERR=DONE_WITH_ECU)"ECV"+X3$(9,3)
2140 NEXT_ECU: ECUKEY$=KEY(100,END=DONE_WITH_ECU); READ RECORD (100,KEY=ECUKEY$)ECU$
2145 WRITE RECORD (101,KEY=ECUKEY$,DOM=NEXT_ECU)ECU$ ! if already on file, skip doing ECV and get next ECU to do
2150 REM "update ECV... 
2155 READ (200,KEY=DIM(10)+ECUKEY$,DOM=*NEXT)
2160 NEXT_ECV: ECVKEY$=KEY(200,END=NEXT_ECU); IF NOT(NUL(MID(ECVKEY$,1,10))) OR MID(ECVKEY$,11,LEN(ECUKEY$))<>ECUKEY$ THEN GOTO NEXT_ECU
2165 READ RECORD (200,KEY=ECVKEY$,DOM=NEXT_ECV)ECV$
2170 WRITE RECORD (201,KEY=ECVKEY$)ECV$
2175 GOTO NEXT_ECV
2180 GOTO NEXT_ECU
2190 DONE_WITH_ECU:
2195 CLOSE (100); CLOSE (101); CLOSE (200); CLOSE (201)
2199 ! 
2300 ! SSP184884, setup TopForm standard color scheme records
2305 CLOSE (100); CLOSE (101)
2310 OPEN (100,ERR=*NEXT)"tf2w/EC3/EE2000"; GOTO 2320
2315 OPEN (100,ERR=DONE_WITH_EE2)"EE2000"
2320 OPEN (101,ERR=DONE_WITH_EE2)"EE2"+X3$(9,3)
2325 NEXT_EE2: EE2KEY$=KEY(100,END=DONE_WITH_EE2); READ RECORD (100,KEY=EE2KEY$)EE2$
2330 IF NUM(EE2KEY$,ERR=NEXT_EE2)>1000 THEN GOTO DONE_WITH_EE2 ELSE WRITE RECORD (101,KEY=EE2KEY$)EE2$; GOTO NEXT_EE2
2340 DONE_WITH_EE2:
2345 CLOSE (100); CLOSE (101)
2349 ! 
2350 CALL "ECGUTR;QUIETLY",ERR=*NEXT,X3$,X4$,"","UT25EC"
2351 PERFORM "ECGMMB;SETUP_MESSAGE_08" ! 238095
2352 PERFORM "ECGMMB;SETUP_MESSAGE_63" ! 242686
2399 ! 
2400 ! SSP269627, clear data from OFA-OFF files used for EC order creation, these are temp records and all are leftovers if present.
2410 C5$="OFA...OFB...OFC...OFD...OFE...OFF..."
2420 IF C5$="" THEN GOTO 2490 ELSE C4$=C5$(1,6),C5$=C5$(7)
2430 Q$=C4$(1,3)+X3$(9,3)
2440 H1$="Clearing file "+Q$; GOSUB 8800
2450 F$=C4$; F1$="O"; GOSUB 7500
2460 CALL "ZZINIT",STR(F:"00")
2470 F1$="C"; GOSUB 7500
2480 GOTO 2420
2490 ! Done clearing OFA-OFF files
2495 ! 
3990 GOTO 5000
3995 ! 
4000 REM "Define missing files F5$=Files to check, G5$=use number of records from this exisiting file or if number use it.
4010 F5$="EEB...EEC...EED...EEE...EEF...EEG...EEH...EEI...EEJ...EEK...EEL...EEM...EEP...EEQ...EER...EES..."
4020 G5$="020   020   020   020   020   020   020   020   020   020   020   020   020   020   020   020   "
4030 IF F5$="" THEN GOTO 4190 ELSE F4$=F5$(1,6),F5$=F5$(7),G4$=G5$(1,6),G5$=G5$(7)
4040 FIND (Z[12],KEY=F4$(1,3)+"   ")A$
4050 CLOSE (14); OPEN (14,ERR=4051)F4$(1,3)+X3$(9,3); CLOSE (14); GOTO 4180
4055 R1=NUM(G4$,ERR=4056),D=0; GOTO 4080
4060 F$=G4$; F1$="O"; GOSUB 7500
4070 GOSUB 7900; F1$="C"; GOSUB 7500
4080 DIM A[4]; A[2]=R1,A[3]=D,Q$=F4$(1,3)+X3$(9,3)
4090 IF A$(47,1)="S" THEN T0=3,A[0]=NUM(A$(48,2)) ELSE IF A$(47,1)="D" THEN T0=2,A[0]=NUM(A$(48,2)),A[1]=NUM(A$(50,4)) ELSE IF A$(47,1)="I" THEN T0=0,A[1]=NUM(A$(50,4)) ELSE ESCAPE 
4100 GOSUB 7650
4105 H1$="Defined file "+Q$; GOSUB 8800
4110 IF T0<>0 THEN GOTO 4180
4120 F$=F4$; F1$="O"; GOSUB 7500
4130 CALL "ZZINIT",STR(F:"00")
4140 F1$="C"; GOSUB 7500
4180 GOTO 4030
4190 RETURN 
4195 ! 
5000 REM "EOJ
5010 PRINT @(0,4),'CE',@(0,10),"The automated portion of your DB Distributor eCommerce 5.6 installation is now  complete.  Contact Software Support for further instructions at 770 239-7777."
5020 IF Q1$<>"*" THEN CALL "ZZPROM",".4",X3$,Z,"","","",0
5040 GOTO 9900
5095 ! 
6000 REM "Bkg
6020 PRINT 'SB',@(0,3),"This release program will update eCOMMERCE FUNCTIONS ONLY to level 5.6",@(0,5),"After answering YES to the Proceed question below, new files will be defined and",@(0,7),"some of your data will be adjusted to be compatible with DB Distributor Release 5.6.",@(0,9),"Also, various sort file rebuilds will be run automatically.",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6195 ! 
7500 REM "Open/Close Files
7505 Z$=""
7510 FOR I=1 TO LEN(F$) STEP 6
7515 Z$=Z$+STR(I:"00")+F1$+" "+F$(1,6)+"  "
7520 NEXT I
7525 GOSUB 9750
7530 F=Z[1]
7540 RETURN 
7545 ! 
7600 REM "CREATE TEMP FILE
7610 Q$="-T"+FID(0)
7615 ERASE Q$,ERR=7616; GOTO 7615
7625 IF ERR<>12 THEN GOTO 9000
7630 GOSUB 7650
7635 Z$="11O "+Q$; GOSUB 9750
7640 RETURN 
7645 ! 
7650 REM DEFINE FILE
7660 IF T0=0 THEN INDEXED Q$,A[2],A[1],A[3],0 ELSE IF T0=2 THEN DIRECT Q$,A[0],0,A[1],A[3],0 ELSE IF T0=3 THEN SORT Q$,A[0],0,A[3],0 ELSE ESCAPE 
7690 RETURN 
7695 ! 
7700 REM "EXPAND FILE -NOTE: Cannot expand strings of Indexed files!!!!
7705 C=0
7707 IF T0<>0 THEN EXTRACT (F,KEY="",DOM=7708) ELSE EXTRACT (F,IND=0)
7710 IF T0<>0 THEN K$=KEY(F,END=7780) ELSE SETERR 7790; I0=IND(F)
7715 READ RECORD (F,END=7780)A$
7718 REM LET C=C+1; IF MOD(C,100)=0 THEN LET H1$="Record: "+STR(C); GOSUB 08800
7719 C=C+1; IF MOD(C,T2)=1 THEN GOSUB 8150
7720 IF A[4]=0 OR T=0 THEN GOTO 7735 ELSE P0=POS($8A$=A$); IF P0=0 THEN P0=LEN(A$)+1,A$=A$+$8A$
7721 IF P0-1>A[4] THEN REM CALL "ZZPROM",".5",X3$,A,"String will be truncated!","","",0; ON A GOTO 07722,07900
7722 DIM S$(A[4]); S$(1)=A$(1,P0-1),A$=S$+A$(P0)
7730 REM IF LEN(A$)>A[1] THEN IF POS($00$<>A$(A[1]))>0 THEN CALL "ZZPROM",".5",X3$,A,"Record will be truncated!","","",0; ON A GOTO 07731,07900
7735 IF LEN(A$)>A[1] THEN A$=A$(1,A[1])
7750 IF T0=0 THEN WRITE RECORD (Z[11])A$ ELSE WRITE RECORD (Z[11],KEY=K$)A$
7760 GOTO 7710
7790 SETERR 9000
7795 RETURN 
7799 ! 
7800 REM "RENAME FILES
7810 ERASE "TX"+FID(0),ERR=7811; GOTO 7810
7815 IF ERR<>12 THEN GOTO 9000
7820 Z$="01C "+F$(1,6)+"  "; GOSUB 9750; CLOSE (Z[1],ERR=7821)
7830 RENAME D,F9$,"TX"+FID(0)
7840 TMP=Z[11]; Z$="11C "+Q$+"  "; GOSUB 9750; CLOSE (TMP,ERR=7841)
7845 RENAME A[3],Q$,F9$
7850 ERASE "TX"+FID(0)
7860 H2$="*Finished*"; GOSUB 8800
7890 RETURN 
7895 ! 
7900 ! 
7910 CALL "ZZINFO",F,X0,X3$,R0,R1,K,B,D,T0,S0,F9$
7940 RETURN 
7945 ! 
8000 ! 
8005 GOSUB 8100
8010 H2$="*Starting*"; GOSUB 8800
8040 GOSUB 7600
8050 GOSUB 7700
8060 IF K=0 THEN GOSUB 8200
8070 GOSUB 7800
8090 RETURN 
8095 ! 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T2 to the reporting interval (report every T2 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",F,T9,X3$,T,T2,K,B,D,S0,S1,F$
8115 H2$=STR(T)+" # of records to do "; GOSUB 8800
8129 REM "Set T2, we make sure T2 is > 1, because later on we MOD and look for avalue of 1. IF T2 is 1, then nothing would get reported. We look fora result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T2'th record to get the first report
8130 T2=INT(T*.02); IF T2<1 THEN T2=2
8135 C=0,T1=0
8140 PRINT @(0,4),'CL',
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",4,10,50,T1,T,C
8195 RETURN 
8199 ! 
8200 REM "INDEXED FILES
8210 DIM Y[6]; EXTRACT (Z[11],IND=0)IOL=0290
8220 Y[3]=A[2]-1
8230 WRITE (Z[11],IND=0)IOL=0290
8290 RETURN 
8295 ! 
8600 REM "Expand String-F=file slot, F0$=key prefix, F0=string length
8605 C=0,H2$="*STARTING*"; GOSUB 8800
8610 READ (F,KEY=F0$,DOM=8611)
8620 K$=KEY(F,END=8690)
8630 IF K$(1,LEN(F0$))<>F0$ THEN GOTO 8690
8640 EXTRACT RECORD (F,KEY=K$)A$
8650 P0=POS($8A$=A$); IF P0=0 THEN ESCAPE 
8660 DIM A1$(F0); A1$(1)=A$(1,P0-1),A1$=A1$+A$(P0),A1$=A1$(1,LEN(A$))
8665 C=C+1; IF MOD(C,100)=0 THEN H2$="Record: "+STR(C); GOSUB 8800
8670 WRITE RECORD (F,KEY=K$)A1$
8680 GOTO 8620
8690 H2$="*FINISHED*"; GOSUB 8800
8695 RETURN 
8699 ! 
8800 REM "Display progress  - Eventually write a log file
8810 L=L+1; IF L>15 THEN PRINT @(0,5),'LD',@(0,20),; L=15 ELSE PRINT @(0,L+5),
8815 X$=STR(INT(TIM):"00")+STR(INT(FPT(TIM)*100):"00")
8820 PRINT DAY," @ ",FNT$(X$)," ",H1$," ",H2$
8830 H2$=""
8840 RETURN 
8845 ! 
8900 REM "Special handling for FM4"
8902 DIM B[15]
8905 IOLIST B$,B1$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15]
8910 READ (Z[1],KEY="",DOM=8911)
8920 EXTRACT (Z[1],END=8940)IOL=8905
8930 B1$=""; WRITE (Z[1])IOL=8905; GOTO 8920
8940 RETURN 
8945 ! 
9000 REM "ERROR PROCESSING
9001 ESCAPE 
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; RETRY 
9750 REM 
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9770 ON Z0 GOTO 9771,9900
9790 RETURN 
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 IF PARMSFOUND$<>"Y" THEN CLOSE (100) END_IF ; RUN "ZMENU"
9999 END 
56000 REM "224200-Create UT26EC, call from UT2560. UT2550 will call UT25EC.
56002 REM "224138-Additional to WO215369, need 4 additional fields and tags
56004 ! Capture Four51 info by order, retrieve later for ShipNotice
56005 REM "224917-EC modifications to support B-type orders, special shipping.
56006 REM "234315-Capture lineItemId from Four51 orders, modify Vendor PO
56008 REM "233493-Retrieve/process DSSI PO files, create/send DSSI PO Acks.
56009 REM "238095-Can enter a eCommerce order with a neg. qty. Javascript vali
56010 REM "237271-EC output/input tags for 3rd party frt calculation, share
56012 REM "242458-EC Server feature - Ability to save archived requests to
56014 REM "242686-Custom project using the EC Budget module.                  
56016 ! 247495-EEJ file - EC Worksheet Other
56018 REM "269627-Add clearing of OFA/OFB/OFC/OFD/OFE/OFF files to UT26EC     
56020 REM "229993-PCI/PA-DSS New CC Module                                    
56022 REM "290559-FlowPoint; 3p_full_url tag, files by worksheet and order.   
56024 REM "303296-FlowPoint; Required changes to allow Propago order be an    
56026 REM "307244-DBD-88; Spec URL from DB/a support                          
56028 REM "307319-DBSPT-87098-DBD-147; Change timing of ShipNotice Request to 
56030 REM "307358-DBSPT-115417-DBD-185; Expand EEE/EEP for ORDER_ID_3P_2, add 
56032 REM "307309-DBD-113 - Authorize.Net support                             
