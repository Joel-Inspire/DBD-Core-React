0010 REM "<UT2REV> Compare rel and dev Programs
0035 REM "5.0 - 6/15/01 - plh - SSP# 129412
0036 REM "4.4 - 04/04/00 - 9.17 - cwj - SSP# 104424
0040 REM "Copyright 2000 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
1010 REM "F0$=PROG NAME, F9=1 MEANS FAST CHECK
1011 REM "H0$=string to print in heading (""=no print), H1$, H2$, H3$ and H"+"(all) for future use
1012 P9$="P1"
1013 P9$="LP"
1015 SETERR 7000
1016 REM "V0$ IS OLD VERSION (I.E. 312), V1$ IS NEW (I.E. 300)
1020 ENTER X3$,V0$,V1$,F0$,W9,H0$,H1$,H2$,H3$,H{ALL},V2$
1022 SETERR 9000
1025 IF V0$="" THEN V0$="dev" ELSE V0=1
1026 IF V1$="" THEN V1$="rel" ELSE V1=1
1030 A0$="",A1$=""
1035 IF POS(" "<>V2$)>0 THEN P9$=V2$(1,2)
1039 REM "Get operator code in H8$
1040 CALL "ZSTRTU",X3$,H8$,"",""; IF H8$<>"" THEN H8$="Operator; "+H8$
1050 IF W9=0 THEN PRINT (0,ERR=1051)'CS','CP'
1080 ESCAPE ; IF W9>0 THEN CLOSE (W9)
1085 IF V2$(3,1)="Y" THEN {
1090 W8=18
1095 IF W9>0 THEN {
1100 OPEN (W9)P9$; W8=44
1105 PRINT (W9)'LM','SP',
1110 GOTO 1145
1115  } }
1120 W8=18
1125 IF W9>0 THEN {
1130 OPEN (W9)P9$
1135 W8=50
1140 REM IF P9$="P1" THEN PRINT (W9)'ES',$2873313748$
1145 IF P9$="LP" THEN PRINT (W9)'CP',
1150  }
1155 IF W9<0 THEN W9=10; CLOSE (W9); OPEN (W9)X$; LOCK (W9)
1205 W0=58
1210 W1=W0+2
1220 DIM S$(W1),D9$(W1,".")
1230 DIM Y5$(W1*2+8)
1235 IF F0$="" THEN P5$="(Compares different copies of program)"; GOTO 1250
1236 IF F0$(1,1)=":" THEN F1$=":",F0$=F0$(2)
1240 CALL "ZZ2FNC;GET_PRG_REMARK",F0$,P5$
1245 P5$="Program: "+F0$+" "+P5$
1300 REM Build File Paths
1302 IF F0$="" THEN GOTO 1400
1305 REM IF F1$=":"W0$=V0$+"/"+F0$,W1$=V1$+"/"+F0$;GOTO 01500
1310 IF V0$(1,1)=":" THEN V0$=V0$(2),W0$=V0$+"/"+F0$ ELSE W0$=V0$+"/tf2p/"+F0$(1,3)+"/"+F0$
1340 IF V1$(1,1)=":" THEN V1$=V1$(2); W1$=V1$+"/"+F0$ ELSE W1$=V1$+"/tf2p/"+F0$(1,3)+"/"+F0$
1390 GOTO 1500
1420 CLOSE (14); OPEN (14,ERR=9900)V0$; X$=FID(14); CLOSE (14); OPEN (14)"D"+STR(ASC(X$(20))); X$=FID(14); X$=X$(4,6)+X$(21)
1425 W0$=FNS$(X$)+"/"+V0$(1,3)+"/"+V0$
1440 CLOSE (14); OPEN (14,ERR=9900)V1$; X$=FID(14); CLOSE (14); OPEN (14)"D"+STR(ASC(X$(20))); X$=FID(14); X$=X$(4,6)+X$(21)
1450 W1$=FNS$(X$)+"/"+V1$(1,3)+"/"+V1$
2000 REM "Get older version code
2005 F9$=FID(0)+"TMP"; ERASE F9$,ERR=2006
2010 INVOKE "umask 0;cp "+W0$+" ./UTILS/"+F9$+" > /dev/null"
2020 GOSUB GET_PROG_FILE; A0$=F9_DATA$,F9_DATA$=""
2030 IF A0$>"" THEN GOTO 2100 ELSE W0$=V0$+"/"+F0$(1,3)+"/"+F0$
2032 PRINT "Trying with sub-DIRectory option"
2040 INVOKE "umask 0;cp "+W0$+" ./UTILS/"+F9$+" > /dev/null"
2050 GOSUB GET_PROG_FILE; A0$=F9_DATA$,F9_DATA$=""
2060 IF A0$>"" THEN GOTO 2100 ELSE W0$=V0$+"/tf2p/USR/"+F0$
2070 INVOKE "umask 0;cp "+W0$+" ./UTILS/"+F9$+" > /dev/null"
2080 GOSUB GET_PROG_FILE; A0$=F9_DATA$,F9_DATA$=""
2100 REM "Get newer version code
2105 ERASE F9$,ERR=2106
2110 INVOKE "umask 0;cp "+W1$+" ./UTILS/"+F9$+" > /dev/null"
2120 GOSUB GET_PROG_FILE; A1$=F9_DATA$,F9_DATA$=""
2125 IF A1$>"" THEN GOTO 2200 ELSE W1$=V1$+"/"+F0$(1,3)+"/"+F0$
2126 PRINT "Trying with sub-DIRectory option"
2130 INVOKE "umask 0;cp "+W1$+" ./UTILS/"+F9$+" > /dev/null"
2140 GOSUB GET_PROG_FILE; A1$=F9_DATA$,F9_DATA$=""
2150 IF A1$>"" THEN GOTO 2200 ELSE W1$=V1$+"/tf2p/USR/"+F0$
2160 INVOKE "umask 0;cp "+W1$+" ./UTILS/"+F9$+" > /dev/null"
2170 GOSUB GET_PROG_FILE; A1$=F9_DATA$,F9_DATA$=""
2200 REM Test for results and remove program headers(binary)
2210 IF A0$="" AND A1$="" THEN PRINT @(12,14),"NO PROGRAM LINES TO COMPARE!"; WAIT 1
2220 W=999
2225 A0$=A0$(13),P=13+ASC(A0$(12,1))*3,A0$=A0$(P)
2227 A1$=A1$(13),P=13+ASC(A1$(12,1))*3,A1$=A1$(P)
2240 GOTO COMPARE_LINES
2700 PAGE_HEADING:
2705 IF W9=0 AND P>0 THEN INPUT 'LF',"PRESS RETURN",*; IF CTL=4 THEN GOTO 0010
2710 P=P+1,W=0
2720 IF W9=0 THEN PRINT 'CS' ELSE IF P>1 THEN PRINT (W9)'FF'
2740 Y5$(1)=DAY,Y5$(40)="Compare Development Program with Current Release"; GOSUB OUTPUT_LINE
2750 Y5$(1)=FNT$(TIM),Y5$(40)=P5$; GOSUB OUTPUT_LINE
2753 IF H8$<>"" THEN Y5$(INT((LEN(Y5$)-LEN(H8$))/2))=H8$; GOSUB OUTPUT_LINE
2755 IF H0$<>"" THEN Y5$(INT((LEN(Y5$)-LEN(H0$))/2))=H0$; GOSUB OUTPUT_LINE
2760 GOSUB OUTPUT_LINE; GOSUB OUTPUT_LINE
2800 HEADER_LINE:
2805 C=0,C9=0
2820 Q$=V0$+" Version"; Q1=1; GOSUB 3400
2830 Q$=V1$+" Version",Q1=0; GOSUB 3400
2840 GOSUB OUTPUT_LINE
2850 DIM Q$(W0,"-"); Q1=0; GOSUB 3400; DIM Q$(W0,"-"); Q1=1; GOSUB 3400
2860 GOSUB OUTPUT_LINE
2890 RETURN 
3000 COMPARE_LINES:
3005 IF LEN(A0$)<7 AND LEN(A1$)<7 THEN GOTO 9900
3010 C0=0,C1=0; IF A0$>"" THEN C0=DEC($00$+A0$(2,2))
3012 IF A1$>"" THEN C1=DEC($00$+A1$(2,2)) ELSE C1=C0
3014 IF C0<=C1 THEN GOSUB 6000 ELSE C1$="",B1$=""
3040 IF C1<=C0 AND A1$>"" THEN GOSUB 6100 ELSE C1$="",B1$=""
3044 ESCAPE ; C=C0; IF LEN(C0$)>6 THEN C0$=C0$(7) ELSE C=C1
3045 IF LEN(C1$)>6 THEN C1$=C1$(7)
3046 IF LEN(C0$)>2 AND C0$(2,3)="END" THEN C0$="",A0$="" END_IF ; IF LEN(C1$)>2 AND C1$(2,3)="END" THEN C1$="",A1$=""
3050 IF C0$=C1$ THEN GOTO COMPARE_LINES
3051 REM "*******************************************
3052 SETERR 3053; IF POS("DEF FND$"=LST(B0$))>0 THEN SETERR 9000; GOTO COMPARE_LINES
3053 SETERR 9000
3055 IF W>W8-2 THEN GOSUB PAGE_HEADING
3056 IF C=0 THEN C=C0
3060 IF A0$>"" AND A1$="" THEN GOSUB OUTPUT_LINE; Y5$(35)="***** NEW PROGRAM IN "+W0$+" *****"; GOSUB OUTPUT_LINE; GOTO 9900
3065 GOSUB OUTPUT_LINE; Y5$(1,5)=STR(C:"00000")
3070 IF C0>0 THEN Q1=1,Q$=C0$; GOSUB 3400; C0$=Q$
3080 IF C1>0 THEN Q1=0,Q$=C1$; GOSUB 3400; C1$=Q$
3085 IF Y5$(7,W0)=S$(1,W0) THEN Y5$(7,W1)=D9$
3090 GOSUB OUTPUT_LINE
3094 IF LEN(C0$)>2 AND C0$(2,3)="END" THEN C0$="" END_IF ; IF LEN(C1$)>2 AND C1$(2,3)="END" THEN C1$=""
3095 IF C0$+C1$<>"" THEN GOTO 3070 ELSE GOTO COMPARE_LINES
3400 REM "LIST OUT Q$,Q0=LINE, Q1=0(LEFT), 1(RIGHT)
3410 IF LEN(Q$)<W0 THEN X$=Q$,Q$="" ELSE X$=Q$(1,W0),Q$=Q$(W0+1)
3420 Y5$(8+Q1*W1,LEN(X$))=X$
3490 RETURN 
5000 REM 
5090 GOTO 9900
6000 REM ^ 6000 Convert line to Text in C1$
6010 B0=ASC(A0$(1,1))
6014 L0=2; REM "ssp ?? pet 03/23/2000 for lines > 255 chars
6015 IF B0=0 THEN B0=DEC(A0$(1,3)); L0=4; REM "ssp ?? pet 03/23/2000
6020 IF B0=0 THEN GOTO 6050
6030 B0$=A0$(L0,B0-L0+1),A0$=A0$(B0+1),C0$=LST(B0$)
6040 GOTO 6090
6050 REM "Invalid line length
6060 C0$="Line 1 length exceeds 255 - can not compare"
6080 ESCAPE 
6090 RETURN 
6100 REM ^ 6100 Convert line to Text in C1$
6110 B1=ASC(A1$(1,1))
6114 L0=2; REM "ssp ?? pet 03/23/2000 for lines > 255 chars
6115 IF B1=0 THEN B1=DEC(A1$(1,3)); L0=4; REM "ssp ?? pet 03/23/2000
6120 IF B1=0 THEN GOTO 6150
6130 B1$=A1$(L0,B1-L0+1),A1$=A1$(B1+1),C1$=LST(B1$)
6140 GOTO 6190
6150 REM "Invalid line length
6160 C1$="Line 2 length exceeds 255 - can not compare"
6180 ESCAPE 
6190 RETURN 
7000 REM "ENTER THE DATA
7002 BEGIN 
7003 P9$="LP"
7010 INPUT 'CS',@(30,0),"COMPARE PROGRAMS UTILITY",@(0,5),"ENTER A PROGRAM "+"NAME (OR CR TO SPECIFY 2 DIFFERENT PROGS: ",F0$; IF CTL=4 THEN GOTO 9980
7020 PRINT "Enter the version to compare from (i.e. dev): dev",; INPUT @(46),V0$; IF V0$="" THEN V0$="dev"
7030 INPUT "Enter the version to (i.e. rel) compare to:   rel",; INPUT @(46),V1$; IF V1$="" THEN V1$="rel"
7050 INPUT "Print hard copy? (Y/CR/TMP): ",Q$; IF POS(Q$="Yy")>0 THEN W9=10 ELSE IF Q$="TMP" THEN GOSUB 7200 ELSE IF Q$<>"" THEN GOTO 7050
7060 IF W9<>10 THEN GOTO 7090
7065 INPUT "Enter printer to use: ",V2$; IF LEN(V2$)<>2 THEN GOTO 7065 ELSE V2$=V2$+" "
7070 CLOSE (14); OPEN (14)"ZZ9500"
7080 FIND (14,KEY=V2$(1,2),DOM=7065)V9$; IF V9$(44,8)="HPLASER " THEN V2$(3,1)="Y"
7090 CLOSE (14); GOTO 1021
7200 REM "CREATE & OPEN TEMP FILE
7210 X$="TMP"+FID(0),W9=-1
7215 ERASE X$,ERR=7220; GOTO 7215
7220 SERIAL X$,100,132,8,0
7245 RETURN 
7300 OUTPUT_LINE:
7310 IF W>W8 THEN GOSUB PAGE_HEADING
7320 PRINT (W9)Y5$
7330 W=W+1,Y5$(1)=""
7390 RETURN 
7500 GET_PROG_FILE:REM "Get program file F9$ into F9_DATA$
7510 F9_DATA$=""; CALL "ZZ2BA8",F9$,0,63999,0,F9_DATA$
7545 END_GET_PROG_FILE:RETURN 
8910 DEF FNA(A0)=DEC(A0$(1,3))
8915 DEF FNT$(X)=STR(INT(X):"00")+":"+STR(FPT(TIM)*60:"00")
8920 DEF FNS$(Z$)=Z$(1,POS(S$=Z$+S$)-1)
9001 ESCAPE 
9900 IF W9=0 THEN INPUT (0,ERR=9901)'LF',"FINISHED - PRESS RETURN ",*,'CS','SP'
9940 SETERR 9950; EXIT 
9950 BEGIN ; GOTO 7000
9980 RUN "ZMENU"
9999 END 
