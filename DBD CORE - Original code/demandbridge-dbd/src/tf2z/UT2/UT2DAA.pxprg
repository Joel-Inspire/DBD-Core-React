0010 REM "ANALYSE LINK LIST FILE <UT2DAA>
0100 BEGIN 
0200 REM "F1$= HEADER; F2$= LINE; F3$= WORK FILE; L = ARRAY INDEX TO FIRST RECORD
0210 F1$="AR6500",F2$="ARO500"
0220 F3$="UT0ZZZ"
0310 OPEN (1)F1$
0320 OPEN (2)F2$
0330 OPEN (3)F3$
0370 OPEN (7)FID(0)
0400 DIM H[16],L[6],Y[4]
0410 IOLIST H$,H[0],H[1],H[2],H[3],H[4],H[5],H[6],H[7],H[8],H[9],H[10],H[11],H[12],H[13],H[14],H[15],H[16]
0420 IOLIST L[0],L$,L[1],L[2],L[3],L[4],L[5]
0429 REM "Y(0) NUMBER OF RECORDS ACTUALLY USED, Y(1) INDEX TO FIRST REMOVED RECORD, Y(2) INDEX TO THE FIRST VIRGIN RECORD, Y(3) HIGHEST AVAILABLE INDEX TO THE FILE, Y(4) FILE STATUS -1 MEANS READY AND -2 MEANS BUSY"
0430 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0500 C=0
0520 F$=FID(3); CLOSE (3); ERASE F$(4,6); FILE F$; OPEN (3)F$(4,6)
0600 READ (2,IND=0)IOL=0430
0900 READ (1,KEY="",DOM=0901)
1000 K$=KEY(1,END=4000)
1020 READ (1,KEY=K$)IOL=0410
1040 L=H[0]
2000 IF L<=0 OR L>Y[2] THEN GOTO 3000
2020 READ (2,IND=L)IOL=0420
2180 C=C+1
2200 A$=STR(L:"00000")+K$+STR(C:"00000")
2220 WRITE (3,KEY=A$)
2900 IF L[0]<=0 THEN GOTO 1000
2920 L=L[0]
2940 GOTO 2000
3000 REM "HEADER POINTING TO <=0 INDEX
3020 PRINT (7)"HEADER= ",K$," IS POINTING TO INDEX ",L,'LF'
3040 IF FID(7)=FID(0) THEN INPUT "CR TO CONTINUE ",*
3090 GOTO 1000
4000 REM "PROCESS SORT FILE"
4020 DIM K1$(80)
4040 READ (3,KEY="",DOM=4041)
4060 K$=KEY(3,END=5000)
4070 READ (3)
4080 IF K1$(1,5)<>K$(1,5) THEN GOTO 4900
4100 A$=K$; GOSUB 7000
4120 A$=K1$; GOSUB 7000; PRINT (7)'LF'
4800 IF FID(7)=FID(0) THEN INPUT "CR TO CONTINUE ",*
4900 K1$=K$
4920 GOTO 4060
5000 REM "EOJ"
5020 PRINT "EOJ"
5040 END 
7000 REM "PRINT THE DUPLICATE LIST"
7020 PRINT (7)"INDEX= ",A$(1,5)," HEADER KEY= ",A$(6,LEN(A$)-10)," SEQ CTR= ",A$(LEN(A$)-4,5)
7040 RETURN 
