0010 REM "<UT2LCK> DB Distributor Software Level
0035 REM "5.5 - 05/25/07 - 15.991666 - jme - SSP# 208400
0037 REM "208400-SDLC release process                                        
0040 REM "Copyright 2007 DemandBridge Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0043 REM "OPTIONS$->'U' means unlock if locked, but don't lock if not locked    MUST be followed by the program name in []s or we exit, eg: U[UT2LCK]
0044 REM "OPTIONS$->'L'=list locks by program name, 'S' = list locks by ssp#
0048 REM "RET_CODE 0=OK, 1=PROGRAM LOCKED BY SOMEONE ELSE
0050 SETERR 0055; ENTER OPTIONS$,RET_CODE
0055 ! SETERR 9000
0110 RET_CODE=0
0120 IF MID(OPTIONS$,1,1)="L" THEN START$=OPTIONS$(2); GOSUB LIST_BY_PROG; GOTO 9900; REM "Do list locks by progam name
0130 IF MID(OPTIONS$,1,1)="S" THEN START$=OPTIONS$(2); GOSUB LIST_BY_SSP; GOTO 9900; REM "Do list locks by ssp#
0140 IF MID(OPTIONS$,1,1)="O" THEN BY_OPERATOR$=OPTIONS$(2),START$=""; GOSUB LIST_BY_PROG; GOTO 9900; REM "Do list locks for an operator
0150 P=POS("U"=OPTIONS$); IF P<>0 THEN UNLOCK_ONLY$="Y"; IF LEN(OPTIONS$)<P+3 THEN GOTO 9900 ELSE TEMP$=OPTIONS$(P+2),PROGRAM_NAME$=TEMP$(1,POS("]"=TEMP$+"]")-1); CLOSE (14); OPEN (14,ERR=9900)PROGRAM_NAME$; CLOSE (14) END_IF ELSE UNLOCK_ONLY$="N"
0160 IF UNLOCK_ONLY$<>"Y" THEN CALL "*conv.tbd/window;CREATE",70,15,1,2,"BORDER=LG|TITLE=UT2LCK|TITLEAT=CENTER|NAME=UT2LCK|INIT=CLEAR"
0200 ! ^100 "Obtain operator code
0220 A$=WHO
0230 OPER_CODE$=A$(1,3)
0240 IF OPER_CODE$="" OR FID(0)="" THEN GOTO ERR_OPERATOR
0250 A$=" - "+OPER_CODE$; GOTO 0300
0300 ! ^ 50 "GET CALLING PROGRAM NAME FROM STACK
0310 IF PROGRAM_NAME$<>"" THEN CALLING_PROG$=PAD(PROGRAM_NAME$,8); GOTO 0360
0320 PRGLEVEL=TCB(12)
0330 CALL "ZZ2FNC;GET_PRG_CALL_STACK","UT2LCK",LEVEL
0340 IF LEVEL<=1 THEN GOTO ERR_NO_STACK
0350 CALL "ZZ2FNC;GET_STACK_PRG_NAME",LEVEL-1,CALLING_PROG$
0360 ! Check if not called or called from unsaved command prompt editor session
0370 IF CALLING_PROG$="" OR CALLING_PROG$>="00000" AND CALLING_PROG$<="99999" THEN GOTO ERR_NO_STACK
0380 _PGM=HFN; OPEN (_PGM)CALLING_PROG$; FULL_PROG_NAME$=PTH(_PGM),FULL_PROG_NAME$=FULL_PROG_NAME$(LEN(HWD)+2); CLOSE (_PGM)
0390 CALLING_PROG$=PAD(CALLING_PROG$,8,1)
0400 ! ^50 GET LOCK DATA
0410 CLOSE (14); OPEN (14)"ZWL"
0420 READ (14,KEY=CALLING_PROG$,DOM=0510)LOCK_DATA$; SSP_NO$=LOCK_DATA$(12,6)
0430 IF UNLOCK_ONLY$<>"Y" THEN GOSUB DISPLAY_LOCK
0440 IF MID(LOCK_DATA$,9,3)=OPER_CODE$ THEN GOTO RESTORE_FROM_ARCHIVE_4000
0450 RET_CODE=1
0460 IF UNLOCK_ONLY$="Y" THEN {
0470 GOTO 9900
0480  } ELSE {
0490 PRINT "NOTE: You may only view this locked program.  <RET> to Continue ",; INPUT *,; GOTO 9900
0500  }
0510 ! "only unlocking and the program is not locked, so exit
0520 IF UNLOCK_ONLY$="Y" THEN GOTO 9900
0550 ! ^50 Enter SSP
0560 CLOSE (14); OPEN (14)"ZW2"
0565 XP=PRM('XI')
0570 PRINT "LOCK PROGRAM - Enter 6-digit SSP# (F4 to end): ",; INPUT SSP_NO$,
0572 SET_PARAM 'XI'=XP
0575 IF CTL=4 THEN GOTO 9900
0580 SSP_NO$=STR(NUM(SSP_NO$,ERR=0570):"000000")
0590 SET_PARAM 'XI'; READ (14,KEY=SSP_NO$,DOM=0570,BSY=SSP_BUSY)SSP_DATA$
0591 SET_PARAM 'XI'=XP
0600 PRINT @(5,2),SSP_DATA$(53,60),@(5,3),SSP_DATA$(113,60),
0610 INPUT @(1,5),"Correct? (Y/N): ",Q$
0620 IF POS(Q$="Yy")=0 THEN GOTO 0570
0650 ! ^50 "Archive Version before lock applied
0660 Q$="cd /usr/lib/pvx; cp "+FULL_PROG_NAME$+" archive/"+STP(CALLING_PROG$,1)+"."+"BU"+"; cd /usr/lib/pvx; sudo ./bbaccess.pvx "+FULL_PROG_NAME$+" "+OPER_CODE$+" 644"
0680 INVOKE Q$
0690 DIM LOCK_DATA$(39)
0700 LOCK_DATA$(1,8)=CALLING_PROG$,LOCK_DATA$(9,3)=OPER_CODE$,LOCK_DATA$(12,6)=SSP_NO$,LOCK_DATA$(18,20)=FN%CDS$
0710 CLOSE (14); OPEN (14)"ZWL"
0720 WRITE (14,KEY=LOCK_DATA$(1,8))LOCK_DATA$
0725 CLOSE (14); OPEN (14)"ZV0"; DIM ZV0$(1022); ZV0$(1,6)=LOCK_DATA$(12,6),ZV0$(7,8)=LOCK_DATA$(1,8),ZV0$(17,6)="000000"; WRITE (14,KEY=ZV0$(1,22))ZV0$
0730 CLOSE (14); OPEN (14)"ZWM"
0740 WRITE (14,KEY=LOCK_DATA$(12,6)+LOCK_DATA$(1,8))
0800 ! ^100 "Create Code within Calling Program
0810 A$=A$+" - SSP# "+SSP_NO$
0820 PREINPUT "00035 REM "+QUO+"5.7 - "+DAY+" - "+STR(TIM)+A$
0825 PREINPUT "00037 REM "+QUO+SSP_NO$+"-"+SSP_DATA$(53,60)
0830 DAY_FORMAT READ OLD_FORMAT$
0840 DAY_FORMAT "YYYY"
0850 PREINPUT "00040 REM "+QUO+"Copyright "+DAY+" Demand Bridge, LLC.; Norcross, Georgia"
0860 DAY_FORMAT OLD_FORMAT$
0870 PREINPUT "00041 REM "+QUO+"        Licensed Software - All Rights Reserved."
0880 PREINPUT "9999 END"
0890 REM "
0900 SETERR 0990; Q$=PGM(10,PRGLEVEL); P0$=Q$
0910 Q$=LST(Q$); IF MID(Q$,1,5)<>"00010" THEN PRINT 'RB',"THIS PROGRAM HAS NO LINE 10!"
0920 PRINT 'LF',Q$
0930 SETERR 0990; Q$=PGM(35,MAIN)
0940 PRINT LST(Q$)
0950 PRINT "** BE SURE TO SAVE! **"
0960 PRINT 'LF',
0970 INPUT "<RET> to continue ",*
0980 GOTO 9900
0990 SETERR 0000
1000 PRINT 'RB',"ERROR ENCOUNTERED.  CHECK LINE 10!"
1010 GOTO 9900
3000 DISPLAY_LOCK:REM 
3020 PRINT @(0,1),"Program: "+CALLING_PROG$+" is currently locked",'RB',
3030 PRINT @(0,3),"Operator: ",LOCK_DATA$(9,3),"  SSP#: ",LOCK_DATA$(12,6),"  on "+LOCK_DATA$(18,20),
3040 CLOSE (14); OPEN (14)"ZW2"
3050 READ (14,KEY=LOCK_DATA$(12,6),DOM=3090)SSP_DATA$
3060 PRINT @(10,4),SSP_DATA$(53,60),@(10,5),SSP_DATA$(113,60)
3090 DISPLAY_LOCK_END:RETURN 
4000 REM "RESTORE?
4001 RESTORE_FROM_ARCHIVE_4000:
4005 IF UNLOCK_ONLY$="Y" THEN GOTO 4040; REM "Skip reloading if only unlocking
4010 PRINT @(1,8),"You presently have this program locked.",'LF',"Do you want to restore it to its orginal state? (Y/N): ",; INPUT Q$,
4015 IF POS(Q$="Nn")>0 THEN GOTO 0800 ELSE IF POS(Q$="Yy")=0 THEN GOTO 4010
4020 INPUT @(1,10),"Are You Sure? (Y/N): ",Q$
4025 IF POS(Q$="Nn")>0 THEN GOTO 4010 ELSE IF POS(Q$="Yy")=0 THEN GOTO 4020
4030 REM "Restore file from archive
4040 Q$="cd /usr/lib/pvx; "
4045 IF UNLOCK_ONLY$="Y" THEN {
4050 Q$=Q$+"rm -f archive/"+STP(CALLING_PROG$,1)+".BU;"
4055  } ELSE {
4060 Q$=Q$+"mv archive/"+STP(CALLING_PROG$,2)+".BU "+FULL_PROG_NAME$+";"
4065  }
4070 REM Q$=Q$+"cd /usr/lib/pvx; sudo ./bbaccess.pvx "+FULL_PROG_NAME$+" "+OPER_CODE$+" 666"
4071 Q$=Q$+"cd /usr/lib/pvx; sudo ./bbaccess.pvx "+FULL_PROG_NAME$+" root 644"
4075 INVOKE Q$
4080 IF UNLOCK_ONLY$<>"Y" THEN PRINT "Remember to reload the program!"
4090 ! ^50
4105 CLOSE (14); OPEN (14)"ZWL"
4110 REMOVE (14,KEY=LOCK_DATA$(1,8),DOM=4111)
4115 CLOSE (14); OPEN (14)"ZWM"
4120 REMOVE (14,KEY=LOCK_DATA$(12,6)+LOCK_DATA$(1,8),DOM=4121)
4130 CLOSE (14); OPEN (14)"ZV0"
4135 REMOVE (14,KEY=LOCK_DATA$(12,6)+LOCK_DATA$(1,8)+DIM(2)+"000000",ERR=*NEXT)
4140 CLOSE (14)
4190 IF UNLOCK_ONLY$<>"Y" THEN INPUT "<RET> to continue ",*
4195 GOTO 9900
7500 ! ^ 7500
7510 LIST_BY_PROG:REM "List locks by program name
7520 CALL "*conv.tbd/window;CREATE",49,22,5,1,"BORDER=LG|TITLE=LOCKS BY PROGRAM|TITLEAT=CENTER|NAME=UT2LCK|INIT=CLEAR"
7530 PRINT @(1,0),'SB',"Program",@(12,0),"Opr",@(17,0),"SSP #",@(25,0),"Date / Time",'SF',
7540 CLOSE (14); OPEN (14)"ZWL"; L=1
7550 READ (14,KEY=START$,DOM=7555)
7560 READ (14,END=7665)ZWL$
7570 IF BY_OPERATOR$>"Y" THEN IF MID(ZWL$,9,3)<>BY_OPERATOR$ THEN GOTO 7560 ELSE GOTO 7590
7580 IF MID(ZWL$,1,LEN(START$))<>START$ THEN GOTO 7665
7590 PRINT @(1,L),ZWL$(1,8),@(12,L),ZWL$(9,3),@(17,L),ZWL$(12,6),@(25,L),ZWL$(18,20),
7600 L=L+1
7610 IF L>17 THEN {
7620 PRINT @(1,L),"<RET> to continue <F4> to end"
7630 INPUT *
7640 IF CTL=4 THEN GOTO 7690 ELSE PRINT @(1,1),'CE',; L=1
7650  }
7660 GOTO 7560
7670 PRINT @(1,L),"<RET> to end "
7680 INPUT *
7690 LIST_BY_PROG_END:RETURN 
7700 ! ^ 100
7710 LIST_BY_SSP:REM "List locks by ssp #
7720 CALL "*conv.tbd/window;CREATE",49,22,5,1,"BORDER=LG|TITLE=LOCKS BY SSP|TITLEAT=CENTER|NAME=UT2LCK|INIT=CLEAR"
7730 PRINT @(1,0),'SB'," SSP # ",@(9,0),"Program",@(19,0),"Opr",@(25,0),"Date / Time",'SF',
7740 CLOSE (14); OPEN (14)"ZWL"; L=1
7750 CLOSE (32500); OPEN (32500)"ZWM"
7760 READ (32500,KEY=START$,DOM=7765)
7770 ZWM$=KEY(32500,END=7865); READ (32500); IF MID(ZWM$,1,LEN(START$))<>START$ THEN GOTO 7865
7780 READ (14,KEY=ZWM$(7,8),DOM=7770)ZWL$
7790 PRINT @(1,L),ZWL$(12,6),@(9,L),ZWL$(1,8),@(19,L),ZWL$(9,3),@(25,L),ZWL$(18,20),
7800 L=L+1
7810 IF L>17 THEN {
7820 PRINT @(1,L),"<RET> to continue <F4> to end"
7830 INPUT *
7840 IF CTL=4 THEN GOTO 7890 ELSE PRINT @(1,1),'CE',; L=1
7850  }
7860 GOTO 7770
7870 PRINT @(1,L),"<RET> to end "
7880 INPUT *
7890 CLOSE (32500)
7900 LIST_BY_SSP_END:RETURN 
8000 ! ^8000
8010 ERR_NO_STACK:
8020 PRINT "ERROR - There is no calling program name - no action taken - if new program, first save, then try again. - <RET> to Continue ",
8030 INPUT *,
8040 GOTO 9900
8050 ERR_OPERATOR:
8060 PRINT "No TopForm operator exists.  Login to TopForm and try again ",
8070 INPUT *
8080 GOTO 9900
9000 REM " Error logic
9001 ESCAPE 
9010 GOTO 9900
9900 ! close window
9930 IF UNLOCK_ONLY$<>"Y" THEN CALL "*conv.tbd/window;DELETE","UT2LCK"
9940 EXIT 
9999 END 
10000 SSP_BUSY:! Error 0 on read of SSP, 
10005 SET_PARAM 'XI'
10010 READ (14,KEY=SSP_NO$,DOM=*NEXT)SSP_DATA$
10020 SET_PARAM -'XI'
10095 GOTO 0591
10099 ! *********************************************
56000 ! Program changes starting 4/12/06
56010 REM "194893-Change release from 5.4 to 5.5
56012 REM "208400-SDLC release process                                        
56014 ! Change CHUI release from 5.5 to 5.6.  Change GUI release
56016 REM "212146-New company name. Menu, documentation, etc.
56018 REM "223692-UT2LCK/UT2MOD say DemandBridge, Inc., should be
56020 REM "229899-Change CHUI release to 5.7 and combine all release activity
