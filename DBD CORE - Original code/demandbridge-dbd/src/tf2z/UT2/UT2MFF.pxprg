0010 REM "UT2MFF Utility to create initial Multple Finders Fee Files
0035 REM "5.7 - 10/19/15 - 10.577222 - jvv - SSP# 276772
0037 REM "276772-Multiple Finder's Fees per Customer/Item/Order Line.        
0040 REM "Copyright 2015 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 BEGIN 
0110 X0$="UT2MFF",X1$="Populate Multi Finder Fee Files"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=2,K9=0
0135 C9=-1
0200 REM "
0210 X2=-2
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14],A[15],A[16],A[17],A[18],A[19],A[20],A[21],A[22],A[23],A[24],A[25],A[26],A[27],A[28],A[29]
0320 IOLIST FV$,FV[0],FV[1],FV[2]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20]
0340 IOL_ATI:IOLIST ATI$,ATI[0],ATI[1],ATI[2]
0350 IOL_FV4:IOLIST FV4$,FV4[0],FV4[1],FV4[2]
0360 IOL_FSK:IOLIST FSK$,FSK[0],FSK[1],FSK[2],FSK[3],FSK[4]
0370 IOL_FV7:IOLIST FV7$,FV7[0],FV7[1],FV7[2],FV7[3],FV7[4]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))],D[1]
0510 Z$="01O FS2...  02O FV5...  04O FT4...  05O FV6...  06O FMP...  07O ATI...  08O FM1...  09O FV4...  10O FSK...  11O FV7...  13O ZZPARM"
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0610 GOSUB 6000
0640 CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0910 DIM FV$(125),FV[2]
0920 DIM A$(356),A[29]
0930 DIM FSK$(66),FSK[4]
0940 DIM FV7$(178),FV7[4]
1000 ! Update All Multiple Finders Fee Files
1010 GOSUB UPDATE_CUST
1020 GOSUB UPDATE_ITEM
1030 GOSUB UPDATE_ORDER
1040 GOSUB UPDATE_REPEAT_ORDER
1050 GOSUB UPDATE_REPORT_FF
1060 GOTO 9900
1099 ! 
2000 UPDATE_CUST:
2020 READ (Z[6],KEY="D"+"",ERR=*NEXT)
2030 NEXT_FMP:
2040 KK$=KEY(Z[6],END=END_UPDATE_CUST); READ (Z[6],KEY=KK$)FMP$,FMP
2045 IF KK$(1,1)<>"D" THEN GOTO END_UPDATE_CUST
2048 IF STP(FMP$(13,10),2)<>"" AND FMP$(12,1)<>" " AND FMP<>0 THEN GOTO *NEXT ELSE GOTO NEXT_FMP
2050 FL_KEY$=FMP$(2,10)
2070 GOSUB ADD_NEW_ATI
2075 GOTO NEXT_FMP
2080 END_UPDATE_CUST:
2090 RETURN 
2099 ! 
2500 ADD_NEW_ATI:
2510 DIM ATI$(125),ATI[3]; FF_SEQ=0
2520 ATI$(1,10)=FMP$(2,10),CUSTOMER$=ATI$(1,10)
2530 ATI$(14,1)=FMP$(12,1)
2540 ATI$(15,10)=FMP$(13,10)
2550 ATI[0]=FMP
2560 ADD_CSEQ_NUM:
2565 FF_SEQ=FF_SEQ+1; FF_SEQ$=STR(FF_SEQ:"000")
2570 ATI_KEY$=CUSTOMER$+FF_SEQ$
2575 ATI$(11,3)=FF_SEQ$
2580 WRITE (Z[7],KEY=ATI_KEY$,DOM=ADD_CSEQ_NUM)IOL=IOL_ATI
2595 RETURN 
2599 ! 
3000 UPDATE_ITEM:
3020 READ (Z[8],KEY="",ERR=*NEXT)
3030 NEXT_FM1:
3040 KK$=KEY(Z[8],END=END_UPDATE_ITEM); READ (Z[8],KEY=KK$)FM1$,*,*,*,*,*,*,*,FM1
3045 IF STP(FM1$(116,10),2)<>"" AND FM1$(115,1)<>" " AND FM1<>0 THEN GOTO *NEXT ELSE GOTO NEXT_FM1
3060 GOSUB ADD_NEW_FV4
3070 GOTO NEXT_FM1
3080 END_UPDATE_ITEM:
3090 RETURN 
3099 ! 
3510 ADD_NEW_FV4:
3520 DIM FV4$(135),FV4[3]; FF_SEQ=0
3530 CUSTOMER$=FM1$(1,10),FV4$(1,10)=CUSTOMER$
3540 ITEM_CODE$=FM1$(11,10),FV4$(11,10)=ITEM_CODE$
3550 FV4$(24,1)=FM1$(115,1)
3560 FV4$(25,10)=FM1$(116,10)
3570 FV4[0]=FM1
3580 ADD_ISEQ_NUM:
3590 FF_SEQ=FF_SEQ+1; FF_SEQ$=STR(FF_SEQ:"000")
3600 FV4_KEY$=CUSTOMER$+ITEM_CODE$+FF_SEQ$
3610 FV4$(21,3)=FF_SEQ$
3620 WRITE (Z[9],KEY=FV4_KEY$,DOM=ADD_ISEQ_NUM)IOL=IOL_FV4
3695 RETURN 
3699 ! 
4000 UPDATE_ORDER:! Write FV5/FV6 from FS2/FT4
4010 FL=Z[2]
4020 READ (Z[1],KEY="",ERR=*NEXT)
4030 NEXT_FS2:
4040 KK$=KEY(Z[1],END=END_UPDATE_ORDER); READ (Z[1],KEY=KK$)IOL=0310
4050 IF STP(A$(137,10),2)<>"" AND A$(135,1)<>" " AND A[11]<>0 THEN GOTO *NEXT ELSE GOTO NEXT_FS2
4070 GOSUB ADD_NEW_FF
4075 GOTO NEXT_FS2
4080 END_UPDATE_ORDER:
4090 RETURN 
4099 ! 
5000 UPDATE_REPEAT_ORDER:! Write FT4 file to FV6
5010 FL=Z[5]
5020 READ (Z[4],KEY="",ERR=*NEXT)
5030 NEXT_FT4:
5040 KK$=KEY(Z[4],END=END_UPDATE_REPEAT_ORDER); READ (Z[4],KEY=KK$)IOL=0310
5050 IF STP(A$(137,10),2)<>"" AND A$(135,1)<>" " AND A[11]<>0 THEN GOTO *NEXT ELSE GOTO NEXT_FT4
5070 GOSUB ADD_NEW_FF
5075 GOTO NEXT_FT4
5080 END_UPDATE_REPEAT_ORDER:
5090 RETURN 
5099 ! 
5400 UPDATE_REPORT_FF:
5420 READ (Z[10],KEY="",ERR=*NEXT)
5430 NEXT_FSK:
5440 KK$=KEY(Z[10],END=END_UPDATE_REPORT_FF); READ (Z[10],KEY=KK$)IOL=IOL_FSK
5460 GOSUB ADD_NEW_FV7
5470 GOTO NEXT_FSK
5480 END_UPDATE_REPORT_FF:
5490 RETURN 
5499 ! 
5600 ADD_NEW_FV7:
5605 SEQ_NUM=0 ! This is refering toINV_SEQ_NUM(21,3) 
5610 FV7$(1,6)=FSK$(1,6)
5620 FV7$(7,8)=FSK$(21,8) ! Inv num
5625 FV7$(15,6)="001001" ! ,FV7$(21,3)="001"
5630 FV7$(24,14)=FSK$(7,14)
5640 FV7$(38,38)=FSK$(29,38)
5650 FOR XX=0 TO 4; FV7[XX]=FSK[XX]; NEXT XX
5660 FV7_KEY$=FV7$(1,20),FV7_SEQ$="001"
5699 ! 
5700 WRITE_MFEE:
5710 SEQ_NUM=SEQ_NUM+1; SEQ_NUM$=STR(SEQ_NUM:"000")
5720 FV7_WKEY$=FV7_KEY$+SEQ_NUM$ ! +FV7$(21,3)
5730 FV7$(21,3)=SEQ_NUM$
5740 WRITE (Z[11],KEY=FV7_WKEY$,DOM=WRITE_MFEE)IOL=IOL_FV7
5780 MFEE_UPD_END:
5790 RETURN 
5799 ! 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(3,4),"This utility will update the new Multi Vendor Finder Fee files for",@(3,5),"default Customer, default items, orders and repeat orders." ! (15,6),"...Progress will be reported every 100 records...", ! SSP#236052
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7000 ADD_NEW_FF:
7010 DIM FV$(136),FV[3]; FF_SEQ=0
7020 ORD_LINE$=KK$
7030 FV$(1,11)=ORD_LINE$
7040 FV$(15,1)=A$(135,1) ! FIND_FEE_TYPE
7050 FV$(16,10)=A$(137,10) ! Finder Fee Vendor
7060 FV[0]=A[11] ! FIND_FEE_RATE
7100 ADD_SEQ_NUM:
7110 FF_SEQ=FF_SEQ+1; FF_SEQ$=STR(FF_SEQ:"000")
7120 FV_KEY$=ORD_LINE$+FF_SEQ$
7130 FV$(12,3)=FF_SEQ$
7140 WRITE (FL,KEY=FV_KEY$,DOM=ADD_SEQ_NUM)IOL=0320
7180 RETURN 
7190 ! 
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 RUN "ZMENU"
9999 END 
