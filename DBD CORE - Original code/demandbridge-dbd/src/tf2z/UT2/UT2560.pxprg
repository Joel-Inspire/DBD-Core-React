0010 REM "DemandBridge Release 5.6 Update <UT2560>
0020 SETESC 9300; SETERR 9000
0025 ! Changing to release 5.7 on 5/27/09, use UT2700 instead
0035 REM "5.7 - 06/03/09 - 8.888888 - dmm - SSP# 229899
0037 REM "229899-Change CHUI release to 5.7 and combine all release activity 
0040 REM "Copyright 2009 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="UT2560",X1$="DemandBridge Release 5.6 Update"
0120 DIM Z0$(80,"-"),S$(40)
0130 K0=20,K1=1
0135 C9=-1
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0509 REM "Stay away from slot Z(11)
0510 Z$="12O ZZE  13O ZZPARM  "
0520 GOSUB 9750
0600 REM "
0610 GOSUB 6000
0640 IF Q1$<>"*" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
1000 REM 
1005 PRINT @(0,4),'CE',
1010 GOSUB 4000
1090 ! 
1100 H1$="A/R Transaction file, expand string to 70"; REM "SSP#209664
1110 F$="AR7...",F1$="O"; GOSUB 7500; GOSUB 7900
1120 DIM A[4]; A[0]=K,A[1]=256,A[2]=R1,A[3]=D,A[4]=70; IF K<>0 THEN GOSUB 8000; REM "K=Key size if 0 still index file do not expand string
1130 H1$="A/P Transaction file, expand string to 80"; REM "SSP#209664
1140 F$="APJ...",F1$="O"; GOSUB 7500; GOSUB 7900
1150 DIM A[4]; A[0]=K,A[1]=256,A[2]=R1,A[3]=D,A[4]=70; IF K<>0 THEN GOSUB 8000; REM "K=Key size if 0 still index file do not expand string
1155 IF K<>0 THEN CALL "UT2HIS",ERR=*NEXT,X3$,X4$,"","*" ! SSP#209664
1160 H1$="O/S Price Plan Priorites expand string to 185"
1170 F$="OS6...",F1$="O"; GOSUB 7500; GOSUB 7900
1180 DIM A[4]; A[0]=K,A[1]=185,A[2]=R1,A[3]=D,A[4]=185; IF K<>0 THEN GOSUB 8000
1185 ! 
1190 H1$="Order Header file to 896, string to 606" ! WO212768
1200 F$="FS1...",F1$="O"; GOSUB 7500; GOSUB 7900
1210 DIM A[4]; A[0]=K,A[1]=896,A[2]=R1,A[3]=D,A[4]=606; GOSUB 8000
1215 ! 
1220 H1$="Repeat Order Header file to 896, string to 606" ! WO212768
1230 F$="FT3...",F1$="O"; GOSUB 7500; GOSUB 7900
1240 DIM A[4]; A[0]=K,A[1]=896,A[2]=R1,A[3]=D,A[4]=606; GOSUB 8000
1245 ! 
1250 F$="SMC...",F1$="O"; GOSUB 7500; GOSUB 7900 ! SSP#217970
1255 DIM A[4]; A[0]=K,A[1]=768,A[2]=R1,A[3]=D,A[4]=355; GOSUB 8000 ! SSP#217970
1258 ! 
1260 H1$="Comm Booked by Line to 576, string to 200" ! WO217566
1265 F$="AS9...",F1$="O"; GOSUB 7500; GOSUB 7900 ! SSP#217566
1270 DIM A[4]; A[0]=K,A[1]=576,A[2]=R1,A[3]=D,A[4]=200; GOSUB 8000 ! SSP#217566
1990 ! 
2000 REM "Erase files
2095 ! 
3000 REM "Set Parameter Records
3095 ! 
3500 REM "Rebuilds-
3595 ! 
3990 GOTO 5000
3995 ! 
4000 REM "Define missing files F5$=Files to check, G5$=use number of records from this exisiting file or if number use it.
4010 F5$="DTO...DTF...DTS...DTY...DTG...CD1...DTC...OSM...SMC...OSU...FTU...FTV...IL2...ZYG...TW3...TW2...DRF..." ! SSP 219381 SSP 219370
4020 G5$="020   020   020   020   020   020   020   020   020   020   020   020   020   020   020   020   020   " ! SSP 219381 SSP 219370
4030 IF F5$="" THEN GOTO 4190 ELSE F4$=F5$(1,6),F5$=F5$(7),G4$=G5$(1,6),G5$=G5$(7)
4040 FIND (Z[12],KEY=F4$(1,3)+"   ")A$
4050 CLOSE (14); OPEN (14,ERR=4051)F4$(1,3)+X3$(9,3); CLOSE (14); GOTO 4180
4055 R1=NUM(G4$,ERR=4056),D=0; GOTO 4080
4060 F$=G4$; F1$="O"; GOSUB 7500
4070 GOSUB 7900; F1$="C"; GOSUB 7500
4080 DIM A[4]; A[2]=R1,A[3]=D,Q$=F4$(1,3)+X3$(9,3)
4090 IF A$(47,1)="S" THEN T0=3,A[0]=NUM(A$(48,2)) ELSE IF A$(47,1)="D" THEN T0=2,A[0]=NUM(A$(48,2)),A[1]=NUM(A$(50,4)) ELSE IF A$(47,1)="I" THEN T0=0,A[1]=NUM(A$(50,4)) ELSE ESCAPE 
4095 IF F4$(1,3)="DTF" THEN A[1]=22800 ! SSP212088      
4100 GOSUB 7650
4105 H1$="Defined file "+Q$; GOSUB 8800
4110 IF T0<>0 THEN GOTO 4180
4120 F$=F4$; F1$="O"; GOSUB 7500
4130 CALL "ZZINIT",STR(F:"00")
4140 F1$="C"; GOSUB 7500
4180 GOTO 4030
4190 RETURN 
4195 ! 
5000 REM "EOJ
5010 PRINT @(0,3),'CE',@(0,10),"The automated portion of your DemandBridge Release 5.6 installation is now      complete.  Contact DBI Support for further instructions at 770 239-7777."
5020 IF Q1$<>"*" THEN CALL "ZZPROM",".4",X3$,Z,"","","",0
5040 GOTO 9900
5095 ! 
6000 REM "Bkg
6020 PRINT 'SB',@(0,3),"The DemandBridge Release 5.6 updates will be performed automatically by this    program.",@(0,6),"After answering YES to the Proceed question below, new files will be defined and",@(0,7),"some of your data will be adjusted to be compatible with DemandBridge Release   5.6.",@(0,10),"Also, various sort file rebuilds will be run automatically."
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6195 ! 
7500 REM "Open/Close Files
7505 Z$=""
7510 FOR I=1 TO LEN(F$) STEP 6
7515 Z$=Z$+STR(I:"00")+F1$+" "+F$(1,6)+"  "
7520 NEXT I
7525 GOSUB 9750
7530 F=Z[1]
7540 RETURN 
7600 REM "CREATE TEMP FILE
7610 Q$="-T"+FID(0)
7615 ERASE Q$,ERR=7616; GOTO 7615
7625 IF ERR<>12 THEN GOTO 9000
7630 GOSUB 7650
7635 Z$="11O "+Q$; GOSUB 9750
7640 RETURN 
7645 ! 
7650 REM DEFINE FILE
7660 IF T0=0 THEN INDEXED Q$,A[2],A[1],A[3],0 ELSE IF T0=2 THEN DIRECT Q$,A[0],0,A[1],-A[3],0 ELSE IF T0=3 THEN SORT Q$,A[0],0,A[3],0 ELSE ESCAPE ! -A[3] -> negative value means create a variable length record. This will allow multi segments
7690 RETURN 
7695 ! 
7700 REM "EXPAND FILE -NOTE: Cannot expand strings of Indexed files!!!!
7705 C=0
7707 IF T0<>0 THEN EXTRACT (F,KEY="",DOM=7708) ELSE EXTRACT (F,IND=0)
7710 IF T0<>0 THEN K$=KEY(F,END=7780) ELSE SETERR 7790; I0=IND(F)
7715 READ RECORD (F,END=7780)A$
7718 REM LET C=C+1; IF MOD(C,100)=0 THEN LET H1$="Record: "+STR(C); GOSUB 08800
7719 C=C+1; IF MOD(C,T2)=1 THEN GOSUB 8150
7720 IF A[4]=0 OR T=0 THEN GOTO 7735 ELSE P0=POS($8A$=A$); IF P0=0 THEN P0=LEN(A$)+1,A$=A$+$8A$
7721 IF P0-1>A[4] THEN REM CALL "ZZPROM",".5",X3$,A,"String will be truncated!","","",0; ON A GOTO 07722,07900
7722 DIM S$(A[4]); S$(1)=A$(1,P0-1),A$=S$+A$(P0)
7730 REM IF LEN(A$)>A[1] THEN IF POS($00$<>A$(A[1]))>0 THEN CALL "ZZPROM",".5",X3$,A,"Record will be truncated!","","",0; ON A GOTO 07731,07900
7735 IF LEN(A$)>A[1] THEN A$=A$(1,A[1])
7750 IF T0=0 THEN WRITE RECORD (Z[11])A$ ELSE WRITE RECORD (Z[11],KEY=K$)A$
7760 GOTO 7710
7790 SETERR 9000
7795 RETURN 
7799 ! 
7800 REM "RENAME FILES
7810 ERASE "TX"+FID(0),ERR=7811; GOTO 7810
7815 IF ERR<>12 THEN GOTO 9000
7820 Z$="01C "+F$(1,6)+"  "; GOSUB 9750; CLOSE (Z[1],ERR=7821)
7830 RENAME F9$,"TX"+FID(0)
7840 TMP=Z[11]; Z$="11C "+Q$+"  "; GOSUB 9750; CLOSE (TMP,ERR=7841)
7845 RENAME Q$,F9$
7850 ERASE "TX"+FID(0)
7860 H2$="*Finished*"; GOSUB 8800
7890 RETURN 
7895 ! 
7900 REM 
7910 CALL "ZZINFO",F,X0,X3$,R0,R1,K,B,D,T0,S0,F9$
7940 RETURN 
7945 ! 
8000 REM 
8005 GOSUB 8100
8010 H2$="*Starting*"; GOSUB 8800
8040 GOSUB 7600
8050 GOSUB 7700
8060 IF K=0 THEN GOSUB 8200
8070 GOSUB 7800
8090 RETURN 
8095 ! 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T2 to the reporting interval (report every T2 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",F,T9,X3$,T,T2,K,B,D,S0,S1,F$
8115 H2$=STR(T)+" # of records to do "; GOSUB 8800
8129 REM "Set T2, we make sure T2 is > 1, because later on we MOD and look for avalue of 1. IF T2 is 1, then nothing would get reported. We look fora result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T2'th record to get the first report
8130 T2=INT(T*.02); IF T2<1 THEN T2=2
8135 C=0,T1=0
8140 PRINT @(0,4),'CL',
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",4,10,50,T1,T,C
8195 RETURN 
8199 ! 
8200 REM "INDEXED FILES
8210 DIM Y[6]; EXTRACT (Z[11],IND=0)IOL=0290
8220 Y[3]=A[2]-1
8230 WRITE (Z[11],IND=0)IOL=0290
8290 RETURN 
8295 ! 
8600 REM "Expand String-F=file slot, F0$=key prefix, F0=string length
8605 C=0,H2$="*STARTING*"; GOSUB 8800
8610 READ (F,KEY=F0$,DOM=8611)
8620 K$=KEY(F,END=8690)
8630 IF K$(1,LEN(F0$))<>F0$ THEN GOTO 8690
8640 EXTRACT RECORD (F,KEY=K$)A$
8650 P0=POS($8A$=A$); IF P0=0 THEN ESCAPE 
8660 DIM A1$(F0); A1$(1)=A$(1,P0-1),A1$=A1$+A$(P0),A1$=A1$(1,LEN(A$))
8665 C=C+1; IF MOD(C,100)=0 THEN H2$="Record: "+STR(C); GOSUB 8800
8670 WRITE RECORD (F,KEY=K$)A1$
8680 GOTO 8620
8690 H2$="*FINISHED*"; GOSUB 8800
8695 RETURN 
8699 ! 
8800 REM "Display progress  - Eventually write a log file
8810 L=L+1; IF L>15 THEN PRINT @(0,5),'LD',@(0,20),; L=15 ELSE PRINT @(0,L+5),
8815 X$=STR(INT(TIM):"00")+STR(INT(FPT(TIM)*100):"00")
8820 PRINT DAY," @ ",FNT$(X$)," ",H1$," ",H2$
8830 H2$=""
8840 RETURN 
8845 ! 
8900 REM "Special handling for FM4"
8902 DIM B[15]
8905 IOLIST B$,B1$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15]
8910 READ (Z[1],KEY="",DOM=8911)
8920 EXTRACT (Z[1],END=8940)IOL=8905
8930 B1$=""; WRITE (Z[1])IOL=8905; GOTO 8920
8940 RETURN 
8945 ! 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; RETRY 
9750 REM 
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9770 ON Z0 GOTO 9771,9900
9790 RETURN 
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 ! Starting this new release program on 10/29/07
56010 REM "209664-Cust# 03-BRINK showing inv #'s from C/R 2002 are pulling in 
56012 REM "213071-Change CHUI release from 5.5 to 5.6.  Change GUI release
56013 REM "213820-PE(001, OS2MAA-6710) when in price plan priorities.
56014 REM "212674-Add more freight discounts based on weight.                 
56016 REM "212768-Add Blanket PO feature to work with the Contract feature.
56017 REM "220872-DBE - Alpha numeric order numbers will not allow you to     
56018 REM "208706-Audit control numbers are duplicating from last year        
56019 REM "215368-Create Monthly Budget feature by customer location.
56021 REM "223557-Modify Data Replication with the ability to filter data
56022 ! "225427-Florida Sales Tax - each county can set a sales tax percent,
56024 REM "227714-WMS: Change the WMS user setup, allow multiple WMS roles to 
56026 REM "229899-Change CHUI release to 5.7 and combine all release activity
