0010 REM "Compare Direct Files <UT2CDF>"
0020 SETESC 9300; SETERR 9000
0035 REM "3.7 - 04/13/94 - 10.19 - kmc
0040 REM "Copyright 1994 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0110 X0$="UT2CDF",X1$="Compare Direct Files",K9$="",K9=0
0120 DIM Z0$(80,"-"),S0$(80)
0130 K0=2,K1=1
0135 C9=-1,V9=-2
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(65,1)>"2" THEN PRECISION NUM(X3$(65,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1)
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O ZZE...  13O ZZPARM  "
0520 GOSUB 9750; ON Z0 GOTO 0521,9900
0600 REM "
0680 GOSUB 6000
1000 REM "
1020 CLOSE (11); CLOSE (12)
1030 DIM B$(200),B1$(200),C$(200)
1035 DIM A$(80)
1040 A$(1)=A1$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 SETERR 9000; C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1250,1300,1400,1350,1500
1200 REM "Source Directory A$(1,30)
1202 A$(1,30)="dev",A$(31,30)="360"
1205 IF C0<>2 AND POS(" "<>A$(1,30))<>0 THEN PRINT @(23,3),A$(1,30),; GOTO 1240
1209 X$="The closer full path name, the quicker the search"
1210 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,23,3,1,30,C0,"","{1"+X$,"","UT2CDF00","","","  H"; IF C0=6 THEN GOSUB 6400; C0=1
1212 IF INT(ABS(C0))=3 THEN C0=-ABS(C0); READ (Z[1],KEY=A$(1,2),DOM=1213)
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9800,6970
1240 GOTO 1120
1250 REM "Destination Directory A$(31,30)
1260 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,23,4,31,30,C0,"","{3"+X$,"","UT2CDF02","","",""; IF C0=6 THEN GOSUB 6400; C0=1
1290 GOTO 1120
1300 REM "New File Name A$(61,8)
1310 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,23,5,61,8,C0,"","{3"+X$,"","UT2CDF04","","",""; IF C0=6 THEN GOSUB 6400; C0=1
1320 DIM B$(200); FIND (Z[2],KEY=A$(61,3)+"   ",DOM=1321)B$
1325 PRINT @(32,5),B$(7,35),
1330 A$(71,8)=A$(61,8); PRINT @(23,6),A$(71,8); GOTO 1420
1340 GOTO 1120
1350 REM "Output Device A$(69,2)
1355 X$="Blank for terminal or LP, P1, etc"
1360 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,23,7,69,2,C0,"","{3"+X$,"","UT2CDF06","","",""; IF C0=6 THEN GOSUB 6400; C0=1
1365 IF POS(" "<>A$(69,2))=0 THEN A$(69,2)=FID(0); PRINT @(23,7),A$(69,2)
1370 CLOSE (10); OPEN (10,ERR=1360)A$(69,2)
1375 IF FID(10)=FID(0) THEN W0=20 ELSE W0=60
1390 GOTO 1120
1400 REM "Current File Name A$(71,8)
1405 IF ABS(C0)<>2 THEN GOTO 1120
1410 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,23,6,71,8,C0,"","{3"+X$,"","UT2CDF08","","",""; IF C0=6 THEN GOSUB 6400; C0=1
1420 DIM B1$(200); FIND (Z[2],KEY=A$(71,3)+"   ",DOM=1421)B1$
1425 PRINT @(32,6),B1$(7,35),
1440 GOTO 1120
1500 REM "
1505 CALL "ZZPROM","10",X3$,Z,"","","",0
1510 IF Z=0 THEN X0=2; GOTO 1140
2000 REM "compare the key files
2010 REM "link the files using a system call into Disk 8, should be utils, under temporary names, then open, compare, and then unlink
2015 REM "Find file first
2020 GOSUB 8000
2025 F0$="TMP1"+FID(0),F1$=FNS$(A$(1,30))+"/"+P0$+"/"+FNS$(A$(61,8))
2030 F2$="TMP2"+FID(0),F3$=FNS$(A$(31,30))+"/"+P0$+"/"+FNS$(A$(71,8))
2040 ERASE F0$,ERR=2041
2042 ERASE F2$,ERR=2043
2050 S0$="rm UTILS/"+F0$+" UTILS/"+F2$+" 2>/dev/null; ln "+F1$+" UTILS/"+F0$+"; ln "+F3$+" UTILS/"+F2$
2055 INVOKE S0$
2060 CLOSE (11); CLOSE (12)
2065 OPEN (11)F0$
2070 OPEN (12)F2$
2100 REM "Setup to print
2105 W=0; DIM Y5$(79)
2110 IF FID(10)=FID(0) THEN PRINT 'CS',
2130 GOSUB 2800
2200 REM 
2205 K0=0,K1=0; REM "IF 1 then at end of that file
2210 GOSUB 2400; GOSUB 2450
2218 REM "check for end of file cases, then check for key mismatches,etc.
2220 IF K0=1 AND K1=1 THEN GOTO 2295 ELSE IF K0=1 THEN Y5$(40)=K1$; GOSUB 2300; GOSUB 2450; GOTO 2220 ELSE IF K1=1 THEN Y5$(1)=K0$; GOSUB 2300; GOSUB 2400; GOTO 2220
2225 IF K0$=K1$ THEN GOSUB 2400; GOSUB 2450; GOTO 2220 ELSE IF K0$>K1$ THEN Y5$(40)=K1$; GOSUB 2300; GOSUB 2450; GOTO 2220 ELSE IF K0$<K1$ THEN Y5$(1)=K0$; GOSUB 2300; GOSUB 2400; GOTO 2220
2295 IF FID(10)=FID(0) THEN GOSUB 8200
2296 S0$="rm UTILS/"+F0$+" UTILS/"+F2$+" 2>/dev/null"; REM "remove the links we set up
2298 INVOKE S0$
2299 GOTO 0010
2300 REM "Print a line, checking for end of page
2310 IF W>W0 THEN Y7$=Y5$; GOSUB 2800; Y5$=Y7$
2315 GOSUB 7300
2345 RETURN 
2400 REM "Read file 12 (0)
2405 K0$=KEY(12,END=2406); READ (12); GOTO 2410
2406 K0=1
2445 RETURN 
2450 REM "read file 11 (1)
2455 K1$=KEY(11,END=2456); READ (11); GOTO 2460
2456 K1=1
2495 RETURN 
2800 REM "Print Heading
2805 IF W<>0 THEN W=0; IF FID(10)<>FID(0) THEN PRINT 'FF', ELSE CALL "ZZPROM",".5",X3$,Z0,"","","",0; IF Z0=1 THEN EXITTO 8200 ELSE PRINT 'CS',
2810 Y5$(1)=FND$(X3$(21,6)); GOSUB 7300
2815 Y5$(1)="Compare Direct Files"; GOSUB 7300
2820 Y5$(1)=F3$,Y5$(40)=F1$; GOSUB 7300
2825 GOSUB 7300
2845 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(5,3),"   New DIRectory:",@(0,4),"    Current DIRectory:",@(8,5),"New file name:",@(4,6),"Current file name:",@(8,7),"Output device:",@(0,8),Z0$,
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6390 RETURN 
6400 REM "WHOLE SCREEN
6402 IF C9=0 THEN IF ABS(C0)=17 THEN GOTO 6970
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 PRINT @(23,3),A$(1,30),@(23,4),A$(31,30),@(23,5),A$(61,8),@(32,5),B$(7,35),@(23,6),A$(71,8),@(32,6),B1$(7,35),@(23,7),A$(69,2),
6490 RETURN 
7300 REM "Print Y5$ on (10)
7310 PRINT (10)@(0),Y5$
7315 W=W+1
7320 DIM Y5$(79)
7345 RETURN 
8000 REM "Get path from A$(1,30) DIR to A$(61,8) program
8005 J0$=FNS$(A$(1,30)),J1$=FNS$(A$(61,8)),J3$="TMP"+FID(0)
8010 J2$="find "+J0$+" ! -type d -a -name "+J1$+" -a -print > /usr/lib/pvx/UTILS/"+J3$
8015 PRINT @(15,20),"Searching for the file, one moment please..."
8020 ERASE J3$,ERR=8021; GOTO 8020
8025 INVOKE J2$
8030 CLOSE (14,ERR=8031)
8035 OPEN (14)J3$
8036 PRINT @(0,20),'CL',
8038 REM "Read in path, we assume we will get 1 or 0 (not found). If more than one, then we always get first one, give warning of more
8039 B0$=""
8040 READ RECORD (14,END=8041)B0$
8045 IF POS($00$<>B0$)=0 OR LEN(B0$)=0 THEN J4$="Program "+J1$+" not found in source DIRectory "+J0$+"!!"; CALL "ZZPROM",".4",X3$,0,J4$,"","",0; EXITTO 1100
8060 REM "B0$ should have source DIR on front and file name on back, remove those, leaving just the path between intact.
8061 REM "Special case with files of 3 chars, check for dev/tf2z/ZZL/ZZL type case, where DIR name may mask real file name
8062 IF LEN(J1$)=3 THEN J=POS(J1$+"/"+J1$=B0$); IF J<>0 THEN P0$=B0$(1,J+2); GOTO 8066
8065 P0$=B0$(1,POS(J1$=B0$+J1$)-2); REM "Remove trailing '/' alsoe
8070 IF P0$(1,LEN(J0$))=J0$ THEN IF LEN(P0$)<=LEN(J0$) THEN P0$="" ELSE P0$=P0$(LEN(J0$)+2); REM "Remove leading "/" also
8090 PRINT @(0,20),'CE',
8095 RETURN 
8100 REM "Print a message (M$) to the archive log
8105 M0$=FND$(X3$(21,6))+" "+STR(TIM:"00.00")
8109 M8$=FNS$(A$(31,30))+"/"+"archive"+"/"+"tflog"
8110 M9$="echo "+QUO+M0$+QUO+" "+A$(61,8)+" $LOGNAME "+QUO+M$+QUO+" >>"+M8$
8125 INVOKE M9$
8145 RETURN 
8200 REM "done with print to screen
8205 CALL "ZZPROM",".4",X3$,0,"","","",0
8245 RETURN 
8905 DEF FNP$(Z9$)="("+Z9$(1,3)+") "+Z9$(4,3)+"-"+Z9$(7,4)+" Ext: "+Z9$(11,4)
8910 DEF FND$(Z9$)=Z9$(3,2)+"/"+Z9$(5,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8920 DEF FNB$(Z9$)=X3$(214-6*NUM(Z9$),1)
8925 DEF FNS$(Z9$)=Z9$(1,POS(S0$=Z9$+S0$)-1)
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5); IF Y5=68 OR Y5=69 THEN GOTO 9500
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9120 ON Y8 GOTO 9900,0990,9920
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)<>"1" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; SETESC 9300; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9740 RETURN 
9750 REM "FILES
9760 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
9790 RETURN 
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9905 Y8=2
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
