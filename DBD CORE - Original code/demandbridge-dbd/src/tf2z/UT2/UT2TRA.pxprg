0010 REM " A/R TRANSACTION REBUILD <UT2TRA>
0050 BEGIN 
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0310 IOLIST A1,A$,A[0],A[1]
0320 IOLIST A$,A[0],A[1]; REM FOR WRITE PURPOSES ONLY
0330 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14]
0340 IOLIST C$,C[0],C[1]
0350 IOLIST D$,D[0],D[1],D[2],D[3],D[4],D[5],D[6]
0400 DIM Y[5],A[2],B[14],C[2],D[6]
0410 M1$="0000"
0420 D0$="I81128"
0430 ERASE "AR7DIR",ERR=0431
0440 DIRECT "AR7DIR",20,4000,96
0500 REM " OPEN FILES
0510 OPEN (1)"AR7001X"
0520 OPEN (2)"AR6001"
0530 OPEN (3)"AR8001"
0550 OPEN (5)"AR7DIR"
0570 OPEN (7)"LP"
0575 OPEN (10)"AR6NEW"
0580 OPEN (11)"AR8NEW"
0590 OPEN (12)"AR7NEW"
1000 REM " CREATE DIRECT FILE FROM INDEXED AR7...
1010 READ (1,IND=0)IOL=0290
1015 IF Y[1]<=0 THEN GOTO 1090
1020 A1=Y[1]
1025 REM " BUILD DELETE CHAIN LIST IN I$
1030 READ (1,IND=A1)A2
1035 IF POS(STR(A2:M1$)=I$)<>0 THEN GOTO 1090
1040 I$=I$+STR(A1:M1$),A1=A2
1050 IF A1<=0 THEN GOTO 1090
1060 GOTO 1025
1090 L=1
2000 REM " BUILD DIRECT FILE SKIPPING ITEMS ON DELETE CHAIN
2010 READ (1,IND=L,END=2090)IOL=0310
2020 IF POS(STR(L:M1$)=I$)<>0 THEN L=L+1; GOTO 2010
2022 IF POS("9999"=A$)<>0 THEN GOTO 2060
2025 Y$="00",Y=0
2028 IF LEN(A$)<18 THEN GOTO 2060
2030 READ (5,KEY=A$(1,18)+Y$,DOM=2050)
2040 Y=Y+1; Y$=STR(Y:"00"); GOTO 2030
2050 WRITE (5,KEY=A$(1,18)+Y$)IOL=0320
2055 PRINT A$(1,18)+Y$
2060 L=L+1; IF L>=Y[2] THEN GOTO 2090 ELSE GOTO 2010
3000 REM " PROCESS HEADER FILE AND CHECK BALANCE AGAINST TRANSACTIONS 
3001 REM " IF NOT IN BALANCE THEN SET UP BALANCING TRANSACTION.
3010 K$=KEY(2,END=3090)
3020 READ (2,KEY=K$)IOL=0330
3025 PRINT K$
3030 Y=0,Y$="00",T=0
3040 IF B[2]=B[3] THEN GOTO 3010 ELSE GOSUB 6000
3045 IF POS("ONACCNT"=B$)<>0 THEN GOTO 3080
3050 REM IF B(2)+T<>B(3) THEN PRINT (7) B$(1,18)," ",B(2)," + ",T," = ",B(3)," + ",B(4); ESCAPE 
3060 IF B[2]+T<>B[3] THEN C$=B$(1,18)+"P"+D0$+"*UT2TRA*"+"999999"+"198903",C[0]=(-1)*(B[2]-B[3]+T),C[1]=0; WRITE (5,KEY=C$(1,18)+Y$)IOL=0340
3080 GOTO 3000
4000 REM " PROCESS HISTORY HEADER FILE AND CHECK BALANCE AGAINST TRANS.
4001 REM " IF NOT IN BALANCE THEN SET UP BALANCING TRANSACTION.
4010 K1$=KEY(3,END=4090)
4015 PRINT K1$
4020 READ (3,KEY=K1$)IOL=0350
4025 B$=D$(1,18)
4030 Y=0,Y$="00",T=0
4040 GOSUB 6000
4043 REM IF D(4)+T<>0 THEN PRINT (7) D$(1,18)," ",D(4)," + ",T
4045 IF D[4]+T<>0 THEN C$=D$(1,18)+"P"+D0$+"*UT2TRA*"+"999999"+"198903",C[0]=(-1)*(D[4]+T),C[1]=0; WRITE (5,KEY=C$(1,18)+Y$)IOL=0340
4050 REM IF D(4)>0 THEN IF D(4)+T<>0 THEN LET C$=D$(1,18)+"P"+D0$+"*UT2TRA*"+"999999"+"198903",C(0)=D(4)+T,C(1)=0; WRITE (5,KEY=C$(1,18)+Y$) IOL=0340
4080 GOTO 4000
5000 REM " REBUILD AR7DIR INTO AN INDEXED FILE WHILE ALSO SETTING THE INDEX'S IN THE 'AR6' AND THE 'AR8' FILES
5005 I=1
5010 K2$=KEY(2,END=5200)
5015 PRINT K2$
5020 READ (2,KEY=K2$)IOL=0330
5022 Y=0,Y$="00"
5025 IF B[2]=B[3] THEN B[1]=0; WRITE (10,KEY=K2$)IOL=0330; GOTO 5010
5030 B[1]=I
5040 WRITE (10,KEY=K2$)IOL=0330
5060 GOSUB 6200
5090 GOTO 5010
5200 REM DUE AR8 RECORDS
5210 K3$=KEY(3,END=5300)
5215 PRINT K3$
5220 READ (3,KEY=K3$)IOL=0350
5222 Y=0,Y$="00"
5230 D[0]=I
5240 WRITE (11,KEY=K3$)IOL=0350
5250 GOSUB 6200
5290 GOTO 5210
5999 GOTO 9900
6000 REM " PROCESS TRANSACTIONS
6001 REM " RETURN TRANSACTION VALUE IN T
6010 READ (5,KEY=B$(1,18)+Y$,DOM=6090)A$,A[0],A[1]
6020 T=T+A[0]+A[1]
6030 Y=Y+1; Y$=STR(Y:"00")
6040 GOTO 6010
6095 RETURN 
6200 REM " PROCESS TRANSACTIONS AND WRITE OUT TO FILE
6210 READ (5,KEY=B$(1,18)+Y$,DOM=6090)A$,A[0],A[1]
6260 Y=Y+1; Y$=STR(Y:"00")
6263 A1=I+1
6265 READ (5,KEY=B$(1,18)+Y$,DOM=6266); GOTO 6270
6268 A1=0
6270 WRITE (12,IND=I)A1,A$,A[0],A[1]; I=I+1
6280 GOTO 6210
6290 RETURN 
9900 PRINT "EOJ"
9999 END 
