0010 REM "Define Files for a company <UT2DEF>
0040 REM "Copyright 1987 Computer Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0100 BEGIN 
0110 OPEN (1)"ZZE"
0500 INPUT 'CS',"Setup files for a company.  Enter Company: ",C0$
0520 IF C0$="" THEN GOTO 9900
0530 IF LEN(C0$)>3 THEN GOTO 0500 ELSE C0$=STR(NUM(C0$,ERR=0500):"000")
0540 IF C0$<"001" OR C0$>"999" THEN GOTO 0500
0550 PRINT "Company: ",C0$
1000 INPUT "ENTER FILE NAME OR CR FOR NEXT: ",F$
1010 IF F$="END" THEN GOTO 9000
1020 IF F$="" THEN F$=KEY(1,END=5000); READ (1); IF F$(4,3)<>"   " THEN F$=""; GOTO 1020 ELSE GOTO 1060
1040 IF LEN(F$)<>3 THEN GOTO 1000 ELSE F$=F$+"   "
1060 READ (1,DOM=1000,KEY=F$)A$
1080 PRINT "FILE: ",A$(1,3),C0$,"-",A$(7,40),"  TYPE: ",A$(47,1)," KEY: ",A$(48,2)," BPR: ",A$(50,4)
1100 F$(4,3)=C0$; OPEN (2,ERR=1160)F$; CLOSE (2); GOTO 1101
1120 INPUT (0,ERR=1120)"FILE ALREADY DEFINED! SKIP? ",'RB',Q$:("Y"=1000,"N"=1121)
1140 ERASE F$
1160 IF A$(47,1)<>"S" THEN GOTO 1200
1170 OPEN (14,ERR=1200)A$(50,3)+C0$
1175 U$=FID(14),R=DEC(U$(12,3))
1180 PRINT "  MASTER FILE RECORDS = ",R,'RB'
1185 CLOSE (14)
1200 INPUT "  ENTER RECORDS: ",R
1205 IF R=0 THEN GOTO 1000
1210 IF POS(A$(47,1)="DSI")=0 THEN ESCAPE 
1220 IF A$(47,1)="D" THEN DIRECT F$,NUM(A$(48,2)),R,NUM(A$(50,4))
1240 IF A$(47,1)="S" THEN SORT F$,NUM(A$(48,2)),R
1260 IF A$(47,1)="I" THEN INDEXED F$,R,NUM(A$(50,4)); OPEN (2)F$; CALL "ZZINIT","02"; CLOSE (2)
1300 INPUT "  COPY RECORDS FROM COMPANY 500? (Y/CR): ",Q$
1320 IF Q$<>"Y" THEN GOTO 1900
1330 OPEN (2)F$
1340 OPEN (3,ERR=1341)F$(1,3)+"500"; GOTO 1360
1350 INPUT "  CAN'T OPEN FILE FOR COMPANY 500! CR TO CONTINUE ",*; GOTO 1900
1360 K1$=KEY(3,END=1420); READ RECORD (3,KEY=K1$)R1$
1370 PRINT "  ",K1$,
1380 WRITE RECORD (2,KEY=K1$)R1$
1400 GOTO 1360
1420 CLOSE (2); CLOSE (3)
1900 PRINT "  FILE ",F$," DEFINED!"
1990 GOTO 1000
5000 REM "EOF"
5020 INPUT "END OF FILE - CR TO CONTINUE ",*
5040 READ (1,DOM=1000,KEY=""); GOTO 1000
6000 REM "COPY CSYSFL TO *VBU"
6020 BEGIN 
6030 INPUT 'CS',"COPY CSYSFL TO *VBU - CR TO CONTINUE ",*
6040 OPEN (1)"CSYSFL"+FID(0)
6060 OPEN (2,ERR=6061)"*VBU"; INPUT "CLEAR *VBU? (CR/N): ",Q$; IF Q$="N" THEN GOTO 6080 ELSE CLOSE (2); ERASE "*VBU"
6070 CWDIR "/usr/lib/pvx/UTILS"; SORT "*VBU",9,500,8,0; CWDIR HWD; OPEN (2)"*VBU"
6080 K$=KEY(1,END=9000); READ (1)
6100 OPEN (3,ERR=6080)K$
6120 F$=FID(3); CLOSE (3)
6140 WRITE (2,KEY=STR(DEC(F$(20,1)))+K$)
6160 GOTO 6080
7000 REM "
7020 BEGIN 
7040 INPUT 'CS',"COPY ZZPARM TO ZZPARM* FOR COMPANY 001 - CONTINUE? ",Q$
7060 IF Q$<>"Y" THEN GOTO 9000
7080 OPEN (2,ERR=7100)"ZZPARM*"; CLOSE (2); ERASE "ZZPARM*"
7100 DIRECT "ZZPARM*",16,300,896
7120 OPEN (1)"ZZPARM"
7140 OPEN (2)"ZZPARM*"
7160 K$=KEY(1,END=9000); READ RECORD (1,KEY=K$)R$
7170 PRINT K$," ",
7180 IF K$(1,3)="500" THEN K$(1,3)="001"
7190 IF R$(1,3)="500" THEN R$(1,3)="001"
7200 WRITE RECORD (2,KEY=K$)R$
7220 GOTO 7160
9900 REM "EOJ"
9940 RUN "ZMENU"
9999 END 
