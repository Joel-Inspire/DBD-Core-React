0010 REM "PII Masking Utility <UT2PII>                                     
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 08/09/22 - 11.065084 - crg - SSP# 307414
0037 REM "307414-DBD-246 PII masking utility                                 
0040 REM "Copyright 2022 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="UT2PII",X1$="PII Masking Utility"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "                                                                 
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS                                                          
0310 IOLIST A$
0320 IOLIST B$
0500 REM "FILES                                                            
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0540 GOSUB 6000; FILE_NAME$=""; INPUT EDIT (0,ERR=0540)@(4,10),"PII Fields List (Excel): ",FILE_NAME$; IF CTL=4 OR NUL(FILE_NAME$) THEN GOTO 9900
0541 INPUT EDIT (0,ERR=0541)@(6,11),"Default email address: ",DEFAULT_EMAIL_ADDR$; IF CTL=4 OR NUL(DEFAULT_EMAIL_ADDR$) THEN GOTO 9900
0581 PIILIST_OK=0; ORIG_FILE_NAME$=FILE_NAME$; GOSUB GET_PIIFIELDS_LIST; IF NOT(PIILIST_OK) THEN GOTO 0540
0600 REM "                                                                 
0610 GOSUB 6000
0620 GOSUB 8100
0640 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0900 ! 
0905 LOG_OPTIONS$="A"; CALL "ZZ2LOG;OPEN_LOG",ERR=*NEXT,X3$,X4$,FN%GET_BASENAME$(PGN)+"."+FID(0)+".log",LOG_OPTIONS$,LOG_CHANNEL
0915 FN_LOGMSG("=== Start ===",%LOG_INFO)
0916 FN_LOGMSG("PII File|"+ORIG_FILE_NAME$+"|",%LOG_INFO)
0917 FN_LOGMSG("Fields|"+STR(T)+"|",%LOG_INFO)
0989 ! 
0990 READ (PIICHAN,KEY="",DOM=*NEXT)
0999 ! 
1000 REM "BEGIN MAIN PROCESS                                               
1010 PIIK$=KEY(PIICHAN,END=5000); READ (PIICHAN,KEY=PIIK$)IOL=IOL_PII
1015 IF TABLE_NAME$<>OLD_TABLE_NAME$ THEN OLD_TABLE_NAME$=TABLE_NAME$; GOSUB GET_PIIFIELDS_FOR_FILE
1020 PRINT @(10,14),'CL',OLD_TABLE_NAME$,
1030 GOSUB CONVERT_FILE
1600 GOTO 1000
5000 REM "EOJ                                                              
5010 C=T; GOSUB 8150
5015 GOSUB PROCESS_SORTS
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5025 FN_LOGMSG("=== End ===",%LOG_INFO)
5030 CALL "ZZ2LOG;CLOSE_LOG",ERR=*NEXT,X3$,X4$,LOG_CHANNEL
5040 GOTO 9900
6000 REM "BACKGROUND                                                       
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This utility program will mask the specified DB Distributor data so as to make "
6021 PRINT @(0,5),"it safe for external use without the risk of exposing customer/user PII."
6024 PRINT @(0,7),"IMPORTANT: DO NOT RUN IN A LIVE DBD DATASET, USE THIS UTILITY IN A TEST DATASET ONLY."
6025 PRINT @(4,10),"PII Fields List (Excel): ",
6026 PRINT @(6,11),"Default email address: ",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
8100 ! 
8110 T=NUM(FIN(PIICHAN,"NUMREC"))
8115 PRINT @(0,12),"There are "+STR(T)+" records to process"
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 ! 
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,C
8195 RETURN 
8199 ! 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING                                                 
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL                                
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC                                       
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM                                      
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 ! 
10000 GET_PIIFIELDS_LIST:
10005 IOL_PII:IOLIST TABLE_NAME$,COLUMN_NAME$,COLUMN_TYPE$,COLUMN_SIZE$,DESCRIPTION$,COLUMN_OFFSET$,KEYDEF$,PII_TYPE$,COMMENTS$,SORT$,REBUILD$
10007 CALL "[WDX]*CMDWORD;GET_EXCEL1",ERR=*NEXT,AA_OBJ,FILE_NAME$
10010 OPEN (HFN,ERR=*RETURN)"[DDE]Excel;"+FILE_NAME$; EXCEL=LFO; I=2 ! Open Excel sheet and skip header row
10012 OPEN (HFN)"*MEMORY*"; PIICHAN=LFO
10020 READ DATA FROM RCD(EXCEL,KEY="R"+STR(I)+"C1:R"+STR(I)+"C15",END=10080),SEP=$09$ TO IOL=IOL_PII
10025 I++
10035 IF NUL(TABLE_NAME$) OR NUL(COLUMN_NAME$) THEN GOTO 10080
10037 TABLE_NAME$=UCS(TABLE_NAME$),COLUMN_NAME$=UCS(COLUMN_NAME$),PII_TYPE$=UCS(PII_TYPE$)
10040 WRITE (PIICHAN,KEY=PAD(TABLE_NAME$,60)+PAD(COLUMN_NAME$,60))IOL=IOL_PII
10070 GOTO 10020
10085 CLOSE (EXCEL)
10086 CALL "[WDX]*CMDWORD;CLOSE",ERR=*NEXT,AA_OBJ
10090 PIILIST_OK=1
10095 RETURN 
10099 ! 
10100 GET_PIIFIELDS_FOR_FILE:
10105 DIM PIIFLD$[100,5]; NUM_FIELDS=0
10110 ++NUM_FIELDS; PIIFLD$[NUM_FIELDS,1]=TABLE_NAME$,PIIFLD$[NUM_FIELDS,2]=COLUMN_NAME$,PIIFLD$[NUM_FIELDS,3]=COLUMN_OFFSET$,PIIFLD$[NUM_FIELDS,4]=COLUMN_SIZE$,PIIFLD$[NUM_FIELDS,5]=PII_TYPE$
10120 PIIK$=KEY(PIICHAN,END=*RETURN); READ (PIICHAN,KEY=PIIK$)IOL=IOL_PII
10130 IF TABLE_NAME$=OLD_TABLE_NAME$ THEN GOTO 10110 ELSE READ (PIICHAN,KEY=TABLE_NAME$,DOM=*NEXT) ! If we moved past current table into the next table, then reset pointer to start of next table and return
10195 RETURN 
10199 ! 
10200 CONVERT_FILE:
10205 TOTAL_RECS=0,FC=0
10207 FN_LOGMSG("File|"+OLD_TABLE_NAME$+"|Start|",%LOG_INFO)
10210 OPEN (HFN)MID(UCS(OLD_TABLE_NAME$),1,3)+%C$; FCHAN=LFO
10215 TOTAL_RECS=NUM(FIN(FCHAN,"NUMREC")); PRINT @(45,14),'CL',"Total: ",STR(TOTAL_RECS),@(60,14),"Processed: 0",
10220 FKEY$=KEY(FCHAN,END=10280); READ RECORD (FCHAN,KEY=FKEY$)FREC$
10225 ++FC; IF MOD(FC,1000)=0 THEN PRINT @(60,14),'CL',"Processed: ",STR(FC),
10230 CHANGE_FLAG=0; GOSUB CONVERT_RECORD
10240 IF CHANGE_FLAG THEN WRITE RECORD (FCHAN,KEY=FKEY$)FREC$
10270 GOTO 10220
10280 PRINT @(60,14),'CL',"Processed: ",STR(FC),
10290 CLOSE (FCHAN)
10292 FN_LOGMSG("File|"+OLD_TABLE_NAME$+"|End|"+STR(FC)+"|",%LOG_INFO)
10295 RETURN 
10299 ! 
10300 CONVERT_RECORD:
10305 IF NUL(FKEY$) THEN RETURN 
10310 FOR IDX=1 TO NUM_FIELDS
10315 COLOFF=NUM(PIIFLD$[IDX,3]),COLLEN=NUM(PIIFLD$[IDX,4]); IF NUL(FREC$(COLOFF,COLLEN)) THEN CONTINUE
10320 SWITCH UCS(PIIFLD$[IDX,5])
10321 CASE "ADDRESS"
10322 IF POS("2"=PIIFLD$[IDX,2]) THEN FREC$(COLOFF,COLLEN)="Addr 2 ["+STP(FKEY$)+"]" ELSE FREC$(COLOFF,COLLEN)="Addr ["+STP(FKEY$)+"]"
10329 BREAK
10331 CASE "PHONENUMBER"
10332 RND$=STR(INT(RND*1000000):"0000000"); FREC$(COLOFF,COLLEN)=MID(RND$,1,3)+"555"+MID(RND$,4)
10339 BREAK
10341 CASE "NAME"
10342 FREC$(COLOFF,COLLEN)="Name ["+STP(FKEY$)+"]"
10349 BREAK
10351 CASE "EMAIL"
10352 FREC$(COLOFF,COLLEN)=DEFAULT_EMAIL_ADDR$
10359 BREAK
10380 END SWITCH 
10385 CHANGE_FLAG=1
10390 NEXT IDX
10395 RETURN 
10399 ! 
10400 PROCESS_SORTS:
10405 FN_LOGMSG("Sort Utilities|Start|",%LOG_INFO)
10410 IF FN_FILE_WAS_CONVERTED("AP4") THEN {
10411 CALL "AP2UT5",X3$,X4$,"*","*"
10412 FN_LOGMSG("AP4|AP2UT5|",%LOG_INFO)
10414  }
10415 IF FN_FILE_WAS_CONVERTED("AR1") THEN {
10416 CALL "AR2UT8",X3$,X4$,"*","*"
10417 FN_LOGMSG("AR1|AR2UT8|",%LOG_INFO)
10419  }
10420 IF FN_FILE_WAS_CONVERTED("AR3") THEN {
10421 CALL "AR2UT9",X3$,X4$,"*","*"
10422 FN_LOGMSG("AR3|AR2UT9|",%LOG_INFO)
10424  }
10425 IF FN_FILE_WAS_CONVERTED("FM0") THEN {
10426 CALL "FM2UTT",X3$,X4$,"*","**"
10427 FN_LOGMSG("FM0|FM2UTT|",%LOG_INFO)
10429  }
10430 IF FN_FILE_WAS_CONVERTED("ZP2") THEN {
10431 CALL "ZP2UT0",X3$,X4$,"*","*"
10432 FN_LOGMSG("ZP2|ZP2UT0|",%LOG_INFO)
10434  }
10440 IF FN_FILE_WAS_CONVERTED("XPA") THEN {
10441 FN_LOGMSG("XPA|XPA-FMP|",%LOG_INFO)
10442 OPEN (HFN)"XPA"+%C$; XPACHAN=LFO
10443 OPEN (HFN)"FMP"+%C$; FMPCHAN=LFO
10445 XPAK$=KEY(XPACHAN,END=10448); READ RECORD (XPACHAN,KEY=XPAK$)XPAREC$
10446 WRITE RECORD (FMPCHAN,KEY=XPAK$)XPAREC$
10447 GOTO 10445
10448 CLOSE (XPACHAN); CLOSE (FMPCHAN)
10449  }
10490 FN_LOGMSG("Sort Utilities|End|",%LOG_INFO)
10495 RETURN 
10499 ! 
20000 DEF FN_FILE_WAS_CONVERTED(LOCAL FILE$)
20010 WAS_CONVERTED=0
20020 READ (PIICHAN,KEY=FILE$,DOM=*PROCEED); IF POS(FILE$=KEY(PIICHAN,END=*NEXT)) THEN WAS_CONVERTED=1
20095 RETURN WAS_CONVERTED
20098 END DEF
20099 ! 
20100 DEF FN_LOGMSG(LOCAL MSG$, LOCAL LOGLEVEL)
20120 CALL "ZZ2LOG;LOG_MSG",ERR=*NEXT,X3$,X4$,LOG_CHANNEL,MSG$,LOGLEVEL
20195 RETURN 0
20198 END DEF
20199 ! 
56000 ! 
56001 REM "307414-DBD-246 PII masking utility                                 
