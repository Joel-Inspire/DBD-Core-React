0010 REM "Using ZZE check to see if files defined <UT2UZF>
0020 SETESC 9300; SETERR 9000
0035 REM "5.5 - 05/29/07 - 12.298888 - lms - SSP# 205315
0037 REM "205315-Make sure the utility that creates files by module          
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="UT2UZF",X1$="Be sure all ZZE files are defined"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 ! ^ 200
0201 HEADER:
0210 F0$="AP AR AS DR FE FM FS FT FW FX GL IC OS PO QS SM ZE "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF MID(X3$,66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 ! ^ 300 "IOLISTS
0310 IOLIST A$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O ZZE...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 ! ^ 600 DISPLAY PANEL
0610 GOSUB DISPLAY_BACKGROUND
0640 IF Q1$<>"*" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0900 DIM A[7]
0990 PRINT @(10,13),"File", ! k$ KEY OF ZZE
1000 ! ^ 1000
1010 MAIN:
1020 K$=KEY(Z[1],END=5000); READ (Z[1],KEY=K$)IOL=0310
1025 PRINT @(20,13),K$,
1035 IF MID(K$,1,3)="ZYP" THEN GOTO CHECK_FOR_FILE
1040 IF POS(MID(K$,1,2)+" "=F0$,3)=0 OR MID(A$,7,1)="*" THEN GOTO MAIN
1100 ! ^ 1100
1101 CHECK_FOR_FILE:
1110 CLOSE (14,ERR=*NEXT)
1120 OPEN (14,ERR=FILE_MISSING)K$(1,3)+X3$(9,3); GOTO MAIN
1150 ! ^ 50
1160 FILE_MISSING:
1180 F$=K$(1,3)+X3$(9,3); DIM F[5]
1200 X$=F$+" Missing!  Define "+FNS$(A$(7,39))+"?"
1220 CALL "ZZPROM",".Y",X3$,Z,X$,"","",0; ON Z GOTO MAKE_FILE,MAIN
1300 ! ^ 1300
1310 MAKE_FILE:
1320 ! LOAD ZZE FILE PARAMETERS
1330 F[0]=NUM(A$(48,2),ERR=*NEXT)
1340 F[1]=NUM(A$(50,4),ERR=*NEXT)
1350 F[2]=NUM(A$(54,4),ERR=*NEXT)
1360 ! 
1370 SWITCH A$(47,1)
1380 CASE ="D"
1390 IF F[0]=0 OR F[1]=0 THEN GOTO ERR_DEF
1391 F[2]=0 ! ; GOSUB NUMREC_ENTRY
1410 DIRECT F$,F[0],F[2],-F[1]; REM SSP 205315
1420 BREAK
1430 CASE ="S"
1440 IF F[0]=0 THEN GOTO ERR_DEF
1441 F[2]=0 ! ; GOSUB NUMREC_ENTRY
1460 SORT F$,F[0],F[2]
1470 BREAK
1480 CASE ="I"
1490 IF F[1]=0 THEN GOTO ERR_DEF
1491 IF F[2]=0 THEN F[2]=20
1500 GOSUB NUMREC_ENTRY
1510 INDEXED F$,F[2],F[1]
1520 CLOSE (14); OPEN (14)F$; CALL "ZZINIT","14"; CLOSE (14)
1530 BREAK
1540 END SWITCH 
1541 ! 
1550 GOTO MAIN
1600 ! ^ 100
1610 ERR_DEF:
1620 X$="Incorrect file definition data in ZZE for file "+F$
1630 CALL "ZZPROM",".4",X3$,0,X$,"","",0
1640 GOTO MAIN
2000 ! ^ 2000
2010 NUMREC_ENTRY:
2020 PRINT @(10,18),"Enter number of records ",@(38,18),F[2]
2040 INPUT @(38,18),Q$
2060 IF Q$<>"" THEN F[2]=NUM(Q$,ERR=NUMREC_ENTRY)
2080 PRINT @(0,18),'CL',
2100 RETURN 
5000 REM "EOJ
5020 IF Q1$<>"*" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 ! ^ 6000
6001 DISPLAY_BACKGROUND:
6005 PRINT (0,ERR=*NEXT)'SB',
6020 PRINT @(0,4),"This program processes ZZE and checks that files are defined that begin with ",F0$
6165 PRINT (0,ERR=*NEXT)'SF',
6190 RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF MID(X3$,47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "205315-Make sure the utility that creates files by module    
