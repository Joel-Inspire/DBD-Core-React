0010 REM ""EC Support Issue summary Processing <EC4IS1>"
0020 SETESC 9300; SETERR 9000
0035 REM "5.4 - 04/26/05 - 16.098333 - jme - SSP# 183431
0040 REM "Copyright 2005 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT
0100 SETERR 9000
0110 X0$="EC4IS1",X1$="EC Support Issue Summary Processing"
0120 EOL$=$0D0A$; REM EOL$=ATH(STP(EC_PARM$(57,8),1))
0127 HTML_TEXT$="Content-type: text/html"
0140 DIM MESS_INFO$[20],EC_PARM$(1275)
0210 DIM ESA$(1540),ESA2$(1000)
0220 DIM ESB$(1984),ESB[3]
0230 DIM ESY$(512)
0240 DIM ECC$(1984),ECC[3]
0250 DIM ESZ$(128),ESZ[18]
0260 DIM SS1$(726)
0270 DIM ZW2$(411),ZW2[3]
0300 DIM ZWT$(126)
0310 IOLIST ESA$,ESA2$
0320 IOLIST ESB$,ECB[0],ECB[1],ECB[2]
0330 IOLIST ESY$
0340 IOLIST ECC$,ECC[0],ECC[1],ECC[2]
0350 IOLIST ESZ$,ESZ[0],ESZ[1],ESZ[2],ESZ[3],ESZ[4],ESZ[5],ESZ[6],ESZ[7],ESZ[8],ESZ[9],ESZ[10],ESZ[11],ESZ[12],ESZ[13],ESZ[14],ESZ[15],ESZ[16],ESZ[17],ESZ[18]
0360 IOLIST SS1$
0370 IOLIST ZW2$,ZW2[0],ZW2[1],ZW2[2]
0380 IOLIST ZW9A$,ZW9B$
0400 IOLIST ZW7A$,ZW7B$
0410 IOLIST ZW8A$
0420 IOLIST ZWT$
0500 REM "Open files
0505 DIM Z[NUM(X3$(60,3))],BLGRBNKS$(100)
0510 Z$="010 ESA... 02O ESB... 03O ESY... 04O ESC... 05O ESZ... 06O SS1    07O ZW2    08O ZW9    09O ZWD    10O ZWY    11O ZW8    12O ZWT    13O ZWE    14O ZWF    15O ZWH    "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0900 PRINT_DOC=0
1000 REM "Process DATA array
1005 BEGDAT$="",ENDDAT$="",PRINT_DOC=0,SHOW_TAGS=0,ISSDET=0
1010 FOR I=1 TO NUM_ENTRIES
1015 TAG$=DATA$[I,0],VALUE$=DATA$[I,1]
1050 SWITCH TAG$ ! 1185 end switch
1055 CASE "print_doc"
1057 PRINT_DOC=1; BREAK
1058 CASE "show_tags"
1059 SHOW_TAGS=1; BREAK
1070 CASE "beg_date"
1072 BEGDAT$=VALUE$; BREAK
1075 CASE "end_date"
1077 ENDDAT$=VALUE$; BREAK
1185 END SWITCH ! 1050
1190 NEXT I
1195 DIM BYBINFO$[5]; BYBINFO$[1]=PGN,BYBINFO$[2]="171393.20040601",BYBINFO$[3]=TEMPLATE$,BYBOPTIONS$=""
1200 REM set file
1205 SET_PARAM -'XI'
1210 IF PRINT_DOC=0 THEN {! 1290 }
1230 IF PRINT_DOC=0 THEN READ (Z[7],KNO=1,KEY=%CLIENT$,ERR=1231)
1290  }
1505 IF PRINT_DOC=0 THEN CLOSE (100); OPEN (100,OPT="TEXT",ERR=9000)%TEMPLATE$
1510 IF PRINT_DOC THEN READ DATA LINE$,ERR=2950 ELSE IF ISSDET=0 THEN READ (100,END=2950)LINE$; GOSUB BYB_PROCESSING ELSE GOSUB 4000; IF ISSDET=0 THEN GOTO *SAME ! 2950 end of main routine
1515 CHKTAG=1
1520 WHILE CHKTAG ! 2800 wend
1522 P1=MSK(LINE$,"\?ss\?[^?]*\?"); IF P1=0 THEN CHKTAG=0; BREAK
1527 TAG$=LINE$(P1,MSL)
1530 LINE1$=LINE$(1,P1-1); LINE2$=MID(LINE$,P1+MSL)
1600 SWITCH TAG$ ! 2790 end switch
1610 CASE "?ss?clientname?"
1615 IF PRINT_DOC THEN TAGDESC$="Contains the Client name"; GOSUB 5000 ELSE LINE$=LINE1$+%CLIENTNAME$+LINE2$
1619 BREAK
1620 CASE "?ss?issuestart?"
1625 IF PRINT_DOC THEN TAGDESC$="Contains the area to begin displaying issues"; GOSUB 5000 ELSE ISSDET=1; GOSUB 4100; LINE$=LINE1$+LINE2$
1629 BREAK
1640 CASE "?ss?session_id?"
1645 IF PRINT_DOC THEN TAGDESC$="Contains the current session Id"; GOSUB 5000 ELSE LINE$=LINE1$+%SESSIONID$+LINE2$
1649 BREAK
1660 CASE "?ss?base_page_ref?"
1662 IF PRINT_DOC THEN TAGDESC$="Contains the base web page location"; GOSUB 5000 ELSE LINE$=LINE1$+%BASEPAGEREF$+LINE2$
1664 BREAK
1665 CASE "?ss?cgi_command?"
1667 IF PRINT_DOC THEN TAGDESC$="Contains the cgi-command path"; GOSUB 5000 ELSE LINE$=LINE1$+%CGICOMMAND$+LINE2$
1669 BREAK
1670 CASE "?ss?issueend?"
1675 IF PRINT_DOC THEN TAGDESC$=" Specifies the end of the issue detail section"; GOSUB 5000
1679 BREAK
1680 CASE "?ss?plat_start?"
1682 IF PRINT_DOC THEN TAGDESC$="Contains the Platinum Support tags"; GOSUB 5000 ELSE GOSUB 4400
1685 BREAK
2185 CASE "?ss?issuenum?"
2187 IF PRINT_DOC THEN TAGDESC$="Displays the Support Issue Number"; GOSUB 5000 ELSE LINE$=LINE1$+ZW2$(1,6)+LINE2$
2189 BREAK
2190 CASE "?ss?initialdate?"
2192 IF PRINT_DOC THEN TAGDESC$="Displyas the date the issued opened"; GOSUB 5000 ELSE DATEIN$=ZW2$(7,6); CALL "ZZDISP","DX",DATEIN$,"",X3$,DATEOUT$,"",0,0,X4$; LINE$=LINE1$+DATEOUT$+LINE2$
2194 BREAK
2195 CASE "?ss?initialtime?"
2197 IF PRINT_DOC THEN TAGDESC$="Displays the Time the issue was opened"; GOSUB 5000 ELSE LINE$=LINE1$+FNT$(ZW2$(13,4))+LINE2$
2199 BREAK
2200 CASE "?ss?contactname?"
2202 IF PRINT_DOC THEN TAGDESC$="Displays the client contact name"; GOSUB 5000 ELSE LINE$=LINE1$+ZW2$(23,16)+LINE2$
2204 BREAK
2205 CASE "?ss?status?"
2207 IF PRINT_DOC=0 THEN {! 2230 }
2210 STATUS$=ZW2$(195,1)
2215 READ (Z[11],KEY=ZW2$(195,1),ERR=*NEXT)ZW8$; STATUS$=ZW8$(2,20)
2225 LINE$=LINE1$+STATUS$+LINE2$
2230  } ELSE TAGDESC$="Displays the issue status"; GOSUB 5000
2235 BREAK
2240 CASE "?ss?briefdesc1?"
2242 IF PRINT_DOC THEN TAGDESC$="Displays a brief description of the issue"; GOSUB 5000 ELSE LINE$=LINE1$+STP(ZW2$(53,60),2)+LINE2$
2244 BREAK
2245 CASE "?ss?briefdesc2?"
2247 IF PRINT_DOC THEN TAGDESC$="Displays the 2nd brief description line"; GOSUB 5000 ELSE LINE$=LINE1$+STP(ZW2$(113,60),2)+LINE2$
2248 BREAK
2780 DEFAULT 
2785 IF PRINT_DOC=0 THEN LINE$=LINE1$+LINE2$
2790 END SWITCH ! 1600
2800 WEND ! 1520
2805 DATA "?ss?clientname","?ss?issuestart?","?ss?session_id?","?ss?base_page_ref?","?ss?cgi_command?","?ss?issueend"
2806 DATA "?ss?issuenum?","?ss?initialdate?","?ss?initialtime?","?ss?contactname?","?ss?status?","?ss?briefdesc1?","?ss?briefdesc2?","?ss?plat_start?","?ss?plat_end?"
2810 IF LINE$<>"" THEN PRINT (OUTPUT)LINE$
2900 GOTO 1510; REM next read
2950 SET_PARAM 'XI'
2990 GOTO 9900
4000 REM process zw2 records
4005 IF REF$="" THEN ISSDET=0,LINE$=""; GOTO *RETURN
4010 ZW2KY$=REF$(1,6); XP=PRM('XI'); SET_PARAM 'XI'; READ (Z[7],KNO=0,KEY=ZW2KY$)IOL=0370; SET_PARAM 'XI'=XP
4040 IF LEN(REF$)=6 THEN REF$="" ELSE REF$=REF$(7)
4050 ISSDET=1
4090 LINE$=WKLINE$
4095 RETURN 
4100 REM ' get detail tags
4105 WKLINE$="",LINE$=""
4110 READ (100,END=*NEXT)DLINE$; IF POS("?ss?issueend?"=DLINE$)=0 THEN WKLINE$=WKLINE$+DLINE$+EOL$; GOTO *SAME
4150 READ (Z[7],KNO=1,KEY=%CLIENT$,ERR=4151)
4160 GOSUB 4200
4190 RETURN 
4200 REM build ref list
4205 REF$=""
4210 ZW2KY$=KEY(Z[7],END=*RETURN); IF ZW2KY$(1,10)<>%CLIENT$ THEN GOTO *RETURN ELSE XP=PRM('XI'); SET_PARAM 'XI'; READ (Z[7],KEY=ZW2KY$,DOM=*SAME)IOL=0370; SET_PARAM 'XI'=XP; IF ZW2$(195,1)="C" THEN READ (Z[7],KEY=%CLIENT$+"D",ERR=*SAME); GOTO *SAME ELSE IF POS(ZW2$(195,1)="FYEL")>0 THEN GOTO *SAME
4215 IF REF$="" THEN REF$=ZW2$(1,6); GOTO 4210
4220 FOR BLDREF=1 TO LEN(REF$) STEP 6
4225 IF ZW2$(1,6)<REF$(BLDREF) THEN {
4230 IF BLDREF=1 THEN REF$=ZW2$(1,6)+REF$; EXITTO 4210
4235 REF$=REF$(1,BLDREF-1)+ZW2$(1,6)+REF$(BLDREF); EXITTO 4210
4240  }
4250 NEXT BLDREF
4260 REF$=REF$+ZW2$(1,6)
4270 GOTO 4210
4290 RETURN 
4400 REM ' process special platinum support buttons
4405 WKLINE$="",LINE$=""
4410 READ (100,END=*RETURN)DLINE$; IF POS("?ss?plat_end?"=DLINE$)<>0 THEN GOTO *RETURN
4420 IF POS(%CLIENT$="00000500000000049900000004870000000093000000044900",10)=0 THEN GOTO 4410
4430 LINE$=LINE$+DLINE$
4440 GOTO 4410
4490 RETURN 
5000 REM print tag desc html
5010 LINE$=TAG$+" "+TAGDESC$
5090 RETURN 
5100 ADD_MSG_TO_LOG:! Add message to log file
5110 MOUT$="SESSION ID|"+EC$[1](1,7)+"|FILE|"+%WEBEC_FILE_NAME$+"|WORKSHEET|"+ECD$(1,10)+"|LINE|"+ECE$(25,3)+"|MESSAGE NO|"+STR(MESSAGE_NO:"00000")+"|MESSAGE|"+MESSAGE$+"|"
5135 MX=FN%_LOG_MESSAGE("MESG",MOUT$)
5145 RETURN 
5149 ! 
5490 RETURN ! 5200
5500 REM create description html
5590 RETURN 
8150 BYB_PROCESSING:! Send LINE$ to EC3BYB for generic fields processing
8160 CALL "EC3BYB",ERR=8161,X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,LINE$,BYBINFO${ALL},BYBOPTIONS$
8195 RETURN 
8910 DEF FND$(Z9$)=Z9$(3,2)+"/"+Z9$(5,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
9000 REM "ERROR PROCESSING
9002 J=ERR,K=TCB(5); SETERR 0000; ENDTRACE 
9010 %WEBSSERR$="ERR: "+STR(J)+" AT: "+STR(K)+" IN "+PGN
9090 GOTO 9900
9900 REM "End
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9950 EXIT 
9999 END 
