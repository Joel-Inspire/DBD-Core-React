0010 REM ""EC Support Issue New Update Processing <EC4IA1>"
0020 SETESC 9300; SETERR 9000
0035 REM "5.3 - 05/11/04 - 15.19 - jme - SSP# 171390
0040 REM "Copyright 2004 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT
0100 SETERR 9000
0110 X0$="EC4IA1",X1$="EC Support New Issue Update Processing"
0120 REM EOL$=$0D0A$; REM EOL$=ATH(STP(EC_PARM$(57,8),1))
0121 EOL$="~~!"
0125 LSTISS$=""
0127 HTML_TEXT$="Content-type: text/html"
0140 DIM MESS_INFO$[20],EC_PARM$(1275)
0210 DIM ESA$(1540),ESA2$(1000)
0220 DIM ESB$(1984),ESB[3]
0230 DIM ESY$(512)
0240 DIM ECC$(1984),ECC[3]
0250 DIM ESZ$(128),ESZ[18]
0260 DIM SS1$(726)
0270 DIM ZW2$(411),ZW2[3]
0300 DIM ZWT$(126)
0310 IOLIST ESA$,ESA2$
0320 IOLIST ESB$,ECB[0],ECB[1],ECB[2]
0330 IOLIST ESY$
0340 IOLIST ECC$,ECC[0],ECC[1],ECC[2]
0350 IOLIST ESZ$,ESZ[0],ESZ[1],ESZ[2],ESZ[3],ESZ[4],ESZ[5],ESZ[6],ESZ[7],ESZ[8],ESZ[9],ESZ[10],ESZ[11],ESZ[12],ESZ[13],ESZ[14],ESZ[15],ESZ[16],ESZ[17],ESZ[18]
0360 IOLIST SS1$
0370 IOLIST ZW2$,ZW2[0],ZW2[1],ZW2[2]
0380 IOLIST ZW9A$,ZW9B$
0400 IOLIST ZW7A$,ZW7B$
0410 IOLIST ZW8A$
0420 IOLIST ZWT$
0500 REM "Open files
0505 DIM Z[NUM(X3$(60,3))],BLGRBNKS$(100)
0510 Z$="010 ESA... 02O ESB... 03O ESY... 04O ESC... 05O ESZ... 06O SS1    07O ZW2    08O ZW9    09O ZWD    10O ZWY    11O ZW8    12O ZWT    13O ZWE    14O ZWF    15O ZWH    16O ZW1    17O ZWI    "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0900 PRINT_DOC=0
1000 REM "Process DATA array
1005 CONTACT$="",EMAILADDRESS$="",CATEGORY$="",PROGRAM$="",ERRNUM$="",MENUCODE$="",MENUOPT$="",BRIEFDESC1$="",BRIEFDESC2$="",ADDCOMMENT$="",PGMLINE$=""
1010 FOR I=1 TO NUM_ENTRIES
1015 TAG$=DATA$[I,0],VALUE$=DATA$[I,1]
1050 SWITCH TAG$ ! 1185 end switch
1055 CASE "print_doc"
1057 PRINT_DOC=1; BREAK
1060 CASE "contact"
1062 CONTACT$=VALUE$; BREAK
1065 CASE "emailaddress"
1067 EMAILADDRESS$=VALUE$; BREAK
1070 CASE "addcomments"
1075 TEXT$=VALUE$; BREAK
1080 CASE "category"
1082 CATEGORY$=VALUE$; BREAK
1085 CASE "program"
1087 PROGRAM$=VALUE$; BREAK
1090 CASE "errnum"
1092 ERRNUM$=VALUE$; BREAK
1095 CASE "menucode"
1097 MENUCODE$=VALUE$; BREAK
1100 CASE "menuoption"
1102 MENUOPT$=VALUE$
1104 BREAK
1105 CASE "briefdesc1"
1107 BRIEFDESC1$=VALUE$; BREAK
1110 CASE "briefdesc2"
1112 BRIEFDESC2$=VALUE$; BREAK
1115 CASE "pgmline"
1117 PGMLINE$=VALUE$; BREAK
1185 END SWITCH ! 1050
1190 NEXT I
1195 DIM BYBINFO$[5]; BYBINFO$[1]=PGN,BYBINFO$[2]="171393.20040601",BYBINFO$[3]=TEMPLATE$,BYBOPTIONS$=""
1200 REM ' get issue number
1210 IF PRINT_DOC THEN GOTO 9900; REM no tag display
1215 ZWPCH=UNT; OPEN (ZWPCH)"ZWP"
1250 SET_PARAM 'XI'
1260 EXTRACT (ZWPCH,KEY="SSP")Q$,Q0; ISSUE$=STR(Q0:"000000"); Q0=Q0+1
1270 WRITE (ZWPCH,KEY="SSP")"SSP",Q0
1272 READ (Z[7],KNO=0,KEY=ISSUE$,DOM=1273)TMPZ7$; GOTO 1260
1275 CLOSE (ZWPCH)
1290 ZW2$(1,6)=ISSUE$
1295 ZW2$(194,1)="A",ZW2$(195,1)="!",ZW2$(206,1)="S",ZW2$(253,3)="WWW",ZW2[0]=.25; IF %SUPPLAN$="A" THEN ZW2$(194,1)="1"
1296 ZW2$(359,1)=%SUPPLAN$
1300 TEMP$=DAY; YY$="20"+TEMP$(7,2); TEMP$=TEMP$(1,2)+TEMP$(4,2)
1305 Q$=CHR(NUM(YY$(1,3),ERR=8016)-125,ERR=8016)+YY$(4,1)+TEMP$(1,4)+STR(100*TIM:"0000")
1310 ZW2$(7,10)=Q$,ZW2$(196,10)=Q$; REM "Initial date, status date to today's date                                                                     
1320 SET_PARAM -'XI'; FIND (Z[6],KEY=%CLIENT$,ERR=1321)IOL=0360; SET_PARAM 'XI'; ZW2$(39,14)=SS1$(195,14)
1325 IF STP(CONTACT$,2)="" THEN CONTACT$=SS1$(231,16)
1330 ZW2$(240,10)=%CLIENT$,ZW2$(23,16)=PAD(CONTACT$,16)
1335 ZW2$(53,60)=PAD(BRIEFDESC1$,60),ZW2$(113,60)=PAD(BRIEFDESC2$,60)
1345 ZW2$(175,8)=PAD(PROGRAM$,8),ZW2$(183,3)=PAD(ERRNUM$,3),ZW2$(186,5)=PAD(PGMLINE$,5)
1350 ZW2$(206,1)="S"
1352 ZW2$(359,1)=SS1$(691,1),ZW2$(360,3)=SS1$(692,3),ZW2$(363,3)=SS1$(695,3)
1355 ZW2$(214,8)=PAD(MENUCODE$+MENUOPT$,8)
1357 IF ADDCOMMENT$<>"Y" THEN ZW2$(238,1)="*"
1360 ZW2$(299,40)=PAD(EMAILADDRESS$,40)
1400 ZW2$(173,2)="PA"
1405 IF STP(CATEGORY$,2)="TopForm Software Issue" THEN ZW2$(173,2)="SI"
1407 IF STP(CATEGORY$,2)="Enhancement Request" THEN ZW2$(173,2)="EH"
1410 IF STP(CATEGORY$,2)="Work Order" THEN ZW2$(173,2)="WO"
1412 IF STP(CATEGORY$,2)="Procedural Assistance" THEN ZW2$(173,2)="PA"
1414 IF STP(CATEGORY$,2)="Program Exception" THEN ZW2$(173,2)="PE"
1416 IF STP(CATEGORY$,2)="VSI*FAX" THEN ZW2$(173,2)="3P",ZW2$(175,8)="VSIFAX  "
1418 IF POS("Terminal Emulation"=CATEGORY$)<>0 THEN ZW2$(173,2)="HT"
1420 IF STP(CATEGORY$,2)="Forms Express" THEN ZW2$(173,2)="SI"
1422 IF POS("Windows"=CATEGORY$)<>0 THEN ZW2$(173,2)="HO",ZW2$(175,8)="Windows "
1424 IF POS("OTHER 3rd"=CATEGORY$)<>0 THEN ZW2$(173,2)="3P"
1426 IF STP(CATEGORY$,2)="Modem" THEN ZW2$(173,2)="HM"
1428 IF STP(CATEGORY$,2)="Tape Backup" THEN ZW2$(173,2)="HD"
1430 IF STP(CATEGORY$,2)="Printer" THEN ZW2$(173,2)="HP"
1432 IF STP(CATEGORY$,2)="Terminal" THEN ZW2$(173,2)="HT"
1434 IF STP(CATEGORY$,2)="OTHER Hardware" THEN ZW2$(173,2)="HW"
1500 IF TEXT$<>"" THEN {! 1795                                                
1502 SET_PARAM -'XI'; READ (Z[12],KEY=ISSUE$+"999999",ERR=*NEXT)
1505 SEQ=1; KYP$=KEP(Z[12],ERR=*NEXT); IF KYP$(1,6)=ISSUE$ THEN SEQ=NUM(KYP$(7,6))+1
1507 SET_PARAM 'XI'
1508 IF STP(TEXT$,2)<>"" THEN DIM ZWT$(126); ZWT$(1,12)=ISSUE$+STR(SEQ:"000000"); WRITE (Z[12],KEY=ZWT$(1,12))ZWT$; SEQ=SEQ+1; DIM ZWT$(126); ZWT$(1,12)=ISSUE$+STR(SEQ:"000000"),ZWT$(13)="*clientin*WWW*"+FND$(X3$(21,6))+"*"+FNT$(STR(INT(100*TIM):"0000"))+"*"+ZW2$(23,16); WRITE (Z[12],KEY=ZWT$(1,12))ZWT$; SEQ=SEQ+1
1510 REPEAT ! 1690 until text$=""                                              
1520 PC=POS($0D$=TEXT$); PC2=POS($0A$=TEXT$)
1525 IF PC=0 THEN WTEXT$=TEXT$; TEXT$=""
1530 IF PC>0 THEN IF PC=LEN(TEXT$) THEN WTEXT$=TEXT$(1,PC-1); TEXT$="" ELSE WTEXT$=TEXT$(1,PC-1); IF PC2=0 THEN TEXT$=TEXT$(PC+1) ELSE IF PC2=LEN(TEXT$) THEN TEXT$="" ELSE TEXT$=TEXT$(PC2+1)
1550 IF LEN(WTEXT$)>80 THEN {
1565 TEXT$=WTEXT$(81)+$0D$+$0A$+TEXT$; WTEXT$=WTEXT$(1,80)
1570 PC=POS(" "=WTEXT$,-1)
1575 IF PC<>0 THEN TEXT$=WTEXT$(PC+1)+TEXT$; WTEXT$=WTEXT$(1,PC); WTEXT$=PAD(WTEXT$,80)
1590  } ELSE WTEXT$=PAD(WTEXT$,80)
1620 DIM ZWT$(126); ZWT$(1,12)=ISSUE$+STR(SEQ:"000000"); ZWT$(13)=WTEXT$; WRITE (Z[12],KEY=ZWT$(1,12))ZWT$
1650 SEQ=SEQ+1
1690 UNTIL TEXT$="" ! 1510   
1795  } ! 1500               
1900 REM write
1910 WRITE (Z[7],KEY=ISSUE$)IOL=0370
1915 WRITE (Z[9],KEY=ZW2$(195,1)+ZW2$(7,6)+ZW2$(194,1)+ZW2$(13,6)+ZW2$(1,6))
1920 WRITE (Z[13],KEY=ZW2$(175,8)+ZW2$(183,8)+ZW2$(1,6))
1925 WRITE (Z[14],KEY=ZW2$(240,10)+ZW2$(1,6))
1930 WRITE (Z[15],KEY=ZW2$(7,10)+ZW2$(1,6))
1935 WRITE (Z[17],KEY=ZW2$(1,6))ZW2$(1,6)+ZW2$(195,1)+DIM(22)
1950 IF ZW2$(359,1)="A" AND ZW2$(360,3)<>"   " THEN SNDSSP$=ZW2$(360,3); GOSUB 8700; REM auto email for platimum suppt
1952 IF ZW2$(359,1)="A" AND ZW2$(363,3)<>"   " THEN SNDSSP$=ZW2$(363,3); GOSUB 8700; REM auto email for platimum suppt
1996 GOTO 9900
5100 ADD_MSG_TO_LOG:! Add message to log file
5110 MOUT$="SESSION ID|"+EC$[1](1,7)+"|FILE|"+%WEBEC_FILE_NAME$+"|WORKSHEET|"+ECD$(1,10)+"|LINE|"+ECE$(25,3)+"|MESSAGE NO|"+STR(MESSAGE_NO:"00000")+"|MESSAGE|"+MESSAGE$+"|"
5135 MX=FN%_LOG_MESSAGE("MESG",MOUT$)
5145 RETURN 
5149 ! 
8150 BYB_PROCESSING:! Send LINE$ to EC3BYB for generic fields processing      
8160 CALL "EC3BYB",ERR=8161,X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,LINE$,BYBINFO${ALL},BYBOPTIONS$
8195 RETURN 
8700 REM send email                                                            
8764 A0$=">>> Premium support "+ZW2$(1,6)+" "+ZW2$(240,10)+" "+ZW2$(7,6)+" <<<"
8765 A0$=A0$+"\n"+"Categ: "+ZW2$(173,2)+"  Status: "+ZW2$(195,1)+"  Current SSP"+ZW2$(191,3)+"  Date entered: "+FND$(ZW2$(7,6))+"\n"+"Solution provider: "+ZW2$(250,3)+"  Responsible person: "+ZW2$(253,3)+"  Scheduled: "+FND$(ZW2$(228,6))+" "
8771 S3$="echo "+$22$+A0$+"\n\n"+CVS(ZW2$(53,60),2); IF CVS(ZW2$(113,60),2)<>"" THEN S3$=S3$+"\n"+CVS(ZW2$(113,60),2)
8772 S3$=S3$+"\n"+$22$+" |mail -s "+$22$+"SSP#"+ZW2$(1,6)+"/"+ZW2$(240,10)+"  "+$22$+" "+SNDSSP$
8775 INVOKE S3$
8790 RETURN 
8910 DEF FND$(Z9$)=Z9$(3,2)+"/"+Z9$(5,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
9000 REM "ERROR PROCESSING
9002 J=ERR,K=TCB(5); SETERR 0000; ENDTRACE 
9010 %WEBSSERR$="ERR: "+STR(J)+" AT: "+STR(K)+" IN "+PGN
9090 GOTO 9900
9900 REM "End
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9950 EXIT 
9999 END 
