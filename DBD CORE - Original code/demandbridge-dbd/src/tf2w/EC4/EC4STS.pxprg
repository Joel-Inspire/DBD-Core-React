0010 REM "WebEC Support Server Monitor <EC4STS>
0020 SETESC 9300; SETERR 9000
0035 REM "5.1 - 11/05/02 - 19.124166 - kmc - SSP# 155075
0040 REM "Copyright 2002 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="EC4STS",X1$="WebEC Support Server Monitor"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1,M0$="#,###,##0",FIRST_LINE=5,SPACING=5,MAX_SERVERS=3
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0242 IF X3$="" THEN CLOSE (14); OPEN (14)"ZZPARM"; READ (14,KEY="TFEEC4STS  ",DOM=0243)X3$,*,X4$; REM "If X3$ not set then read env
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 SET_PARAM 'LU' ! We do so much with text files, don't lock them
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST ECY$
0320 IOLIST ECZ$,ECZ[0],ECZ[1],ECZ[2],ECZ[3],ECZ[4],ECZ[5],ECZ[6],ECZ[7],ECZ[8],ECZ[9],ECZ[10],ECZ[11],ECZ[12],ECZ[13],ECZ[14],ECZ[15],ECZ[16],ECZ[17],ECZ[18]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O ESY...  02O ESZ...  13O ZZPARM  14OSZZPARM "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0550 CLOSE (120); OPEN (120,ERR=0551)"EC4STS.LOG"; GOTO 0555
0551 IF ERR<>12 THEN OPEN (120,OPT="TEXT")"EC4STS.LOG"; GOTO 0555 ELSE SERIAL "EC4STS.LOG",0,0; GOTO 0550
0555 MESS$="STARTED"; GOSUB 7600
0560 REM "See if we are the on to monitor                                  
0564 MON_KEY$="EC4STSMN"
0565 FIND (Z[14],KEY=MON_KEY$,DOM=0566,ERR=0568)MONITOR$; GOTO 0570
0566 MONITOR=1,MONITOR$=MON_KEY$+FID(0); WRITE (Z[14],KEY=MON_KEY$)MONITOR$; GOTO 0570; REM "If record not found, set it up and we know we are the monitor                                                      
0568 REM "if here, err occurred, probably err 0, so we won't be the monitor
0569 DIM MONITOR$(10); MONITOR=0; GOTO 0575
0570 REM "If here, we can be the monitor                                   
0572 MONITOR=1; EXTRACT (Z[14],KEY=MON_KEY$,ERR=0568)MONITOR$; REM "If we get error here, then someone got the record or we had readprm on 
0575 REM "Done with setting monitor stuff here, so display                 
0576 IF MONITOR THEN X1$=X1$+" - Monitor ON" ELSE X1$=X1$+" - Monitor OFF"
0578 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,-1
0600 REM "
0610 GOSUB 6000
1000 REM "Get list of configured servers and load into SERVER$[]
1005 DIM ECY$(512),ECZ$(128),ECZ[18]
1010 DIM SERVER$[100],LAST_TIME[100,1]
1100 REM "Read ECY for servers
1105 S_INDEX=0; READ (Z[1],KEY="",DOM=1106)
1110 DIM ECY$(512); READ (Z[1],END=1150)IOL=0310
1115 S_INDEX=S_INDEX+1; SERVER$[S_INDEX]=ECY$
1145 GOTO 1110
1150 REM "if no servers configured, then give message
1155 IF S_INDEX=0 THEN CALL "ZZPROM",".4",X3$,0,"There are no WebEC Servers configured on your system!","","",0; GOTO 9900 ELSE S_TOTAL=S_INDEX
1158 CALL "ZZTIME",X3$,1,0; REM "Update date/time on screen
1160 PRINT @(25,3),STR(S_TOTAL)+" Servers configured",
1175 FIRST_ONE=1; GOSUB 2000; REM "display list
1176 GOSUB 4000; REM "Check for servers running
1178 IF SUB_CALL THEN SUB_CALL=0; RETURN 
1179 IF DISPLAY_ONLY THEN DISPLAY_ONLY=0; GOTO 1200
1180 PRINT @(0,20),"Press I to interrupt display ",; READ RECORD (0,SIZ=1,TIM=5,ERR=1100)IN$; IF POS(IN$="Ii")<>0 OR CTL=4 THEN GOTO 1200 ELSE GOTO 1100
1200 REM "Prompt for next action
1205 PRINT @(0,20),'CE',
1210 CALL "ZZPROM","X0EC3STS",X3$,Z,"","","",0
1215 ON Z GOTO 1220,1230,1240,1250,1260,1270,1280,1210
1220 REM "Stop a Server
1225 GOSUB 3000; DISPLAY_ONLY=1; GOTO 1100
1230 REM "Run a Server
1235 GOSUB 3100; DISPLAY_ONLY=1; GOTO 1100
1240 REM "Exit
1245 GOTO 9900
1250 REM "Redisplay status
1255 GOTO 1100
1260 REM "Parameters
1265 CALL "EC4MSA",X3$,X4$,"",""
1267 GOSUB 6400
1268 DISPLAY_ONLY=1; GOTO 1100
1270 REM "View Log
1275 CALL "EC4STL",X3$,X4$,"",""
1277 GOSUB 6400; DISPLAY_ONLY=1; GOTO 1100
1280 REM "Restart a Server
1285 GOSUB 3200; DISPLAY_ONLY=1; GOTO 1100
1995 GOTO 9900
2000 REM "Display page of servers, starting at FIRST_ONE
2005 PRINT @(0,6),'CE',
2006 CURRENT=FIRST_ONE,CURR_LINE=FIRST_LINE,SERVERS_ON_PAGE=0
2010 IF SERVERS_ON_PAGE>=MAX_SERVERS THEN GOTO 2100
2012 PRINT @(0,CURR_LINE),SERVER$[CURRENT](1,15),@(17,CURR_LINE),SERVER$[CURRENT](16,40),
2015 DIM ECZ$(128),ECZ[18]; FIND (Z[2],KEY=SERVER$[CURRENT](1,15),DOM=2016)IOL=0320; GOTO 2018
2016 ECZ$(16,10)="STAT N/A"; PRECISION 14; ECZ[0]=FN%GET_DATETIME("",0); PRECISION 2; REM "If not on file, then create default info
2020 PRECISION 14; PRINT @(54,CURR_LINE),ECZ$(16,10),@(65,CURR_LINE),FN%PRINT_DATETIME$(ECZ[0],"%Mz/%Dz %Hz:%mz:%sz"),; PRECISION 2
2025 PRINT (0,ERR=2026)'SB'
2026 PRINT @(17,CURR_LINE),SERVER$[CURRENT](16,35),@(2,CURR_LINE+1),"Started:",@(29,CURR_LINE+1),"Process id:",@(57,CURR_LINE+1),"Errors:",@(3,CURR_LINE+2),"Logins:",@(25,CURR_LINE+2),"Invalid logins:",@(48,CURR_LINE+2),"Active sessions:",@(3,CURR_LINE+3),"Orders:",@(28,CURR_LINE+3),"Order lines:",@(53,CURR_LINE+3),"Files done:",@(52,CURR_LINE+4),"Files to do:",
2028 PRINT (0,ERR=2029)'SF',
2030 REM "Server info
2032 IF ECZ[1]<>0 THEN PRECISION 14; PRINT @(11,CURR_LINE+1),FN%PRINT_DATETIME$(ECZ[1],"%Mz/%Dz %Hz:%mz:%sz"),; PRECISION 2
2034 PRINT @(41,CURR_LINE+1),STR(ECZ[2]),@(65,CURR_LINE+1),STP(STR(ECZ[8]:M0$),2),
2040 REM "Line 2
2042 PRINT @(11,CURR_LINE+2),STP(STR(ECZ[6]:M0$),2),@(41,CURR_LINE+2),STP(STR(ECZ[7]:M0$),2),@(65,CURR_LINE+2),STP(STR(ECZ[10]:M0$),2),
2045 REM "Line 3
2047 PRINT @(11,CURR_LINE+3),STP(STR(ECZ[4]:M0$),2),@(41,CURR_LINE+3),STP(STR(ECZ[5]:M0$),2),@(65,CURR_LINE+3),STP(STR(ECZ[3]:M0$),2),
2050 REM "Line 4
2052 PRINT @(65,CURR_LINE+4),STP(STR(ECZ[9]:M0$),2),
2075 IF CURRENT<S_TOTAL THEN SERVERS_ON_PAGE=SERVERS_ON_PAGE+1,CURRENT=CURRENT+1,CURR_LINE=CURR_LINE+SPACING; GOTO 2010
2095 RETURN 
2100 REM "Page full logic - arrived from 2010
2110 PRINT @(0,20),"Press I to interrupt display or <RET> to continue",; READ RECORD (0,SIZ=1,TIM=5,ERR=2115)IN$; IF POS(IN$="Ii")<>0 OR CTL=4 THEN GOTO 1200
2115 FIRST_ONE=CURRENT; GOTO 2000
3000 REM "Stop Server logic
3004 IF AUTOKILL THEN MESS$=S$+"|AUTOKILL"; GOSUB 7600; GOTO 3020
3005 PRINT @(0,21),'CE',@(0,21),"Server to stop:",; DIM S$(15),S[1]; X$="<F4> to skip, or enter server name to stop"
3010 C9=0; CALL "ZZENTR","SUZX",S{ALL},S$,X4$,X3$,16,21,1,15,C0,"",X$,"","EC4STS02","ESY","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3010,3011
3011 X$=""
3012 IF ABS(C0)>1 THEN GOTO 3090
3015 FIND (Z[1],KEY=S$,DOM=3005)IOL=0310
3020 FIND (Z[2],KEY=S$,DOM=3021)IOL=0320; GOTO 3030
3021 REM "Server status record not found
3022 IF AUTOKILL THEN GOTO 3090 ELSE SP$="Server "+STP(S$,2)+" status record not found, cannot stop this server!"; CALL "ZZPROM",".4",X3$,Z,SP$,"","",0; GOTO 3090
3025 IF ECZ$(16,10)<>"ENDED     " THEN GOTO 3030 ELSE IF AUTOKILL THEN GOTO 3026 ELSE SP$="Server status record indicates server stopped. Try KILL command?"; CALL "ZZPROM",".Y",X3$,Z,SP$,"","",0; ON Z GOTO 3026,3090
3026 COMMAND$="{WEBEC_KILL_SERVER}"; GOSUB 7550; REM "Kill Command
3027 REMOVE (Z[2],KEY=S$,DOM=3028)
3028 COMMAND$="{WEBEC_CLEAR_STOP}"; GOSUB 7550; REM "remove end.servid file
3029 GOTO 3090
3030 REM "If here, stop server using END.server name  file
3034 IF AUTOKILL THEN GOTO 3035 ELSE PRINT @(30,22),"Stopping "+STP(S$,2)
3035 COMMAND$="{WEBEC_STOP_SERVER}"; GOSUB 7550; REM "End command
3045 WAIT 2; REM "Give the server a chance to stop
3050 ECZ$(16,10)="ENDED"; FIND (Z[2],KEY=S$,DOM=3051)IOL=0320
3055 IF ECZ$(16,10)="ENDED     " THEN GOTO 3060; REM "Server stopped ok
3056 REM "Server didn't stop yet, wait a few seconds before panic
3057 WAIT 3; FIND (Z[2],KEY=S$,DOM=3058)IOL=0320
3058 IF ECZ$(16,10)="ENDED     " THEN GOTO 3060 ELSE IF AUTOKILL THEN GOTO 3026 ELSE SP$="Could not stop "+STP(S$,2)+", try KILL command?"; CALL "ZZPROM",".Y",X3$,Z,SP$,"","",0; ON Z GOTO 3026,3090
3060 COMMAND$="{WEBEC_CLEAR_STOP}"; GOSUB 7550; REM "Clear End file
3061 IF AUTOKILL THEN GOTO 3062 ELSE PRINT @(0,20),'CE',; SUB_CALL=1; GOSUB 1100; SP$="Server "+STP(S$,2)+" stopped successfully."; CALL "ZZPROM",".4",X3$,Z,SP$,"","",0
3093 IF AUTOKILL THEN GOTO 3094 ELSE DISPLAY_ONLY=1; GOTO 1100
3095 RETURN 
3100 REM "Run a server
3104 IF AUTOSTART THEN MESS$=S$+"|AUTOSTART"; GOSUB 7600; GOTO 3117
3105 PRINT @(0,21),'CE',@(0,21),"Server to run:",; DIM S$(15),S[1]; X$="<F4> to skip, or enter server name to run"
3110 C9=1; CALL "ZZENTR","SUZX",S{ALL},S$,X4$,X3$,15,21,1,15,C0,"",X$,"","EC4STS04","ESY","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3110,3111
3111 X$=""
3112 IF ABS(C0)>1 THEN GOTO 3190
3115 FIND (Z[1],KEY=S$,DOM=3105)IOL=0310
3117 COMMAND$="{WEBEC_CLEAR_STOP}"; GOSUB 7550; REM "Make sure END file is gone
3120 FIND (Z[2],KEY=S$,DOM=3125)IOL=0320
3121 REM "Server already running
3122 IF ECZ$(16,10)="ENDED     " THEN GOTO 3125
3123 IF AUTOSTART THEN GOTO 3124 ELSE SP$="Server status indicates that "+STP(S$,2)+" is running. Try emergency stop?"; CALL "ZZPROM",".Y",X3$,Z,SP$,"","",0; ON Z GOTO 3124,3190
3124 COMMAND$="{WEBEC_KILL_SERVER}"; GOSUB 7550; REM "Kill command
3125 REM "Start server
3126 IF AUTOSTART THEN GOTO 3127 ELSE PRINT @(30,22),"Running server "+STP(S$,2)
3130 COMMAND$="{WEBSS_RUN_SERVER}"; GOSUB 7550; REM "Run Command
3140 WAIT 2; REM "Give the server a chance to get started
3145 FIND (Z[2],KEY=S$,DOM=3146)IOL=0320; GOTO 3150
3146 WAIT 3; REM "wait a little more before panicing
3147 FIND (Z[2],KEY=S$,DOM=3148)IOL=0320; GOTO 3150
3148 IF AUTOSTART THEN MESS$=S$+"|FAILED"; GOSUB 7600; GOTO 3195 ELSE SP$="Looks like server didn't start!"; CALL "ZZPROM",".4",X3$,Z,SP$,"","",0
3149 GOTO 3190
3150 REM "looks like it started
3151 IF AUTOSTART THEN MESS$=S$+"|STARTED"; GOSUB 7600; GOTO 3195
3152 PRINT @(0,21),'CE',
3155 SUB_CALL=1; GOSUB 1100
3160 SP$="Server "+STP(S$,2)+" started."; CALL "ZZPROM",".4",X3$,Z,SP$,"","",0
3190 IF AUTOSTART THEN GOTO 3191 ELSE DISPLAY_ONLYL=1; GOTO 1100
3195 RETURN 
3200 REM "Restart a Server
3205 PRINT @(0,21),'CE',@(0,21),"Restart server:",; DIM S$(15),S[1]; X$="<F4> to skip, or enter server name to restart"
3210 C9=0; CALL "ZZENTR","SUZX",S{ALL},S$,X4$,X3$,16,21,1,15,C0,"",X$,"","EC4STS02","ESY","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 3010,3011
3211 X$=""
3212 IF ABS(C0)>1 THEN GOTO 3290
3215 FIND (Z[1],KEY=S$,DOM=3205)IOL=0310
3220 FIND (Z[2],KEY=S$,DOM=3221)IOL=0320; GOTO 3230
3222 SP$="Server "+STP(S$,2)+" status record not found, cannot restart this server!"; CALL "ZZPROM",".4",X3$,Z,SP$,"","",0; GOTO 3290
3230 REM "If here, restart server using RESTART.server name file
3234 PRINT @(30,22),"Restarting "+STP(S$,2)
3235 COMMAND$="{WEBEC_RESTART_SERVER}"; GOSUB 7550; REM "Restart
3295 RETURN 
4000 REM "Check monitored servers to see if they are running
4005 IF MONITOR THEN GOTO 4006 ELSE RETURN ; REM "If set not to monitor, then don't                                                             
4010 FOR RINDEX=1 TO S_TOTAL
4015 IF NUM(SERVER$[RINDEX](376,5),ERR=4080)=0 THEN GOTO 4080; REM "If 0 then don't monitor
4020 DIM ECZ$(128),ECZ[18]; FIND (Z[2],KEY=SERVER$[RINDEX](1,15),DOM=4050)IOL=0320; REM "Find stats, if no record then assume restart needed
4025 IF ECZ$(16,10)="ENDED     " THEN GOTO 4050
4030 IF LAST_TIME[RINDEX,0]=0 THEN PRECISION 14; LAST_TIME[RINDEX,0]=ECZ[0],LAST_TIME[RINDEX,1]=FN%GET_DATETIME("",0); PRECISION 2; GOTO 4035; REM "If not set yet, set & continue
4031 REM "get diff w last time and compare to # secs allowed
4032 PRECISION 14; IF LAST_TIME[RINDEX,0]<>ECZ[0] THEN LAST_TIME[RINDEX,0]=ECZ[0],LAST_TIME[RINDEX,1]=FN%GET_DATETIME("",0); PRECISION 2; GOTO 4035
4033 PRECISION 14; SECONDS=(FN%GET_DATETIME("",0)-LAST_TIME[RINDEX,1])*86400; PRECISION 2
4034 IF SECONDS>NUM(SERVER$[RINDEX](376,5)) THEN GOTO 4050 ELSE PRECISION 14; LAST_TIME[RINDEX]=ECZ[0]; PRECISION 2
4035 REM "Pickup processing here
4045 GOTO 4080
4050 REM "Restart process
4052 ECY$=SERVER$[RINDEX],S$=SERVER$[RINDEX](1,15); REM "As if we read the data
4053 GOSUB 7650
4055 FIND (Z[2],KEY=SERVER$[RINDEX](1,15),DOM=4061)IOL=0320; REM "IF no status record then skip stop attempt, we don't have the info
4060 AUTOKILL=1; GOSUB 3000; AUTOKILL=0; REM "kill server for sure
4062 REM "Remove last file processed if it exists it may have been stuck on it
4064 ERASE STP(SERVER$[RINDEX](56,60),3," ")+DLM+STP(ECZ$(26,20),1),ERR=4065; GOTO 4064
4068 REMOVE (Z[2],KEY=SERVER$[RINDEX](1,15),DOM=4069); REM "Remove status record we don't need it any more
4069 REM "Now attempt to start the file
4070 PRINT @(0,22),'CL',@(20,22),"Attempting restart of: ",SERVER$[RINDEX](1,15),
4075 AUTOSTART=0; GOSUB 3100; AUTOSTART=0
4077 PRINT @(0,22),'CL',
4080 NEXT RINDEX
4095 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,3),"WebEC Servers",@(54,3),"Status",@(65,3),"Status time",@(0,4),Z0$,
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6410 GOSUB 6000
6420 SUB_CALL=1; GOSUB 1100
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
7500 REM "Run COMMAND$ and capture output in OUTPUT$
7515 CALL "ZZ2CMD",X3$,X4$,COMMAND$,ARG$,"NR",RETURN_CODE,RETURN$
7545 RETURN 
7550 REM "Setup Args to run command 1=server name, 2=environment code, 3=Process id, 4=End file name, 5=restartfile name, 6=email message, 7=email subject, 8=email address, 9=arguments
7555 ARG$=""
7560 ARG$=ARG$+STP(S$,3)+SEP; REM "{SERVER}
7562 IF ECY$(433,8)=DIM(8) THEN ENV$="EC4SRV" ELSE ENV$=ECY$(433,8) END_IF ; ARG$=ARG$+ENV$+SEP; REM "{ENV}
7566 ARG$=ARG$+STR(ECZ[2])+SEP; REM "{PID}
7568 ARG$=ARG$+STP(ECY$(56,60),2)+DLM+"END."+STP(S$,3)+SEP; REM {ENDFILE}
7570 ARG$=ARG$+STP(ECY$(56,60),2)+DLM+"RESTART."+STP(S$,3)+SEP; REM {RESTARTFILE}
7572 ARG$=ARG$+EMESS$+SEP; EMESS$="" ! Set & then clear email message
7574 ARG$=ARG$+SUBJECT$+SEP; SUBJECT$="" ! Set & then clear subject 
7576 ARG$=ARG$+STP(ECY$(442,40),1)+SEP ! Email address
7578 ARG$=ARG$+STP(ECY$(116,60),1)+SEP ! Arguments
7590 REM "Now execute COMMAND
7593 GOSUB 7500
7595 RETURN 
7600 REM "Write message to LOG
7610 OUT$=DTE(0:"%Y-%Mz-%Dz|%Hz:%mz:%sz")+"|"+MESS$
7620 REM "Position to current end of file incase multiple EC4STS's running
7622 TMP$=FN%XFD$(120,0); IF DEC(TMP$(38,4))>0 THEN READ (120,IND=DEC(TMP$(38,4))-1,ERR=7623)
7625 WRITE (120)OUT$
7645 RETURN 
7650 REM "Notify of failure
7654 REM "Log failure
7655 MESS$=SERVER$[RINDEX](1,15)+"|INACTIVE"; GOSUB 7600
7660 REM "Email notify
7662 IF STP(SERVER$[RINDEX](442,40),1)="" OR X3$(77,1)<>"U" THEN GOTO 7670; REM "If no email address or not a unix system then skip it
7664 EMESS$="WebEC Server "+STP(SERVER$[RINDEX](1,15),1)+" appears inactive. A restart is being attempted."; PRECISION 14; EMESS$=EMESS$+" ["+DTE(0:"%Y-%Mz-%Dz|%Hz:%mz:%sz")+"]"; PRECISION 2; SUBJECT$="WebEC Server "+STP(SERVER$[RINDEX](1,15),1)+" appears inactive"
7665 COMMAND$="{WEBEC_FAIL_NOTIFY}"; GOSUB 7550
7670 REM "After email notify
7695 RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 3005,3105
9900 REM "END PROGRAM
9905 SET_PARAM -'LU' ! Clear text file lock
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
