0010 REM ""EC Support Login, Logout Processing <EC4LG1>"
0020 SETESC 9300; SETERR 9000
0035 REM "5.3 - 05/11/04 - 15.19 - dmm - SSP# 171777
0040 REM "Copyright 2004 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT
0100 SETERR 9000
0110 X0$="EC4LG1",X1$="EC Support Login, Logout Processing"
0120 EOL$=$0D0A$; EOL$=ATH(STP(EC_PARM$(57,8),1))
0125 LGNMSG$=""
0127 HTML_TEXT$="Content-type: text/html"
0140 DIM MESS_INFO$[20],EC_PARM$(1275)
0210 DIM ESA$(1540),ESA2$(1000)
0220 DIM ESB$(1984),ESB[3]
0230 DIM ESY$(512)
0240 DIM ECC$(1984),ECC[3]
0250 DIM ESZ$(128),ESZ[18]
0260 DIM SS1$(726)
0270 DIM ZW2$(411),ZW2[3]
0300 DIM ZWT$(126)
0310 IOLIST ESA$,ESA2$
0320 IOLIST ESB$,ESB[0],ESB[1],ESB[2]
0330 IOLIST ESY$
0340 IOLIST ECC$,ECC[0],ECC[1],ECC[2]
0350 IOLIST ESZ$,ESZ[0],ESZ[1],ESZ[2],ESZ[3],ESZ[4],ESZ[5],ESZ[6],ESZ[7],ESZ[8],ESZ[9],ESZ[10],ESZ[11],ESZ[12],ESZ[13],ESZ[14],ESZ[15],ESZ[16],ESZ[17],ESZ[18]
0360 IOLIST SS1$
0370 IOLIST ZW2$,ZW2[0],ZW2[1],ZW2[2]
0380 IOLIST ZW9A$,ZW9B$
0400 IOLIST ZW7A$,ZW7B$
0410 IOLIST ZW8A$
0420 IOLIST ZWT$
0500 REM "Open files
0505 DIM Z[NUM(X3$(60,3))],BLGRBNKS$(100)
0510 Z$="010 ESA... 02O ESB... 03O ESY... 04O ESC... 05O ESZ... 06O SS1    07O ZW2    08O ZW9    09O ZWD    10O ZWY    11O ZW8    12O ZWT    13O ZWE    14O ZWF    15O ZWH    "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0900 PRINT_DOC=0
1000 REM "Process DATA array
1005 PRINT_DOC=0
1010 FOR I=1 TO NUM_ENTRIES
1015 TAG$=DATA$[I,0],VALUE$=DATA$[I,1]
1050 SWITCH TAG$ ! 1185 END SWITCH
1055 CASE "print_doc"
1057 PRINT_DOC=1; BREAK
1060 CASE "client"
1062 CL_ID$=PAD(VALUE$,15); BREAK
1065 CASE "password"
1067 PASSWD$=VALUE$; BREAK
1070 CASE "login"
1072 LOGIN$=VALUE$; BREAK
1185 END SWITCH 
1190 NEXT I
1195 DIM BYBINFO$[5]; BYBINFO$[1]=PGN,BYBINFO$[2]="171393.20040601",BYBINFO$[3]=TEMPLATE$,BYBOPTIONS$=""
1200 REM verify
1210 IF PRINT_DOC THEN GOTO 9900 ! display web tag function, dont login
1220 IF LOGIN$<>"IN" AND LOGIN$<>"OUT" THEN %WEBSSERR$="Invalid Login/Logout operation specified",%BASIC2$=""; GOTO 9900
1300 IF LOGIN$="OUT" THEN {! 1390
1310 READ (Z[2],KEY=%SESSIONID$,DOM=*NEXT)IOL=0320; ESB$(93,1)="Y"; WRITE (Z[4],KEY=%SESSIONID$)IOL=0320; REMOVE (Z[2],KEY=%SESSIONID$,ERR=*NEXT)
1315 EC$[1]="",LGNMSG$="You are now logged out",%BASIC2$=""
1320 %BASIC2$=""
1390  }
1400 IF LOGIN$="IN" THEN {! 1490
1405 SET_PARAM 'XI'
1410 OK=0; READ (Z[1],KEY=CL_ID$,DOM=*NEXT)IOL=0310; IF STP(ESA$(65,15),1)=PASSWD$ THEN CLIENT$=ESA$(51,10); READ (Z[6],KEY=CLIENT$,DOM=*NEXT)IOL=0360; OK=1
1412 IF CL_ID$="" THEN CL_ID$="***nof"
1415 IF OK=0 THEN CLIENT$=STR(NUM(CL_ID$,ERR=1416):"00000000")+"00"
1420 IF OK=0 THEN READ (Z[6],KEY=CLIENT$,DOM=*NEXT,ERR=*NEXT)IOL=0360; IF STP(SS1$(621,10),1)=PASSWD$ THEN OK=1
1422 SET_PARAM -'XI'
1425 IF OK THEN {! 1460
1430 BUSY=1; DIM TMP_EC_PARM$(LEN(EC_PARM$)); EXTRACT RECORD (33013,KEY=X3$(9,3)+"S/S",DOM=1435,ERR=*NEXT)TMP_EC_PARM$(1); BUSY=0
1431 IF BUSY THEN IF ERR=0 THEN WAIT 1; GOTO 1430 ELSE GOTO 9000
1435 SESSIONID$=TMP_EC_PARM$(65,7); TMP_EC_PARM$(65,7)=STR(NUM(TMP_EC_PARM$(65,7))+1:"0000000"); WRITE RECORD (33013,KEY=X3$(9,3)+"S/S")TMP_EC_PARM$
1440 EC$[1]=SESSIONID$,%SESSIONID$=SESSIONID$,%CLIENT$=CLIENT$,%CLIENTNAME$=SS1$(11,35)
1450 ESB$(8,10)=SS1$(1,10),ESB$(23,35)=SS1$(11,35)
1455 ESB[0]=FN%GET_DATETIME("",0),ESB[1]=FN%GET_DATETIME("",0)
1460 WRITE (Z[2],KEY=%SESSIONID$)IOL=0320
1480  } ELSE LGNMSG$="Client ID or Password is Incorrect. Please try again",%BASIC2$=""
1490  }
1500 IF LGNMSG$<>"" THEN {! 1900
1505 CLOSE (100); OPEN (100,OPT="TEXT",ERR=9000)%RELOGINTEMPLATE$
1506 REM "Setup line_tags$
1510 READ (100,END=1900)LINE$
1515 P1=MSK(LINE$,"\?ss\?[^?]*\?")
1520 IF P1>0 THEN {! 1800
1527 TAG$=LINE$(P1,MSL)
1530 LINE1$=LINE$(1,P1-1); LINE2$=MID(LINE$,P1+MSL)
1600 SWITCH TAG$ ! 1750 END SWITCH
1610 CASE "?ss?loginmsg?"
1615 LINE$=LINE1$+LGNMSG$+LINE2$
1620 BREAK
1625 CASE "?ss?base_page_ref?"
1630 LINE$=LINE1$+%BASEPAGEREF$+LINE2$; BREAK
1635 CASE "?ss?cgi_command?"
1637 LINE$=LINE1$+%CGICOMMAND$+LINE2$; BREAK
1750 END SWITCH 
1800  }
1810 PRINT (OUTPUT)LINE$
1850 GOTO 1510
1900  }
1996 GOTO 9900
5100 ADD_MSG_TO_LOG:! Add message to log file
5110 MOUT$="SESSION ID|"+EC$[1](1,7)+"|FILE|"+%WEBEC_FILE_NAME$+"|WORKSHEET|"+ECD$(1,10)+"|LINE|"+ECE$(25,3)+"|MESSAGE NO|"+STR(MESSAGE_NO:"00000")+"|MESSAGE|"+MESSAGE$+"|"
5135 MX=FN%_LOG_MESSAGE("MESG",MOUT$)
5145 RETURN 
5149 ! 
8150 BYB_PROCESSING:! Send LINE$ to EC3BYB for generic fields processing      
8160 CALL "EC3BYB",ERR=8161,X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,LINE$,BYBINFO${ALL},BYBOPTIONS$
8195 RETURN 
9000 REM "ERROR PROCESSING
9002 J=ERR,K=TCB(5); SETERR 0000; ENDTRACE 
9010 %WEBSSERR$="ERR: "+STR(J)+" AT: "+STR(K)+" IN "+PGN
9090 GOTO 9900
9900 REM "End
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9950 EXIT 
9999 END 
