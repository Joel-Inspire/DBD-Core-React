0010 REM "Rebuild Order Status Sort Files
0020 SETESC 9300; SETERR 9000
0035 REM "5.2 - 10/20/03 - 14.8575 - dmm - SSP# 165579
0040 REM "Copyright 2003 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="EW3UT8",X1$="Order Status Sort Rebuild"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13]
0320 IOLIST B$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))],A[13]
0510 Z$="01O FS1...  02O FT3...  03O ECW...  04O ECS...  05O AR1...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0605 ACTIVE_CHANNEL=1,ACTIVE_FILES$="FS1"
0610 GOSUB 6000
0620 GOSUB 8100
0640 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0700 REM "SELECTED CUSTOMER
0710 IF LEN(Q1$)=10 THEN CUSTOMER$=Q1$; GOTO 0740
0715 DIM CUSTOMER$(10)
0720 PRINT @(12,8),"Enter the customer or leave blank for all:",
0730 DIM W0$(10); CALL "ZZENTR","AXU",A{ALL},CUSTOMER$,X4$,X3$,58,8,1+K9,10,C0,"A/R","{1"+X$,"","AR2MAA00","AR1","",""
0740 IF CUSTOMER$=DIM(10) THEN GOTO 0760
0742 FIND (Z[5],KEY=CUSTOMER$,DOM=0700)AR1$; PRINT @(30,10),AR1$(11,35),
0744 FIND (Z[4],KEY=CUSTOMER$,DOM=0745)ECS$; GOTO 0760
0746 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Customer has no WebEC parameters","","",0; GOTO 0700
0780 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Correct?","","",0; ON Z GOTO 0781,0700
0799 PRINT @(0,20),'CE',"Clearing existing sort records...",
0800 IF CUSTOMER$=DIM(10) THEN CALL "ZZINIT",STR(Z[3]:"00"); GOTO 0850
0810 REM "clear sort records for selected customer
0812 READ (Z[3],KEY=CUSTOMER$,DOM=0813)
0820 ECWKEY$=KEY(Z[3],END=0830); IF ECWKEY$(1,LEN(CUSTOMER$))<>CUSTOMER$ THEN GOTO 0830
0822 REMOVE (Z[3],KEY=ECWKEY$); GOTO 0820
0850 PRINT @(0,20),'CL',
0990 PRINT @(20,16),"Processing Open Orders",
1000 REM "BEGIN MAIN PROCESS
1010 GOSUB SET_READONLY; K$=KEY(Z[ACTIVE_CHANNEL],END=2000); READ (Z[ACTIVE_CHANNEL],KEY=K$)IOL=0310; GOSUB CLEAR_READONLY
1020 C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
1100 REM "verify that customer on order is set up in web EC
1110 FIND (Z[4],KEY=A$(6,10),DOM=1000); REM "if no cust parms then skip
1120 IF CUSTOMER$<>DIM(10) THEN IF CUSTOMER$<>A$(6,10) THEN GOTO 1000
1200 CALL "EW3SOR",X3$,X4$,1,A$,A{ALL},ACTIVE_FILE$,"","",""
1600 GOTO 1000
2000 REM "end of file
2005 GOSUB CLEAR_READONLY
2010 IF ACTIVE_CHANNEL=1 THEN ACTIVE_CHANNEL=2,ACTIVE_FILE$="FT3"; GOSUB 8100; C=0; PRINT @(20,16),'CE',"Processing Archived Orders",; GOTO 1000
5000 REM "EOJ
5010 C=T; GOSUB 8150
5020 IF Q1$="" THEN CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This utility will clear the User Defined Order Status sort file and rebuild",@(0,5),"it from both the open order file and the archived order file"
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[ACTIVE_CHANNEL],T9,X3$,T,T0,K,B,D,S0,S1,F$
8115 PRINT @(0,7),"There are "+STR(T)+" records to process"
8129 REM "Set T0, we make sure T0 is > 1, because later on we MOD and look for avalue of 1. IF T0 is 1, then nothing would get reported. We look for a result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T0'th record to get the first report
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,C
8195 RETURN 
8500 SET_READONLY:REM "Set the PRM READONLY to on
8510 SAVE_XI=PRM('XI'); SET_PARAM 'XI' ! Ignore exclusive locks, save previous setting
8525 SET_READONLY_END:RETURN 
8530 CLEAR_READONLY:REM "Restore previous setting
8535 SET_PARAM 'XI'=SAVE_XI ! restore previous setting
8545 CLEAR_READONLY_END:RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
