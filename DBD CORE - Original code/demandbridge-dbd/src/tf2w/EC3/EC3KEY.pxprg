0010 REM "Create WebEC Login One-Time Key <EC3KEY>
0020 SETESC 9300; SETERR 9000
0035 REM "5.4 - 06/07/05 - 12.590277 - dmm - SSP# 182735
0040 REM "Copyright 2005 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! This program will be called from EC3BYB to create a one-time key for login.  Original work done for WO182735.
0051 ! ACTION$="W" - write a new one_time_key, ACTION$="U" - use a one_time_key, if valid return buyer/password, remove key
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,ACTION$,BUYER$,ONE_TIME_KEY$,PASSWORD$,RETURN_CODE
0100 SETERR 9000
0110 X0$="EC3KEY",X1$="Create WebEC Login One-Time Key"
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "IOLISTS                                                          
0310 IOLIST A$
0320 IOLIST B$
0500 REM "FILES                                                            
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O EE0...  02O ECA...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9200
0595 ! 
0700 ! Based on ACTION$, go to appropriate routine
0710 ON POS(ACTION$="WU")-1 GOTO 0900,1500
0900 ! Read buyer record, make sure it's valid
0910 GOSUB SET_READONLY; DIM ECA$(1540); BUYER$=PAD(BUYER$,15); READ (Z[2],KEY=BUYER$,DOM=9210)ECA$(1)
0915 GOSUB CLEAR_READONLY
0945 ! 
0980 ALPHA$="ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz"
0990 ONE_TIME_KEY$=""
1000 ! Create record in EE0 file, return key to calling program
1010 TIME$=STP(STR(TIM:"00.000000"),3,"."); NUM_TIME=NUM(TIME$)
1015 RANDOMIZE NUM_TIME
1020 FOR X=1 TO 4
1025 RANDOM_NUM=RND(NUM_TIME)
1030 RANDOM_NUM$=STR(RANDOM_NUM:"00000")
1035 REPLACE$=RANDOM_NUM$(X,1)
1040 REPLACE=NUM(REPLACE$)
1045 IF REPLACE=0 THEN REPLACE=9+X
1050 REPLACE=REPLACE*X
1055 RANDOM_NUM$(X,1)=ALPHA$(REPLACE,1)
1060 ONE_TIME_KEY$+=RANDOM_NUM$
1065 NEXT X
1100 DIM EE0$(127)
1110 EE0$(1,20)=ONE_TIME_KEY$
1115 EE0$(21,15)=BUYER$
1120 EE0$(36,6)=X3$(21,6)
1125 WRITE (Z[1],KEY=EE0$(1,20),DOM=*NEXT)EE0$; GOTO 1200
1130 EE0$(1,20)=FN%NEXT_SEQ$(EE0$(1,20),5); GOTO 1125
1200 ONE_TIME_KEY$=EE0$(1,20)
1300 MESSAGE$="EC3KEY created record "+EE0$(1,20)+" for Buyer ID "+BUYER$; GOSUB 7500
1400 ! All done
1410 GOTO 9240
1500 ! ONE_TIME_KEY$ passed in, check if valid, if so read buyer record for password, set BUYER$ and PASSWORD$, remove key, can only be used once.
1510 DIM EE0$(127); EXTRACT (Z[1],KEY=ONE_TIME_KEY$,DOM=9220)EE0$(1)
1520 BUYER$=EE0$(21,15)
1530 GOSUB SET_READONLY; DIM ECA$(1540); BUYER$=PAD(BUYER$,15); READ (Z[2],KEY=BUYER$,DOM=9210)ECA$(1)
1535 GOSUB CLEAR_READONLY
1540 PASSWORD$=STP(ECA$(65,15),1)
1590 REMOVE (Z[1],KEY=ONE_TIME_KEY$)
1595 ! 
1690 MESSAGE$="EC3KEY used record "+EE0$(1,20)+" for Buyer ID "+BUYER$; GOSUB 7500
1695 ! 
1700 ! All done
1710 GOTO 9240
7400 SET_READONLY:
7410 SET_PARAM 'XI'
7420 SET_READONLY_END:RETURN 
7430 CLEAR_READONLY:
7435 SET_PARAM -'XI'
7440 CLEAR_READONLY_END:RETURN 
7500 ! Log message 
7510 IF NOT(%QUIET_MODE) THEN {
7520 MESSAGE_TYPE$="INFO"
7540 IF %X3$(48,2)="2" THEN OUT$=DTE(0:"%Y-%Dz-%Mz|%Hz:%mz:%sz")+"|"+MESSAGE_TYPE$+"|"+MESSAGE$ ELSE OUT$=DTE(0:"%Y-%Mz-%Dz|%Hz:%mz:%sz")+"|"+MESSAGE_TYPE$+"|"+MESSAGE$; PRECISION 2
7550 IF %NO_LOG THEN GOTO *NEXT ELSE PRINT (%WEBEC_LOG_FILE)OUT$
7560 IF %DISPLAY_LOG THEN PRINT OUT$
7570  } ! NOT (%QUIET_MODE)
7590 RETURN 
7595 ! 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9002 J=ERR,K=TCB(5); SETERR 0000; ENDTRACE 
9010 LAST_WEBEC_ERR$="ERR: "+STR(J)+" AT: "+STR(K); LAST_WEBEC_ERR$=GBL("LAST_WEBEC_ERR",LAST_WEBEC_ERR$); REM "131928
9090 GOTO 9250
9200 RETURN_CODE=1; GOTO 9900 ! Problem opening files
9210 GOSUB CLEAR_READONLY; RETURN_CODE=2; GOTO 9900 ! Buyer record not found
9220 MESSAGE$="ECEKEY can't find record "+ONE_TIME_KEY$; GOSUB 7500; RETURN_CODE=3; GOTO 9900 ! one_time_key not found
9230 RETURN_CODE=4; GOTO 9900 ! No email action codes matching
9240 RETURN_CODE=5; GOTO 9900 ! All done
9250 RETURN_CODE=6; GOTO 9900 ! Error occurred, check log
9900 REM "End
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9950 EXIT 
9999 END 
