0010 REM "Generate 3rd Party Order Confirmation Request Files <EC3OS3>
0035 REM "5.7 - 05/10/21 - 17.22902 - dmm - SSP# 307345
0037 REM "307345-DBD-167-DBSPT-110833;Sender Id in Cust Defaults OrdConf,Ship
0040 REM "Copyright 2021 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! Will be called from EC3WS1 if EC parm set for 3rd Party Order Confirmation
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,ORDER$,OTHER$
0100 SETERR 9000
0110 X0$="EC3OS3",X1$="Generate 3rd Party Order Confirmation Request Files"
0310 IOLIST EEE$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O EEE...  02O ECY...  03O ECA...  04O EC9...  05O FS1...  06O AR1...  13O ZZPARM"
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0525 ! 
0550 FIND (Z[13],KEY=X3$(9,3)+"XMLOUT",DOM=9900)XMLOUT$ ! Must have XMLOUT parameters
0560 READ (Z[2],KEY=XMLOUT$(25,15),DOM=9900)ECY$
0570 READ (Z[3],KEY=XMLOUT$(10,15),DOM=9900)ECA$
0580 BUYER$=STP(XMLOUT$(10,15)),PASSWORD$=STP(ECA$(65,15))
0595 ! 
0900 ! DBD-167-DBSPT-110833-SSP307345, read FS1 for to get customer code
0910 GOSUB SET_READONLY
0915 CUST$=DIM(10); DIM FS1$(606); READ (Z[5],KEY=ORDER$,DOM=*NEXT)FS1$(1); CUST$=FS1$(6,10)
0920 GOSUB CLEAR_READONLY
1000 ! Read EEE with order number, if found move on, if not we are done
1005 DIM AR1$(599); FIND (Z[6],KEY=CUST$,DOM=*NEXT)AR1$(1) ! DBD-167-DBSPT-110833-SSP307345, read customer for possible 3p sender id
1010 READ (Z[1],KEY=ORDER$,DOM=*NEXT)IOL=0310; GOTO 1015 ! DBD-167-DBSPT-110833-SSP307345, was DOM=9900, if no EEE then check AR1 for this customer for possible 3p Sender ID
1011 IF AR1$(543,10)=DIM(10) THEN GOTO 9900 ELSE SENDER_ID_3P_$=AR1$(543,10),AR1_SENDER_MODE=1; GOTO 1025 ! DBD-167-DBSPT-110833-SSP307345, no EEE record, but if AR1 has 3p Sender ID for OC then set it and move on
1015 IF AR1$(543,10)<>DIM(10) THEN SENDER_ID_3P$=AR1$(543,10),ORDER_ID_3P$=STP(EEE$(9,150),2),AR1_SENDER_MODE=1; GOTO 1025 ELSE IF EEE$(9,150)=DIM(150) OR EEE$(159,10)=DIM(10) THEN GOTO 9900 ! EEE record must contain the 3P order ID and the 3P sender ID DBD-167-DBSPT-110833-SSP307345
1020 ORDER_ID_3P$=STP(EEE$(9,150),2),SENDER_ID_3P$=EEE$(159,10)
1025 GOSUB GET_3P_INFO
1029 ! 
1095 ! 
1500 ! create request file
1505 RND$=STR(INT(RND*1000000):"0000000")
1510 OUT$=STP(ECY$(56,60))+DLM+STP(SUB(ECY$(176,20),"*","3P_ORD_CONF_"+DTE(JUL(DAY)+((TIM+(TCB(44)/3600))/24),*:"%Y%Mz%Dz_%Hz%mz_%sz")+"_"+RND$))
1520 OUT_TMP$=OUT$+".tmp"
1530 ERASE OUT_TMP$,ERR=*NEXT
1540 SERIAL OUT_TMP$,ERR=9900
1550 OUTPUT=HFN; OPEN LOCK (OUTPUT)OUT_TMP$
1590 ! 
1600 ! add entries to request file
1610 PRINT (OUTPUT)"basic=EC3OS1"
1620 PRINT (OUTPUT)"user="+BUYER$
1630 PRINT (OUTPUT)"password="+PASSWORD$
1640 PRINT (OUTPUT)"template="+TEMPLATE$
1650 PRINT (OUTPUT)"no_header=Y"
1660 PRINT (OUTPUT)"remove_file=Y"
1670 PRINT (OUTPUT)"send_data=Y"
1680 PRINT (OUTPUT)"url="+URL$
1690 PRINT (OUTPUT)"3p_order_id="+ORDER_ID_3P$
1700 PRINT (OUTPUT)"3p_sender_id="+SENDER_ID_3P$
1710 PRINT (OUTPUT)"display_order="+ORDER$
1895 ! 
1900 ! done with entries to request file
1910 CLOSE (OUTPUT)
1920 RENAME OUT_TMP$,OUT$,ERR=*NEXT
1940 IF SECOND_TIME THEN GOTO 5000
1945 ! 
1950 ! DBD-167-DBSPT-110833-SSP307345 if AR1_SENDER_MODE then see if we should do another with the same info for the EEE_SENDER
1955 IF NOT(AR1_SENDER_MODE) OR EEE$="" THEN GOTO 5000
1960 IF EEE$(159,10)<>DIM(10) AND EEE$(9,150)<>DIM(150) AND SENDER_ID_3P$<>EEE$(159,10) THEN SENDER_ID_3P$=EEE$(159,10); GOSUB GET_3P_INFO ELSE GOTO 5000
1965 SECOND_TIME=1; GOTO 1500
1990 GOTO 5000
1995 ! 
5000 REM "EOJ
5040 GOTO 9900
5095 ! 
7000 GET_3P_INFO:! Read EC9 for template and url
7010 READ (Z[4],KEY=SENDER_ID_3P$+"00201",DOM=9900)EC9_00201$
7015 TEMPLATE_FILE$=STP(ECY$(56,60),2)+DLM+STP(EC9_00201$(16,30),2)
7020 OPEN (HFN,ERR=9900)TEMPLATE_FILE$; CHAN_1=LFO
7025 READ RECORD (CHAN_1,END=9900)TEMPLATE$
7030 TEMPLATE$=STP(TEMPLATE$)
7040 CLOSE (CHAN_1)
7045 ! 
7050 READ (Z[4],KEY=SENDER_ID_3P$+"00202",DOM=9900)EC9_00202$
7055 URL_FILE$=STP(ECY$(56,60),2)+DLM+STP(EC9_00202$(16,30),2)
7060 OPEN (HFN,ERR=9900)URL_FILE$; CHAN_2=LFO
7065 READ RECORD (CHAN_2,END=9900)URL$
7070 URL$=STP(URL$)
7080 CLOSE (CHAN_2)
7090 RETURN 
7095 ! 
7400 SET_READONLY:
7410 SET_PARAM 'XI'
7420 SET_READONLY_END:RETURN 
7430 CLEAR_READONLY:
7435 SET_PARAM -'XI'
7440 CLEAR_READONLY_END:RETURN 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,1
9045 REM 
9050 ON Y7 GOTO 9060,9100,9190,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9190 GOSUB 6600; GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9700 REM "FILES
9740 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 0521,9900
9790 RETURN 
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9940 EXIT 
9999 END 
56000 REM "233389-XML Order Confirmation for H&R Block
56002 REM "307345-DBD-167-DBSPT-110833;Sender Id in Cust Defaults OrdConf,Ship
