0010 REM "<EC3PO2> Get XML files to process
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 10/02/13 - 13.836666 - crg - SSP# 265877
0037 REM "265877-Broker Forms; process to retrieve asn/invoice XML includes  
0040 REM "Copyright 2013 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="EC3PO2",X1$="Get XML files to process"
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "IOLISTS
0310 IOLIST A$
0320 IOLIST B$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O AP4... 13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
1000 ! Get number of files to process
1005 DIM AP4$:IOL(Z[1]); READ DATA FROM DIM(LEN(AP4$)),REC=AP4$ TO IOL=IOL(AP4$)
1010 SELECT *,REC=AP4$ FROM Z[1] WHERE STP(AP4.SPC$)="PO2XML"
1020 RESP$=FN_SEND_CMD$("get_list","","GET LIST OF FILES")
1025 REMOVE$="remove_file"; GOSUB PROCESS_RESP
1040 RESP$=FN_SEND_CMD$("get_invoice","","GET LIST OF INVOICE FILES")
1045 REMOVE$="remove_invoice"; GOSUB PROCESS_RESP
1085 VENDOR_DONE:
1090 NEXT RECORD 
1095 GOTO 9900
1096 SEND_OPEN_ERR: MX=FN%_LOG_MESSAGE("EXCP","|FILE|"+%WEBEC_FILE_NAME$+"|EC3PO2|GET XML LIST FROM VENDOR|VENDOR|"+FN%ZZDISP$(AP4.VEND_DIV$+AP4.VEND_CODE$,"A/P")+"|ERROR ON OPEN|"+STR(ERR)+"|"); GOTO VENDOR_DONE
1099 ! ************************************************************
1600 GOTO 1000
3000 PROCESS_RESP:! Process RESP$ to pull out file names, put into LIST_CHAN, LIST_COUNT is number found
3005 LIST_COUNT=0,ERROR_COUNT=0
3010 MS=MSK(RESP$,"<:>MESSAGE:[^<]*<:>")
3015 IF MS<>0 THEN MESSAGE$=RESP$(MS,MSL),RESP$=SUB(RESP$,MESSAGE$,""),MESSAGE$=SUB(SUB(MESSAGE$,"MESSAGE:",""),"<:>",""); MX=FN%_LOG_MESSAGE("MESG","|FILE|"+%WEBEC_FILE_NAME$+"|EC3PO2|GET LIST OF FILES|VENDOR|"+FN%ZZDISP$(AP4.VEND_DIV$+AP4.VEND_CODE$,"A/P")+"|RETURN MESSAGE|"+MESSAGE$+"|")
3050 FS=MSK(RESP$,"<:>FILE:[^<]*<:>")
3060 WHILE FS>0
3065 FILE_URL$=RESP$(FS,MSL),RESP$=SUB(RESP$,FILE_URL$,""),FILE_URL$=SUB(SUB(FILE_URL$,"FILE:",""),"<:>",""),LIST_COUNT+=1
3066 FILE_URL$=STP(STP(FILE_URL$,3,$0D$),3,$0A$)
3067 MX=FN%_LOG_MESSAGE("MESG","|FILE|"+%WEBEC_FILE_NAME$+"|EC3PO2|GET LIST OF FILES|VENDOR|"+FN%ZZDISP$(AP4.VEND_DIV$+AP4.VEND_CODE$,"A/P")+"|FILE TO GET|"+FILE_URL$+"|")
3070 GOSUB GET_FILE
3080 FS=MSK(RESP$,"<:>FILE:[^<]*<:>")
3085 WEND 
3090 MX=FN%_LOG_MESSAGE("MESG","|FILE|"+%WEBEC_FILE_NAME$+"|EC3PO2|GET LIST OF FILES|VENDOR|"+FN%ZZDISP$(AP4.VEND_DIV$+AP4.VEND_CODE$,"A/P")+"|FILES RETREIVED|"+STR(LIST_COUNT)+"|FILES WITH ERRORS|"+STR(ERROR_COUNT)+"|")
3095 RETURN 
3099 ! **************************************************
3100 GET_FILE:! Given FILE_URL$ url, get the file. File will be in same dir as we are in, with extension of xml
3110 MAKE_NAME: NEW_FILE$=DTE(JUL(DAY)+((TIM+(TCB(44)/3600))/24),*:"%Y%Mz%Dz_%Hz%mz%sz")+"_"+STP(FN%ZZDISP$(AP4.VEND_DIV$+AP4.VEND_CODE$,"A/P"),3)+"_"+STR(SEQ++)+".xml" ! ssp 199338 - Ensure no spaces in file name, 203045 - Embed timstamp in filename
3115 CHK=HFN; OPEN (CHK,ERR=*NEXT)NEW_FILE$; CLOSE (CHK); GOTO MAKE_NAME ! If file exists, add to seq and keep going
3120 CONTENT$=FN_SEND_CMD$("get_file","'"+FILE_URL$+"' '"+%SERVER_DIR$+NEW_FILE$+"'","GET URL|"+FILE_URL$+"|NEW FILE NAME|"+NEW_FILE$)
3125 MS=MSK(CONTENT$,"<:>MESSAGE:[^<]*<:>")
3130 IF MS THEN {
3135 LIST_COUNT-=1,ERROR_COUNT+=1; MS$=CONTENT$(MS,MSL),MS$=SUB(SUB(MS$,"MESSAGE:",""),"<:>","")
3140 MX=FN%_LOG_MESSAGE("MESG","|FILE|"+%WEBEC_FILE_NAME$+"|EC3PO2|GET URL|VENDOR|"+FN%ZZDISP$(AP4.VEND_DIV$+AP4.VEND_CODE$,"A/P")+"|URL TO GET|"+FILE_URL$+"|MESSAGE|"+MS$+"|")
3145  } ELSE {
3146 MX=FN%_LOG_MESSAGE("MESG","|FILE|"+%WEBEC_FILE_NAME$+"|EC3PO2|GET URL|VENDOR|"+FN%ZZDISP$(AP4.VEND_DIV$+AP4.VEND_CODE$,"A/P")+"|URL TO GET|"+FILE_URL$+"|CONTENT LENGTH RECEIVED|"+CONTENT$+"|")
3150 ! SERIAL %SERVER_DIR$+NEW_FILE$,ERR=*PROCEED; WR=HFN; OPEN LOCK (WR)%SERVER_DIR$+NEW_FILE$
3155 ! PRINT (WR)CONTENT$
3160 ! CLOSE (WR)
3165 GOSUB REMOVE_FILE
3185  }
3195 RETURN 
3199 ! ************************************************************
3200 REMOVE_FILE:! Given file URL$, get file name and send request to remove it
3205 LOCAL FILE_NAME$,RETRY_COUNT,REMOVE_OK
3210 FILE_NAME$=MID(FILE_URL$,POS("/"=FILE_URL$,-1)+1)
3213 RETRY_COUNT=0,REMOVE_OK=0
3215 WHILE RETRY_COUNT<3 ! 265877 - Retry removal of file max 3 times
3217 RETRY_COUNT++
3220 CONTENT$=FN_SEND_CMD$(REMOVE$,"'"+FILE_NAME$+"'","REMOVE FILE|"+FILE_NAME$)
3225 MS=MSK(CONTENT$,"<:>MESSAGE:[^<]*<:>")
3230 IF MS THEN {
3235 LIST_COUNT-=1,ERROR_COUNT+=1; MS$=CONTENT$(MS,MSL),MS$=SUB(SUB(MS$,"MESSAGE:",""),"<:>","")
3240 MX=FN%_LOG_MESSAGE("MESG","|FILE|"+%WEBEC_FILE_NAME$+"|EC3PO2|REMOVE FILE|"+FILE_NAME$+"|MESSAGE|"+MS$+"|")
3241 REMOVE_OK=1
3242 BREAK ! If here, removal succeeded, break out of retry loop
3245  } ELSE {! SSP250603, we don't have expected msg, need to capture
3250 MX=FN%_LOG_MESSAGE("MESG","|FILE|"+%WEBEC_FILE_NAME$+"|EC3PO2|REMOVE FILE|"+FILE_NAME$+"|RAW MESSAGE|"+CONTENT$+"|ATTEMPT|"+STR(RETRY_COUNT)) ! SSP250603 
3255  }
3260 WEND ! End of retry loop
3265 IF NOT(REMOVE_OK) THEN {! 265877 - file remove attempts were unsuccessful
3270 MX=FN%_LOG_MESSAGE("MESG","|FILE|"+%WEBEC_FILE_NAME$+"|EC3PO2|REMOVE FILE|"+FILE_NAME$+"|REMOVE UNSUCCESSFUL|"+CONTENT$+"|") ! 
3275  }
3295 RETURN 
3299 ! *********************************************
4051 MM=MSK(RESP$,"<:>MESSAGE:.*<:>"); IF MM<>0 THEN RESP_HOLD$=RESP$,RESP$=SUB(SUB(RESP$(MM,MSL),"<:>",""),"MESSAGE:","")
4060 MX=FN%_LOG_MESSAGE("MESG","SESSION_ID|"+%EC3PR0_ECA$(1,7)+"|FILE|"+%WEBEC_FILE_NAME$+"|EC3PO1|SEND XML INFO TO VENDOR|PO_NUM|"+FN%ZZDISP$(PO_NUM$,"P/O")+"|FILE_TO_SEND|"+FILE_TO_SEND$+"|RESPONSE|"+RESP$+"|")
4075 SEND_DONE:
4085 MX=FN%_LOG_MESSAGE("MESG","SESSION_ID|"+%EC3PR0_ECA$(1,7)+"|FILE|"+%WEBEC_FILE_NAME$+"|EC3PO1|SEND XML INFO TO VENDOR|PO_NUM|"+FN%ZZDISP$(PO_NUM$,"P/O")+"|SEND DONE|")
8000 FN_SEND_CMD:
8002 DEF FN_SEND_CMD$(LOCAL CMD$, LOCAL ARG$, LOCAL MESSAGE$)
8005 LOCAL RESP$; RESP$=""
8010 SEND_CMD$="< "+%SERVER_TEMPLATE_DIR$+STP(AP4.WEB_ADDRESS$)+"_"+CMD$+" "+ARG$
8015 MX=FN%_LOG_MESSAGE("MESG","|FILE|"+%WEBEC_FILE_NAME$+"|EC3PO2|"+MESSAGE$+"|VENDOR|"+FN%ZZDISP$(AP4.VEND_DIV$+AP4.VEND_CODE$,"A/P")+"|COMMAND|"+SEND_CMD$+"|")
8030 SEND_CHAN=HFN; OPEN (SEND_CHAN,ERR=SEND_OPEN_ERR)SEND_CMD$
8035 READ (SEND_CHAN,BSY=*SAME,END=SEND_READ_DONE)RESP_IN$; RESP$+=RESP_IN$+$0A$; GOTO *SAME; REM "ssp 199338
8040 SEND_READ_DONE:
8045 RESP_IN$=""; CLOSE (SEND_CHAN)
8090 RETURN RESP$
8095 END DEF
8099 ! ****************************************************
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56002 REM "199338-SENDING ORDER TO BROKER FORMS
56004 REM "199040-XML integration with Broker Forms - questions, documentation
56006 REM "203045-File naming convention to use timestamps and vendor code
56008 REM "250603-CIGP: 7 Broker orders duplicated quantities. File picked up 
56009 REM "265877-Broker Forms; process to retrieve asn/invoice XML includes  
