0010 REM "<EC3PAS> Email/Retrieve forgotten password
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 02/18/14 - 16.68 - crg - SSP# 268517
0037 REM "268517-Getting Forgot Password Page: Mail Delivery Subsystem Error 
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT
0100 SETERR 9000
0110 X0$="EC3PAS",X1$="Email/Retrieve forgotten password"
0120 EOL$=$0D0A$
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "IOLISTS
0310 IOLIST A$
0320 IOLIST B$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O ECA...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0610 SEND_EMAIL_BUYER$="N"
0620 IF NOT(NUL(EC$[14](482,5))) THEN FILE_SUFFIX$=STP(EC$[14](482,5),3) ELSE XX$=STP(EC$[14](176,20),3," "),XX=POS("."=XX$); FILE_SUFFIX$=MID(XX$,XX)
1000 REM "Process DATA array
1010 FOR I=1 TO NUM_ENTRIES
1015 TAG$=DATA$[I,0],VALUE$=DATA$[I,1]
1025 SWITCH TAG$ ! 1185 end switch
1030 CASE "template"; TEMPLATE$=VALUE$; BREAK
1040 CASE "mail_template"; MAIL_TEMPLATE$=VALUE$; BREAK
1050 CASE "err_template"; ERR_TEMPLATE$=VALUE$; BREAK
1060 CASE "buyer_id"; BUYER_ID_LOGIN$=STP(VALUE$,2); BREAK
1070 CASE "buyer_email"; BUYER_EMAIL_LOGIN$=STP(VALUE$,2); BREAK
1180 DEFAULT ; BREAK ! Set to same name variable as TAG$
1185 END SWITCH ! 1050
1186 ! 
1190 NEXT I
1195 DIM BYBINFO$[5]; BYBINFO$[1]=PGN,BYBINFO$[2]="214956.20080312",BYBINFO$[3]=TEMPLATE$,BYBOPTIONS$=""
1200 ! 
1210 REM Process Buyer Input
1213 IF NOT(NUL(BUYER_ID_LOGIN$)) THEN READ (Z[1],KEY=PAD(BUYER_ID_LOGIN$,15),DOM=*NEXT)BUYER_REC$ ! 268517
1216 IF NUL(BUYER_REC$) THEN SEND_BUYER_EMAIL$="N" ELSE IF UCS(BUYER_REC$(109,60))=UCS(PAD(BUYER_EMAIL_LOGIN$,60)) THEN SEND_BUYER_EMAIL$="Y" ELSE SEND_BUYER_EMAIL$="N"
1220 IF SEND_BUYER_EMAIL$="Y" THEN BUYER_PASSWORD$=BUYER_REC$(65,15); ERR_MESSAGE$="Your password will be e-mailed to you in a few minutes!" ELSE ERR_MESSAGE$="The entered buyer id and e-mail address could not be found in our records!" END_IF 
1230 IF SEND_BUYER_EMAIL$="Y" AND NUL(MAIL_TEMPLATE$) THEN MAIL_TEMPLATE$=TEMPLATE$
1231 IF SEND_BUYER_EMAIL$="N" AND NUL(ERR_TEMPLATE$) THEN ERR_TEMPLATE$=TEMPLATE$
1240 IF SEND_BUYER_EMAIL$="N" THEN TEMPLATE$=ERR_TEMPLATE$; END_IF ; PROCESS_TEMPLATE$=TEMPLATE$,PROCESS_OUTPUT=OUTPUT; GOSUB PROCESS_TEMPLATE ! Process output for viewing on browser
1250 IF SEND_BUYER_EMAIL$="Y" THEN GOSUB SEND_BUYER_EMAIL
1260 GOTO 9900
1280 ! 
1500 REM "Process template
1501 PROCESS_TEMPLATE:
1504 CLOSE (100); OPEN (100,OPT="TEXT",ERR=9000)PROCESS_TEMPLATE$
1510 READ_TEMPLATE:READ (100,END=DONE_WITH_TEMPLATE)LINE$; LINE$=FNBYB$(LINE$)
1515 CHKTAG=1
1520 WHILE CHKTAG ! 1920 wend
1522 P1=MSK(LINE$,"\?tf\?[^?]*\?"); IF P1=0 THEN CHKTAG=0; BREAK
1530 TAG$=LINE$(P1,MSL); LINE1$=LINE$(1,P1-1),LINE2$=MID(LINE$,P1+MSL)
1600 SWITCH TAG$ ! 1910 end switch
1615 CASE "?tf?forgot_buyer_password?"; IF EMAIL_MESSAGE$="Y" THEN LINE$=LINE1$+BUYER_PASSWORD$+LINE2$ ELSE LINE$=LINE1$+LINE2$ END_IF ; BREAK
1620 CASE "?tf?err_message?"; LINE$=LINE1$+ERR_MESSAGE$+LINE2$; BREAK
1630 CASE "?tf?forgot_buyer_id?"; LINE$=LINE1$+BUYER_ID_LOGIN$+LINE2$; BREAK
1640 CASE "?tf?forgot_buyer_email?"; LINE$=LINE1$+BUYER_EMAIL_LOGIN$+LINE2$; BREAK
1900 DEFAULT ; LINE$=LINE1$+LINE2$ ! didn't match existing tag, so remove it
1910 END SWITCH ! 1600
1920 WEND ! 1520
2810 IF LINE$<>"" THEN PRINT (PROCESS_OUTPUT)LINE$
2900 GOTO READ_TEMPLATE; REM next read
2950 DONE_WITH_TEMPLATE:
2960 RETURN 
2990 ! 
4200 ! Send buyer email message containing password
4210 SEND_BUYER_EMAIL:
4212 HTML_TEXT$=%SERVER_DIR$+MAIL_TEMPLATE$; TMP_NAME$=%SERVER_DIR$+MAIL_TEMPLATE$
4217 MX=FN%_LOG_MESSAGE("MESG","SESSION_ID|"+EC$[1](1,7)+"|FILE|"+%WEBEC_FILE_NAME$+"|"+PGN)
6600 REM "Create email request file 148750
6601 REM "Use port 1001 and 1002
6602 DIM ETEXT_NEEDED$(30); ETEXT_NEEDED$(1,10)="email",ETEXT_NEEDED$(16,10)="email",ETEXT_NEEDED$(11,5)="00001",ETEXT_NEEDED$(26,5)="00002"; CALL "EC3BYC",X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,ETEXT_NEEDED$,ETEXT${ALL}
6605 CLOSE (1002); CLOSE (1003)
6610 PRECISION 5; EMAIL_FILE$=%SERVER_DIR$+"TMP"+EMAIL_ACTION$+STR(INT(RND*100000)); PRECISION 2
6611 OPEN (1002,ERR=6615)EMAIL_FILE$; CLOSE (1002); GOTO 6610; REM "if we can open the file then it is in use, try again
6615 REM "Create file, then open files and copy
6620 SERIAL EMAIL_FILE$,DISK_NO,0,ERR=6610
6621 OPEN (1002)EMAIL_FILE$; MESS$="Creating temp HTML e-mail file "+EMAIL_FILE$,MTYPE$="EMAIL"; GOSUB 7500
6622 TEXT_IN$=ETEXT$[1]; TEXT_IN$=SUB(TEXT_IN$,"?tf?email?",STP(BUYER_REC$(109,60),2)); TEXT_IN$=SUB(TEXT_IN$,"?tf?subject?","Password request")
6623 WRITE RECORD (1002)TEXT_IN$; REM "write out email header
6625 PROCESS_TEMPLATE$=TMP_NAME$,PROCESS_OUTPUT=1002,EMAIL_MESSAGE$="Y"; GOSUB PROCESS_TEMPLATE ! Process and write output for email message
6629 WRITE RECORD (1002)ETEXT$[2]; REM "write out email closing
6630 PRECISION 5; EMAIL_ACTION_FILE$=%SERVER_DIR$+STR(INT(RND*100000))+FILE_SUFFIX$; PRECISION 2; CLOSE (1003); OPEN (1003,ERR=6631)EMAIL_ACTION_FILE$; GOTO 6630; REM "find unused tmp file name for email control file
6631 SERIAL EMAIL_ACTION_FILE$,DISK_NO,0,ERR=6640; OPEN (1003)EMAIL_ACTION_FILE$
6632 WRITE RECORD (1003)"basic=EC3EM0"; WRITE RECORD (1003)"template="+EMAIL_FILE$; WRITE RECORD (1003)"no_header=Y"; WRITE RECORD (1003)"remove_file=Y"
6633 MESS$="Posting e-mail to send : "+EMAIL_ACTION_FILE$,MTYPE$="EMAIL"; GOSUB 7500
6640 CLOSE (1002); CLOSE (1003)
6645 RETURN 
6650 ! 
7500 REM "Put MESS$ in log & display to screen if IMODE
7501 REM "MTYPE$ is type, clear it after, if '' then say info
7502 REM "valid are INFO,WARN,EXIT,EXCP,STAT,EMAIL
7510 MX=FN%_LOG_MESSAGE(MTYPE$,MESS$)
7541 MTYPE$=""
7545 RETURN 
7950 ! 
8800 FNBYB:
8801 DEF FNBYB$(LOCAL DATA$)
8802 ! Send DATA$ through EC3BYB
8820 CALL "EC3BYB",ERR=*NEXT,X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,DATA$,BYBINFO${ALL},BYBOPTIONS$
8840 RETURN DATA$
8845 END DEF
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56001 REM "214956-Provide ability for users to retrieve forgotten passwords
56002 REM "268517-Getting Forgot Password Page: Mail Delivery Subsystem Error 
