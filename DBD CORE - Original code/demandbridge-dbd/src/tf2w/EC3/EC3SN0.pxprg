0010 REM "Generate Ship Notice Request Files <EC3SN0>
0035 REM "5.7 - 05/17/22 - 17.37101 - dmm - SSP# 307401
0037 REM "307401-DBD-239; Add AR1 EQD_CUST field to DBDXML SHIP_NOTICE       
0040 REM "Copyright 2022 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! Will be called from AR2DUM if EC parm set for Ship Notices
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,AZB_KEY$,ORDER$,OTHER$
0100 SETERR 9000
0110 X0$="EC3SN0",X1$="Generate Ship Notice Request Files"
0310 IOLIST EEE$
0320 IOLIST SM4$,SM4[0],SM4[1],SM4[2],SM4[3],SM4[4],SM4[5],SM4[6],SM4[7],SM4[8],SM4[9],SM4[10],SM4[11],SM4[12]
0330 IOLIST AZB$,AZB[0],AZB[1],AZB[2],AZB[3],AZB[4],AZB[5],AZB[6],AZB[7],AZB[8],AZB[9],AZB[10],AZB[11],AZB[12],AZB[13],AZB[14],AZB[15],AZB[16],AZB[17],AZB[18],AZB[19],AZB[20],AZB[21],AZB[22],AZB[23],AZB[24],AZB[25],AZB[26],AZB[27],AZB[28],AZB[29],AZB[30]
0340 IOLIST AZT$,AZT[0],AZT[1],AZT[2],AZT[3],AZT[4],AZT[5],AZT[6],AZT[7],AZT[8],AZT[9],AZT[10],AZT[11],AZT[12],AZT[13],AZT[14],AZT[15],AZT[16],AZT[17],AZT[18],AZT[19],AZT[20]
0350 IOLIST EDW$
0360 IOLIST FM1$,FM1[0],FM1[1],FM1[2],FM1[3],FM1[4],FM1[5],FM1[6],FM1[7],FM1[8],FM1[9],FM1[10],FM1[11],FM1[12],FM1[13],FM1[14],FM1[15],FM1[16],FM1[17],FM1[18],FM1[19],FM1[20],FM1[21],FM1[22],FM1[23],FM1[24],FM1[25],FM1[26],FM1[27],FM1[28],FM1[29],FM1[30],FM1[31]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O EEE...  02O SM4...  03O AZB...  04O AZT...  05O EDW...  06O ECY...  07O ECA...  08O EC9...  09O EC6...  10O ECS...  11O FMP...  12O ECR...  13O ZZPARM 14O FM1...  15O AP4...  16O FT4...  17O FT3...  18O FS1...  19O AR1...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0525 ! 
0550 FIND (Z[13],KEY=X3$(9,3)+"XMLOUT",DOM=9900)XMLOUT$ ! Must have XMLOUT parameters
0560 READ (Z[6],KEY=XMLOUT$(25,15),DOM=9900)ECY$
0570 READ (Z[7],KEY=XMLOUT$(10,15),DOM=9900)ECA$
0580 BUYER$=STP(XMLOUT$(10,15)),PASSWORD$=STP(ECA$(65,15))
0595 ! 
0900 DIM SM4[12],AZB[30],AZT[20]
0920 NUM_LINES=0,NUM_TRACKS=0,MULTIPLE_TRACKING$="" ! WO263088
0930 SEPERATOR$=", "
0950 CALL "ZZDISP","AX",ORDER$,"O/P",X3$,ORDER_NO$,"",0,0,X4$
0960 GOSUB READ_FT3; CUST$=FT3$(6,10) ! DBD-167-DBSPT-110833-SSP307345
0995 ! 
1000 ! Read EEE with order number, if found move on, if not we are done
1005 DIM AR1$(599); READ (Z[19],KEY=CUST$,DOM=*NEXT)AR1$(1) ! DBD-167-DBSPT-110833-SSP307345
1010 READ (Z[1],KEY=ORDER$,DOM=*NEXT)IOL=0310; GOTO 1015 ! DBD-167-DBSPT-110833-SSP307345
1011 IF AR1$(553,10)=DIM(10) THEN GOTO 9900 ELSE SENDER_ID_3P$=AR1$(553,10),AR1_SENDER_MODE=1; GOTO 1025 ! DBD-167-DBSPT-110833-SSP307345
1015 IF AR1$(553,10)<>DIM(10) THEN SENDER_ID_3P$=AR1$(553,10),ORDER_ID_3P$=STP(EEE$(9,150),2),AR1_SENDER_MODE=1; GOTO 1025 ELSE IF EEE$(9,150)=DIM(150) OR EEE$(159,10)=DIM(10) THEN GOTO 9900 ! EEE record must contain the 3P order ID and the 3P sender ID DBD-167-DBSPT-110833-SSP307345
1020 ORDER_ID_3P$=STP(EEE$(9,150),2),SENDER_ID_3P$=EEE$(159,10)
1025 GOSUB GET_3P_INFO
1026 DIM EC6_SENDER$(900); READ (Z[9],KEY=SENDER_ID_3P$,DOM=*NEXT)EC6_SENDER$(1) ! WO243602
1027 MULT_SN=0; IF EC6_SENDER$(282,1)="Y" THEN MULT_SN=1; DIM SN$[10,15,500] ! WO243602, if an external line number is referenced more than once, spin off an additional request, receiver of ShipNotice can't have line number referenced more than once. SSP278673, increase level 2 from 6 to 7 SSP296725, increase level 2 from 7 to 8 WO304831, increase level 2 from 8 to 9 SSP307255-DBD-101, increase level 2 from 9 to 12 SSP307285-DBD-115, increase level 2 from 12 to 15
1028 ORDER_ID_3P_2$=MID(EEE$,269,150) ! DBD-185-DBSPT-115417-SSP307358
1029 ! 
1030 ! Now look for an SM4 record for this order that has this invoice number
1035 INVOICE$=AZB_KEY$(7,8); IF INVOICE$=DIM(8) THEN GOTO 9900
1040 READ (Z[2],KEY=ORDER$,DOM=*NEXT)
1045 READ_SM4: SM4_KEY$=KEY(Z[2],END=NO_SM4_MATCH); READ (Z[2],KEY=SM4_KEY$)IOL=0320 ! WO300317, END was set to 9900 but now see if parm set that allows ship notice to generate even if no matching SM4 record
1050 IF SM4_KEY$(1,8)<>ORDER$ THEN GOTO NO_SM4_MATCH ! WO300317 GOTO was set to 9900 but now see if parm set that allows ship notice to generate even if no matching SM4 record
1055 IF SM4$(101,8)<>INVOICE$ THEN GOTO READ_SM4
1060 IF SM4$(383,60)=DIM(60) THEN GOTO READ_SM4 ! SSP 257952
1065 IF SM4$(338,1)="Y" THEN GOTO READ_SM4 ! ship notice sent flag set
1069 ! 
1070 ! We have an SM4 record for this order, this invoice, with a tracking number. WO300317, if parm allows then proceed even if no matching SM4 available, tracking number will be blank and carrier name will be blank
1075 TRACKING_NUMBER$=STP(SM4$(383,60)); GOSUB GET_CARRIER_NAME ! SSP257952
1080 READ (Z[3],KEY=AZB_KEY$,DOM=9900)IOL=0330
1085 CUST_PO$=STP(AZB$(103,15))
1090 DATE_SHIPPED$=AZB$(183,6) ! SSP228984
1092 AZB_FREIGHT_NUM$=STR(AZB[2]) ! SSP278673
1094 GOSUB HEADER_FIELDS ! SSP307255-DBD-101
1095 ! 
1100 READ (Z[4],KEY=AZB_KEY$,DOM=*NEXT)
1110 READ_AZT: AZT_KEY$=KEY(Z[4],END=1400); READ (Z[4],KEY=AZT_KEY$)IOL=0340 ! WO243602
1120 IF AZT_KEY$(1,17)<>AZB_KEY$ THEN GOTO 1400 ! WO243602
1122 IF AZT$(21,1)="S" THEN GOSUB SPECIAL_CHARGES ! SSP278673
1125 IF AZT$(21,1)="M" OR AZT$(67,3)=DIM(3) OR AZT$(67,3)="FRT" OR AZT[1]<0 THEN GOTO READ_AZT ! WO264379, was <=0, will check ECS parm to see if 0 shipped allowed DBD-167-DBSPT-110833-SSP307345 bypass if order line is FRT
1126 DIM ECS$(1000); FIND (Z[10],KEY=AZB$(18,10),DOM=*NEXT)ECS$(1) ! WO264379
1127 IF AZT[1]=0 AND ECS$(765,1)<>"Y" THEN GOTO READ_AZT ! WO264379, only allow zero qty shipped lines if ECS parm activated
1130 READ (Z[5],KEY=ORDER$+AZT$(67,3),DOM=*NEXT)IOL=0350; GOSUB EDW_FIELDS; GOTO 1135 ! SSP307285-DBD-115, DBD-167-DBSPT-110833-SSP307345
1131 IF NOT(AR1_SENDER_MODE) THEN GOTO READ_AZT ELSE DIM EDW$(200) ! DBD-167-DBSPT-110833-SSP-307345, we don't need EDW if in AR1_SENDER_MODE 
1135 IF EDW$(35,3)=DIM(3) THEN IF EC6_SENDER$(284,1)="Y" AND EDW$(38,20)<>DIM(20) THEN GOTO *NEXT ELSE IF AR1_SENDER_MODE THEN GOTO *NEXT ELSE GOTO READ_AZT ! SSP276575, if ext line num blank but parm set and ext field 1 not blank then go on else get next azt (default) DBD-167-DBSPT-110833-SSP307345, if AR1_SENDER_MODE then allow for blank EDW
1145 ! 
1150 ! We have an order line, an external line number, and qty > 0 WO264379, qty may be = 0 if ECS parms says OK
1155 TOT_AZT_LINES+=1 ! WO243602
1160 EXT_LINE$=STP(EDW$(35,3),2); IF EXT_LINE$="" THEN EXT_LINE$=STP(EDW$(38,20),2) ! SSP276575
1170 ! Read u/m record, use info to calculate qty shipped in units
1173 UOM$=AZT$(70,4),QTY_PER_UM=AZT[6]; IF AZT$(21,1)<>"S" THEN GOSUB FM1_FIRST_ALT_UM ! SSP296725, will check FM1 for override of U/M and qty per U/M
1175 DIM UM$(28),UM[1]; READ (Z[13],KEY="U/M"+UOM$,DOM=*NEXT)UM$(1),UM[0],UM[1] ! SSP 296725 move setting of UOM$ to line 1173
1180 QTY_SHIPPED=AZT[1]; IF QTY_PER_UM=0 THEN QTY_PER_UM=1 ! SSP296725 move setting of QTY_PER_UM to line 1173
1185 IF UM$(20,1)<>"Y" THEN DIVISOR=QTY_PER_UM ELSE DIVISOR=UM[1]
1190 QTY_SHIPPED_UNITS=QTY_SHIPPED/DIVISOR,UOM$=STP(UOM$)
1192 ITEM$=STP(AZT$(85,10),1) ! SSP278673
1193 ECR_EXT_REF_1$=DIM(20); DIM ECR$(1000); FIND (Z[12],KEY=AZT$(75,10)+AZT$(85,10),DOM=*NEXT)ECR$(1); ECR_EXT_REF_1$=ECR$(577,20) ! SSP296725, 3P system item code stored in ECR
1194 NO_SHIPNOTICE$="N"; DIM FT4$(356); FIND (Z[16],KEY=ORDER$+AZT$(67,3),DOM=*NEXT)FT4$(1); IF FT4$(9,1)<>DIM(1) THEN DIM AP4$(426); FIND (Z[15],KEY=FT4$(94,10),DOM=*NEXT)AP4$(1); IF POS("S"=AP4$(382,20))>0 THEN NO_SHIPNOTICE$="Y" ! WO304831, set no shipnotice flag for this line
1195 ! 
1198 GOSUB LINE_FIELDS ! SSP307255-DBD-101
1200 ! Line detail, add to line detail variables
1205 IF MULT_SN THEN GOSUB MULTIPLE_SHIP_NOTICES; GOTO READ_AZT ! WO243602
1207 NUM_LINES+=1 ! WO243602
1210 ELNS$+=EXT_LINE$+SEPERATOR$ ! External line number
1220 UOMS$+=UOM$+SEPERATOR$ ! Unit of measure
1230 QPUS$+=STR(QTY_PER_UM)+SEPERATOR$ ! Quantity per unit
1240 QSES$+=STR(QTY_SHIPPED)+SEPERATOR$ ! Quantity shipped in eaches
1250 QSUS$+=STR(QTY_SHIPPED_UNITS)+SEPERATOR$ ! Quantity shipped in units
1255 ITEMS$+=ITEM$+SEPERATOR$ ! SSP278673, item codes
1260 EER1S$+=ECR_EXT_REF_1$+SEPERATOR$ ! SSP296725, ECR external ref 1
1265 NOSNS$+=NO_SHIPNOTICE$+SEPERATOR$ ! WO304831, no shipnotice flag
1270 TFDS$+=TF_DESCRIPTION$+SEPERATOR$ ! SSP307255-DBD-101
1272 QORS$+=STR(QTY_ORDERED)+SEPERATOR$ ! SSP307255-DBD-101
1274 CWTS$+=STR(CTN_WT)+SEPERATOR$ ! SSP307255-DBD-101
1275 RCNS$+=EDW_RC_NUM$+SEPERATOR$ ! SSP307285-DBD-115
1276 EF1S$+=EDW_EXT_FIELD_1$+SEPERATOR$ ! SSP307285-DBD-115
1277 EF2S$+=EDW_EXT_FIELD_2$+SEPERATOR$ ! SSP307285-DBD-115
1290 GOTO READ_AZT
1299 ! 
1400 ! Done with AZT, ready to write request file WO243602
1402 ! WO263088, check EC cust parm, all tracking that matches order/invoice
1403 DIM ECS$(1000); FIND (Z[10],KEY=AZB$(18,10),DOM=*NEXT)ECS$(1); IF ECS$(764,1)="Y" THEN GOSUB MULTIPLE_TRACKING
1404 ! 
1405 IF TOT_AZT_LINES=0 THEN GOTO 9900 ! WO243602
1407 TOTAL_FRT_SPECIAL_LINES_NUM$=STR(TOTAL_FRT_SPECIAL_LINES),TOTAL_FREIGHT_NUM$=STR(AZB[2]+TOTAL_FRT_SPECIAL_LINES) ! SSP278673
1410 IF NOT(MULT_SN) THEN GOSUB CREATE_REQUEST_FILE; GOTO 1440 ! WO243602, DBD-167-DBSPT-110833-SSP307345
1420 IF NUM_SN=0 THEN GOTO 9900 ! WO243602
1421 SAVE_NUM_SN=NUM_SN ! DBD-167-DBSPT-110833-SSP307345
1425 FOR RSA=1 TO NUM_SN; GOSUB READ_SN_ARRAY; GOSUB CREATE_REQUEST_FILE; NEXT RSA ! WO243602
1430 ! move GOTO 5000 to line 1490
1440 ! DBD-167-DBSPT-110833-SSP307345, if AR1_SENDER_MODE then see if we should do another with the same info for the EEE_SENDER
1445 IF NOT(AR1_SENDER_MODE) OR EEE$="" OR SECOND_TIME THEN GOTO 1490
1450 IF EEE$(159,10)<>DIM(10) AND EEE$(9,150)<>DIM(150) AND SENDER_ID_3P$<>EEE$(159,10) THEN SENDER_ID_3P$=EEE$(159,10),SECOND_TIME=1; GOSUB GET_3P_INFO ELSE GOTO 1490
1455 IF NOT(MULT_SN) THEN GOSUB CREATE_REQUEST_FILE; GOTO 1490
1460 NUM_SN=SAVE_NUM_SN
1465 FOR RSA=1 TO NUM_SN; GOSUB READ_SN_ARRAY; GOSUB CREATE_REQUEST_FILE; NEXT RSA
1490 GOTO 5000 ! WO243602, DBD-167-DBSPT-110833-SSP307345
1499 ! 
1500 CREATE_REQUEST_FILE:! WO243602
1507 RND$=STR(INT(RND*1000000):"0000000")
1510 OUT$=STP(ECY$(56,60))+DLM+STP(SUB(ECY$(176,20),"*","SHIP_NOTICE_"+DTE(JUL(DAY)+((TIM+(TCB(44)/3600))/24),*:"%Y%Mz%Dz_%Hz%mz_%sz")+"_"+RND$))
1520 OUT_TMP$=OUT$+".tmp"
1530 ERASE OUT_TMP$,ERR=*NEXT
1540 SERIAL OUT_TMP$,ERR=9900
1550 OUTPUT=HFN; OPEN LOCK (OUTPUT)OUT_TMP$
1590 ! 
1600 ! add entries to request file
1610 PRINT (OUTPUT)"basic=EC3SN1"
1620 PRINT (OUTPUT)"user="+BUYER$
1630 PRINT (OUTPUT)"password="+PASSWORD$
1640 PRINT (OUTPUT)"template="+TEMPLATE$
1650 PRINT (OUTPUT)"no_header=Y"
1660 PRINT (OUTPUT)"remove_file=Y"
1670 PRINT (OUTPUT)"send_data=Y"
1680 PRINT (OUTPUT)"url="+URL$
1690 PRINT (OUTPUT)"3p_order_id="+ORDER_ID_3P$
1700 PRINT (OUTPUT)"3p_sender_id="+SENDER_ID_3P$
1710 PRINT (OUTPUT)"order_no="+ORDER_NO$
1715 PRINT (OUTPUT)"order_uf="+ORDER$ ! WO263088, order number unformatted 
1720 PRINT (OUTPUT)"cust_po="+CUST_PO$
1722 PRINT (OUTPUT)"azb_freight_num="+AZB_FREIGHT_NUM$ ! SSP278673
1723 PRINT (OUTPUT)"total_frt_special_lines_num="+TOTAL_FRT_SPECIAL_LINES_NUM$ ! SSP278673
1724 PRINT (OUTPUT)"total_freight_num="+TOTAL_FREIGHT_NUM$ ! SSP278673
1725 PRINT (OUTPUT)"date_shipped="+DATE_SHIPPED$ ! SSP228984
1727 PRINT (OUTPUT)"num_tracks="+STR(NUM_TRACKS) ! WO263088
1728 PRINT (OUTPUT)"multiple_tracking_no="+MULTIPLE_TRACKING$ ! WO263088 
1730 PRINT (OUTPUT)"tracking_no="+TRACKING_NUMBER$
1740 PRINT (OUTPUT)"carrier_name="+CARRIER_NAME$
1750 PRINT (OUTPUT)"num_lines="+STR(NUM_LINES)
1760 PRINT (OUTPUT)"ext_line_number="+ELNS$
1770 PRINT (OUTPUT)"um="+UOMS$
1780 PRINT (OUTPUT)"qty_per_um="+QPUS$
1790 PRINT (OUTPUT)"qty_shipped_eaches="+QSES$
1800 PRINT (OUTPUT)"qty_shipped_units="+QSUS$
1805 PRINT (OUTPUT)"item_code="+ITEMS$ ! SSP278673
1810 PRINT (OUTPUT)"ext_ref1="+EER1S$ ! SSP296725
1820 PRINT (OUTPUT)"no_shipnotice="+NOSNS$ ! WO304831
1822 PRINT (OUTPUT)"ship_via_code="+SHIP_VIA_CODE$ ! SSP307255-DBD-101
1824 PRINT (OUTPUT)"loc_addr_1="+LOC_ADDR_1$ ! SSP307255-DBD-101
1826 PRINT (OUTPUT)"loc_addr_2="+LOC_ADDR_2$ ! SSP307255-DBD-101
1828 PRINT (OUTPUT)"loc_city="+LOC_CITY$ ! SSP307255-DBD-101
1830 PRINT (OUTPUT)"loc_st="+LOC_ST$ ! SSP307255-DBD-101
1832 PRINT (OUTPUT)"loc_zip_code="+LOC_ZIP_CODE$ ! SSP307255-DBD-101
1834 PRINT (OUTPUT)"ship_to_name="+SHIP_TO_NAME$ ! SSP307255-DBD-101
1836 PRINT (OUTPUT)"attn_name="+ATTN_NAME$ ! SSP307255-DBD-101
1838 PRINT (OUTPUT)"tf_description="+TFDS$ ! SSP307255-DBD-101
1840 PRINT (OUTPUT)"qty_ordered="+QORS$ ! SSP307255-DBD-101
1842 PRINT (OUTPUT)"ctn_wt="+CWTS$ ! SSP307255-DBD-101
1844 PRINT (OUTPUT)"ordered_by="+ORDERED_BY$ ! SSP307255-DBD-101
1846 PRINT (OUTPUT)"ec_source="+EC_SOURCE$ ! SSP307255-DBD-101
1848 PRINT (OUTPUT)"rc_number="+RCNS$ ! SSP307285-DBD-115
1849 PRINT (OUTPUT)"ext_field_1="+EF1S$ ! SSP307285-DBD-115
1850 PRINT (OUTPUT)"ext_field_2="+EF2S$ ! SSP307285-DBD-115
1851 PRINT (OUTPUT)"cust_code_formatted="+CUST_CODE_FORMATTED$ ! SSP307285-DBD-115
1852 PRINT (OUTPUT)"cust_code_unformatted="+CUST_CODE_UNFORMATTED$ ! SSP307285-DBD-115
1853 PRINT (OUTPUT)"inv_num="+INV_NUM$ ! SSP307285-DBD-115
1854 PRINT (OUTPUT)"order_status="+ORDER_STATUS$ ! SSP307319-DBD-147
1855 PRINT (OUTPUT)"3p_order_id_2="+ORDER_ID_3P_2$ ! DBD-185-DBSPT-115417-SSP307358
1856 PRINT (OUTPUT)"eqd_cust="+STP(MID(AR1$,564,6)) ! DBD-239-SSP307401
1895 ! 
1900 ! done with entries to request file
1910 CLOSE (OUTPUT)
1920 RENAME OUT_TMP$,OUT$,ERR=*NEXT
1940 ! 
1990 RETURN ! WO243602
1995 ! 
5000 REM "EOJ
5005 IF NO_SM4_MODE THEN GOTO 5040 ! WO300317, if no SM4 and parm allows then go around the writing of the flag since no SM4
5010 ! Write flag to SM4 record that ship notice sent WO243602
5020 EXTRACT RECORD (Z[2],KEY=SM4_KEY$,DOM=*NEXT)SM4U$; SM4U$(338,1)="Y"; WRITE RECORD (Z[2],KEY=SM4_KEY$)SM4U$ ! WO243602
5040 GOTO 9900
5095 ! 
7000 GET_3P_INFO:! Read EC9 for template and url
7010 READ (Z[8],KEY=SENDER_ID_3P$+"00001",DOM=9900)EC9_00001$
7015 TEMPLATE_FILE$=STP(ECY$(56,60),2)+DLM+STP(EC9_00001$(16,30),2)
7020 OPEN (HFN,ERR=9900)TEMPLATE_FILE$; CHAN_1=LFO
7025 READ RECORD (CHAN_1,END=9900)TEMPLATE$
7030 TEMPLATE$=STP(TEMPLATE$)
7040 CLOSE (CHAN_1)
7045 ! 
7050 READ (Z[8],KEY=SENDER_ID_3P$+"00002",DOM=9900)EC9_00002$
7055 URL_FILE$=STP(ECY$(56,60),2)+DLM+STP(EC9_00002$(16,30),2)
7060 OPEN (HFN,ERR=9900)URL_FILE$; CHAN_2=LFO
7065 READ RECORD (CHAN_2,END=9900)URL$
7070 URL$=STP(URL$)
7080 CLOSE (CHAN_2)
7090 RETURN 
7095 ! 
7100 GET_CARRIER_NAME:! Read EC6 for match on carrier id or name from SM4, use description as carrier name
7102 ! SSP241112, we were making sure the codes were valid based on Four51 documentation but that has changed, we need to just use what we find in EC6 if there is a matching record
7103 IF NO_SM4_MODE THEN CARRIER_NAME$=""; RETURN ! WO300317, if we don't have SM4 and parm allows this then set carrier name to blank
7105 CARRIER_NAME$="UPS" ! default
7110 READ (Z[9],KEY=PAD(SM4$(31,6),10),DOM=*NEXT)EC6$; CARRIER_NAME$=STP(EC6$(11,30),2); GOTO *RETURN ! use carrier id from SM4. SSP241112, don't uppercase, don't do any validation, if found just use it.
7120 READ (Z[9],KEY=SM4$(37,10),DOM=*RETURN)EC6$; CARRIER_NAME$=STP(EC6$(11,20),2); GOTO *RETURN ! use carrier name from SM4.  SSP241112, don't uppercase, don't do any validation, if found just use it.
7140 RETURN 
7145 ! 
7150 SPECIAL_CHARGES:! SSP278673 - copy from EC3IN1
7155 DIM SPECIAL$(72); READ (Z[11],KEY="X"+AZT$(85,10),DOM=*RETURN)SPECIAL$(1)
7160 TOTAL_SPECIAL_LINES+=AZT[4]
7165 IF SPECIAL$(60,1)<>"Y" THEN TOTAL_NON_FRT_SPECIAL_LINES+=AZT[4]
7170 IF SPECIAL$(60,1)="Y" THEN TOTAL_FRT_SPECIAL_LINES+=AZT[4]
7190 RETURN 
7195 ! 
7200 MULTIPLE_SHIP_NOTICES:! WO243602, SN$[] is three level array, level one is the number of ship notices - always one or more - max of 10, level two is the line data for this particular ship notice - max of 6 - increase this as more data is added, level three is the external line numbers for this ship notice - we'll check each time and if already there then go to the next one down and so on - only one instance of an external line number per ship notice - max of 500.
7205 IF NUM_SN=0 THEN NUM_SN=1,MSN=1,X=1; GOTO WRITE_MULT_SN ! First time here
7210 FOR MSN=1 TO NUM_SN
7215 X=0,START_ONE=0,WRITE_ONE=0
7220 FOR Y=1 TO 500; IF SN$[MSN,0,Y]=EXT_LINE$ THEN IF MSN=NUM_SN THEN START_ONE=1; EXITTO *NEXT ELSE EXITTO 7230 END_IF ELSE IF SN$[MSN,0,Y]="" THEN X=Y,WRITE_ONE=1; EXITTO *NEXT ELSE NEXT Y
7225 IF START_ONE THEN EXITTO START_NEW_SN ELSE IF WRITE_ONE THEN EXITTO WRITE_MULT_SN
7230 NEXT MSN
7240 START_NEW_SN:
7245 IF NUM_SN=10 THEN FOR Z=1 TO 500; IF SN$[NUM_SN,0,Z]="" THEN X=Z; EXITTO WRITE_MULT_SN ELSE NEXT Z ! Only supporting ten ship notices at this time, if we are on number ten and we have a duplicate, we'll just write it anyway.
7248 NUM_SN+=1,MSN=NUM_SN,X=1 ! Start a new ship notice, this is the first ext line
7249 ! 
7250 WRITE_MULT_SN:
7255 SN$[MSN,0,X]=EXT_LINE$ ! Keep track of external line numbers for this ship notice in array level 3
7260 SN$[MSN,1,0]+=EXT_LINE$+SEPERATOR$ ! External line number
7261 SN$[MSN,2,0]+=UOM$+SEPERATOR$ ! Unit of measure
7262 SN$[MSN,3,0]+=STR(QTY_PER_UM)+SEPERATOR$ ! Quantity per unit
7263 SN$[MSN,4,0]+=STR(QTY_SHIPPED)+SEPERATOR$ ! Quantity shipped in eaches
7264 SN$[MSN,5,0]+=STR(QTY_SHIPPED_UNITS)+SEPERATOR$ ! Quantity shipped in units 
7265 SN$[MSN,6,0]=STR(NUM(SN$[MSN,6,0])+1) ! Number of line for this SN
7266 SN$[MSN,7,0]+=ITEM$+SEPERATOR$ ! SSP278673
7267 SN$[MSN,8,0]+=ECR_EXT_REF_1$+SEPERATOR$ ! SSP296725
7268 SN$[MSN,9,0]+=NO_SHIPNOTICE$+SEPERATOR$ ! WO304831
7269 SN$[MSN,10,0]+=TF_DESCRIPTION$+SEPERATOR$ ! SSP307255-DBD-101
7270 SN$[MSN,11,0]+=STR(QTY_ORDERED)+SEPERATOR$ ! SSP307255-DBD-101
7271 SN$[MSN,12,0]+=STR(CTN_WT)+SEPERATOR$ ! SSP307255-DBD-101
7272 SN$[MSN,13,0]+=EDW_RC_NUM$+SEPERATOR$ ! SSP307285-DBD-115
7273 SN$[MSN,14,0]+=EDW_EXT_FIELD_1$+SEPERATOR$ ! SSP307285-DBD-115
7274 SN$[MSN,15,0]+=EDW_EXT_FIELD_2$+SEPERATOR$ ! SSP307285-DBD-115
7298 RETURN 
7299 ! 
7300 READ_SN_ARRAY:! WO243602, Get the line data for one ship notice in the correct variables for writing to the request file
7310 ELNS$=SN$[RSA,1,0] ! for ext_line_number
7315 UOMS$=SN$[RSA,2,0] ! for um
7320 QPUS$=SN$[RSA,3,0] ! for qty_per_um
7325 QSES$=SN$[RSA,4,0] ! qty_shipped_eaches
7330 QSUS$=SN$[RSA,5,0] ! for qty_shipped_units
7335 NUM_LINES=NUM(SN$[RSA,6,0]) ! for num_lines
7340 ITEMS$=SN$[RSA,7,0] ! SSP278673, item codes
7345 EER1S$=SN$[RSA,8,0] ! SSP296725, ECR external ref 1. SSP298737
7350 NOSNS$=SN$[RSA,9,0] ! WO304831, no shipnotices
7355 TFDS$=SN$[RSA,10,0] ! SSP307255-DBD-101
7356 QORS$=SN$[RSA,11,0] ! SSP307255-DBD-101
7357 CWTS$=SN$[RSA,12,0] ! SSP307255-DBD-101
7358 RCNS$=SN$[RSA,13,0] ! SSP307285-DBD-115
7359 EF1S$=SN$[RSA,14,0] ! SSP307285-DBD-115
7360 EF2S$=SN$[RSA,15,0] ! SSP307285-DBD-115
7390 RETURN 
7395 ! 
7400 MULTIPLE_TRACKING:! WO263088, get all tracking numbers that match on order/invoice
7405 MULTIPLE_TRACKING$=TRACKING_NUMBER$+SEPERATOR$,NUM_TRACKS=1 ! get first one found earlier into the list before adding any
7410 READ (Z[2],KEY=SM4_KEY$)SM4T$
7415 READ_SM4T: SM4_KEYT$=KEY(Z[2],END=*RETURN); READ (Z[2],KEY=SM4_KEYT$)SM4T$
7420 IF SM4_KEYT$(1,8)<>ORDER$ THEN GOTO *RETURN
7425 IF SM4T$(101,8)<>INVOICE$ THEN GOTO READ_SM4T
7430 IF SM4T$(383,60)=DIM(60) THEN GOTO READ_SM4T
7435 IF SM4T$(338,1)="Y" THEN GOTO READ_SM4T ! ship notice sent flag set
7445 MULTIPLE_TRACKING$+=STP(SM4T$(383,60))+SEPERATOR$,NUM_TRACKS+=1
7450 EXTRACT RECORD (Z[2],KEY=SM4_KEYT$,DOM=*NEXT)SM4T$; SM4T$(338,1)="Y"; WRITE RECORD (Z[2],KEY=SM4_KEYT$)SM4T$ ! update the ship notice sent flag here while we have the key
7460 GOTO READ_SM4T
7490 RETURN 
7495 ! 
7500 FM1_FIRST_ALT_UM:! SSP296725, check for first alt U/M and qty per
7505 DIM FM1$(448),FM1[31]
7510 FIND (Z[14],KEY=AZT$(75,10)+AZT$(85,10),DOM=*RETURN)IOL=0360
7520 IF FM1$(238,4)=DIM(4) OR FM1[25]=0 THEN RETURN 
7525 UOM$=FM1$(238,4),QTY_PER_UM=FM1[25]
7540 RETURN 
7545 ! 
7550 NO_SM4_MATCH:! WO300317, no matching SM4 found which used to goto 9900 but now check for parm that says ok to continue and use blank tracking number and carrier
7560 IF EC6_SENDER$(285,1)<>"Y" THEN GOTO 9900
7570 DIM SM4$(700)
7575 NO_SM4_MODE=1
7580 GOTO 1070
7595 ! 
7600 HEADER_FIELDS:! SSP307255-DBD-101
7610 SHIP_VIA_CODE$=AZB$(189,1)
7615 LOC_ADDR_1$=STP(AZB$(194,30))
7620 LOC_ADDR_2$=STP(AZB$(224,30))
7625 LOC_CITY$=STP(AZB$(254,16))
7630 LOC_ST$=AZB$(270,2)
7635 LOC_ZIP_CODE$=STP(AZB$(272,9))
7640 SHIP_TO_NAME$=STP(AZB$(293,35))
7645 ATTN_NAME$=STP(AZB$(507,30))
7650 ORDERED_BY$=AZB$(439,20)
7655 GOSUB READ_FT3
7660 CUST_CODE_FORMATTED$=FN%ZZDISP$(AZB$(18,10),"A/R") ! SSP307285-DBD-115
7661 CUST_CODE_UNFORMATTED$=AZB$(18,10) ! SSP307285-DBD-115
7662 INV_NUM$=STP(AZB$(7,8)) ! SSP307285-DBD-115
7665 GOSUB READ_FS1 ! SSP307319-DBD-147
7695 RETURN 
7699 ! 
7700 LINE_FIELDS:! SSP307255-DBD-101
7710 TF_DESCRIPTION$=STP(AZT$(27,40))
7715 QTY_ORDERED=AZT[0]
7720 GOSUB READ_FM1
7795 RETURN 
7799 ! 
7800 READ_FM1:! SSP307255-DBD-101
7805 DIM FM1$(448),FM1[31]; CTN_WT=0
7810 FIND (Z[14],KEY=AZT$(75,10)+AZT$(85,10),DOM=*RETURN)IOL=0360
7815 CTN_WT=FM1[18]
7845 RETURN 
7849 ! 
7850 READ_FT3:! SSP307255-DBD-101
7855 DIM FT3$(606); EC_SOURCE$=""
7860 FIND (Z[17],KEY=ORDER$,DOM=*RETURN)FT3$(1)
7865 EC_SOURCE$=STP(FT3$(387,10))
7895 RETURN 
7899 ! 
7900 EDW_FIELDS:! SSP307285-DBD-115
7905 EDW_RC_NUM$="",EDW_EXT_FIELD_1$="",EDW_EXT_FIELD_2$=""
7910 EDW_RC_NUM$=STP(MID(EDW$,12,20))
7915 EDW_EXT_FIELD_1$=STP(MID(EDW$,38,20))
7920 EDW_EXT_FIELD_2$=STP(MID(EDW$,58,20))
7945 RETURN 
7949 ! 
7950 READ_FS1:! SSP307319-DBD-147
7955 DIM FS1$(606); ORDER_STATUS$="CLOSED"
7960 FIND (Z[18],KEY=ORDER$,DOM=*RETURN)FS1$(1)
7965 ORDER_STATUS$="OPEN"
7995 RETURN 
7999 ! 
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,1
9045 REM 
9050 ON Y7 GOTO 9060,9100,9190,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9190 GOSUB 6600; GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9700 REM "FILES
9740 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 0521,9900
9790 RETURN 
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9940 EXIT 
9999 END 
10000 ENQUEUE:! SSP307319-DBD-147 write records to EES for later processing
10010 ENTER X3$,X4$,AZB_KEY$,ORDER$,OTHER$
10015 BATCH_NUMBER$=X3$(174,4)+X3$(85,1)
10020 DIM Z[NUM(X3$(60,3))]
10025 Z$="01O EES... "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
10030 DIM EES$(120)
10035 EES$(1,5)=BATCH_NUMBER$,EES$(6,17)=AZB_KEY$
10040 EES$(23,8)=ORDER$,EES$(31,1)="N"
10060 WRITE (Z[1],KEY=EES$(1,22))EES$
10085 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
10090 EXIT 
10095 ! 
10100 PROCESS:! SSP307319-DBD-147, read EES for current batch and process
10110 ENTER X3$,X4$,RETURN$
10115 BATCH_NUMBER$=X3$(174,4)+X3$(85,1)
10120 DIM Z[NUM(X3$(60,3))]
10125 Z$="01O EES... "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
10130 DIM EES$(120)
10135 READ (Z[1],KEY=BATCH_NUMBER$,DOM=*NEXT)
10140 NEXT_RECORD: K$=KEY(Z[1],END=DONE)
10145 READ (Z[1],KEY=K$)EES$(1)
10150 IF EES$(1,5)<>BATCH_NUMBER$ THEN GOTO DONE
10160 AZB_KEY$=EES$(6,17),ORDER$=EES$(23,8)
10165 CALL "EC3SN0",X3$,X4$,AZB_KEY$,ORDER$,OTHER$
10170 EES$(31,1)="Y"; WRITE (Z[1],KEY=K$)EES$ ! Processed flag
10175 GOTO NEXT_RECORD
10185 DONE:CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
10190 EXIT 
10195 ! 
56000 REM "224137-Capture Four51 info by order, retrieve later for ShipNotice
56002 REM "228984-Modify the Four51 ShipNotice for noticeDate to be the date
56004 REM "241112-Four51 ShipNotice carrier identifier for Federal Express is 
56006 REM "243602-Option for ShipNotice, if external line split into multiple 
56007 REM "257952-Need larger tracking number field to replace 20 character   
56008 REM "257952-Need larger tracking number field to replace 20 character   
56010 REM "263088-Pier1; Modify EC Invoice/ShipNotice programs used in SJ     
56012 REM "264379-Addt'l to WO263088; EC Cust Parm-Allow 0 qty shipped inv lns
56014 REM "276575-Flowpoint; Marcom/PTI Order Closeout                        
56016 REM "278673-Flowpoint; Template/EC3SN1 changes for Ship Notice/Closeout 
56018 REM "296725-FlowPoint; Propago NEED ECR EXT_REF_1 FOR SHIP NOTICE
56019 REM "296725-FlowPoint; Propago USE FIRST ALT U/M FROM FM1 IF NOT BLANK
56020 REM "298737-Flowpoint - MULTIPLE SHIP NOTICE ECR EXT REF NOT CORRECT  
56022 REM "300317-ShipNotice option to bypass tracking history requirement.   
56024 REM "304831-Vendor option to instruct FlowPoint to not send shipping    
56026 REM "307255-DBD-101; DBSPT-80384/499 Order Acknowledgement/Shipment need
56028 REM "307285-DBD-115-DBSPT-87098; DBDXML SHIP_NOTICE EDW fields addition 
56030 REM "307319-DBSPT-87098-DBD-147; Change timing of ShipNotice Request to 
56032 REM "307345-DBD-167-DBSPT-110833;Sender Id in Cust Defaults OrdConf,Ship
56034 REM "307358-DBSPT-115417-DBD-185; Expand EEE/EEP for ORDER_ID_3P_2, add 
56036 REM "307401-DBD-239; Add AR1 EQD_CUST field to DBDXML SHIP_NOTICE       
