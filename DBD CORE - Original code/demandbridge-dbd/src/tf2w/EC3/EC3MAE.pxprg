0010 REM "EC Buyer Setup  <EC3MAE>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 02/27/13 - 16.500833 - dmm - SSP# 257291
0037 REM "257291-Buyer Parm to trigger cust default CC info for DBE orders.  
0040 REM "Copyright 2013 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="EC3MAE",X1$="EC Buyer Setup II",K9$="",K9=0
0120 PRECISION 7
0130 K0=3,K1=1
0135 C9=-1,V9=-2
0140 M0$="##.0000",M1$="####,###.00-"
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1,LEN(A$)),ECA2$
0320 IOLIST B$(1)
0330 IOLIST FM0$
0380 IOLIST ECS$
0400 IOLIST ASP$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X ECA...  02O AR1... 03O FM0... 04O FMP... 05O ECG... 06O ZZPARM  07O ECH... 08O ECS...  09O EDA...  10O ASP... 11O EDA... 12O EDF... 13O ECU... 14O EDG... 15O AR2...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0570 DIM F0$(100)
0600 REM "
0610 DIM EC_PARM$(1024); FIND (Z[6],KEY=X3$(9,3)+"E/C")EC_PARM$
0611 DIM ARPARMS$(300); FIND (Z(6),KEY=X3$(9,3)+"A/R")ARPARMS$
0620 GOSUB 6000
0650 REM "get operator security - for credit card access
0660 DIM OSEC$(200); FIND (Z[6],KEY="osec"+X3$(40,3),DOM=0661)OSEC$
0900 REM "Setup prompts
0910 EMAIL_ACTION_PROMPT$="O=Order purchased",EMAIL_ACTIONS$=" O"
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(600),C$(200),FMPD$(500)
1035 DIM A$(1540),A[1],FMPD$(500),FM0$(500)
1037 ORDERTYPES$="S RW",ORDERX$="S-Sales orders (O/P-O1)  R-Requisitions  W-Billable warehouse releases"; REM 'SALES ORDERS , R/W RELEASES
1040 REM IF V9<>-1 THEN GOSUB 06900
1045 A$(1,0)=K9$+A1$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 SETERR 9000; C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1500
1200 REM "Buyer's ID
1205 IF Q0$<>"" THEN A$(1,15)=Q0$; GOTO 1211
1210 CALL "ZZENTR","SX",A{ALL},A$,X4$,X3$,18,3,1,15,C0,"","{1"+X$,"","EC3MAE00","ECA","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1212 IF INT(ABS(C0))=3 THEN C0=-ABS(C0); READ (Z[1],KEY=A$(1,15),DOM=1213)
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9800,6970
1240 GOTO 1120
1500 REM "
1505 Z9=-1
1510 A1$=K9$+A$(LEN(K9$)+1,15),A$(1,LEN(K9$))=K9$
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1525 Z9=0
1530 GOSUB 5800
1590 GOTO 1700
1600 REM "
1610 REM IF A$(1,3)=F0$(17,3) THEN GOTO 02000
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630,6975
1630 IF Q0$="" THEN GOTO 1000 ELSE GOTO 9900; REM IF F0$(16,1)="N" THEN GOTO 9800 ELSE GOTO 1000
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1820 CALL "ZZPROM",STR(6+C9),X3$,Z,"","","V",X0; ON Z GOTO 1850,6500,1630,6700,1821
1821 REM "CALL "ZZPROM","X0EC3MAE",X3$,Z,"","","V",X0
1822 REM "ON Z GOTO 01850,06500,01630,06700,06700,01823
1825 GOTO 6700
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310
1865 C9=1
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF C0>3 OR X0<0 THEN GOTO 1800
2035 IF X0*6+11<LEN(V9$) THEN IF V9$(X0*6+11,1)="2" THEN GOTO 2020
2039 SETERR 9000
2040 ON X0 GOTO 2100,2150,2200,2250,2950,3050,3000,2300,2350,2900,4050,2400,2450,2500,2550,2600,2650,2700,2750,2800,2850,1800
2100 REM "credit card type
2101 IF ARPARMS$(130,1)<>"Y" THEN A$(493,4)=DIM(4); GOTO 2140
2102 IF OSEC$(77,1)<>"Y" THEN GOTO 2140
2105 X$="Enter credit card type if buyer has permanent credit card info for purchases"
2110 CALL "ZZENTR","SZXUX",A{ALL},A$,X4$,X3$,14,5,493,4,C0,"","{3"+X$,"","EC3MAE02","ASP","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2110,2111
2112 IF A$(493,4)=DIM(4) THEN GOTO 2140
2120 FIND (Z[10],KEY=A$(493,4),DOM=2100)IOL=0400
2122 GOSUB 6200
2140 GOTO 2020
2150 REM "credit card number
2151 IF ARPARMS$(130,1)<>"Y" THEN A$(497,20)=DIM(20); GOTO 2190
2154 IF OSEC$(77,1)<>"Y" THEN GOTO 2190
2155 IF A$(493,4)=DIM(4) THEN GOTO 2190
2160 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,53,5,497,20,C0,"","{3"+X$,"","EC3MAE04","","",""
2170 PRINT @(53,5),DIM(20),; REM "clear display of number
2190 GOTO 2020
2200 REM "expiration date on credit card
2201 IF ARPARMS$(130,1)<>"Y" THEN A$(517,4)=DIM(4); GOTO 2240
2202 IF OSEC$(77,1)<>"Y" THEN GOTO 2240
2203 IF A$(493,4)=DIM(4) THEN GOTO 2240
2204 X$="MMYY format"
2210 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,67,6,517,4,C0,"","{3"+X$,"","EC3MAE06","","",""
2240 GOTO 2020
2250 REM "credit card - name on card
2251 IF ARPARMS$(130,1)<>"Y" THEN A$(521,40)=DIM(40); GOTO 2290
2252 IF OSEC$(77,1)<>"Y" THEN GOTO 2290
2253 IF A$(493,4)=DIM(4) THEN GOTO 2290
2260 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,14,6,521,40,C0,"","{3"+X$,"","EC3MAE08","","",""
2290 GOTO 2020
2300 REM "default attention
2305 X$="Will default if the attention line on ship-to is blank"
2310 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,11,9,561,30,C0,"","{3"+X$,"","EC3MAE10","","",""
2340 GOTO 2020
2350 REM "default mark for
2352 X$="Will default as the mark for instructions on web orders"
2360 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,11,10,591,35,C0,"","{3"+X$,"","EC3MAE12","","",""
2390 GOTO 2020
2400 REM "Catalog 1
2402 X$="Primary catalog (from screen I of the Buyer record)"
2404 IF A$(626,10)=DIM(10) THEN A$(626,10)=A$(325,10)
2410 CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,3,12,626,10,C0,"","{3"+X$,"","EC3MAE14","EDA","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2410,2411
2415 REM " dont check 1st one comes from screen one, if dup locks user up;CAT=626; GOSUB 7900; IF CATVALID$<>"Y" THEN GOTO 2400
2420 IF A$(626,10)<>DIM(10) THEN GOSUB 6230
2440 IF A$(626,10)=DIM(10) THEN GOTO 2400 ELSE A$(325,10)=A$(626,10); GOTO 2020
2450 REM "Catalog 2
2460 CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,3,13,636,10,C0,"","{3"+X$,"","EC3MAE16","EDA","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2460,2461
2465 CAT=636; GOSUB 7900; IF CATVALID$<>"Y" THEN GOTO 2450
2470 IF A$(636,10)<>DIM(10) THEN GOSUB 6230
2490 GOTO 2020
2500 REM "Catalog 3
2510 CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,3,14,646,10,C0,"","{3"+X$,"","EC3MAE18","EDA","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2510,2511
2515 CAT=646; GOSUB 7900; IF CATVALID$<>"Y" THEN GOTO 2500
2520 IF A$(646,10)<>DIM(10) THEN GOSUB 6230
2540 GOTO 2020
2550 REM "Catalog 4
2560 CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,3,15,656,10,C0,"","{3"+X$,"","EC3MAE20","EDA","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2560,2561
2565 CAT=656; GOSUB 7900; IF CATVALID$<>"Y" THEN GOTO 2550
2570 IF A$(656,10)<>DIM(10) THEN GOSUB 6230
2590 GOTO 2020
2600 REM "Catalog 5
2610 CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,3,16,666,10,C0,"","{3"+X$,"","EC3MAE22","EDA","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2610,2611
2615 CAT=666; GOSUB 7900; IF CATVALID$<>"Y" THEN GOTO 2600
2620 IF A$(666,10)<>DIM(10) THEN GOSUB 6230
2640 GOTO 2020
2650 REM "Catalog 6
2660 CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,44,12,676,10,C0,"","{3"+X$,"","EC3MAE24","EDA","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2660,2661
2665 CAT=676; GOSUB 7900; IF CATVALID$<>"Y" THEN GOTO 2650
2670 IF A$(676,10)<>DIM(10) THEN GOSUB 6230
2690 GOTO 2020
2700 REM "Catalog 7
2710 CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,44,13,686,10,C0,"","{3"+X$,"","EC3MAE26","EDA","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2710,2711
2715 CAT=686; GOSUB 7900; IF CATVALID$<>"Y" THEN GOTO 2700
2720 IF A$(686,10)<>DIM(10) THEN GOSUB 6230
2740 GOTO 2020
2750 REM "Catalog 8
2760 CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,44,14,696,10,C0,"","{3"+X$,"","EC3MAE28","EDA","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2760,2761
2765 CAT=696; GOSUB 7900; IF CATVALID$<>"Y" THEN GOTO 2750
2770 IF A$(696,10)<>DIM(10) THEN GOSUB 6230
2790 GOTO 2020
2800 REM "Catalog 9
2810 CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,44,15,706,10,C0,"","{3"+X$,"","EC3MAE30","EDA","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2810,2811
2815 CAT=706; IF CATVALID$<>"Y" THEN GOTO 2800
2820 IF A$(706,10)<>DIM(10) THEN GOSUB 6230
2840 GOTO 2020
2850 REM "Catalog 10
2860 CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,44,16,716,10,C0,"","{3"+X$,"","EC3MAE32","EDA","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2860,2861
2865 CAT=716; GOSUB 7900; IF CATVALID$<>"Y" THEN GOTO 2850
2870 IF A$(716,10)<>DIM(10) THEN GOSUB 6230
2890 GOTO 2020
2900 REM "buyer type
2910 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,53,9,83,3,C0,M0$,"{3"+X$,"","ZXY2BAA24","EDF","EC3CBA",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2910,2911
2915 IF A$(83,3)=DIM(3) THEN GOTO 2940
2920 FIND (Z[12],KEY=A$(83,3),DOM=2900); GOSUB 6200
2940 GOTO 2020
2950 REM "Credit card required?
2951 IF ARPARMS$(130,1)<>"Y" THEN A$(254,1)=" "; GOTO 2990
2952 IF AR2$(57,1)="N" THEN A$(254,1)="N"; GOSUB 6200; GOTO 2990
2953 IF A$(254,1)=" " THEN A$(254,1)="Y"
2954 IF A$(1089,1)="Y" THEN A$(254,1)="N"; GOSUB 6200; GOTO 2990 ! WO257291, if Create order CC record using cust CC data set to Y then set CC required to N.
2955 X$="CUSTOMER FLAGGED FOR CC:  N=Will allow buyer to purchase WITHOUT Credit Card"
2960 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,14,7,254,1,C0,M0$,"{3"+X$,"","EC3MAE36","","",""
2990 GOTO 2020
3000 ! Credit card ID, WO174246
3005 X$="ID to credit card number in other database"
3010 CALL "ZZENTR","SZXU",A{ALL},A$,X4$,X3$,67,7,985,10,C0,"","{3"+X$,"","EC3MAE38","","",""
3040 GOTO 2020
3045 ! 
3050 ! WO257291, Create order CC record using cust CC data?
3051 IF ARPARMS$(130,1)<>"Y" THEN A$(1089,1)=DIM(1); GOTO 3090
3052 IF AR2$(57,1)<>"Y" THEN A$(1089,1)="N"; GOSUB 6200; GOTO 3090
3053 IF A$(254,1)="Y" THEN A$(1089,1)="N"; GOSUB 6200; GOTO 3090 ! If CC Required set to Y then this one must be N
3055 X$="Y=For EC orders, use customer's default CC data"
3060 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,61,7,1089,1,C0,M0$,"{3"+X$,"","EC3MAE40","","",""
3090 GOTO 2020
3095 ! 
4050 REM "shipvia code set
4052 X$="Assign ship via code set (must be SHIP_VIAxx or code will be rejected)"
4060 CALL "ZZENTR","SU",A{ALL},A$,X4$,X3$,61,10,735,10,C0,"","{3"+X$,"","EC3MAE34","ECU","EC3SRA",""; IF ABS(C0)=6 THEN GOSUB 6400
4065 IF A$(735,10)=DIM(10) THEN GOTO 4090
4066 IF A$(735,8)<>"SHIP_VIA" THEN GOTO 4050
4067 IF A$(743,2)=DIM(2) THEN GOTO 4050
4070 DIM ECU$(421); FIND (Z[13],KEY=A$(735,10),DOM=4050)ECU$
4072 PRINT @(5,21),ECU$(11,50),
4090 GOTO 2020
5800 REM "Alt data reads
5805 DIM ASP$(256); FIND (Z[10],KEY=A$(493,4),DOM=5806)IOL=0400
5807 DIM AR1$(1000); FIND (Z[2],KEY=A$(51,10),DOM=5808)AR1$
5808 DIM ECS$(1000); FIND (Z[8],KEY=A$(51,10),DOM=5809)IOL=0380
5810 DIM CAT1$(200),CAT2$(200),CAT3$(200),CAT4$(200),CAT5$(200),CAT6$(200),CAT7$(200),CAT8$(200),CAT9$(200),CAT10$(200)
5820 FIND (Z[11],KEY=A$(626,10),DOM=5821)CAT1$
5821 FIND (Z[11],KEY=A$(636,10),DOM=5822)CAT2$
5822 FIND (Z[11],KEY=A$(646,10),DOM=5823)CAT3$
5823 FIND (Z[11],KEY=A$(656,10),DOM=5824)CAT4$
5824 FIND (Z[11],KEY=A$(666,10),DOM=5825)CAT5$
5825 FIND (Z[11],KEY=A$(676,10),DOM=5826)CAT6$
5826 FIND (Z[11],KEY=A$(686,10),DOM=5827)CAT7$
5827 FIND (Z[11],KEY=A$(696,10),DOM=5828)CAT8$
5828 FIND (Z[11],KEY=A$(706,10),DOM=5829)CAT9$
5829 FIND (Z[11],KEY=A$(716,10),DOM=5830)CAT10$
5840 DIM EDF$(100); FIND (Z[12],KEY=A$(83,3),DOM=5841)EDF$
5841 DIM AR2$(100); FIND (Z(15),KEY=AR1$(359,2),DOM=5842)AR2$
5845 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(1,3),"Buyer's id code:",@(35,3),"name:",@(0,4),Z0$,@(20,4),"Default credit card information",@(3,5),"Card type:",@(45,5),"Number:",@(55,6),"Expiration:",@(0,6),"Name on card:",@(0,8),Z0$,@(10,8),"Defaults",@(0,9),"Attention:",@(1,10),"Mark for:",@(47,9),"Type:",@(47,10),"Ship via set:",
6062 PRINT @(0,11),Z0$,@(30,11),"Multiple Catalog Assignments",@(0,12),"1.",@(0,13),"2.",@(0,14),"3.",@(0,15),"4.",@(0,16),"5.",@(41,12),"6.",@(41,13),"7.",@(41,14),"8.",@(41,15),"9.",@(40,16),"10.",
6064 PRINT @(0,17),Z0$,@(35,17),"Auxiliary Catalog Assignments",@(6,18),"Customer category:",@(9,19),"Buyer category:",@(0,20),"Customer/Buyer category:",@(69,18),"Customer",@(69,19),"Catalogs",
6071 PRINT @(1,7),"CC Required?",
6075 PRINT @(63,7),"ID:",
6080 PRINT @(18,7),"Create order CC record using cust CC data?", ! WO257291
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6205 GOSUB 6450
6210 PRINT @(42,3),A$(16,35),@(11,9),A$(561,30),@(11,10),A$(591,35),@(53,9),A$(83,3),@(61,10),A$(735,10),
6219 IF OSEC$(77,1)<>"Y" THEN GOTO 6225
6220 REM "credit card information
6221 PRINT @(14,5),A$(493,4),@(67,6),A$(517,4),@(14,6),A$(521,40),
6222 PRINT @(19,5),ASP$(5,25),
6223 PRINT @(67,7),A$(985,10),
6225 REM "credit card module stuff
6226 IF ARPARMS$(130,1)<>"Y" THEN GOTO 6230
6227 PRINT @(14,7),A$(254,1),
6228 PRINT @(61,7),A$(1089,1), ! WO257291
6230 REM "multi catalog stuff
6231 GOSUB 5800
6232 PRINT @(3,12),A$(626,10),@(14,12),CAT1$(11,24),
6233 PRINT @(3,13),A$(636,10),@(14,13),CAT2$(11,24),
6234 PRINT @(3,14),A$(646,10),@(14,14),CAT3$(11,24),
6235 PRINT @(3,15),A$(656,10),@(14,15),CAT4$(11,24),
6236 PRINT @(3,16),A$(666,10),@(14,16),CAT5$(11,24),
6237 PRINT @(44,12),A$(676,10),@(55,12),CAT6$(11,24),
6238 PRINT @(44,13),A$(686,10),@(55,13),CAT7$(11,24),
6239 PRINT @(44,14),A$(696,10),@(55,14),CAT8$(11,24),
6240 PRINT @(44,15),A$(706,10),@(55,15),CAT9$(11,24),
6241 PRINT @(44,16),A$(716,10),@(55,16),CAT10$(11,24),
6242 PRINT @(57,9),EDF$(4,22),
6250 REM "auxiliary catalog assignments
6252 AUXKEY$="C"+AR1$(220,9),AUXC=25,AUXR=18; GOSUB 8000
6254 AUXKEY$="B"+A$(83,3),AUXC=25,AUXR=19; GOSUB 8000
6256 AUXKEY$="D"+A$(51,10)+A$(83,3),AUXC=25,AUXR=20; GOSUB 8000
6260 GOSUB 8100; PRINT @(68,20),CUSTCAT_COUNT," catalogs",
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 PRINT @(18,3),A$(1,15),
6490 RETURN 
6500 REM "DELETE
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY
6705 IF Z9=0 THEN GOTO 6790
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6720 IF Q0$<>"" THEN GOTO 9900
6790 C9=0; IF Z=6 OR K1=0 THEN GOTO 9900 ELSE IF Z<4 THEN GOTO 1000 ELSE C0=-(3.1+(Z-4)/10); GOTO 6800
6800 REM "NEXT ENTRY IN FILE
6805 ON 10*FPT(C0)+2 GOTO 6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 IF A1$="" THEN GOTO 6800 ELSE IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6825 A$(1,LEN(A1$))=A1$
6830 GOSUB 6600
6835 GOSUB 6450
6840 GOTO 1515
6850 REM "PRIOR ENTRY
6855 CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6856 CALL "ZZPROM","K0",X3$,S3,"","","",0; ON S3 GOTO 6857,9900
6860 PRINT 'RB',; CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6865 GOTO 1100
6870 GOTO 6895
6890 GOTO 1515
6895 CALL "ZZPROM","K1",X3$,S3,"","","",0; IF S3=1 THEN GOTO 9900 ELSE IF A1$="" THEN GOTO 6860 ELSE PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=6896)
6898 ON S9 GOTO 6810,1100
6900 REM "GET DEFAULTS
6930 RETURN 
6950 REM "WRITE DEFAULT
6970 REM "ENTRY DEFAULTS
6975 PRINT 'CF'; V9=-2,Q$=""
6980 FOR X=1 TO LEN(V9$) STEP 6
6982 REM PRINT @(NUM(V9$(X,2)),NUM(V9$(X+2,2))),; IF V9=-2 THEN PRINT V9$(X+4,1), ELSE READ RECORD (0,SIZ=1) Q$; IF POS(Q$="1234Z")=0 THEN PRINT 'RB'; GOTO 06982
6990 IF Q$="Z" THEN EXITTO 6950 ELSE V9$(X+4,LEN(Q$))=Q$; NEXT X
6995 V9=0; GOTO 6980
7500 REM "Regenerate active web categories
7501 REM "web categories will only be included if:
7502 REM "a) the buyer has the necessary secure code (only if the category has a code)
7503 REM "the category currently has active items"
7505 WRITE (Z[1],KEY=A1$)IOL=0310
7506 IF A$(480,3)<>DIM(3) AND A$(480,3)<>X3$(9,3) THEN GOTO 0754
7520 CALL "EC3UT4",X3$,X4$,A$(1,15),CATEGORIES$,""
7522 ECA2$=CATEGORIES$
7530 WRITE (Z[1],KEY=A1$)IOL=0310
7540 RETURN 
7800 REM "copy buyer paramters
7805 DIM COPYBUYER[1],COPYBUYER$(15)
7810 PRINT @(3,21),'CE',"Enter buyer to copy from (blank for manual setup manually):",
7820 CALL "ZZENTR","S",COPYBUYER{ALL},COPYBUYER$,X4$,X3$,63,21,1,15,C0,"","{3"+X$,"","EC3AR110","ECA","",""; IF ABS(C0)>4 THEN GOSUB 6400
7830 IF STP(COPYBUYER$,3," ")="" THEN GOTO 7890
7832 READ RECORD (Z[1],KEY=COPYBUYER$,DOM=7834)COPYREC$; GOTO 7840
7834 CALL "ZZPROM",".4",X3$,Z,"Buyer not on file","","",0; GOTO 7800
7840 COPYREC$(1,15)=A$(1,15); WRITE RECORD (Z[1],KEY=COPYREC$(1,15))COPYREC$; FIND (Z[1],KEY=A$(1,15))IOL=0310; GOSUB 6450; GOSUB 6200
7850 GOSUB 7500
7890 RETURN 
7900 REM "ck if catalog valid
7905 CATVALID$="Y"
7910 CAT$=A$(CAT,10); IF CAT$=DIM(10) THEN GOTO 7940
7912 FIND (Z[11],KEY=CAT$,DOM=7913); GOTO 7915
7913 CATVALID$="N"; GOTO 7940
7920 FOR CC=0 TO 9
7922 CCINDEX=CC*10+626; IF CCINDEX=CAT THEN GOTO 7929
7923 IF A$(CCINDEX,10)=A$(CAT,10) THEN CATVALID$="N"
7929 NEXT CC
7940 RETURN 
8000 REM "get aux catalog information
8005 PRINT @(AUXC,AUXR),DIM(42),
8010 AUXKEY$=PAD(AUXKEY$,14)
8020 DIM AUX$(100); FIND (Z[14],KEY=AUXKEY$,DOM=8021)AUX$
8022 IF AUX$(15,10)=DIM(10) THEN GOTO 8040
8024 DIM AUXEDA$(200); FIND (Z[9],KEY=AUX$(15,10),DOM=8040)AUXEDA$
8026 PRINT @(AUXC,AUXR),AUX$(15,10),@(AUXC+12,AUXR),AUXEDA$(11,30),
8040 RETURN 
8100 REM "get count of customer based catalogs
8110 CUSTCAT_COUNT=0
8120 FOR CCC=0 TO 4
8122 IF ECS$(CCC*10+536,10)<>DIM(10) THEN CUSTCAT_COUNT=CUSTCAT_COUNT+1
8124 NEXT CCC
8140 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,1
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 GOSUB 6400
9520 ON C9 GOTO 1140,2040
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "257291-Buyer Parm to trigger cust default CC info for DBE orders.  
