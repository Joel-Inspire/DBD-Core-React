0010 REM "Return qty break pricing for stock items w/pricing tables <EC3UTE>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 06/18/15 - 15.201111 - dmm - SSP# 278081
0037 REM "278081-see 277028; remaining issues with EC quantity breaks display
0040 REM "Copyright 2015 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "FMPCHAN= file channel FMP open
0052 REM "IC0$/IC0(ALL) - ic0 record
0054 REM "CUSTCAT$ - customer a/r category code (9 characters)
0056 REM "QTY(ALL) - 12 quantity break levels (1-12) (0)-base price 157200
0057 REM (2ND INDEX) 0-EACHES   1-IN SELLING UNITS
0058 REM "PRICE(ALL) - 13 pricing levels (0-list  1-12 qty break prices)
0060 REM "SQP= selling quantity per unit (will be pulled off pricing table if table on file)
0065 ! WO175232, OPTIONS$(1,1)="Y", means use_alt_um_qb set to Y coming from EC3QB1 (EC3PC1). Will set OPTIONS$(2,1)="Y" if using alt u/m logic
0066 ! ORG_SQP=Original (from IC0) selling quantity per unit, ORD_PRC_DIV=Original price divisor
0067 ! AUMQP=Alternate unit of measure quantity per unit, AUMPD=Alt unit price divisor
0068 ! %BPACK and %SELL_UM$, using alt unit logic, need these in EC3QB1
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,EC_PARM$,EC${ALL},FMPCHAN,IC0$,IC0{ALL},CUSTCAT$,Q{ALL},P{ALL},OPTIONS$
0100 SETERR 9000
0110 X0$="EC3UTE",X1$="Return qty break pricing"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST T$,T[0],T[1],T[2],T[3],T[4],T[5],T[6],T[7],T[8],T[9],T[10],T[11],T[12],T[13],T[14],T[15],T[16],T[17],T[18],T[19],T[20],T[21],T[22],T[23],T[24]
0320 IOLIST UM$,UM[0],UM[1],UM[2]
0330 IOLIST FM1$,FM1[0],FM1[1],FM1[2],FM1[3],FM1[4],FM1[5],FM1[6],FM1[7],FM1[8],FM1[9],FM1[10],FM1[11],FM1[12],FM1[13],FM1[14],FM1[15],FM1[16],FM1[17],FM1[18],FM1[19],FM1[20],FM1[21],FM1[22],FM1[23],FM1[24],FM1[25],FM1[26],FM1[27],FM1[28],FM1[29],FM1[30],FM1[31]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O ZZPARM  02O FM1...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0550 DIM Q[12,1],P[12],T[24]
0552 DIM UM$(21),UM[2]; FIND (Z[1],KEY="U/M"+IC0$(120,4),DOM=*NEXT)IOL=0320
0553 IF UM$(20,1)<>"Y" THEN SQP=IC0[14],PRC_DIV=IC0[14] ELSE SQP=UM[1],PRC_DIV=UM[0]
0554 IF SQP=0 THEN SQP=1
0555 IF PRC_DIV=0 THEN PRC_DIV=1
0560 QBI$="010305070911212325272931",PBI$="020406081012222426283032"
0562 TQBI$="010305070911131517192123",TPBI$="020406081012141618202224"
0600 REM "find existing pricing table
0605 PTABLE$="N"
0610 FIND (FMPCHAN,KEY="P"+IC0$(109,3)+CUSTCAT$,DOM=0611)IOL=0310; PTABLE$="Y"
0900 REM "
0910 IF PTABLE$="N" THEN GOSUB 7000; GOTO 5000
0920 SQP=T[0]; IF SQP=0 THEN SQP=IC0[0] END_IF ; IF SQP=0 THEN SQP=1
0980 ON POS(T$(38,1)="SDMG")-1 GOTO 1000,1500,2000,2500
1000 REM "Pricing table: S-method (Sell price)
1020 FOR X=0 TO 11
1022 INDEX=X+1,QI=NUM(TQBI$(X*2+1,2)),PI=NUM(TPBI$(X*2+1,2)); REM "157200 SSP277028
1030 Q[INDEX,0]=T[QI],Q[INDEX,1]=T[QI]/SQP,P[INDEX]=T[PI]
1080 NEXT X
1081 IF Q[1,1]>1 THEN Q[0,0]=1,Q[0,1]=1,P[0]=IC0[0] ! SSP278081
1450 GOTO 5000
1500 REM "Pricing table: D-method (Discount from list)
1520 FOR X=0 TO 11
1522 INDEX=X+1,QI=NUM(TQBI$(X*2+1,2)),PI=NUM(TPBI$(X*2+1,2)); REM "157200 SSP277028
1524 Q[INDEX,0]=T[QI],Q[INDEX,1]=T[QI]/SQP
1530 REM "calc discount
1532 LIST=IC0[0],DISCOUNT=LIST*(T[PI]/100)
1534 P[INDEX]=LIST-DISCOUNT
1580 NEXT X
1581 IF Q[1,1]>1 THEN Q[0,0]=1,Q[0,1]=1,P[0]=IC0[0] ! SSP278081
1950 GOTO 5000
2000 REM "Pricing table: M-method (markup from cost)
2020 FOR X=0 TO 11
2022 INDEX=X+1,QI=NUM(TQBI$(X*2+1,2)),PI=NUM(TPBI$(X*2+1,2)); REM "157200 SSP277028
2024 Q[INDEX,0]=T[QI],Q[INDEX,1]=T[QI]/SQP
2030 REM "calc sell
2032 ON POS(T$(39,1)="SAL") GOTO 2034,2034,2035,2036
2034 BASECOST=IC0[20]; GOTO 2040
2035 BASECOST=IC0[18]; GOTO 2040
2036 BASECOST=IC0[17]; GOTO 2040
2040 P[INDEX]=BASECOST+BASECOST*T[PI]*.01 ! SSP 278081, changed to match logic in IC2ICF to calc price P[INDEX]=BASECOST/(100-T[PI]/100)
2080 NEXT X
2081 IF Q[1,1]>1 THEN Q[0,0]=1,Q[0,1]=1,P[0]=IC0[0] ! SSP278081
2450 GOTO 5000
2500 REM "Pricing table: G-method ! SSP278081, added/fixed logic
2520 FOR X=0 TO 11
2522 INDEX=X+1,QI=NUM(TQBI$(X*2+1,2)),PI=NUM(TPBI$(X*2+1,2))
2524 Q[INDEX,0]=T[QI],Q[INDEX,1]=T[QI]/SQP
2530 REM "calc sell
2532 ON POS(T$(39,1)="SAL") GOTO 2534,2534,2535,2536
2534 BASECOST=IC0[20]; GOTO 2540
2535 BASECOST=IC0[18]; GOTO 2540
2536 BASECOST=IC0[17]; GOTO 2540
2540 P[INDEX]=BASECOST*100/(100-T[PI])
2580 NEXT X
2581 IF Q[1,1]>1 THEN Q[0,0]=1,Q[0,1]=1,P[0]=IC0[0]
2650 GOTO 5000
5000 REM "EOJ
5040 GOTO 9900
7000 REM "load quantity breaks from IC0 (when no pricing table exists)
7002 IF SQP=0 THEN SQP=1
7003 ORG_SQP=SQP,AUMQP=SQP,ORG_PRC_DIV=PRC_DIV,AUMPD=PRC_DIV
7005 IF STP(OPTIONS$,1)<>"" THEN IF OPTIONS$(1,1)="Y" THEN GOSUB 7100 ! WO175232, use alt unit of issue from FM1 if there is one
7020 FOR X=0 TO 11
7022 INDEX=X+1,QI=NUM(QBI$(X*2+1,2)),PI=NUM(PBI$(X*2+1,2))
7025 Q[INDEX,0]=IC0[QI],Q[INDEX,1]=IC0[QI]/SQP,P[INDEX]=IC0[PI]*AUMPD/ORG_PRC_DIV ! WO175232, Add calculation to price, could be using alt u/m from FM1, if not will come out the same as without the calculation
7030 NEXT X
7031 IF Q[1,1]>1 THEN Q[0,0]=1,Q[0,1]=1,P[0]=IC0(0)*AUMPD/ORG_PRC_DIV; REM "base price 157200 SSP278081, if first break qty is 1 then don't populate range 0
7040 RETURN 
7045 ! 
7100 ! WO175232, want to use alt unit of issue from FM1 if there is one
7110 DIM FM1$(448),FM1[31]; FIND (Z[2],KEY=IC0$(1,20),DOM=*RETURN)IOL=0330
7115 IF STP(FM1$(238,4),1)="" OR FM1[25]=0 THEN GOTO *RETURN
7120 DIM UM$(21),UM[2]; FIND (Z[1],KEY="U/M"+FM1$(238,4),DOM=*NEXT)IOL=0320
7125 IF UM$(20,1)<>"Y" THEN SQP=FM1[25],PRC_DIV=FM1[25] ELSE SQP=UM[1],PRC_DIV=UM[0]
7130 IF SQP=0 THEN SQP=1
7131 IF PRC_DIV=0 THEN PRC_DIV=1
7135 AUMQP=SQP,AUMPD=PRC_DIV,OPTIONS$(2,1)="Y",%BPACK=AUMQP,%SELL_UM$=FM1$(238,4)
7140 RETURN 
7145 ! 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "277028-EC Qty Break display logic not correct if pricing table type
56002 REM "278081-see 277028; remaining issues with EC quantity breaks display
