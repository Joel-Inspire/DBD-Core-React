0010 REM "EC Credit Card Information <EC3MAD>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 07/07/15 - 16.269166 - jvv - SSP# 229993
0037 REM "229993-PCI/PA-DSS New Credit Card Processing - Vault Module        
0040 REM "Copyright 2015 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="EC3MAD",X1$="Credit Card Posting",K9$="",K9=0
0120 PRECISION 7
0130 K0=3,K1=1
0135 C9=-1,V9=-2
0140 M0$="###,###.00-"
0145 OK_TO_EXIT$=""
0146 IF TCB(88)=0 THEN MSGBOX MSG("WARN_WINDX"),MSG("ERROR"),"OK"; GOTO 9900 ! SSP 229993
0148 CCV_MODULE=0,CC_ARG1$="",CC_ARG2$=""; IF FN%HAS_MODULE("CV") THEN CCV_MODULE=1 ! SSP 229993
0150 IF LEN(Q0$)>12 THEN INV_NUM$=Q0$(13,8),ORDER_DIV$=Q0$(1,2),ORDER_NUM$=Q0$(3,6),Q0$=Q0$(1,12); CC_ARG1$=ORDER_DIV$+ORDER_NUM$,CC_ARG2$=INV_NUM$; GOTO 0170 ! PROCESS "ECGMAD","../ECG/EC.EN",ORDER$,INV_NUM$; GOTO 9900 ! SSP 229993 
0160 IF LEN(Q0$)>8 THEN CC_TYPE$=Q0$(9,4),Q0$=Q0$(1,8),ORDER$=Q0$,CC_ARG1$=ORDER$; GOTO 0170 ! PROCESS "ECGMAD","../ECG/EC.EN",ORDER$; GOTO 9900 ! SSP 229993
0170 IF CCV_MODULE THEN PROCESS "CVGMAD","../CVG/CV.EN",CC_ARG1$,CC_ARG2$; GOTO 9900 ELSE PROCESS "ECGMAD","../ECG/EC.EN",CC_ARG1$,CC_ARG2$; GOTO 9900 ! SSP 229993"
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1,LEN(A$)),A[0],A[1],A[2],A[3],A[4],A[5],A[6]
0320 IOLIST B$(1)
0330 IOLIST C$
0340 IOLIST AR1$
0350 IOLIST ASP$
0360 IOLIST FS1$
0370 IOLIST FSV$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X EDD...  02O AR1...  03O AR6...  04O ASP...  05O FSV...  06O ZZPARM  07O FS1...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0570 DIM F0$(100)
0600 REM "
0620 GOSUB 6000
0900 REM "Date Mask
0905 DATE_MASK$="MM/DD/YYYY HH:MI:SS"
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(600),C$(200),D$(87),ASP$(35)
1035 DIM A$(316),A[6]
1036 IF LEN(INV_NUM$)>0 THEN {
1037 ENTRY_EXISTS=0; CALL "UPDEDD;GET_LAST_INVOICE_ENTRY",Z[1],ORDER_DIV$,ORDER_NUM$,INV_NUM$,A$,A{ALL},ENTRY_EXISTS
1038 IF ENTRY_EXISTS<>0 THEN C9=1; A1$=A$(1,8); INV_NUM$=""; GOTO 1530
1039  }
1045 A$(1,0)=K9$+A1$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1200,1250,1500
1200 REM "Order number
1201 FIND (Z[4],KEY=CC_TYPE$,DOM=1202)ASP$
1205 IF LEN(A1$)>0 THEN A$(1,8)=A1$(1,8); GOSUB 6460; GOTO 1220
1206 IF Q0$<>DIM(8) THEN A$(118,1)="M"
1207 IF Q0$<>DIM(8) THEN A$(1,8)=Q0$,A$(9,3)="001"
1210 CALL "ZZENTR","AUZ",A{ALL},A$,X4$,X3$,20,3,1,8,C0,"O/P","{1"+X$,"","EC3MAD00","EDD","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1211 IF C0=4 AND MID(ASP$,43,10)=DIM(10) THEN GOTO 9900; REM "IF F4 AND NOT SET U THE VALIDATE...EXIT
1212 IF POS(Q1$="FM2ODAFS2DAAFM2ODM",6)<>0 AND C0=4 AND A$(12,28)=DIM(28) THEN GOTO 1210
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,6800,9800,6970
1240 GOTO 1120
1250 REM "Sequence Number
1260 CALL "ZZENTR","ZUX",A{ALL},A$,X4$,X3$,35,3,9,3,C0,"","{2"+X$,"","ECDMAD00","","",""; IF C0=6 THEN GOSUB 6400; C0=1
1261 IF ABS(C0)=4 AND OK_TO_EXIT$="YES" THEN GOTO 1290
1262 IF ABS(C0)=4 AND MID(ASP$,43,10)="" THEN GOTO 9900
1263 IF POS(Q1$="FM2ODAFS2DAAFM2ODMAR2EAB",6)<>0 AND ABS(C0)=4 AND A$(12,28)=DIM(28) THEN GOTO 1250
1264 IF ABS(C0)<>4 AND A$(9,3)=DIM(3) OR A$(9,3)="000" THEN A$(9,3)="001"; GOTO 1260
1265 IF ABS(C0)<>4 AND MID(A$,9,3)<"001" OR MID(A$,9,3)>"999" THEN A$(9,3)="001"; GOTO 1260
1266 IF ABS(C0)=4 AND OK_TO_EXIT$<>"YES" THEN IF POS(Q1$="FM2ODAFS2DAAFM2ODMAR2EAB",6)<>0 THEN GOTO 1260 ELSE GOTO 9900
1285 IF ABS(C0)=2 AND LEN(A1$)>0 THEN A1$=A1$(1,0)
1290 GOTO 1120
1500 REM "
1505 Z9=-1
1510 A1$=K9$+A$(LEN(K9$)+1,11),A$(1,LEN(K9$))=K9$
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1521 ! IF A$(88,30)<>DIM(30) AND Q1$<>"" THEN GOTO 6800
1526 Z9=0
1530 GOSUB 5800
1590 GOTO 1700
1600 REM "
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630,6975
1630 GOTO 1000
1660 GOSUB 6200
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1820 CALL "ZZPROM",STR(6+C9),X3$,Z,"","","V",X0; ON Z GOTO 1850,6500,6700,6700,6800
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310
1865 C9=1
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF C0>3 OR X0<0 THEN GOTO 1800
2035 IF X0*6+11<LEN(V9$) THEN IF V9$(X0*6+11,1)="2" THEN GOTO 2020
2039 SETERR 9000
2040 ON X0 GOTO 2100,2150,2200,2550,2250,2300,2500,1800
2100 REM "Card type
2105 IF %SAVE_CC_INFO$="Y" AND A$(16,20)=DIM(20) THEN CALL "ECGMAD;GET_FIRST_CC",%SAVE_CUSTOMER$,CC_TYPE$,CC_NUM$,EXP_DATE$,CC_NAME$; A$(16,20)=CC_NUM$,A$(36,4)=EXP_DATE$,A$(12,4)=CC_TYPE$,A$(40,40)=CC_NAME$; PRINT @(20,5),CC_TYPE$,@(20,7),EXP_DATE$,@(20,8),CC_NAME$ ! SSP 225020
2110 CALL "ZZENTR","SUZ",A{ALL},A$,X4$,X3$,20,5,12,4,C0,"","{3"+X$,"","EC3MAD02","ASP","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2110,2111 ! WO227413, fix entry type, added U and Z
2120 FIND (Z[4],KEY=A$(12,4),DOM=2100)ASP$
2130 PRINT @(26,5),ASP$(5,30),
2140 GOTO 2020
2150 REM "Card number
2155 X$="Enter Credit Card Number"
2160 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,20,6,16,20,C0,"","{3"+X$,"","EC3MAD04","","",""
2165 IF ASP$(43,10)<>DIM(10) AND A$(16,20)=DIM(20) THEN GOTO 2150
2170 PRINT @(20,6),'CL',
2190 GOTO 2020
2200 REM "Expiration date
2205 X$="MMYY format"
2210 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,20,7,36,4,C0,"","{3"+X$,"0000","EC3MAD06","","",""
2211 IF ASP$(43,10)=DIM(10) THEN GOTO 2240; REM " IF NOT 10, DON'T VALIDATE
2215 IF NUM(A$(36,2),ERR=2210)<1 THEN GOTO 2210
2216 IF NUM(A$(36,2),ERR=2210)>12 THEN GOTO 2210
2217 IF NUM(A$(38,2),ERR=2210)<4 THEN GOTO 2210
2218 IF NUM(A$(38,2),ERR=2210)>99 THEN GOTO 2210
2219 DATE$=DAY
2220 REM "IF A$(36,2)+A$(38,2)<DATE$(1,2)+DATE$(7,2) THEN GOTO 2210
2230 X=NUM(A$(36,2)),X1=NUM(A$(38,2))
2231 GOSUB 8100
2232 Q$=A$(38,2)+A$(36,2)+STR(Q:"00"); GOSUB 8500; EXP_DATE$=Q0$
2233 Q$=DATE$(7,2)+DATE$(1,2)+DATE$(4,2); GOSUB 8500; TODAY_DATE$=Q0$
2234 IF EXP_DATE$<TODAY_DATE$ THEN GOTO 2200
2240 GOTO 2020
2250 REM "Name on card
2255 X$="You must enter a Name"
2260 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,20,8,40,40,C0,"","{3"+X$,"","EC3MAD08","","",""
2261 IF ASP$(43,10)=DIM(10) THEN GOTO 2290; REM " IF NOT 10, DON'T VALIDATE
2265 IF A$(40,40)=DIM(40) THEN GOTO 2250
2290 GOTO 2020
2300 REM "Approval reference number - DON'T ALLOW ENTRY 172576
2301 IF POS(X3$(9,3)="500377",3)<>0 THEN GOTO 2340
2310 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,20,9,88,30,C0,"","{3"+X$,"","EC3MAD10","","",""
2340 GOTO 2020
2350 REM "Invoice number
2360 CALL "ZZENTR","SZU",A{ALL},A$,X4$,X3$,20,11,80,8,C0,"","{3"+X$,"","EC3MAD12","ARB","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 2360,2361
2390 GOTO 2020
2400 REM "invoice total
2410 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,20,12,0,922,C0,M0$,"{3"+X$,"","","","",""
2440 GOTO 2020
2450 REM "Total Sales Tax
2460 CALL "ZZENTR","N",A{ALL},A$,X4$,X3$,20,13,1,922,C0,M0$,"{3"+X$,"","","","",""
2490 GOTO 2020
2500 REM "Billing Address
2501 IF A$(126,87)=DIM(87) THEN GOSUB GET_BILLING_ADDR; GOSUB 6400
2503 ADDR$=A$(126,87)
2510 CALL "ZY2AAA",X3$,X4$,11,16,"D","",2,ADDR$,"",1,C0,0
2511 A$(126,87)=ADDR$
2540 GOTO 2020
2550 ! Card security code, WO227413
2560 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,60,7,231,4,C0,"","{3"+X$,"","EC3MAD14","","",""
2570 PRINT @(60,7),'CL',
2590 GOTO 2020
2595 ! 
5800 REM "Alt data reads
5805 DIM ASP$(256); FIND (Z[4],KEY=A$(12,4),DOM=5806)IOL=0340
5845 RETURN 
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6060 PRINT @(3,3),"   Order number:",@(0,4),Z0$,@(9,5),"Card type:",@(7,6),"Card number:",@(3,7),"Expiration date:",@(6,8),"Name on card:",@(0,9),"Approval reference:",@(0,10),Z0$,
6062 PRINT @(30,3),"Seq: ",
6065 PRINT @(4,11),"Invoice number:",@(5,12),"Invoice total:",
6067 PRINT @(7,13),"Invoice Tax:",
6068 PRINT @(12,14),"Status:",
6069 PRINT @(61,3),"INV DT:",@(0,15),Z0$
6070 PRINT @(3,16),"Billing Address:",@(14,18),"City:",@(38,18),"State:",@(15,19),"Zip:",
6075 PRINT @(34,7),"Card security code (CVV):", ! WO227413
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6205 GOSUB 6450; IF A$(12,68)=DIM(68) THEN RETURN 
6210 PRINT @(20,5),A$(12,4),@(20,7),A$(36,4),@(26,5),ASP$(5,30),@(20,8),A$(40,40),@(20,9),A$(88,30),@(20,11),A$(80,8),@(20,12),A[0]:M0$,
6212 IF A$(118,1)="M" THEN PRINT @(40,3),"MANUAL ENTRY" ELSE PRINT @(40,3),"WEBEC ENTRY ",
6214 PRINT @(20,13),A[1]:M0$,
6215 IF A$(213,8)<>DIM(8) THEN PRINT @(40,9),"Charged to:",@(51,9),A$(213,8),",",@(60,9),"Amount:",@(67,9),A[2]:M0$,
6216 IF A$(221,10)<>DIM(10) THEN PRINT @(50,8),"Merchant account: ",A$(221,10) ! SSP 218488
6218 CALL "ZZDISP","D",A$(120,6),"",X3$,"","",70,3,X4$
6220 SWITCH A$(119,1)
6221 CASE "G"
6222 STATUS$="GOOD VALIDATION"; BREAK
6223 CASE "B"
6224 STATUS$="BAD VALIDATION"; BREAK
6225 CASE "N"
6226 STATUS$="NOT VALIDATED DURING ENTRY"; BREAK
6227 CASE "W"
6228 STATUS$="VALIDATED WEBEC"; BREAK
6229 CASE "A"
6230 STATUS$="AUTHORIZED-AMOUNT CHARGED"; BREAK
6231 CASE "E"
6232 STATUS$="CARD AUTHORIZATION ERROR"; BREAK
6233 CASE "D"
6234 STATUS$="DECLINED-TERMS CODE CHANGED"; BREAK
6235 CASE "Z"
6236 STATUS$="DECLINED-NO ALT. TERMS CODE"; BREAK
6237 DEFAULT 
6238 STATUS$="UNKNOWN STATUS"
6239 END SWITCH 
6270 PRINT @(20,14),STATUS$,
6280 CALL "ZZDISP","D",A$(120,6),"",X3$,"","",70,3,X4$
6290 PRINT @(20,16),A$(126,30),@(20,17),A$(156,30),@(20,18),A$(186,16),@(45,18),A$(202,2),@(20,19),A$(204,9)
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000; GOSUB 5800
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6460 CALL "ZZDISP","A",A$(1,8),"O/P",X3$,"","",20,3,X4$
6470 PRINT @(35,3),A$(9,3),
6490 RETURN 
6500 REM "DELETE
6501 ! IF ASP$(43,10)=DIM(10) THEN GOTO 6510
6502 CALL "ZZPROM","X0EC3MAD",X3$,Z,"","","",0; IF Z=0 THEN GOTO 1820 ELSE DIM IN$(6); PRINT 'RB',; CALL "ZZENTR","SPUT",X{ALL},IN$,X4$,X3$,49,22,1,6,C0,"","Enter password: ","","","","",""; IF IN$<>"030364" THEN GOTO 1820
6503 REM "IF FLAG$<>"GOOD" THEN CALL "ZZPROM","X1EC3MAD",X3$,Z,"","","",0; IF Z=0 THEN GOTO 1820 ELSE INPUT @(0,21),'CL',"Password: ",IN$,; PRINT @(0,21),'CL',; IF IN$<>"090669" THEN GOTO 1820
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY
6707 REM "IF A$(9,4)=DIM(4) OR A$(13,20)=DIM(20) OR A$(33,4)=DIM(4) THEN GOTO 1820
6708 IF ASP$(43,10)=DIM(10) THEN GOTO 6710
6709 IF A$(12,4)=DIM(4) OR A$(16,20)=DIM(20) OR A$(36,4)=DIM(4) THEN CALL "ZZPROM",".4",X3$,Z,"Card Type, Card Number and Expiration date must be entered.","","",0; GOTO 1820
6710 CALL "AR2VCC",X3$,X4$,STP(A$(16,20),1)+"|"+A$(36,4),A$(12,4),FLAG$
6713 IF FLAG$<>"GOOD" AND FLAG$<>"NO" THEN CALL "ZZPROM","X0EC3MAD",X3$,Z,"","","",0; IF Z=0 THEN GOTO 1820 ELSE DIM IN$(6); PRINT 'RB',; CALL "ZZENTR","SPUT",X{ALL},IN$,X4$,X3$,49,22,1,6,C0,"","Enter password: ","","","","",""; IF IN$<>"030364" THEN GOTO 1820
6714 REM "IF FLAG$<>"GOOD" AND FLAG$<>"NO" THEN CALL "ZZPROM","X0EC3MAD",X3$,Z,"","","",0; IF Z=0 THEN GOTO 1820 ELSE INPUT @(0,21),'CL',"Password: ",IN$,; PRINT @(0,21),'CL',; IF IN$<>"090669" THEN GOTO 1820
6720 IF FLAG$="GOOD" THEN A$(119,1)="G" ELSE IF FLAG$="NO" THEN A$(119,1)="N" ELSE A$(119,1)=DIM(1)
6740 WRITE (Z[1],KEY=A$(1,11))IOL=0310
6755 IF Q1$="" AND FLAG$="GOOD" THEN OK_TO_EXIT$="YES"
6760 IF POS(Q1$="FM2ODAFS2DAAFM2ODMAR2EAB",6)<>0 THEN OK_TO_EXIT$="YES"; REM "GOTO 9900
6770 GOSUB 6400
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6805 ON Z-4 GOTO 6806,6850,6806
6810 A1$=KEY(Z[1],END=6895)
6820 IF A1$="" THEN GOTO 6800 ELSE IF A1$(1,8)<>A$(1,8) THEN GOTO 6895
6825 A$(1,LEN(A1$))=A1$
6830 GOSUB 6600
6835 GOSUB 6400
6840 GOTO 1515
6850 REM "PRIOR ENTRY
6855 A1$=KEP(Z[1],ERR=6856); GOTO 6820 ! CALL "ZZKEYP",ERR=6856,Z[1],A1$; GOTO 6820
6860 A1$=KEN(Z[1],ERR=6855); GOTO 6820 ! CALL "ZZKEYL",ERR=6861,Z[1],A1$; GOTO 6820
6870 GOTO 6895
6890 GOTO 1515
6895 EDD_NEXT_SEQ=-1; VALID_SEQ=0; CALL "UPDEDD;GET_NEXT_SEQ",Z[1],A$(1,2),A$(3,6),EDD_NEXT_SEQ,VALID_SEQ; IF VALID_SEQ<=0 OR EDD_NEXT_SEQ<0 THEN GOTO 1140
6896 A$(9)=STR(EDD_NEXT_SEQ:"000"),A$(118,1)="M"; GOSUB 6400; PRINT 'RB',; X0=1; GOTO 1140
6900 REM "GET DEFAULTS
6901 GOTO 6930
6902 V9$="1803  4603  1804  5306  5307  2909  2910  2911  2913  2914  2916  7016  2917  "
6910 IF V9>=0 THEN READ (Z[6],IND=V9)V9$,IOL=0310; GOTO 6930 ELSE V9=-1
6920 EXTRACT (Z[6],KEY=X0$+"DFLT",DOM=6921)V9$,IOL=0310; V9=IND(Z[6]); READ (Z[6])
6930 RETURN 
6950 REM "WRITE DEFAULT
6960 WRITE (Z[6],KEY=X0$+"DFLT")V9$,IOL=0310
6970 REM "ENTRY DEFAULTS
6975 PRINT 'CF'; V9=-2,Q$=""
6980 FOR X=1 TO LEN(V9$) STEP 6
6982 REM PRINT @(NUM(V9$(X,2)),NUM(V9$(X+2,2))),; IF V9=-2 THEN PRINT V9$(X+4,1), ELSE READ RECORD (0,SIZ=1) Q$; IF POS(Q$="1234Z")=0 THEN PRINT 'RB'; GOTO 06982
6990 IF Q$="Z" THEN EXITTO 6950 ELSE V9$(X+4,LEN(Q$))=Q$; NEXT X
6995 V9=0; GOTO 6980
8100 REM "GET MAX # DAYS FOR MONTH X IN Q FOR YEAR X1
8110 Q=31-SGN(POS(STR(X:"00")="0904061102",2))
8120 IF X=2 THEN Q=28; IF MOD(X1,4)=0 AND MOD(X1,100)<>0 OR MOD(X1,400)=0 THEN Q=29
8121 REM "IF X=2 THEN LET Q=Q-1-SGN(FPT(X1/4))
8190 RETURN 
8500 REM "Convert date Q$ into Q0$
8501 REM "Q$ in YYYYMMDD format or YYMMDD
8505 Q0$=""
8506 Q$=STP(Q$,1)
8507 IF Q$="" THEN GOTO 7549
8510 IF LEN(Q$)=8 THEN GOTO 7520 ELSE IF LEN(Q$)=6 THEN D1$=STR(NUM(Q$(1,2)):"00") ELSE GOTO 7549
8515 IF D1$<"50" THEN Q$="20"+Q$ ELSE Q$="19"+Q$
8520 Q0$=CHR(NUM(Q$(1,3),ERR=7521)-125)+Q$(4,1)+Q$(5,2)+Q$(7,2)
8549 RETURN 
8700 GET_BILLING_ADDR:
8710 FIND (Z[6],KEY=X3$(9,3)+"F/M",DOM=*NEXT)P1$
8730 IF P1$(64,1)="Y" THEN {
8740 FIND (Z[5],KEY=A$(1,8),DOM=*NEXT)IOL=0370
8750 FSV$=PAD(FSV$,152)
8760 A$(126,87)=FSV$(54,87)
8770  } ELSE {
8780 FIND (Z[7],KEY=A$(1,8),DOM=8860)IOL=0360
8790 FIND (Z[2],KEY=FS1$(6,10),DOM=*NEXT)IOL=0340
8800 A$(126,87)=AR1$(56,87)
8850  }
8860 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,1
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 GOSUB 6400
9520 ON C9 GOTO 1140,2040
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9905 OK_TO_EXIT$=""
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "190176-Some EDD entries have '   ' for sequence and other start
56010 REM "189822-Credit Card Processing for webec back orders filled
56012 ! "225020-Populate the order credit card info from the customer credit
56014 REM "227413-Credit card transactions need to support CVV security field
56015 REM "229993-PCI/PA-DSS New CC Module                                    
