0010 REM "ec3srv tag processor <EC3BYG>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 05/04/20 - 11.381111 - dmm - SSP# 307280
0037 REM "307280-DBD-89; Output tag error_message in EC3SRV                  
0040 REM "Copyright 2020 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,EC_PARM$,EC${ALL},OUTPUT,OTHER${ALL},OPTIONS$,LAST_WEBEC_ERR$,CALL_PROG$,ERROR_FLAG$
0091 ! ?ec3srv? tag processor, called from EC3SRV, use output file as input, find/replace special tags, write new output
0100 SETERR 9000
0110 X0$="EC3BYG",X1$="ec3srv tag processor"
0120 EOL$=$0D0A$; EOL$=ATH(STP(EC_PARM$(57,8),1))
0199 ! 
1100 REM "Process line for ec3srv tags
1105 DATA_TAGS$="001<error_message>"
1199 ! 
1300 ! SSP307280-DBD-89 - use output file as input, read and write until ?ec3srv? tag is found, replace then write and continue until done
1310 OUT_FILE_NAME$=PTH(OUTPUT); CLOSE (OUTPUT)
1315 NEW_OUT_FILE_NAME$=OUT_FILE_NAME$+"2"; ERASE NEW_OUT_FILE_NAME$,ERR=*NEXT
1325 SERIAL NEW_OUT_FILE_NAME$
1330 IN_FILE_NAME$=OUT_FILE_NAME$
1335 IN_CHANNEL=HFN; OPEN (IN_CHANNEL,OPT="TEXT")IN_FILE_NAME$
1340 OUT_CHANNEL=HFN; OPEN LOCK (OUT_CHANNEL)NEW_OUT_FILE_NAME$
1350 ! 
1360 READ_FILE:READ (IN_CHANNEL,END=ALL_DONE)LINE$
1399 ! 
1500 REM "process tags
1510 START_INDEX=1
1515 INDEX=POS("?ec3srv?"=LINE$(START_INDEX)); IF INDEX=0 THEN GOSUB WRITE_LINE; GOTO READ_FILE
1520 END_INDEX=POS("?"=LINE$(START_INDEX+INDEX+7)),TAG$=LINE$(START_INDEX+INDEX+7,END_INDEX-1),LINE1$=LINE$(1,START_INDEX+INDEX-2),LINE2$=LINE$(START_INDEX+INDEX+END_INDEX+7),LPOS=POS("<"+TAG$+">"=DATA_TAGS$); IF LPOS=0 THEN LINDEX=0 ELSE LINDEX=NUM(DATA_TAGS$(LPOS-3,3))
1530 GOSUB 2000; GOTO 1515
1599 ! 
1600 WRITE_LINE:
1610 PRINT (OUT_CHANNEL)LINE$
1640 RETURN 
1649 ! 
1995 GOTO 9900
1999 REM "Get tag value & substitute
2000 ON LINDEX GOTO 2090,2001,2090
2001 MESSAGE$=""; IF LAST_WEBEC_ERR$>"" THEN MESSAGE$="Message="+LAST_WEBEC_ERR$+" P="+CALL_PROG$,ERROR_FLAG$="Y" END_IF ; LINE$=LINE1$+FN%HTML_ESC$(MESSAGE$)+LINE2$; GOTO 2095 ! error_message
2090 LINE$=LINE1$+"?ec3srv?"+TAG$+"?"+LINE2$,START_INDEX=START_INDEX+INDEX+END_INDEX+7; GOTO 2095; REM "tag not found, Unlike other programs, we want to perserve this tag for the calling program, so rebuild line$with tag in place and move start_index, so we don't look again at this tag
2095 RETURN 
2099 ! 
5000 ALL_DONE:! close files, rename and open original output file
5010 CLOSE (IN_CHANNEL)
5015 CLOSE (OUT_CHANNEL)
5020 ERASE OUT_FILE_NAME$,ERR=*NEXT
5025 RENAME NEW_OUT_FILE_NAME$,OUT_FILE_NAME$
5030 OPEN (OUTPUT)OUT_FILE_NAME$
5050 GOTO 9900
5099 ! 
9000 REM "ERROR PROCESSING
9002 J=ERR,K=TCB(5); SETERR 0000; ENDTRACE 
9010 LAST_WEBEC_ERR$="ERR: "+STR(J)+" AT: "+STR(K)+" IN: EC3BYG"; LAST_WEBEC_ERR$=GBL("LAST_WEBEC_ERR",LAST_WEBEC_ERR$); REM "131928
9090 GOTO 9900
9900 REM "End
9950 EXIT 
9999 END 
56000 REM "307280-DBD-89; Output tag error_message in EC3SRV                  
