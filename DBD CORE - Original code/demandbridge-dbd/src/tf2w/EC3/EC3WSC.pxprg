0010 REM "Order entry Gateway - Change/Cancel Line <EC3WSC>
0020 SETESC 9300; SETERR 9000
0035 REM "5.5 - 11/03/06 - 10.260555 - crg - SSP# 198994
0037 REM "198994-Import XML to create cust supply p.o. and sls order p.o
0040 REM "Copyright 2006 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0085 REM "ORDER_TYPE$ : Order type : B - Bill as shipped, S - Stock PO
0086 REM "EXT_REF$    : External customer reference
0087 REM "LINE_NUM$   : Order line number
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,EC_PARM$,EC${ALL},ORDER_TYPE$,EXT_REF$,LINE_NUM$,NEW_QTY,RET_VAL,MESG$,ORDER_NUM$
0100 SETERR 9000
0110 X0$="EC3WSC",X1$="WebEC Order Entry Gateway - Cancel line"
0300 REM "IOLISTS
0310 IOLIST FS1$,FS1[0],FS1[1],FS1[2],FS1[3],FS1[4],FS1[5],FS1[6],FS1[7],FS1[8],FS1[9],FS1[10],FS1[11],FS1[12],FS1[13]
0320 IOLIST FS2$,FS2[0],FS2[1],FS2[2],FS2[3],FS2[4],FS2[5],FS2[6],FS2[7],FS2[8],FS2[9],FS2[10],FS2[11],FS2[12],FS2[13],FS2[14],FS2[15],FS2[16],FS2[17],FS2[18],FS2[19],FS2[20],FS2[21],FS2[22],FS2[23],FS2[24],FS2[25],FS2[26],FS2[27],FS2[28],FS2[29]
0330 IOLIST PO1$
0340 IOLIST PO2$,PO2[0],PO2[1],PO2[2],PO2[3],PO2[4],PO2[5],PO2[6],PO2[7],PO2[8],PO2[9],PO2[10],PO2[11],PO2[12],PO2[13],PO2[14],PO2[15],PO2[16],PO2[17],PO2[18],PO2[19],PO2[20],PO2[21],PO2[22],PO2[23],PO2[24],PO2[25],PO2[26],PO2[27]
0400 IOLIST PO3$
0401 IOLIST PO3[0],PO3[1],PO3[2],PO3[3]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O FS1...  02O FS2...  03O PO1... 04O PO2...  05O PO3...  13O ZZPARM  18O FTO...  19O ZY9...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
1000 REM "Check order type
1001 IF NUL(ORDER_TYPE$) THEN GOTO ERR_INV_ORDER_TYPE
1002 ON POS(ORDER_TYPE$="BS",1) GOTO ERR_INV_ORDER_TYPE,CANCEL_BAS_LINE,CANCEL_PO_LINE
1010 ! 
2000 CANCEL_BAS_LINE:
2010 DIM FS1$(350),FS1[13],FS2$(356),FS2[29],PO2$(170),PO2[28],PO3$(428),PO3[3]
2020 READ (Z[1],KEY=PAD(EXT_REF$,15),KNO=4,DOM=*NEXT)
2030 FS1_KEY$=KEY(Z[1],KNO=4,END=ERR_NO_ORD_FOUND); IF FS1_KEY$(1,15)<>PAD(EXT_REF$,15) THEN GOTO ERR_NO_ORD_FOUND
2040 READ (Z[1],KEY=FS1_KEY$(16,8),KNO=0,DOM=ERR_NO_ORD_FOUND)IOL=0310
2050 READ (Z[2],KEY=FS1_KEY$(16,8)+LINE_NUM$,DOM=ERR_NO_LINE_FOUND)IOL=0320
2060 READ (Z[5],KEY=FS1_KEY$(16,8)+" "+LINE_NUM$,DOM=*NEXT)
2070 PO3_KEY$=KEY(Z[5],END=ERR_NO_SHIPPOINT); IF PO3_KEY$(1,12)<>FS1_KEY$(16,8)+" "+LINE_NUM$ THEN GOTO ERR_NO_SHIPPOINT
2080 READ (Z[5],KEY=PO3_KEY$)IOL=0400,IOL=0401
2090 ORDER_NUM$=FS1_KEY$(16,8); OPEN_QTY=FS2[0]
2091 READ (Z[4],KEY=FS1_KEY$(16,8)+FS2$(9,1)+FS2$(6,3),DOM=ERR_NO_LINE_FOUND)IOL=0340; IF PO2[6]>0 THEN GOTO ERR_QTY_PREV_RECVD
2095 CHECK_PO$=FS1_KEY$(16,8); GOSUB 5100; IF VAL_BATCH$="Y" THEN GOTO ERR_PO_BEING_RECVD
2100 CALL "FM2ODH",X3$,-1,FS1$,FS1{ALL}
2110 FS2[0]=NEW_QTY*FS2[5]; FS2[10]=NEW_QTY*FS2[5]; CURR_QTY=NEW_QTY*FS2[5]
2120 REM "Cost extension
2130 J0=FS2[2],J1=FS2[0],J2=FS2[1]; CALL "FM2EXT",13,0,FS2$(120,4),J0,J1,J2,J3,0; FS2[3]=J3
2140 REM "Price extension
2150 J0=FS2[5],J1=FS2[0],J2=FS2[4]; CALL "FM2EXT",13,0,FS2$(124,4),J0,J1,J2,J3,0; FS2[6]=J3
2160 WRITE (Z[2],KEY=FS1_KEY$(16,8)+LINE_NUM$,ERR=ERR_ON_WRITE)IOL=0320
2170 PO3[0]=FS2[0]
2180 WRITE (Z[5],KEY=PO3_KEY$)IOL=0400,IOL=0401
2190 CALL "FM2ODH",X3$,1,FS1$,FS1{ALL}
2200 GOTO CANCEL_DONE
4000 ! 
4010 CANCEL_PO_LINE:
4020 DIM PO1$(383),PO2$(170),PO2[28]
4030 READ (Z[3],KEY=PAD(EXT_REF$,15),KNO=4,DOM=*NEXT)
4040 PO1_KEY$=KEY(Z[3],KNO=4,END=ERR_NO_ORD_FOUND); IF PO1_KEY$(1,15)<>PAD(EXT_REF$,15) THEN GOTO ERR_NO_ORD_FOUND
4050 READ (Z[3],KEY=PO1_KEY$(16,9),KNO=0,DOM=ERR_NO_ORD_FOUND)IOL=0330
4060 READ (Z[4],KEY=PO1_KEY$(16,9)+LINE_NUM$,DOM=ERR_NO_LINE_FOUND)IOL=0340
4070 ORDER_NUM$=PO1_KEY$(16,9); OPEN_QTY=PO2[1]
4071 IF PO2[6]>0 THEN GOTO ERR_QTY_PREV_RECVD
4075 CHECK_PO$=PO1_KEY$(16,8); GOSUB 5100; IF VAL_BATCH$="Y" THEN GOTO ERR_PO_BEING_RECVD
4080 CALL "PO2MAH",X3$,"S","!"+PO1_KEY$(16,9),"-1"
4090 PO2[1]=NEW_QTY*PO2[2]; PO2[11]=NEW_QTY*PO2[2]; CURR_QTY=NEW_QTY*PO2[2]; REM " Qty, Real Qty Ordered
4100 J0=PO2[2],J1=PO2[1],J2=PO2[0],J3=0; CALL "FM2EXT",13,0,PO2$(77,4),J0,J1,J2,J3,0; PO2[4]=J3; REM " Cost extension
4110 WRITE (Z[4],KEY=PO1_KEY$(16,9)+LINE_NUM$,ERR=ERR_ON_WRITE)IOL=0340
4120 CALL "PO2MAH",X3$,"S","!"+PO1_KEY$(16,9),"1"
4130 GOTO CANCEL_DONE
4140 ! 
5100 REM "Check if PO is being received, logic from PO2MAM
5105 VAL_BATCH$="N"
5110 DIM FTO$(60); READ (Z[18],KEY=CHECK_PO$,DOM=*NEXT)
5113 READ (Z[18],END=5199)FTO$; IF FTO$(1,8)<>CHECK_PO$ THEN GOTO 5199
5115 READ (Z[19],KEY=FTO$(10,4),DOM=*NEXT)
5120 READ (Z[19],END=5170)ZY9$; IF ZY9$(1,4)=FTO$(10,4) AND ZY9$(6,6)="PO2MAG" THEN VAL_BATCH$="Y"; GOTO 5199 ELSE GOTO 5120
5170 REMOVE (Z[18],KEY=FTO$(1,9)); REM Invalid FTO record, remove and return
5199 RETURN 
5200 ! 
7700 CANCEL_DONE:
7705 RET_VAL=0; MESG$="Line changed, quantity was "+STR(OPEN_QTY)+", set to "+STR(CURR_QTY); GOTO 9900
7710 ERR_NO_ORD_FOUND:
7720 RET_VAL=-10; MESG$="No order found from external reference"; GOTO 9900
7723 ERR_NO_LINE_FOUND:
7726 RET_VAL=-20; MESG$="No order line found"; GOTO 9900
7730 ERR_INV_ORDER_TYPE:
7740 RET_VAL=-30; MESG$="Unsupported order type specified : "+ORDER_TYPE$; GOTO 9900
7743 ERR_ON_WRITE:
7746 RET_VAL=-40; MESG$="Error on write: "+MSG(ERR); GOTO 9900
7750 ERR_NO_SHIPPOINT:
7752 RET_VAL=-50; MESG$="No ship point found for order line"; GOTO 9900
7754 ERR_PO_BEING_RECVD:
7756 RET_VAL=-60; MESG$="Access denied for PO "+CHECK_PO$+" being received in batch "+MID(FTO$,10,4)+" by "+MID(FTO$,14,3); GOTO 9900
7757 ERR_QTY_PREV_RECVD:
7758 RET_VAL=-70; MESG$="Access denied as PO line shows non-zero quantity has been received previously"; GOTO 9900
9000 REM "ERROR PROCESSING
9002 J=ERR,K=TCB(5); SETERR 0000; ENDTRACE 
9005 IF ERR=69 THEN GOTO 9500
9010 LAST_WEBEC_ERR$="ERR: "+STR(J)+" AT: "+STR(K); LAST_WEBEC_ERR$=GBL("LAST_WEBEC_ERR",LAST_WEBEC_ERR$)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 GOTO 9900
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2160
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 ! Program changes starting 10/10/06
56002 REM "198994-Import XML to create cust supply p.o. and sls order p.o
