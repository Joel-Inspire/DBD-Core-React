0010 REM "EC Email Processing Program <EC3EML>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 12/20/10 - 9.548055 - dmm - SSP# 242458
0037 REM "242458-EC Server feature - Ability to save archived requests to    
0040 REM "Copyright 2010 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,EC_PARM$,MESSAGE_PATH$
0100 SETERR 9000
0110 X0$="EC3EML",X1$="E/C Email Processing Program"
0120 EOL$=$0D0A$; EOL$=ATH(STP(EC_PARM$(57,8),1))
0130 WEBEC_SEND_MAIL$="WEBEC_SEND_MAIL"
0140 SAVE_QUIET_MODE=%QUIET_MODE; %QUIET_MODE=0
0499 ! 
0500 REM "Open files
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="13O ZZPARM "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
1000 ! 
1100 REM "PROCESSING
1105 MX=FN%_LOG_MESSAGE("EMAIL","Sending: "+MESSAGE_PATH$)
1110 GOSUB LOG_REQUEST
1205 CALL "ZZ2CMD",X3$,X4$,"{"+WEBEC_SEND_MAIL$+"}",(MESSAGE_PATH$),"NR",RETURN_CODE,RESP$
1210 IF RETURN_CODE=-99 THEN MX=FN%_LOG_MESSAGE("EXCP","WEBEC SEND MAIL COMMAND NOT FOUND"); GOTO 1995
1220 IF NOT(NUL(RESP$)) THEN GOSUB LOG_RESPONSE
1260 GOSUB TMP_TO_ARCHIVE_DIR ! SSP242458 ERASE MESSAGE_PATH$,ERR=*NEXT
1990 MX=FN%_LOG_MESSAGE("EMAIL","Send done: "+MESSAGE_PATH$)
1995 GOTO 9900
1999 ! 
6000 REM "Log request - read first 10 lines or until the first blank line 
6005 LOG_REQUEST:
6010 LINE_COUNT=0; OPEN (HFN,ERR=ERR_LOG_REQUEST)MESSAGE_PATH$; MPCHAN=LFO
6020 NEXT_REQUEST_LINE:READ (MPCHAN,ERR=REQUEST_DONE,END=REQUEST_DONE)LINE$
6025 MX=FN%_LOG_MESSAGE("EMAIL",LINE$); LINE_COUNT++
6030 IF LINE_COUNT>9 OR NUL(STP(LINE$,3,$0D$)) THEN GOTO REQUEST_DONE ELSE GOTO NEXT_REQUEST_LINE
6040 REQUEST_DONE:
6044 IF MPCHAN THEN CLOSE (MPCHAN,ERR=*PROCEED); MPCHAN=0
6045 RETURN 
6049 ! 
6050 ERR_LOG_REQUEST: MX=FN%_LOG_MESSAGE("EXCP","ERROR OPENING MAIL REQUEST FILE "+MESSAGE_PATH$); RETURN 
7000 REM "Log response
7005 LOG_RESPONSE:
7010 IF NUL(RESP$) THEN RETURN 
7015 IF LEN(RESP$)>1048576 THEN RESP$=MID(RESP$,1,1048576) END_IF ; RESP$=SUB(RESP$,$0D0A$,$0A$); RESP$=SUB(RESP$,SEP,$0A$) ! Restrict to 1 MB if larger, replace \r and SEP chars with \n
7020 LINE=0; LINE=POS($0A$=RESP$); IF LINE>0 THEN LINE$=MID(RESP$,1,LINE-1); RESP$=MID(RESP$,LINE+1); ELSE LINE$=RESP$; RESP$=""
7030 MX=FN%_LOG_MESSAGE("EMAIL",LINE$)
7035 IF NOT(NUL(RESP$)) THEN GOTO 7020
7045 RETURN 
7049 ! 
8350 TMP_TO_ARCHIVE_DIR:! SSP242458
8355 IF STP(MESSAGE_PATH$)="" OR %SERVER_DIR$=%ARCHIVE_DIR$ OR STP(%ARCHIVE_DIR$)="" THEN ERASE MESSAGE_PATH$,ERR=*RETURN; GOTO *RETURN
8360 JUST_FILE_NAME$=FN%GET_BASENAME$(MESSAGE_PATH$) ! Get file name
8365 AF_NAME$=JUST_FILE_NAME$+"_"+DTE(JUL(DAY)+((TIM+(TCB(44)/3600))/24):"%Y%Mz%Dz_%Hz%mz%sz") ! archive file name
8370 AF_FULL_NAME$=%ARCHIVE_DIR$+AF_NAME$ ! Full path archive file
8375 ERASE AF_FULL_NAME$,ERR=*NEXT
8380 RENAME MESSAGE_PATH$,AF_FULL_NAME$,ERR=*NEXT; GOTO ARCHIVE_RENAME_DONE
8385 INVOKE "mv -f "+MESSAGE_PATH$+" "+AF_FULL_NAME$,ERR=*NEXT ! Try unix mv if RENAME failed
8390 ARCHIVE_RENAME_DONE:
8395 RETURN 
8399 ! 
9000 REM "ERROR PROCESSING
9002 J=ERR,K=TCB(5); SETERR 0000; ENDTRACE 
9010 LAST_WEBEC_ERR$="ERR: "+STR(J)+" AT: "+STR(K); LAST_WEBEC_ERR$=GBL("LAST_WEBEC_ERR",LAST_WEBEC_ERR$)
9090 GOTO 9900
9900 REM "End
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 %QUIET_MODE=SAVE_QUIET_MODE
9950 EXIT 
9999 END 
56001 REM "232226-Allow customized interfacing with sendmail
56002 REM "242458-EC Server feature - Ability to save archived requests to    
