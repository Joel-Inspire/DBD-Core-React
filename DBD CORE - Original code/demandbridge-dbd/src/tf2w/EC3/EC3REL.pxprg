0010 REM "EC Relogin routine <EC3REL>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 12/10/15 - 12.200277 - dmm - SSP# 281178
0037 REM "281178-PCI Scan - Relogin feature directory traversal issue        
0040 REM "Copyright 2015 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT
0100 SETERR 9000
0110 X0$="EC3REL",X1$="E/C Relogin"
0120 EOL$=$0D0A$,EOL$=ATH(STP(EC_PARM$(57,8),1)); REM "Default to CR/LF, then set from parameters
0130 IF NOT(NUL(%RELOGIN_TEMPLATE$)) THEN TEMPLATE$=%RELOGIN_TEMPLATE$ ELSE TEMPLATE$=STP(EC_PARM$(72,40),1) ! 178579
0135 IF STP(EC$[17],3)>"" THEN CALL "EC3SVT",X3$,X4$,EC_PARM$,EC${ALL},TEMPLATE$ ! SSP174750, if we have a webcode then see if there is a template to use
0500 REM "Open files
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="13O ZZPARM "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 ! Verify template is present in server directory SSP281178
0610 CLOSE (101); OPEN (101,ERR=*NEXT)TEMPLATE$; IF POS(%SERVER_TEMPLATE_DIR$=PTH(101))=0 THEN GOTO *NEXT ELSE CLOSE (101); GOTO 0621
0620 CLOSE (101); TEMPLATE$=STP(EC_PARM$(128,40),1)
1000 REM "Look for ?tf?hidden comment and replace with hidden variables representing the values in the DATA$[]
1010 FOR I=1 TO NUM_ENTRIES
1015 TAG$=DATA$[I,0],VALUE$=DATA$[I,1]
1021 IF TAG$="user" THEN USER$=VALUE$; GOTO 1090
1090 NEXT I
1100 REM "If use next $ to open file, read line by line and insert
1101 VERSION$="EV",PROGRAM$=PGN; DIM BYBINFO$[5]; BYBINFO$[1]=PROGRAM$,BYBINFO$[2]=VERSION$,BYBINFO$[3]=TEMPLATE$,BYBOPTIONS$="" ! SSP146024, never moved to release, moved with 174750
1105 CLOSE (100); OPEN (100,OPT="TEXT")TEMPLATE$
1110 READ (100,END=1195)LINE$; GOSUB 7550
1115 INDEX=POS("?tf?"=LINE$); IF INDEX=0 THEN PRINT (OUTPUT)LINE$,EOL$,; GOTO 1110
1120 END_INDEX=POS("?"=LINE$(INDEX+4)); TAG$=LINE$(INDEX+4,END_INDEX-1),LINE1$=LINE$(1,INDEX-1),LINE2$=LINE$(INDEX+END_INDEX+4)
1130 GOSUB 2000; GOTO 1115
1995 GOTO 9900
2000 REM "Get tag value & substitute
2010 IF TAG$="hidden" THEN GOSUB 2100; LINE$=LINE1$+VARIABLES$+LINE2$; GOTO 2095
2015 IF TAG$="ec_company" THEN LINE$=LINE1$+STP(EC_PARM$(7,50),1)+LINE2$; GOTO 2095
2020 IF TAG$="message" THEN LINE$=LINE1$+EC$[3]+LINE2$; GOTO 2095
2025 IF TAG$="user" THEN LINE$=LINE1$+USER$+LINE2$; GOTO 2095
2090 LINE$=LINE1$+LINE2$; GOTO 2095; REM "If here, no matching tag, remove it so processing can continue
2095 RETURN 
2100 REM "Add variables as hidden variables
2105 VARIABLES$="",XPOS=POS(">"=LINE2$); LINE1$=LINE1$+"hidden variables"+LINE2$(1,XPOS)+EOL$,LINE2$=LINE2$(XPOS+1); REM "hidden was part of a comment so move end of comment marker to line1$ side and add variables after the comment.
2110 FOR II=1 TO NUM_ENTRIES
2114 IF DATA$[II,0]="session_id" OR DATA$[II,0]="user" OR DATA$[II,0]="password" THEN GOTO 2120; REM "We will reset these, don't change now
2115 VARIABLES$=VARIABLES$+"<input type="+QUO+"hidden"+QUO+" name="+QUO+DATA$[II,0]+QUO+" value="+QUO+DATA$[II,1]+QUO+">"
2120 NEXT II
2195 RETURN 
7550 REM "Send LINE$ to EC3BYB for generic fields processing
7560 CALL "EC3BYB",ERR=7561,X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,LINE$,BYBINFO${ALL},BYBOPTIONS$ ! SSP146024, never moved to release, moved with 174750
7595 RETURN 
8910 DEF FND$(Z9$)=Z9$(1*2+1,2)+"/"+Z9$(7-1*2,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
9000 REM "ERROR PROCESSING
9002 J=ERR,K=TCB(5); SETERR 0000; ENDTRACE 
9010 LAST_WEBEC_ERR$="ERR: "+STR(J)+" AT: "+STR(K); LAST_WEBEC_ERR$=GBL("LAST_WEBEC_ERR",LAST_WEBEC_ERR$); REM "131928
9090 GOTO 9900
9900 REM "End
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9950 EXIT 
9999 END 
56000 REM "281178-PCI Scan - Relogin feature directory traversal issue        
