0010 REM "EC Image Library Functions <EC3IL0>"
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 07/11/11 - 14.383055 - dmm - SSP# 247556
0037 REM "247556-EC3IL0 is not adding the EOL like other EC programs.        
0040 REM "Copyright 2011 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT
0100 SETERR 9000
0110 X0$="EC3IL0",X1$="EC Image Library Functions"
0120 EOL$=$0D0A$; EOL$=ATH(STP(EC_PARM$(57,8),1)) ! SSP247556, remove REM
0127 HTML_TEXT$="Content-type: text/html"
0140 DIM MESS_INFO$[20]
0150 DIM IL1$(320)
0200 REM "Default values here
0210 DISP_DATE_FMT$="MM/DD/YYYY" ! Default date format 
0500 REM "Open files
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O IL1... 13O ZZPARM"
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0540 %HAS_IMAGE_LIB=0; IF FN%HAS_MODULE("IL") THEN FIND (Z[13],KEY=%C$+"I/L",DOM=*NEXT); %HAS_IMAGE_LIB=1
1000 REM "Process DATA array
1010 FOR I=1 TO NUM_ENTRIES
1015 TAG$=DATA$[I,0],VALUE$=DATA$[I,1]
1020 T_IN=POS("."=TAG$); IF T_IN THEN TAG_PARM$=MID(TAG$,T_IN+1),TAG$=MID(TAG$,1,T_IN-1) ELSE TAG_PARM$=""
1025 SWITCH TAG$ ! 1185 end switch
1030 CASE "action"; ACTION$=VALUE$; BREAK ! Valid actions: A - Add new IL1 record, V - View IL1 record 
1035 CASE "template"; TEMPLATE$=VALUE$; BREAK
1040 CASE "image_key"; IL1$(1,5)=VALUE$; BREAK
1045 CASE "image_type"; IL1$(6,2)=VALUE$; BREAK
1055 CASE "image_ref1"; IL1$(8,10)=VALUE$; BREAK
1065 CASE "image_ref2"; IL1$(213,30)=VALUE$; BREAK
1075 CASE "image_location"; IL1$(53,160)=VALUE$; BREAK
1080 CASE "version_label"; IL1$(243,20)=VALUE$; BREAK ! 211234
1110 CASE "use_date_format"; DISP_DATE_FMT$=VALUE$; BREAK
1180 DEFAULT ! VIA TAG$=VALUE$; BREAK ! Set to same name variable as TAG$
1185 END SWITCH ! 1050
1190 NEXT I
1195 DIM BYBINFO$[5]; BYBINFO$[1]=PGN,BYBINFO$[2]="229001.20090424",BYBINFO$[3]=TEMPLATE$,BYBOPTIONS$=""
1205 IF NOT(%HAS_IMAGE_LIB) THEN ERR_CODE=-99,ERR_MSG$="Image library module is not activated"; GOTO 1500
1206 IF EC$[2](1067,1)<>"Y" THEN ERR_CODE=-98,ERR_MSG$="No Image library access for buyer:"+EC$[2](1,15); GOTO 1500
1210 ! Process submitted Image library data
1220 IF POS(ACTION$="AV")=0 THEN ERR_CODE=-10,ERR_MSG$="Invalid action code:"+ACTION$
1230 IF %HAS_IMAGE_LIB THEN IF NOT(%IL_CODES) THEN PERFORM "ILGFUN;BUILD_DOC_LIST"
1240 GOSUB GET_IL0; IF ACTION$="A" THEN GOSUB VALIDATE_IL1; IF ERR_CODE=0 THEN GOSUB WRITE_IL1
1250 GOSUB GET_IL1; IF ACTION$="V" THEN GOSUB GET_IL0
1251 IF ERR_CODE=0 THEN ERR_MSG$="Success"
1260 ! 
1503 ! status and status_msg are given as examples
1504 IF NOT(NUL(%BASIC2$)) THEN GOTO TEMPLATE_DONE
1505 CLOSE (100); OPEN (100,OPT="TEXT",ERR=9000)TEMPLATE$
1510 READ (100,END=2950)LINE$; LINE$=FNBYB$(LINE$)
1515 CHKTAG=1
1520 WHILE CHKTAG ! 2800 wend
1522 P1=MSK(LINE$,"\?tf\?[^?]*\?"); IF P1=0 THEN CHKTAG=0; BREAK
1530 TAG$=LINE$(P1,MSL); LINE1$=LINE$(1,P1-1),LINE2$=MID(LINE$,P1+MSL)
1600 SWITCH TAG$ ! 2790 end switch
1610 CASE "?tf?status_code?"; NN=FNN(STR(ERR_CODE)); BREAK
1620 CASE "?tf?status_msg?"; NN=FNN(ERR_MSG$); BREAK
1630 CASE "?tf?image_key?"; NN=FNN(STP(IL1$(1,5),2)); BREAK
1631 CASE "?tf?image_type?"; NN=FNN(STP(IL1$(6,2),2)); BREAK
1632 CASE "?tf?image_ref1?"; NN=FNN(STP(IL1$(8,10),2)); BREAK
1633 CASE "?tf?image_ref2?"; NN=FNN(STP(IL1$(213,30),2)); BREAK
1634 CASE "?tf?image_location?"; NN=FNN(STP(IL1$(53,160),2)); BREAK
1635 CASE "?tf?created_date?"; NN=FNN(STP(IL1$(38,6),2)); BREAK
1636 CASE "?tf?created_time?"; NN=FNN(STP(IL1$(44,6),2)); BREAK
1637 CASE "?tf?created_by?"; NN=FNN(STP(IL1$(50,3),2)); BREAK
1638 CASE "?tf?version_label?"; NN=FNN(STP(IL1$(243,20),2)); BREAK ! 211234
1647 CASE "?tf?created_date_formatted?"; NN=FNN(FN%FMT_TFDATE$(IL1$(38,6),DISP_DATE_FMT$)); BREAK
1650 CASE "?tf?created_time_formatted?"; NN=FNN(STR(IL1$(44,6):"XX:XX:XX")); BREAK
1660 CASE "?tf?image_type_desc?"; NN=FNN(IL0_DESC$); BREAK
2785 DEFAULT ; LINE$=LINE1$+LINE2$ ! didn't match existing tag, so remove it
2790 END SWITCH ! 1600
2800 WEND ! 1520
2810 PRINT (OUTPUT)LINE$,EOL$, ! SSP274556, add ,EOL$, and remove check for ""
2900 GOTO 1510; REM next read
2990 TEMPLATE_DONE:GOTO 9900
5100 ADD_MSG_TO_LOG:! Add message to log file
5105 ! Add other info as needed here
5110 MOUT$="SESSION ID|"+EC$[1](1,7)+"|FILE|"+%WEBEC_FILE_NAME$+"|"
5135 MX=FN%_LOG_MESSAGE("MESG",MOUT$)
5145 RETURN 
5149 ! 
5150 REM "Validation for IL1
5160 VALIDATE_IL1:
5161 ! IF NOT(%HAS_IMAGE_LIB) THEN ERR_CODE=-99,ERR_MSG$="Image library module is not activated"; GOTO *RETURN
5170 IF NUL(IL1$(6,2)) THEN ERR_CODE=-11,ERR_MSG$="Missing image type"; GOTO *RETURN
5180 IF NUL(IL0_CODE$) THEN ERR_CODE=-12,ERR_MSG$="No such image type:"+IL1$(6,2); GOTO *RETURN
5190 IF NUL(IL1$(8,10)) AND NUL(IL1$(213,30)) THEN ERR_CODE=-13,ERR_MSG$="Missing image references 1 and 2"; GOTO *RETURN
5200 IF NUL(IL1$(53,160)) THEN ERR_CODE=-14,ERR_MSG$="Missing image path"; GOTO *RETURN
5210 ERR_CODE=0
5245 RETURN 
5249 ! 
5250 REM "Write IL1 record
5260 WRITE_IL1:
5270 CALL "ILGFUN;ADD_IMAGE",ERR=ERR_WRITE_IL1,Z[1],IL1KEY$,IL1$(6,2),IL1$(8,10),IL1$(213,30),IL1$(53,160),IL1$(243,20) ! SSP247490
5280 IL1$(1,5)=IL1KEY$
5290 RETURN 
5300 ERR_WRITE_IL1: ERR_CODE=-15,TERR=ERR,ERR_MSG$="Error on IL1 write: "+MSG(TERR); GOTO *RETURN
5310 ! 
5320 GET_IL0:
5330 FIND (%IL_CODES,KEY=IL1$(6,2),DOM=*NEXT)IL0_CODE$,IL0_DESC$,IL0_DIR$,IL0_FMT$,IL0_SCAN$,IL0_ONLY_SAVE_LAST$,IL0_ACCESS$,IL0_ADD_FMT_CODE$
5340 RETURN 
5350 ! 
5360 GET_IL1:
5365 IF NUL(IL1$(1,5)) THEN GOTO 5385
5370 FIND (Z[1],KEY=IL1$(1,5),DOM=5386)IL1$
5380 RETURN 
5385 ERR_CODE=-16,ERR_MSG$="Missing image key"; GOTO *RETURN
5386 ERR_CODE=-17,ERR_MSG$="Record not found"; GOTO *RETURN
5390 ! 
8800 DEF FNBYB$(LOCAL DATA$)
8801 ! Send DATA$ through EC3BYB
8820 CALL "EC3BYB",ERR=*NEXT,X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,DATA$,BYBINFO${ALL},BYBOPTIONS$,100 ! 227154
8840 RETURN DATA$
8845 END DEF
8849 ! *****************************************************
8850 DEF FNM$(LOCAL AMOUNT, LOCAL MASK$)
8855 ! if AMOUNT is 0 then return null string, else STR AMOUNT with MASK and strip leading and trailing blanks
8860 IF AMOUNT=0 THEN RETURN "" ELSE RETURN STP(STR(AMOUNT:MASK$),2)
8895 END DEF
8899 ! **********************************************************
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8920 DEF FNN(LOCAL DATA$)
8925 LINE$=LINE1$+DATA$+LINE2$
8930 RETURN 0
8949 ! *****************************
8950 ! FND$ - Standard date routine
8955 DEF FND$(LOCAL DATE_IN$, LOCAL SPECIAL_FORMAT$)
8960 LOCAL DATE_OUT$
8965 IF NUL(DATE_IN$) THEN DATE_OUT$="" ELSE IF NOT(NUL(SPECIAL_FORMAT$)) THEN DATE_OUT$=FN%FMT_TFDATE$(DATE_IN$,SPECIAL_FORMAT$) ELSE CALL "ZZDISP","DX",DATE_IN$,"",X3$,DATE_OUT$,"",0,0,X4$
8970 RETURN DATE_OUT$
8975 END DEF
9000 REM "ERROR PROCESSING
9002 J=ERR,K=TCB(5); SETERR 0000; ENDTRACE 
9010 LAST_WEBEC_ERR$="ERR: "+STR(J)+" AT: "+STR(K); LAST_WEBEC_ERR$=GBL("LAST_WEBEC_ERR",LAST_WEBEC_ERR$); REM "131928
9090 GOTO 9900
9900 REM "End
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9950 EXIT 
9999 END 
56002 REM "227154-Modify EC3BYB to support a condition tag : ?tf?if?[expr]
56003 REM "229001-Create a new eCommerce program that will create an IL1
56004 REM "211234-Designate labels for up to 3 versions of Image Type
56006 REM "247490-EC3IL0; version_label input/output tags added with SSP211234
56008 REM "247556-EC3IL0 is not adding the EOL like other EC programs.        
