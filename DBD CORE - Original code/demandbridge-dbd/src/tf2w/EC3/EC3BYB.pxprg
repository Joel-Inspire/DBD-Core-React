0010 REM "Buyer information extractor <EC3BYB>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 05/04/20 - 11.378888 - dmm - SSP# 307280
0037 REM "307280-DBD-89; Output tag error_message in EC3SRV                  
0040 REM "Copyright 2020 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "other$[X] = meta tag information (see routine @1200)
0051 REM "OTHER$[1]=program   [2]=version  [3]=template
0052 REM "other meta pulled from EC$[X]
0053 REM "buyer / oe warehouses / server / server directory
0055 REM "OPTIONS$ = not currently in use
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,LINE$,OTHER${ALL},OPTIONS$,TEMPLATE,ERR=*NEXT
0091 REM "LINE$ - passed in / processed / return populated with any applicable buyer information
0100 SETERR 9000
0110 X0$="EC3BYB",X1$="Buyer info extractor"
0120 EOL$=$0D0A$; EOL$=ATH(STP(EC_PARM$(57,8),1))
0130 DISP_SUBTOTAL=0,DISP_SHIPPING=0,DISP_TOTAL=0 ! WO215367
1000 REM "Process DATA array
1001 GOTO 1100
1005 DATA_TAGS$="001<template>002<user>003<password>004<maxrows>005<position_item>006<direction>007<session_id>"
1010 FOR I=1 TO NUM_ENTRIES
1015 TAG$=DATA$[I,0],VALUE$=DATA$[I,1]
1018 DPOS=POS("<"+TAG$+">"=DATA_TAGS$); IF DPOS=0 THEN GOTO 1090 ELSE DINDEX=NUM(DATA_TAGS$(DPOS-3,3),ERR=1090)
1019 ON DINDEX GOTO 1090,1021,1022,1023,1024,1025,1026,1027,1090
1021 TEMPLATE$=VALUE$; GOTO 1090; REM "template
1022 USER$=VALUE$; GOSUB 3000; GOTO 1090; REM "user
1023 PASSWORD$=VALUE$; GOTO 1090; REM "password
1024 MAX_ROWS=NUM(VALUE$,ERR=1090); GOTO 1090; REM "maxrows
1025 POSITION_ITEM$=VALUE$; GOTO 1090; REM "position_item
1026 IF VALUE$="backward" THEN DIRECTION$="B"; GOTO 1090; REM "direction, only set if it is 'backward'
1027 SESSION_ID$=VALUE$; GOTO 1090; REM "Get session id
1090 NEXT I
1100 REM "Process line for buyer tags
1105 DATA_TAGS$="001<buyer_id>002<buyer_name>003<buyer_password>004<buyer_session_expires>005<cust_code>006<cust_loc>007<buyer_web_code>008<buyer_enter_orders>009<buyer_purchase_orders>010<buyer_approve_orders>011<buyer_admin_buyers>012<buyer_loc_restrict>013<buyer_oe_types>014<buyer_os_types>015<buyer_po_types>016<buyer_approve_types>017<buyer_email>018<buyer_pa_email>019<buyer_csr_email>020<buyer_sls_email>021<buyer_usage_oelimit>022<buyer_secure_forms>023<buyer_cust_fms_group>024<buyer_email_actions>025<buyer_pa_email_actions>026<buyer_csr_email_actions>027<buyer_sls_email_actions>028<session_id>029<cust_name>030<cust_loc_name>031<session_expired>032<session_valid_timeout>033<worksheet_number>034<ec_company>035<date>036<time>037<cust_po_reqd>038<cust_fob_code>039<cust_fob_desc>040<cust_howtoship_code>041<cust_howtoship_desc>042<cust_inside_delivery>043<cust_shipvia_code>044<cust_shipvia_desc>045<cust_fms_year>046<cust_fms_period>047<cust_fms_reduce_onhand>048<cust_whse>049<st_location>050<st_recvg_dept>051<st_fms_method>052<st_fob_code>053<st_fob_desc>054<st_howtoship_code>055<st_howtoship_desc>056<st_inside_delivery>057<st_shipvia_code>058<st_shipvia_desc>059<tf_cust_code_only>060<tf_cust_div_only>061<catalog_id>062<image_path>063<cust_logo>064<cust_code_formatted>"; REM "Don't add new fields here, see 1106
1106 DATA_TAGS$=DATA_TAGS$+"065<cust_logo_link>066<buyer_contact_phone>067<buyer_contact_name>068<buyer_contact_fax>069<buyer_contact_email>070<company_logo>071<company_logo_image_source>072<base_page_ref>073<cgi_command>074<shipto_ddlist>075<last_webec_err>076<call_prog>077<shipto_ddlist_all>078<comp_logo>079<nof_image_link>080<max_oss_days>081<default_oss_days>082<os_order_type_list>083<item_code_list>084<item_desc_list>085<base_page_refs>086<include>087<has_tops>088<has_vp>089<catalog_count>090<has_multiple_catalogs>091<pa_buyer>092<pa_approver>093<display_ecp_link>094<display_admin_link>095<composite_pdf>096<coupon_available>097<save_wk_available>098<one_time_key>099<color_scheme>100<timestamp_zulu>101<timestamp_local>102<num_wk_items>103<image_view>104<ec_image_path>105<comp_logo_file>106<cust_logo_file>107<stat_tracking_acct_num>108<ecp_template_buyer>"; REM "Add new fields to 1107
1107 DATA_TAGS$+="109<sage_prod_code>110<sage_item_type>111<sage_3p_id>112<has_sage>113<browser_form_post>114<buyer_cookie>115<to_domain>116<to_identity>117<from_domain>118<from_identity>119<eligible_coupon_available>120<buyer_cc_required>121<num_items>122<disp_subtotal>123<disp_shipping>124<disp_total>125<use_primary_functions>126<date_yyyymmdd>127<budget_enabled>128<budget_total>129<budget_shipped>130<budget_remaining>131<allow_maintain_budget>132<restrict_reports>133<show_my_acct>134<if>135<endif>136<wms_r_enabled>137<wms_b_enabled>138<wms_a_enabled>139<wms_p_enabled>140<wms_i_enabled>141<wms_not_enabled>142<test_buyer>143<sage_app_prod_code>144<sage_app_spec_chg>145<report_type>146<cc_coupon_allowed>147<timestamp_cxml>148<dont_show_price>149<total_carton_weight>150<zip_code_origin>151<budget_active_catalog_shipped>152<budget_catalog_start>153<active_shipto>154<budget_open>155<user_defined_1>156<user_defined_2>157<user_defined_3>158<user_defined_4>159<user_defined_5>160<user_defined_6>161<user_defined_7>162<user_defined_8>163<user_defined_9>164<user_defined_10>165<timestamp_random>166<random>167<ec_counter_curr_val>168<ec_counter_next_val>169<date_time_custom>"
1108 DATA_TAGS$+="170<pa_is_edit_allowed>171<pa_edit_mode>172<context_code>173<flowpoint_url>174<flowpoint_token>175<sid>"
1200 REM "insertion of meta tags
1210 CKLINE$=UCS(LINE$),HEADPOS=POS("<HEAD>"=CKLINE$); IF HEADPOS=0 THEN GOTO 1300; REM "135009
1212 TEMPLINE$=LINE$(1,HEADPOS+5)+EOL$
1213 TEMPLINE$=TEMPLINE$+"<meta name="+QUO+"template"+QUO+" content="+QUO+OTHER$[3]+QUO+">"+EOL$ ! Ray Alen. SSP301389, was line 1222, changed to 1213
1215 IF %META_MODE THEN GOTO 1250 ! Ray Alen. SSP301389
1220 TEMPLINE$=TEMPLINE$+"<meta name="+QUO+"webprogram"+QUO+" content="+QUO+OTHER$[1]+QUO+">"+EOL$
1221 TEMPLINE$=TEMPLINE$+"<meta name="+QUO+"pgmversion"+QUO+" content="+QUO+OTHER$[2]+QUO+">"+EOL$
1223 TEMPLINE$=TEMPLINE$+"<meta name="+QUO+"byr"+QUO+" content="+QUO+EC$[1](8,15)+QUO+">"+EOL$
1224 TEMPLINE$=TEMPLINE$+"<meta name="+QUO+"tfwebserver"+QUO+" content="+QUO+EC$[14](1,15)+QUO+">"+EOL$
1225 TEMPLINE$=TEMPLINE$+"<meta name="+QUO+"tfserverdir"+QUO+" content="+QUO+EC$[14](56,60)+QUO+">"+EOL$
1226 TEMPLINE$=TEMPLINE$+"<meta name="+QUO+"tfoewhses"+QUO+" content="+QUO+EC$[10]+QUO+">"
1227 TEMPLINE$=TEMPLINE$+"<meta name="+QUO+"current_worksheet"+QUO+" content="+QUO+EC$[1](97,10)+QUO+">"
1250 IF LEN(LINE$)>HEADPOS+5 THEN TEMPLINE$=TEMPLINE$+LINE$(HEADPOS+6)
1255 LINE$=TEMPLINE$
1300 ! SSP307280-DBD-89 - add to count of ?ec3srv? tags in EC$[19]
1305 GOSUB COUNT_EC3SRV_TAGS ! SSP307280-DBD-89
1500 REM "process tags
1510 START_INDEX=1
1515 INDEX=POS("?tf?"=LINE$(START_INDEX)); IF INDEX=0 THEN GOTO 1600; REM "exit when no more tags found
1520 END_INDEX=POS("?"=LINE$(START_INDEX+INDEX+3)),TAG$=LINE$(START_INDEX+INDEX+3,END_INDEX-1),LINE1$=LINE$(1,START_INDEX+INDEX-2),LINE2$=LINE$(START_INDEX+INDEX+END_INDEX+3),LPOS=POS("<"+TAG$+">"=DATA_TAGS$); IF LPOS=0 THEN LINDEX=0 ELSE LINDEX=NUM(DATA_TAGS$(LPOS-3,3))
1530 GOSUB 2000; GOTO 1515
1995 GOTO 9900
1999 REM "Get tag value & substitute
2000 ON LINDEX GOTO 2090,2001,2002,2003,2004,2005,2006,2007,2008,2009,2010,2011,2012,2013,2014,2015,2016,2017,2018,2019,2020,2021,2022,2023,2024,2025,2026,2027,2028,2029,2030,2031,2032,2033,2034,2035,2036,2037,2038,2039,2040,2041,2042,2043,2044,2045,2046,2047,2048,2049,2050,2051,2052,2053,2054,2055,2056,2057,2058,2059,2060,2061,2062,2063,2064,2065,2066,2067,2068,2069,2070,2071,2072,2073,2074,2075,2076,2077,2078,2079,2080,2081,2082,2083,2084,2085,2086,2087,2088,2089,2300,2301,2302,2303,2304,2305,2306,2307,2308,2309,2310,2311,2312,2313,2314,2315,2316,2317,2318,2319,2320,2321,2322,2323,2324,2325,2326,2327,2328,2329,2330,2331,2332,2333,2334,2335,2336,2337,2338,2339,2340,2341,2342,2343,2344,2345,2346,2347,2348,2349,2350,2351,2352,2353,2354,2355,2356,2357,2358,2359,2360,2361,2362,2363,2364,2365,2366,2367,2368,2369,2370,2371,2372,2373,2374,2375,2376,2377,2378,2379,2380,2381,2382,2383,2384,2385,2090
2001 LINE$=LINE1$+STP(EC$[2](1,15),2)+LINE2$; GOTO 2095; REM "buyer id / 135041
2002 LINE$=LINE1$+EC$[2](16,35)+LINE2$; GOTO 2095; REM "buyer_name
2003 LINE$=LINE1$+EC$[2](65,15)+LINE2$; GOTO 2095; REM "buyer_password
2004 LINE$=LINE1$+EC$[2](80,3)+LINE2$; GOTO 2095; REM "buyer_session_expires
2005 LINE$=LINE1$+EC$[2](51,10)+LINE2$; GOTO 2095; REM "buyer_cust_code
2006 LINE$=LINE1$+EC$[2](61,4)+LINE2$; GOTO 2095; REM "buyer_cust_loc
2007 LINE$=LINE1$+STP(EC$[2](276,15))+LINE2$; GOTO 2095; REM "buyer_web_code
2008 LINE$=LINE1$+EC$[2](88,1)+LINE2$; GOTO 2095; REM "buyer_enter_orders
2009 LINE$=LINE1$+EC$[2](89,1)+LINE2$; GOTO 2095; REM "buyer_purchase_orders
2010 LINE$=LINE1$+EC$[2](90,1)+LINE2$; GOTO 2095; REM "buyer_approve_orders
2011 LINE$=LINE1$+EC$[2](91,1)+LINE2$; GOTO 2095; REM "buyer_admin_buyers
2012 LINE$=LINE1$+EC$[2](92,1)+LINE2$; GOTO 2095; REM "buyer_loc_restrict
2013 LINE$=LINE1$+EC$[2](93,4)+LINE2$; GOTO 2095; REM "buyer_oe_types
2014 LINE$=LINE1$+EC$[2](97,4)+LINE2$; GOTO 2095; REM "buyer_os_types
2015 LINE$=LINE1$+EC$[2](101,4)+LINE2$; GOTO 2095; REM "buyer_po_types
2016 LINE$=LINE1$+EC$[2](105,4)+LINE2$; GOTO 2095; REM "buyer_approve_types
2017 IF STP(EC$[2](109,60),3," ")="" THEN EMAIL$=STP(EC_PARM$(596,60),1) ELSE EMAIL$=STP(EC$[2](109,60),1) END_IF ; LINE$=LINE1$+EMAIL$+LINE2$; GOTO 2095; REM "buyer_email
2018 IF STP(EC$[2](169,60),3," ")="" THEN EMAIL$=STP(EC_PARM$(596,60),1) ELSE EMAIL$=STP(EC$[2](169,60),1) END_IF ; LINE$=LINE1$+EMAIL$+LINE2$; GOTO 2095; REM "buyer_pa_email  "
2019 IF STP(EC$[2](340,60),3," ")="" THEN EMAIL$=STP(EC_PARM$(596,60),1) ELSE EMAIL$=STP(EC$[2](340,60),1) END_IF ; LINE$=LINE1$+EMAIL$+LINE2$; GOTO 2095; REM "buyer_csr_email
2020 IF STP(EC$[2](400,60),3," ")="" THEN EMAIL$=STP(EC_PARM$(596,60),1) ELSE EMAIL$=STP(EC$[2](400,60),1) END_IF ; LINE$=LINE1$+EMAIL$+LINE2$; GOTO 2095; REM "buyer_sls_email
2021 LINE$=LINE1$+EC$[2](229,4)+LINE2$; GOTO 2095; REM "buyer_usage_oelimit
2022 LINE$=LINE1$+EC$[2](233,20)+EC$[2](898,40)+LINE2$; GOTO 2095; REM "buyer_secure_forms
2023 LINE$=LINE1$+EC$[2](253,3)+LINE2$; GOTO 2095; REM "buyer_cust_fms_group
2024 LINE$=LINE1$+EC$[2](256,10)+LINE2$; GOTO 2095; REM "buyer_email_actions
2025 LINE$=LINE1$+EC$[2](266,10)+LINE2$; GOTO 2095; REM "buyer_pa_email_actions
2026 LINE$=LINE1$+EC$[2](460,10)+LINE2$; GOTO 2095; REM "buyer_csr_email_actions
2027 LINE$=LINE1$+EC$[2](470,10)+LINE2$; GOTO 2095; REM "buyer_sls_email_action
2028 IF NOT(%SESSION_COOKIE_MODE) THEN IF EC_PARM$(1177,1)="Y" THEN LINE$=LINE1$+EC$[1](1,7)+LINE2$ ELSE LINE$=LINE1$+EC$[1](1213,64)+LINE2$; END_IF ELSE LINE$=LINE1$+LINE2$ END_IF ; GOTO 2095; REM "session_id  ! SSP212160 jdf; 303495 - Disable session display if cookie mode is enabled in EC server
2029 LINE$=LINE1$+EC$[1](23,35)+LINE2$; GOTO 2095; REM "cust_name
2030 LINE$=LINE1$+EC$[1](58,35)+LINE2$; GOTO 2095; REM "cust_loc_name
2031 LINE$=LINE1$+EC$[1](93,1)+LINE2$; GOTO 2095; REM "session_expired
2032 LINE$=LINE1$+EC$[1](94,3)+LINE2$; GOTO 2095; REM "session_valid_timeout
2033 LINE$=LINE1$+EC$[1](97,10)+LINE2$; GOTO 2095; REM "worksheet_number
2034 LINE$=LINE1$+STP(EC_PARM$(7,50),1)+LINE2$; GOTO 2095; REM "ec_company
2035 LINE$=LINE1$+EC$[4]+LINE2$; GOTO 2095; REM "date
2036 LINE$=LINE1$+EC$[5]+LINE2$; GOTO 2095; REM "time
2037 LINE$=LINE1$+EC$[1](107,1)+LINE2$; GOTO 2095; REM "cust_po_reqd
2038 LINE$=LINE1$+EC$[1](108,1)+LINE2$; GOTO 2095; REM "cust_fob_code
2039 LINE$=LINE1$+EC$[1](109,15)+LINE2$; GOTO 2095; REM "cust_fob_desc
2040 LINE$=LINE1$+EC$[1](124,1)+LINE2$; GOTO 2095; REM "cust_howtoship_code
2041 LINE$=LINE1$+EC$[1](125,15)+LINE2$; GOTO 2095; REM "cust_howtoship_desc
2042 LINE$=LINE1$+EC$[1](140,15)+LINE2$; GOTO 2095; REM "cust_inside_delivery
2043 LINE$=LINE1$+EC$[1](155,1)+LINE2$; GOTO 2095; REM "cust_shipvia_code
2044 LINE$=LINE1$+EC$[1](156,15)+LINE2$; GOTO 2095; REM "cust_shipvia_desc
2045 LINE$=LINE1$+EC$[1](171,4)+LINE2$; GOTO 2095; REM "cust_fms_year
2046 LINE$=LINE1$+EC$[1](175,2)+LINE2$; GOTO 2095; REM "cust_fms_period
2047 LINE$=LINE1$+EC$[1](177,1)+LINE2$; GOTO 2095; REM "cust_fms_reduce_onhand
2048 LINE$=LINE1$+EC$[1](178,4)+LINE2$; GOTO 2095; REM "cust_whse
2049 LINE$=LINE1$+EC$[1](182,9)+LINE2$; GOTO 2095; REM "st_location
2050 LINE$=LINE1$+EC$[1](191,20)+LINE2$; GOTO 2095; REM "st_recvg_dept
2051 LINE$=LINE1$+EC$[1](211,1)+LINE2$; GOTO 2095; REM "st_fms_method
2052 LINE$=LINE1$+EC$[1](212,1)+LINE2$; GOTO 2095; REM "st_fob_code
2053 LINE$=LINE1$+EC$[1](213,15)+LINE2$; GOTO 2095; REM "st_fob_desc
2054 LINE$=LINE1$+EC$[1](228,1)+LINE2$; GOTO 2095; REM "st_howtoship_code
2055 LINE$=LINE1$+EC$[1](229,15)+LINE2$; GOTO 2095; REM "st_howtoship_desc
2056 LINE$=LINE1$+EC$[1](244,15)+LINE2$; GOTO 2095; REM "st_inside_delivery
2057 LINE$=LINE1$+EC$[1](259,1)+LINE2$; GOTO 2095; REM "st_shipvia_code
2058 LINE$=LINE1$+EC$[1](260,15)+LINE2$; GOTO 2095; REM "st_shipvia_desc
2059 LINE$=LINE1$+STP(EC$[2](53,8),1)+LINE2$; GOTO 2095; REM "tf_cust_code_only - is internal TopForm cust_code without div. Used in Crystal Reports to access custcode field SSP292328 add STP
2060 LINE$=LINE1$+EC$[2](51,2)+LINE2$; GOTO 2095; REM "tf_cust_div_only - is internal TopForm cust_div without custcode. Used in Crystal Reports to access cust_div field
2061 LINE$=LINE1$+EC$[2](325,10)+LINE2$; GOTO 2095; REM "catalog_id  - default web catalog
2062 LINE$=LINE1$+EC$[8]+LINE2$; GOTO 2095; REM "image path (customers path if set - otherwise pulled from EC_PARM$)
2063 LINE$=LINE1$+STP(EC_PARM$(311,20),2)+QUO+EC$[8]+STP(EC$[7](11,30),2)+QUO+STP(EC_PARM$(331,60),2)+">"+STP(EC_PARM$(391,20),2)+LINE2$; GOTO 2095; REM "cust_logo
2064 GOSUB 7050; LINE$=LINE1$+STP(RESULT$,1)+LINE2$; GOTO 2095; REM "cust_code_formatted  ! SSP177906, added STP
2065 IF STP(EC$[7](11,30),3," ")="" THEN ITMP$=EC_PARM$(411,20) ELSE ITMP$=EC$[7](11,30) END_IF ; LINE$=LINE1$+EC$[8]+STP(ITMP$,2)+LINE2$; GOTO 2095; REM "cust_log_link - includes image path
2066 IF STP(EC$[7](234,14),1)="" THEN PTMP$="" ELSE PTMP$=EC$[7](234,14); IF EC$[7](244,4)=DIM(4) THEN CALL "ZZDISP","POX",EC$[7](234,10),"",X3$,PTMP$,"",0,0,X4$ ELSE CALL "ZZDISP","PX",EC$[7](234,14),"",X3$,PTMP$,"",0,0,X4$ END_IF ; LINE$=LINE1$+PTMP$+LINE2$; GOTO 2095; REM "buyer_contact_phone. SSP189327, if there is an extension then include it
2067 LINE$=LINE1$+STP(EC$[7](248,30),2)+LINE2$; GOTO 2095; REM "buyer_contact_name
2068 IF STP(EC$[7](278,14),1)="" THEN PTMP$="" ELSE CALL "ZZDISP","POX",EC$[7](278,10),"",X3$,PTMP$,"",0,0,X4$ END_IF ; LINE$=LINE1$+PTMP$+LINE2$; GOTO 2095; REM "buyer_contact_fax
2069 LINE$=LINE1$+STP(EC$[7](454,60),2)+LINE2$; GOTO 2095; REM "buyer_contact_email SSP#152743
2070 IF STP(EC$[7](354,30),2)="" THEN COMPANY_LOGO$=STP(EC_PARM$(951,30),2) ELSE COMPANY_LOGO$=STP(EC$[7](354,30),2) END_IF ; LINE$=LINE1$+STP(EC_PARM$(311,20),2)+QUO+EC$[8]+COMPANY_LOGO$+QUO+STP(EC_PARM$(331,60),2)+">"+STP(EC_PARM$(391,20),2)+LINE2$; GOTO 2095; REM "company logo
2071 IF STP(EC$[7](354,30),2)="" THEN COMPANY_LOGO$=STP(EC_PARM$(198,60),2)+STP(EC_PARM$(951,30),2) ELSE COMPANY_LOGO$=EC$[8]+STP(EC$[7](354,30),2) END_IF ; IF NUL(COMPANY_LOGO$) THEN COMPANY_LOGO$=EC$[8]+STP(EC_PARM$(411,20),2) END_IF ; LINE$=LINE1$+COMPANY_LOGO$+LINE2$; GOTO 2095; REM "company_logo_image_source, 144872->If normal logo use regular image path, else use customer image path 159960-> if both logos blank substitute nof image
2072 BPR$=STP(EC$[14](256,60),2); IF STP(MID(EC$[7],627,60),2)>"" THEN BPR$=STP(MID(EC$[7],627,60),2) END_IF ; LINE$=LINE1$+BPR$+LINE2$; GOTO 2095; REM "base_page_ref. SSP228104, support customer specific.
2073 CGI$=STP(EC$[14](316,60),2); IF STP(MID(EC$[7],687,60),2)>"" THEN CGI$=STP(MID(EC$[7],687,60),2) END_IF ; LINE$=LINE1$+CGI$+LINE2$; GOTO 2095; REM "cgi_command  (ie. cgi-bin/ec.pl). SSP228104, support customer specific.
2074 SHIPTO_DDL_ALL$="N"; GOSUB 6000; LINE$=LINE1$+LOC_LIST$+LINE2$; GOTO 2095; REM "shipto_ddlist
2075 GOSUB 7100; LINE$=LINE1$+LAST_WEBEC_ERR$+LINE2$; GOTO 2095; REM "last_webec_err
2076 GOSUB 7130; LINE$=LINE1$+CALL_PROG$+LINE2$; GOTO 2095; REM "call_prog
2077 SHIPTO_DDL_ALL$="Y"; GOSUB 6000; LINE$=LINE1$+LOC_LIST$+LINE2$; GOTO 2095; REM "shipto_ddlist_all
2078 LINE$=LINE1$+STP(EC_PARM$(198,60),2)+STP(EC_PARM$(951,30),2)+LINE2$; GOTO 2095; REM "comp_logo
2079 LINE$=LINE1$+STP(EC_PARM$(198,60),2)+STP(EC_PARM$(411,20),2)+LINE2$; GOTO 2095; REM "nof_image_link
2080 LINE$=LINE1$+STP(STR(NUM(EC_PARM$(797,3))),2)+LINE2$; GOTO 2095; REM "max_oss_days - cutoff days for order keyword search
2081 LINE$=LINE1$+STP(STR(NUM(EC_PARM$(800,3))),2)+LINE2$; GOTO 2095; REM "default_oss_days - specified days for order keyword search
2082 GOSUB 2150; LINE$=LINE1$+OSTYPE_LIST$+LINE2$; GOTO 2095; REM "os_order_type_list
2083 DDL_TYPE$="I",DDL_INDEX=1,DDL_ALL$="N",DDL_SELECTED$=""; GOSUB 2200; LINE$=LINE1$+DDLIST$[DDL_INDEX]+LINE2$; GOTO 2095; REM "item_code_lsit (catalog item code drop down list)
2084 DDL_TYPE$="D",DDL_INDEX=2,DDL_ALL$="N",DDL_SELECTED$=""; GOSUB 2200; LINE$=LINE1$+DDLIST$[DDL_INDEX]+LINE2$; GOTO 2095; REM "item_code_lsit (catalog item code drop down list)
2085 BPR$=STP(EC$[14](256,60),2); IF STP(MID(EC$[7],627,60),2)>"" THEN BPR$=STP(MID(EC$[7],627,60),2) END_IF ; IF LCS(MID(BPR$,1,4))="http" AND LCS(MID(BPR$,1,5))<>"https" THEN BPR$="https"+MID(BPR$,5) END_IF ; LINE$=LINE1$+BPR$+LINE2$; GOTO 2095; REM "base_page_refs. SSP228104, support customer specific.
2086 GOSUB GET_INCLUDE_FILE; LINE$=LINE1$+INCLUDE_DATA$+LINE2$; GOTO 2095 ! include
2087 HAS_TOPS$="N"; CALL "ZZ2PRP",ERR=*PROCEED,"OS",HAS_TOPS$,TOPS_DATE$; IF EC$[2](997,1)="Y" THEN HAS_TOPS$="N" END_IF ; LINE$=LINE1$+HAS_TOPS$+LINE2$; GOTO 2095 ! has_tops ssp#165905.  WO181248, check for Don't show on buyer
2088 HAD_VP$="N"; CALL "ZZ2PRP",ERR=*PROCEED,"VP",HAS_VP$,VP_DATE$; LINE$=LINE1$+HAS_VP$+LINE2$; GOTO 2095 ! has_vp ssp#166162
2089 CTMP$=MID(EC$[1],565,180); IF NUL(CTMP$) THEN CATALOG_COUNT$="0" ELSE CATALOG_COUNT$=STR(INT(LEN(STP(CTMP$,2))/10)+1) END_IF ; LINE$=LINE1$+CATALOG_COUNT$+LINE2$; GOTO 2095 ! catalog_count ssp#166162
2090 LINE$=LINE1$+"?tf?"+TAG$+"?"+LINE2$,START_INDEX=START_INDEX+INDEX+END_INDEX+3; GOTO 2095; REM "tag not found, Unlike other programs, we want to perserve this tag for the calling program, so rebuild line$with tag in place and move start_index, so we don't look again at this tag
2095 RETURN 
2150 REM "return order status types in drop down list
2152 DIM OSTYPEDESC$[3],OSTYPEVALUE$[3]; OSTYPECOUNT=0,OSTYPES$=STP(EC$[2](97,4),2); REM "get order status types from buyer record
2153 IF OSTYPES$="" THEN GOTO 2195
2158 IF POS("<!"=LINE1$)<>0 THEN LINE1$=LINE1$(1,POS("<!"=LINE1$)-1); REM "Remove start of comment if there
2159 OSTYPECOUNT=OSTYPECOUNT+1,OSTYPEDESC$[OSTYPECOUNT]="All Order Types",OSTYPEVALUE$[OSTYPECOUNT]=STP(EC$[2](97,4),2)
2160 IF POS("R"=OSTYPES$)<>0 OR POS("W"=OSTYPES$)<>0 THEN OSTYPECOUNT=OSTYPECOUNT+1,OSTYPEDESC$[OSTYPECOUNT]=STP(EC_PARM$(803,20),2),OSTYPEVALUE$[OSTYPECOUNT]="RW"
2162 IF POS("S"=OSTYPES$)<>0 THEN OSTYPECOUNT=OSTYPECOUNT+1,OSTYPEDESC$[OSTYPECOUNT]=STP(EC_PARM$(823,20),2),OSTYPEVALUE$[OSTYPECOUNT]="S"
2165 FOR PP=1 TO OSTYPECOUNT
2166 IF PP=1 THEN SELECTED$="<option selected" ELSE SELECTED$="<option"
2167 OSTYPE_LIST$=OSTYPE_LIST$+SELECTED$+" value="+QUO+OSTYPEVALUE$[PP]+QUO+">"+OSTYPEDESC$[PP]+"</option>"
2180 NEXT PP
2185 STMP=POS("</select>"=LINE1$,-1); IF STMP<>0 THEN OSTYPE_LIST$=OSTYPE_LIST$+"</select>"; IF STMP=1 THEN LINE1$="" ELSE LINE1$=LINE1$(1,STMP-1)
2186 IF LINE2$>"" THEN IF LINE2$(1,1)=">" THEN LINE2$=LINE2$(2); REM "if token was part of a comment, then remove the end of the comment.
2195 RETURN 
2200 REM "return EC3BYE drop down list (items/desc/future)
2205 IF POS("<!"=LINE1$)<>0 THEN LINE1$=LINE1$(1,POS("<!"=LINE1$)-1); REM "Remove start of comment if there
2220 STMP=POS("</select>"=LINE1$,-1); IF STMP<>0 THEN OSTYPE_LIST$=OSTYPE_LIST$+"</select>"; IF STMP=1 THEN LINE1$="" ELSE LINE1$=LINE1$(1,STMP-1)
2225 IF LINE2$>"" THEN IF LINE2$(1,1)=">" THEN LINE2$=LINE2$(2); REM "if token was part of a comment, then remove the end of the comment.
2230 DIM ALL$[10],SELECTED_VALUE$[10]; ALL$[DDL_INDEX]=DDL_ALL$,SELECTED_VALUE$[DDL_INDEX]=DDL_SELECTED$; CALL "EC3BYE",X3$,X4$,EC_PARM$,EC${ALL},DDL_TYPE$,ALL${ALL},SELECTED_VALUE${ALL},DDLIST${ALL},"","",""
2249 RETURN 
2250 REM "Return CATALOG_LIST$ as HTML for drop down list
2251 IF CATALOG_SEARCH_LIST$="Y" THEN IF TEXT_OPT THEN ADD_TEXT$=DIM(10,"*")+" ALL "+STP(EC_PARM$(734,20),2) ELSE ADD_TEXT$="<option selected value="+QUO+DIM(10,"*")+QUO+" SELECTED> ALL "+STP(EC_PARM$(734,20),2)+"s</option>"
2253 CATALOG_LIST$="",TEMP_PUBLISHED$=EC$[1](565,180),PUBLISHED$=""
2256 FOR PP=0 TO 17; IF TEMP_PUBLISHED$(PP*10+1,10)<>DIM(10) THEN PUBLISHED$=PUBLISHED$+TEMP_PUBLISHED$(PP*10+1,10) END_IF ; NEXT PP
2257 IF LEN(STP(PUBLISHED$,2))=0 THEN IF CATALOG_SEARCH_LIST$<>"Y" THEN GOTO 2295 ELSE CATALOG_LIST$=ADD_TEXT$; GOTO 2295
2258 IF POS("<!"=LINE1$)<>0 THEN LINE1$=LINE1$(1,POS("<!"=LINE1$)-1); REM "Remove start of comment if there
2259 FOR PP=0 TO LEN(PUBLISHED$)/10-1; CURRCATALOG$=PUBLISHED$(PP*10+1,10); IF CURRCATALOG$=DIM(10) THEN GOTO 2280
2262 IF TEXT_OPT THEN CATALOG_LIST$=CATALOG_LIST$+CURRCATALOG$+" "+EC$[1](745+PP*20,20)+EOL$; GOTO 2280; REM "137648
2263 IF CATALOG$="" AND CATALOG_SEARCH_LIST$="N" THEN IF PP=0 THEN SELECTED$="<option selected"; GOTO 2265 ELSE SELECTED$="<option"; GOTO 2265 ELSE SELECTED$="<option"; GOTO 2265
2264 IF CATALOG$<>"" THEN IF CURRCATALOG$=CATALOG$ THEN SELECTED$="<option selected" ELSE SELECTED$="<option"
2265 CATALOG_LIST$=CATALOG_LIST$+SELECTED$+" value="+QUO+CURRCATALOG$+QUO+">"+EC$[1](745+PP*20,20)+"</option>"; REM "if first item in list (pp=0) then set to be SELECTED as the initial value in the dd list
2280 NEXT PP
2281 IF CATALOG_SEARCH_LIST$="Y" THEN CATALOG_LIST$=ADD_TEXT$+CATALOG_LIST$
2285 STMP=POS("</select>"=LINE1$,-1); IF STMP<>0 THEN CATALOG_LIST$=CATALOG_LIST$+"</select>"; IF STMP=1 THEN LINE1$="" ELSE LINE1$=LINE1$(1,STMP-1)
2286 IF LINE2$>"" THEN IF LINE2$(1,1)=">" THEN LINE2$=LINE2$(2); REM "if token was part of a comment, then remove the end of the comment.
2295 RETURN 
2299 ! Continuation of tag processing
2300 CTMP$=MID(EC$[1],565,180),HAS_MULTIPLE_CATALOGS$="N"; IF LEN(STP(CTMP$,2))>10 THEN HAS_MULTIPLE_CATALOGS$="Y" END_IF ; LINE$=LINE1$+HAS_MULTIPLE_CATALOGS$+LINE2$; GOTO 2095 ! has_multiple_catalogs ssp#166162. SSP171980
2301 IF EC_PARM$(946,1)="Y" AND EC$[2](867,1)="N" AND EC$[2](868,1)="N" THEN PA_BUYER$="Y" ELSE PA_BUYER$="N" END_IF ; LINE$=LINE1$+PA_BUYER$+LINE2$; GOTO 2095 ! pa_buyer 166342
2302 IF EC_PARM$(946,1)="Y" AND EC$[2](867,1)="Y" THEN PA_APPROVER$="Y" ELSE PA_APPROVER$="N" END_IF ; LINE$=LINE1$+PA_APPROVER$+LINE2$; GOTO 2095 ! pa_approver 166342
2303 DISPLAY_ECP_LINK$="N"; IF EC$[7](600,1)="Y" AND EC$[2](995,1)<>"Y" THEN DISPLAY_ECP_LINK$="Y" END_IF ; LINE$=LINE1$+DISPLAY_ECP_LINK$+LINE2$; GOTO 2095 ! display_ecp_link, WO174468, check buyer in addition to customer
2304 DISPLAY_ADMIN_LINK$="N"; IF EC$[2](996,1)="Y" THEN DISPLAY_ADMIN_LINK$="Y" END_IF ; LINE$=LINE1$+DISPLAY_ADMIN_LINK$+LINE2$; GOTO 2095 ! display_admin_link, SSP177696
2305 COMPOSITE_PDF$="N"; IF EC_PARM$(1137,1)="Y" THEN COMPOSITE_PDF$="Y" END_IF ; LINE$=LINE1$+COMPOSITE_PDF$+LINE2$; GOTO 2095 ! composite_pdf, WO178946
2306 GOSUB CHECK_FOR_COUPON; LINE$=LINE1$+COUPON_AVAILABLE$+LINE2$; GOTO 2095 ! coupon_available, wo177733
2307 SAVE_WK_AVAILABLE$="N"; IF EC$[2](998,10)<>DIM(10) AND EC$[2](998,10)<>EC$[1](97,10) THEN SAVE_WK_AVAILABLE$="Y" END_IF ; LINE$=LINE1$+SAVE_WK_AVAILABLE$+LINE2$; GOTO 2095 ! save_wk_available, WO181395; SSP 197420 - current wk not same as saved wk
2308 GOSUB 7700; LINE$=LINE1$+ONE_TIME_KEY$+LINE2$; GOTO 2095 ! one_time_key, WO182735
2309 COLOR_SCHEME$=""; IF NOT(NUL(MID(EC$[2],1029,6))) THEN COLOR_SCHEME$=EC$[2](1029,6) ELSE IF NOT(NUL(MID(EC$[7],602,6))) THEN COLOR_SCHEME$=EC$[7](602,6) END_IF END_IF ; LINE$=LINE1$+COLOR_SCHEME$+LINE2$; GOTO 2095 ! color_scheme, SSP184884
2310 TS_Z$="null"; TS_Z$=DTE(JUL(DAY)+((TIM+(TCB(44)/3600))/24),*:"%Y-%Mz-%DzT%Hz:%mz:%szZ",ERR=*PROCEED); LINE$=LINE1$+TS_Z$+LINE2$; GOTO 2095 ! timestamp_zulu 186736
2311 TS_L$="null"; TS_L$=DTE(JUL(DAY)+(TIM/24),*:"%Y-%Mz-%DzT%Hz:%mz:%szZ",ERR=*PROCEED); LINE$=LINE1$+TS_L$+LINE2$; GOTO 2095 ! timestamp_local 186736
2312 CALL "EC3WSL",X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,NUM_WK_ITEMS,NUM_ITEMS,DISP_SUBTOTAL,TOTAL_CARTON_WEIGHT; LINE$=LINE1$+STR(NUM_WK_ITEMS)+LINE2$; GOTO 2095 ! WO189757, num_wk_items. ! WO215367, WO237271
2313 IMAGE_VIEW$="N"; IF EC$[2](1038,1)="Y" THEN IMAGE_VIEW$="Y" END_IF ; LINE$=LINE1$+IMAGE_VIEW$+LINE2$; GOTO 2095 ! WO189764, image_view
2314 LINE$=LINE1$+STP(EC_PARM$(198,60),2)+LINE2$; GOTO 2095 ! SSP193065, ec_image_path
2315 IF STP(EC$[7](354,30),2)="" THEN COMPANY_LOGO$=STP(EC_PARM$(951,30),2) ELSE COMPANY_LOGO$=STP(EC$[7](354,30),2) END_IF ; IF NUL(COMPANY_LOGO$) THEN COMPANY_LOGO$=STP(EC_PARM$(411,20),2) END_IF ; LINE$=LINE1$+COMPANY_LOGO$+LINE2$; GOTO 2095; REM "comp_logo_file ,SSP193065, SSP197968
2316 LINE$=LINE1$+STP(EC$[7](11,30),2)+LINE2$; GOTO 2095 ! SSP193065, cust_logo_file
2317 LINE$=LINE1$+STP(EC_PARM$(1153,20),2)+LINE2$; GOTO 2095 ! WO189560, stat_tracking_acct_num
2318 LINE$=LINE1$+STP(EC$[7](611,15),2)+LINE2$; GOTO 2095 ! WO204583, ecp_template_buyer
2319 IF NOT(SAGE_PARM) THEN GOSUB 6100; END_IF ; LINE$=LINE1$+STP(MID(SAGE_PARM$,10,3))+LINE2$; GOTO 2095 ! sage_prod_code
2320 IF NOT(SAGE_PARM) THEN GOSUB 6100; END_IF ; LINE$=LINE1$+STP(MID(SAGE_PARM$,13,1))+LINE2$; GOTO 2095 ! sage_item_type
2321 IF NOT(SAGE_PARM) THEN GOSUB 6100; END_IF ; LINE$=LINE1$+STP(MID(SAGE_PARM$,14,10))+LINE2$; GOTO 2095 ! sage_3p_id
2322 LINE$=LINE1$+EC$[7](626,1)+LINE2$; GOTO 2095 ! has_sage
2323 IF NOT(PUNCHOUT_HDR) THEN GOSUB GET_PUNCHOUT_HDR; END_IF ; LINE$=LINE1$+STP(EEA$(8,250),2)+LINE2$; GOTO 2095 ! browser_form_post
2324 IF NOT(PUNCHOUT_HDR) THEN GOSUB GET_PUNCHOUT_HDR; END_IF ; LINE$=LINE1$+STP(EEA$(258,100),2)+LINE2$; GOTO 2095 ! buyer_cookie
2325 IF NOT(PUNCHOUT_HDR) THEN GOSUB GET_PUNCHOUT_HDR; END_IF ; LINE$=LINE1$+STP(EEA$(358,30),2)+LINE2$; GOTO 2095 ! to_domain
2326 IF NOT(PUNCHOUT_HDR) THEN GOSUB GET_PUNCHOUT_HDR; END_IF ; LINE$=LINE1$+STP(EEA$(388,30),2)+LINE2$; GOTO 2095 ! to_identity
2327 IF NOT(PUNCHOUT_HDR) THEN GOSUB GET_PUNCHOUT_HDR; END_IF ; LINE$=LINE1$+STP(EEA$(418,30),2)+LINE2$; GOTO 2095 ! from_domain
2328 IF NOT(PUNCHOUT_HDR) THEN GOSUB GET_PUNCHOUT_HDR; END_IF ; LINE$=LINE1$+STP(EEA$(448,30),2)+LINE2$; GOTO 2095 ! from_identity
2329 GOSUB CHECK_FOR_ELIGIBLE_COUPON; LINE$=LINE1$+ELIGIBLE_COUPON$+LINE2$; GOTO 2095 ! eligible_coupon_available, WO214910
2330 BUYER_CC_REQD$="N"; IF EC$[2](254,1)="Y" THEN BUYER_CC_REQD$="Y" END_IF ; LINE$=LINE1$+BUYER_CC_REQD$+LINE2$; GOTO 2095; REM "buyer_cc_required SSP 219044
2331 CALL "EC3WSL",X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,NUM_WK_ITEMS,NUM_ITEMS,DISP_SUBTOTAL,TOTAL_CARTON_WEIGHT; LINE$=LINE1$+STR(NUM_ITEMS)+LINE2$; GOTO 2095 ! WO215367, num_items, WO237271
2332 CALL "EC3WSL",X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,NUM_WK_ITEMS,NUM_ITEMS,DISP_SUBTOTAL,TOTAL_CARTON_WEIGHT; IF EC$[2](322,1)="Y" THEN LINE$=LINE1$+LINE2$; GOTO 2095 ELSE LINE$=LINE1$+FN%HTML_ESC$(STP(STR(DISP_SUBTOTAL:"###,###.00-"),1))+LINE2$; GOTO 2095 ! WO215367, disp_subtotal, WO237271
2333 CALL "EC3WSH",X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,EC3WSH_ECD$,EC3WSH_ECD2$; IF EC$[2](322,1)="Y" THEN LINE$=LINE1$+LINE2$; GOTO 2095 ELSE GOSUB GET_DISP_SHIPPING; LINE$=LINE1$+FN%HTML_ESC$(STP(STR(DISP_SHIPPING:"###,###.00-"),1))+LINE2$; GOTO 2095 ! WO215367, disp_shipping
2334 DISP_TOTAL=DISP_SUBTOTAL+DISP_SHIPPING; IF EC$[2](322,1)="Y" THEN LINE$=LINE1$+LINE2$; GOTO 2095 ELSE LINE$=LINE1$+FN%HTML_ESC$(STP(STR(DISP_TOTAL:"###,###.00-"),1))+LINE2$; GOTO 2095 ! WO215367, disp_total
2335 UPF$="N",EEC_ACTION$="U"; CALL "EC3EEC",X3$,X4$,EC_PARM$,EC${ALL},EEC_ACTION$,"",UPF$,EC3EEC_OTHER$; LINE$=LINE1$+UPF$+LINE2$; GOTO 2095 ! WO215369, use_primary_functions
2336 YYYYMMDD$=DTE(0:"%Y-%Mz-%Dz"); LINE$=LINE1$+YYYYMMDD$+LINE2$; GOTO 2095 ! WO224137, date_yyyymmdd
2337 IF BUDGET_DONE$<>"Y" THEN GOSUB GET_BUDGET; END_IF ; LINE$=LINE1$+BUDGET_ENABLED$+LINE2$; GOTO 2095 ! WO 215368, budget_enabled
2338 IF BUDGET_DONE$<>"Y" THEN GOSUB GET_BUDGET; END_IF ; LINE$=LINE1$+STR(BUDGET_TOTAL:"###,##0.00-")+LINE2$; GOTO 2095 ! WO 215368, budget_total
2339 IF BUDGET_DONE$<>"Y" THEN GOSUB GET_BUDGET; END_IF ; LINE$=LINE1$+STR(BUDGET_SHIPPED:"###,##0.00-")+LINE2$; GOTO 2095 ! WO 215368, budget_shipped
2340 IF BUDGET_DONE$<>"Y" THEN GOSUB GET_BUDGET; END_IF ; LINE$=LINE1$+STR(BUDGET_REMAINING:"###,##0.00-")+LINE2$; GOTO 2095 ! WO 215368, budget_remaining
2341 AMB$="N"; IF EC$[2](1064,1)="Y" THEN AMB$="Y" END_IF ; LINE$=LINE1$+AMB$+LINE2$; GOTO 2095 ! SSP226479, allow_maintain_budget
2342 RR$="N"; IF EC$[2](1065,1)="Y" THEN RR$="Y" END_IF ; LINE$=LINE1$+RR$+LINE2$; GOTO 2095 ! SSP226479, restrict_reports
2343 SMA$="N"; IF EC$[2](1066,1)="Y" THEN SMA$="Y" END_IF ; LINE$=LINE1$+SMA$+LINE2$; GOTO 2095 ! SSP226479, show_my_acct
2344 IF TEMPLATE THEN GOSUB PROCESS_IF; LINE$=LINE1$+LINE2$; GOTO 2095 ! 227154, if
2345 LINE$=LINE1$+LINE2$; GOTO 2095 ! 227154, endif
2346 IF NOT(WMS_USER) THEN GOSUB GET_WMS_USER; END_IF ; YN$="N"; IF POS("R"=MID(RFK$,111,20)) THEN YN$="Y"; END_IF ; LINE$=LINE1$+YN$+LINE2$; GOTO 2095 ! wms_r_enabled, SSP 227739
2347 IF NOT(WMS_USER) THEN GOSUB GET_WMS_USER; END_IF ; YN$="N"; IF POS("B"=MID(RFK$,111,20)) THEN YN$="Y"; END_IF ; LINE$=LINE1$+YN$+LINE2$; GOTO 2095 ! wms_b_enabled, SSP 227739
2348 IF NOT(WMS_USER) THEN GOSUB GET_WMS_USER; END_IF ; YN$="N"; IF POS("A"=MID(RFK$,111,20)) THEN YN$="Y"; END_IF ; LINE$=LINE1$+YN$+LINE2$; GOTO 2095 ! wms_a_enabled, SSP 227739
2349 IF NOT(WMS_USER) THEN GOSUB GET_WMS_USER; END_IF ; YN$="N"; IF POS("P"=MID(RFK$,111,20)) THEN YN$="Y"; END_IF ; LINE$=LINE1$+YN$+LINE2$; GOTO 2095 ! wms_p_enabled, SSP 227739
2350 IF NOT(WMS_USER) THEN GOSUB GET_WMS_USER; END_IF ; YN$="N"; IF POS("I"=MID(RFK$,111,20)) THEN YN$="Y"; END_IF ; LINE$=LINE1$+YN$+LINE2$; GOTO 2095 ! wms_i_enabled, SSP 227739
2351 IF NOT(WMS_USER) THEN GOSUB GET_WMS_USER; END_IF ; YN$="N"; IF NUL(MID(RFK$,111,20)) THEN YN$="Y"; END_IF ; LINE$=LINE1$+YN$+LINE2$; GOTO 2095 ! ! wms_not_enabled, SSP 227739
2352 TEST_BUYER$="N"; IF EC$[2](338,1)="Y" THEN TEST_BUYER$="Y" END_IF ; LINE$=LINE1$+TEST_BUYER$+LINE2$; GOTO 2095 ! SSP231512, test_buyer
2353 IF NOT(SAGE_PARM) THEN GOSUB 6100; END_IF ; LINE$=LINE1$+STP(MID(SAGE_PARM$,24,3))+LINE2$; GOTO 2095 ! sage_app_prod_code, WO 232282
2354 IF NOT(SAGE_PARM) THEN GOSUB 6100; END_IF ; LINE$=LINE1$+STP(MID(SAGE_PARM$,27,10))+LINE2$; GOTO 2095 ! sage_app_spec_chg, WO 232282
2355 REPORT_TYPE$=""; IF EC$[2](1068,1)<>DIM(1) THEN REPORT_TYPE$=EC$[2](1068,1) END_IF ; LINE$=LINE1$+REPORT_TYPE$+LINE2$; GOTO 2095 ! report_type WO231698
2356 GOSUB GET_CUST_COUPON_PARAM; LINE$=LINE1$+EDX_ALLOW_CC_COUPONS$+LINE2$; GOTO 2095 ! cc_coupon_enabled - WO 233319
2357 TS_C$="null"; TS_C$=DTE(JUL(DAY)+(TIM/24),*:"%Y-%Mz-%DzT%Hz:%mz:%sz")+STR((TCB(44)/3600+TCB(45))*-100:"+00:00"); LINE$=LINE1$+TS_C$+LINE2$; GOTO 2095 ! WO233389, timestamp_cxml
2358 DSP$="N"; IF EC$[2](322,1)="Y" THEN DSP$="Y" END_IF ; LINE$=LINE1$+DSP$+LINE2$; GOTO 2095 ! dont_show_price, SSP2239417
2359 CALL "EC3WSL",X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,NUM_WK_ITEMS,NUM_ITEMS,DISP_SUBTOTAL,TOTAL_CARTON_WEIGHT; LINE$=LINE1$+FN%HTML_ESC$(STP(STR(TOTAL_CARTON_WEIGHT:"###,###.00-"),2))+LINE2$; GOTO 2095 ! WO237271, total_carton_weight
2360 IF EC$[2](1069,9)=DIM(9) THEN LINE$=LINE1$+LINE2$; GOTO 2095 ELSE LINE$=LINE1$+FN%HTML_ESC$(STP(FN%ZZDISP$(EC$[2](1069,9),"ZIP"),1))+LINE2$; GOTO 2095 ! WO237271, zip_code_origin (from buyer record)
2361 BC_CATALOG$=EC$[1](1105,10); GOSUB GET_BUDGET; LINE$=LINE1$+STR(BUDGET_CATALOG_SHIPPED:"###,##0.00-")+LINE2$; GOTO 2095 ! budget_active_catalog_shipped, 242686
2362 GOSUB BUDGET_CATALOG_LIST; LINE$=LINE1$+BC_SECTION$+LINE2$; GOTO 2095; REM "budget_catalog_start, 242686
2363 LINE$=LINE1$+EC$[1](1167,4)+LINE2$; GOTO 2095; REM "active_shipto 242686
2364 IF BUDGET_DONE$<>"Y" THEN GOSUB GET_BUDGET; END_IF ; LINE$=LINE1$+STR(BUDGET_OPEN_BAL:"###,##0.00-")+LINE2$; GOTO 2095 ! WO 249071, budget_open
2365 LINE$=LINE1$+FNUDF$("1")+LINE2$; GOTO 2095 ! WO249058, user_defined_1
2366 LINE$=LINE1$+FNUDF$("2")+LINE2$; GOTO 2095 ! WO249058, user_defined_2
2367 LINE$=LINE1$+FNUDF$("3")+LINE2$; GOTO 2095 ! WO249058, user_defined_3
2368 LINE$=LINE1$+FNUDF$("4")+LINE2$; GOTO 2095 ! WO249058, user_defined_4
2369 LINE$=LINE1$+FNUDF$("5")+LINE2$; GOTO 2095 ! WO249058, user_defined_5
2370 LINE$=LINE1$+FNUDF$("6")+LINE2$; GOTO 2095 ! WO249058, user_defined_6
2371 LINE$=LINE1$+FNUDF$("7")+LINE2$; GOTO 2095 ! WO249058, user_defined_7
2372 LINE$=LINE1$+FNUDF$("8")+LINE2$; GOTO 2095 ! WO249058, user_defined_8
2373 LINE$=LINE1$+FNUDF$("9")+LINE2$; GOTO 2095 ! WO249058, user_defined_9
2374 LINE$=LINE1$+FNUDF$("10")+LINE2$; GOTO 2095 ! WO249058, user_defined_10
2375 LINE$=LINE1$+FN%TIMRND$+LINE2$; GOTO 2095 ! SSP250181, timestamp_ramdom
2376 RND$=STR(INT(RND*1000000):"0000000"); LINE$=LINE1$+RND$+LINE2$; GOTO 2095 ! SSP250181, random
2377 GOSUB GET_COUNTER_CURR_VAL; LINE$=LINE1$+COUNTER$+LINE2$; GOTO 2095; REM "ec_counter_curr_val 251794
2378 GOSUB GET_COUNTER_NEXT_VAL; LINE$=LINE1$+COUNTER$+LINE2$; GOTO 2095; REM "ec_counter_next_val 251794
2379 GOSUB GET_DATE_TIME_CUSTOM; LINE$=LINE1$+DT_CUSTOM$+LINE2$; GOTO 2095; REM "date_time_custom 251794
2380 TMP_7_763_1$="N"; IF EC$[7](763,1)="Y" THEN TMP_7_763_1$="Y" END_IF ; LINE$=LINE1$+TMP_7_763_1$+LINE2$; GOTO 2095 ! SSP255476, pa_is_edit_allowed
2381 TMP_1_1318_1$="N"; IF EC$[1](1318,1)="Y" THEN TMP_1_1318_1$="Y" END_IF ; LINE$=LINE1$+TMP_1_1318_1$+LINE2$; GOTO 2095 ! SSP255476, pa_edit_mode
2382 LINE$=LINE1$+STP(EC$[1](1319,10),2)+LINE2$; GOTO 2095 ! SSP259474, context_code
2383 IF NOT(XMLOUT_PARM) THEN GOSUB 6300; END_IF ; LINE$=LINE1$+STP(MID(XMLOUT_PARM$,211,20))+LINE2$; GOTO 2095 ! SSP278673, flowpoint_url
2384 IF NOT(XMLOUT_PARM) THEN GOSUB 6300; END_IF ; LINE$=LINE1$+STP(MID(XMLOUT_PARM$,331,40))+LINE2$; GOTO 2095 ! SSP278673, flowpoint_token
2385 IF %SESSION_COOKIE_MODE THEN IF EC_PARM$(1177,1)="Y" THEN LINE$=LINE1$+EC$[1](1,7)+LINE2$ ELSE LINE$=LINE1$+EC$[1](1213,64)+LINE2$; END_IF ELSE LINE$=LINE1$+LINE2$ END_IF ; GOTO 2095 ! SSP307160, sid, session ID when in cookie mode
5110 MOUT$="SESSION ID|"+EC$[1](1,7)+"|FILE|"+%WEBEC_FILE_NAME$+"|WORKSHEET|"+ECD$(1,10)+"|LINE|"+ECE$(25,3)+"|MESSAGE NO|"+STR(MESSAGE_NO:"00000")+"|MESSAGE|"+MESSAGE$+"|"
5135 MX=FN%_LOG_MESSAGE("MESG",MOUT$)
6000 REM "Return LOC_LIST$ as HTML for drop down list
6005 LOC_LIST$="",SELECTED$=""; IF EC$[2](61,4)<>DIM(4) THEN SELECTED$=EC$[2](61,4)
6010 IF EC$[9]="" THEN PGMNAME$="EC3SCS" ELSE PGMNAME$=STP(EC$[9](57,6),2)
6015 CALL PGMNAME$,X3$,X4$,EC_PARM$,EC${ALL},ST_LIST$,SELECTED$,SHIPTO_DDL_ALL$,""
6020 LOC_LIST$=LOC_LIST$+ST_LIST$
6025 IF POS("<!"=LC1$,-1)<>0 THEN LC1$=LC1$(1,POS("<!"=LC1$,-1)-1); REM "If in comment remove the start of the comment
6030 STMP=POS("</select>"=LC1$,-1); IF STMP<>0 THEN LOC_LIST$=LOC_LIST$+"</select>"; IF STMP=1 THEN LC1$="" ELSE LC1$=LC1$(1,STMP-1)
6035 IF LC2$>"" THEN IF LC2$(1,1)=">" THEN LC2$=LC2$(2); REM "if token was part of a comment, then remove the end of the comment.
6040 RETURN 
6100 REM "Sage global tags
6105 DIM SAGE_PARM$(512),SAGE_PARM[2]
6106 IOL_SAGE:IOLIST SAGE_PARM$,SAGE_PARM[0],SAGE_PARM[1],SAGE_PARM[2]
6110 ZZPARM=FN%FFN("ZZPARM"); IF ZZPARM<0 THEN ZZPARM=HFN; OPEN (ZZPARM)"ZZPARM"
6120 READ (ZZPARM,KEY=X3$(9,3)+"SAGE",DOM=*NEXT)IOL=IOL_SAGE
6125 SAGE_PARM=1
6145 RETURN 
6149 ! 
6150 GET_PUNCHOUT_HDR:! SSP 210688
6155 PUNCHOUT_HDR=1
6160 DIM EEA$(678)
6165 EEA=FN%FFN("EEA"+%C$); IF EEA<0 THEN EEA=HFN; OPEN (EEA,ERR=GET_PH_DONE)"EEA"+%C$
6170 FIND (EEA,KEY=EC$[1](1,7),DOM=*NEXT)EEA$
6190 GET_PH_DONE:
6195 RETURN 
6199 ! 
6200 GET_WMS_USER:! SSP 227739, get WMS user data - RFK
6205 WMS_USER=1
6210 RFK=FN%FFN("RFK"+%C$); IF RFK<0 THEN RFK=HFN; OPEN (RFK,ERR=GET_WU_DONE)"RFK"+%C$
6220 FIND (RFK,KEY=EC$[2](1,15),DOM=*NEXT)RFK$
6240 GET_WU_DONE:
6245 RETURN 
6249 ! 
6250 GET_CATALOG:! 242686, get EDA catalog info
6260 IF NOT(EDA) THEN EDA=HFN; OPEN (EDA,ERR=GET_EDA_DONE)"EDA"+%C$
6270 FIND (EDA,KEY=NEXT_BUYER_CATALOG$,DOM=*NEXT)EDA$
6290 GET_EDA_DONE:
6295 RETURN 
6299 ! 
6300 ! SSP278673, XMLOUT parameters
6305 DIM XMLOUT_PARM$(512),XMLOUT_PARM[2]
6306 IOL_XMLOUT:IOLIST XMLOUT_PARM$,XMLOUT_PARM[0],XMLOUT_PARM[1],XMLOUT_PARM[2]
6310 ZZPARM=FN%FFN("ZZPARM"); IF ZZPARM<0 THEN ZZPARM=HFN; OPEN (ZZPARM)"ZZPARM"
6320 READ (ZZPARM,KEY=X3$(9,3)+"XMLOUT",DOM=*NEXT)IOL=IOL_XMLOUT
6325 XMLOUT_PARM=1
6345 RETURN 
6349 ! 
6350 BUDGET_CATALOG_LIST:
6352 BC_SECTION$="",BC_TAGS$="001<budget_catalog_shipped>002<budget_catalog_id>003<budget_catalog_desc>"
6353 BC_TEMPLATE$=LINE$
6355 IF POS("?tf?budget_catalog_end?"=LINE$)=0 THEN READ (TEMPLATE,END=*NEXT)LINE$; BC_TEMPLATE$=BC_TEMPLATE$+LINE$+EOL$; GOTO *SAME
6356 BSTART=POS("?tf?budget_catalog_start?"=BC_TEMPLATE$); IF BSTART<>0 THEN LINE1$=MID(BC_TEMPLATE$,1,BSTART-1),BC_TEMPLATE$=BC_TEMPLATE$(BSTART+25); REM "get rid of leading tag
6357 BSEND=POS("?tf?budget_catalog_end?"=BC_TEMPLATE$); IF BSEND<>0 THEN LINE2$=MID(BC_TEMPLATE$,BSEND),BC_TEMPLATE$=BC_TEMPLATE$(1,BSEND-1)
6360 FOR BC_CAT=565 TO 735 STEP 10
6361 IF NOT(NUL(EC$[1](BC_CAT,10))) THEN NEXT_BUYER_CATALOG$=EC$[1](BC_CAT,10) ELSE GOTO *CONTINUE
6364 BC$=BC_TEMPLATE$
6365 BC_INDEX=POS("?tf?"=BC$); IF BC_INDEX=0 THEN GOTO BC_DONE
6366 BSEND_INDEX=POS("?"=BC$(BC_INDEX+4)),BC_TAG$=BC$(BC_INDEX+4,BSEND_INDEX-1),BC1$=BC$(1,BC_INDEX-1),BC2$=BC$(BC_INDEX+BSEND_INDEX+4),BC_POS=POS("<"+BC_TAG$+">"=BC_TAGS$); IF BC_POS=0 THEN BC$=BC1$+BC2$,BC_INDEX=0 ELSE BC_INDEX=NUM(BC_TAGS$(BC_POS-3,3))
6370 ON BC_INDEX GOTO 6389,6371,6372,6373,6389
6371 BC_CATALOG$=NEXT_BUYER_CATALOG$; GOSUB GET_BUDGET; BC$=BC1$+STR(BUDGET_CATALOG_SHIPPED:"###,##0.00-")+BC2$; GOTO 6390; REM "budget_catalog_shipped
6372 BC$=BC1$+NEXT_BUYER_CATALOG$+BC2$; GOTO 6390; REM "budget_catalog_id
6373 GOSUB GET_CATALOG; BC$=BC1$+STP(MID(EDA$,11,30),2)+BC2$; GOTO 6390; REM "budget_catalog_desc
6389 BC$=BC1$+BC2$; GOTO 6390; REM "Not found
6390 GOTO 6365
6395 BC_DONE:
6396 BC_SECTION$+=BC$
6397 NEXT BC_CAT
6398 RETURN 
6399 ! 
7050 REM "account type
7052 CALL "ZZDISP","AX",EC$[2](51,10),"A/R",X3$,RESULT$,"",0,0,X4$
7080 RETURN 
7100 REM "Get last_webec_err
7105 LAST_WEBEC_ERR$=""; LAST_WEBEC_ERR$=GBL("LAST_WEBEC_ERR",ERR=7106)
7125 RETURN 
7130 REM "Get call_prog
7135 CALL_PROG$=""; CALL_PROG$=GBL("CALL_PROG",ERR=7136)
7145 RETURN 
7500 GET_INCLUDE_FILE:! found include tag now get name from brackets, find file, and set INCLUDE_DATA$ to it's contents
7505 INCLUDE_DATA$=""
7510 IF MSK(LINE2$,"\[[^\]]*\]")=1 THEN {
7515 INCLUDE_FILE$=LINE2$(2,MSL-2),LINE2$=MID(LINE2$,MSL+1)
7519 ! If we see a tag in the include file name, then call EC3BYB to add info
7520 IF POS("?tf?"=INCLUDE_FILE$) THEN CALL "EC3BYB",ERR=*NEXT,X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,INCLUDE_FILE$,OTHER${ALL},OPTIONS$,TEMPLATE
7529 ! If webcode then try with webcode first
7530 IF NOT(NUL(MID(EC$[2],276,15))) THEN IF_CHAN=HFN; OPEN (IF_CHAN,ERR=*NEXT)STP(EC$[14](56,60),1)+DLM+STP(EC$[2](276,15),1)+INCLUDE_FILE$; GOTO GET_INCLUDE_DATA
7540 IF_CHAN=HFN; OPEN (IF_CHAN,ERR=*RETURN)STP(EC$[14](56,60),1)+DLM+INCLUDE_FILE$
7545 GET_INCLUDE_DATA:READ (IF_CHAN,ERR=*NEXT,END=*NEXT)IF_IN$; INCLUDE_DATA$+=IF_IN$+EOL$; GOTO *SAME
7546 CLOSE (IF_CHAN,ERR=*NEXT)
7550 ! Remove the last EOL$ so we don't introduce an extra line feed
7555 INCLUDE_DATA$=MID(INCLUDE_DATA$,1,LEN(INCLUDE_DATA$)-(LEN(EOL$)))
7585  }
7595 RETURN 
7599 ! 
7600 CHECK_FOR_COUPON:! check to see if buyer has available coupon, 177733
7605 COUPON_AVAILABLE$="N"
7610 IF EC$[1](1318,1)="Y" THEN GOTO CA_DONE ! SSP255476, if pa_edit_mode then disable coupons
7620 SELECT *,REC=EDZ$ FROM "EDZ"+%C$,KNO=1 BEGIN EC$[2](1,15) END EC$[2](1,15)+$FE$ WHERE NUL(EDZ.WORKSHEET$)=1
7625 COUPON_AVAILABLE$="Y"
7630 GOTO *BREAK
7635 NEXT RECORD 
7690 CA_DONE:
7695 RETURN 
7699 ! **************************************************
7700 ! Call EC3KEY to generate one-time key for future login, WO182735
7705 ONE_TIME_KEY$=""
7710 ACTION$="W"
7715 BUYER$=EC$[2](1,15)
7720 STACK$=FN%GET_STACK$
7725 IF POS("EC3EM1"=STACK$)>0 THEN BUYER$=EC$[2](869,15) ! Buyer that needs on-time key is the approver for this buyer
7730 CALL "EC3KEY",X3$,X4$,ACTION$,BUYER$,ONE_TIME_KEY$,"",RETURN_CODE
7745 RETURN 
7749 ! 
7750 CHECK_FOR_ELIGIBLE_COUPON:! WO214910
7755 ELIGIBLE_COUPON$="N"
7757 IF EC$[1](1318,1)="Y" THEN RETURN ! SSP255476, if pa_edit_mode then disable coupons 
7760 CALL "EC3CPN;ELIGIBLE_COUPON_CHECK",ERR=*NEXT,X3$,X4$,EC${ALL},ELIGIBLE_COUPON$
7795 RETURN 
7799 ! 
7800 GET_DISP_SHIPPING:! WO215367
7805 DISP_SHIPPING=0
7810 DISP_SHIPPING=NUM(EC3WSH_ECD$(997,12),ERR=*NEXT)
7845 RETURN 
7849 ! 
7850 GET_BUDGET:! WO 215368
7855 BUDGET_DONE$="Y",BUDGET_CATALOG_SHIPPED=0
7860 CALL "FMGFTW;GET_BUDGET_STATS",X3$,X4$,EC$[2](51,10),EC$[1](1167,4),BUDGET_ENABLED$,BUDGET_AUDIT$,BUDGET_TOTAL,BUDGET_SHIPPED,BUDGET_OPEN_BAL,ERR_VAL,ERR_MSG$,BDGOPT$,BUDGET_CATALOG_SHIPPED,BC_CATALOG$ ! 242686,249071
7865 BUDGET_REMAINING=BUDGET_TOTAL-BUDGET_SHIPPED-BUDGET_OPEN_BAL
7895 RETURN 
7899 ! 
7900 PROCESS_IF:! Found "if" tag, get expression in [ ] brackets, evaluate and take action - 227154
7905 IF_EXPR$=""
7910 IF MSK(LINE2$,"\[[^\]]*\]")=1 THEN {
7915 IF_EXPR$=LINE2$(2,MSL-2),LINE2$=MID(LINE2$,MSL+1)
7920 IF POS("?tf?"=IF_EXPR$) THEN CALL "EC3BYB",ERR=*NEXT,X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,IF_EXPR$,OTHER${ALL},OPTIONS$,TEMPLATE
7925 EXPR_VAL=0; EXECUTE "IF "+IF_EXPR$+" THEN EXPR_VAL=1",ERR=*NEXT; GOTO 7930
7926 MX=FN%_LOG_MESSAGE("EXCP","INVALID EXPRESSION|"+IF_EXPR$+"|TEMPLATE|"+PTH(TEMPLATE)+"|STACK|"+FN%GET_STACK$+"|")
7930 IF EXPR_VAL THEN GOTO *RETURN ELSE GOTO *NEXT
7935 FOUND_ENDIF=POS("?tf?endif?"=LINE2$); IF FOUND_ENDIF=0 THEN READ (TEMPLATE,ERR=*RETURN,END=*RETURN)NEXT_LINE$; LINE2$=NEXT_LINE$; GOTO *SAME ELSE LINE2$=MID(LINE2$,FOUND_ENDIF+10)
7940  }
7945 RETURN 
7949 ! 
7950 GET_CUST_COUPON_PARAM:! WO 233319
7955 EDX_ALLOW_CC_COUPONS$="N"
7957 IF EC$[1](1318,1)="Y" THEN RETURN ! SSP255476, if pa_edit_mode then disable coupons 
7960 EDX=FN%FFN("EDX"+%C$); IF EDX<0 THEN EDX=HFN; OPEN (EDX,IOL=*,ERR=GET_CCP_DONE)"EDX"+%C$
7965 FIND (EDX,KEY=EC$[2](51,10),DOM=*NEXT)EDX$
7970 IF MID(EDX$,23,1)="Y" THEN EDX_ALLOW_CC_COUPONS$="Y"
7975 GET_CCP_DONE:
7995 RETURN 
7999 ! 
8000 GET_COUNTER_NEXT_VAL: CINCR_VAL=1; GOTO 8010
8005 GET_COUNTER_CURR_VAL: CINCR_VAL=0
8010 COUNTER$="?",CINDEX=0,COUNTER_MASK$=""
8015 IF MSK(LINE2$,"\[[^\]]*\]")=1 THEN {
8020 CINDEX$=LINE2$(2,MSL-2),LINE2$=MID(LINE2$,MSL+1)
8025 IF POS("?tf?"=CINDEX$) THEN CALL "EC3BYB",ERR=*NEXT,X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,CINDEX$,OTHER${ALL},OPTIONS$,TEMPLATE
8030 CINDEX=NUM(CINDEX$,ERR=*RETURN)
8035  }
8040 CALL "ECGXOB;GET_COUNTER_VALUE",ERR=*NEXT,CINDEX,COUNTER$,COUNTER_MASK$,CINCR_VAL
8095 RETURN 
8099 ! 
8100 ! Get custom date time string, given the desired format
8105 GET_DATE_TIME_CUSTOM:
8110 DT_CUSTOM$="",DT_MASK$="%Y-%Mz-%Dz %Hz:%mz:%sz" ! Default mask
8115 IF MSK(LINE2$,"\[[^\]]*\]")=1 THEN {
8120 DT_MASK$=LINE2$(2,MSL-2),LINE2$=MID(LINE2$,MSL+1)
8125 IF POS("?tf?"=DT_MASK$) THEN CALL "EC3BYB",ERR=*NEXT,X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,DT_MASK$,OTHER${ALL},OPTIONS$,TEMPLATE
8130  }
8140 DT_CUSTOM$=DTE(JUL(DAY)+(TIM/24),*:DT_MASK$)
8145 RETURN 
8149 ! 
8200 COUNT_EC3SRV_TAGS:! SSP307280-DBD-89
8205 COUNT_LINE$=LINE$
8210 COUNT_START_INDEX=1,EC3SRV_COUNT=0
8215 COUNT_INDEX=POS("?ec3srv?"=COUNT_LINE$(COUNT_START_INDEX)); IF COUNT_INDEX=0 THEN GOTO 8235
8220 COUNT_END_INDEX=POS("?"=COUNT_LINE$(COUNT_START_INDEX+COUNT_INDEX+7)),COUNT_TAG$=COUNT_LINE$(COUNT_START_INDEX+COUNT_INDEX+7,COUNT_END_INDEX-1),COUNT_LINE1$=COUNT_LINE$(1,COUNT_START_INDEX+COUNT_INDEX-2),COUNT_LINE2$=COUNT_LINE$(COUNT_START_INDEX+COUNT_INDEX+COUNT_END_INDEX+7),EC3SRV_COUNT+=1
8230 COUNT_LINE$=COUNT_LINE1$+"?ec3srv?"+COUNT_TAG$+"?"+COUNT_LINE2$,COUNT_START_INDEX=COUNT_START_INDEX+COUNT_INDEX+COUNT_END_INDEX+7; GOTO 8215
8235 EC$[19]=STR(NUM(EC$[19])+EC3SRV_COUNT)
8240 RETURN 
8245 ! 
8910 DEF FND$(Z9$)=Z9$(1*2+1,2)+"/"+Z9$(7-1*2,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8950 DEF FNUDF$(LOCAL UDF_NUM$)
8960 LOCAL UDF_NUM; UDF_NUM=NUM(UDF_NUM$,ERR=*NEXT); GOTO GET_UDF_DATA
8965 UDF_DATA$=""; GOTO *RETURN
8970 GET_UDF_DATA: UDF_DATA$=EC$[2](1078+UDF_NUM,1)
8975 IF UDF_DATA$=DIM(1) THEN UDF_DATA$=EC$[7](750+UDF_NUM,1)
8980 IF UDF_DATA$=DIM(1) THEN UDF_DATA$=""
8990 RETURN UDF_DATA$
8995 END DEF
8999 ! 
9000 REM "ERROR PROCESSING
9002 J=ERR,K=TCB(5); SETERR 0000; ENDTRACE 
9010 LAST_WEBEC_ERR$="ERR: "+STR(J)+" AT: "+STR(K)+" IN: EC3BYB"; LAST_WEBEC_ERR$=GBL("LAST_WEBEC_ERR",LAST_WEBEC_ERR$); REM "131928
9090 GOTO 9900
9900 REM "End
9950 EXIT 
9999 END 
56000 ! Program changes starting 11/3/05
56010 REM "189327-Using the tag for buyer contact phone, but the extension
56015 REM "189757-WebEC global tag that displays the number of worksheet lines
56020 REM "189764-Buyer parameter to show product catalog in new style with
56025 REM "193065-Ned needs tags for image paths
56030 REM "189560-Integrate WebEC pages with Onestat web analytics
56035 REM "197420-Bug in Webec "save shopping cart"
56040 REM "197968-WebEC tag for company logo at customer level not overriding
56045 REM "192407-Buyer needs 20 additional secure codes.
56047 REM "200279-Buyer web code returns wrong value ; issue with EC3BYB
56049 REM "201193-Questions about security of Boars Head EC Promo site.
56051 REM "206511-Relogin page showing up without include files being
56053 REM "204583-EC Promo Template Buyer
56055 REM "208674-Sage On Line - Webec Interface 
56056 REM "210688-Modify WebEC to support Peoplesoft punchout
56058 REM "212160-Webec is down.  Cannot use 64 character session id          
56060 REM "214910-Additional to WO214047; Add a tag that will display when 
56061 REM "219044-New HTML Pages/Folder for CC Only Billing                   
56064 REM "215367-EC shipping amt by customer, % of sale, or flat amt plus amt
56066 REM "215369-Primary function by customer, can be up to three levels,
56068 REM "224137-Capture Four51 info by order, retrieve later for ShipNotice
56069 REM "215368-Create Monthly Budget feature by customer location.
56072 ! 226479-Restrict reports, show my account, allow budget maintenance
56073 REM "227154-Support for a condition tag in EC : ?tf?if?[ expr ]
56076 REM "228104-Ability to have a custom base page ref and cgi command for
56077 REM "227739-WMS: Modifications to allow WMS user to have multiple roles
56080 REM "231512-Also see 231473.  Change to EC3BYB, need tag in EC3AC0.
56082 REM "232282-Product code and special charge for apparel items in SAGE
56084 REM "231698-Buyer parameter, report type.  Global tag, report_type.
56085 REM "233319-Allow cc orders to be paid with coupons * sales tax, freight
56086 ! 233389-Generate 3rd party order confirmation request (timestamp_cxml)
56088 REM "239417-Need a global tag for EC buyer "Don't show price?".
56090 REM "237271-EC output/input tags for 3rd party frt calculation, share 
56092 REM "242686-Custom project using the EC Budget module.                  
56094 REM "249058-10 EC Buyer & 10 EC Customer user defined 1 character fields
56096 REM "250181-Global function TIMRND$ and global tag timestamp_random.    
56097 REM "255476-TVIINC workflow - Phase 1: Approver edit worksheets feature 
56098 REM "259474-Ability to store and display the session's current context code
56100 REM "278673-Flowpoint; Template/EC3SN1 changes for Ship Notice/Closeout 
56102 REM "292328-Global tag tf_cust_code_only needs to strip trailing spaces 
56104 REM "301389-Deluxe security, PEN test results, need program changes.    
56106 REM "307160-DBD-9 DBSPT-30576 WebEC Session Cookie Mode Global Tag      
56108 REM "307280-DBD-89; Output tag error_message in EC3SRV                  
