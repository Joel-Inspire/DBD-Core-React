0010 REM "E/C System parameters V <EC3MPF>"
0020 SETESC 9300; SETERR 9000
0035 REM "5.6 - 09/08/08 - 17.8875 - dmm - SSP# 221193
0037 REM "221193-CHUI and GUI places that refer to WebEC or TopForm.         
0040 REM "Copyright 2008 DemandBridge, Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="EC3MPF",X1$="E/C System Parameters V"
0135 C9=-1,V0=-5
0136 L4=15,L5=5,L6=24,L7=9,L8=28
0140 M0$="-##,##0",M1$="-##0",M2$="-###.00"
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 K9$=X3$(9,3)+"E/C",K9=LEN(K9$)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A$(1); REM "148750
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X ZZPARM  02O EC6... 06O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0610 CALL "ZZ2PRP","EW",MULTI_WHSE$,DATE$; IF POS(X3$(9,3)="060092500524447470112525527112377538546449499543",3)<>0 THEN MULTI_WHSE$="Y"
0611 DIM ARPARMS$(300); FIND (Z(6),KEY=X3$(9,3)+"A/R")ARPARMS$
0620 GOSUB 6000
1000 REM "
1010 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1035 DIM A$(1275); REM "148750
1045 A$(1,0+K9)=K9$+A1$
1060 PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1070 GOSUB 6000; GOSUB 6450
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9900
1140 ON X0 GOTO 1500
1200 REM "
1215 IF C0<0 THEN GOTO 9900
1500 REM "
1505 Z9=-1
1510 A1$=K9$
1520 READ (Z[1],KEY=A1$,DOM=1600)IOL=0310
1525 Z9=0
1590 GOTO 1700
1600 REM "
1620 CALL "ZZPROM","2",X3$,Z,"","","",0; ON Z GOTO 1650,1630
1630 GOTO 9800
1690 GOTO 2000
1700 REM "
1790 GOSUB 6200
1800 REM "
1820 CALL "ZZPROM","P",X3$,Z,"","","",X0; ON Z GOTO 1850,6700
1830 GOTO 2000
1850 REM "MODIFY
1855 X0=X0-1; IF Z9=1 THEN GOTO 1880 ELSE IF Z9=0 THEN Z9=1
1860 EXTRACT (Z[1],KEY=A1$,DOM=1870)IOL=0310
1865 C9=1
1870 GOSUB 6200
1890 IF Z<0 THEN GOTO 2035 ELSE GOTO 2000
2000 REM "DATA SEQ
2010 C0=0,X0=-1,X1=1
2020 C9=1,X$="",C0=ABS(C0),X0=X0+1; IF C0=2 THEN IF X0>0 THEN X0=X0-2
2025 IF C0>3 OR X0<0 THEN GOTO 1800
2040 ON X0 GOTO 2100,2150,2200,2250,2800,2300,2350,2400,2450,2500,2550,2600,2650,2700,2750,2850,2900,1800 ! SSP212160 jdf
2100 REM "purchase approval: initial email notification
2101 IF A$(946,1)<>"Y" THEN A$(1021,10)=DIM(10); GOSUB 6200; GOTO 2140
2110 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,68,4,1021,10,C0,"","{3"+X$,"","EC3MPF02","EC6","",""; IF C0=6 THEN GOSUB 6400; C0=1
2120 ISTART=1021; GOSUB 7000; IF OK$<>"Y" THEN GOTO 2110
2140 GOTO 2020
2150 REM "purchase approval: order approved
2151 IF A$(946,1)<>"Y" THEN A$(1031,10)=DIM(10); GOSUB 6200; GOTO 2190
2160 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,68,5,1031,10,C0,"","{3"+X$,"","EC3MPF04","EC6","",""; IF C0=6 THEN GOSUB 6400; C0=1
2170 ISTART=1031; GOSUB 7000; IF OK$<>"Y" THEN GOTO 2150
2190 GOTO 2020
2200 REM "purchase approval: order rejected
2201 IF A$(946,1)<>"Y" THEN A$(1041,10)=DIM(10); GOSUB 6200; GOTO 2240
2210 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,68,6,1041,10,C0,"","{3"+X$,"","EC3MPF06","EC6","",""; IF C0=6 THEN GOSUB 6400; C0=1
2220 ISTART=1041; GOSUB 7000; IF OK$<>"Y" THEN GOTO 2200
2240 GOTO 2020
2250 REM "order confirmation template to use for approved orders
2252 IF A$(946,1)<>"Y" THEN A$(1051,35)=DIM(35); GOSUB 6200; GOTO 2290
2253 X$="PURCHASE APPROVAL: Order confirmation template to use when approval given"
2260 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,44,7,1051,35,C0,"","{3"+X$,"","EC3MPF08","","",""
2290 GOTO 2020
2300 ! CC orders: Don't invoice lines with PO code?  SSP171698
2305 X$="N=For CC invoices, lines with PO code WILL be invoiced."
2310 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,68,10,1093,1,C0,"","{3"+X$,"","EC3MPF10","","",""
2340 GOTO 2020
2345 ! 
2350 ! Use cost qty breaks from supplier record if order line has PO code? WO172963
2355 X$="If 1 or more available in supplier purchasing record for vendor/plant"
2360 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,68,11,1094,1,C0,"","{3"+X$,"","EC3MPF12","","",""
2390 GOTO 2020
2395 ! 
2400 REM ' Use random session id
2405 X$="Use Random Session id instead of sequential id"
2410 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,68,12,1095,1,C0,"","{3"+X$,"","EC3MPF14","","",""
2440 GOTO 2020
2445 ! 
2450 ! SSP175716, Use nof image in product catalog?
2455 X$="Y=Logic like product detail (EC3PC1), N=default"
2460 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,68,13,1136,1,C0,"","{3"+X$,"","EC3MPF16","","",""
2490 GOTO 2020
2495 ! 
2500 ! Create composite pdf for VP, send one vendor email per PO?, WO178946
2510 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,68,14,1137,1,C0,"","{3"+X$,"","EC3MPF18","","",""
2540 GOTO 2020
2545 ! 
2550 ! Next Purchase-based Coupon Number
2560 CALL "ZZENTR","SZ",A{ALL},A$,X4$,X3$,68,15,1138,10,C0,"","{3"+X$,"","EC3MPF20","","",""
2590 GOTO 2020
2600 ! Automatically save current worksheet when saved worksheet retrieved? WO186837
2610 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,68,16,1150,1,C0,"","{3"+X$,"","EC3MPF22","","",""
2640 GOTO 2020
2645 ! 
2650 ! Enable active items feature for multiple catalogs? WO184145
2653 IF A$(1152,1)="Y" THEN A$(1151,1)="N"; PRINT @(68,17),A$(1151,1),; GOTO 2690
2655 X$="Buyer 'My Catalog' category for multiple catalogs, same category"
2660 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,68,17,1151,1,C0,"","{3"+X$,"","EC3MPF24","","",""
2690 GOTO 2020
2695 ! 
2700 ! Show active items for all buyer catalogs in active items category? WO184145
2703 IF A$(1151,1)="Y" THEN A$(1152,1)="N"; PRINT @(68,18),A$(1152,1),; GOTO 2740
2705 X$="Buyer 'My Catalog' category shows items for all buyer catalogs"
2710 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,68,18,1152,1,C0,"","{3"+X$,"","EC3MPF26","","",""
2740 GOTO 2020
2745 ! 
2750 ! Statistics tracking account number, WO189560
2755 X$="For integration with Onestat Web Analytics or other web tracking"
2760 CALL "ZZENTR","S",A{ALL},A$,X4$,X3$,49,19,1153,20,C0,"","{3"+X$,"","EC3MPF28","","",""
2790 GOTO 2020
2795 ! 
2800 ! Create message lines for order approval comments? WO 193640
2810 IF A$(946,1)<>"Y" THEN A$(1173,1)=DIM(1); GOSUB 6200; GOTO 2840
2820 X$="Message lines added to order as max. 6 message lines of 40 chars each"
2830 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,68,8,1173,1,C0,"","{3"+X$,"","EC3MPF24","","",""
2840 GOTO 2020
2845 ! 
2850 ! Override operator code in WebEC, 193098
2855 X$="Leave blank to use the default"
2860 CALL "ZZENTR","SZX",A{ALL},A$,X4$,X3$,68,20,1174,3,C0,"","{3"+X$,"o","EC3MPF30","ZZ2MOA","",""; IF C0=6 THEN GOSUB 6400; C0=1
2865 IF NOT(NUL(A$(1174,3))) THEN FIND (Z[13],KEY="o"+A$(1174,3),DOM=2850)
2870 GOTO 2020
2895 ! 
2900 ! Make WEBEC revert back to using 7-char session ID  ! SSP212160 jdf
2905 X$="Revert back to using 7-char session ID" ! SSP212160 jdf, SSP221193, change WebEC to EC
2910 CALL "ZZENTR","Y",A{ALL},A$,X4$,X3$,68,21,1177,1,C0,"","{3"+X$,"","EC3MPF32","","","" ! SSP212160 jdf
2945 GOTO 2020 ! SSP212160 jdf
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6045 PRINT @(2,3),"Purchase Approvals :",
6050 PRINT @(12,4),"              Initial notification to purchasing agent:",@(15,5),"             Email notification that order approved:",@(15,6),"              Email notification of order rejection:",@(1,7),"                   Order confirm template:",
6053 PRINT @(9,8),"              Create message lines from approval comments?", ! WO 193640
6055 PRINT @(0,9),Z0$,
6060 PRINT @(23,10),"CC orders: Don't invoice lines with PO code?",
6065 PRINT @(0,11),"Use cost qty breaks from supplier record if order line has PO code?",
6067 PRINT @(38,12),"Generate a random session id?",
6070 PRINT @(34,13),"Use nof image in Product Catalog?",
6072 PRINT @(9,14),"Create composite pdf for VP, send one vendor email per PO?",
6074 PRINT @(33,15),"Next Purchase-based Coupon Number:"
6076 PRINT @(8,16),"Auto save current worksheet when saved worksheet retrieved?",
6078 PRINT @(17,17),"Enable active items feature for multiple catalogs?",
6080 PRINT @(1,18),"Show active items for all buyer catalogs in active items category?",
6082 PRINT @(13,19),"Statistics tracking account number:",
6085 PRINT @(40,20),"Use as operator code in EC?", ! 193098, SSP221193, change WebEC to EC
6090 PRINT @(28,21),"Revert back to using 7-char session ID?", ! SSP212160 jdf, SSP221193, change WebEC to EC
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6200 REM "DISPLAY DATA
6215 GOSUB 6450
6220 PRINT @(68,4),A$(1021,10),@(68,5),A$(1031,10),@(68,6),A$(1041,10),@(44,7),A$(1051,35),
6223 PRINT @(68,8),A$(1173,1), ! WO 193640
6225 PRINT @(68,10),A$(1093,1),
6230 PRINT @(68,11),A$(1094,1),
6235 PRINT @(68,12),A$(1095,1),
6240 PRINT @(68,13),A$(1136,1),
6245 PRINT @(68,14),A$(1137,1),
6250 PRINT @(68,15),A$(1138,10),
6255 PRINT @(68,16),A$(1150,1),
6260 PRINT @(68,17),A$(1151,1),
6265 PRINT @(68,18),A$(1152,1),
6270 PRINT @(49,19),A$(1153,20),
6275 PRINT @(68,20),A$(1174,3), ! 193098
6280 PRINT @(68,21),A$(1177,1), ! SSP212160 jdf
6390 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6490 RETURN 
6500 REM "DELETE
6510 REMOVE (Z[1],KEY=A1$,DOM=6511)
6590 GOTO 1630
6600 REM "ALT KEY DATA READS
6690 RETURN 
6700 REM "ADD ENTRY
6705 IF Z9=0 THEN GOTO 6790
6710 WRITE (Z[1],KEY=A1$)IOL=0310
6790 GOTO 1630
6800 REM "NEXT ENTRY IN FILE
6810 A1$=KEY(Z[1],END=6895)
6820 A$(1,LEN(A1$))=A1$
6825 IF A1$(1,LEN(K9$))<>K9$ THEN GOTO 6895
6830 GOSUB 6600
6835 GOSUB 6450
6890 GOTO 1515
6895 PRINT 'RB',; READ (Z[1],KEY=K9$,DOM=1140); GOTO 1140
6902 V9$="2803  2804  4803  2906  2907  4806  6009  6010  6011  "
7000 REM "ck EC6$
7001 IF A$(ISTART,10)=DIM(10) THEN OK$="Y"; GOTO 7040
7005 OK$="N"
7010 DIM EC6$(200); FIND (Z(2),KEY=A$(ISTART,10),DOM=*NEXT)EC6$; OK$="Y"; GOTO 7040
7040 RETURN 
7510 IF STP(A$(ST,LEN),1)="" THEN GOTO 7540
7520 CLOSE (14); OPEN (14,ERR=7530)STP(A$(ST,LEN),1); GOTO 7540
7530 TMP$="|WARNING - The template "+STP(A$(ST,LEN),1)+" cannot be found."; CALL "ZZPROM",".4",X3$,Z,TMP$,"","",0
7540 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9949 SETERR 9951
9960 RUN "ZMENU"
9999 END 
56000 ! Program changes starting 10/31/05
56010 REM "184145-Ability to have "My Catalog" feature work for multiple
56015 REM "189560-Integrate WebEC pages with Onestat web analytics
56017 REM "193640-WebEC parameter to check approval worksheet for approver
56019 REM "193098-Need a way to set the operator code for WebEC as this       
56021 REM "212160-Webec is down.  Cannot use 64 character session id          
56024 REM "221193-Some panels still read WEBEC
