0010 REM "Webec - Build global drop down lists <EC3BYE>
0020 SETESC 9300; SETERR 9000
0035 REM "5.5 - 07/24/06 - 9.593333 - crg - SSP# 192407
0037 REM "192407-Buyer needs 20 additional secure codes.                     
0040 REM "Copyright 2006 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "LIST_TYPE =requested list types (I-catalog item code  D-item description)
0060 REM "ALL$[ALL] = Y will include an ALL option in all requested lists (Y needs to be in the index position corresponding to the ddlist <see line 62 for index definitions>)
0062 REM "DDLIST$[ALL] = 1=catalog item codes  2=item descriptions 3-10=unused
0064 REM "E1$-E3$ = extra unused call variables
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,EC_PARM$,EC${ALL},LIST_TYPE$,ALL${ALL},SELECTED_VALUE${ALL},DDLIST${ALL},E1$,E2$,E3$
0100 SETERR 9000
0110 X0$="EC3BYE",X1$="Webec - Build global drop down lists"
0120 DIM Z0$(80,"-"),FM0(30),DDLIST$[10]
0125 EOL$=$0D0A$; EOL$=ATH(STP(EC_PARM$(57,8),1))
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST ECH$
0320 IOLIST ECG$
0330 IOLIST FM1$,FM1(0),FM1(1),FM1(2),FM1(3),FM1(4),FM1(5),FM1(6),FM1(7),FM1(8),FM1(9),FM1(10),FM1(11),FM1(12),FM1(13),FM1(14),FM1(15),FM1(16),FM1(17),FM1(18),FM1(19),FM1(20),FM1(21),FM1(22),FM1(23),FM1(24),FM1(25),FM1(26),FM1(27),FM1(28),FM1(29),FM1(30)
0340 IOLIST ECR$
0350 IOLIST IC0$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O ECH...  02O ECG...  03O FM1...  04O ECR...  05O IC0...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0540 REM "memory file for sorting of items/descriptions
0542 REM "key:  (1,1)=type  (I=item  D=description)
0543 REM "key:  (2,x)= value   (2,10)=item code  (2,40)=description
0550 CLOSE (2000); OPEN (2000)"*MEMORY*"
0900 REM "
0910 CATALOG$=EC$[2](325,10); REM "set default catalog
0911 BUYER_SECURE$=STP(EC$[2](233,20),2)+STP(EC$[2](898,40),2); REM "get secure codes from buyers record
0980 GOSUB SET_READONLY
0990 ON POS(LIST_TYPE$="ID") GOTO 9900,1000,1000,9900
1000 REM "catalog item code and description ddl's
1005 READ (Z(1),KEY=CATALOG$,DOM=1006)
1010 ECHKEY$=KEY(Z(1),END=1100); IF ECHKEY$(1,LEN(CATALOG$))<>CATALOG$ THEN GOTO 1100 ELSE READ (Z(1),KEY=ECHKEY$)IOL=0310
1015 GOSUB 7000; REM "get updated category secure codes
1017 DIM ECR$(1024); FIND (Z(4),KEY=ECH$(41,20),DOM=1018)IOL=0340
1020 IF POS("D"=LIST_TYPE$)=0 THEN GOTO 1030
1021 REM "get item description
1023 DIM IC0$(200),FM1$(400),FM1(31)
1024 IF ECH$(41,10)=DIM(10) THEN FIND (Z(5),KEY=ECH$(41,20),DOM=1010)IOL=0350; DESC$=IC0$(21,40) ELSE FIND (Z(3),KEY=ECH$(41,20),DOM=1010)IOL=0330; DESC$=FM1$(42,40)
1030 GOSUB 7100; IF RESTRICT$="Y" THEN GOTO 1010; REM "if security code restriction on category or item then skip
1040 WRITE (2000,KEY="I"+ECHKEY$(21,20))ECH$(41,20)
1042 IF POS("D"=LIST_TYPE$)<>0 THEN WRITE (2000,KEY="D"+DESC$)ECH$(41,20)
1080 GOTO 1010
2000 REM "build lists
2010 REM "catalog item code lists
2011 READ (2000,KEY="I",DOM=2012)
2012 TMPKEY$=KEY(2000,END=2100); IF TMPKEY$(1,1)<>"I" THEN GOTO 2100; REM "if no file entries then skip
2013 SEL_VALUE$=STP(SELECTED_VALUE$[1],1),DDLIST$=""; IF ALL$[1]="Y" THEN CODE$=DIM(10,"*"),VALUE$="All Item Codes",SEL_VALUE$=VALUE$; GOSUB 7500; REM "clear current list
2020 TMPKEY$=KEY(2000,END=2080); IF TMPKEY$(1,1)<>"I" THEN GOTO 2080 ELSE READ (2000,KEY=TMPKEY$)CODE$
2022 VALUE$=TMPKEY$(2,20)
2025 GOSUB 7500; REM "add entry to current ddlist
2030 GOTO 2020
2080 DDLIST$[1]=DDLIST$
2100 REM "item description list
2105 SEL_VALUE$=STP(SELECTED_VALUE$[2],1),DDLIST$=""; IF ALL$[2]="Y" THEN CODE$=DIM(40,"*"),VALUE$="ALL Item Descriptions",SEL_VALUE$=CODE$; GOSUB 7500
2107 READ (2000,KEY="D",DOM=2108)
2108 TMPKEY$=KEY(2000,END=2200); IF TMPKEY$(1,1)<>"D" THEN GOTO 2200
2120 TMPKEY$=KEY(2000,END=2180); IF TMPKEY$(1,1)<>"D" THEN GOTO 2180 ELSE READ (2000,KEY=TMPKEY$)CODE$
2122 VALUE$=TMPKEY$(2,40)
2124 GOSUB 7500; REM "add entry to current ddlist
2130 GOTO 2120
2180 DDLIST$[2]=DDLIST$
3000 REM "exit
3050 GOTO 9900
7000 REM "get updated category security codes
7005 IF CATEGORY$=ECHKEY$(11,10) THEN GOTO 7040 ELSE CATEGORY$=ECHKEY$(11,10)
7020 CATEGORY_SECURE$=""; READ (Z(2),KEY=ECHKEY$(11,10),DOM=7021)IOL=0320
7030 CATEGORY_SECURE$=STP(ECG$(101,6),2)
7040 RETURN 
7100 REM "ck if security codes restricting items
7105 RESTRICT$="N"
7110 ITEM_SECURE$=STP(ECR$(233,10),2)
7120 REM "category security
7122 IF CATEGORY_SECURE$="" THEN GOTO 7130
7124 FOR S=0 TO LEN(CATEGORY_SECURE$)/2-1
7125 CS$=CATEGORY_SECURE$(S*2+1,2)
7126 IF POS(CS$=BUYER_SECURE$,2)<>0 THEN EXITTO 7130
7127 NEXT S
7128 RESTRICT$="Y"; GOTO 7190
7130 REM "item secure
7132 IF ITEM_SECURE$="" THEN GOTO 7140
7134 FOR S=0 TO LEN(ITEM_SECURE$)/2-1
7135 IS$=ITEM_SECURE$(S*2+1,2)
7136 IF POS(IS$=BUYER_SECURE$,2)<>0 THEN EXITTO 7140
7137 NEXT S
7138 RESTRICT$="Y"; GOTO 7190
7190 RETURN 
7500 REM "put on html
7510 IF SEL_VALUE$=VALUE$ THEN SELECTED$="<option selected value=" ELSE SELECTED$="<option value="
7515 ENTRY$=SELECTED$+QUO+CODE$+QUO+">"+VALUE$+"</option>"
7520 DDLIST$=DDLIST$+ENTRY$
7590 RETURN 
8500 SET_READONLY:REM "Set the PRM READONLY to on
8510 SET_PARAM 'XI'
8525 SET_READONLY_END:RETURN 
8530 CLEAR_READONLY:REM "Clear the PRM READONLY flag to off
8535 SET_PARAM -'XI'
8545 CLEAR_READONLY_END:RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9002 OUTPUT=2,J=ERR,K=TCB(5); SETERR 0000; ENDTRACE ; REM "135781
9003 PRINT (OUTPUT)"Content-type: text/html",EOL$,EOL$,
9005 PRINT (OUTPUT,ERR=9010)"ERR=",STR(J)," LINE = ",STR(K),"<BR>",EOL$,
9006 PRINT (OUTPUT)"VALUE$=",VALUE$,EOL$,
9007 PRINT (OUTPUT)"HTA(VALUE$)=",HTA(VALUE$),EOL$,
9010 GOTO 9900
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "192407-Buyer needs 20 additional secure codes.                     
