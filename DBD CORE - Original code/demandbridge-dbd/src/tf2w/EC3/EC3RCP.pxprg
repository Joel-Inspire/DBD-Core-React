0010 REM "WebEC Warehouse Receiving Report Engine <EC3RCP>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 10/27/09 - 21.502777 - crg - SSP# 233219
0037 REM "233219-Create excel versions of some of the EC html reports to use 
0040 REM "Copyright 2009 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT
0100 SETERR 9000
0102 GOSUB SET_READONLY
0110 X0$="EC3RCP",X1$="WebEC Warehouse Receiving Report"
0115 RECV_ITEM$="",RECV_PO$=""
0120 EOL$=$0D0A$; EOL$=ATH(STP(EC_PARM$(57,8),1))
0132 UMASK$="#,###,##0-"
0320 IOLIST FM1$,FM1[0],FM1[1],FM1[2],FM1[3],FM1[4],FM1[5],FM1[6],FM1[7],FM1[8],FM1[9],FM1[10],FM1[11],FM1[12],FM1[13],FM1[14],FM1[15],FM1[16],FM1[17],FM1[18],FM1[19],FM1[20],FM1[21],FM1[22],FM1[23],FM1[24],FM1[25],FM1[26],FM1[27],FM1[28],FM1[29],FM1[30],FM1[31]
0340 IOLIST ECG$
0350 IOLIST ECH$
0360 IOLIST ECR$
0380 IOLIST FM0$
0490 IOLIST FMZ$,FMZ(0),FMZ(1),FMZ(2),FMZ(3),FMZ(4),FMZ(5),FMZ(6),FMZ(7),FMZ(8),FMZ(9),FMZ(10),FMZ(11),FMZ(12),FMZ(13),FMZ(14)
0500 REM "Open files
0505 DIM Z[NUM(X3$(60,3))]
0507 WORK_FILE$="W"+EC$[1](1,7); ERASE WORK_FILE$,ERR=*NEXT; REM "work file is "W"+session id
0508 CLOSE (1001); OPEN (1001,ERR=0509)WORK_FILE$; CLOSE (1001); GOTO 0510
0509 DIRECT WORK_FILE$,60,0,2048; REM "create work file if it doesn't exist. SSP173078, changed from 50,0,597 to 60,0,2048
0510 Z$="01O AR1... 020 FM1... 03O FT1... 04O ECG... 05O ECH... 06O ECR... 07O "+WORK_FILE$+" 08O FM0... 09O IC1... 10O FM3... 11O FM4... 12O EDE... 13O ZZPARM 14O EDI... 15O EDA... 18O EDQ... 19O FMZ... "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 ! RPTLINE$=""; CLOSE (1001); OPEN (1001,ERR=0531)"rpt_rr_line.txt"; READ (1001)RPTLINE$; CLOSE (1001); ! 233219 - Disable this code, interfering with HTML and Excel rpt generation in new page set
0600 REM 'SET IMG SRC PREFIX/SUFFIX
0601 ITEM_SEARCH_ELEMENTS=8,ST_SEARCH_ELEMENTS=7
0602 DIM SI$[ITEM_SEARCH_ELEMENTS,3]; REM "item search elements (index 1=search text   2=<string>AAAABBBB  A=starting position B=length of fm1 field 2=<numeric>AAAA A=numeric element 3=S/N (string or numeric  <string processed unless set to N>
0603 DIM ST$[ST_SEARCH_ELEMENTS,3]; REM "shipto search elements (index 1=search text   2=<string>AAAABBBB  A=starting position B=length of fm1 field 2=<numeric>AAAA A=numeric element 3=S/N (string or numeric  <string processed unless set to N>
0610 IMGSRC_PREFIX$=STP(EC_PARM$(311,20),2),IMGSRC_ATTRIB$=" "+STP(EC_PARM$(331,60),2)+">",IMGSRC_SUFFIX$=STP(EC_PARM$(391,20),2)
0620 SEARCH_WHSE_TYPE$="C"
0800 GOSUB 3000
1000 REM "Process DATA array
1005 DATA_TAGS$="001<template>002<user>003<password>004<maxrows>005<xxxxx>006<xxxxx>007<session_id>008<xxxxx>009<xxxxxx>010<search_type>011<xxxxxxxxxxxxxxxx>012<xxxxxxxxxxxxxxxxxxxxxxx>013<xxxxx>014<xxxxx>015<xxxxx>016<xxxxx>017<xxxxx>018<xxxxx>019<xxxxx>020<xxxxx>021<xxxxxx>022<xxxxx>023<xxxxx>024<xxxxx>025<xxxxx>026<xxxxx>027<xxxxxxxxxxxxxxxxxx>028<xxxxx>029<xxxxx>030<xxxxx>031<xxxxx>032<xxxxx>033<xxxxx>034<recpt_date_start>035<recpt_date_end>036<search_whse_type>"
1006 MAX_ROWS=NUM(EC_PARM$(258,3)),POSITION_ITEM$="",DIRECTION$="F"
1010 FOR I=1 TO NUM_ENTRIES
1015 TAG$=DATA$[I,0],VALUE$=DATA$[I,1]
1018 DPOS=POS("<"+TAG$+">"=DATA_TAGS$); IF DPOS=0 THEN GOTO 1090 ELSE DINDEX=NUM(DATA_TAGS$(DPOS-3,3),ERR=1090)
1019 ON DINDEX GOTO 1090,1021,1022,1023,1024,1025,1026,1027,1028,1029,1030,1031,1032,1033,1034,1035,1036,1037,1038,1039,1040,1041,1042,1043,1044,1045,1046,1047,1048,1049,1050,1051,1052,1053,1054,1055,1056,1090
1021 TEMPLATE$=VALUE$; GOTO 1090; REM "template
1022 USER$=VALUE$; GOSUB 3000; GOTO 1090; REM "user
1023 PASSWORD$=VALUE$; GOTO 1090; REM "password
1024 MAX_ROWS=NUM(VALUE$,ERR=1090); GOTO 1090; REM "maxrows
1025 POSITION_ITEM$=VALUE$; GOTO 1090; REM "position_item
1026 IF VALUE$="backward" THEN DIRECTION$="B"; GOTO 1090; REM "direction, only set if it is 'backward'
1027 SESSION_ID$=VALUE$; GOTO 1090; REM "Get session id
1028 CATEGORY$=VALUE$; GOTO 1090; REM "category(s) to display (10 *'s is for all categories otherwise this can contain a single or multiple categories)
1029 SEARCH=POS(VALUE$="Y"); GOTO 1090; REM "if search set then will initiate searching mechanism - if not will work off the product catalog
1030 SEARCH_TYPE$=VALUE$; GOTO 1090; REM "search_type
1031 SEARCH_ITEM_CODE$=VALUE$,SI$[1,1]=VALUE$,SI$[1,2]="00110010"; GOTO 1090; REM "value to search item codes
1032 SEARCH_ITEM_DESC$=VALUE$,SI$[2,1]=VALUE$,SI$[2,2]="00420040"; GOTO 1090; REM "value to search item descriptions
1033 SEARCH_ITEM_LR$=VALUE$,SI$[3,1]=VALUE$,SI$[3,2]="00240008"; GOTO 1090; REM "search_item_lr (left-right dimensions)
1034 SEARCH_ITEM_TB$=VALUE$,SI$[4,1]=VALUE$,SI$[4,2]="00320008"; GOTO 1090; REM "search_item_tb (top-bottom)
1035 SEARCH_ITEM_PLY$=VALUE$,SI$[5,1]=VALUE$,SI$[5,2]="00400002"; GOTO 1090; REM "search_item_ply
1036 SEARCH_ITME_FMS$=VALUE$,SI$[6,1]=VALUE$,SI$[6,2]="01080001"; GOTO 1090; REM "search_item_fms (should be Y or N to only get certain FMS flagging
1037 SEARCH_ITEM_VENDCD$=VALUE$,SI$[7,1]=VALUE$,SI$[7,2]="00820010"; GOTO 1090; REM "search_item_vendorcd
1038 SEARCH_ITEM_CIC$=VALUE$,SI$[8,1]=VALUE$,SI$[8,2]="03500020"; GOTO 1090; REM "search_item_cic
1039 SEARCH_ST_CODE$=VALUE$,ST$[1,1]=VALUE$,ST$[1,2]="00120004"; GOTO 1090; REM "search_st_code (tf shipto code)
1040 SEARCH_ST_AREA_CODE$=VALUE$,ST$[2,1]=VALUE$,ST$[2,2]="02000003"; GOTO 1090; REM "search_st_area_code (uses first 3 char of phone)
1041 SEARCH_ST_STATE$=VALUE$,ST$[3,1]=VALUE$,ST$[3,2]="01270002"; GOTO 1090; REM "search_st_state
1042 SEARCH_ST_REC_DEPT$=VALU$,ST$[4,1]=VALUE$,ST$[4,2]="03820020"; GOTO 1090
1043 SEARCH_ST_LOC$=VALUE$,ST$[5,1]=VALUE$,ST$[5,2]="02140009"; GOTO 1090; REM "search_st_loc (customer 9 character location)
1044 SEARCH_ST_ZIP$=VALUE$,ST$[6,1]=VALUE$,ST$[6,2]="00240009"; GOTO 1090; REM "search_st_zip
1045 SEARCH_ST_CITY$=VALUE$,ST$[7,1]=VALUE$,ST$[7,2]="01110016"; GOTO 1090; REM "search_city
1046 USAGE_YEAR$=VALUE$; GOTO 1090; REM "requested_usage_year
1047 USAGE_LOCATION$=VALUE$; GOTO 1090; REM "requested_location
1048 USAGE_PERIOD$=VALUE$; GOTO 1090; REM "requested_period
1049 SUPPRESS_DUPLICATES$=VALUE$; GOTO 1090; REM "suppress_duplicates
1050 RS=POS(VALUE$="Yy"); IF RS<>0 THEN RS$="Y" ELSE RS$="N" END_IF ; GOTO 1090; REM " set flag for reporting in selling units
1051 BALANCE_TYPE$=UCS(VALUE$); GOTO 1090; REM "Set balance type to report from FM3 (if blank set to U in the 1100's)
1052 CATALOG$=VALUE$; GOTO 1090; REM "catalog
1053 BELOW_REORDER$=VALUE$; IF BELOW_REORDER$="ON" THEN BELOW_REORDER$="Y" END_IF ; GOTO 1090; REM "only_below_reorderpt (only applies to custom items FM1)
1054 GOSUB 6000; TEST$=STR(NUM(VALUE$,ERR=1090):"00000000"); CALL "EC3DAT",X3$,1,VALUE$(7,2)+VALUE$(1,4),START_RECPT_DATE$,""; GOTO 1090; REM "recpt_date_start
1055 GOSUB 6000; TEST$=STR(NUM(VALUE$,ERR=1090):"00000000"); CALL "EC3DAT",X3$,1,VALUE$(7,2)+VALUE$(1,4),END_RECPT_DATE$,""; GOTO 1090; REM "recpt_date_end
1056 IF NOT(NUL(VALUE$)) AND POS(MID(VALUE$,1,1)="ADC",1)>0 THEN SEARCH_WHSE_TYPE$=MID(VALUE$,1,1); GOTO 1090; REM "search_whse_type
1090 NEXT I
1100 REM "preprocessing
1101 VERSION$="171217.173398.20040701",PROGRAM$=PGN; DIM BYBINFO$[5]; BYBINFO$[1]=PROGRAM$,BYBINFO$[2]=VERSION$,BYBINFO$[3]=TEMPLATE$,BYBOPTIONS$=""
1109 DIM FM0$(500)
1126 MAX_ROWS=999999999; REM "set rows to for reports
1130 REM "ck if new search
1132 GOSUB 5000; REM "get valid list of items
1500 REM "Use template$ to open file, read line by line and insert
1505 CLOSE (100); OPEN (100,OPT="TEXT",ERR=9000)TEMPLATE$
1506 REM "Setup line_tags$
1507 LINE_TAGS$="001<user>002<cust_code>003<cust_name>004<ec_company>005<entrystart>006<session_id>007<buyer_id>008<buyer_name>009<date>010<time>"
1510 READ (100,END=1595)LINE$; GOSUB 7550
1515 INDEX=POS("?tf?"=LINE$); IF INDEX=0 THEN PRINT (OUTPUT)LINE$,EOL$,; GOTO 1510
1520 END_INDEX=POS("?"=LINE$(INDEX+4)),TAG$=LINE$(INDEX+4,END_INDEX-1),LINE1$=LINE$(1,INDEX-1),LINE2$=LINE$(INDEX+END_INDEX+4),LPOS=POS("<"+TAG$+">"=LINE_TAGS$); IF LPOS=0 THEN LINDEX=0 ELSE LINDEX=NUM(LINE_TAGS$(LPOS-3,3))
1530 GOSUB 2000; GOTO 1515
1995 GOTO 9900
1999 REM "Get tag value & substitute
2000 ON LINDEX GOTO 2090,2001,2002,2003,2004,2005,2006,2007,2008,2009,2010,2011,2090
2001 LINE$=LINE1$+USER$+LINE2$; GOTO 2095; REM "user
2002 LINE$=LINE1$+CUST_CODE$+LINE2$; GOTO 2095; REM "cust_code
2003 LINE$=LINE1$+STP(EC$[1](23,35),1)+LINE2$; GOTO 2095; REM "cust_name
2004 LINE$=LINE1$+STP(EC_PARM$(7,50),1)+LINE2$; GOTO 2095; REM "ec_company
2005 GOSUB 3200; LINE$=TABLE$; GOTO 2095; REM "entrystart
2006 LINE$=LINE1$+STP(SESSION_ID$,1)+LINE2$; GOTO 2095; REM "session_id
2007 LINE$=LINE1$+STP(EC$[1](8,15),1)+LINE2$; GOTO 2095; REM "buyer_id
2008 LINE$=LINE1$+STP(EC$[2](16,35),1)+LINE2$; GOTO 2095; REM "buyer_name
2009 LINE$=LINE1$+EC$[4]+LINE2$; GOTO 2095; REM " date
2010 LINE$=LINE1$+EC$[5]+LINE2$; GOTO 2095; REM "time
2090 LINE$=LINE1$+LINE2$; GOTO 2095; REM "tag not found, replace with '?'. 233219, remove "?"
2095 RETURN 
3000 REM "Read customer data into AR1$
3010 DIM AR1$(600); READ (Z[1],KEY=EC$[2](51,10),DOM=3011)AR1$
3015 CALL "ZZDISP","AX",EC$[2](51,10),"A/R",X3$,CUST_CODE$,"",0,0,X4$
3045 RETURN 
3200 REM 
3201 ENTRY_TAGS$="001<item_code>002<recpt_date>003<order_no>004<cust_po>005<uom>006<pack_size>007<starting_no>008<warehouse_code>009<qty_recv>010<qty_ext>011<type>012<style_type>"
3204 ENTRY_TEMPLATE$=LINE$; REM "Setup ENTRY_TEMPLATE$
3205 READ (100,END=3206)LINE$; GOSUB 7550; ENTRY_TEMPLATE$=ENTRY_TEMPLATE$+LINE$+EOL$; IF POS("?tf?entryend?"=LINE$)=0 THEN GOTO 3205
3209 REM "now read search file (work file)
3210 READ (Z(7),KEY="",DOM=*NEXT)
3211 LASTITEM$="",LASTTYPE$=""
3212 FMZKEY$=KEY(Z(7),END=3246); READ (Z(7),KEY=FMZKEY$)IOL=0490
3215 IF FMZ$(35,1)="R" THEN RECV_ITEM$=FMZ$(16,10),RECV_PO$=FMZ$(231,9) ELSE IF RECV_ITEM$<>FMZ$(16,10) THEN GOTO 3212 ELSE IF RECV_PO$<>FMZ$(231,9) THEN GOTO 3212
3220 ENTRY$=ENTRY_TEMPLATE$
3225 IF LASTITEM$="" THEN LASTITEM$=FMZ$(16,10),LASTTYPE$="" ELSE IF LASTITEM$<>FMZ$(16,10) THEN LASTITEM$=FMZ$(16,10),LASTTYPE$="" ELSE FMZ$(16,10)=DIM(10)
3227 IF SEARCH_TYPE$="A" THEN IF LASTTYPE$="" OR (LASTTYPE$<>"R" AND FMZ$(35,1)="R") THEN ENTRY$=RPTLINE$+ENTRY$
3230 GOSUB 3300
3233 LASTTYPE$=FMZ$(35,1)
3235 IF LEN(TABLE$+ENTRY$)>25000 THEN PRINT (OUTPUT)TABLE$,EOL$,; TABLE$="" END_IF ; TABLE$=TABLE$+ENTRY$
3245 GOTO 3212
3295 RETURN 
3300 REM "Given FM1$, create row in table
3305 EINDEX=POS("?tf?"=ENTRY$); IF EINDEX=0 THEN GOTO 3396
3306 EEND_INDEX=POS("?"=ENTRY$(EINDEX+4)),ETAG$=ENTRY$(EINDEX+4,EEND_INDEX-1),ENTRY1$=ENTRY$(1,EINDEX-1),ENTRY2$=ENTRY$(EINDEX+EEND_INDEX+4),EPOS=POS("<"+ETAG$+">"=ENTRY_TAGS$); IF EPOS=0 THEN EINDEX=0 ELSE EINDEX=NUM(ENTRY_TAGS$(EPOS-3,3))
3310 ON EINDEX GOTO 3392,3311,3312,3313,3314,3315,3316,3317,3318,3319,3320,3321,3322,3392
3311 ENTRY$=ENTRY1$+STP(FMZ$(16,10),2)+ENTRY2$; GOTO 3395; REM "item_code
3312 DATEIN$=FMZ$(26,6); GOSUB GET_DATE; ENTRY$=ENTRY1$+DATEOUT_LONG$+ENTRY2$; GOTO 3395; REM "recptdate "
3313 CALL "ZZDISP","AX",FMZ$(113,9),"P/O",X3$,TF_ORDER_NO$,"",0,0,X4$; IF FMZ$(35,1)="S" AND TF_ORDER_NO$<>DIM(9) THEN TF_ORDER_NO$="Ord.#"+TF_ORDER_NO$ END_IF ; ENTRY$=ENTRY1$+TF_ORDER_NO$+ENTRY2$; GOTO 3395; REM "tf_order_no
3314 ENTRY$=ENTRY1$+STP(FMZ$(122,15),1)+ENTRY2$; GOTO 3395; REM "cust_po
3315 ENTRY$=ENTRY1$+STP(FMZ$(268,4),2)+ENTRY2$; GOTO 3395; REM "uom
3316 ENTRY$=ENTRY1$+STR(FMZ(11):"###0")+ENTRY2$; GOTO 3395; REM "pack_size
3317 ENTRY$=ENTRY1$+STP(FMZ$(198,9),1)+ENTRY2$; GOTO 3395; REM "starting_no
3318 ENTRY$=ENTRY1$+FMZKEY$(11,4)+ENTRY2$; GOTO 3395; REM "warehouse_code
3319 ENTRY$=ENTRY1$+STR(FMZ[13]:"####-")+ENTRY2$; GOTO 3395; REM "qty_recv
3320 ENTRY$=ENTRY1$+STR(FMZ(1):"######-")+ENTRY2$; GOTO 3395; REM "qty_ext
3321 ENTRY$=ENTRY1$+FMZ$(35,1)+ENTRY2$; GOTO 3395; REM "type
3322 IF FMZ$(35,1)="S" THEN ENTRY$=ENTRY1$+"I"+ENTRY2$; GOTO 3395 ELSE ENTRY$=ENTRY1$+FMZ$(35,1)+ENTRY2$; GOTO 3395
3392 ENTRY$=ENTRY1$+ENTRY2$; REM "unknown tag. 233219 - Remove "?"
3395 GOTO 3305
3399 RETURN 
3400 REM "Output report from work file built @5000
3445 RETURN 
4400 REM "Use Product Catalog file to build display array
4401 REM "** currently not supporting next/previous buttons
4402 FIRST_ITEM$="",LAST_ITEM$="",TOTAL_RECORDS=0,INDEX=1,ITEM_LIST$=""; DIM DISPLAY$[6000]
4404 REM "GOSUB 04450
4405 FOR CATEGORY_INDEX=0 TO LEN(CATEGORY$)/10-1
4406 CURRENT_CATEGORY$=CATEGORY$(CATEGORY_INDEX*10+1,10)
4407 IF CURRENT_CATEGORY$=EC$[2](483,10) THEN READ (Z[12],KEY=EC$[2](1,15),KNO=CATALOGSORT,DOM=4408) ELSE READ (Z[5],KEY=CATALOG$+CURRENT_CATEGORY$,KNO=CATALOGSORT,DOM=4410)
4408 EDEKEY$=KEY(Z[12],KNO=CATALOGSORT,END=4435); IF EDEKEY$(1,15)<>EC$[2](1,15) THEN GOTO 4435 ELSE READ (Z[12],KEY=EDEKEY$,KNO=CATALOGSORT)EDE$; ECRKEY$=EDE$(36,20); DIM ECH$(190); ECH$(1,10)=EC$[2](325,10),ECH$(11,10)=EC$[2](483,10),ECH$(21,40)=EDE$(16,40),ECH$(61,40)=EDE$(56,40); GOTO 4411; REM "153058
4410 ECHKEY$=KEY(Z[5],KNO=CATALOGSORT,END=4435); IF ECHKEY$(1,20)<>CATALOG$+CURRENT_CATEGORY$ THEN GOTO 4442 ELSE READ (Z[5],KEY=ECHKEY$,KNO=CATALOGSORT)ECH$; ECRKEY$=ECH$(41,20); REM "153058
4411 IF EC$[2](339,1)<>"Y" THEN GOTO 4413; REM "ck if only reporting activeitems
4412 FIND (Z[11],KEY=ECH$(41,20)+EC$[2](61,4),DOM=4410); REM "skip if no FM4 record
4413 DIM ECR$(1000); FIND (Z(6),KEY=ECRKEY$,DOM=*NEXT)IOL=0360
4420 IF SUPPRESS_DUPLICATES$="Y" THEN IF POS(ECH$(41,20)=ITEM_LIST$,20)<>0 THEN GOTO 4426; REM "if already in display array then do not add again
4423 GOSUB 8000; IF ITEM_OK$<>"Y" THEN GOTO 4426
4425 DISPLAY$[INDEX]=ECH$,TOTAL_RECORDS=TOTAL_RECORDS+1,INDEX=INDEX+1,ITEM_LIST$=ITEM_LIST$+ECH$(41,20)
4430 IF CURRENT_CATEGORY$=EC$[2](483,10) THEN GOTO 4408 ELSE GOTO 4410
4442 NEXT CATEGORY_INDEX
4443 FIRST_ITEM$=DISPLAY$[1],LAST_ITEM$=DISPLAY$[TOTAL_RECORDS]
4445 RETURN 
4450 REM "Set VERY_FIRST_ITEM$ and VERY_LAST_ITEM$. These will be used to determine when to NOT show prev and next buttons
4455 VERY_FIRST_ITEM$="",VERY_LAST_ITEM$=""
4460 READ (Z[5],KEY=EC$[2](325,10),DOM=4461)
4465 VK$=KEY(Z[5],END=4466); IF VK$(1,10)=EC$[2](325,10) THEN VERY_FIRST_ITEM$=VK$
4470 READ (Z[5],KEY=EC$[2](325,10)+$FF$,DOM=4471)
4471 VK$=KEP(Z[5],END=4472); IF VK$(1,10)=EC$[2](325,10) THEN VERY_LAST_ITEM$=VK$
4495 RETURN 
5000 REM "Item Search
5001 DIM TOTALS(30,3); USAGE_LOCATION$="****"
5002 FMZHIKEY$=EC$[2](51,10)+"C"
5003 FOUND=0; DIM FMZ(14)
5005 CALL "ZZINIT",STR(Z[7]:"00")
5009 READ (Z(19),KEY=FMZHIKEY$,DOM=*NEXT)
5010 FMZKEY$=KEY(Z(19),END=5160); READ (Z(19),KEY=FMZKEY$)IOL=0490
5020 IF FMZ$(1,10)<>EC$[2](51,10) THEN GOTO 5160
5032 IF FMZ$(26,6)<START_RECPT_DATE$ THEN READ (Z(19),KEY=FMZ$(1,25)+START_RECPT_DATE$,DOM=5010)
5033 IF FMZ$(26,6)>END_RECPT_DATE$ THEN READ (Z(19),KEY=FMZKEY$(1,31)+$FF$,DOM=5010)
5037 IF FMZ$(11,1)="C" THEN IF SEARCH_WHSE_TYPE$="A" THEN WHSELOC$=FMZ$(93,4) ELSE IF (SEARCH_WHSE_TYPE$="C" AND FMZ$(93,1)="9") OR (SEARCH_WHSE_TYPE$="D" AND FMZ$(93,1)<>"9") THEN WHSELOC$=FMZ$(93,4); ELSE GOTO 5010
5038 IF FMZ$(11,1)<>"C" THEN IF SEARCH_WHSE_TYPE$="A" THEN WHSELOC$=FMZ$(12,4) ELSE IF (SEARCH_WHSE_TYPE$="C" AND FMZ$(12,1)="9") OR (SEARCH_WHSE_TYPE$="D" AND FMZ$(12,1)<>"9") THEN WHSELOC$=FMZ$(12,4); ELSE GOTO 5010
5045 IF SEARCH_TYPE$<>"A" AND FMZ$(35,1)<>"R" THEN GOTO 5010 ELSE IF POS(FMZ$(35,1)="RI")=0 THEN GOTO 5010; REM dont show adj
5050 IF STP(SEARCH_ITEM_CODE$,3," ")="" THEN GOTO *NEXT ELSE IF FMZ$(16,10) LIKE SEARCH_ITEM_CODE$ THEN GOTO *NEXT ELSE GOTO 5010
5060 IF STP(SEARCH_ITEM_DESC$,3," ")="" THEN GOTO *NEXT ELSE IF UCS(FMZ$(46,40)) LIKE SEARCH_ITEM_DESC$ THEN GOTO *NEXT ELSE GOTO 5010
5090 DIM ECR$(1000); READ (Z[6],KEY=FMZ$(1,10)+FMZ$(16,10),DOM=*NEXT)ECR$ ! SSP173398
5095 GOSUB 8000; IF ITEM_OK$<>"Y" THEN GOTO 5010 ! SSP173398
5100 REM "record qualifies
5102 IF FMZ$(35,1)="I" THEN FMZ$(35,1)="S"
5105 FMZKEY$=FMZ$(16,10)+WHSELOC$+FMZ$(231,9)+FMZ$(26,6)+FMZ$(35,1)+FMZ$(1,15)+FMZ$(32,3)
5110 WRITE (Z(7),KEY=FMZKEY$)IOL=0490
5120 FOUND=FOUND+1
5150 GOTO 5010
5160 READ (Z[7],KEY="",DOM=*NEXT)
5190 RETURN 
5400 REM "Create totals template
5405 EINDEX=POS("?tf?"=ENTRY$); IF EINDEX=0 THEN GOTO 5496
5406 EEND_INDEX=POS("?"=ENTRY$(EINDEX+4)),ETAG$=ENTRY$(EINDEX+4,EEND_INDEX-1),ENTRY1$=ENTRY$(1,EINDEX-1),ENTRY2$=ENTRY$(EINDEX+EEND_INDEX+4),EPOS=POS("<"+ETAG$+">"=TOTAL_TAGS$); IF EPOS=0 THEN EINDEX=0 ELSE EINDEX=NUM(TOTAL_TAGS$(EPOS-3,3))
5410 ON EINDEX GOTO 5492,5411,5412,5413,5414,5415,5416,5417,5418,5419,5420,5492
5411 ENTRY$=ENTRY1$+STP(STR(TOTALS(1,TOTALSINDEX):"##,###,##0.00-"),2)+ENTRY2$; GOTO 5495; REM "fms_price_ext_total
5412 ENTRY$=ENTRY1$+STP(STR(TOTALS(2,TOTALSINDEX):"##,###,##0.00-"),2)+ENTRY2$; GOTO 5495; REM "sell_price_ext_total
5413 ENTRY$=ENTRY1$+STP(STR(TOTALS(3,TOTALSINDEX):"##,###,##0.00-"),2)+ENTRY2$; GOTO 5495; REM "freight_prorated_total
5414 ENTRY$=ENTRY1$+STP(STR(TOTALS(4,TOTALSINDEX):"##,###,##0.00-"),2)+ENTRY2$; GOTO 5495; REM "tax_prorated_total
5415 ENTRY$=ENTRY1$+STP(STR(TOTALS(6,TOTALSINDEX):"##,###,##0.00-"),2)+ENTRY2$; GOTO 5495; REM "qty_shipped_sell_total
5416 ENTRY$=ENTRY1$+STP(ITEMBREAK$,2)+ENTRY2$; GOTO 5495; REM "item_code
5417 ENTRY$=ENTRY1$+STP(LOCATIONBREAK$,2)+ENTRY2$; GOTO 5495; REM "fmz_shipto_code
5418 ENTRY$=ENTRY1$+STP(RECDEPTBREAK$,2)+ENTRY2$; GOTO 5495; REM "fmz_receving_dept
5419 ENTRY$=ENTRY1$+STP(STR(TOTALS(5,TOTALSINDEX):"##,###,##0.00-"),2)+ENTRY2$; GOTO 5495; REM "qty_shipped_eaches_total
5420 ENTRY$=ENTRY1$+STP(STR(TOTALS(7,TOTALSINDEX):"##,##0"),2)+ENTRY2$; GOTO 5495; REM "item_count
5492 ENTRY$=ENTRY1$+ENTRY2$; GOTO 5405 ! 233219 - Remove "?"
5495 GOTO 5405
5499 RETURN 
6000 REM strip / off date
6010 WHILE POS("/"=VALUE$)<>0
6015 SL=POS("/"=VALUE$)
6020 IF SL<>LEN(VALUE$) THEN VALUE$=VALUE$(1,SL-1)+VALUE$(SL+1) ELSE VALUE$=VALUE$(1,SL-1)
6050 WEND 
6060 IF LEN(VALUE$)=6 THEN IF VALUE$(5,2)>"80" THEN VALUE$=VALUE$(1,4)+"19"+VALUE$(5,2) ELSE VALUE$=VALUE$(1,4)+"20"+VALUE$(5,2)
6090 RETURN 
7000 GET_DATE:
7010 CALL "EC3DAT",X3$,2,DATEIN$,DATEOUT$,DATEOUT_LONG$
7020 RETURN 
7500 REM "ck if form over reorder point
7502 REORDER_TRIGGERED$="N"
7510 IF FM1$(109,6)<X3$(21,6) AND FM1$(109,6)<>DIM(6) THEN REORDER_TRIGGERED$="Y",RO_TYPE$="Date"; GOTO 7540; REM "ck reorder date
7512 IF IC1INFO[33]<FM1(23) THEN REORDER_TRIGGERED$="Y",RO_TYPE$="Qty"; GOTO 7540; REM "ck reorder qty
7513 IF SELL_UM=0 OR FM1(24)=0 THEN GOTO 7540
7514 ROD=((IC1INFO[33]/SELL_UM)/(FM1(24)/SELL_UM)*30); IF ROD<FM1(22) THEN REORDER_TRIGGERED$="Y",RO_TYPE$="Days"; GOTO 7540; REM "ck reorder point in days
7540 RETURN 
7550 REM "Send LINE$ to EC3BYB for generic fields processing
7560 CALL "EC3BYB",ERR=7561,X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,LINE$,BYBINFO${ALL},BYBOPTIONS$,100
7595 RETURN 
7600 REM "format zip code
7620 CALL "ZZDISP","A",ZIPIN$,"ZIP",X3$,ZIPOUT$,"",0,0,X4$
7650 REM "format phone
7670 CALL "ZZDISP","P",PIN$,"",X3$,POUT$,"",0,0,X4$
7680 RETURN 
7700 REM "calcualte and extended price
7701 REM "first convert to each price and then calculate
7705 EACHQTY=FM1[3]*1000/SELL_UM
7800 REM "get active uom
7810 ON POS(ITEM_TYPE$="CI")-1 GOTO 7820,7840
7820 REM "C - items FM1
7830 IF STP(FM1$(238,4),3," ")="" OR FM1[25]=0 THEN UM$=FM1$(100,4),PACK=FM1[4]; GOTO 7860 ELSE UM$=FM1$(238,4),PACK=FM1[25]; GOTO 7860
7840 REM "I - items IC0
7850 UM$=IC0$(120,4),PACK=IC0[14]
7860 UM$=STP(UM$,1),EXT_UM$=STP(UM$,1)+"/"+STR(PACK:"##,##0")
7890 RETURN 
7900 REM "get inventory / usage figures
7909 DIM USAGE_KEYS$(10); USAGE_KEYS$(1,4)=USAGE_YEAR$,USAGE_KEYS$(5,2)=USAGE_PERIOD$; IF USAGE_LOCATION$<>"****" THEN USAGE_KEYS$(7,4)=USAGE_LOCATION$; REM "set to value passed in
7910 DIM CHANNELS[4]; CHANNELS[1]=Z[9],CHANNELS[2]=Z[10],CHANNELS[3]=Z[11],CHANNELS[4]=Z[6]
7911 DIM OPTIONS$(20); OPTIONS$(1,2)="RB"; REM "R-reported warehouses   B-both usage/inventory figs, SSP165240 change DIM to 20
7912 OPTIONS$(5,1)=BALANCE_TYPE$
7930 CALL "EC3IC0",X3$,X4$,EC_PARM$,EC${ALL},FM1$(1,20),USAGE_KEYS$,CHANNELS{ALL},IC1INFO{ALL},FM3{ALL},FM3ALL{ALL},FM4$,FM4{ALL},FM4ALL{ALL},FORM_OTHER{ALL},FORM_OTHERALL{ALL},OPTIONS$
7940 REM "set other vars to local variables
7941 FM4_AVG_MOS_USAGE=FORM_OTHER[0],FM4_AVG_MOS_USAGEALL=FORM_OTHERALL[0]
7942 LAST_COMPLETED_PER_USAGE=FORM_OTHER[1],LAST_COMPLETED_PER_USAGEALL=FORM_OTHERALL[1]
7943 YTD_USAGE=FORM_OTHER[2],YTD_USAGE_ALL=FORM_OTHERALL[2]
7990 RETURN 
8000 REM "ck item secure codes against buyer record
8001 IF EC$[2](86,1)="Y" THEN GOTO 8030; REM "bypass if secure code super user
8002 ITEM_OK$="Y"
8004 IF ECR$(233,10)=DIM(10) THEN GOTO 8019
8005 ITEM_OK$="N"
8010 FOR ITEM_OK=0 TO 4
8012 TEMP$=ECR$(ITEM_OK*2+233,2); IF TEMP$=DIM(2) THEN GOTO 8018
8014 IF POS(TEMP$=EC$[2](233,20)+EC$[2](898,40),2)<>0 THEN EXITTO 8030
8018 NEXT ITEM_OK
8019 IF EC_PARM$(554,1)<>"Y" THEN GOTO 8030
8020 REM "ck for unlimited secure codes 153058
8021 DIM EDQ$(1000); EDQ$(1,20)=ECR$(1,20),EDQ$(21,2)="01"; READ (Z(18),KEY=EDQ$(1,22),DOM=8040)EDQ$
8022 CODES$=STP(EDQ$(23,978),3," "); IF CODES$="" THEN GOTO 8040 ! SSP173398, was GOTO 8030
8023 ITEM_OK$="N" ! SSP173398
8024 FOR ITEM_OK=0 TO LEN(CODES$)/2-1; CURRCODE$=CODES$(ITEM_OK*2+1,2)
8025 IF POS(CURRCODE$=EC$[2](233,20)+EC$[2](898,40),2)<>0 THEN EXITTO 8030
8026 NEXT ITEM_OK
8027 GOTO 8040
8030 ITEM_OK$="Y"
8040 RETURN 
8500 SET_READONLY:REM "Set the PRM READONLY to on
8510 SAVE_XI=PRM('XI'); SET_PARAM 'XI' ! Ignore exclusive locks, save previous setting
8525 SET_READONLY_END:RETURN 
8530 CLEAR_READONLY:REM "Restore previous setting
8535 SET_PARAM 'XI'=SAVE_XI ! restore previous setting
8545 CLEAR_READONLY_END:RETURN 
8910 DEF FND$(Z9$)=Z9$(1*2+1,2)+"/"+Z9$(7-1*2,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
9000 REM "ERROR PROCESSING
9002 J=ERR,K=TCB(5); SETERR 0000; ENDTRACE 
9010 LAST_WEBEC_ERR$="ERR: "+STR(J)+" AT: "+STR(K); LAST_WEBEC_ERR$=GBL("LAST_WEBEC_ERR",LAST_WEBEC_ERR$); REM "131928
9090 GOTO 9900
9900 REM "End
9905 SETERR 9910; GOSUB CLEAR_READONLY
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9950 EXIT 
9999 END 
56000 REM "192407-Buyer needs 20 additional secure codes.
56002 REM "200394-Support for 'D' type warehouses, new tag 'search_whse_type'
56003 REM "233219-Create excel versions of some of the EC html reports to use 
