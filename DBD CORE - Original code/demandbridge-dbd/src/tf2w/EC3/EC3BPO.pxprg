0010 REM "EC Buyer Previous Order Info <EC3BPO>"
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 12/23/14 - 12.530555 - dmm - SSP# 273337
0037 REM "273337-EC Approval; ability to list the requesting buyer's last 3  
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0085 SWITCH TCB(20) ! Number of param in CALL
0090 CASE 7; ENTER X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT; BREAK ! Standard Webec call entry point, from EC3SRV
0091 CASE 9; ENTER X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},BUYER$,OPTIONS$,LINE$,NUM_ORDS; BREAK ! Internal program call entry point, from non-EC3SRV programs
0095 CASE 12; ENTER X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},BUYER$,OPTIONS$,LINE_START_TAG$,LINE$,LINE_END_TAG$,BPO_STAT${ALL},NUM_ORDS; BREAK ! Internal program call entry point, from non-EC3SRV programs
0099 END SWITCH 
0100 SETERR 9000
0110 X0$="EC3BPO",X1$="EC Buyer Previous Order Info"
0120 EOL$=$0D0A$; EOL$=ATH(STP(EC_PARM$(57,8),1))
0127 HTML_TEXT$="Content-type: text/html"
0140 DIM MESS_INFO$[20]
0299 ! 
0300 REM "IOLISTS
0310 IOL_FT3:IOLIST FT3$,FT3{ALL}
0399 ! 
0500 REM "Open files
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O FT3...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 ! Set Masks
0610 COUNT_MASK$="###",M0$="########0",SELL_DIVISOR=1
0699 ! 
1000 REM "Process DATA array
1005 IF NOT(OUTPUT) THEN GOTO 1200 ! called mode, skip to main processing
1010 FOR I=1 TO NUM_ENTRIES
1015 TAG$=DATA$[I,0],VALUE$=DATA$[I,1]
1025 SWITCH TAG$ ! 1185 end switch
1180 DEFAULT ; VIA TAG$=VALUE$,ERR=*PROCEED; BREAK ! Set to same name variable as TAG$
1185 END SWITCH ! 1050
1190 NEXT I
1195 DIM BYBINFO$[5]; BYBINFO$[1]=PGN,BYBINFO$[2]="273337.20141216",BYBINFO$[3]=TEMPLATE$,BYBOPTIONS$=""
1199 ! 
1200 ! Get info
1205 GOSUB SET_READONLY
1295 MX=FN%_LOG_MESSAGE("MESG","SESSION_ID|"+EC$[1](1,7)+"|FILE|"+%WEBEC_FILE_NAME$+"|EC3BPO|BUYER|"+BUYER$+"|")
1300 IF STP(BUYER$,3)="" THEN GOTO 2985 ELSE BUYER$=PAD(BUYER$,15)
1305 GOSUB LIST_OF_ORDERS
1495 IF NOT(OUTPUT) THEN GOTO 1515 ! called mode, skip reading in the template file from disk, use passed in template snippet line$ instead
1499 ! 
1500 ! Fill out template
1504 IF NOT(NUL(%BASIC2$)) THEN GOTO 2985 ! Skip outputting template if additional program to be done
1505 CLOSE (100); OPEN (100,OPT="TEXT",ERR=9000)TEMPLATE$
1510 READ (100,END=*NEXT)TMP_LINE$; LINE$+=FNBYB$(TMP_LINE$)+EOL$; GOTO *SAME
1595 ! 
1600 IF NUL(LINE_START_TAG$) THEN LINE_START_TAG$="?tf?buyer_previous_orders_start?"
1602 IF NUL(LINE_END_TAG$) THEN LINE_END_TAG$="?tf?buyer_previous_orders_end?"
1615 CHKTAG=1
1620 WHILE CHKTAG ! 2800 wend
1622 P1=MSK(LINE$,"\?tf\?[^?]*\?"); IF P1=0 THEN CHKTAG=0; BREAK
1630 TAG$=LINE$(P1,MSL); LINE1$=LINE$(1,P1-1),LINE2$=MID(LINE$,P1+MSL)
1633 T_IN=POS("."=TAG$); IF T_IN THEN TAG_PARM$=MID(TAG$,T_IN+1),TAG$=MID(TAG$,1,T_IN-1) ELSE TAG_PARM$=""
1700 SWITCH TAG$ ! 2790 end switch
1730 CASE LINE_START_TAG$; GOSUB PROCESS_BPO_LINE; PP=FNP(BPO_SECTION$); BREAK
2785 DEFAULT ; LINE$=LINE1$+LINE2$ ! didn't match existing tag, so discard it
2790 END SWITCH ! 1700
2800 WEND ! 1620
2955 IF (OUTPUT) THEN PRINT (OUTPUT)LINE$
2985 GOSUB CLEAR_READONLY
2990 GOTO 9900
2999 ! 
6000 PROCESS_BPO_LINE:
6003 BPO_TAGS$="001<order_date>002<order_no>003<order_total>004<order_key>"
6005 BPO_SECTION$="",BPO_TEMPLATE$=LINE2$
6010 BPO_END=POS(LINE_END_TAG$=BPO_TEMPLATE$),BPO_TEMPLATE$=BPO_TEMPLATE$(1,BPO_END-1),LINE2$=LINE2$(BPO_END+LEN(LINE_END_TAG$))
6013 IF FOUND_ORDS=0 THEN GOTO 6195
6015 FOR BPO=1 TO FOUND_ORDS
6016 ORDER$=BPO_ORDERS$[BPO]
6017 DIM FT3$(606),FT3[13]; READ (Z[1],KNO=0,KEY=ORDER$,DOM=6190)IOL=IOL_FT3
6020 BPO_LINE$=BPO_TEMPLATE$
6025 BPO_INDEX=POS("?tf?"=BPO_LINE$); IF BPO_INDEX=0 THEN GOTO 6185
6030 BPOEND_INDEX=POS("?"=BPO_LINE$(BPO_INDEX+4)),BPO_TAG$=BPO_LINE$(BPO_INDEX+4,BPOEND_INDEX-1),SS1$=BPO_LINE$(1,BPO_INDEX-1),SS2$=BPO_LINE$(BPO_INDEX+BPOEND_INDEX+4),BPO_POS=POS("<"+BPO_TAG$+">"=BPO_TAGS$); IF BPO_POS=0 THEN BPO_LINE$=SS1$+SS2$,BPO_INDEX=0 ELSE BPO_INDEX=NUM(BPO_TAGS$(BPO_POS-3,3))
6035 ON BPO_INDEX GOTO 6179,6041,6042,6043,6044,6179
6041 IF STP(FT3$(16,6),3," ")="" THEN TMP$="" ELSE CALL "ZZDISP","DX",FT3$(16,6),"",X3$,TMP$,"",0,0,X4$ END_IF ; BPO_LINE$=SS1$+FN%HTML_ESC$(TMP$)+SS2$; GOTO 6180 ! order_date
6042 TMP$=""; CALL "ZZDISP","AX",FT3$(118,8),"O/P",X3$,TMP$,"",0,0,X4$; BPO_LINE$=SS1$+FN%HTML_ESC$(TMP$)+SS2$; GOTO 6180 ! order_number
6043 BPO_LINE$=SS1$+FN%HTML_ESC$(X3$(69,1)+STP(STR(FT3[6]:"###,###.00-"),1))+SS2$; GOTO 6180 ! order_total
6044 BPO_LINE$=SS1$+FN%HTML_ESC$(FT3$(118,8))+SS2$; GOTO 6180 ! order_key
6179 SS$=SS1$+"?tf?"+BPO_TAG$+"?"+SS2$; GOTO 6180 ! Leave unknown tags unchanged
6180 GOTO 6025
6185 BPO_SECTION$+=BPO_LINE$
6190 NEXT BPO
6195 RETURN 
6199 ! 
7600 SET_READONLY:
7610 SET_PARAM 'XI'
7620 SET_READONLY_END:RETURN 
7630 CLEAR_READONLY:
7640 SET_PARAM -'XI'
7645 CLEAR_READONLY_END:RETURN 
7649 ! 
7700 LIST_OF_ORDERS:! Get list of orders
7705 IF NUM_ORDS=0 THEN NUM_ORDS=3 ! Default number of previous orders is 3
7710 DIM BPO_ORDERS$[NUM_ORDS]
7715 READ (Z[1],KNO=7,KEY=BUYER$,DOM=*NEXT) ! key 7 is buyer,order date(descending),order div, order number
7720 NEXT_BPO_KEY: BPO_KEY$=KEY(Z[1],END=*RETURN); READ (Z[1])* ! Dummy var "*", prevent use of internal Z[1] IOLIST
7725 IF BPO_KEY$(1,15)<>BUYER$ THEN GOTO *RETURN
7730 FOUND_ORDS+=1,BPO_ORDERS$[FOUND_ORDS]=BPO_KEY$(22,8)
7735 IF FOUND_ORDS=NUM_ORDS THEN GOTO *RETURN
7740 GOTO NEXT_BPO_KEY
7745 RETURN 
7749 ! 
8750 FNP:
8751 DEF FNP(LOCAL DATA$)
8760 LINE$=LINE1$+DATA$+LINE2$
8790 RETURN 0
8799 ! ********************************************************
8800 FNBYB:
8801 DEF FNBYB$(LOCAL DATA$)
8802 ! Send DATA$ through EC3BYB
8820 CALL "EC3BYB",ERR=*NEXT,X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,DATA$,BYBINFO${ALL},BYBOPTIONS$
8840 RETURN DATA$
8845 END DEF
8899 ! **********************************************************
8915 FNT:DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
8930 DEF FNN$(Z9$)=TBL(Z9$<"A",STR(ASC(Z9$)-ASC("A")+10:"000"),STR(NUM(Z9$):"000")) ! CHAR(1) SEQ# (1-9,A-Z) => CHAR(3) SEQ# (1-35)
8949 ! *****************************
9000 REM "ERROR PROCESSING
9002 J=ERR,K=TCB(5); SETERR 0000; ENDTRACE 
9010 LAST_WEBEC_ERR$="ERR: "+STR(J)+" AT: "+STR(K); LAST_WEBEC_ERR$=GBL("LAST_WEBEC_ERR",LAST_WEBEC_ERR$); REM "131928
9090 GOTO 9900
9900 REM "End
9905 GOSUB CLEAR_READONLY
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9950 EXIT 
9999 END 
56000 REM "273337-EC Approval; ability to list the requesting buyer's last 3  
