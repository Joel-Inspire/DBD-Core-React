0010 REM "Dashboard Server <DBGSVR>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 07/15/10 - 19.506388 - crg - SSP# 230158
0037 REM "230158-Dashboards
0040 REM "Copyright 2010 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0110 X0$="DBGSVR",X1$="Dashboard Server"
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0250 IF %X3$="" THEN %X3$=X3$,%X4$=X4$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O DB0... 02O DB1... 03O DB2... 13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0599 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
0600 ! Initialize anything needed
0605 IF NOT(FN%HAS_MODULE("RP")) THEN GOTO 9900
0610 _X$=MSE; XCHARW=DEC($00$+_X$(10,1)),YCHARW=DEC($00$+_X$(11,1)),XMAX=DEC(_X$(27,2)),YMAX=DEC(_X$(29,2)),XCOL=INT(XMAX/XCHARW),YROW=INT(YMAX/YCHARW)
0620 LOOP_NUMBER=0
0625 CTL_LIST=HFN; OPEN (CTL_LIST)"*MEMORY*"
0630 PRINT '3D',
0699 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
0700 ! Pick Dashboard
0710 NUM_DBS=NUM(FIN(Z[1],"NUMREC")); IF NUM_DBS=0 THEN MSGBOX MSG("DBGSVR_00"),MSG("ATTN"),"!"; GOTO 9900 ELSE IF NUM_DBS=1 AND %C$="500" THEN DB_INDEX$=KEY(Z[1],ERR=*NEXT); GOTO HAVE_DASHBOARD
0715 IF %C$="500" AND WHO="crg" THEN PERFORM "RPGRPC.1;LIST_MY_DASHBOARDS"; DB_INDEX$=SEL_DB$; GOTO 0730
0720 PROCESS "QRY_DB0","../DBG/DB.EN",DB_INDEX$
0730 IF NUL(DB_INDEX$) THEN GOTO 9900 ELSE GOTO HAVE_DASHBOARD
0799 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
0800 HAVE_DASHBOARD:! DB_INDEX$ is set to index of dashboard to use
0805 DIM DASHBOARD$:IOL(Z[1])
0810 READ (Z[1],KNO=0,KEY=DB_INDEX$,REC=DASHBOARD$)
0825 UPLEFTX=INT((XCOL-DASHBOARD.WIDTH)/2),UPLEFTY=INT((YROW-DASHBOARD.HEIGHT)/2)
0830 %DB_UP_LEFT_X=UPLEFTX,%DB_UP_LEFT_Y=UPLEFTY
0865 IF NOT(%GUI) THEN PRINT 'SHOW'(-1)
0870 PRINT 'DIALOGUE'(UPLEFTX,UPLEFTY,DASHBOARD.WIDTH,DASHBOARD.HEIGHT,5,STP(DASHBOARD.TITLE$),'_BLACK'+'WHITE',OPT="_"),'SR','CS',
0875 PRINT 'CS', ! Clear background
0899 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
1000 PROCESSING_LOOP:
1010 LOOP_NUMBER+=1,GADGET_NUMBER=0
1045 READ (Z[3],KNO=1,KEY=DASHBOARD.DASHBOARD_INDEX$,DOM=*PROCEED)
1050 SELECT IOL=IOL(Z[3]),REC=GADGET$ FROM Z[3],KNO=1 WHERE GADGET.DASHBOARD_INDEX$=DASHBOARD.DASHBOARD_INDEX$
1060 IF LOOP_NUMBER=1 OR MOD(LOOP_NUMBER,MAX(GADGET.PRIORITY,1))=0 THEN {
1065 READ (Z[2],KEY=GADGET.GADGET_INDEX$,DOM=*CONTINUE,REC=GADGET_INFO$) ! If not found, then CONTINUE to next record
1100 READ DATA FROM REC(IOL(Z[3]),REC=GADGET$),REC=%CURR_GADGET$ TO IOL=IOL(Z[3])
1105 IF LOOP_NUMBER=1 THEN GADGET_FLAG$="%GADGET_"+GADGET.DB_GADGET_INDEX$; VIA GADGET_FLAG$=0 ! If first time through dashboard, clear gadget's firstime flag, this is mainly a problem during development and testing
1109 ! Each GADGET is allowed to use controls in the range 15000+(100*GADGET_NUMBER) to 15000+(100*GADGET_NUMBER)+99
1110 DIM OPTIONS$[5]
1120 OPTIONS$[0]=GADGET.OPTIONS$
1140 CALL STP(GADGET_INFO.CALLED_PROG$),ERR=*CONTINUE,X3$,X4$,CTL_LIST,++GADGET_NUMBER,OPTIONS${ALL},RETURN$
1150  } ! mod(loop_number,gadget.priority)
1195 NEXT RECORD 
1199 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~
1200 GADGETS_DONE:! Finished Gadgets, now do global stuff, and wait
1210 OBTAIN (0,TIM=DASHBOARD.WAIT_SECONDS,ERR=*NEXT)'C0',IN$
1215 CTL_VAL=CTL; IF CTL_VAL=4 THEN PRINT 'POP',; GOTO 9900
1220 CTL_VAL$=STR(CTL_VAL); FIND (CTL_LIST,KEY=CTL_VAL$,DOM=*NEXT)ACTION$,OPTIONS$; CALL ACTION$,X3$,X4$,OPTIONS$
1895 GOTO PROCESSING_LOOP
1950 PROCESSING_DONE:
1960 PRINT 'POP'
1980 ESCAPE 
1995 GOTO 9900
1999 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9001 MSGBOX "ERR IN DBGSVR"; PRINT 'SHOW'(1); ESCAPE 
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9945 PRINT 'SHOW'(1)
9950 RUN "ZMENU"
9999 END 
