0010 ! ZZWFTO - Copy list_box data from F_CODE.CTL to T_CODE.CTL
0020 ! assumes that from and to boxes are report view
0030 ! Note: If there is a limit to the number of records allowed then
0040 ! set MAX_ALLOW in the performing program
0050 ! 
1000 ADD:! ^1000,5 - Add
1005 FROM_BOX=F_CODE.CTL,TO_BOX=T_CODE.CTL
1010 GOSUB 1400
1015 EXIT 
1020 ! 
1100 ADD_ALL:! ^100,5 - Add All
1105 FROM_BOX=F_CODE.CTL,TO_BOX=T_CODE.CTL
1110 GOSUB 1600
1115 EXIT 
1120 ! 
1200 REMOVE:! ^100,5 - Remove
1205 FROM_BOX=T_CODE.CTL,TO_BOX=F_CODE.CTL
1210 GOSUB 1400
1215 EXIT 
1220 ! 
1300 REMOVE_ALL:! ^100,5 - Remove All
1305 FROM_BOX=T_CODE.CTL,TO_BOX=F_CODE.CTL
1310 GOSUB 1600
1315 EXIT 
1320 ! 
1400 ! ^100,5 - Move selected items from one list box to another
1405 X$="",HIGH_LINE=1
1410 LIST_BOX READ FROM_BOX,X$,ERR=*NEXT
1415 IF X$="" THEN MSGBOX "Nothing selected - please select a line or lines and try again.","Attention"; NEXT_ID=FROM_BOX,REFRESH_FLG=1; GOTO *RETURN
1420 T_CODE_SORT=FROM_BOX'SORT; IF T_CODE_SORT=0 THEN T_CODE_SORT=1
1425 NUM_SEL=FROM_BOX'SELECTCOUNT,TEST_COUNT=0
1430 FOR LINE=NUM_SEL TO 1 STEP -1
1435 FROM_BOX'SELECTINDEX=LINE,LINE_INDEX=FROM_BOX'SELECTITEM
1440 IF LINE_INDEX>HIGH_LINE THEN HIGH_LINE=LINE_INDEX
1445 IF TO_BOX=T_CODE.CTL THEN TEST_COUNT=TO_BOX'ITEMCOUNT
1450 IF TEST_COUNT<MAX_ALLOW OR MAX_ALLOW=0 THEN {
1455 LIST_BOX FIND FROM_BOX,LINE_INDEX,LINE$
1460 LIST_BOX LOAD TO_BOX,0,LINE$
1465 LIST_BOX LOAD FROM_BOX,LINE_INDEX,*
1470  }
1475 NEXT LINE
1480 TO_BOX'SORT=T_CODE_SORT,NEXT_ID=FROM_BOX,REFRESH_FLG=1,HIGH_LINE=HIGH_LINE-NUM_SEL+1
1485 IF HIGH_LINE>=FROM_BOX'ITEMCOUNT THEN HIGH_LINE=FROM_BOX'ITEMCOUNT
1490 LIST_BOX WRITE FROM_BOX,HIGH_LINE
1495 RETURN 
1500 ! 
1600 ! ^100,5 - Move all items from one box to another
1605 NUM_REC=FROM_BOX'ITEMCOUNT,TEST_COUNT=0
1610 T_CODE_SORT=FROM_BOX'SORT; IF T_CODE_SORT=0 THEN T_CODE_SORT=1
1615 FOR LINE=NUM_REC TO 1 STEP -1
1620 IF TO_BOX=T_CODE.CTL THEN TEST_COUNT=TO_BOX'ITEMCOUNT
1625 IF TEST_COUNT<MAX_ALLOW OR MAX_ALLOW=0 THEN {
1630 LIST_BOX FIND FROM_BOX,LINE,LINE$
1635 LIST_BOX LOAD TO_BOX,0,LINE$
1640 LIST_BOX LOAD FROM_BOX,LINE,*
1645  }
1650 NEXT LINE
1655 TO_BOX'SORT=T_CODE_SORT,NEXT_ID=TO_BOX,REFRESH_FLG=1
1660 LIST_BOX WRITE TO_BOX,1,ERR=*NEXT
1665 RETURN 
1670 ! 
