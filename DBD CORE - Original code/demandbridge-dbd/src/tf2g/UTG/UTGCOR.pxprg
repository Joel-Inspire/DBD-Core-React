1000 ! 1000,10
1005 ENTER TITLE$,TEXT$,X,Y,L,H
1010 BUTTON -30000,@(0,0,0,0)=""
1020 X=-30000
1030 FONT$=STP(X'FONT$,3,QUO)
1040 IF UCS(MID(FONT$,1,6))="SYSTEM" THEN FONT$="Tahoma,-11,W"
1050 BUTTON REMOVE -30000
1060 ! 
1070 M$=MSE ! Get window size
1080 CHR_WD=DEC($00$+M$(10,1)),CHR_HI=DEC($00$+M$(11,1))
1090 SCRN_WD=DEC($00$+M$(27,2)),SCRN_HI=DEC($00$+M$(29,2))
1100 ! 
1110 MAX_WD=INT((SCRN_WD*5)/8)-15-8
1120 IF BITMAP$<>"" THEN MAX_WD-=48
1130 ! 
1140 MAX_CH=INT(MAX_WD/CHR_WD)
1150 ! 
1160 ! Compute Graphic unit to pixel conversion
1170 ! 
1180 FACTOR=16/CHR_WD ! 16 Graphic units per character
1190 ! 
1200 TEXT$=SUB(TEXT$,SEP,$0D0A$)
1210 ! 
1220 IF LEN(SUB(STP(TEXT$),$0D0A$,""))<LEN(TITLE$) THEN TXT_WD=TXW(TITLE$,FNT=FONT$,SIZ=INT(16*MAX_CH))+(10*LEN(TITLE$)); GOTO NXT
1230 TXT_WD=TXW(TEXT$,FNT=FONT$,SIZ=INT(16*MAX_CH))
1240 NXT:
1250 TXT_HI=TXH(TEXT$,FNT=FONT$,SIZ=TXT_WD+6)
1260 ! 
1270 WDW_WD=TXT_WD/FACTOR,WDW_HI=TXT_HI/FACTOR
1280 TOP_OFS=0
1290 ! 
1300 IF BITMAP$<>"" THEN WDW_WD+=48; IF WDW_HI<48 THEN TOP_OFS=(48-WDW_HI)/2,WDW_HI=48
1310 ! 
1320 WDW_WD+=15+8
1330 ! 
1340 MIN_WD=CHR_WD*(2+2+10*N_BTN-1)
1350 IF WDW_WD<MIN_WD THEN WDW_WD=MIN_WD
1360 ! 
1370 WDW_HI+=CHR_HI*(1+2+1)
1380 ! 
1390 WDW_W=INT(WDW_WD/CHR_WD)+1
1400 WDW_H=INT(WDW_HI/CHR_HI)
1410 ! 
1420 SCRN_WD=INT(SCRN_WD/CHR_WD)
1430 SCRN_HI=INT(SCRN_HI/CHR_HI)
1440 ! 
1450 WDW_C=INT((SCRN_WD-WDW_W)/2)
1460 WDW_L=INT((SCRN_HI-WDW_H)/2)+2 ! Allow 2 lines for window frame
1470 ! 
1480 X=WDW_C,Y=WDW_L,L=WDW_W,H=WDW_H
1490 EXIT 
