0010 ! UTGFLA - File Layout Display
0035 REM "5.5 - 03/01/07 - 13.439166 - mhe - SSP# 205471
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 PROCESS "UTGFLA","../UTG/UT.EN"
0060 EXIT 
0100 CLEAR 
0108 INIT:
0110 _ZZP=HFN; OPEN (_ZZP)"ZZPARM"; READ (_ZZP,KEY=FID(0)+"      ")X3$; CLOSE (_ZZP)
0130 C0$=X3$(9,3)
0190 _ZZE=FN%FFN("ZZE"); IF _ZZE<=0 THEN _ZZE=HFN; OPEN (_ZZE)"ZZE" ! [205471]-changed FFN() to FN%FFN()
0195 _ZZF=FN%FFN("ZZF"); IF _ZZF<=0 THEN _ZZF=HFN; OPEN (_ZZF)"ZZF" ! [205471]-changed FFN() to FN%FFN()
0200 DIM Z0$(80,"-")
0210 M0$="##,##0-",L=8,L9=L,NEXT_ID=ML_FILE_NAME.CTL
0220 F0$="IndexedSerial Direct Sort   ProgramText    Graphic 777777778888888899999999Directry",%F0$=F0$
0225 RETURN 
9999 END 
10000 ! 10000,10
10010 VAL:
10020 ENTER VAL$
10030 IF NUM(VAL$(48,2))<>0 THEN %KEY$=VAL$(48,2) ELSE %KEY$="n/a"
10040 IF VAL$(47,1)<>"S" THEN %REC$=VAL$(50,4) ELSE %REC$="n/a"
10050 IF VAL$(47,1)=" " THEN VAL$(47,1)="D"
10060 %TYPE$=MID(%F0$,POS(VAL$(47,1)="IzDSTX")*7-6,7)
10070 EXIT 
10080 SELECT_FILE_NAME: ML_FILE_NAME$=PAD(ML_FILE_NAME$,6),REFRESH_FLG=1,ZZE_NAME$=STP(ML_FILE_NAME$)
10090 FIND (_ZZE,KEY=ML_FILE_NAME$)A$
10100 IF NUM(A$(48,2))<>0 THEN ZZE_KS$=A$(48,2) ELSE ZZE_KS$="n/a"
10110 IF A$(47,1)<>"S" THEN ZZE_BYTES$=A$(50,4) ELSE ZZE_BYTES$="n/a"
10120 IF A$(47,1)=" " THEN A$(47,1)="D"
10130 ZZE_TYPE$=%F0$(POS(A$(47,1)="IzDSTX")*7-6,7)
10140 ML_FILE_DESC$=A$(7,39),NEXT_ID=BT_EXIT.CTL
10150 GOSUB LOAD_LIST_BOX
10155 GOSUB CHECK_FILE_NAME
10160 RETURN 
10170 NO_ZZE: ML_FILE_NAME$="",NEXT_ID=ML_FILE_NAME.CTL; EXIT 
10180 LOAD_LIST_BOX: IDX=0
10190 READ (_ZZF,KEY=ML_FILE_NAME$,DOM=*NEXT)
10200 READ (_ZZF,END=END_ZZF)B$
10210 IF MID(B$,1,6)<>ML_FILE_NAME$ THEN GOTO END_ZZF
10220 LIST_BOX LOAD LB_LINE.CTL,0,STP(B$(13,40))+SEP+B$(7,1)+SEP+B$(53,1)+SEP+B$(10,3)+SEP+B$(56,3)+SEP+B$(59,9)+SEP+B$(68,8)+SEP
10230 IDX=IDX+1
10240 GOTO 10200
10250 END_ZZF:RETURN 
10260 DELETE_LIST_BOX:
10270 HIDE CONTROL LB_LINE.CTL
10280 FOR A=1 TO IDX
10290 LIST_BOX LOAD LB_LINE.CTL,1,*,ERR=END_DEL
10300 NEXT A
10310 SHOW CONTROL LB_LINE.CTL; GOSUB CLEAR_REC; RETURN 
10320 END_DEL:EXITTO 10310
10400 CHECK_FILE_NAME:
10410 FIL=HFN; OPEN (FIL,ERR=NO_FILE)ML_FILE_NAME$(1,3)+%C$
10420 DSK_NAME$=FIN(FIL,"FILENAME"),DSK_KS$=FIN(FIL,"KSZ"),DSK_BYTES$=FIN(FIL,"RECORD_SIZE"),DSK_RECS$=FIN(FIL,"RECORD_ALLOWED"),DSK_USED$=FIN(FIL,"RECORDS_USED"),DSK_VAIL$=STR(NUM(DSK_RECS$)-NUM(DSK_USED$)),DSK_TYPE$=ZZE_TYPE$
10430 CLOSE (FIL)
10435 RETURN 
10440 NO_FILE:
10450 OPEN (FIL,ERR=NO_MORE)STP(ML_FILE_NAME$)
10460 DSK_NAME$=STP(ML_FILE_NAME$),DSK_KS$=FIN(FIL,"KSZ"),DSK_BYTES$=FIN(FIL,"RECORD_SIZE"),DSK_RECS$=FIN(FIL,"RECORD_ALLOWED"),DSK_USED$=FIN(FIL,"RECORDS_USED"),DSK_VAIL$=STR(NUM(DSK_RECS$)-NUM(DSK_USED$)),DSK_TYPE$=ZZE_TYPE$
10470 CLOSE (FIL)
10480 RETURN 
10485 NO_MORE:
10490 DSK_NAME$="Not defined"
10500 RETURN 
10600 CLEAR_REC:
10610 ML_FILE_NAME$="",DSK_NAME$="",DSK_KS$="",DSK_BYTES$="",DSK_RECS$="",DSL_USED$="",DSK_VAIL$="",DSK_TYPE$=""
10620 ZZE_NAME$="",ZZE_KS$="",ZZE_BYTES$="",ZZE_RECS$="",REFRESH_FLG=1,ZZE_TYPE$=""
10630 RETURN 
56000 REM + Modification History
56002 REM "205471-Oracle - FFN usage in tf2g progs. to be replaced by FN%FFN  
