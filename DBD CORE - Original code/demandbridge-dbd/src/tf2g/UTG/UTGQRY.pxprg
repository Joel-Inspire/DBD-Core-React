0010 ! UTGQRY - Export to Excel 
0035 REM "5.7 - 10/03/12 - 16.885833 - crg - SSP# 258531
0037 REM "258531-Export to Excel from WebEc Customer Parameters - Failed to  
0040 REM "Copyright 2012 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 MSGBOX "Missing entry point specification on call","UTGEXP","!"; EXIT 45
0060 END 
0070 ! 
0080 EXPORT:
0085 IF %EXPORT_TO_EXCEL THEN MSGBOX MSG("NOT_AUT"),MSG("WARNING"),"STOP"; EXIT 
0090 ! 
0091 SWITCH %X3_DATE_FMT$ ! SSP 208551
0092 CASE "1"
0093 DF$="mm"+%X3_DATE_DLM$+"dd"+%X3_DATE_DLM$+"yyyy"; BREAK
0094 CASE "2"
0095 DF$="dd"+%X3_DATE_DLM$+"mm"+%X3_DATE_DLM$+"yyyy"; BREAK
0096 CASE "3"
0097 DF$="yyyy"+%X3_DATE_DLM$+"mm"+%X3_DATE_DLM$+"dd"; BREAK
0098 END SWITCH 
0100 ! ENTER _EXCEL_REC$,_EXCEL_HEADER$,_EXCEL_LEN$,_EXCEL_TXT$,COL,_NXREF,_XFN{ALL},_XREC{ALL},_IN_FN,_TRN_TBL$,_XKEY${ALL},_XKNO{ALL},_FORMAT_PGM${ALL},_NO_DICT,_MEN_FLG,_NME_TBL${ALL},_FLD_NME${ALL},_R_MIN$,_R_MAX$,_EXCEL_STM$,_KNUM,RESULT$,ERR=*NEXT ! SSP 22145
0101 LOCAL COL,_KNUM; COL=_TOT_COL,_KNUM=_SORTKEY
0105 NUMREC=NUM(FIN(_IN_FN,"NUMREC")),TEXT$=_EXCEL_TXT$; REC$=SUB(_EXCEL_REC$,SEP,"+"+QUO+$09$+QUO+"+"),REC$=REC$(1,LEN(REC$)-1),COUNT_REC=1,SHEET=1,TOTAL=NUMREC
0106 DIM _MN_.FIELD$[10]
0107 IF _EXCEL_STM$<>"" THEN XX=POS("THEN A="=_EXCEL_STM$); IF XX<>0 THEN NEW_STM$=_EXCEL_STM$(1,XX-1)+" THEN GOTO *NEXT ELSE GOTO NXT_REC",NEW_STM$(1,5)="01185"; EXECUTE NEW_STM$
0108 IF RESULT$<>"" AND _MEN_FLG<>1 THEN XX=POS("THEN"=UCS(RESULT$),-1),RR$=RESULT$,RR$(1,5)="01162",RR$=RR$(1,XX+4)+" GOTO NXT_REC"; EXECUTE RR$ ! SSP 221450
0110 IF DEC(MID(MSE,22,1))>3 THEN WINDX=1,WDX$="[wdx]" ELSE WINDX=0,WDX$=""
0120 _FIL_NAME$=FIN(_IN_FN,"FILENAME"); GOSUB EXCEL
0125 CALL "$CTL-10;AGENT_MSG",ERR=*NEXT,"Select Auto Formating",0
0130 WRITE RECORD (EXCEL)"[Select("+QUO+"r1c1:r1c1"+QUO+")]"; WRITE RECORD (EXCEL,ERR=*NEXT)"[Format.Auto]"
0140 GOTO 0190
0150 FOR REC=1 TO LB_VAR'ITEMCOUNT
0160 K$="R"+STR(R+REC)+"C1:R"+STR(R+REC)+"C"+STR(COL)
0170 IF COLOR[REC,0] THEN WRITE RECORD (EXCEL)"[Select("+QUO+K$+QUO+")]"; WRITE RECORD (EXCEL)"[Patterns(1,1,"+STR(COLOR[REC,0])+",0)]"; WRITE RECORD (EXCEL)"[format.font("+QUO+"Ariel"+QUO+",10,0,0,0,0,"+STR(COLOR[REC,1])+",0,0)]"
0180 NEXT 
0190 WRITE RECORD (EXCEL)"[Select("+QUO+"r1c1:r1c1"+QUO+")]"
0195 CALL "$CTL-10;AGENT_MSG",ERR=*NEXT,%USER_NAME$+", Export Done",1
0200 CLOSE (EXCEL,ERR=*NEXT)
0205 CALL "[WDX]*CMDWORD;CLOSE",ERR=*NEXT,OB_EXCEL
0207 PRINT 'MESSAGE'('F8'+'BLACK'+"",0)
0210 EXIT 
1000 EXCEL:! 1000,10
1005 CALL "$CTL-10;AGENT_MSG",ERR=*NEXT,"Creating Header",0
1010 R=0
1020 ! IF WINDX THEN EXECUTE "[wdx]invoke "+QUO+"c:\program Files\Microsoft Office\Office\excel.exe"+QUO ELSE INVOKE "c:\program Files\Microsoft Office\Office\excel.exe"
1030 WAIT 2
1040 GOSUB CREATE_HEADER
1042 CALL "[wdx]*CMDWORD;GET_EXCEL",ERR=*NEXT,OB_EXCEL,TF_NAME$; IF TF_NAME$<>"" THEN GOTO 1048
1045 SYSTEM_HELP "excel.exe"
1048 WAIT 1
1050 EXCEL=HFN; IF TF_NAME$="" THEN OPEN (EXCEL,ERR=NO_FOUND)"[WDX][dde]excel;" ELSE OPEN (EXCEL,ERR=NO_FOUND)"[WDX][DDE]Excel;"+TF_NAME$
1060 R=1
1070 K$="R"+STR(R)+"C1:R"+STR(R)+"C"+STR(COL)
1080 WRITE RECORD (EXCEL,KEY=K$)HEADER$; COL_LEN=0
1085 IF NUMREC>65536 THEN SEL=65536,NUMREC=NUMREC-65535 ELSE SEL=NUMREC+1
1090 TEXT=0 ! len of all column
1100 XX=POS(","=ALL_LEN$); IF XX=0 THEN GOTO 1150
1110 COL_LEN+=1,VAL$=ALL_LEN$(1,XX-1),ALL_LEN$=ALL_LEN$(XX+1)
1120 BB$="[SELECT("+QUO+"R1C"+STR(COL_LEN)+":R"+STR(SEL)+"C"+STR(COL_LEN)+QUO+","+QUO+"R"+STR(SEL)+"C"+STR(COL_LEN)+QUO+")]"; WRITE RECORD (EXCEL)BB$
1130 WRITE RECORD (EXCEL)"[Column.width("+VAL$+")]"; IF COL_LEN<=LEN(TEXT$) AND TEXT$(COL_LEN,1)="@" THEN WRITE RECORD (EXCEL)"[Format.Number("+QUO+TEXT$(COL_LEN,1)+QUO+")]" ELSE IF COL_LEN<=LEN(TEXT$) AND TEXT$(COL_LEN,1)=" " THEN WRITE RECORD (EXCEL)"[Format.Number("+QUO+"###,###,###.00_);[RED](###,###,###.00)"+QUO+")]" ELSE IF COL_LEN<=LEN(TEXT$) AND TEXT$(COL_LEN,1)="D" THEN WRITE RECORD (EXCEL)"[Format.Number("+QUO+DF$+QUO+")]" ! SSP 208551
1140 GOTO 1100
1150 WRITE RECORD (EXCEL)"[SELECT(""R1C1:R1"+X$+"C1"","""+"R1"+"C1"")]"
1155 IF SHEET<>1 THEN RETURN 
1156 READ (_IN_FN,KEY=_R_MIN$,KNO=_KNUM,DOM=*NEXT)
1160 NXT_REC: K_IN$=KEY(_IN_FN,END=END_OF_FILE); READ (_IN_FN,ERR=*NEXT)
1165 IF STP(_R_MAX$)<>"" AND MID(K_IN$,1,LEN(_R_MAX$))>_R_MAX$ THEN GOTO END_OF_FILE
1166 PERFORM "*winqry;select_record"; IF _REJECT<>0 THEN GOTO NXT_REC
1168 FOR _MN_COUNT=10 TO 1 STEP -1; _MN_.FIELD$[_MN_COUNT]=_MN_.FIELD$[_MN_COUNT-1]; NEXT _MN_COUNT
1180 ! GOSUB READ_XREF_FILES; GOSUB CALL_FORMATTER
1190 TF_VALUE$=EVS(REC$,ERR=NXT_REC),REC+=1
1195 IF POS("msglib"=PTH(_IN_FN))<>0 THEN TF_VALUE$=K_IN$+TF_VALUE$
1200 COLOR=0,COLOR1=0; IF POS($1B$=TF_VALUE$)=0 THEN GOTO 1400
1210 ! 
1220 ! Begin Check for Color
1230 ! 
1240 COLOR=0,COLOR1=0
1250 IF POS($1B53461B4236$=TF_VALUE$) THEN COLOR=8,COLOR1=1 ! Cyan
1260 IF POS($1B42321B4637$=TF_VALUE$) THEN COLOR=4,COLOR1=2 ! Green
1270 IF POS($1B4232$=TF_VALUE$) THEN COLOR=4,COLOR1=2 ! Green
1280 IF POS($1B53461B4232$=TF_VALUE$) THEN COLOR=4,COLOR1=1 ! Green
1290 IF POS($1B4234$=TF_VALUE$) THEN COLOR=5 ! Blue
1300 IF POS($1B42341B4637$=TF_VALUE$)<>0 OR POS($1B4634$=TF_VALUE$)<>0 THEN COLOR=5,COLOR1=2 ! Blue
1310 IF POS($1B4252$=TF_VALUE$)<>0 THEN COLOR=1,COLOR1=2
1320 IF POS($1B53461B4233$=TF_VALUE$) THEN COLOR=6,COLOR1=1 ! Yellow
1330 IF POS($1B53461B4231$=TF_VALUE$)<>0 OR POS($1B4631$=TF_VALUE$)<>0 THEN COLOR=3,COLOR1=2 ! Red
1340 IF POS($1B42321B4637$=TF_VALUE$) THEN COLOR=10,COLOR1=2 ! Green
1350 TF_VALUE$=SUB(TF_VALUE$,$1B53461B4236$,""),TF_VALUE$=SUB(TF_VALUE$,$1B53461B4232$,""),TF_VALUE$=SUB(TF_VALUE$,$1B53461B4231$,""),TF_VALUE$=SUB(TF_VALUE$,$1B4631$,""),TF_VALUE$=SUB(TF_VALUE$,$1B53461B4233$,""),TF_VALUE$=SUB(TF_VALUE$,$1B4252$,""),TF_VALUE$=SUB(TF_VALUE$,$1B4234$,""),TF_VALUE$=SUB(TF_VALUE$,$1B4637$,""),TF_VALUE$=SUB(TF_VALUE$,$1B42321B4637$,""),TF_VALUE$=SUB(TF_VALUE$,$1B1B42341B4637$,""),TF_VALUE$=SUB(TF_VALUE$,$1B4232$,""),TF_VALUE$=SUB(TF_VALUE$,$1B4634$,"")
1360 ! 
1370 ! Ending Check For Color
1380 ! 
1390 ! XX=POS(SEP=TF_VALUE$); IF XX<>0 THEN TF_VALUE$=TF_VALUE$(XX+1)
1400 XX=POS(SEP=TF_VALUE$); IF XX<>0 THEN TF_VALUE$(XX,1)=$09$; GOTO 1400
1410 K$="R"+STR(R+COUNT_REC)+"C1:R"+STR(R+COUNT_REC)+"C"+STR(COL)
1420 IF POS(" "=TEXT$) THEN GOSUB CHECK_NUMERIC
1430 WRITE RECORD (EXCEL,KEY=K$)TF_VALUE$
1440 IF COLOR THEN COLOR[REC,0]=COLOR,COLOR[REC,1]=COLOR1 ! THEN WRITE RECORD (EXCEL)"[Select("+QUO+K$+QUO+")]"; WRITE RECORD (EXCEL)"[Patterns(1,1,"+STR(COLOR)+",0)]"; COLOR=0
1445 COUNT_REC+=1; IF COUNT_REC>65535 THEN GOSUB NEW_SHEET
1447 IF MOD(REC,500)=0 THEN CALL "$CTL-10;AGENT_MSG","Please Wait, Exporting to Excel :"+STR(INT(REC/TOTAL*100))+"%"
1448 IF TOTAL>1000 AND MOD(REC,100)=0 THEN WRITE RECORD (EXCEL)"[SELECT(""R"+STR(COUNT_REC)+"C1:R"+STR(COUNT_REC)+X$+"C1"","""+"R"+STR(COUNT_REC)+"C1"")]"
1449 IF MOD(REC,500)=0 THEN PRINT 'MESSAGE'('_RED'+'WHITE'+"Please Wait, Exporting to Excel Records:"+STR(REC)+" of "+STR(TOTAL)+" - "+STR(INT(REC/TOTAL*100))+"%",0)
1450 GOTO NXT_REC
1455 END_OF_FILE:
1460 FMT$=B_FMT$
1470 WRITE RECORD (EXCEL,ERR=*NEXT)"[Select("+QUO+"r"+STR(REC)+"c1:r"+STR(REC)+"c1"+QUO+")]"
1480 RETURN 
1490 CREATE_HEADER:
1500 HEADER$=_EXCEL_HEADER$,HEADER$=SUB(HEADER$,SEP,$09$)
1510 ALL_LEN$=_EXCEL_LEN$; RETURN 
1520 XX=POS("["=FMT$); IF XX=0 THEN RETURN 
1530 XXX=POS("]"=FMT$(XX)),COL=COL+1
1540 TMP$=FMT$(XX+1,XXX-1-XX),FMT$=FMT$(XXX+XX),HEADER$=HEADER$+TMP$+$09$
1550 XX=POS(" "=FMT$),TMP$=FMT$(1,XX-1),FMT$=FMT$(XX+1)
1560 ! Rutine for len
1570 IF TMP$(1,1)="#" THEN TEXT$+=" " ELSE TEXT$+="@"
1580 IF POS(TMP$(1,1)="0123456789")=0 THEN TMP$=TMP$(2); GOTO 1580
1590 ALL_LEN$=ALL_LEN$+TMP$+","
1600 GOTO 1520
5000 XML:! 5000,10
5010 ENTER LB_VAR
5020 GOSUB CREATE_HEADER; FMT$=B_FMT$
5030 ! _WORD=HFN; OPEN (_WORD)"[dde]winword;"
5040 TMP$="/tmp/"+LOG_FILE$+FID(0)+".xml"; ERASE TMP$,ERR=*NEXT
5050 SERIAL TMP$; TMP=HFN; OPEN LOCK (TMP)TMP$; GOSUB CREATE_DEF
5060 PRINT (TMP)"<"+LOG_FILE$+">"
5070 FOR REC=1 TO LB_VAR'ITEMCOUNT
5080 LIST_BOX FIND LB_VAR,REC,TF_VALUE$
5090 XX=POS(SEP=TF_VALUE$,-1),TF_VALUE$=TF_VALUE$(1,XX-1),XX=POS(SEP=TF_VALUE$,-1),REC_KEY$=TF_VALUE$(XX+1),TF_VALUE$=TF_VALUE$(1,XX)
5100 REC_KEY$=SUB(SUB(SUB(SUB(SUB(REC_KEY$,"<","&lt;"),">","&gt;"),"&","&amp;"),"'","&apos;"),QUO,"&quot;")
5110 YES=0
5120 XX=POS(CHR(127)<=REC_KEY$); IF XX<>0 THEN YES=1,TC$=REC_KEY$(XX,1); REC_KEY$=SUB(REC_KEY$,TC$,"&#"+STR(ASC(TC$))+";"); GOTO *SAME
5130 PRINT (TMP)"<Record Key="+QUO+REC_KEY$+QUO+">"
5140 B_HEADER$=HEADER$,B_HTML_TO_FLD$=HTML_TO_FLD$
5150 NXT_FLD: VAL1=POS(SEP=TF_VALUE$); IF VAL1=0 THEN GOTO END_REC ELSE VAL1$=TF_VALUE$(1,VAL1-1),TF_VALUE$=TF_VALUE$(VAL1+1)
5160 VAL1=POS($09$=HTML_TO_FLD$); IF VAL1=0 THEN CONTINUE ELSE VAL2$=HTML_TO_FLD$(1,VAL1-1),HTML_TO_FLD$=HTML_TO_FLD$(VAL1+1)
5170 VAL1=POS($09$=HEADER$); IF VAL1=0 THEN CONTINUE ELSE VAL3$=HEADER$(1,VAL1-1),HEADER$=HEADER$(VAL1+1)
5180 VAL3$=SUB(SUB(SUB(SUB(SUB(VAL3$,"<","&lt;"),">","&gt;"),"&","&amp;"),"'","&apos;"),QUO,"&quot;")
5190 VAL1$=SUB(SUB(SUB(SUB(SUB(VAL1$,"<","&lt;"),">","&gt;"),"&","&amp;"),"'","&apos;"),QUO,"&quot;")
5200 IF YES THEN XX=POS(CHR(127)<=VAL1$); IF XX<>0 THEN YES=1,TC$=VAL1$(XX,1); VAL1$=SUB(VAL1$,TC$,"&#"+STR(ASC(TC$))+";"); GOTO *SAME
5210 PRINT (TMP)DIM(5)+"<"+LCS(VAL2$)+" Desc="+QUO+STP(VAL3$)+QUO+">"+STP(VAL1$)+"</"+LCS(VAL2$)+">"
5220 GOTO NXT_FLD
5230 END_REC:
5240 PRINT (TMP)"</Record>"
5250 HEADER$=B_HEADER$,HTML_TO_FLD$=B_HTML_TO_FLD$
5260 ! CLIP_BOARD WRITE REC$; REC$=""; WRITE RECORD (_WORD)"[editpaste]"; WRITE RECORD (_WORD)"[InsertPara]"; L=0
5270 NEXT 
5280 PRINT (TMP)"</"+LOG_FILE$+">"
5290 CLOSE (TMP)
5300 GOSUB SEND_FILES
5310 RETURN 
5320 SEND_FILES:
5330 F$="\\"+NID+TMP$
5340 XX=POS("/"=TMP$); IF XX<>0 THEN TMP$(XX,1)="\"; GOTO 5340
5350 MSGBOX "File Done: "+TMP$+SEP+SEP+"Do you Wish to View File?",MSG("CONFIRMING"),"INFO,YESNO",OPT$; IF OPT$="NO" THEN RETURN 
5360 OPEN (1,ERR=*NEXT)"[dde]explorer;\\"+NID+TMP$
5370 RETURN 
5380 SYSTEM_HELP "winword.exe"
5390 WAIT 2
5400 _WINWORD=HFN; OPEN (_WINWORD)"[dde]winword;"
5410 WRITE RECORD (_WINWORD)"[InsertFile .Name="+QUO+F$+QUO+"]"
5420 WAIT 3
5430 WRITE RECORD (_WINWORD)"[EditSelectAll]"
5440 WAIT 1
5450 WRITE RECORD (_WINWORD)"[FormatFont .Points=7, .FONT="+QUO+"Courier New"+QUO+"]"
5460 WAIT 1
5470 WRITE RECORD (_WINWORD)"[FilePageSetup .TopMargin=108.18]"
5480 WRITE RECORD (_WINWORD)"[FIlePageSetup .BottomMargin=36.03]"
5490 WRITE RECORD (_WINWORD)"[FilePageSetup .LeftMargin=18.02]"
5500 WRITE RECORD (_WINWORD)"[FilePageSetup .RightMargin=18.02]"
5510 WRITE RECORD (_WINWORD)"[Viewzoom .zoompercent=125]"
5520 WRITE RECORD (_WINWORD)"[startofdocument(0)]"
5530 WRITE RECORD (_WINWORD,ERR=*NEXT)"[EditReplace.Find="+QUO+CHR(138)+QUO+",.Replace="+QUO+QUO+",.Direction=0,.ReplaceAll,.WRap=0]"
5540 WRITE RECORD (_WINWORD)"[FileSaveAs .name="+QUO+LOG_FILE$+".txt"+QUO+"]"
5550 CLOSE (_WINWORD)
5560 RETURN 
5570 CREATE_DEF:
5580 PRINT (TMP)"<?xml version="+QUO+"1.0"+QUO+" standalone="+QUO+"yes"+QUO+"?>"
5590 PRINT (TMP)"<!DOCTYPE "+LOG_FILE$
5600 PRINT (TMP)DIM(5)+"["
5610 PRINT (TMP)DIM(5)+"<!ELEMENT "+LOG_FILE$+" (Record*)>"
5620 B_HTML_TO_FLD$=LCS(HTML_TO_FLD$),B_HTML_TO_FLD$=SUB(B_HTML_TO_FLD$,$09$,","),B_HTML_TO_FLD$=B_HTML_TO_FLD$(1,LEN(B_HTML_TO_FLD$)-1)
5630 PRINT (TMP)DIM(5)+"<!ELEMENT Record ("+B_HTML_TO_FLD$+")>"
5640 PRINT (TMP)DIM(5)+"<!ATTLIST Record Key CDATA #IMPLIED>"
5650 B_HTML_TO_FLD$=B_HTML_TO_FLD$+","
5660 NXT_FIELDS:
5670 XX=POS(","=B_HTML_TO_FLD$); IF XX=0 THEN GOTO END_FIELDS
5680 VAL$=B_HTML_TO_FLD$(1,XX-1),B_HTML_TO_FLD$=B_HTML_TO_FLD$(XX+1)
5690 PRINT (TMP)DIM(5)+"<!ELEMENT "+VAL$+" (#PCDATA)>"
5700 PRINT (TMP)DIM(5)+"<!ATTLIST "+VAL$+" Desc CDATA #IMPLIED>"
5710 GOTO NXT_FIELDS
5720 END_FIELDS:
5730 PRINT (TMP)DIM(5)+"]>"
5740 RETURN 
5750 CHECK_NUMERIC:
5760 HH$="0000",B_VALUE$=TF_VALUE$
5770 XX=POS($09$=TF_VALUE$); IF XX<>0 THEN HH$=HH$+STR(XX:"0000"),TF_VALUE$(XX,1)=" "; GOTO 5770
5780 B_TEXT$=TEXT$,TF_VALUE$=B_VALUE$
5790 XX=POS(" "=TEXT$); IF XX=0 THEN TEXT$=B_TEXT$; RETURN 
5800 FR=NUM(MID(HH$,XX*4-3,4))+1,HH=NUM(MID(HH$,(XX+1)*4-3,4)),HH=HH-FR,TEXT$(XX,1)="|"
5810 IF HH<0 THEN GOTO 5790
5820 TF_VALUE$(FR,HH)=STR(NUM(MID(TF_VALUE$,FR,HH)))
5830 GOTO 5790
5840 ! 
5850 ! "[Format.font("Ariel"),10,true,false,false,false,3,false,false)]"
5860 ! "Font Name",FontSize,Bold,Italic,Underline,Strike-Through,color,outline, Shadow
5870 ! 
5880 WORD:
5890 ENTER LB_VAR
5900 IF LB_VAR'ITEMCOUNT=0 THEN EXIT 
5910 SYSTEM_HELP "WINWORD.EXE"
5920 WAIT 3
5930 B$="",L=0,_WINWORD=HFN; OPEN (_WINWORD)"[DDE]winword;"
5940 GOSUB CREATE_HEADER; B$="",B$=B$+HEADER$+$0A$; CLIP_BOARD WRITE B$; B$=""; WRITE RECORD (_WINWORD)"[editpaste]"; WRITE RECORD (_WINWORD)"[InsertPara]"; L=0
5950 FOR AA=1 TO LB_VAR'ITEMCOUNT
5960 LIST_BOX FIND LB_VAR,AA,A$
5970 XX=POS($1B3059$=A$); IF XX<>0 THEN A$=A$(1,XX-1)+A$(XX+4); GOTO 5970
5980 XX=POS($1B3058$=A$); IF XX<>0 THEN A$=A$(1,XX-1)+A$(XX+4); GOTO 5980
5990 XX=POS($1B4A53$=A$); IF XX<>0 THEN A$=A$+" ",A$=A$(1,XX-1)+A$(XX+4); GOTO 5990
6000 XX=POS($1B2A43$=A$); IF XX<>0 THEN A$=A$+" ",A$=A$(1,XX-1)+A$(XX+4); GOTO 6000
6010 A$=SUB(A$,SEP,$09$)
6020 L=L+1
6030 B$=B$+A$+$0A$; IF MOD(L,200)=0 THEN CLIP_BOARD WRITE B$; B$=""; WRITE RECORD (_WINWORD)"[editpaste]"; WRITE RECORD (_WINWORD)"[InsertPara]"; L=0
6040 NEXT 
6050 CLIP_BOARD WRITE B$; B$=""; WRITE RECORD (_WINWORD)"[editpaste]"; WRITE RECORD (_WINWORD)"[InsertPara]"; L=0
6060 WRITE RECORD (_WINWORD)"[EditSelectAll]"
6070 WRITE RECORD (_WINWORD)"[FormatFont .Points=3, .FONT="+QUO+"Courier New"+QUO+"]"
6080 WRITE RECORD (_WINWORD)"[FilePageSetup .TopMargin=36.03]"
6090 WRITE RECORD (_WINWORD)"[FIlePageSetup .BottomMargin=36.03]"
6100 WRITE RECORD (_WINWORD)"[FilePageSetup .LeftMargin=36.03]"
6110 WRITE RECORD (_WINWORD)"[FilePageSetup .RightMargin=36.03]"
6120 WRITE RECORD (_WINWORD)"[Viewzoom .zoompercent=250]"
6130 CLOSE (_WINWORD)
6140 EXIT 
8300 ! ^100 - Call formatter program
8310 CALL_FORMATTER: _TOT_COL=COL
8315 IF POS("*memory*"=_FIL_NAME$)<>0 THEN RETURN 
8320 FOR _Y=1 TO _TOT_COL
8330 IF _FORMAT_PGM$[_Y]="" THEN CONTINUE
8340 _P$=_FORMAT_PGM$[_Y]
8350 IF _NO_DICT=0 OR _MEM_FLG=0 THEN _TMP$=_NME_TBL$[_Y] ELSE _TMP$=_FLD_NME$[_Y]
8360 _X$=EVS(_TMP$,ERR=*CONTINUE); CALL _P$,ERR=*CONTINUE,_X$
8370 EXECUTE _TMP$+"=_X$"
8380 NEXT 
8390 RETURN 
8800 ! ^100 - Read the cross-reference files
8810 READ_XREF_FILES:
8820 IF _NXREF=0 THEN GOTO *RETURN
8830 FOR _NXR=1 TO _NXREF
8840 IF _XFN[_NXR]=0 THEN CONTINUE
8850 IF _XREC[_NXR]=0 THEN READ (_XFN[_NXR],KEY=EVS(_XKEY$[_NXR]),KNO=_XKNO[_NXR],ERR=8860); CONTINUE
8851 IF _XREC[_NXR]=1 THEN READ RECORD (_XFN[_NXR],KEY=EVS(_XKEY$[_NXR]),KNO=_XKNO[_NXR],ERR=8860)
8852 CONTINUE
8860 ! READ DATA FROM "",REC=VIS(REC(FILE _XFN[_NXR])) TO IOL=IOL(_XFN[_NXR],ERR=*NEXT) ! Read error - blank fields
8870 NEXT _NXR
8880 RETURN 
8890 NO_FOUND: MSGBOX "Excel no found",MSG("WARNING"),"INFO,TIM=3"; EXIT 
9000 NEW_SHEET:! 9000,10
9010 WRITE RECORD (EXCEL,ERR=*NEXT)"[Format.Auto]"
9020 WAIT 3
9030 SHEET+=1,COUNT_REC=1
9040 CLOSE (EXCEL); OPEN (EXCEL)"[wdx][DDE]Excel;"+TF_NAME$+":Sheet"+STR(SHEET)
9050 WAIT 1; WRITE RECORD (EXCEL,ERR=*SAME)"[WorkBook.Select("+QUO+"Sheet"+STR(SHEET)+QUO+")]"
9060 GOSUB CREATE_HEADER
9070 GOSUB 1060
9080 RETURN 
9999 END 
56000 ! "208551-Invoiced Order History by Date Export Error                 
56002 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
56005 ! "221450-if you restrict operator to a division you cannot exit out  
56006 REM "258531-Export to Excel from WebEc Customer Parameters - Failed to  
