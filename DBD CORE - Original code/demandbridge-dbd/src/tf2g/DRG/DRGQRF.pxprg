0010 REM "DRGQRF - Assign filters to datarep targets
0035 REM "5.7 - 05/28/15 - 9.496111 - crg - SSP# 277590
0037 REM "277590-Issues with Data Replication Filters.                       
0040 REM "Copyright 2015 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! 
0070 IF TCB(88)=0 THEN MSGBOX "You must be using Windx to access this program" ELSE PROCESS "DRGQRF","../DRG/DR.EN"
0080 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 X0=-1
0160 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2; IF X1>0 THEN CMD_STR$="END"; EXIT 
0190 DIM Z[NUM(X3$(60,3))]
0200 ! Files
0210 Z$="01O DRF...  02O DR0...  03O AR1...  13O ZZPARM  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
0300 ! IOList Section
0310 IOL_DRF:IOLIST DRF$
0320 IOL_DR0:IOLIST DR0$
0400 ! 
0410 _FYI$=MSG("FYI"),_ERROR$=MSG("ERROR")
0420 CTAB$=DIM(2)
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0530 ! GOSUB CLEAR_REC
0570 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0970 RETURN 
1000 ! 1000 - Start of maintenance only code - Find/Add/Delete/Clear record
1010 FIND_REC:
1015 IF NUL(DATA_REP_TARGET$) THEN GOTO CLEAR_REC
1020 FIND (Z[2],KEY=PAD(DATA_REP_TARGET$,20),DOM=NO_SUCH)IOL=IOL_DR0
1030 GOSUB LOAD_FILTERS
1110 EXIT 
1120 ! 
1130 PROCESS_FILTER_CODE:
1140 FIELD_NAME$="CUST_DIV"
1150 FIND (Z[3],KEY=PAD(FILTER_CODE$,10),DOM=*RETURN)AR1$
1160 CUST_NAME$=MID(AR1$,11,35)
1170 REFRESH_FLG=1
1180 RETURN 
1200 ! 1200 - New RECORD check
1210 NEW_RECORD:
1290 RETURN 
1400 ! 1400 - Add RECORD
1410 WRITE_REC:
1470 CHANGE_FLG=0
1480 NEXT_ID=_FIRST_KEY
1490 EXIT 
1600 ! 1600 - Delete
1610 DELETE_REC:
1690 CHANGE_FLG=0
1710 EXIT 
1720 NO_SUCH: MSGBOX _MSG_REC_NOTFND$,_FYI$,"!"; EXIT 
1800 ! 1800 - Clear record
1810 CLEAR_REC:
1820 DATA_REP_TARGET$="",FIELD_NAME$="",FILTER_CODE$="",FILTER_DESC$=""
1830 GRID LOAD FILTER_GRID.CTL,0,0,""; FILT_CNT=0
1850 REFRESH_FLG=1
1860 RETURN 
1899 ! 
2000 LOAD_FILTERS:
2005 GRID LOAD FILTER_GRID.CTL,0,0,""; FILT_CNT=0
2007 IF FCHAN THEN CLOSE (FCHAN); FCHAN=0
2010 READ (Z[1],KEY=PAD(DATA_REP_TARGET$,20),DOM=*NEXT)
2020 NEXT_FILTER:
2030 READ (Z[1],END=END_LOAD_FILTERS)IOL=IOL_DRF
2040 IF MID(DRF$,1,20)<>PAD(DATA_REP_TARGET$,20) THEN GOTO END_LOAD_FILTERS
2095 LOAD_GRID_LINE:
2096 GOSUB SET_READONLY; FIND (Z[3],KEY=PAD(STP(MID(DRF$,51,30),2),10),DOM=*NEXT)FILTER_REC$ ! SSP#253613
2097 FILTER_DESC$=MID(FILTER_REC$,11,35)
2098 GOSUB CLEAR_READONLY ! SSP#253613
2100 GRID LOAD FILTER_GRID.CTL,1,0,SEP+MID(DRF$,51,10)+SEP+CTAB$+FN%ZZDISP$(MID(DRF$,51,10),"A/R")+SEP+CTAB$+FILTER_DESC$+SEP; ++FILT_CNT
2110 GOTO NEXT_FILTER
2120 END_LOAD_FILTERS:
2130 IF NOT(FILT_CNT) THEN GRID LOAD FILTER_GRID.CTL,1,0,SEP
2135 GRID LOCK FILTER_GRID.CTL,0,0; IF FILT_CNT THEN GRID UNLOCK FILTER_GRID.CTL,1,0
2140 REFRESH_FLG=1
2150 RETURN 
2199 ! 
3000 ! Apply new filter to datarep target
3010 APPLY_FILTER:
3020 DIM DRF$(580)
3030 DRF$(1,20)=DATA_REP_TARGET$
3031 DRF$(21,30)=FIELD_NAME$
3040 DRF$(51,30)=FILTER_CODE$
3090 WRITE (Z[1],KEY=DRF$(1,80),DOM=ERR_EXISTS)IOL=IOL_DRF
3092 GOSUB LOAD_FILTERS
3095 RETURN 
3099 ERR_EXISTS: MSGBOX MSG("DRGQRF_01"),_FYI$,"!,TIM=3"; RETURN 
4000 ! Delete selected filters
4010 DELETE_FILTERS:
4012 MSGBOX MSG("DRGQRF_03"),MSG("CONFIRM"),"YESNO,2",ANS$
4014 IF ANS$<>"YES" THEN GOTO END_DELETE_FILTERS
4020 IF NOT(FN%ISOPEN(FCHAN)) THEN GOTO END_DELETE_FILTERS
4030 READ (FCHAN,KEY="",DOM=*NEXT)
4040 DKEY$=KEY(FCHAN,END=END_DELETE_FILTERS); READ (FCHAN,KEY=DKEY$)C_FILTER$
4050 REMOVE (Z[1],KEY=PAD(DATA_REP_TARGET$,20)+PAD("CUST_DIV",30)+PAD(STP(C_FILTER$,2),30),ERR=*NEXT)
4060 GOTO 4040
4105 END_DELETE_FILTERS:
4110 GOSUB LOAD_FILTERS
4195 RETURN 
4199 ! 
4200 ! Replicate selected filters
4210 REPLICATE_FILTERS:
4212 MSGBOX MSG("DRGQRF_04"),MSG("CONFIRM"),"YESNO,2",ANS$
4214 IF ANS$<>"YES" THEN GOTO END_REPLICATE_FILTERS
4286 CALL "DRGQRF;REPLICATE_ALL_FILTERS",X3$,X4$,DATA_REP_TARGET$,"CUST_DIV",10,FCHAN,PROGRESS1.CTL,1 ! 239169
4290 END_REPLICATE_FILTERS:
4293 GOSUB LOAD_FILTERS; REFRESH_FLG=1 ! 232963
4294 MSGBOX MSG("COMPLETED")+"!",_FYI$,"!" ! 232963
4295 RETURN 
4299 ! 
4300 SET_READONLY:! SSP#253613   
4310 SET_PARAM 'XI'
4320 SET_READONLY_END:RETURN 
4330 CLEAR_READONLY:! SSP#253613             
4340 SET_PARAM -'XI'
4350 CLEAR_READONLY_END:RETURN 
5000 REM "Event handler - OnSelect
5005 SELECT_FILTER:
5010 READ DATA FROM FILTER_GRID.CTL'ROWDATA$ TO IOL=FILTER_GRID.CTL'LOADIOLIST$
5015 IF NOT(FN%ISOPEN(FCHAN)) THEN FCHAN=HFN; OPEN (FCHAN)"*MEMORY*"
5020 IF C_DELETE$="1" THEN {
5030 WRITE (FCHAN,KEY=PAD(C_FILTER$,10),ERR=*NEXT)C_FILTER$
5040  } ELSE {
5050 REMOVE (FCHAN,KEY=PAD(C_FILTER$,10),ERR=*NEXT)
5060  }
5095 RETURN 
5099 ! 
9999 END 
10000 REM "Return an array of all supported fields one can filter by in data rep
10005 GET_SUPPORTED_FIELDS:
10010 ENTER FIELDS${ALL},COUNT,ERR=*NEXT
10015 COUNT=1
10020 DIM FIELDS$[COUNT-1,1]
10025 FIELDS$[0,0]="CUST_DIV",FIELDS$[0,1]="10"
10095 EXIT 
10099 ! 
10100 REM "Is given target currently setup to be filtered
10105 IS_TARGET_FILTERED:
10110 ENTER TARGET$,IS_FILTERED$,ERR=*NEXT
10120 IS_FILTERED$="N"
10130 OPEN (HFN,ERR=ITF_DONE)"DR0"+%C$; DR0=LFO
10140 FIND (DR0,KEY=PAD(TARGET$,20),ERR=*NEXT)DR0$; IF MID(DR0$,182,1)="Y" THEN IS_FILTERED$="Y"
10190 ITF_DONE:
10192 IF DR0 THEN CLOSE (DR0)
10195 EXIT 
10199 ! 
12000 REM "Given filename, fieldname, get position and length of the field in external key (KEY# 0)
12005 GET_FIELD_POS:
12010 ENTER TABLENAME$,FIELDNAME$,POSITION,ERRMSG$,ERR=*NEXT
12011 SETERR ERR_GFP_EXIT
12015 POSITION=0,DDF_DIR$="" ! Hardcoded for now, different dir could be specified later
12020 DB=NEW("*dict/database",ERR=ERR_GFP_EXIT)
12030 RETCODE=DB'SETDATABASE(DDF_DIR$); IF RETCODE=0 THEN ERRMSG$="Error: Unable to set data base: "+DDF_DIR$+" :"+MSG(ERR); GOTO ERR_GFP_EXIT
12035 TABLENAME$=STP(TABLENAME$,2)
12040 COL_CNT=DB'GETKEYCOLUMNCOUNT(TABLENAME$)
12050 DB'SETRECORDNO(1)
12060 FOR C=1 TO COL_CNT
12070 COL=DB'GETKEYCOLUMNINFO(TABLENAME$,C)
12080 IF COL=0 THEN GOTO NOKEYCOLUMN ! Check if we have Column Object
12085 IF STP(UCS(COL'NAME$),2)=STP(UCS(FIELDNAME$),2) THEN POSITION=TOTAL_LEN+1; GOTO *BREAK
12095 TOTAL_LEN+=COL'LENGTH
12100 NOKEYCOLUMN:
12110 NEXT C
12170 DROP OBJECT DB
12190 EXIT 
12210 ERR_GFP_EXIT:
12220 IF ERRMSG$="" THEN ERRMSG$="Error: Determining field position: "+MSG(ERR)
12230 IF DB THEN DROP OBJECT DB
12240 EXIT ERR
13000 ! 
13001 REM "Replicate given set of filters (in memory file) for a target
13004 REPLICATE_ALL_FILTERS:
13005 ENTER X3$,X4$,TARGET$,FILTER_TYPE$,FILTER_LEN,FILTERS,(PROG1),(PROG2)
13010 REM "FILES
13015 DIM Z[NUM(X3$(60,3))]
13020 Z$="01O DR1...  02O DR0...  13O ZZPARM  "
13025 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO *NEXT,END_PROG
13030 REM "
13031 SAVE_PERCENT_GUI=%GUI; %GUI=1
13032 IF %DRGQRF_NO_PROG_DISP$="Y" THEN PROG1=0,PROG2=0 ! 232963
13035 IF FILTERS=0 THEN GOTO END_PROG
13040 DIM DR0$(500)
13045 FIND (Z[2],KEY=PAD(TARGET$,20),DOM=END_PROG)DR0$
13050 ! 
13055 GOSUB INIT_ALL
13060 ! T0=1,T=1 ! 239169
13065 READ (Z[1],KEY="",DOM=*NEXT)
13070 %DR_TARGET_SPECIFIC$=TARGET$
13075 ! GOSUB UPDATE_PROG_ALL ! 239169
13080 START_MAIN:REM "BEGIN MAIN PROCESS
13085 DR1_KEY$=KEY(Z[1],END=EOJ); READ (Z[1],END=EOJ)DR1$
13087 C=C+1; IF MOD(C,T0)=1 THEN GOSUB UPDATE_PROG_ALL ! 239169
13090 IF DR1$(41,20)<>TARGET$ THEN GOTO START_MAIN ! If replicating specific target, skip current table if no match on target
13095 FLDPOS=0; CALL "DRGQRF;GET_FIELD_POS",ERR=*PROCEED,DR1$(314,32),FILTER_TYPE$,FLDPOS,FERR$; IF FLDPOS=0 THEN GOTO START_MAIN ! If replicating specific table, skip current table if no match on table
13100 ! C=C+1; IF MOD(C,T0)=1 THEN GOSUB UPDATE_PROG_ALL ! 239169, moved to line 13087
13105 IF DR1$(301,1)<>"Y" THEN GOTO START_MAIN ! Table not being replicated currently
13110 ! Process file
13115 DR=HFN; OPEN (DR,ERR=FILE_OPEN_ERR)STP(DR1$(1,8))
13120 GOSUB INIT_DRFILE
13125 NEXT_DR_REC: DRK$=KEY(DR,END=DR_DONE); READ RECORD (DR,KEY=DRK$,DOM=*SAME)DRR$
13130 DC=DC+1; IF MOD(DC,DT0)=1 THEN GOSUB UPDATE_PROG_DRFILE
13135 FIND (FILTERS,KEY=MID(DRK$,FLDPOS,FILTER_LEN),DOM=NEXT_DR_REC) ! Is the key field in the filter list? If no, then get next record
13140 WRITE RECORD (DR,KEY=DRK$)DRR$ ! Replicate record
13145 GOTO NEXT_DR_REC
13150 DR_DONE:
13152 DC=DT; GOSUB UPDATE_PROG_DRFILE ! 239169
13155 CLOSE (DR)
13160 GOTO START_MAIN
13165 EOJ:REM "EOJ
13170 C=T; GOSUB UPDATE_PROG_ALL
13175 GOTO END_PROG
13180 ! 
13185 FILE_OPEN_ERR:! Error opening file
13190 FOE=ERR
13195 MSGBOX "Error "+STR(FOE)+" occurred on file "+STP(DR1$(1,10))+"!"
13200 GOTO START_MAIN
13205 ! 
13210 INIT_ALL:
13215 CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F$
13220 T0=INT(T*.02); IF T0<=1 THEN T0=2
13225 RETURN 
13230 ! 
13235 UPDATE_PROG_ALL:
13240 ! IF PROG1 THEN CALL "ZZBARG",X3$,"HG",PROG1,10,50,T1,T,C
13242 IF PROG1 THEN IF C<>T THEN PROG1'VALUE$="Processing files: "+STR(C)+" of "+STR(T) ELSE PROG1'VALUE$="Processing files: "+STR(C)+" files done!"
13245 RETURN 
13250 ! 
13255 INIT_DRFILE:
13260 CALL "ZZINFO",DR,DT9,X3$,DT,DT0,DK,DB,DD,DS0,DS1,DF$
13265 DC=0
13270 DT0=INT(DT*.02); IF DT0<=1 THEN DT0=2
13275 RETURN 
13280 ! 
13285 UPDATE_PROG_DRFILE:
13290 IF PROG2 THEN CALL "ZZBARG",X3$,"HG",PROG1'LINE+2,10,50,DT1,DT,DC
13295 RETURN 
13300 ! 
13305 END_PROG:
13310 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
13315 %DR_TARGET_SPECIFIC$="" ! Used in DR2EDA
13316 %GUI=SAVE_PERCENT_GUI
13320 EXIT 
13999 ! 
56001 ! 
56002 REM "229312-Datareplication enhancement for selective replication of    
56003 REM "232963-Ability to not show progress, slows processing remotely
56004 REM "239169-Use of two progress bars not working properly
56005 REM "253613-Data Rep error                                              - ERROR 0 2096
56006 REM "277590-Issues with Data Replication Filters.                       
