0010 REM "DRGQAN - Analyze datarep queue
0035 REM "5.7 - 01/26/21 - 9.434444 - crg - SSP# 307328
0037 REM "307328-DBD-138: Datarep queue transfer loses numerics from entries 
0040 REM "Copyright 2021 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 IF TCB(13)=1 THEN BEGIN 
0052 IF NUL(%WDX$) THEN MSGBOX "You must be using Windx to access this program"; GOTO 0075
0055 IF NOT(%GUI) THEN CHUI_MODE=1; %GUI=1; PRINT 'SHOW'(0)
0060 PROCESS "DRGQAN","../DRG/DR.EN"
0065 IF CHUI_MODE THEN %GUI=0; PRINT 'SHOW'(1); RUN "ZMENU"
0075 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 X0=-1
0125 %NOMADS_POST_DISPLAY=1
0160 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2; IF X1>0 THEN CMD_STR$="END"; EXIT 
0190 DIM Z[NUM(X3$(60,3))]
0200 ! Files
0210 Z$="13O ZZPARM  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
0300 ! IOList Section
0310 IOL_DR2:IOLIST F1$,F2$,F3$,F4$,F5$
0315 IOL_DR2$=CPL("IOLIST F1$,F2$,F3$,F4$,F5$:[CHR($FE$)]") ! 307328
0320 IOL_DR3:IOLIST F1$,FERR$,F2$,F3$,F4$,F5$
0325 IOL_DR3$=CPL("IOLIST F1$,FERR$,F2$,F3$,F4$,F5$:[CHR($FE$)]") ! 307328
0370 IOL_CHN:IOLIST _QUE$,_TIM$,_TBL$
0400 ! 
0410 _FYI$=MSG("FYI"),_ERROR$=MSG("ERROR")
0420 CTAB$=DIM(2)
0430 GOSUB CENTER_WDW
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0515 DIM DR_PARM$(1024); FIND (Z[13],KEY=X3$(9,3)+"D/R",DOM=*NEXT)DR_PARM$ ! 300498
0520 IF MID(DR_PARM$,139,1)="Y" THEN WRITE (Z[13],KEY=X3$(9,3)+"DR2")X3$(9,3)+"DR2"+SEP+FID(0)+SEP+FN%PRINT_DATETIME$(0,"%Y-%Mz-%Dz %Hz:%mz:%sz") ! Monitor record, only if exclusivity flag is enabled
0530 GOSUB CLEAR_REC
0540 SET_PARAM 'XI'
0570 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0915 REMOVE (Z[13],KEY=X3$(9,3)+"DR2",ERR=*NEXT)
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0925 %NOMADS_POST_DISPLAY=0
0930 SET_PARAM -'XI'
0970 RETURN 
1800 ! 1800 - Clear record
1810 CLEAR_REC:
1815 CLOSE (_QUE,ERR=*PROCEED),(_TBL,ERR=*PROCEED),(_TIM,ERR=*PROCEED)
1820 GRID HIDE STATS_GRID.CTL
1830 GRID LOAD STATS_GRID.CTL,0,0,""; STAT_CNT=0
1850 REFRESH_FLG=1
1860 RETURN 
1899 ! 
2800 CENTER_WDW:
2805 GOSUB GET_XYINFO
2810 UP_LEFT_X=INT((_XMAX/_XCHAR-WDW_WIDE)/2)
2815 UP_LEFT_Y=INT((_YMAX/_YCHAR-WDW_HIGH)/2)
2820 RETURN 
2849 ! 
2850 GET_XYINFO:
2855 IF _XCHAR<>0 THEN RETURN 
2860 _X$=MSE
2865 _XCHAR=DEC($00$+_X$(10,1))
2870 _YCHAR=DEC($00$+_X$(11,1))
2875 _XMAX=DEC(_X$(27,2))
2880 _YMAX=DEC(_X$(29,2))
2885 _WIN_VER=TCB(25)
2890 RETURN 
2899 ! 
2900 SHOW_PROG_WIN:
2910 PRINT 'DIALOGUE'(UP_LEFT_X-35,UP_LEFT_Y-5,70,5,"Progress")
2915 RETURN 
2920 ! 
2925 DROP_PROG_WIN:
2930 PRINT 'POP'
2935 RETURN 
2940 ! 
3000 ANALYZE_QUEUE:
3005 MSGBOX MSG("DRGQAN_03"),MSG("CONFIRM"),"YESNO,2",ANS$; IF ANS$<>"YES" THEN GOTO DONE_ANAL_QUEUE
3010 IF STAT_CNT THEN GOSUB CLEAR_REC
3020 GOSUB GET_STATS
3030 GOSUB LOAD_STATS_GRID
3040 DONE_ANAL_QUEUE:
3045 RETURN 
3049 ! 
3100 GET_STATS:
3105 GOSUB SHOW_PROG_WIN
3110 Q0$="",Q1$="P"; CALL "DR2QAN",X3$,X4$,Q0$,Q1$,DRFILE$
3115 GOSUB DROP_PROG_WIN
3120 READ DATA FROM Q1$ TO IOL=IOL_CHN
3130 _QUE=NUM(_QUE$,ERR=*NEXT)
3132 _TIM=NUM(_TIM$,ERR=*NEXT)
3134 _TBL=NUM(_TBL$,ERR=*NEXT)
3140 PRINT @(0,18),'CL',@(0,19),'CL',@(0,20),'CL',@(0,21),'CL',
3145 RETURN 
3149 ! 
3200 LOAD_STATS_GRID:
3205 TABTEXT$=FLDR'CURRENTTAB'GETORIGTEXT$()
3206 GRID HIDE STATS_GRID.CTL
3207 GRID LOAD STATS_GRID.CTL,0,0,""; STAT_CNT=0,XFER_OK=0
3210 SWITCH UCS(TABTEXT$)
3215 CASE "QUEUES"; GOSUB DISP_QUE; BREAK
3220 CASE "TABLES"; GOSUB DISP_TBL; BREAK
3230 CASE "TIMES"; GOSUB DISP_TIM; BREAK
3231 DEFAULT ; BREAK
3232 END SWITCH 
3235 GRID SHOW STATS_GRID.CTL
3295 RETURN 
3299 ! 
5200 LOAD_LINE:
5210 PRINTSTR$+=Y5$+SEP
5220 GRID LOAD STATS_GRID.CTL,1,0,SEP+Y5$+SEP; ++STAT_CNT
5245 RETURN 
5249 ! 
5400 DISP_QUE:
5405 IF NOT(FN%ISOPEN(_QUE)) THEN RETURN 
5410 READ (_QUE,KEY="",DOM=*NEXT)
5420 QUE_COUNT: QK$=KEY(_QUE,END=DONE_DISP_QUE); READ (_QUE,KEY=QK$)SCOUNT$
5430 Y5$=QK$+SEP+QK$+SEP+SCOUNT$; GOSUB LOAD_LINE
5435 GOTO QUE_COUNT
5440 DONE_DISP_QUE:
5442 XFER_OK=1
5445 RETURN 
5449 ! 
5500 DISP_TIM:
5505 IF NOT(FN%ISOPEN(_TIM)) THEN RETURN 
5510 READ (_TIM,KEY="",DOM=*NEXT)
5520 TIM_COUNT: QK$=KEY(_TIM,END=DONE_DISP_TIM); READ (_TIM,KEY=QK$)SCOUNT$
5530 Y5$=QK$+SEP+MID(QK$,1,6)+" "+MID(QK$,7)+"0"+SEP+SCOUNT$; GOSUB LOAD_LINE
5535 GOTO TIM_COUNT
5540 DONE_DISP_TIM:
5545 RETURN 
5549 ! 
5600 DISP_TBL:
5605 IF NOT(FN%ISOPEN(_TBL)) THEN RETURN 
5610 READ (_TBL,KEY="",DOM=*NEXT)
5620 TBL_COUNT: QK$=KEY(_TBL,END=DONE_DISP_TBL); READ (_TBL,KEY=QK$)SCOUNT$
5630 Y5$=QK$+SEP+MID(QK$,1,6)+", Seq: "+MID(QK$,9,2)+TBL(NUL(MID(QK$,11)),", Queue: "+MID(QK$,11),"")+SEP+SCOUNT$; GOSUB LOAD_LINE
5635 GOTO TBL_COUNT
5640 DONE_DISP_TBL:
5642 XFER_OK=1
5645 RETURN 
5649 ! 
6000 TRANSFER_RECORDS:
6003 QUE_CODE$="",DO_XFER$="0"
6005 QUE_CODES$=""; READ (_QUE,KEY="",DOM=*NEXT)
6006 QK$=KEY(_QUE,END=*NEXT); READ (_QUE,KEY=QK$)SCOUNT$; QUE_CODES$+=QK$+SEP; GOTO *SAME
6008 PROCESS "DRGQAN.2","",QUE_CODE$,DO_XFER$,QUE_CODES$; IF DO_XFER$<>"1" THEN GOTO XFER_DONE
6010 TABTEXT$=FLDR'CURRENTTAB'GETORIGTEXT$()
6030 SWITCH UCS(TABTEXT$)
6050 CASE "QUEUES"; DR2_KNO=0,DR2_KPAD=3; BREAK
6070 CASE "TABLES"; DR2_KNO=1,DR2_KPAD=13; BREAK
6090 DEFAULT ; DO_XFER$="0"; BREAK
6110 END SWITCH 
6130 IF DO_XFER$<>"1" THEN GOTO XFER_DONE
6135 IF NOT(POS(DRFILE$="DR2DR3",3)) THEN MSGBOX "ERROR: Unknown datarep system file was specified: "+DRFILE$+"|"; RETURN 
6140 OPEN (HFN)"DR2"+%C$; _DR2=LFO
6141 OPEN (HFN)"DR3"+%C$; _DR3=LFO
6145 DRTARGET=0,DRTARGETIOL$=""; IF DRFILE$="DR2" THEN DRTARGET=_DR2,DRTARGETIOL$=IOL_DR2$ ELSE DRTARGET=_DR3,DRTARGETIOL$=IOL_DR3$
6170 FOR I=1 TO STATS_GRID.CTL'ROWSHIGH
6190 STATS_GRID.CTL'ROW=I
6210 READ DATA FROM STATS_GRID.CTL'ROWDATA$ TO IOL=STATS_GRID.CTL'LOADIOLIST$
6230 IF C_SELECT$="1" THEN {
6240 IF (UCS(TABTEXT$)="QUEUES" AND C_STAT_KEY$=QUE_CODE$) OR (UCS(TABTEXT$)="TABLES" AND MID(C_STAT_KEY$,11,3)=QUE_CODE$) THEN MSGBOX MSG("DRGQAN_04"),MSG("ERROR"),"!,TIM=3"; GOTO *CONTINUE
6260 GOSUB SHOW_PROG_WIN
6265 GOSUB INIT_PROGBAR
6270 SELECT IOL=DRTARGETIOL$ FROM DRFILE$+%C$,KNO=DR2_KNO BEGIN PAD(C_STAT_KEY$,DR2_KPAD) END PAD(C_STAT_KEY$,DR2_KPAD)+$FF$,ERR=*CONTINUE
6275 C=C+1; IF MOD(C,T0)=1 THEN GOSUB UPDATE_PROGBAR
6280 QUE_KEY$=PAD(PAD(QUE_CODE$,3)+DTE(0:"%Yz%Mz%Dz")+STR(TIM:"00.00000")+"."+MID(STR(%DR2_COUNT++:DIM(10,"0")),-4),25)
6285 REMOVE (DRTARGET,KEY=F1$,ERR=*NEXT)
6290 F1$=QUE_KEY$; WRITE (_DR2,KEY=QUE_KEY$)IOL=IOL_DR2
6310 NEXT RECORD 
6340 GOSUB DROP_PROG_WIN
6345  }
6350 NEXT I
6450 XFER_DONE:
6470 IF _DR2 THEN CLOSE (_DR2); _DR2=0
6471 IF _DR3 THEN CLOSE (_DR3); _DR3=0
6495 RETURN 
6499 ! 
6500 PURGE_RECORDS:
6510 TABTEXT$=FLDR'CURRENTTAB'GETORIGTEXT$()
6520 SWITCH UCS(TABTEXT$)
6530 CASE "QUEUES"; DR2_KNO=0,DR2_KPAD=3; BREAK
6540 CASE "TABLES"; DR2_KNO=1,DR2_KPAD=13; BREAK
6550 DEFAULT ; DO_XFER$="0"; BREAK
6555 END SWITCH 
6570 MSGBOX MSG("DRGQAN_05"),MSG("CONFIRM"),"YESNO,2",ANS$
6575 IF ANS$<>"YES" THEN GOTO PURGE_DONE
6580 IF NOT(POS(DRFILE$="DR2DR3",3)) THEN MSGBOX "ERROR: Unknown datarep system file was specified: "+DRFILE$+"|"; RETURN 
6585 OPEN (HFN)"DR2"+%C$; _DR2=LFO
6586 OPEN (HFN)"DR3"+%C$; _DR3=LFO
6588 DRTARGET=0,DRTARGETIOL$=""; IF DRFILE$="DR2" THEN DRTARGET=_DR2,DRTARGETIOL$=IOL_DR2$ ELSE DRTARGET=_DR3,DRTARGETIOL$=IOL_DR3$
6590 FOR I=1 TO STATS_GRID.CTL'ROWSHIGH
6600 STATS_GRID.CTL'ROW=I
6610 READ DATA FROM STATS_GRID.CTL'ROWDATA$ TO IOL=STATS_GRID.CTL'LOADIOLIST$
6620 IF C_SELECT$="1" THEN {
6630 GOSUB SHOW_PROG_WIN
6640 GOSUB INIT_PROGBAR
6650 SELECT IOL=DRTARGETIOL$ FROM DRFILE$+%C$,KNO=DR2_KNO BEGIN PAD(C_STAT_KEY$,DR2_KPAD) END PAD(C_STAT_KEY$,DR2_KPAD)+$FF$,ERR=*CONTINUE
6660 C=C+1; IF MOD(C,T0)=1 THEN GOSUB UPDATE_PROGBAR
6680 REMOVE (DRTARGET,KEY=F1$,ERR=*NEXT)
6690 NEXT RECORD 
6700 GOSUB DROP_PROG_WIN
6710  }
6720 NEXT I
6770 PURGE_DONE:
6780 IF _DR2 THEN CLOSE (_DR2); _DR2=0
6781 IF _DR3 THEN CLOSE (_DR3); _DR3=0
6795 RETURN 
6799 ! 
8100 REM "Initialize progress bargraph
8105 INIT_PROGBAR:
8110 T=NUM(C_DESC$,ERR=*NEXT)
8130 C=0,T0=INT(T*.06); IF T0<=1 THEN T0=2
8145 RETURN 
8149 ! 
8150 REM "Update progress bargraph
8155 UPDATE_PROGBAR:
8165 CALL "ZZBARG",X3$,"HG",1,10,50,T1,T,C
8195 RETURN 
8199 ! 
9999 END 
56001 REM "264884-Datarep & IMS cycles  - How can we speed this up?
56002 REM "286759-Datarep Improvements - Error queue analysis and handling    
56003 REM "300498-Queue Analysis Utility - When the tool is used,             
56004 REM "307328-DBD-138: Datarep queue transfer loses numerics from entries 
