0010 REM "Custom Item GUI Delete Routine <FMGUAB>
0035 REM "5.6 - 07/30/08 - 8.270277 - lms - SSP# 218678
0037 REM "218678-PE in FM1 - Deleting Item                                   
0040 REM "Copyright 2008 DemandBridge, Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$,Q2$,Q3$
0110 X0$="FMGUAB",X1$="Custom Item Delete Routine",K9$="",K9=0
0120 DIM Z0$(80,"-"),Y[4],F[6],K[14],Z[9]
0160 DEF FNS$(Z9$)=Z9$(1,POS("  "=Z9$+"  ")-1)
0200 REM "
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0300 REM "IOLISTS
0310 IOLIST A$
0320 IOLIST B$
0360 IOLIST F$,F[0],F[1],F[2],F[3],F[4],F[5],F[6]
0500 REM "FILES
0505 GOSUB SETUP_MESSAGES
0510 _FILE_NAME$="FM1"+%C$,_FM1=HFN; OPEN (_FM1,ERR=OPEN_ERR)_FILE_NAME$
0515 _FILE_NAME$="FS1"+%C$,_FS1=HFN; OPEN (_FS1,ERR=OPEN_ERR)_FILE_NAME$
0520 _FILE_NAME$="FS2"+%C$,_FS2=HFN; OPEN (_FS2,ERR=OPEN_ERR)_FILE_NAME$
0525 _FILE_NAME$="FM2"+%C$,_FM2=HFN; OPEN (_FM2,ERR=OPEN_ERR)_FILE_NAME$
0530 _FILE_NAME$="FM3"+%C$,_FM3=HFN; OPEN (_FM3,ERR=OPEN_ERR)_FILE_NAME$
0535 _FILE_NAME$="FM4"+%C$,_FM4=HFN; OPEN (_FM4,ERR=OPEN_ERR)_FILE_NAME$
0540 _FILE_NAME$="FMS"+%C$,_FMS=HFN; OPEN (_FMS,ERR=OPEN_ERR)_FILE_NAME$
0545 _FILE_NAME$="FMU"+%C$,_FMU=HFN; OPEN (_FMU,ERR=OPEN_ERR)_FILE_NAME$
0550 _FILE_NAME$="FMV"+%C$,_FMV=HFN; OPEN (_FMV,ERR=OPEN_ERR)_FILE_NAME$
0555 _FILE_NAME$="FM5"+%C$,_FM5=HFN; OPEN (_FM5,ERR=OPEN_ERR)_FILE_NAME$
0560 _FILE_NAME$="FT1"+%C$,_FT1=HFN; OPEN (_FT1,ERR=OPEN_ERR)_FILE_NAME$
0585 RT_PARM$=""; CALL "RTGPRM",ERR=*NEXT,RT_PARM$
0660 GOTO CONTINUE
0665 ! 
0670 OPEN_ERR: MSGBOX _MSG_FILOPNERR1$+QUO+_FILE_NAME$+QUO+SEP+_MSG_DIRECTORY$+LWD+SEP+_MSG_PREFIX$+PFX,MSG(ERR),"!"
0675 GOTO DONE
0685 ! 
0690 CONTINUE:
0695 ! 
1000 REM "
1015 PRINT 'CURSOR'(1),
1020 CALL "*progbar;init","Checking item for activity...",40
1030 T_R=NUM(FN%FIN$(_FS1,"numrec")); IF T_R<1 THEN T_R=1
1040 P_R=INT(T_R/20); IF P_R<1 THEN P_R=1
1050 COUNTER=0
1095 ! 
1100 REM "Open orders?
1102 SET_PARAM 'XI' ! SSP#217279
1105 Q2$="Y"; READ (_FM1,KEY=Q1$(1,20),DOM=5020)F$
1110 READ (_FS1,KEY="",DOM=1111)
1120 READ (_FS1,END=2000)IOL=0310
1123 COUNTER++
1125 IF MOD(COUNTER,P_R)=0 THEN CALL "*progbar;update_percent",ERR=5000,"Order number: "+A$(118,8),COUNTER/T_R*100
1130 IF A$(6,10)<>F$(1,10) THEN GOTO 1120
1195 ! 
1200 REM "Order line detail
1210 READ (_FS2,KEY=A$(118,8),DOM=1211)
1215 READ (_FS2,END=1290)IOL=0320
1220 IF B$(147,8)<>A$(118,8) THEN GOTO 1290
1240 IF B$(19,10)=F$(11,10) THEN GOTO 5000
1290 GOTO 1120
1295 ! 
2000 REM "Delete Custom Item Information
2001 CALL "*progbar;update_percent",ERR=*NEXT,"Activity check complete",100
2002 CALL "*progbar;wrap_up"
2003 CALL "*progbar;init","Deleting all item data...",40
2005 IF Q3$<>"P" THEN GOTO 2100
2010 CALL "*progbar;update_percent",ERR=*NEXT,"Custom Item No: "+Q1$(11,10),1
2100 REM "Product locator file
2110 REMOVE (_FM2,KEY=F$(21,19)+F$(40,2)+F$(11,10)+F$(1,10),DOM=2111)
2114 REM "Customer's Item Code
2115 REMOVE (_FT1,KEY=F$(1,10)+F$(350,20)+F$(11,10),DOM=2116)
2120 REM "Item spec - comp
2130 REMOVE (_FMS,KEY=F$(1,20),DOM=2131)
2140 REM "Item spec - ply
2150 READ (_FMU,KEY=F$(1,20),DOM=2151)
2160 K$=KEY(_FMU,END=2200)
2170 READ (_FMU)
2180 IF MID(K$,1,20)<>F$(1,20) THEN GOTO 2200
2190 REMOVE (_FMU,KEY=K$)
2195 GOTO 2160
2200 REM "Item spec - comment
2210 READ (_FMV,KEY=F$(1,20),DOM=2211)
2220 K$=KEY(_FMV,END=2270)
2230 READ (_FMV)
2240 IF MID(K$,1,20)<>F$(1,20) THEN GOTO 2270
2250 REMOVE (_FMV,KEY=K$)
2260 GOTO 2220
2270 REM "Reorder notice
2280 REMOVE (_FM5,KEY=F$(1,20),DOM=2281)
2290 CALL "*progbar;update_percent",ERR=*NEXT,"",25
2300 REM "Item by location
2310 READ (_FM4,KEY=F$(1,20),DOM=2311)
2320 K$=KEY(_FM4,END=2400)
2330 IF K$(1,20)<>F$(1,20) THEN GOTO 2400
2340 REMOVE (_FM4,KEY=K$); IF RT_PARM$<>"" THEN CUST$=K$(1,10); CALL "RTGWOC",ERR=*NEXT,CUST$,"FM4...","D",K$
2350 GOTO 2320
2400 REM "Combine Stats
2430 READ (_FM3,KEY=F$(1,20),DOM=2431)
2440 K$=KEY(_FM3,END=2590)
2445 IF K$(1,20)<>F$(1,20) THEN GOTO 2590
2545 REMOVE (_FM3,KEY=K$)
2546 IF RT_PARM$<>"" THEN CUST$=K$(1,10); CALL "RTGWOC",ERR=*NEXT,CUST$,"FM3...","D",K$
2547 READ (_FM3,KEY=K$,DOM=2548)
2550 GOTO 2440
2590 CALL "*progbar;update_percent",ERR=*NEXT,"",50
2600 REM "History
2605 _FILE_NAME$="FS6"+%C$,_FS6=HFN; OPEN (_FS6,ERR=OPEN_ERR)_FILE_NAME$
2610 _FILE_NAME$="FS7"+%C$,_FS7=HFN; OPEN (_FS7,ERR=OPEN_ERR)_FILE_NAME$
2615 _FILE_NAME$="FS8"+%C$,_FS8=HFN; OPEN (_FS8,ERR=OPEN_ERR)_FILE_NAME$
2620 _FILE_NAME$="ASJ"+%C$,_ASJ=HFN; OPEN (_ASJ,ERR=OPEN_ERR)_FILE_NAME$
2625 _FILE_NAME$="ASC"+%C$,_ASC=HFN; OPEN (_ASC,ERR=OPEN_ERR)_FILE_NAME$
2630 _FILE_NAME$="FSY"+%C$,_FSY=HFN; OPEN (_FSY,ERR=OPEN_ERR)_FILE_NAME$
2700 REM "Order History
2710 T9$="BD"
2715 FOR I=1 TO LEN(T9$)
2720 READ (_FS6,KEY=F$(1,10)+T9$(I,1)+F$(11,10),DOM=2721)
2730 K$=KEY(_FS6,END=2990); READ (_FS6)C$
2740 IF K$(1,21)<>F$(1,10)+T9$(I,1)+F$(11,10) THEN GOTO 2990
2750 REMOVE (_FS6,KEY=K$)
2751 IF RT_PARM$<>"" THEN CUST$=K$(1,10); CALL "RTGWOC",ERR=*NEXT,CUST$,"FS6...","D",K$
2760 REM "Sorts
2770 REMOVE (_FS7,KEY=C$(31,3)+C$(11,1)+C$(12,10)+C$(1,10)+C$(22,8)+C$(30,1),DOM=2771)
2780 REMOVE (_FS8,KEY=C$(1,10)+C$(22,8)+C$(11,1)+C$(12,10)+C$(30,1),DOM=2781)
2790 X$=C$(1,10)+C$(22,9); READ (_ASJ,KEY=X$,DOM=2791)
2800 K$=KEY(_ASJ,END=2850); READ (_ASJ)ASJ$
2810 IF K$(1,LEN(X$))<>X$ THEN GOTO 2850
2815 IF ASJ$(29,10)<>C$(12,10) THEN GOTO 2800
2820 REMOVE (_ASJ,KEY=K$)
2830 GOTO 2800
2850 REM "Change Z[5] from FS9 to ASJ, add ASC and FSY, SSP151214
2860 REMOVE (_ASC,KEY=C$(1,10)+C$(54,15)+C$(11,1)+C$(12,10)+C$(22,8)+C$(30,1),DOM=*NEXT)
2870 F9$=C$(48,6)
2875 CALL "ZZDATE",X3$,"","G10101",F9$,D0,1,0,0,0,0,0,"","",""
2880 Q$=C$(1,10)+F9$+C$(12,10)+C$(11,1)+C$(22,8)+C$(30,1)
2885 Q$(11,6)=STR(100000-D0:"000000")
2890 REMOVE (_FSY,KEY=Q$,DOM=*NEXT)
2950 REM 
2960 GOTO 2730
2990 NEXT I
3000 REM "Add additional files to routine, SSP125807
3003 _FILE_NAME$="FMC"+%C$,Z[2]=HFN; OPEN (Z[2],ERR=OPEN_ERR)_FILE_NAME$
3004 _FILE_NAME$="FMD"+%C$,Z[3]=HFN; OPEN (Z[3],ERR=OPEN_ERR)_FILE_NAME$
3005 _FILE_NAME$="FMI"+%C$,Z[4]=HFN; OPEN (Z[4],ERR=OPEN_ERR)_FILE_NAME$
3006 _FILE_NAME$="FST"+%C$,Z[5]=HFN; OPEN (Z[5],ERR=OPEN_ERR)_FILE_NAME$
3007 _FILE_NAME$="AS7"+%C$,Z[6]=HFN; OPEN (Z[6],ERR=OPEN_ERR)_FILE_NAME$
3008 _FILE_NAME$="KT0"+%C$,Z[7]=HFN; OPEN (Z[7],ERR=OPEN_ERR)_FILE_NAME$
3009 _FILE_NAME$="FTL"+%C$,Z[8]=HFN; OPEN (Z[8],ERR=OPEN_ERR)_FILE_NAME$
3010 _FILE_NAME$="FSW"+%C$,Z[9]=HFN; OPEN (Z[9],ERR=OPEN_ERR)_FILE_NAME$
3011 _FILE_NAME$="FSF"+%C$,_FSF=HFN; OPEN (_FSF,ERR=OPEN_ERR)_FILE_NAME$
3012 _FILE_NAME$="FT0"+%C$,_FT0=HFN; OPEN (_FT0,ERR=OPEN_ERR)_FILE_NAME$
3013 _FILE_NAME$="FS0"+%C$,_FS0=HFN; OPEN (_FS0,ERR=OPEN_ERR)_FILE_NAME$
3014 _FILE_NAME$="FTM"+%C$,_FTM=HFN; OPEN (_FTM,ERR=OPEN_ERR)_FILE_NAME$
3015 _FILE_NAME$="PP3"+%C$,_PP3=HFN; OPEN (_PP3,ERR=OPEN_ERR)_FILE_NAME$; REM SSP 152118
3016 _FILE_NAME$="ZZO"+%C$,_ZZO=HFN; OPEN (_ZZO,ERR=OPEN_ERR)_FILE_NAME$; REM "SSP 152118
3020 FOR X=2 TO 9
3025 READ (Z[X],KEY=F$(1,20),DOM=3026)
3030 K$=KEY(Z[X],END=3050); READ (Z[X])
3035 IF MID(K$,1,20)<>MID(F$,1,20) THEN GOTO 3050
3040 REMOVE (Z[X],KEY=K$); IF X=8 THEN REMOVE (_FTM,KEY=K$(21,4)+K$(1,20),DOM=3041)
3045 GOTO 3030
3050 NEXT X
3060 REMOVE (_FSF,KEY=F$(1,20),DOM=3061)
3070 REMOVE (_FT0,KEY=F$(1,20),DOM=3071)
3080 REMOVE (_FS0,KEY=F$(1,20),DOM=3081)
3090 READ (Z[7],KEY="",DOM=3091); REM "Read through KIT file for components
3092 K$=KEY(Z[7],END=3095); READ (Z[7],KEY=K$)KT0$
3094 IF K$(21,20)=MID(F$,1,20) THEN REMOVE (Z[7],KEY=K$) ELSE GOTO 3092; REM SSP 218678
3095 REM "REMOVE ZZO,PP3 ECR RECORDS SSP 152118
3096 REMOVE (_PP3,KEY=F$(1,20),DOM=*NEXT); REM SSP 152118
3097 READ (_ZZO,KEY="003"+F$(1,20)+DIM(30),DOM=*NEXT); REM SSP 152118
3098 ZZO_KEY$=KEY(_ZZO,END=3104); READ (_ZZO)
3099 IF ZZO_KEY$(1,23)<>"003"+F$(1,20) THEN GOTO 3104
3100 REMOVE (_ZZO,KEY=ZZO_KEY$)
3101 GOTO 3098
3104 ! If WebEC is present, then open ECR and remove  SSP 152118
3105 CALL "ZZ2PRP","EC",RESULT$,TF_DATE$; IF RESULT$<>"Y" THEN GOTO 4090
3110 _FILE_NAME$="ECR"+%C$,_ECR=HFN; OPEN (_ECR,ERR=OPEN_ERR)_FILE_NAME$
3120 REMOVE (_ECR,KEY=F$(1,20),DOM=*NEXT)
4090 REMOVE (_FM1,KEY=F$(1,20),DOM=4091)
4091 IF RT_PARM$<>"" THEN CUST$=F$(1,10); CALL "RTGWOC",ERR=*NEXT,CUST$,"FM1...","D",F$(1,20)
4100 REM "Deleted Return to Overlay
4110 Q2$="Y"
4115 CALL "*progbar;update_percent",ERR=*NEXT,"Custom item delete complete",100
4120 CALL "*progbar;wrap_up"
4140 GOTO 9900
5000 REM "No Delete Return to Overlay
5010 Q2$="N"
5020 CALL "*progbar;wrap_up"
5090 GOTO 9900
9200 SETUP_MESSAGES:
9210 _MSG_FILOPNERR1$=MSG("FILOPNERR1")
9220 _MSG_DIRECTORY$=MSG("DIRECTORY")
9230 _MSG_PREFIX$=MSG("PREFIX")
9290 SETUP_MESSAGES_END:RETURN 
9295 ! 
9700 WRAPUP:
9710 IF _FM1<>0 THEN CLOSE (_FM1)
9715 IF _FS1<>0 THEN CLOSE (_FS1)
9720 IF _FS2<>0 THEN CLOSE (_FS2)
9725 IF _FM2<>0 THEN CLOSE (_FM2)
9730 IF _FM3<>0 THEN CLOSE (_FM3)
9735 IF _FM4<>0 THEN CLOSE (_FM4)
9740 IF _FMS<>0 THEN CLOSE (_FMS)
9745 IF _FMU<>0 THEN CLOSE (_FMU)
9750 IF _FMV<>0 THEN CLOSE (_FMV)
9755 IF _FM5<>0 THEN CLOSE (_FM5)
9760 IF _FT1<>0 THEN CLOSE (_FT1)
9765 IF _FS6<>0 THEN CLOSE (_FS6)
9770 IF _FS7<>0 THEN CLOSE (_FS7)
9775 IF _FS8<>0 THEN CLOSE (_FS8)
9780 IF _ASJ<>0 THEN CLOSE (_ASJ)
9785 FOR X=2 TO 9
9786 IF Z[X]<>0 THEN CLOSE (Z[X])
9787 NEXT X
9790 IF _FSF<>0 THEN CLOSE (_FSF)
9795 IF _FT0<>0 THEN CLOSE (_FT0)
9800 IF _FS0<>0 THEN CLOSE (_FS0)
9805 IF _FTM<>0 THEN CLOSE (_FTM)
9810 IF _ASC<>0 THEN CLOSE (_ASC)
9815 IF _FSY<>0 THEN CLOSE (_FSY)
9890 WRAPUP_END:RETURN 
9895 ! 
9900 REM "END PROGRAM
9902 SET_PARAM -'XI' ! SSP#217279
9905 DONE:
9910 GOSUB WRAPUP
9915 PRINT 'CURSOR'(0),
9930 EXIT 
9999 END 
56000 REM "152118-When you delete a custom form, it does not delete the    
56002 REM "205460-Oracle - FIN(CHANNEL,"NUMREC") changed to use FN%FIN$()     
56004 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
56005 REM "217279-Can't delete a custom item. Getting record is use by some   
56006 REM "218678-PE in FM1 - Deleting Item  
