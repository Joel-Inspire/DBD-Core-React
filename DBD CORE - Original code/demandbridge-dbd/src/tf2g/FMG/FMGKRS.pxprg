0010 ! FMGKRS - Item Usage Report
0035 REM "5.6 - 10/27/08 - 8.901111 - tma - SSP# 224122
0037 REM "224122-GUI version on FMR-R8, Issue w/ setup of alternate selection
0040 REM "Copyright 2008 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 PROCESS "FMGKRS","../FMG/FM.EN"
0060 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 _SV_KR=PRM('KR'); SET_PARAM 'KR'=0 ! Ensure native handling of KEP()
0130 X0=-1,X2=-1
0140 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2
0150 ! 
0155 DIM ZZS$(90),RNG$(100),PRT$(126),RNG[4],B.RNG[4]
0160 ! iolist
0170 IOLIST ZZS$
0180 IOLIST PRT$,RNG$,RNG{ALL}
0300 ! Open Files
0310 DIM Z[NUM(X3$(60,3))]
0320 Z$="01O ZZS...  06O ZZPARM  07O ZZP     "
0330 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
0400 ! 
0410 RPT_SELEC$="           "; VIEW$=""
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 KEY_1$="FM2KRA"+RPT_SELEC$,KEY_1$=PAD(KEY_1$,18," "); %K9$="FM2KRA"
0530 READ (Z[1],KEY=KEY_1$,DOM=ADD_OPT)IOL=0170,IOL=0180
0535 B_ZZS$=ZZS$; B_PRT$=PRT$; B_RNG$=RNG$; CHG_FLG=0
0536 NEXT_FOLDER=11000+NUM(ZZS$(71,1)),NEXTFOLDER_FLG=NEXT_FOLDER,REFRESH_FLG=1
0580 GOSUB SELECT_OPTIONS
0590 RETURN 
0600 ! SELECTION OPTIONS
0610 SELECT_OPTIONS:
0620 PRG_NAME$=ZZS$(1,6); RPT_SELEC$=ZZS$(7,12); RPT_DESC$=ZZS$(19,40)
0630 RPT_OPT$=ZZS$(59,12); RPT_SEQ$=ZZS$(71,1)
0640 CB_1$=ZZS$(73,1); IF NUL(CB_1$) THEN CB_1$="N" ! SSP 221233
0800 ! Output Options
0810 PRT_MODE$=PRT$(1,1); RPT_OUTDEV$=PRT$(2,6)
0820 RPT_VERT$=PRT$(9,1); RPT_HORIZ$=PRT$(10,1)
0830 RPT_AUX1$=PRT$(11,1); RPT_AUX2$=PRT$(12,1); RPT_AUX3$=PRT$(13,1); RPT_AUX4$=PRT$(14,1)
0840 RPT_LINES$=PRT$(15,2); RPT_FORMCODE$=PRT$(17,1)
0850 RPT_JOBCARD$=PRT$(65,60); RPT_JOBCARD_FLG$=PRT$(125,1)
0860 GOSUB LOAD_RANGE
0870 REM "NEXT_ID=RPT_VEIW.CTL!SSP#224122
0880 REFRESH_FLG=1
0890 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0960 SET_PARAM 'KR'=_SV_KR
0970 EXIT 
1000 ! load the sequence range
1010 LOAD_RANGE:
1020 SWITCH RPT_SEQ$
1030 CASE "1"
1035 IF NUL(RNG$(1,10)) THEN CUST_START$="" ELSE CUST_START$=RNG$(1,10)
1040 IF RNG$(11,3)="~~~" THEN CUST_END$="" ELSE CUST_END$=RNG$(11,10)
1050 IF NUL(RNG$(21,10)) THEN ITEM_START$="" ELSE ITEM_START$=RNG$(21,10)
1060 IF RNG$(31,3)="~~~" THEN ITEM_END$="" ELSE ITEM_END$=RNG$(31,10)
1085 NEXTFOLDER_FLG=FLDR.FM1.2.CTL
1090 RETURN 
1100 CASE "2"
1110 IF NUL(RNG$(1,4)) THEN SLS_START$="" ELSE SLS_START$=RNG$(1,4)
1120 IF RNG$(5,3)="~~~" THEN SLS_END$="" ELSE SLS_END$=RNG$(5,4)
1130 IF NUL(RNG$(9,10)) THEN CUST_START$="" ELSE CUST_START$=RNG$(9,10)
1140 IF RNG$(19,3)="~~~" THEN CUST_END$="" ELSE CUST_END$=RNG$(19,10)
1165 NEXTFOLDER_FLG=FLDR.FM1.7.2.CTL
1170 RETURN 
1490 END SWITCH 
1500 RETURN 
3800 CHECK_CHANGES:
3810 GOSUB LOAD_OPTIONS
3820 IF CHG_FLG=0 THEN RETURN 
3830 MSGBOX MSG("UPD_RPT_1")+SEP+MSG("UPD_RPT_2"),MSG("FYI"),"?,YesNo",ANS$
3840 IF ANS$="NO" THEN RETURN 
3850 WRITE (Z[1],KEY=KEY_1$)IOL=0170,IOL=0180
3860 B_ZZS$=ZZS$; B_PRT$=PRT$; B_RNG$=RNG$; CHG_FLG=0
3870 RETURN 
8000 ! Messages
8010 ADD_OPT:
8020 MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"?,YesNo",ANS$
8030 IF ANS$="NO" THEN CMD_STR$="END"; RETURN 
8040 KEY_1$="FM2KRA"+RPT_SELEC$
8050 DIM ZZS$(90),RNG$(100); ZZS$(1,18)=KEY_1$,ZZS$(71,1)="1",RNG$(11,10)=DIM(10,"~"),RNG$(31,10)=DIM(10,"~") ! SSP#224122
8060 GOSUB SELECT_OPTIONS
8070 REFRESH_FLG=1; NEXT_ID=RPT_DESC.CTL
8090 RETURN 
9999 END 
17000 ! 
20000 LOAD_OPTIONS:
20010 ! View Report Routine.
20020 ! 
20030 ZZS$(1,6)=PRG_NAME$
20040 ZZS$(7,12)=RPT_SELEC$; ZZS$(19,40)=RPT_DESC$
20050 ZZS$(59,12)=RPT_OPT$; ZZS$(71,1)=RPT_SEQ$
20055 ! 
20060 ZZS$(73,1)=CB_1$ ! SSP 221233
20100 ! 
20110 PRT$(1,1)=PRT_MODE$; PRT$(2,2)=RPT_OUTDEV$
20120 PRT$(9,1)=RPT_VERT$; PRT$(10,1)=RPT_HORIZ$
20130 PRT$(11,1)=RPT_AUX1$; PRT$(12,1)=RPT_AUX2$
20140 PRT$(13,1)=RPT_AUX3$; PRT$(14,1)=RPT_AUX4$
20150 PRT$(15,2)=RPT_LINES$; PRT$(17,1)=RPT_FORMCODE$
20160 PRT$(65,60)=RPT_JOBCARD$; PRT$(125,1)=RPT_JOBCARD_FLG$
20170 ! 
20175 RNG$=""
20180 ! 
20190 IF RPT_SEQ$="1" THEN {
20200 T1$="FM1..."
20210 GOSUB GET_CUST_RANGE; GOSUB GET_ITEM_RANGE }
20220 IF RPT_SEQ$="2" THEN {
20230 T1$="ARU..."
20250 GOSUB GET_SLS_RANGE; GOSUB GET_CUST_RANGE }
20380 IF B_ZZS$=ZZS$ AND B_PRT$=PRT$ AND B_RNG$=RNG$ THEN CHG_FLG=0 ELSE CHG_FLG=1
20390 RETURN 
25000 ! 
25010 PRINT_REPORT:
25020 IF NOT(NUL(VIEW$)) THEN PRT$(2,2)=VIEW$
25030 K$=X3$(1,8)
25040 %B_WINDATE_FMT$=%WINDATE_FMT$,%WINDATE_FMT$="YYYYMMDD",JJ$=DTE(0:"%Yl%Mz%Dz"); CALL "ZZWDTE;VALIDATE",JJ$,JJJ$,JJJJ$; X3$(21,6)=JJ$
25050 WRITE (Z[7],KEY=K$)X3$,X4$,T1$,PRT$,RNG$,ZZS$
25053 K9$="FM2KRA"
25055 IF POS(RPT_OPT$="BCXFEGH")>0 THEN K9$="FM2RO"+RPT_OPT$
25057 IF ZZS$(60,1)="B" THEN K9$="FM2KRB"
25060 CALL K9$
25070 CMD_STR$="END"
25080 RETURN 
28000 ! 
28010 GET_CUST_RANGE:
28020 IF NUL(CUST_START$) THEN RNG$+="          " ELSE RNG$+=CUST_START$
28030 IF NUL(CUST_END$) THEN RNG$=RNG$+"~~~~~~~~~~" ELSE RNG$=RNG$+CUST_END$ }
28040 RETURN 
28050 ! 
28060 GET_ITEM_RANGE:
28070 IF NUL(ITEM_START$) THEN RNG$+="          " ELSE RNG$+=ITEM_START$
28080 IF NUL(ITEM_END$) THEN RNG$+="~~~~~~~~~~" ELSE RNG$+=ITEM_END$
28090 RETURN 
28100 ! 
28110 GET_LOC_RANGE:
28120 IF NUL(LOC_START$) THEN RNG$+="     " ELSE RNG$=RNG$+LOC_START$
28130 IF NUL(LOC_END$) THEN RNG$=RNG$+"~~~~~" ELSE RNG$=RNG$+LOC_START$
28140 RETURN 
28150 ! 
28160 GET_GROUP_RANGE:
28170 IF NUL(GROUP_START$) THEN RNG$+="   " ELSE RNG$+=GROUP_START$
28180 IF NUL(GROUP_END$) THEN RNG$+="~~~" ELSE RNG$+=GROUP_END$
28190 RETURN 
28200 ! 
28210 GET_SLS_RANGE:
28220 IF NUL(SLS_START$) THEN RNG$+="    " ELSE RNG$+=SLS_START$
28230 IF NUL(SLS_END$) THEN RNG$+="~~~~" ELSE RNG$+=SLS_END$
28240 RETURN 
30000 ! 
30005 SELECT_SELECTION:
30010 _KEY$="FM2KRA"+RPT_SELEC$,_KEY$=PAD(_KEY$,18," ")
30020 READ (Z(1),KEY=_KEY$,DOM=NEW_RECORD1)A$,R0$,W$,RNG{ALL}
30030 GOTO MAIN_POST_DISPLAY
30500 NEW_RECORD1:
30510 MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"?,YESNO",_YESNO$
30520 IF _YESNO$="NO" THEN GOSUB MAIN_POST_DISPLAY; EXIT 
30530 DIM A$(90),R0$(128),W$(128),RNG(4)
30540 A$(71,1)="1",A$(1,6)="FM2KRA",A$(7,LEN(RPT_SELEC$))=RPT_SELEC$
30545 A$(73,1)="N" ! SSP 221233
30548 DIM ZZS$(90),RNG$(100); ZZS$(1,18)=A$(1,18),ZZS$(71,1)="1",ZZS$(73,1)="N",ZZS$(59,1)=RPT_OPT$,RNG$(11,10)=DIM(10,"~"),RNG$(31,10)=DIM(10,"~") ! SSP#224122
30550 WRITE (Z[1],KEY=_KEY$)IOL=0170,IOL=0180 ! SSP#224122
30555 REFRESH_FLG=1; NEXT_ID=RPT_DESC.CTL ! SSP#224122
30560 GOTO MAIN_POST_DISPLAY
56000 ! 
56001 REM "221233-Modify report to print usage amts even if total usage is 0
