0010 ! FMGUDS - Export User Defined Specifications for Custom Items
0035 REM "5.7 - 08/27/15 - 16.921111 - crg - SSP# 273353
0037 REM "273353-User Defined Spec Export and Import programs.  See 272455   
0040 REM "Copyright 2015 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0049 ! 
0050 IF TCB(13)=1 THEN BEGIN 
0052 IF TCB(88)=0 THEN MSGBOX MSG("WARN_WINDX"),MSG("ERROR"),"OK"; GOTO 0075
0055 IF NOT(%GUI) THEN CHUI_MODE=1; %GUI=1; PRINT 'SHOW'(0)
0060 PROCESS "FMGUDS","../FMG/FM.EN"
0065 IF CHUI_MODE THEN %GUI=0; PRINT 'SHOW'(1); RUN "ZMENU"
0075 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 _SV_KR=PRM('KR'); SET_PARAM 'KR'=0 ! Ensure native handling of KEP()
0130 X0=-1,X2=-1
0140 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2
0150 ! 
0155 DIM ZZS$(90),PRT$(125),RNG$(100),RNG[4],B.RNG[4]
0160 ! iolist
0170 IOLIST ZZS$
0180 IOLIST PRT$,RNG$,RNG{ALL}
0300 ! Open Files
0310 DIM Z[NUM(X3$(60,3))]
0320 Z$="01O ZZS...  06O ZZPARM  07O ZZP     "
0330 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
0400 ! 
0410 RPT_SELEC$="            "; VIEW$=""
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 KEY_1$="FM2UDE"+RPT_SELEC$; %K9$="FM2UDE"
0530 READ (Z[1],KEY=KEY_1$,DOM=ADD_OPT)IOL=0170,IOL=0180
0535 B_ZZS$=ZZS$; B_PRT$=PRT$; B_RNG$=RNG$; CHG_FLG=0
0540 GOSUB LOAD_SPEC_CODES
0580 GOSUB SELECT_OPTIONS
0590 RETURN 
0600 ! SELECTION OPTIONS
0610 SELECT_OPTIONS:
0620 PRG_NAME$=ZZS$(1,6); RPT_SELEC$=ZZS$(7,12); RPT_DESC$=ZZS$(19,40)
0630 RPT_OPT$=ZZS$(59,12); RPT_SEQ$=ZZS$(71,1)
0640 PRICE_M$=ZZS$(72,1); ALT_UNIT$=ZZS$(73,1); LAST_ORD$=ZZS$(74,1)
0650 FORM_EX$=STP(ZZS$(75,5)); PRINT_PCS$=ZZS$(80,1); DOUBLE_SR$=ZZS$(81,1)
0660 SPEC_CODE$=ZZS$(82,3)
0670 INCL_COLREF$=ZZS$(85,1),INCL_COLDESC$=ZZS$(86,1),INCL_DATATYPE$=ZZS$(87,1)
0800 ! Output Options
0810 PRT_MODE$=PRT$(1,1); RPT_OUTDEV$=PRT$(2,6)
0820 RPT_VERT$=PRT$(9,1); RPT_HORIZ$=PRT$(10,1)
0830 RPT_AUX1$=PRT$(11,1); RPT_AUX2$=PRT$(12,1); RPT_AUX3$=PRT$(13,1); RPT_AUX4$=PRT$(14,1)
0840 RPT_LINES$=PRT$(15,2); RPT_FORMCODE$=PRT$(17,1)
0850 RPT_JOBCARD$=PRT$(65,60); RPT_JOBCARD_FLG$=PRT$(125,1)
0860 GOSUB LOAD_RANGE
0870 ! NEXT_ID=RPT_VEIW.CTL  !SSP#224109
0880 REFRESH_FLG=1
0890 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0960 SET_PARAM 'KR'=_SV_KR
0970 EXIT 
1000 ! load the sequence range
1010 LOAD_RANGE:
1020 SWITCH RPT_SEQ$
1030 CASE "1"
1035 IF NUL(RNG$(1,10)) THEN CUST_START$="" ELSE CUST_START$=RNG$(1,10)
1040 IF RNG$(11,3)="~~~" THEN CUST_END$="" ELSE CUST_END$=RNG$(11,10)
1045 IF NUL(RNG$(21,10)) THEN ITEM_START$="" ELSE ITEM_START$=RNG$(21,10)
1047 IF RNG$(31,3)="~~~" THEN ITEM_END$="" ELSE ITEM_END$=RNG$(31,10)
1058 RETURN 
1490 END SWITCH 
1500 RETURN 
3800 CHECK_CHANGES:
3805 GOSUB LOAD_OPTIONS
3810 IF CHG_FLG=0 THEN RETURN 
3820 MSGBOX MSG("UPD_RPT_1")+SEP+MSG("UPD_RPT_2"),MSG("CONFIRM"),"?,YESNO",ANS$
3840 IF ANS$="NO" THEN RETURN 
3850 WRITE (Z[1],KEY=KEY_1$)IOL=0170,IOL=0180
3860 B_ZZS$=ZZS$; B_PRT$=PRT$; B_RNG$=RNG$; CHG_FLG=0
3870 RETURN 
8000 ! Messages
8010 ADD_OPT:
8020 MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"?,YesNo",ANS$
8030 IF ANS$="NO" THEN CMD_STR$="END"; RETURN 
8040 KEY_1$="FM2UDE"+RPT_SELEC$
8050 DIM ZZS$(90),RNG$(100); ZZS$(1,18)=KEY_1$,ZZS$(71,1)="1",ZZS$(72,3)="SNN",ZZS$(80,2)="NN",RNG$(11,10)=DIM(10,"~"),RNG$(31,10)=DIM(10,"~") ! SSP#224109
8060 GOSUB SELECT_OPTIONS
8070 REFRESH_FLG=1; NEXT_ID=RPT_DESC.CTL
8090 RETURN 
9999 END 
17000 ! 
20000 LOAD_OPTIONS:
20010 ! View Report Routine.
20020 ! 
20030 ZZS$(1,6)=PRG_NAME$
20040 ZZS$(7,12)=RPT_SELEC$; ZZS$(19,40)=RPT_DESC$
20050 ZZS$(59,12)=RPT_OPT$; ZZS$(71,1)=RPT_SEQ$
20060 ZZS$(72,1)=PRICE_M$; ZZS$(73,1)=ALT_UNIT$; ZZS$(74,1)=LAST_ORD$
20070 ZZS$(75,5)=PAD(FORM_EX$,5); ZZS$(80,1)=PRINT_PCS$; ZZS$(81,1)=DOUBLE_SR$
20080 ZZS$(82,3)=SPEC_CODE$
20090 ZZS$(85,1)=INCL_COLREF$,ZZS$(86,1)=INCL_COLDESC$,ZZS$(87,1)=INCL_DATATYPE$
20100 ! 
20110 PRT$(1,1)=PRT_MODE$; PRT$(2,2)=RPT_OUTDEV$
20120 PRT$(9,1)=RPT_VERT$; PRT$(10,1)=RPT_HORIZ$
20130 PRT$(11,1)=RPT_AUX1$; PRT$(12,1)=RPT_AUX2$
20140 PRT$(13,1)=RPT_AUX3$; PRT$(14,1)=RPT_AUX4$
20150 PRT$(15,2)=RPT_LINES$; PRT$(17,1)=RPT_FORMCODE$
20160 PRT$(65,60)=RPT_JOBCARD$; PRT$(125,1)=RPT_JOBCARD_FLG$
20170 ! 
20175 RNG$=""
20180 ! 
20190 IF RPT_SEQ$="1" THEN {
20200 T1$="FSF..."
20210 GOSUB GET_CUST_RANGE; GOSUB GET_ITEM_RANGE
20220  }
20300 IF B_ZZS$=ZZS$ AND B_PRT$=PRT$ AND B_RNG$=RNG$ THEN CHG_FLR=0 ELSE CHG_FLG=1
20390 RETURN 
25000 ! 
25010 PRINT_REPORT:
25030 K$=X3$(1,8)
25040 %B_WINDATE_FMT$=%WINDATE_FMT$,%WINDATE_FMT$="YYYYMMDD",JJ$=DTE(0:"%Yl%Mz%Dz"); CALL "ZZWDTE;VALIDATE",JJ$,JJJ$,JJJJ$; X3$(21,6)=JJ$
25050 WRITE (Z[7],KEY=K$)X3$,X4$,T1$,PRT$,RNG$,ZZS$
25060 K9$="FM2UDE"; CALL K9$
25070 CMD_STR$="END"
25080 RETURN 
28000 ! 
28010 GET_CUST_RANGE:
28020 IF NUL(CUST_START$) THEN RNG$+="          " ELSE RNG$+=CUST_START$
28030 IF NUL(CUST_END$) THEN RNG$=RNG$+"~~~~~~~~~~" ELSE RNG$=RNG$+CUST_END$ }
28040 RETURN 
28050 ! 
28060 GET_ITEM_RANGE:
28070 IF NUL(ITEM_START$) THEN RNG$+="          " ELSE RNG$+=ITEM_START$
28080 IF NUL(ITEM_END$) THEN RNG$+="~~~~~~~~~~" ELSE RNG$+=ITEM_END$
28090 RETURN 
28100 ! 
28200 LOAD_SPEC_CODES:
28202 OPEN (HFN)"FSE"+%C$; FSECHAN=LFO
28205 LIST_BOX LOAD SPEC_CODE.CTL,""
28210 READ (FSECHAN,KEY="",DOM=*NEXT)
28220 FSEK$=KEY(FSECHAN,END=28290); DROP_BOX LOAD SPEC_CODE.CTL,0,MID(FSEK$,1,3)
28230 READ (FSECHAN,KEY=MID(FSEK$,1,3)+$FF$,DOM=28220)
28290 CLOSE (FSECHAN)
28295 RETURN 
28299 ! 
56000 REM "273353-User Defined Spec Export and Import programs.  See 272455   
