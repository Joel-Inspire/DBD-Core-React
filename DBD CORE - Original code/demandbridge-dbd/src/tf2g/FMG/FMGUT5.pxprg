0010 ! FMGUT5 - Cleanup Order files
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 04/05/12 - 12.5275 - dmm - SSP# 252273
0037 REM "252273-New Deleted Order and Deleted PO file. Modify delete routine
0040 REM "Copyright 2012 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 PROCESS "FMGUT5","../FMG/FM.EN"
0060 EXIT 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0105 INIT:
0110 X0$="FM2UT5",X1$="Cleanup Order Files"
0120 DIM Z0$(80,"-")
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O FS2...  02O FS9...  05O PO2...  06O PO3...  07O PO6...  "
0515 IF %IN_JOBSTREAM THEN Z$=Z$+"03O FS1...  04O PO1...  " ELSE Z$=Z$+"03L FS1...  04L PO1...  " ! In job stream mode don't lock FS1/PO1, we will skip the logic to check for headers with no lines
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 0521,9900
0600 C0$=X3$(9,3)
0666 SETERR 0000
0800 GOSUB 6000
0810 RETURN 
0820 IF Q1$="*" THEN F9$="N"; GOTO 0822 ELSE F9$=""; CALL "ZZPROM",".Y",X3$,Z,"Verify Deletes?","","",0; ON Z GOTO 0821,0822
0821 F9$="Y"
0900 REM 
0910 IF Q1$<>"*" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0
0920 ON Z GOTO 0921,9900
0930 PROCEED:
1000 REM "Removed orphaned FS2 Records"
1002 READ (Z[1],KEY="",DOM=1003)
1005 ML_MESS.CTL'VALUE$="Processing FS2"; WAIT 1
1010 K$=KEY(Z[1],END=1100)
1015 ML_MESS1.CTL'VALUE$=K$
1020 READ (Z[1],KEY=K$)A$
1025 GOSUB 7500
1030 FIND (Z[3],KEY=K$(1,8),DOM=1031); GOTO 1010
1035 IF F9$="Y" THEN _KEY$=K$; GOSUB ASK_DELETE; IF OPT$="NO" THEN GOTO 1010
1040 REMOVE (Z[1],KEY=K$)
1050 GOTO 1010
1100 REM "Was trying to cleanup FS9 - but FS6 also uses this file and you cannot get from FS9 to FS6.  Different method is required!! Do not use this 1100 routine!!"
1101 GOTO 1200
1102 READ (Z[2],KEY="",DOM=1103)
1105 ML_MESS.CTL'VALUE$="Processing FS9"; WAIT 1
1110 K$=KEY(Z[2],END=1200)
1115 ML_MESS1.CTL'VALUE$=K$
1120 READ (Z[2],KEY=K$)A$
1130 FIND (Z[3],KEY=K$(1,8),DOM=1131); GOTO 1110
1135 IF F9$="Y" THEN _KEY$=K$; GOSUB ASK_DELETE; IF OPT$="NO" THEN GOTO 1110
1140 REMOVE (Z[2],KEY=K$)
1150 GOTO 1110
1200 REM "Remove order headers with no lines
1205 IF %IN_JOBSTREAM THEN GOTO 1300
1210 READ (Z[3],KEY="",DOM=1211)
1220 ML_MESS.CTL'VALUE$="Processing FS1"
1230 K$=KEY(Z[3],END=1300)
1240 ML_MESS1.CTL'VALUE$=K$
1250 READ (Z[3],KEY=K$)A$
1260 READ (Z[1],KEY=K$,DOM=1261)
1270 K1$=KEY(Z[1],END=1280)
1275 IF K1$(1,8)=K$ THEN GOTO 1230
1280 IF F9$="Y" THEN _KEY$=K$; GOSUB ASK_DELETE; IF OPT$="NO" THEN GOTO 1230
1285 REMOVE (Z[3],KEY=K$); DIM FM2DEL[13]; CALL "FM2DEL;WRITE_FV2",X3$,X4$,A$,FM2DEL{ALL},FM2DEL_OTHER$ ! WO252273
1290 GOTO 1230
1300 REM "Remove orphaned PO2 Records"
1310 READ (Z[5],KEY="",DOM=1311)
1320 ML_MESS.CTL'VALUE$="Processing PO2"; WAIT 1
1330 K$=KEY(Z[5],END=1400)
1340 ML_MESS1.CTL'VALUE$=K$
1350 READ (Z[5],KEY=K$)A$
1360 FIND (Z[4],KEY=K$(1,9),DOM=1361); GOTO 1330
1365 IF F9$="Y" THEN _KEY$=K$; GOSUB ASK_DELETE; IF OPT$="NO" THEN GOTO 1330
1370 REMOVE (Z[5],KEY=K$)
1380 GOTO 1330
1400 REM "Remove P/O headers with no lines
1405 IF %IN_JOBSTREAM THEN GOTO 1500
1410 READ (Z[4],KEY="",DOM=1411)
1420 ML_MESS.CTL'VALUE$="Processing PO1"; WAIT 1
1430 K$=KEY(Z[4],END=1500)
1440 ML_MESS1.CTL'VALUE$=K$
1450 READ (Z[4],KEY=K$)A$
1460 READ (Z[5],KEY=K$,DOM=1461)
1470 K1$=KEY(Z[5],END=1480)
1475 IF K1$(1,9)=K$ THEN GOTO 1430
1480 IF F9$="Y" THEN _KEY$=K$; GOSUB ASK_DELETE; IF OPT$="NO" THEN GOTO 1430
1485 REMOVE (Z[4],KEY=K$); CALL "FM2DEL;WRITE_FV3",X3$,X4$,A$,FM2DEL_OTHER$ ! WO252273
1490 GOTO 1430
1500 REM "Remove orphaned PO6 Records"
1510 READ (Z[7],KEY="",DOM=1511)
1520 ML_MESS.CTL'VALUE$="Processing PO6"; WAIT 1
1530 K$=KEY(Z[7],END=1700)
1540 ML_MESS1.CTL'VALUE$=K$
1550 READ (Z[7],KEY=K$)
1560 P1$="",P2$="",F1$="",F2$=""; ON POS(K$(1,1)="CLPSWA") GOSUB 1596,1600,1625,1650,1675
1570 IF P1$>"" THEN FIND (Z[4],KEY=P1$,DOM=1590); GOTO 1530
1575 IF P2$>"" THEN FIND (Z[5],KEY=P2$,DOM=1590); GOTO 1530
1580 IF F1$>"" THEN FIND (Z[3],KEY=F1$,DOM=1590); GOTO 1530
1585 IF F2$>"" THEN FIND (Z[1],KEY=F2$,DOM=1590); GOTO 1530
1590 IF F9$="Y" THEN _KEY$=K$; GOSUB ASK_DELETE; IF OPT$="NO" THEN GOTO 1530
1592 REMOVE (Z[7],KEY=K$)
1595 GOTO 1530
1596 EXITTO 1590
1600 REM "Carton Labels
1610 IF K$(10,1)<>" " THEN P2$=K$(2,12) ELSE F2$=K$(2,8)+K$(11,3)
1620 RETURN 
1625 REM "Packing List
1630 F2$=K$(6,11)
1640 RETURN 
1650 REM "Purchase Order"
1660 P1$=K$(2,9)
1670 RETURN 
1675 REM "Sales Orders/Work Orders
1680 F1$=K$(2,8)
1690 RETURN 
1700 REM "Remove orphaned PO3 Records"
1710 READ (Z[6],KEY="",DOM=1711)
1720 ML_MESS.CTL'VALUE$="Processing PO3"; WAIT 1
1730 K$=KEY(Z[6],END=1800)
1740 ML_MESS1.CTL'VALUE$=K$
1750 READ (Z[6],KEY=K$)A$
1760 F2$=K$(1,8)+K$(10,3),P2$=K$(1,12)
1765 IF K$(10,4)="    " THEN F1$=K$(1,8),P1$=K$(1,9),F2$="",P2$=""
1770 IF F2$>"" THEN FIND (Z[1],KEY=F2$,DOM=1771); GOTO 1730
1775 IF P2$>"" THEN FIND (Z[5],KEY=P2$,DOM=1781); GOTO 1730
1780 IF F1$>"" THEN FIND (Z[3],KEY=F1$,DOM=1781); GOTO 1730
1782 IF P1$>"" THEN FIND (Z[4],KEY=P1$,DOM=1783); GOTO 1730
1785 IF F9$="Y" THEN _KEY$=K$; GOSUB ASK_DELETE; IF OPT$="NO" THEN GOTO 1730
1790 REMOVE (Z[6],KEY=K$)
1795 GOTO 1730
2000 Y4$="FM2UT9"
5000 REM "End
5020 IF Q1$<>"*" THEN MSGBOX "Process complete",MSG("CONFIRMING"),"!,TIM=5"
5040 CMD_STR$="E"; EXIT 
6000 REM 
6001 RETURN 
6020 PRINT @(0,4),"This program will process FS2 and FS9 removing any lines for which there are no orders.  Then order headers will be removed from FS1 for which there are no lines.  Then PO2 will be processed and any P/O lines for which there is no P/O header will be removed.  Then P/O headers will be removed from PO1 for which there are no lines.  Next, PO6 is processed to verify that for carton label, packing list, purchase order, sales order, and work order print entries that either an Order header, order line, p/o header or p/o lines exists.  If not the PO6 record is removed.  Next PO3 is processed to be sure that for each shipping instruction record there is either an Order or P/O header or line associated with it.  If not, the PO3 record is removed.  -  IT IS RECOMMENDED YOU REBUILD THE S/O AND P/O FILES UPON COMPLETION."
6030 PRINT 'LF',"Files are FS1, FS2, FS9, PO1, PO2, PO3, PO6."
6040 PRINT 'LF',"The company being processed is ",C0$,
6190 RETURN 
7500 REM "Fix Whse code for Direct Ships that require Packing lists 8/23/95
7510 IF A$(9,1)=" " OR POS("P"=A$(156,5))=0 THEN GOTO 7590
7520 IF POS(" "<>A$(215,4))>0 THEN GOTO 7590
7530 READ RECORD (Z[1],KEY=K$)X$
7540 X$(215,4)=X$(10,4),X$(10,4)="DS"
7560 WRITE RECORD (Z[1],KEY=K$)X$
7570 READ (Z[1],KEY=K$)A$
7590 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9011 IF Q0$(1,2)="JS" THEN A1$=$FF$+X0$+"|Error|"+STR(Y5)+"|Line|"+STR(Y6)+"|"; GOTO 9900
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9900,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 9900
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9900
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9800 REM "EXIT PROGRAM
9900 REM "END PROGRAM
9905 END_PRG:
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 SETESC 9350
9945 IF Y4$>"" THEN RUN Y4$
9950 RUN "ZMENU"
9999 END 
10000 ASK_DELETE:
10010 MSGBOX "Remove Key: "+_KEY$,MSG("Verify Delete"),"?,YESNOCANCEL",OPT$
10020 IF OPT$="CANCEL" THEN CMD_STR$="E"; EXIT 
10030 RETURN 
56000 REM "252273-New Deleted Order and Deleted PO file. Modify delete routine
