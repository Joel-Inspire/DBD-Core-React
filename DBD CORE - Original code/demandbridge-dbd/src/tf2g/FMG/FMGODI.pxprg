0010 ! FMGODI - Display Order Line
0035 REM "5.7 - 08/30/19 - 13.540277 - tma - SSP# 307207
0037 REM "307207-DBSPT-71144 GP% not showing the negative                    
0040 REM "Copyright 2019 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 IF TCB(13)=1 THEN BEGIN 
0060 CUSTOMER$=%CUSTOMER$
0070 PROCESS "FMGODI","../ARG/AR.EN"
0080 %CUSTOMER$=CUSTOMER$
0090 EXIT 
0095 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 _SV_KR=PRM('KR'); SET_PARAM 'KR'=0 ! Ensure native handling of KEP()
0140 CHANGE_FLG=0,_ENABLE_FLG=0,_KCNT=1,_FIRST_KEY=0,_FIRST_FIELD=0
0150 INVOICE_TYPE$=""
0163 X2=-1; X0=-1
0165 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2
0170 DIM Z[NUM(X3$(60,3))]
0175 Z$="01O FS2...  02O FS1...  05O PO3...  130 ZZPARM  07O AR1...  080 FM0...  09O PO1...  10O FS4...  11O FSO...  "
0180 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
0190 ! IO list
0200 DIM C[15],FS1[13],FS2[29]
0210 ! iolist for customer information
0220 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[5]
0225 ! iolist for Order
0230 IOLIST FS1$,FS1[0],FS1[1],FS1[2],FS1[3],FS1[4],FS1[5],FS1[6],FS1[7],FS1[8],FS1[9],FS1[10],FS1[11],FS1[12],FS1[13]
0240 IOLIST FS2$,FS2[0],FS2[1],FS2[2],FS2[3],FS2[4],FS2[5],FS2[6],FS2[7],FS2[8],FS2[9],FS2[10],FS2[11],FS2[12],FS2[13],FS2[14],FS2[15],FS2[16],FS2[17],FS2[18],FS2[19],FS2[20],FS2[21],FS2[22],FS2[23],FS2[24],FS2[25],FS2[26],FS2[27],FS2[28],FS2[29]
0265 RETURN 
0300 ! 300 - Set up the message library
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 IF %CUSTOMER$<>"" THEN {
0530 CUSTOMER$=%CUSTOMER$
0540 DISABLE CONTROL CUSTOMER.CTL
0545 DISABLE CONTROL BILLTO_CODE.CTL
0550 GOTO GET_CUSTOMER }
0560 NEXT_ID=CUSTOMER.CTL,REFRESH_FLG=1
0570 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0912 SET_PARAM 'XI'=0 ! SSP249849 jdf
0915 %LOC_TYPE$="",%CUST_DIV$="",%CUST_CODE$=""
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0960 SET_PARAM 'KR'=_SV_KR
0970 RETURN 
2000 ! get the customer information
2010 GET_CUSTOMER:
2020 CALL "ZZWLKU;Parse_cust",CUSTOMER$,B.CUST_DIV$,B.CUST_CODE$
2025 CUSTOMER$=B.CUST_DIV$+B.CUST_CODE$
2030 READ (Z[7],KEY=CUSTOMER$,DOM=*RETURN)IOL=0220
2040 CUST_NAME$=C$(11,35); BILLTO_CODE$=C$(46,2)+C$(48,8)
2050 BILLTO_NAME$=C$(11,35)
2060 %LOC_TYPE$="C",%CUST_DIV$=B.CUST_DIV$,%CUST_CODE$=B.CUST_CODE$
2090 REFRESH_FLG=1
2100 NEXT_ID=SHIPTO.CTL
2110 RETURN 
2400 ! List the open Orders for the customer specified
2410 LIST_ORDERS:
2416 LIST_BOX LOAD OPEN_ORDERS.CTL,""; CALL "*WINGRP;ENABLE",ORD_1.GRP$
2420 KEY_2$="" ! GOSUB LOAD_HEADER
2430 READ (Z[2],KNO=1,KEY=CUSTOMER$,DOM=*NEXT)
2440 KEY_2$=KEY(Z[2],END=2600); SET_PARAM 'XI'; READ (Z[2])IOL=0230; SET_PARAM 'XI'=0 ! SSP249849  jdf   
2450 IF KEY_2$(1,10)<>CUSTOMER$ THEN GOTO 2600
2451 IF NOT(NUL(INVOICE_TYPE$)) THEN GOSUB GET_ORDER_TYPE; IF NOT(OK_FLG) THEN GOTO 2440
2452 IF NUL(SHIP_TO$) THEN GOTO 2460
2454 KEY_5$=""; KEY_5$=FS1$(118,2)+FS1$(120,6); READ (Z[5],KEY=KEY_5$,DOM=*NEXT)
2456 KEY_5$=KEY(Z[5],END=2500); READ (Z[5],KEY=KEY_5$)PO3$
2457 IF KEY_5$(1,8)<>FS1$(118,8) THEN GOTO 2500
2458 IF PO3$(12,4)<>SHIP_TO$ THEN GOTO 2456
2460 D1$=FS1$(16,6); D2$=FS1$(55,6)
2461 MSG_1$=DIM(10); GOSUB GET_DESCR ! IF FS1$(54,1)="B" THEN MSG_1$="Bill as Ship"
2462 BL$=""
2463 IF FS1$(128,1)="S" THEN BL$='SF'+'_YELLOW'; CALL "*WINGRP;SHOW",INVO2.GRP$
2464 GOSUB CHECK_ORDER; IF BO_FLG THEN BL$='B?'; CALL "*WINGRP;SHOW",INVO3.GRP$
2465 IF FS1$(63,1)="Y" THEN BL$='SF'+'_CYAN'; CALL "*WINGRP;SHOW",INVO.GRP$
2470 CALL "ZZWDTE;DISPLAY",D1$,A1$,A2$; CALL "ZZWDTE;DISPLAY",D2$,A1$,A2$
2480 LIST_BOX LOAD OPEN_ORDERS.CTL,0,BL$+FS1$(118,2)+"-"+FS1$(120,6)+SEP+BL$+D1$+SEP+BL$+D2$+SEP+BL$+FS1$(28,15)+SEP+BL$+STR(FS1[6]:"##,###,###.00-")+SEP+BL$+FS1$(129,20)+SEP+BL$+MSG_1$ ! SSP#255989
2490 ! READ (Z[2],KNO=1,KEY=KEY_2$,DOM=*NEXT)
2500 GOTO 2440
2510 RETURN 
2600 LIST_BOX_FINISH:
2605 SET_PARAM 'XI'=0 ! SSP249849 jdf
2610 OPEN_ORDERS.CTL'SORT=7
2620 READ (Z(2),KEY="",KNO=0,DOM=*NEXT) ! SSP#269603
2690 RETURN 
2700 ! load the list box with the header
2710 LOAD_HEADER:
2720 LIST_BOX REMOVE OPEN_ORDERS.CTL
2730 LIST_BOX OPEN_ORDERS.CTL,@(2,6,78,19),OPT="r",FMT="[Order No.]l12 [Order Date]l10 [Req. Date]l10 [P/O #]l15 [Order Amt.]r10 [Ordered by]l20"
2740 RETURN 
2800 ! Use to select the typed of orders to display.
2810 ORDER_TYPE:
2820 REPLACEMENT_SCRN$="FMGODI_2"
2830 PERFORM "*WINPROC;OVERLAY_SCREEN"
2860 GOTO LIST_ORDERS
2890 RETURN 
2900 ! Get the order header information from the first panel
2910 SELECT_ORDER:
2911 ! READ DATA FROM OPEN_ORDERS$ TO XX1$,XX2$,XX3$,XX4$,XX5$,XX6$
2915 IF _EOM$=$01$ THEN RETURN ! 194087
2920 LIST_BOX READ OPEN_ORDERS.CTL,IDX
2930 LIST_BOX FIND OPEN_ORDERS.CTL,IDX,ORD_HEAD$
2935 XX=POS($1B53461B4236$=ORD_HEAD$); IF XX<>0 THEN ORD_HEAD$=ORD_HEAD$(1,XX-1)+ORD_HEAD$(XX+6); GOTO 2935
2936 XX=POS($1B423F$=ORD_HEAD$); IF XX<>0 THEN ORD_HEAD$=ORD_HEAD$(1,XX-1)+ORD_HEAD$(XX+3); GOTO 2936
2937 XX=POS($1B53461B4233$=ORD_HEAD$); IF XX<>0 THEN ORD_HEAD$=ORD_HEAD$(1,XX-1)+ORD_HEAD$(XX+6); GOTO 2937
2940 ORDER_NUMBER$=ORD_HEAD$(1,POS(SEP=ORD_HEAD$)-1),ORD_HEAD$=ORD_HEAD$(POS(SEP=ORD_HEAD$)+1)
2945 IF ORDER_NUMBER$="" THEN RETURN 
2950 ORDER_DATE$=ORD_HEAD$(1,POS(SEP=ORD_HEAD$)-1),ORD_HEAD$=ORD_HEAD$(POS(SEP=ORD_HEAD$)+1)
2960 REQ_DATE$=ORD_HEAD$(1,POS(SEP=ORD_HEAD$)-1),ORD_HEAD$=ORD_HEAD$(POS(SEP=ORD_HEAD$)+1)
2970 PO_NUM$=ORD_HEAD$(1,POS(SEP=ORD_HEAD$)-1),ORD_HEAD$=ORD_HEAD$(POS(SEP=ORD_HEAD$)+1)
2980 ORD_AMT$=ORD_HEAD$(1,POS(SEP=ORD_HEAD$)-1),ORD_HEAD$=ORD_HEAD$(POS(SEP=ORD_HEAD$)+1)
2985 REQ_BY$=ORD_HEAD$(1,POS(SEP=ORD_HEAD$)-1),ORD_HEAD$=ORD_HEAD$(POS(SEP=ORD_HEAD$)+1)
2986 ORDER_TYPE$=ORD_HEAD$
2987 Z1$=STR(Z[1]); Z9$=STR(Z[9])
2988 ORD_NUM$=STP(ORDER_NUMBER$,3,"-")
2990 PROCESS "FMGODI.1","../ARG/AR.EN",ORDER_NUMBER$,ORDER_DATE$,REQ_DATE$,PO_NUM$,ORD_AMT$,REQ_BY$,CUSTOMER$,CUST_NAME$,ORD_NUM$,Z1$,FS1$,C$,Z9$
3000 RETURN 
3100 ! Get the line on each order
3110 SELECT_LINE:
3115 LIST_BOX LOAD ORDER_LINE.CTL,""
3120 KEY_1$=""; KEY_1$=ARG_9$; Z1=NUM(ARG_10$); Z9=NUM(ARG_13$); DIM FS2[29]
3130 READ (Z1,KEY=KEY_1$,DOM=*NEXT)
3140 KEY_1$=KEY(Z1,END=*RETURN)
3150 READ (Z1,KEY=KEY_1$,DOM=*RETURN)IOL=0240
3160 IF KEY_1$(1,8)<>ARG_9$(1,8) THEN RETURN 
3165 IF NOT(NUL(FS2$(9,1))) THEN GOSUB GET_PO_INFO
3170 GP$=STR((FS2[6]-FS2[3]):"###,###,###.00-")
3172 BL$=""
3173 IF FS2$(48,1)="Y" OR FS2[8]<>0 THEN BL$='SF'+'_RED'; CALL "*WINGRP;SHOW",INVO4.GRP$
3175 IF FS2[6]=0 THEN GP_PER$="-100"; GOTO 3190
3180 GP_PER$=STR((((FS2[6]-FS2[3])*100/FS2[6])):"###.00-%") ! SSP#307207-DBSPT-71144
3190 QTY=FN%QTY_IN_UM(FS2(10),FS2$(124,4),FS2(5)); LIST_BOX LOAD ORDER_LINE.CTL,0,BL$+FS2$(6,3)+SEP+BL$+FS2$(19,10)+SEP+BL$+FS2$(50,40)+SEP+BL$+STR(QTY:"###,###,###.00-")+SEP+BL$+FS2$(124,4)+SEP+BL$+STR(FS2[6]:"###,###,###.00-")+SEP+BL$+GP$+SEP+BL$+GP_PER$+SEP+BL$+MID(PO1$,248,12)+SEP+BL$+D1$ ! ssp 198297 !SSP#255989
3200 GOTO 3140
3210 RETURN 
3300 ! Get Purchase Order Information
3310 GET_PO_INFO:
3320 PO1$=""
3330 READ (Z9,KEY=KEY_1$(1,8)+FS2$(9,1),DOM=*RETURN)PO1$
3340 D1$=""
3350 IF NOT(NUL(PO1$(291,6))) THEN D1$=PO1$(291,6); CALL "ZZWDTE;DISPLAY",D1$,A1$,A2$
3390 RETURN 
3500 ! Get the line info for each the order
3510 SELECT_LINE_INFO:
3515 IF _EOM$=$01$ THEN RETURN ! 194087
3520 LIST_BOX READ ORDER_LINE.CTL,IDX
3530 LIST_BOX FIND ORDER_LINE.CTL,IDX,ORD_LINE$
3535 XX=POS($1B53461B4231$=ORD_LINE$); IF XX<>0 THEN ORD_LINE$=ORD_LINE$(1,XX-1)+ORD_LINE$(XX+6); GOTO 3535
3540 ORD_LN$=ORD_LINE$(1,POS(SEP=ORD_LINE$)-1)
3545 IF ORD_LN$="" THEN RETURN 
3550 PROCESS "ICGMAJ.1","../ICG/IC.EN",ARG_9$+ORD_LN$,ARG_11$,ARG_12$
3690 RETURN 
5000 ! check the order details
5010 CHECK_ORDER:
5015 BO_FLG=0
5020 KEY_1$=KEY_2$(11,8)
5030 READ (Z[1],KEY=KEY_1$,DOM=*NEXT)
5040 KEY_1$=KEY(Z[1],END=*RETURN); READ (Z[1],KEY=KEY_1$)IOL=0240
5045 IF KEY_1$(1,8)<>KEY_2$(11,8) THEN RETURN 
5050 IF FS2$(48,1)="Y" OR FS2[8]<>0 THEN BO_FLG=1
5060 GOTO 5040
5090 RETURN 
8000 ! Determine if Order meets the criteria
8010 GET_ORDER_TYPE:
8020 OK_FLG=0
8030 IF INVOICE_TYPE$="S" THEN IF NUL(FS1$(47,1)) AND NUL(FS1$(54,1)) THEN OK_FLG=1; RETURN 
8040 IF INVOICE_TYPE$="B" THEN IF FS1$(54,1)="B" THEN OK_FLG=1; RETURN 
8050 IF INVOICE_TYPE$=FS1$(47,1) THEN OK_FLG=1
8090 RETURN 
8200 ! Get the Order Type Description
8210 GET_DESCR:
8220 IF NUL(FS1$(47,1)) AND NUL(FS1$(54,1)) THEN MSG_1$=MSG("SALES_ORD")
8230 IF FS1$(54,1)="B" THEN MSG_1$=MSG("ORDE_T_3_B")
8240 IF FS1$(47,1)="R" THEN MSG_1$=MSG("ORDE_T_3_R")
8250 IF FS1$(47,1)="W" THEN MSG_1$=MSG("ORDE_T_3_W")
8290 RETURN 
9999 END 
56000 REM "190144-In Open Sales Order Inquiry should have the ability to      
56001 ! "198297-Qty order is not showing correctly in Open Order Inquiry    
56002 ! "       in GUI Accounts Receivable."
56003 REM "249849-Getting record is currently in use when in open order       
56004 REM "269643-GUI:  Change ship via in Order Entry in Open Order Inquiry  
56005 REM "307207-DBSPT-71144 GP% not showing the negative                    
