0010 ! <FMGODQ> Repeat Order Query panel
0035 REM "5.7 - 07/17/15 - 15.186111 - dmm - SSP# 277832
0037 REM "277832-Graphical OE; Repeat Order selection allows other customer's
0040 REM "Copyright 2015 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0100 ! 100 - Initialization
0110 INIT:
0120 _FYI$=MSG("FYI") ! SSP277832
0125 CALL "ZZWMX3"
0135 %RO_CUST$=%CUST_DIV$+%CUST_CODE$
0146 _ZZPARM=HFN; OPEN (_ZZPARM)"ZZPARM"
0147 %OP_PARMS$=""; FIND (_ZZPARM,KEY=%X3_COMP$+"F/M",DOM=*NEXT)%OP_PARMS$
0150 M0$="#,###,###.00-",M1$="##,###-",M2$="#,###,###-" ! SSP#228634
0160 _FILE_NAME$="FT7"+%C$
0170 _FIL_NO=HFN; OPEN (_FIL_NO,IOL=*)_FILE_NAME$
0185 _AR1=HFN; OPEN (_AR1,IOL=*,REC=AR1$)"AR1"+%C$; CALL "ZZWIOL",_AR1,"B",AR1_IOL$; DIM AR1$:CPL(AR1_IOL$)
0187 _FT3=HFN; OPEN (_FT3,IOL=*,REC=FT3$)"FT3"+%C$; CALL "ZZWIOL",_FT3,"B",FT3_IOL$; DIM FT3$:CPL(FT3_IOL$)
0188 _FS2=HFN; OPEN (_FS2,IOL=*,REC=FS2$)"FT4"+%C$; CALL "ZZWIOL",_FS2,"B",FS2_IOL$; DIM FS2$:CPL(FS2_IOL$) ! Code use FS2 like order, but really open on FT4
0189 GOSUB SEE_IF_ANY_RECORDS_TO_DO
0190 RETURN 
0500 MAIN_POST_DISPLAY:
0505 FIND (_AR1,KEY=%RO_CUST$,DOM=*NEXT,REC=AR1$)
0510 %RO_CUST_NAME$=AR1.CUST_NAME$
0520 NEXT_ID=RO_ORDER.CTL
0530 IF NOT(NUL(ARG_1$)) THEN RO_ORDER$=ARG_1$,_EOM$=$0D$; GOTO FIND_REC
0590 REFRESH_FLG=1
0595 RETURN 
0599 ! 
1000 ! 1000 - Find record
1005 FIND_REC:
1010 CHANGE_FLG=0; IF POS(_EOM$=$000102090D$)=0 THEN RETURN ELSE IF _EOM$=$00$ THEN IF QRY_VAL$<>"" THEN QRY_VAL$="" ELSE RETURN 
1015 IF NUL(RO_ORDER$) THEN CMD_STR$="E"; RETURN 
1020 READ (_FIL_NO,KEY=%RO_CUST$+RO_ORDER$,DOM=NOT_FOUND,ERR=CHK_ERR_NXT) ! SSP277832
1023 GOSUB LOAD_DETAILS
1025 CHANGE_FLG=0,REFRESH_FLG=1,NEXT_ID=BUTTON_9.CTL
1030 EXIT 
1200 NOT_FOUND:! Record must be for order's customer code, if not found in FT7 then don't allow use of this order number SSP277832
1205 MSGBOX MSG("FMGODQ_01"),_FYI$,"!"; RO_ORDER$="",NEXT_ID=RO_ORDER.CTL,REFRESH_FLG=1
1210 RETURN 
2070 CHK_ERR_NXT:IF ERR<>0 THEN EXIT ERR
3000 WRAP_UP:
3010 ARG_1$=RO_ORDER$
3020 IF _FIL_NO THEN CLOSE (_FIL_NO); _FIL_NO=0
3021 IF _ZZPARM THEN CLOSE (_ZZPARM); _ZZPARM=0
3022 IF _AR1 THEN CLOSE (_AR1); _AR1=0
3023 IF _FT3 THEN CLOSE (_FT3); _FT3=0
3024 IF _FS2 THEN CLOSE (_FS2); _FS2=0
3025 IF _PO3 THEN CLOSE (_PO3); _PO3=0
3026 IF _FM0 THEN CLOSE (_FM0); _FM0=0
3027 IF _AP4 THEN CLOSE (_AP4); _AP4=0
3090 CMD_STR$="E"
3095 RETURN 
3099 ! 
9999 END 
21000 ! 21000 Read and load order line details 
21005 LOAD_DETAILS:
21010 ORDER_DIV$=MID(RO_ORDER$,1,2),ORDER$=MID(RO_ORDER$,3,6)
21012 TOTAL_AMT=0,TOTAL_GP=0,LINE_COUNT=0
21015 ORDER_LINES_IOL:IOLIST COL1$,COL2$,COL3$,COL3I$,COL4$,COL5$,COL6$,COL7$
21020 LIST_BOX LOAD ORDER_LINES.CTL,""
21025 READ (_FS2,KEY=ORDER_DIV$+ORDER_NUM$,DOM=*NEXT)
21030 NEXT_FS2_REC:READ (_FS2,END=DONE_LOADING,REC=FS2$)
21035 IF FS2.ORDER_DIV$<>ORDER_DIV$ OR FS2.ORDER_NUM$<>ORDER_NUM$ THEN GOTO DONE_LOADING
21040 LINE1: COL1$=FS2.ORDER_LINE_NUM$,COL2$=FS2.TF_DESC$
21042 GOSUB SHIP_FROM_IN_COL6
21045 GOSUB LOAD_LIST
21050 LINE2:IF FS2.LINE_TYPE$="M" THEN GOTO NEXT_FS2_REC
21051 COL2$=PAD(FS2.ITEM_CODE$,21,1),COL3I$=STP(FS2.SELL_UM$,2)+"/"+STP(STR(FS2.QTY_IN_SELL_UM:M1$),2) ! SSP#228634
21052 LINE_COUNT+=1,TOTAL_AMT+=FS2.SELL_PRICE_EXT,TOTAL_GP+=FS2.SELL_PRICE_EXT-FS2.COST_EXT,COL3$=STR(FN%QTY_IN_UM(FS2.ORIG_QTY_ORDERED,FS2.SELL_UM$,FS2.QTY_IN_SELL_UM):M2$),COL4$=STR(FS2.SELL_PRICE_EXT:M0$),COL5$=STR(FS2.SELL_PRICE_EXT-FS2.COST_EXT:M0$) ! SSP228634
21060 DO_SHIPTOS:
21064 ! PO3 used in code, but really opened on FT5 to access repeat order
21065 IF _PO3=0 THEN _PO3=HFN; OPEN (_PO3,IOL=*,ERR=NEXT_FS2_REC)"FT5"+%C$; CALL "ZZWIOL",_PO3,"B",PO3_IOL$; DIM GENERAL$:CPL(PO3_IOL$),SPECIAL$:CPL(PO3_IOL$)
21066 IF _FM0=0 THEN _FM0=HFN; OPEN (_FM0,IOL=*,ERR=NEXT_FS2_REC)"FM0"+%C$; CALL "ZZWIOL",_FM0,"B",FM0_IOL$; DIM FM0$:CPL(FM0_IOL$)
21067 IF FS2.SPECIAL_SHIPPING$="Y" THEN GOTO DO_SPECIAL_SHIPPING
21069 ! General shipping only, print the one line & we're done
21070 READ DATA FROM "" TO GENERAL$; FIND (_PO3,KEY=ORDER_DIV$+ORDER_NUM$+DIM(5),DOM=*NEXT,REC=GENERAL$)
21072 COL6$="To:"+GENERAL.LOC_TYPE$+" "+GENERAL.LOC_CODE$+" "+GENERAL.COMP_NAME_TO_SHIP_TO$
21075 GOSUB LOAD_LIST
21080 GOTO NEXT_FS2_REC
21090 DO_SPECIAL_SHIPPING:! loop through records for line and make multiple lines
21095 GOSUB LOAD_LIST ! Go ahead and put out line, then add special shipping lines
21097 READ (_PO3,KEY=ORDER_DIV$+ORDER_NUM$+" "+FS2.ORDER_LINE_NUM$,DOM=*NEXT)
21099 NEXT_SPECIAL_REC: READ DATA FROM "" TO SPECIAL$; READ (_PO3,END=END_SPECIAL_SHIPPING,REC=SPECIAL$); IF SPECIAL.PO_DIV$<>ORDER_DIV$ OR SPECIAL.PO_NUM$(1,LEN(ORDER_NUM$))<>ORDER_NUM$ OR SPECIAL.ORDER_LINE_NUM$<>FS2.ORDER_LINE_NUM$ THEN GOTO END_SPECIAL_SHIPPING
21101 COL6$=STR(FN%QTY_IN_UM(SPECIAL.SHIP_QTY,FS2.SELL_UM$,FS2.QTY_IN_SELL_UM):M2$)+" To: "+SPECIAL.LOC_TYPE$+" "+SPECIAL.LOC_CODE$+" "+SPECIAL.COMP_NAME_TO_SHIP_TO$ ! SSP#228634
21105 GOSUB LOAD_LIST
21110 GOTO NEXT_SPECIAL_REC
21115 END_SPECIAL_SHIPPING:
21120 DONE_WITH_FS2:GOTO NEXT_FS2_REC
21195 DONE_LOADING:
21196 ! Print totals, freight, deposit
21197 IF LINE_COUNT>0 THEN COL3I$='BR'+MSG("FMGODM_15"),COL4$='BR'+STR(TOTAL_AMT:M0$),COL5$='BR'+STR(TOTAL_GP:M0$); GOSUB LOAD_LIST ! SSP#228634
21198 IF FRT_AMT<>0 THEN COL3I$='_BLUE'+'WHITE'+MSG("FMGODM_16"),COL4$='_BLUE'+'WHITE'+STR(FRT_AMT:M0$); GOSUB LOAD_LIST ! SSP#228634
21199 IF DEPOSIT_AMT<>0 THEN COL3I$='_GREEN'+'WHITE'+MSG("FMGODM_17"),COL4$='_GREEN'+'WHITE'+STR(DEPOSIT_AMT:M0$); GOSUB LOAD_LIST ! SSP#228634
21205 RETURN 
23000 ! 23000 - Load list box & clear line
23005 LOAD_LIST: LIST_BOX LOAD ORDER_LINES.CTL,0,REC(IOL=ORDER_LINES_IOL)
23010 READ DATA FROM "" TO IOL=ORDER_LINES_IOL
23020 RETURN 
24000 ! ^1000 - Load col6 with ship from whse & desc or P/O & vendor
24005 SHIP_FROM_IN_COL6:
24010 ! Open file and load iolist if not open
24013 IF STP(FS2.PO_CODE$,2)<>"" THEN GOTO DO_PO
24015 IF _FM0=0 THEN _FM0=HFN; OPEN (_FM0,IOL=*,ERR=*RETURN)"FM0"+%C$; CALL "ZZWIOL",_FM0,"B",FM0_IOL$; DIM FM0$:CPL(FM0_IOL$)
24019 READ DATA FROM "" TO FM0$
24020 FIND (_FM0,KEY="D"+DIM(10)+FS2.WHSE$,DOM=*NEXT,REC=FM0$)
24030 COL6$="From: "+FS2.WHSE$+"-"+FM0.LOC_DESC$
24045 GOTO *RETURN
24050 DO_PO:! there is a p/O code
24055 IF _AP4=0 THEN _AP4=HFN; OPEN (_AP4,IOL=*,ERR=*RETURN)"AP4"+%C$; CALL "ZZWIOL",_AP4,"B",AP4_IOL$; DIM AP4$:CPL(AP4_IOL$)
24060 READ DATA FROM "" TO AP4$; FIND (_AP4,KEY=FS2.VEND_DIV$+FS2.VEND_CODE$,DOM=*RETURN,REC=AP4$)
24065 COL6$=ORDER$+"-"+FS2.PO_CODE$+"->"+AP4.VEND_NAME$
24090 RETURN 
24100 SEE_IF_ANY_RECORDS_TO_DO:! If no repeat orders for this customer then skip this screen
24110 _FT7=HFN; OPEN (_FT7)"FT7"+%C$
24120 READ (_FT7,KEY=%RO_CUST$,DOM=*NEXT)
24125 FT7_KEY$=KEY(_FT7,END=*NEXT)
24130 IF MID(FT7_KEY$,1,LEN(%RO_CUST$))<>%RO_CUST$ THEN GOTO WRAP_UP
24195 RETURN 
24199 ! 
56000 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
56002 REM "228634-In Repeat order, it shows the dollar sign versus the pound  
56004 REM "277832-Graphical OE; Repeat Order selection allows other customer's
