0010 ! FMGRIS - O/P Open Sales Order report
0035 REM "5.7 - 01/11/12 - 10.776944 - tma - SSP# 251752
0037 REM "251752-Changes to the Open Sales Order Report. Panel is FMGRIS     
0040 REM "Copyright 2012 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 PROCESS "FMGRIS","../FMG/FM.EN"
0060 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 _SV_KR=PRM('KR'); SET_PARAM 'KR'=0 ! Ensure native handling of KEP()
0130 X0=-1,X2=-1
0140 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2
0150 ! 
0155 DIM ZZS$(150),PRT$(125),RNG[4],B.RNG[4],SPC$(126)
0160 ! iolist
0170 IOLIST ZZS$(1)
0180 IOLIST PRT$(1),RNG$,RNG{ALL}
0300 ! Open Files
0310 DIM Z[NUM(X3$(60,3))]
0320 Z$="01O ZZS...  03O AR3...  06O ZZPARM  07O ZZP     "
0330 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
0400 ! 
0410 RPT_SELEC$="            "; VIEW$=""
0480 GOSUB GET_PARAM
0485 PRT_MODES$="QS" ! SSP 240570
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 KEY_1$="FM2RIA"+RPT_SELEC$; %K9$="FM2RIA"
0530 READ (Z[1],KEY=KEY_1$,DOM=ADD_OPT)IOL=0170,IOL=0180
0560 IF P0$(226,1)="Y" THEN CALL "*WINGRP;SHOW",ORD.GRP$
0570 B_ZZS$=ZZS$; B_PRT$=PRT$; B_RNG$=RNG$; CHG_FLG=0
0580 GOSUB SELECT_OPTIONS
0590 RETURN 
0600 ! selection Options
0610 SELECT_OPTIONS:
0630 PRG_NAME$=ZZS$(1,6); RPT_SELEC$=ZZS$(7,12); RPT_DESC$=ZZS$(19,40)
0640 RPT_OPT$=ZZS$(59,12); RPT_SEQ$=ZZS$(71,1)
0650 SO_DET$=ZZS$(72,1); ELIM_REQ$=ZZS$(73,1); PR_NOTE$=ZZS$(98,1)
0660 PR_UNBILL$=ZZS$(99,1); ORD_INV$=ZZS$(105,1); ORD_UNBILL$=ZZS$(106,1)
0670 ELIM_BO$=ZZS$(107,1); SUB_UNBILL$=ZZS$(108,1); SUSP$=ZZS$(109,1)
0675 REC_WAREHOUSE$=ZZS$(110,1); REM SSP 189197
0676 UB_BYLINE$=ZZS$(111,1); REM SSP 193975
0680 ORD_TYPE$=STP(ZZS$(100,5))
0690 ORD_D_START$=ZZS$(74,6); IF ZZS$(80,3)="~~~" THEN ORD_D_END$="      " ELSE ORD_D_END$=ZZS$(80,6)
0700 REQD_START$=ZZS$(86,6); IF ZZS$(92,3)="~~~" THEN REQD_END$="      " ELSE REQD_END$=ZZS$(92,6)
0800 ! Output Options
0810 PRT_MODE$=PRT$(1,1); RPT_OUTDEV$=PRT$(2,6)
0820 RPT_VERT$=PRT$(9,1); RPT_HORIZ$=PRT$(10,1)
0830 RPT_AUX1$=PRT$(11,1); RPT_AUX2$=PRT$(12,1); RPT_AUX3$=PRT$(13,1); RPT_AUX4$=PRT$(14,1)
0840 RPT_LINES$=PRT$(15,2); RPT_FORMCODE$=PRT$(17,1)
0850 RPT_JOBCARD$=PRT$(65,60); RPT_JOBCARD_FLG$=PRT$(125,1)
0860 GOSUB LOAD_RANGE
0870 NEXT_ID=RPT_VEIW.CTL
0880 REFRESH_FLG=1
0890 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0960 SET_PARAM 'KR'=_SV_KR
0970 EXIT 
1000 ! load the sequence range
1010 LOAD_RANGE:
1020 SWITCH RPT_SEQ$
1030 CASE "1"
1040 IF NUL(RNG$(1,8)) THEN ORD_START$="" ELSE ORD_START$=RNG$(1,8)
1050 IF RNG$(9,3)="~~~" THEN ORD_END$="" ELSE ORD_END$=RNG$(9,8)
1090 NEXTFOLDER_FLG=FLDR.FM1.3.CTL
1095 RETURN 
1100 CASE "2"
1103 IF NUL(RNG$(1,4)) THEN SLS_START$="" ELSE SLS_START$=RNG$(1,4)
1106 IF RNG$(5,3)="~~~" THEN SLS_END$="" ELSE SLS_END$=RNG$(5,4)
1110 IF NUL(RNG$(9,2)) THEN DIV_START$="" ELSE DIV_START$=RNG$(9,2)
1120 IF RNG$(11,2)="~~" THEN DIV_END$="" ELSE DIV_END$=RNG$(11,2)
1150 IF NUL(RNG$(13,10)) THEN CUST_START$="" ELSE CUST_START$=RNG$(13,10)
1160 IF RNG$(23,3)="~~~" THEN CUST_END$="" ELSE CUST_END$=RNG$(23,10)
1170 IF NUL(RNG$(33,8)) THEN ORD_START$="" ELSE ORD_START$=RNG$(33,8)
1180 IF RNG$(41,3)="~~~" THEN ORD_END$="" ELSE ORD_END$=RNG$(41,8)
1190 NEXTFOLDER_FLG=FLDR.FM1.7.7.CTL
1200 RETURN 
1300 CASE "3"
1310 IF NUL(RNG$(1,2)) THEN DIV_START$="" ELSE DIV_START$=RNG$(1,2)
1320 IF RNG$(3,2)="~~" THEN DIV_END$="" ELSE DIV_END$=RNG$(3,2)
1330 IF NUL(RNG$(5,10)) THEN CUST_START$="" ELSE CUST_START$=RNG$(5,10)
1340 IF RNG$(15,3)="~~~" THEN CUST_END$="" ELSE CUST_END$=RNG$(15,10)
1350 IF NUL(RNG$(25,8)) THEN ORD_START$="" ELSE ORD_START$=RNG$(25,8)
1360 IF RNG$(33,3)="~~~" THEN ORD_END$="" ELSE ORD_END$=RNG$(33,8)
1380 NEXTFOLDER_FLG=FLDR.FM1.7.8.CTL
1390 RETURN 
1400 CASE "4"
1410 IF NUL(RNG$(1,4)) THEN LOC_START$="" ELSE LOC_START$=RNG$(1,4)
1420 IF RNG$(5,4)="~~~~" THEN LOC_END$="" ELSE LOC_END$=RNG$(5,4)
1430 IF NUL(RNG$(9,8)) THEN ORD_START$="" ELSE ORD_START$=RNG$(9,8)
1440 IF RNG$(17,8)="~~~~~~~~" THEN ORD_END$="" ELSE ORD_END$=RNG$(17,8)
1480 NEXTFOLDER_FLG=FLDR.FM1.7.9.CTL
1485 RETURN 
1490 END SWITCH 
1500 RETURN 
3800 CHECK_CHANGES:
3805 GOSUB LOAD_OPTIONS
3810 IF CHG_FLG=0 THEN RETURN 
3820 MSGBOX MSG("UPD_RPT_1")+SEP+MSG("UPD_RPT_2"),MSG("CONFIRM"),"?,YESNO",ANS$
3840 IF ANS$="NO" THEN RETURN 
3850 WRITE (Z[1],KEY=KEY_1$)IOL=0170,IOL=0180
3860 B_ZZS$=ZZS$; B_PRT$=PRT$; B_RNG$=RNG$; CHG_FLG=0
3870 RETURN 
3880 ! 
8000 ! Messages
8010 ADD_OPT:
8020 MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"?,YesNo",ANS$
8030 IF ANS$="NO" THEN CMD_STR$="END"; RETURN 
8040 KEY_1$="FM2RIA"+RPT_SELEC$
8050 DIM ZZS$(150),RNG$(100); ZZS$(1,18)=KEY_1$,ZZS$(71,1)="1",ZZS$(72,1)="N",ZZS$(73,1)="N",ZZS$(80,6)=DIM(6,"~"),ZZS$(92,6)=DIM(6,"~"),ZZS$(98,3)="NNA",ZZS$(105,5)="NNNNN"; REM ,RNG$(9,8)=DIM(8,"~"),SLS_START$="",SLS_END$="",CUST_START$="",CUST_END$="",DIV_START$="",DIV_END$=""
8060 GOSUB SELECT_OPTIONS
8070 REFRESH_FLG=1; NEXT_ID=RPT_DESC.CTL
8090 RETURN 
9000 ! GET PARAMETERS
9010 GET_PARAM:
9020 READ (Z[6],KEY=X3$(9,3)+"F/M",DOM=*NEXT)P0$
9090 RETURN 
9999 END 
17000 ! 
20000 LOAD_OPTIONS:
20010 ! View Report Routine.
20020 ! 
20025 ZZS$=PAD(ZZS$,150)
20030 ZZS$(1,6)=PRG_NAME$
20040 ZZS$(7,12)=RPT_SELEC$; ZZS$(19,40)=RPT_DESC$
20050 ZZS$(59,12)=RPT_OPT$; ZZS$(71,1)=RPT_SEQ$
20060 ZZS$(72,1)=SO_DET$; ZZS$(73,1)=ELIM_REQ$; ZZS$(98,1)=PR_NOTE$
20070 ZZS$(99,1)=PR_UNBILL$; ZZS$(105,1)=ORD_INV$; ZZS$(106,1)=ORD_UNBILL$
20080 ZZS$(107,1)=ELIM_BO$; ZZS$(108,1)=SUB_UNBILL$; ZZS$(109,1)=SUSP$
20085 ZZS$(100,5)=ORD_TYPE$
20090 ZZS$(74,6)=ORD_D_START$; IF NUL(ORD_D_END$) THEN ZZS$(80,6)="~~~~~~" ELSE ZZS$(80,6)=ORD_D_END$
20095 ZZS$(86,6)=REQD_START$; IF NUL(REQD_END$) THEN ZZS$(92,6)="~~~~~~" ELSE ZZS$(92,6)=REQD_END$
20096 ZZS$(110,1)=REC_WAREHOUSE$; REM "SSP 189197
20097 ZZS$(111,1)=UB_BYLINE$; REM SSP 193975
20100 ! 
20110 PRT$(1,1)=PRT_MODE$; PRT$(2,2)=RPT_OUTDEV$
20120 PRT$(9,1)=RPT_VERT$; PRT$(10,1)=RPT_HORIZ$
20130 PRT$(11,1)=RPT_AUX1$; PRT$(12,1)=RPT_AUX2$
20140 PRT$(13,1)=RPT_AUX3$; PRT$(14,1)=RPT_AUX4$
20150 PRT$(15,2)=RPT_LINES$; PRT$(17,1)=RPT_FORMCODE$
20160 PRT$(65,60)=RPT_JOBCARD$; PRT$(125,1)=RPT_JOBCARD_FLG$
20170 ! 
20175 RNG$=""
20180 ! 
20190 IF RPT_SEQ$="1" THEN {
20200 T1$="FS1..."
20210 GOSUB GET_ORD_RANGE
20215  }
20220 IF RPT_SEQ$="2" THEN {
20230 T1$="FSI..."
20240 GOSUB GET_SLSMAN_RANGE; GOSUB GET_DIV_RANGE
20250 GOSUB GET_CUST_RANGE; GOSUB GET_ORD_RANGE }
20260 IF RPT_SEQ$="3" THEN {
20270 T1$="FSJ..."
20280 GOSUB GET_DIV_RANGE; GOSUB GET_CUST_RANGE
20290 GOSUB GET_ORD_RANGE }
20292 IF RPT_SEQ$="4" THEN {
20294 T1$="RIA..."
20296 GOSUB GET_LOC_RANGE
20297 GOSUB GET_ORD_RANGE }
20300 IF B_ZZS$=ZZS$ AND B_PRT$=PRT$ AND B_RNG$=RNG$ THEN CHG_FLG=0 ELSE CHG_FLG=1
20390 RETURN 
25000 ! 
25010 PRINT_REPORT:
25020 IF NOT(NUL(VIEW$)) THEN PRT$(2,2)=VIEW$
25030 K$=X3$(1,8)
25040 %B_WINDATE_FMT$=%WINDATE_FMT$,%WINDATE_FMT$="YYYYMMDD",JJ$=DTE(0:"%Yl%Mz%Dz"); CALL "ZZWDTE;VALIDATE",JJ$,JJJ$,JJJJ$; X3$(21,6)=JJ$
25045 IF POS(PRT_MODE$="QS")>0 THEN GOSUB REPORT_PRINT_MODE; CMD_STR$="END"; RETURN ! SSP 240570
25050 WRITE (Z[7],KEY=K$)X3$,X4$,T1$,PRT$,RNG$,ZZS$
25060 IF RPT_SEQ$="4" THEN PRG_TO_CALL$="FM2RIB" ELSE PRG_TO_CALL$="FM2RIA"
25065 IF RB_PRT$>"0" THEN CALL "ZZ2DRV",ERR=*NEXT,PRG_TO_CALL$ ELSE CALL PRG_TO_CALL$,ERR=*NEXT
25070 CMD_STR$="END"
25080 RETURN 
28000 ! 
28010 GET_CUST_RANGE:
28020 IF NUL(CUST_START$) THEN RNG$+=DIM(10) ELSE RNG$+=CUST_START$
28030 IF NUL(CUST_END$) THEN RNG$=RNG$+DIM(10,"~") ELSE RNG$=RNG$+CUST_END$
28040 RETURN 
28050 ! 
28060 GET_SLSMAN_RANGE:
28070 IF NUL(SLS_START$) THEN RNG$+="    " ELSE RNG$+=SLS_START$
28080 IF NUL(SLS_END$) THEN RNG$+="~~~~" ELSE RNG$+=SLS_END$
28090 RETURN 
28100 ! 
28110 GET_ITEM_RANGE:
28120 IF NUL(ITEM_START$) THEN RNG$+=DIM(10) ELSE RNG$=RNG$+ITEM_START$
28130 IF NUL(ITEM_END$) THEN RNG$=RNG$+DIM(10,"~") ELSE RNG$=RNG$+ITEM_END$
28140 RETURN 
28150 ! 
28160 GET_PROD_RANGE:
28170 IF NUL(PROD_START$) THEN RNG$=RNG$+"   " ELSE RNG$=RNG$+PROD_START$
28180 IF NUL(PROD_END$) THEN RNG$=RNG$+"~~~" ELSE RNG$=RNG$+PROD_END$
28190 RETURN 
28200 ! 
28210 GET_DIV_RANGE:
28220 IF NUL(DIV_START$) THEN RNG$=RNG$+"  " ELSE RNG$=RNG$+DIV_START$
28230 IF NUL(DIV_END$) THEN RNG$+="~~" ELSE RNG$+=DIV_END$
28240 RETURN 
28250 ! 
28260 GET_ORD_RANGE:
28270 IF NUL(ORD_START$) THEN RNG$=RNG$+DIM(8) ELSE RNG$=RNG$+ORD_START$
28280 IF NUL(ORD_END$) THEN RNG$=RNG$+"~~~~~~~~" ELSE RNG$=RNG$+ORD_END$
28290 RETURN 
28300 ! 
28310 GET_TB_RANGE:
28320 IF NUL(TB_START$) THEN RNG$=RNG$+"        " ELSE RNG$=RNG$+TB_START$
28330 IF NUL(TB_END$) THEN RNG$=RNG$+"~~~~~~~~" ELSE RNG$=RNG$+TB_END$
28340 RETURN 
28350 ! 
28360 GET_LOC_RANGE:
28370 IF NUL(LOC_START$) THEN RNG$=RNG$+DIM(4) ELSE RNG$=RNG$+LOC_START$
28380 IF NUL(LOC_END$) THEN RNG$=RNG$+"~~~~" ELSE RNG$=RNG$+LOC_END$
28390 RETURN 
30180 LOAD_FISCAL_YEAR:
30190 ZZPARM=HFN
30200 OPEN (ZZPARM)"ZZPARM"
30210 READ (ZZPARM,KEY=%C$+"A/R",DOM=*NEXT)GL_REC$; CR_YEAR$=GL_REC$(7,4),CR_PD$=GL_REC$(11,2)
30215 IF NUL(RPT_FISYR$)=0 THEN CR_YEAR$=RPT_FISYR$
30220 KE$=%C$+"G/LYE",RPT_FISYR$=""
30230 READ (ZZPARM,KEY=KE$,DOM=*NEXT)
30240 K$=KEY(ZZPARM,END=30280); READ (ZZPARM)Z9$
30250 IF LEN(K$)<8 OR KE$<>K$(1,8) THEN GOTO END_LOAD_FISCAL_YEAR
30260 RPT_FISYR$=RPT_FISYR$+K$(9)+"/"
30265 IF K$(9,4)=CR_YEAR$ THEN CC$=Z9$(21)
30270 GOTO 30240
30280 END_LOAD_FISCAL_YEAR:
30290 DROP_BOX LOAD RPT_FISYR.CTL,RPT_FISYR$
30300 DROP_BOX WRITE RPT_FISYR.CTL,CR_YEAR$
30310 DROP_BOX LOAD END_FISCAL.CTL,RPT_FISYR$
30320 DROP_BOX WRITE END_FISCAL.CTL,CR_YEAR$
30321 RPT_FISYR$=CR_YEAR$
30322 IF NUL(RPT_PER$)=0 THEN CR_PD$=RPT_PER$
30325 RPT_PER$=""
30330 FOR ACCT=1 TO 12; RPT_PER$=RPT_PER$+STR(ACCT:"00")+"/"; NEXT ACCT
30340 DROP_BOX LOAD RPT_PER.CTL,RPT_PER$
30350 DROP_BOX WRITE RPT_PER.CTL,CR_PD$
30355 RPT_PER$=CR_PD$
30360 END_ACCTPD$=RPT_PER$
30370 DROP_BOX LOAD END_ACCTPD.CTL,RPT_PER$
30380 DROP_BOX WRITE END_ACCTPD.CTL,CR_PD$
30390 RETURN 
32000 LOAD_ENDING_DATE:
32005 IF LEN(RPT_FISYR$)>4 THEN VAL$=CR_YEAR$ ELSE VAL$=RPT_FISYR$
32010 FIND (ZZPARM,KEY=KE$+VAL$)Z9$; CC$=Z9$(21)
32011 IF LEN(RPT_PER$)>2 THEN VAL=NUM(CR_PD$) ELSE VAL=NUM(RPT_PER$)
32020 CC$=CC$(VAL*6-5,6),RPT_AGEDATE$=CC$,REFRESH_FLG=1
32030 RETURN 
38399 ! 
40000 SEL_QRY:ENTER PRIME_KEY$
40010 IF NUL(MID(PRIME_KEY$,7,6)) THEN EXIT 123
40020 EXIT 
45000 ! 
45005 REPORT_PRINT_MODE:
45010 IF RPT_SEQ$="4" THEN RPT_PGM$="FM2RIB" ELSE RPT_PGM$="FM2RIA"
45015 RPT_SPLIT$="" ! Separate File Option
45020 DIM RPT_OPTIONS$[10,20]
45030 ! Set variable names X3$,X4$,T1$,PRT$,RNG$,ZZS$ if required
45070 IF NOT(NUL(RB_PRT$)) AND RB_PRT$>"0" THEN RPT_SPLIT$="Y"
45075 GOSUB SCHD_DATE_VARS
45080 PERFORM "ZGXOPO;PROCESS_PRINT_MODE"
45090 RETURN 
45099 ! 
46000 SCHD_DATE_VARS:
46010 RPT_OPTIONS_COUNT=4 ! Maximum 9 allowed
46015 RPT_OPTIONS$[1,0]=MSG("START_O_D") ! Starting Order Date
46020 RPT_OPTIONS$[1,1]="6"
46025 RPT_OPTIONS$[1,2]="74"
46030 RPT_OPTIONS$[1,3]="ZZS"
46035 RPT_OPTIONS$[2,0]=MSG("END_O_D") ! Ending Order Date
46040 RPT_OPTIONS$[2,1]="6"
46045 RPT_OPTIONS$[2,2]="80"
46050 RPT_OPTIONS$[2,3]="ZZS"
46055 RPT_OPTIONS$[3,0]=MSG("START_D_R") ! Starting Date Requested
46060 RPT_OPTIONS$[3,1]="6"
46065 RPT_OPTIONS$[3,2]="86"
46070 RPT_OPTIONS$[3,3]="ZZS"
46075 RPT_OPTIONS$[4,0]=MSG("END_D_R") ! Ending Date Requested
46080 RPT_OPTIONS$[4,1]="6"
46085 RPT_OPTIONS$[4,2]="92"
46090 RPT_OPTIONS$[4,3]="ZZS"
46095 RETURN 
46099 ! 
56001 REM "193975-Option to print Unbilled Cost/Qty by line      
56002 ! "228228-Ability to split a report into multiple PDFs based on sales 
56004 REM "240570-GUI Printing - Support for queueing and scheduling reports. 
