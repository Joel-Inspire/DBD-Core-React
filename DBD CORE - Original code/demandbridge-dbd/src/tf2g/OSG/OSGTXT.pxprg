0010 REM Create txt file of office supply contracts OSGTXT
0020 SETESC 9300; SETERR 9000
0035 REM "5.6 - 12/11/08 - 6.536388 - lms - SSP# 217383
0037 REM "217383-Would like a list of the custom plans they've created       
0040 REM "Copyright 2008 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 PROCESS "OSGTXT","../OSG/OS.EN"
0060 EXIT 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0091 REM CLEAR 
0095 INIT:
0100 REM SETERR 9000; SETESC 9300
0110 X0$="OSGTXT",X1$="Create Text file of Office Supply Contract Prices",K9$="",K9=0
0120 DIM Z0$(80,"-")
0121 DIM B$(100)
0130 K0=20,K1=1
0135 C9=-1
0140 Z1$="#######.00-"
0150 DIM COMPARE$(6," "),PLAN$(6," "),B$(57," ")
0170 IF POS("MS"=UCS(SYS)) THEN ISWIN=1 ELSE ISWIN=0
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O OS1...  02O OS0...  07O OS7...  08OSOS1...  09O OS5...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0521 SEQ=1
0522 SEQ$=STR(SEQ:"000")
0590 REM FILENAME$="OS1SCAN_"+SEQ$
0595 REM GOSUB 7700
0600 REM "
0610 RETURN 
0611 DIM PLAN$(6); CALL "ZZENTR","SU",A{ALL},PLAN$,X4$,X3$,35,10,1,6,C0,"","{3"+X$,"","OS2MAA04","","",""
0612 IF NUL(PLAN$) THEN PLAN$="ALL"
0615 DIM COMPARE$(6); CALL "ZZENTR","SU",A{ALL},COMPARE$,X4$,X3$,35,14,1,6,C0,"","{3"+X$,"","OS2MAA04","","",""
0640 CALL "ZZPROM",".Y",X3$,Z,"PROCEED?","","",0; ON Z GOTO 0641,9900
1000 REM 
1001 PROCESS:
1002 GOSUB 8100; GOSUB 8150
1003 IF PLAN$="" THEN PLAN$="ALL "
1004 IF PLAN$="ALL " THEN FILENAME$="OS1SCAN_" ELSE FILENAME$="OS1SCAN_"+STP(PLAN$,3)+"_"
1005 SEQ$="001"
1006 GOSUB 7700
1010 K$=KEY(Z[1],END=9900); READ (Z[1])
1012 C=C+1; IF MOD(C,T1)=0 THEN GOSUB 8150
1015 IF MID(PLAN$,1,3)<>"ALL" AND PLAN$(1,6)<>K$(11,6) THEN GOTO 1010; REM ONLY PRINT ONE PLAN
1020 READ RECORD (Z[1],KEY=K$)A$
1021 READ RECORD (Z[9],KEY=A$(11,6),DOM=*NEXT)OS5_1$
1022 IF DOWN$="Y" THEN GOTO 1026; REM include all plans, including those that were downloaded
1023 IF OS5_1$(1,6)<>A$(11,6) THEN GOTO 1025; REM if no plan exists, can't figure out what kind
1025 IF MID(OS5_1$,39,1)<>DIM(1) THEN GOTO 1010
1026 GOSUB 1200
1030 CALL "ZZDISP","DX",A$(25,6),"   ",X3$,X$,"",0,0,X4$; START_DT$=X$
1032 CALL "ZZDISP","DX",A$(31,6),"   ",X3$,X$,"",0,0,X4$; END_DTE$=X$
1040 READ (Z[2],KEY=A$(1,10),DOM=1010)OS0$
1041 DIM B$(57," ")
1042 READ (Z[8],KEY=A$(1,10)+COMPARE$,DOM=*NEXT)B$
1043 IF ITEM_DOWN$="Y" THEN ITEM_DESC$=OS0$(37,40) ELSE ITEM_DESC$=DIM(40," ")
1050 PRINT (32502)A$(11,6),",",OS0$(548,20),",",A$(1,10),",",START_DT$,",",END_DTE$,",",STR(NUM(A$(47,10)):Z1$),",",STR(COST:Z1$),",",STR(NUM(B$(47,10)):Z1$),",",STP(ITEM_DESC$,3,",")
1054 COUNT=COUNT+1
1055 REM C=C+1; IF MOD(C,T1)=0 THEN GOSUB 8150
1056 IF COUNT=65000 THEN CLOSE (32502); GOSUB 2000
1100 GOTO 1010
1200 REM GET LOWEST COST
1201 COST=0,COST$=""
1202 B$(16,6)=X3$(21,6)
1225 READ (Z[7],KEY=A$(1,10),DOM=*NEXT)
1230 OS7_KEY$=KEY(Z[7],END=1299)
1232 IF OS7_KEY$(1,10)<>A$(1,10) THEN GOTO 1299
1233 READ (Z[7],KEY=OS7_KEY$)OS7$
1234 IF B$(16,6)<OS7$(77,6) OR B$(16,6)>OS7$(83,6) THEN GOTO 1230
1242 COST$=OS7$(27,10)
1250 IF COST=0 THEN COST=NUM(COST$)
1255 IF NUM(COST$)<COST THEN COST=NUM(COST$)
1264 GOTO 1230
1299 RETURN 
2000 REM OPEN SECOND FILE
2001 SEQ=SEQ+1
2002 GOSUB 7700
2003 COUNT=0
2099 RETURN 
5000 REM "EOJ
5020 MSGBOX "Process complete!!","F.Y.I","!"
5040 GOTO 9900
6000 REM "BACKGROUND
6001 REM IF %GUI<>0 THEN GOTO 6190
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This utility program will create text files containing all Office Supply prices that were downloaded in a file called OS1SCAN +001 ,etc. There will be a maximum of 65000 records in each file"
6040 PRINT @(10,10),"Enter Price Plan or ALL:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
7700 REM "Create file, open it, lock it, write record, close file                                                                              
7701 CLOSE (32502,ERR=*NEXT)
7702 ERASE FILENAME$+SEQ$,ERR=*NEXT
7710 SERIAL FILENAME$+SEQ$,7,0,ERR=*NEXT
7715 OPEN LOCK (32502,OPT="TEXT")FILENAME$+SEQ$; LOCK (32502)
7740 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and b efore the Proceed? question. Set T to total # to do (by calling ZZINFO) an d T0 to the reporting interval (report every T0 number of records) this is  typically 2% of total; This also prints a message (@8115),so adjust accordingly 
8110 CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F$
8129 REM "Set T0, we make sure T0 is > 1, because later on we MOD and look for  avalue of 1. IF T0 is 1, then nothing would get reported. We look for a re sult of 1 because this causes the first record to automatically start the  reporting instead of waiting until the T0'th record to get the first report
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph  given total #, T and current #, C, and time counter T1 (T1 is init'ed her e and used here, just don't use it somewhere else)
8155 IF %GUI THEN CALL "ZZBARG",X3$,"HG",17.7,7,35,T1,T,C ELSE CALL "ZZBARG",X3$,"H",19,10,50,T1,T,C
8195 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 C=T; GOSUB 8150
9901 END_PRG:
9902 MSGBOX "Process complete",MSG("CONFIRMING"),"!,TIM=5"
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9911 CLOSE (32502,ERR=*NEXT)
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "217383-Would like a list of the custom plans they've created     
