0010 REM "ECGSTS - Gui EC Monitor
0035 REM "5.7 - 07/10/18 - 15.618333 - jvv - SSP# 297583
0037 REM "297583-Graphical version of EC Monitor needs improvements.         
0040 REM "Copyright 2018 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! 
0070 IF TCB(88)=0 THEN MSGBOX "You must be using Windx to access this program"; GOTO EXIT_PGM
0075 PROCESS "ECGSTS","../ECG/EC.EN"
0082 EXIT_PGM:
0085 CLOSE (_MEM,ERR=*NEXT)
0088 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0090 ! 
0100 INIT:
0105 X0=-1
0106 TM=5
0108 %NOMAD_TIMEOUT=0
0110 X0$="ECGSTS",X1$="eCommerce Server Monitor" ! ssp#242161
0120 DIM Z0$(80,"-"),CC[4],LL[4],WW[4],HH[4]
0125 NR_SEL=0
0130 K0=20,K1=1,M0$="#,###,##0",FIRST_LINE=5,SPACING=5,MAX_SERVERS=4
0135 C9=-1,CC(1)=4.7,LL(1)=2.6,CC(2)=46.7,LL(2)=1.6,CC(3)=1.7,LL(3)=15,CC(4)=46.7,LL(4)=15
0200 ! "
0240 CALL "ZZCOMP","","",X2$,X3$,X4$,M9$,X0,X1,0; IF X1>0 THEN GOTO 9920
0242 IF X3$="" THEN CLOSE (14); OPEN (14)"ZZPARM"; READ (14,KEY="TFEEC3STS  ",DOM=0243)X3$,*,X4$ ! "If X3$ not set then read env
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 SET_PARAM 'LU' ! We do so much with text files, don't lock them
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0298 DIM SERVER$[100],LAST_TIME[100,1]
0300 ! "IOLISTS
0310 IOLIST ECY$
0320 IOLIST ECZ$,ECZ[0],ECZ[1],ECZ[2],ECZ[3],ECZ[4],ECZ[5],ECZ[6],ECZ[7],ECZ[8],ECZ[9],ECZ[10],ECZ[11],ECZ[12],ECZ[13],ECZ[14],ECZ[15],ECZ[16],ECZ[17],ECZ[18]
0500 ! "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O ECY...  02O ECZ...  13O ZZPARM  14OSZZPARM "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 CLOSE (_MEM,ERR=*NEXT)
0540 _MEM=HFN; OPEN (_MEM)"*memory*"
0550 CLOSE (120); OPEN (120,ERR=0551)"EC3STS.LOG"; GOTO 0555
0551 IF ERR<>12 THEN OPEN (120,OPT="TEXT")"EC3STS.LOG"; GOTO 0555 ELSE SERIAL "EC3STS.LOG",0,0; GOTO 0550
0555 MESS$="STARTED"; GOSUB 7600
0560 ! "See if we are the on to monitor
0564 MON_KEY$="EC3STSMN"
0565 FIND (Z[14],KEY=MON_KEY$,DOM=0566,ERR=0568)MONITOR$; ML_MONITOR$="I am the Monitor",REFRESH_FLG=1; GOTO 0570 ! ssp#242161
0566 MONITOR=1,MONITOR$=MON_KEY$+FID(0); WRITE (Z[14],KEY=MON_KEY$)MONITOR$; GOTO 0570 ! "If record not found, set it up and we know we are the monitor
0568 ! "if here, err occurred, probably err 0, so we won't be the monitor
0569 DIM MONITOR$(10); MONITOR=0; REFRESH_FLG=1,ML_MONITOR$="I am NOT the Monitor"; GOTO 0575
0570 ! "If here, we can be the monitor
0572 MONITOR=1; EXTRACT (Z[14],KEY=MON_KEY$,ERR=0568)MONITOR$ ! "If we get error here, then someone got the record or we had readprm on
0575 ! "Done with setting monitor stuff here, so display
0576 IF MONITOR THEN XX1$="Monitor ON" ELSE XX1$="Monitor OFF"
0578 ML_MONITOR$=XX1$,REFRESH_FLG=1
0579 %NOMAD_TIMEOUT=TM
0600 RETURN 
0810 MAIN_POST_DISPLAY:
0870 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0970 RETURN 
1800 ! 1800 - Clear record
1810 CLEAR_REC:
1820 DB_FFP$="",FAX_NUM_TYPE$="",EML_STAT$="",FILTER_CODE$="",FILTER_DESC$="",EML_STAT$=""
1830 GRID LOAD EC_GRID.CTL,0,0,""; FILT_CNT=0
1840 DROP_BOX WRITE DB_FFP.CTL," "
1845 DROP_BOX WRITE EML_STAT.CTL," "
1850 REFRESH_FLG=1
1860 RETURN 
1899 ! 
1900 SET_HDG:
1910 EC_GRID.CTL'COLUMN=-1,EC_GRID.CTL'COLUMNWIDTH=.1,EC_GRID.CTL'ROW=-1,EC_GRID.CTL'COLUMN=0,EC_GRID.CTL'FONT$="MS Sans Serif,-9,B"
1920 RETURN 
1999 ! 
2000 LOAD_GRID:
2002 TIME_MASK$="00:00:00"; IF MID(%X3$,56,3)="DMY" THEN DATE_FORMAT$="%Dz-%Mz-%Y" ELSE DATE_FORMAT$="%Mz-%Dz-%Y"
2005 GRID LOAD EC_GRID.CTL,0,0,""; FILT_CNT=0
2007 IF FCHAN THEN CLOSE (FCHAN); FCHAN=0
2008 GOSUB UPDATE_STATUS
2010 READ (Z[1],KEY="",DOM=*NEXT)
2011 ! 
2012 NXT_READ:
2020 DIM ECY$(512); READ (Z[1],END=END_LOAD_GRID)ECY$
2025 DIM ECZ$(128),ECZ(18); READ (Z[2],KEY=ECY$(1,15),DOM=*NEXT)IOL=0320; GOTO 2040
2030 ECZ$(16,10)="STAT N/A"; PRECISION 14; ECZ[0]=FN%GET_DATETIME("",0); PRECISION 2; REM "If not on file, then create default info
2050 PSUID$="   "; IF ECZ[2]<>0 THEN PSUID$=FNTCB89$
2100 LOAD_GRID_LINE:
2135 GRID LOCK EC_GRID.CTL,0,0; IF FILT_CNT THEN GRID UNLOCK EC_GRID.CTL,1,0
2150 IF NUM(ECY$(376,5))=0 THEN TOUT$="None" ELSE TOUT$=ECY$(376,5)
2199 ! 
2250 GRID LOAD EC_GRID.CTL,1,0,SEP+ECY$(1,15)+SEP+ECY$(16,40)+SEP+ECZ$(16,10)+SEP+FN%PRINT_DATETIME$(ECZ[0],"%Mz/%Dz/%Yz %Hz:%mz:%sz")+SEP+TOUT$+SEP+MSG("VIEW")+SEP+FN%PRINT_DATETIME$(ECZ[1],"%Mz/%Dz/%Yz  %Hz:%mz:%sz")+SEP+STR(ECZ[2])+SEP+PSUID$+SEP+STR(ECZ[8])+SEP+STR(ECZ(6))+SEP+STR(ECZ(7))+SEP+STR(ECZ(10))+SEP+STR(ECZ(4):"#,###,##0")+SEP+STR(ECZ(5):"#,###,##0")+SEP+STR(ECZ(3):"#,###,##0")+SEP+STR(ECZ(9):"#,###,##0")+SEP; ++FILT_CNT
2260 EC_GRID.CTL'ROW=FILT_CNT,EC_GRID.CTL'COLUMN=7,EC_GRID.CTL'ROWHEIGHT=1.8,EC_GRID.CTL'FONT$="MS Sans Serif,-9,B",EC_GRID.CTL'ALIGN$="C",EC_GRID.CTL'BACKCOLOUR$="Light Gray",EC_GRID.CTL'TEXTCOLOUR$="Black" ! Set BUTTON    column                         
2290 GOTO NXT_READ
2299 ! 
2300 END_LOAD_GRID:
2310 IF NOT(FILT_CNT) THEN MSGBOX ("No records to Process"); GOSUB CLEAR_REC; GRID LOAD EC_GRID.CTL,1,0,SEP; GOTO 2328
2320 GRID LOCK EC_GRID.CTL,0,0; IF FILT_CNT THEN GRID UNLOCK EC_GRID.CTL,1,0
2325 GOSUB CHECK_MONITORED_SERVERS
2328 DISP_TIME$=DTE(0:"%Y-%Mz-%Dz %Hz:%mz:%sz")
2330 REFRESH_FLG=1
2350 RETURN 
2599 ! 
3500 DISP_TIME:
3510 REPEAT 
3538 DISP_TIME$=DTE(0:"%Y-%Mz-%Dz %Hz:%mz:%sz")
3540 REFRESH_FLG=1
3550 UNTIL (DONE)
3590 RETURN 
3599 ! 
4000 CHECK_MONITORED_SERVERS:! REM "Check monitored servers to see if they are running
4001 %NOMAD_TIMEOUT=0
4002 IF MONITOR THEN GOTO *NEXT ELSE GOTO END_MONITOR; REM "If set not to monitor, then don't 
4010 FOR RINDEX=1 TO S_TOTAL
4015 IF NUM(SERVER$[RINDEX](376,5),ERR=4080)=0 THEN GOTO 4080; REM "If 0 then don't monitor
4020 DIM ECZ$(128),ECZ[18]; FIND (Z[2],KEY=SERVER$[RINDEX](1,15),DOM=4050)IOL=0320; REM "Find stats, if no record then assume restart needed
4025 IF ECZ$(16,10)="ENDED     " THEN GOTO 4050
4030 IF LAST_TIME[RINDEX,0]=0 THEN PRECISION 14; LAST_TIME[RINDEX,0]=ECZ[0],LAST_TIME[RINDEX,1]=FN%GET_DATETIME("",0); PRECISION 2; GOTO 4035; REM "If not set yet, set & continue
4031 REM "get diff w last time and compare to # secs allowed
4032 PRECISION 14; IF LAST_TIME[RINDEX,0]<>ECZ[0] THEN LAST_TIME[RINDEX,0]=ECZ[0],LAST_TIME[RINDEX,1]=FN%GET_DATETIME("",0); PRECISION 2; GOTO 4035
4033 PRECISION 14; SECONDS=(FN%GET_DATETIME("",0)-LAST_TIME[RINDEX,1])*86400; PRECISION 2
4034 IF SECONDS>NUM(SERVER$[RINDEX](376,5)) THEN GOTO 4050 ELSE PRECISION 14; LAST_TIME[RINDEX]=ECZ[0]; PRECISION 2
4035 REM "Pickup processing here
4045 GOTO 4080
4050 REM "Restart process
4052 ECY$=SERVER$[RINDEX],S$=SERVER$[RINDEX](1,15); REM "As if we read the data
4053 GOSUB 7650
4055 FIND (Z[2],KEY=SERVER$[RINDEX](1,15),DOM=4061)IOL=0320; REM "IF no status record then skip stop attempt, we don't have the info
4060 AUTOKILL=1; GOSUB STOP_SERVER; AUTOKILL=0; REM "kill server for sure
4062 REM "Remove last file processed if it exists it may have been stuck on it
4064 ERASE STP(SERVER$[RINDEX](56,60),3," ")+DLM+STP(ECZ$(26,20),1),ERR=4065; GOTO 4064
4068 REMOVE (Z[2],KEY=SERVER$[RINDEX](1,15),DOM=4069); REM "Remove status record we don't need it any more
4069 REM "Now attempt to start the file
4070 ! PRINT @(0,22),'CL',@(20,22),"Attempting restart of: ",SERVER$[RINDEX](1,15),
4075 AUTOSTART=1; GOSUB RUN_SERVER; AUTOSTART=0
4080 NEXT RINDEX
4089 ! 
4090 END_MONITOR:
4092 %NOMAD_TIMEOUT=TM
4095 RETURN 
4099 ! 
4300 SET_READONLY:! SSP#253613   
4310 SET_PARAM 'XI'
4320 SET_READONLY_END:RETURN 
4330 CLEAR_READONLY:! SSP#253613             
4340 SET_PARAM -'XI'
4350 CLEAR_READONLY_END:RETURN 
4359 ! 
5000 REM "Event handler - OnSelect
5005 SELECT_FILTER:
5008 ! %NOMAD_TIMEOUT=0
5010 READ DATA FROM EC_GRID.CTL'ROWDATA$ TO IOL=EC_GRID.CTL'LOADIOLIST$
5015 IF NOT(FN%ISOPEN(FCHAN)) THEN FCHAN=HFN; OPEN (FCHAN)"*memory*"
5018 C_KQ$=PAD(C_SERVER$,15)
5020 IF C_SELECT$="1" THEN {
5030 WRITE (FCHAN,KEY=C_KQ$,ERR=*NEXT)C_KQ$
5040  } ELSE {
5050 REMOVE (FCHAN,KEY=C_KQ$,ERR=*NEXT)
5060  }
5070 NR_SEL=NUM(FIN(FCHAN,"NUMREC"))
5080 IF NR_SEL>0 THEN %NOMAD_TIMEOUT=0 ELSE %NOMAD_TIMEOUT=TM
5095 RETURN 
5099 ! 
6000 SERVER_OPTIONS:
6010 IF FCHAN=0 OR NR_SEL=0 THEN GOTO NO_SERVER_SELECTED
6050 READ (FCHAN,KEY="",ERR=*NEXT)
6060 NEXT_FSEL:
6070 FKY$=KEY(FCHAN,END=SERVER_OPTION_EXIT); READ (FCHAN,KEY=FKY$)*
6080 S$=FKY$
6090 IF ID$="BT_STOP_SERVER" THEN GOSUB STOP_SERVER
6100 IF ID$="BT_RUN_SERVER" THEN GOSUB RUN_SERVER
6190 GOTO NEXT_FSEL
6192 GOTO SERVER_OPTION_EXIT
6199 ! 
6200 NO_SERVER_SELECTED:
6210 SP$="Please select a server to proceed"; MSGBOX SP$,"!Message"
6600 SERVER_OPTION_EXIT:
6610 %NOMAD_TIMEOUT=TM
6620 NR_SEL=0
6630 GOSUB LOAD_GRID
6690 RETURN 
6699 ! 
7500 ! "Run COMMAND$ and capture output in TF_OUTPUT$
7515 %ML_CMD.CTL=ML_CMD.CTL; CALL "ZZ2CMD",X3$,X4$,COMMAND$,ARG$,"NR",RETURN_CODE,RETURN$
7545 RETURN 
7549 ! 
7550 ! "Setup Args to run command 1=server name, 2=environment code, 3=Process id, 4=End file name, 5=restartfile name, 6=email message, 7=email subject, 8=email address, 9=arguments
7555 ARG$=""
7560 ARG$=ARG$+STP(S$,3)+SEP ! "{SERVER}
7562 IF ECY$(433,8)=DIM(8) THEN ENV$="EC3SRV" ELSE ENV$=ECY$(433,8) END_IF ; ARG$=ARG$+ENV$+SEP ! "{ENV}
7566 ARG$=ARG$+STR(ECZ[2])+SEP ! "{PID}
7568 ARG$=ARG$+STP(ECY$(56,60),2)+DLM+"END."+STP(S$,3)+SEP; REM {ENDFILE}
7570 ARG$=ARG$+STP(ECY$(56,60),2)+DLM+"RESTART."+STP(S$,3)+SEP; REM {RESTARTFILE}
7572 ARG$=ARG$+EMESS$+SEP; EMESS$="" ! Set & then clear email message
7574 ARG$=ARG$+SUBJECT$+SEP; SUBJECT$="" ! Set & then clear subject
7576 ARG$=ARG$+STP(ECY$(442,40),1)+SEP ! Email address
7578 ARG$=ARG$+STP(ECY$(116,60),1)+SEP ! Arguments
7590 ! "Now execute COMMAND
7593 GOSUB 7500
7595 RETURN 
7599 ! 
7600 ! "Write message to LOG
7610 OUT$=DTE(0:"%Y-%Mz-%Dz|%Hz:%mz:%sz")+"|"+MESS$
7620 ! "Position to current end of file incase multiple EC3STS's running
7622 TMP$=FN%XFD$(120,0); IF DEC(TMP$(38,4))>0 THEN READ (120,IND=DEC(TMP$(38,4))-1,ERR=7623)
7625 WRITE (120)OUT$
7645 RETURN 
7650 REM "Notify of failure
7654 REM "Log failure
7655 MESS$=SERVER$[RINDEX](1,15)+"|INACTIVE"; GOSUB 7600
7660 REM "Email notify
7662 IF STP(SERVER$[RINDEX](442,40),1)="" OR X3$(77,1)<>"U" THEN GOTO 7670; REM "If no email address or not a unix system then skip it
7664 EMESS$="WebEC Server "+STP(SERVER$[RINDEX](1,15),1)+" appears inactive. A restart is being attempted."; PRECISION 14; EMESS$=EMESS$+" ["+DTE(0:"%Y-%Mz-%Dz|%Hz:%mz:%sz")+"]"; PRECISION 2; SUBJECT$="WebEC Server "+STP(SERVER$[RINDEX](1,15),1)+" appears inactive"
7665 COMMAND$="{WEBEC_FAIL_NOTIFY}"; GOSUB 7550
7670 REM "After email notify
7695 RETURN 
7699 ! 
8000 UPDATE_STATUS:
8010 CALL "UPDSV0;UPDATE_STATUS",ERR=*NEXT,"ECMONITOR","EC MONITOR","","EC Server Status Monitor","ECGSTS",STR(TCB(89))
8015 S_INDEX=0; READ (Z[1],KEY="",DOM=*NEXT)
8020 DIM ECY$(976); READ (Z[1],END=8040)IOL=0310
8025 S_INDEX=S_INDEX+1; SERVER$[S_INDEX]=ECY$
8030 GOTO 8020
8040 S_TOTAL=S_INDEX
8045 RETURN 
8049 ! 
9999 END 
10099 ! 
18299 ! 
18300 END_PROG:
18310 CALL "ZZFLES",X3$,Y1$,Y0$,"END",ZM{ALL},0,0
18315 CLOSE (_EFIL_NO)
18320 EXIT 
18999 ! 
20100 VIEW_PARAMS:
20102 %NOMAD_TIMEOUT=0
20105 SET_FOCUS READ CURR_FOCUS; IF CURR_FOCUS<>EC_GRID.CTL THEN RETURN 
20120 EC_GRID.CTL'ROW=EC_GRID.CTL'CURRENTROW,EC_GRID.CTL'COLUMN=2,%SERVER_ID$=PAD(EC_GRID.CTL'VALUE$,15)
20140 SWITCH EC_GRID.CTL'CURRENTCOLUMN
20150 CASE 7 ! hit BUTTON column
20155 CALL "EC3MSA",X3$,X4$,"",""
20160 BREAK
20165 ! CASE 7 ! Log View
20170 ! PROCESS "ECGSTS.3","../ECG/EC.EN",%SERVER_ID$
20180 ! BREAK
20185 END SWITCH 
20190 VIEW_PARAMS_END: EC_GRID.CTL'CURRENTCOLUMN=1,NEXT_ID=BUTTON_3.CTL
20192 %NOMAD_TIMEOUT=TM
20195 RETURN 
20199 ! 
21000 READ_SERVER_ID:
21001 ML_LOG$=""; LIST_BOX LOAD ML_LOG.CTL,""
21002 ECY=HFN; OPEN (ECY)"ECY"+%C$
21010 DIM ECY$(512); READ (ECY,KEY=PAD(ML_SERVER_ID$,15," "),DOM=21055)IOL=0310; GOTO 21050
21020 MSGBOX "Server ID: "+ML_SERVER_ID$+" Not On File","!Error","!"
21030 NEXT_ID=ML_SERVER_ID.CTL; GOTO 21100
21050 ML_SERVER_DESC$=ECY$(16,40),ML_SERVER_DESC.CTL'VALUE$=ML_SERVER_DESC$,REFRESH_FLG=1
21055 XXX=HFN; OPEN (XXX)FID(0); XXX$=FIN(XXX); CLOSE (XXX); XXX=DEC(XXX$(10,1))
21060 CLOSE (ECY); RUN "ECGSTL"
21100 CLOSE (ECY)
21110 RETURN 
21119 ! 
22000 DEF FNTCB89$
22010 LOCAL TCB_89,OSCHN,PSLINE$
22020 TCB_89=ECZ[2]; IF TCB_89=0 THEN GOTO DONE_TCB89
22025 PROC_ID$=STR(TCB_89)
22030 FIND (_MEM,KEY=PROC_ID$,DOM=*NEXT)TCB_89,PSLINE$; GOTO DONE_TCB89
22040 PSLINE$=""
22100 IF POS("LINUX"=UCS(SYS))=0 THEN GOTO DONE_TCB89
22110 OPEN (HFN,ERR=DONE_TCB89)"<ps -o user= -p "+STR(TCB_89); OSCHN=LFO
22120 READ RECORD (OSCHN,END=*NEXT)PSLINE$
22130 CLOSE (OSCHN)
22150 WRITE (_MEM,KEY=PROC_ID$)TCB_89,PSLINE$
22200 DONE_TCB89:
22210 RETURN PSLINE$
22220 END DEF
22299 ! 
30000 STOP_SERVER:! "Stop Server logic
30004 IF AUTOKILL THEN MESS$=S$+"|AUTOKILL"; GOSUB 7600; GOTO 30020
30011 X$=""
30015 FIND (Z[1],KEY=S$,DOM=30005)IOL=0310
30020 FIND (Z[2],KEY=S$,DOM=30021)IOL=0320; GOTO 30030
30021 REM "Server status record not found
30022 IF AUTOKILL THEN GOTO 30090 ELSE SP$="Server "+STP(S$,2)+" status record not found, cannot stop this server!"; MSGBOX SP$,"!Error","!"
30025 IF ECZ$(16,10)<>"ENDED     " THEN GOTO 30030 ELSE IF AUTOKILL THEN GOTO 30026 ELSE SP$="Server status record indicates server stopped. Try KILL command?"; MSGBOX SP$,"!Error","YesNo",SEL$; IF SEL$="NO" THEN GOTO 30090
30026 COMMAND$="{WEBEC_KILL_SERVER}"; GOSUB 7550; REM "Kill Command
30027 REMOVE (Z[2],KEY=S$,DOM=30028)
30028 COMMAND$="{WEBEC_CLEAR_STOP}"; GOSUB 7550; REM "remove end.servid file
30029 GOTO 30090
30030 REM "If here, stop server using END.server name  file
30034 IF AUTOKILL THEN GOTO 30035
30035 COMMAND$="{WEBEC_STOP_SERVER}"; GOSUB 7550; REM "End command
30045 WAIT 2; REM "Give the server a chance to stop
30050 ECZ$(16,10)="ENDED"; FIND (Z[2],KEY=S$,DOM=*NEXT)IOL=0320
30055 IF ECZ$(16,10)="ENDED     " THEN GOTO 30060; REM "Server stopped ok
30056 REM "Server didn't stop yet, wait a few seconds before panic
30057 WAIT 3; FIND (Z[2],KEY=S$,DOM=*NEXT)IOL=0320
30058 IF ECZ$(16,10)="ENDED     " THEN GOTO 30060 ELSE IF AUTOKILL THEN GOTO 30026 ELSE SP$="Could not stop "+STP(S$,2)+", try KILL command?"; MSGBOX SP$,"!Error","YesNo",SEL$; IF SEL$="NO" THEN GOTO 30090 ELSE GOTO 30026
30060 COMMAND$="{WEBEC_CLEAR_STOP}"; GOSUB 7550; REM "Clear End file
30061 IF AUTOKILL THEN GOTO 30062 ELSE SP$="Server "+STP(S$,2)+" stopped successfully."; MSGBOX SP$,MSG("CONFIRMING"),"!,TIM=4"
30093 IF AUTOKILL THEN GOTO *NEXT
30095 RETURN 
30903 IF AUTOKILL THEN GOTO *NEXT ! ELSE DISPLAY_ONLY=1; GOTO 1100
30909 ! 
31000 RUN_SERVER:! REM "Run a server
31004 IF AUTOSTART THEN MESS$=S$+"|AUTOSTART"; GOSUB 7600; GOTO 31017
31011 X$=""
31015 FIND (Z[1],KEY=S$,DOM=31005)IOL=0310
31017 COMMAND$="{WEBEC_CLEAR_STOP}"; GOSUB 7550; REM "Make sure END file is gone
31020 FIND (Z[2],KEY=S$,DOM=31025)IOL=0320
31021 REM "Server already running
31022 IF ECZ$(16,10)="ENDED     " THEN GOTO 31025
31023 IF AUTOSTART THEN GOTO 31024 ELSE SP$="Server status indicates that "+STP(S$,2)+" is running. Try emergency stop?"; MSGBOX SP$,"!Error","YesNo",SEL$; IF SEL$="NO" THEN GOTO 31090
31024 COMMAND$="{WEBEC_KILL_SERVER}"; GOSUB 7550; REM "Kill command
31025 REM "Start server
31026 IF AUTOSTART THEN GOTO 31027
31030 COMMAND$="{WEBEC_RUN_SERVER}"; GOSUB 7550; REM "Run Command
31040 WAIT 2; REM "Give the server a chance to get started
31045 FIND (Z[2],KEY=S$,DOM=31046)IOL=0320; GOTO 31050
31046 WAIT 3; REM "wait a little more before panicing
31047 FIND (Z[2],KEY=S$,DOM=*NEXT)IOL=0320; GOTO 31050
31048 IF AUTOSTART THEN MESS$=S$+"|FAILED"; GOSUB 7600; GOTO 31095 ELSE SP$="Looks like server didn't start!"; MSGBOX SP$,MSG("WARNING"),"!,TIM=8"
31049 GOTO 31090
31050 REM "looks like it started
31051 IF AUTOSTART THEN MESS$=S$+"|STARTED"; GOSUB 7600; GOTO 31095
31055 ! SUB_CALL=1; GOSUB 1100
31060 SP$="Server "+STP(S$,2)+" started."; MSGBOX SP$,"!Message"
31090 IF AUTOSTART THEN GOTO 31091
31095 RETURN 
31699 ! 
56001 ! 
56002 REM "229312-Datareplication enhancement for selective replication of    
56003 REM "232963-Ability to not show progress, slows processing remotely
56004 REM "239169-Use of two progress bars not working properly
56005 REM "253613-Data Rep error                                              - ERROR 0 2096
56006 REM "277590-Issues with Data Replication Filters.                       
56007 REM "298391-Ability to send E/F emails of Invoices after SJ Update      
56008 REM "297583-Graphical version of EC Monitor needs improvements.         
