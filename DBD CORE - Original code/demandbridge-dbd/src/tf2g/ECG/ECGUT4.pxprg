0010 ! ECGUT4 - Regenerate active web catalogs by buyer
0100 ! ^100 - Call/Enter String
0110 ENTER BUYER$,CATEGORIES$,OPTIONS$
0120 ! buyer$ = the buyer id to process (blank if caglog passed in)
0130 ! categories$ = returned string of categories
0140 ! catalog$ = passed in in place of buyer
0150 ! options$(1,1) = N do not display complete message after eoj
0300 ! 300,5 - Iolists & templates
0305 ! 
0500 ! 500,5 - Open files
0505 ECA=HFN; OPEN (ECA,IOL=*)"ECA"+%C$
0510 ECG=HFN; OPEN (ECG,IOL=*)"ECG"+%C$
0515 ECH=HFN; OPEN (ECH,IOL=*)"ECH"+%C$
0520 FM4=HFN; OPEN (FM4)"FM4"+%C$
0525 ZZPARM=HFN; OPEN (ZZPARM)"ZZPARM"
0530 ! 
0600 ! ^100,5 - Read the file
0605 CATEGORIES$="",OPTIONS$=PAD(OPTIONS$,10)
0610 READ (ECA,KEY=BUYER$,DOM=9700)
0615 IF STP(EC_CATALOG_ID$,2)="" THEN GOTO 9700
0620 READ (ECH,KEY=EC_CATALOG_ID$,DOM=*NEXT)
0625 BUYER_SCODES$=STP(SECURE_ITEMS$,2)
0630 ! 
1000 ! ^1000,5 - Main processing
1005 SELECT * FROM ECH BEGIN EC_CATALOG_ID$ END EC_CATALOG_ID$+$FF$
1010 IF SHOW_ACTIVE_ITEMS$<>"Y" THEN GOTO 1100 ! Not filtering only items used at location (Fm4)
1015 IF CATELOG_CATEGORY$=CURR_CATEGORY$ THEN GOTO 1300 ! Item already found in category - skip remaining items
1020 FIND (FM4,KEY=ITEM_CUST_CODE$+ITEM_ITEM_CODE$+BUYER_LOC_CODE$,DOM=*NEXT); CURR_CATEGORY$=CATALOG_CATEGORY$; GOTO 1200 ! add category to the list
1025 GOTO 1300
1030 ! 
1100 ! ^100,5 - Check categories
1105 IF CATALOG_CATEGORY$=CURR_CATEGORY$ THEN GOTO 1300
1110 CURR_CATEGORY$=CATALOG_CATEGORY$
1115 READ (ECG,KEY=CURR_CATEGORY$,DOM=1300)
1120 CAT_SCODES$=STP(SECURE_CODES$,2)
1125 IF CAT_SCODES$="" AND BUYER_SCODES$="" THEN GOTO 1200
1130 FOR X=0 TO LEN(CAT_SCODES$)/2-1
1135 CODE$=MID(CAT_SCODES$,X*2+1,2)
1140 IF POS(CODE$=BUYER_SCODES$,2)<>0 THEN EXITTO 1200
1145 NEXT X
1150 GOTO 1300
1155 ! 
1200 ! ^100,5 - add category to the list
1205 CATEGORIES$+=CATALOG_CATEGORY$
1300 ! ^100,5 - Next record
1305 NEXT RECORD 
1310 ! 
2000 ! ^1000,5 - End of job
2005 IF LEN(CATEGORIES$)>500 THEN CATEGORIES$=CATEGORIES$(1,500) ! set max number of categories to 50
2010 IF OPTIONS$(1,1)<>"N" THEN {
2015 MSGBOX "Catalog "+EC_CATALOG_ID$+" has been published",MSG("FYI"),""
2020  }
2025 ! 
9700 WRAPUP:! 9700,5 - Close files and clean up
9705 CLOSE (ECA)
9710 CLOSE (ECG)
9715 CLOSE (ECH)
9720 CLOSE (FM4)
9725 CLOSE (ZZPARM)
9998 ! 9998,1 - End
9999 END 
