0010 ! ECGCAA - Catalog Assignment by Code
0035 REM "5.5 - 08/31/07 - 9.496666 - jdf - SSP# 211380
0037 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 PROCESS "ECGCAA","../ECG/EC.EN"
0060 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 ! 
0130 X0$="EC3CAA",X1$="Catalog Code Assignment"
0140 ! 
0150 X0=-1,X2=-1
0160 CALL "ZZCOMP",X0$,X11$,X2$,X3$,X4$,X5$,X0,X1,X2; IF X1>0 THEN CMD_STR$="END"; EXIT 
0170 ! 
0190 DIM Z[NUM(X3$(60,3))],EDG[12]
0200 ! Files
0210 Z$="01O EDG...  02O EDF...  06O ZZPARM  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
0300 ! IOList Section
0310 IOLIST EDG$
0320 IOLIST EDA$
0400 ! System Parameters
0410 GOSUB GET_SYS_PARAM
0430 CALL "*wingrp;disable",FIELDS.GRP$; ENABLE_FLG=0
0440 ! 
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0790 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0970 RETURN 
1000 ! READ RECORD
1010 FIND_REC:
1011 IF CODE_TYPE$="D" AND ID$="C_CODE" THEN B_CODE$=C_CODE$,FLAG=1; GOSUB SEL_EDF; C_CODE$=B_CODE$,B_CODE$="",%CATEGORY_DESC$=BUYER_DESC$,BUYER_DESC$=""
1013 IF CODE_TYPE$<>"B" THEN CODE$=PAD(C_CODE$,13) ELSE CODE$=PAD(B_CODE$,13)
1015 IF NUL(CODE_TYPE$) OR NUL(CODE$) THEN RETURN 
1020 KEY_1$=CODE_TYPE$+CODE$
1025 IF CODE_TYPE$="D" THEN CALL "ZZWLKU;PARSE_CUST",CUSTOMER$,C_D$,C_C$; CUSTOMER$=C_D$+C_C$,KEY_1$=CODE_TYPE$+CUSTOMER$+CODE$(1,3)
1030 FIND (Z[1],KEY=KEY_1$,DOM=TERM_MSG)IOL=0310
1050 GOSUB LOAD_INFO
1060 CALL "*wingrp;Enable",BUTTONS.GRP$; CALL "*wingrp;Enable",FIELDS.GRP$; CALL "*wingrp;Disable",KEY2.GRP$; ENABLE_FLG=1
1070 B_EDG$=EDG$
1185 REFRESH_FLG=1
1187 IF QUERY THEN NEXT_ID=CATALOG_ID.CTL,QUERY=0
1190 RETURN 
1500 ! Write Record
1510 WRITE_REC:
1540 WRITE (Z[1],KEY=KEY_1$)IOL=0310
1550 ! MSGBOX "Record Updated!","FYI","!"
1580 B_EDG$=EDG$; GOSUB CLEAR_REC
1590 RETURN 
2000 ! Load Information
2010 LOAD_INFO:
2013 IF NUL(EDG$) THEN RETURN 
2015 CODE_TYPE$=EDG$(1,1); CODE$=EDG$(2,13)
2020 CATALOG_ID$=EDG$(15,10)
2025 IF CODE_TYPE$="D" THEN CALL "*WINGRP;SHOW",CUST.GRP$; CUSTOMER$=CODE$(1,10),CODE$=CODE$(11) ELSE CALL "*WINGRP;HIDE",CUST.GRP$
2030 IF CODE_TYPE$<>"B" THEN CALL "*WINGRP;HIDE",B_CODE.GRP$; CALL "*WINGRP;SHOW",C_CODE.GRP$; C_CODE$=CODE$ ELSE CALL "*WINGRP;HIDE",C_CODE.GRP$; CALL "*WINGRP;SHOW",B_CODE.GRP$; B_CODE$=CODE$; B_CODE$=MID(B_CODE$,1,3),FLAG=1; GOSUB SEL_EDF; FLAG=0
2035 IF POS(CODE_TYPE$="BD")<>0 THEN CALL "*WINGRP;ENABLE",B_CODE.GRP$
2185 B_EDG$=EDG$
2190 REFRESH_FLG=1
2195 CALL "*wingrp;Enable",BUTTONS.GRP$; CALL "*wingrp;Enable",FIELDS.GRP$; CALL "*wingrp;Disable",KEY2.GRP$; ENABLE_FLG=1
2199 RETURN 
2300 ! Load Slections   
2310 LOAD_SELECTION:
2312 DIM EDG$(25)
2313 IF STP(CODE_TYPE$)="" THEN RETURN 
2315 EDG$(1,1)=CODE_TYPE$; EDG$(2,13)=CODE$
2320 EDG$(15,10)=CATALOG_ID$
2330 IF CODE_TYPE$="D" THEN CALL "ZZWLKU;PARSE_CUST",CUSTOMER$,C_D$,C_C$; CUSTOMER$=C_D$+C_C$,EDG$(2,13)=CUSTOMER$+CODE$
2490 RETURN 
2500 ! Delete Record
2510 DELETE_REC:
2520 MSGBOX MSG("DELETE_REC"),MSG("CONFIRM"),"?,YesNo",ANS$
2530 IF ANS$="NO" THEN RETURN 
2540 REMOVE (Z[1],KEY=KEY_1$)
2550 ! MSGBOX "Record Deleted!","FYI","!"
2570 GOSUB CLEAR_REC
2590 RETURN 
3000 ! First Record
3010 FIRST_REC:
3020 KEY_1$=KEF(Z[1],END=NO_FIRST)
3030 READ (Z[1],KEY=KEY_1$)IOL=0310
3040 GOSUB LOAD_INFO
3090 RETURN 
3200 ! Last Record
3210 LAST_REC:
3220 KEY_1$=KEL(Z[1],ERR=NO_LAST)
3230 READ (Z[1],KEY=KEY_1$)IOL=0310
3240 GOSUB LOAD_INFO
3290 RETURN 
3400 ! Prior Record
3410 PRIOR_REC:
3420 KEY_1$=KEP(Z[1],END=START_FILE)
3430 READ (Z[1],KEY=KEY_1$)IOL=0310
3440 GOSUB LOAD_INFO
3490 RETURN 
3600 ! Next Record
3610 NEXT_REC:
3620 KEY_1$=KEY(Z[1],END=END_FILE)
3630 READ (Z[1],KEY=KEY_1$)IOL=0310
3640 GOSUB LOAD_INFO
3690 RETURN 
4000 ! UPDATE PROCESS
4010 UPDATE_PROC:
4020 ! PRINT 'DIALOGUE'(5,5,80,24,"Updating Catalog/Category Cross Reference")
4030 CALL "EC3UTF",X3$,X4$,"","CALLED"
4040 ! PRINT 'POP'
4090 RETURN 
5000 ! Check Changes
5010 CHECK_CHANGES:
5020 IF EDG$=B_EDG$ THEN RETURN 
5030 MSGBOX MSG("REC_ALTERD"),MSG("UPDATE"),"?,YesNo",ANS$
5040 IF ANS$="NO" THEN CMD_STR$="END"; EXIT 
5050 GOSUB WRITE_REC
5090 RETURN 
5500 ! CLEAR RECORD
5510 CLEAR_REC:
5515 CODE_TYPE$=""; CODE$=""
5520 CATALOG_ID$="",%CATEGORY_DESC$=""
5530 B_CODE$=""; C_CODE$="",CUSTOMER$="",%CUST_NAME$="",BUYER_DESC$=""
5685 REFRESH_FLG=1; NEXT_ID=CODE_TYPE.CTL
5687 CALL "*wingrp;DISABLE",FIELDS.GRP$; CALL "*WINGRP;DISABLE",BUTTONS.GRP$; CALL "*wingrp;disable",KEY2.GRP$; ENABLE_FLG=0
5690 RETURN 
6000 ! CONTROL CODE TYPE
6010 CODE_PROC:
6015 IF POS(_EOM$=$00$) THEN RETURN 
6016 IF _EOM$=$09$ AND POS(CODE_TYPE$="BCD")=0 THEN NEXT_ID=CODE_TYPE.CTL; EXIT 
6020 IF CODE_TYPE$<>"B" THEN CALL "*WINGRP;HIDE",B_CODE.GRP$; CALL "*WINGRP;SHOW",C_CODE.GRP$; NEXT_ID=C_CODE.CTL ELSE CALL "*WINGRP;SHOW",B_CODE.GRP$; CALL "*WINGRP;HIDE",C_CODE.GRP$; NEXT_ID=B_CODE.CTL
6030 CALL "*wingrp;Enable",KEY2.GRP$ ! CALL "*wingrp;Enable",FIELDS.GRP$; CALL "*wingrp;Enable",BUTTONS.GRP$
6040 IF CODE_TYPE$="D" THEN CALL "*wingrp;Show",CUST.GRP$; NEXT_ID=CUSTOMER.CTL ELSE CALL "*wingrp;Hide",CUST.GRP$; END_IF ; REFRESH_FLG=1
6050 IF CODE_TYPE$="B" OR CODE_TYPE$="D" THEN TF_DESC$="Buyer Category:"
6051 IF CODE_TYPE$="C" THEN TF_DESC$="Customer Category:"
6080 REFRESH_FLG=1
6090 RETURN 
7000 SEL_EDF: REFRESH_FLG=1
7010 XX=NUM(B_CODE$,ERR=*NEXT),B_CODE$=STR(XX:"000"); GOTO 7030
7020 B_CODE$=PAD(B_CODE$,3," ")
7030 B_CODE.CTL'VALUE$=B_CODE$
7040 FIND (Z(2),KEY=B_CODE$,DOM=NO_EDF)EDF$
7050 BUYER_DESC$=EDF$(4,30),BUYER_DESC.CTL'VALUE$=BUYER_DESC$
7055 IF FLAG THEN RETURN 
7060 GOTO FIND_REC
7080 NO_EDF: MSGBOX "Buyer Category NOF",MSG("WARNING"),"!,TIM=5"
7090 IF CODE_TYPE$="D" THEN C_CODE$="",NEXT_ID=C_CODE.CTL ELSE B_CODE$="",NEXT_ID=B_CODE.CTL
7095 EXIT 
8000 ! Messages
8010 TERM_MSG:
8020 MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"?,YesNo",ANS$
8030 IF ANS$="NO" THEN CODE_TYPE$=""; NEXT_ID=CODE_TYPE.CTL; EXIT 
8040 DIM EDG$(25); NEXT_ID=TO_WEB_CATALOG_CODE.CTL
8050 CALL "*wingrp;Enable",FIELDS.GRP$; CALL "*WINGRP;ENABLE",BUTTONS.GRP$
8055 CALL "*WINGRP;DISABLE",KEY2.GRP$
8060 ENABLE_FLG=1
8080 REFRESH_FLG=1
8090 RETURN 
8100 ! No First Record
8110 NO_FIRST:
8120 MSGBOX MSG("REC_NO_1ST"),"FYI","!"
8130 RETURN 
8200 ! No Last Record
8210 NO_LAST:
8220 MSGBOX MSG("REC_NO_LST$"),"FYI","!"
8230 RETURN 
8300 ! Start of File
8310 START_FILE:
8320 MSGBOX MSG("START_FILE"),"FYI","!"
8330 RETURN 
8400 ! End of File
8410 END_FILE:
8420 MSGBOX MSG("END_OF_FIL"),"FYI","!"
8430 RETURN 
8500 ! Invalid Day Message
8510 INVALID_MSG:
8520 MSGBOX MSG("INVALID_D"),"FYI","!"
8590 RETURN 
8800 ! Invalid Item Code Message
8810 INVALID_ITM:
8820 MSGBOX MSG("INVA_ITEM"),MSG("WARNING"),"STOP"
8830 ITEM_CODE$=""; NEXT_ID=ITEM_CODE.CTL
8890 RETURN 
9000 ! Parameters
9010 GET_SYS_PARAM:
9020 DIM EDGARM$(1000); READ (Z[6],KEY=X3$(9,3)+"E/C",DOM=*NEXT)EDGARM$
9030 FIND (Z[6],KEY=X3$(9,3)+"F/M",DOM=*NEXT)OPPARM$
9090 RETURN 
9999 END 
10000 ! LOAD THE CORRECT QUERY
10010 QUERY_OPT:
10030 IF CODE_TYPE$="C" THEN %CUSTOMER$=DIM(10); PROCESS "QRY_ARG","../ARG/AR.EN",VAL$; IF NUL(VAL$) THEN RETURN ELSE QRY_VAL$=VAL$
10040 IF CODE_TYPE$="B" OR CODE_TYPE$="D" THEN PROCESS "QRY_EDF","../ECG/EC.EN",VAL$; IF NUL(VAL$) THEN RETURN ELSE QRY_VAL$=VAL$
10090 RETURN 
11000 SEL_REC:
11010 ENTER TF_TYPE$,CODE$
11015 %CUST_NAME$=""
11020 IF TF_TYPE$="B" THEN %TYPE$="Buyer Category"
11030 IF TF_TYPE$="C" THEN %TYPE$="Customer Category",CODE$=FN%ZZDISP$(CODE$,"ARG")
11040 IF TF_TYPE$="D" THEN %TYPE$="Customer/Buyer Category"; %CUST$=CODE$(1,10),CODE$=CODE$(11); CALL "ZZWLKU;AR_CUST_VAL",%CUST$,SS$,SSS$; %CUST$=FN%ZZDISP$(%CUST$,"A/R") ELSE %CUST$=""
11050 EXIT 
11060 QUERY: VAL$=""
11100 PROCESS "QRY_EDG","../ECG/EC.EN",VAL$; IF STP(VAL$)="" THEN NEXT_ID=CODE_TYPE.CTL; RETURN 
11110 KEY_1$=VAL$
11120 QUERY=1
11130 GOTO 1030
56000 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
