0010 ! EGCSRB - File Maintenance Program for APX_GLInterface (="APX"+%C$)
0035 REM "5.6 - 12/19/08 - 16.398611 - jdf - SSP# 225735
0037 REM "225735-buyer specific ship via's coming through with ship via's    
0040 REM "Copyright 2008 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 PROCESS "ECGSRB","../ECG/EC.EN"
0060 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 ! 
0130 X0$="EC3SRB",X1$="WebEC Sort Definition"
0140 ! 
0150 X0=-1,X2=-1
0160 CALL "ZZCOMP",X0$,X11$,X2$,X3$,X4$,X5$,X0,X1,X2; IF X1>0 THEN CMD_STR$="END"; EXIT 
0170 ! 
0190 DIM Z[NUM(X3$(60,3))],ECQ[12]
0200 ! Files
0210 Z$="01O ECV...  02O AR1...  03O ECU...  04O ZZF...  05O FMP...  06O ZZPARM  07O ZZE..."
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
0300 ! IOList Section
0310 IOLIST ECV$
0400 ! System Parameters
0410 GOSUB GET_SYS_PARAM
0430 CALL "*wingrp;disable",FIELDS.GRP$; ENABLE_FLG=0
0440 ! 
0480 %CUSTOMER$=DIM(10)
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0550 ! 
0790 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0970 RETURN 
1000 ! READ RECORD
1010 FIND_REC:
1015 IF NOT(NUL(SHIP_VIA$)) THEN DIM KEY_1$(23); KEY_1$(1,10)=CUSTOMER$; KEY_1$(11,10)=SORT_CODE$; KEY_1$(21,3)=SHIP_VIA$; GOTO 1030
1020 DIM KEY_1$(23); KEY_1$(1,10)=CUSTOMER$; KEY_1$(11,10)=SORT_CODE$; KEY_1$(21,3)=ELEMENT_NUM$
1030 FIND (Z[1],KEY=KEY_1$,DOM=TERM_MSG)IOL=0310
1050 GOSUB LOAD_INFO
1060 CALL "*wingrp;Enable",BUTTONS.GRP$; CALL "*wingrp;Enable",FIELDS.GRP$; ENABLE_FLG=1
1065 GOSUB PROCESS_RADIO_DATA_TYPE
1070 B_ECV$=ECV$
1185 REFRESH_FLG=1
1190 RETURN 
1500 ! Write Record
1510 WRITE_REC:
1540 WRITE (Z[1],KEY=KEY_1$)IOL=0310
1550 ! MSGBOX "Record Updated!","FYI","!"
1560 IF RADIO_GLOBAL$<>"1" AND STP(SORT_CODE$(1,8))<>"SHIP_VIA" THEN GOSUB CLEAR_REC ! SSP225735 jdf
1570 DIM ECV$(120),B_ECV$(120)
1590 RETURN 
2000 ! Load Information
2010 LOAD_INFO:
2020 IF NUL(ECV$) THEN RETURN 
2030 CUSTOMER$=ECV$(1,10); SORT_CODE$=ECV$(11,10)
2040 IF STP(SORT_CODE$)="SHIP_VIA" THEN SHIP_VIA$=STP(ECV$(21,3)); GOSUB SHIP_CHANGE ELSE ELEMENT_NUM$=ECV$(21,3); GOSUB GET_SORT_CODE; GOSUB GET_ELEMENT ! SSP225735 jdf
2050 RADIO_DATA_TYPE$=ECV$(24,1); %D_TYPE$=ECV$(24,1); SOURCE_FILE$=ECV$(87,6); %SRC$=ECV$(87,6)
2060 STRING_POS$=ECV$(27,3); DESC_OR_TEXT$=ECV$(30,40); TF_DESC$=ECV$(30,40)
2065 ! DESC_OR_TEXT$=ECV$(30,40)
2070 LENGTH$=ECV$(70,3)
2080 IF STP(ECV$(73,5))="SR" THEN DATA_TYPE$="R" ELSE DATA_TYPE$=ECV$(73,1)
2090 ACCOUNT_TYPE$=ECV$(78,9)
2100 B_ECV$=ECV$
2110 REFRESH_FLG=1
2120 CALL "*wingrp;Enable",BUTTONS.GRP$; CALL "*wingrp;Enable",FIELDS.GRP$; ENABLE_FLG=1
2125 GOSUB SEL_FILE_DESC
2130 RETURN 
2300 ! Load Slections   
2310 LOAD_SELECTION:
2315 IF STP(ECV$)="" THEN DIM ECV$(120),B_ECV$(120)
2320 ECV$(1,10)=CUSTOMER$; ECV$(11,10)=SORT_CODE$
2325 IF STP(MID(SORT_CODE$,1,8))="SHIP_VIA" THEN ECV$(21,3)=SHIP_VIA$ ELSE ECV$(21,3)=ELEMENT_NUM$ ! SSP225735 jdf
2330 ECV$(24,1)=RADIO_DATA_TYPE$; ECV$(87,6)=SOURCE_FILE$
2340 ECV$(27,3)=STRING_POS$
2345 ECV$(30,40)=DESC_OR_TEXT$; IF STP(SORT_CODE$)="SHIP_VIA" THEN ECV$(30,40)=TF_DESC$
2350 ECV$(70,3)=LENGTH$
2360 IF STP(DATA_TYPE$)="R" THEN ECV$(73,2)="SR" ELSE ECV$(73,5)=DATA_TYPE$
2370 ECV$(78,9)=ACCOUNT_TYPE$
2380 RETURN 
2500 ! Delete Record
2510 DELETE_REC:
2520 MSGBOX MSG("DELETE_REC"),MSG("CONFIRM"),"?,YesNo",ANS$
2530 IF ANS$="NO" THEN RETURN 
2540 REMOVE (Z[1],KEY=KEY_1$,DOM=*NEXT)
2550 ! MSGBOX "Record Deleted!","FYI","!"
2570 GOSUB CLEAR_REC
2590 RETURN 
2900 ! get eda record
2910 GET_EDA:
2920 FIND (Z[3],KEY=KEY_1$,DOM=*NEXT)IOL=0320
2990 RETURN 
3000 ! First Record
3010 FIRST_REC:
3015 GOSUB LOAD_SELECTION; GOSUB CHECK_CHANGES
3020 KEY_1$=KEF(Z[1],END=NO_FIRST)
3030 READ (Z[1],KEY=KEY_1$)IOL=0310
3040 GOSUB LOAD_INFO
3090 RETURN 
3200 ! Last Record
3210 LAST_REC:
3215 GOSUB LOAD_SELECTION; GOSUB CHECK_CHANGES
3220 KEY_1$=KEL(Z[1],ERR=NO_LAST)
3230 READ (Z[1],KEY=KEY_1$)IOL=0310
3240 GOSUB LOAD_INFO
3290 RETURN 
3400 ! Prior Record
3410 PRIOR_REC:
3415 GOSUB LOAD_SELECTION; GOSUB CHECK_CHANGES
3420 KEY_1$=KEP(Z[1],END=START_FILE)
3430 READ (Z[1],KEY=KEY_1$)IOL=0310
3440 GOSUB LOAD_INFO
3490 RETURN 
3600 ! Next Record
3610 NEXT_REC:
3615 GOSUB LOAD_SELECTION; GOSUB CHECK_CHANGES
3620 KEY_1$=KEY(Z[1],END=END_FILE)
3630 READ (Z[1],KEY=KEY_1$)IOL=0310
3640 GOSUB LOAD_INFO
3690 RETURN 
4000 ! SET PARAMS
4010 SET_PARM:
4020 IF EDA$(41,1)="S" THEN %CAT_SORT$=MSG("CAT_SRC_2"),%CAT_S_VAL$=MSG("CAT_SRC_V2") ELSE %CAT_SORT$=MSG("CAT_SRC"),%CAT_S_VAL$=MSG("CAT_SRC_V2")
4030 IF EDA$(41,1)<>"S" THEN CALL "*WINGRP;ENABLE",CAT_S.GRP$ ELSE CALL "*WINGRP;DISABLE",CAT_S.GRP$
4033 DROP_BOX LOAD CATEGORY_SORT.CTL,%CAT_SORT$; CATEGORY_SORT.CTL'TBL$=%CAT_S_VAL$
4035 RETURN 
5000 ! Check Changes
5010 CHECK_CHANGES:
5015 GOSUB LOAD_SELECTION
5020 IF ECV$=B_ECV$ THEN RETURN 
5030 MSGBOX MSG("REC_ALTERD"),MSG("UPDATE"),"?,YesNo",ANS$
5040 IF ANS$="NO" THEN CMD_STR$="END"; EXIT 
5050 GOSUB WRITE_REC
5090 RETURN 
5500 ! CLEAR RECORD
5510 CLEAR_REC:
5515 CUSTOMER$=""; SORT_CODE$=""; ELEMENT_NUM$=""; SRC_CODE_DESC$=""; ELEMENT_DESC$="" ! SSP225735 jdf
5520 RADIO_DATA_TYPE$=""; SOURCE_FILE$=""
5530 STRING_POS$=""; DESC_OR_TEXT$="",TF_DESC$=""
5540 LENGTH$=""; DATA_TYPE$=""
5550 ACCOUNT_TYPE$=""
5560 SHIP_VIA$=""; %SHIP_VIA_DESC$=""
5580 REFRESH_FLG=1
5590 NEXT_ID=RADIO_GLOBAL.CTL
5600 CALL "*wingrp;Disable",KEYS.GRP$
5687 CALL "*wingrp;DISABLE",FIELDS.GRP$; CALL "*WINGRP;DISABLE",BUTTONS.GRP$; ENABLE_FLG=0
5690 RETURN 
6000 ! Determine Type
6010 GLOBAL_ELEMENT:
6020 IF NUL(RADIO_GLOBAL$) THEN NEXT_ID=RADIO_GLOBAL.CTL; RETURN 
6030 IF RADIO_GLOBAL$="1" THEN {
6040 CUSTOMER$=DIM(10),%CUSTOMER$=CUSTOMER$
6050 CALL "*wingrp;Disable",KEY1.GRP$; CALL "*wingrp;Enable",KEY2.GRP$
6060 NEXT_ID=SORT_CODE.CTL
6070 REFRESH_FLG=1
6080  } ELSE {
6090 CALL "*wingrp;Enable",KEY1.GRP$
6100 NEXT_ID=CUSTOMER.CTL
6110 REFRESH_FLG=1
6120  }
6190 RETURN 
6200 ! Get Dort Code Information
6210 GET_SORT_CODE:
6220 IF LEN(SORT_CODE$)<10 THEN SORT_CODE$=PAD(SORT_CODE$,10)
6225 %SORT_CODE$=SORT_CODE$
6230 DIM ECU$(422); FIND (Z[3],KEY=SORT_CODE$,DOM=*NEXT)ECU$; GOSUB LOAD_SOURCE
6240 SRC_CODE_DESC$=ECU$(11,50)
6250 IF ECU$(419,1)="C" AND STP(CUSTOMER$)="" AND STP(SORT_CODE$)="SHIP_VIA" THEN MSGBOX MSG("ECGSRB_01"),MSG("FYI"),"STOP"; SORT_CODE$=""; SRC_CODE_DESC$=""; REFRESH_FLG=1; NEXT_ID=SORT_CODE.CTL; RETURN 
6260 IF ECU$(419,1)="C" THEN CALL "*WINGRP;HIDE",ELEM_1.GRP$; CALL "*WINGRP;SHOW",SHIP_1.GRP$; NEXT_ID=SHIP_VIA.CTL ELSE CALL "*WINGRP;HIDE",SHIP_1.CTL.GRP$; CALL "*WINGRP;SHOW",ELEM_1.GRP$; NEXT_ID=ELEMENT_NUM.CTL
6270 CALL "*wingrp;enable",KEY3.GRP$
6280 REFRESH_FLG=1
6290 RETURN 
6300 ! Get Element Information
6310 GET_ELEMENT:
6320 IF NUL(ELEMENT_NUM$) THEN RETURN 
6325 ELEMENT_NUM$=PAD(ELEMENT_NUM$,3,0,"0"),ELEMENT_NUM.CTL'VALUE$=ELEMENT_NUM$
6330 IF MID(ECU$,419,1)<>"C" THEN RETURN 
6340 DIM FMPS$(500); FIND (Z[5],KEY="S"+ELEMENT_NUM$,DOM=*NEXT)FMPS$
6350 IF NUL(FMPS$) THEN RETURN 
6360 ELEMENT_DESC$=FMPS$(3,15)
6370 IF FMPS$(18,1)="Y" THEN {
6380 MSGBOX MSG("ECGSRB_02")+SEP+"("+MSG("ECGSRB_03")+")",MSG("FYI"),"!"
6390 ELEMENT_NUM$=""; ELEMENT_DESC$=""
6400 NEXT_ID=ELEMENT_NUM.CTL
6410  }
6420 REFRESH_FLG=1
6430 RETURN 
6500 ! Process the Data Type
6510 PROCESS_RADIO_DATA_TYPE:
6520 IF NUL(RADIO_DATA_TYPE$) THEN RETURN 
6530 IF STP(RADIO_DATA_TYPE$)="T" THEN STRING_POS$=DIM(3); SOURCE_FILE$=DIM(6); CALL "*wingrp;disable",TYPE.GRP$; CALL "*wingrp;enable",T_TYPE.GRP$; NEXT_ID=DESC_OR_TEXT.CTL ELSE CALL "*wingrp;enable",TYPE.GRP$; CALL "*wingrp;disable",T_TYPE.GRP$ ! DESC_OR_TEXT$=""; LENGTH$="000"
6570 %D_TYPE$=RADIO_DATA_TYPE$
6580 REFRESH_FLG=1
6590 RETURN 
6600 ! load the source file Options
6610 LOAD_SOURCE:
6620 %SRC_FILE$=""
6630 %SRC_FILE$=MID(ECU$,61,6)+"/"
6640 FOR SF=0 TO 3
6650 IF ECU$(SF*6+355,6)<>DIM(6) THEN %SRC_FILE$+=ECU$(SF*6+355,6)+"/"
6660 NEXT SF
6670 DROP_BOX LOAD SOURCE_FILE.CTL,%SRC_FILE$
6674 DROP_BOX WRITE SOURCE_FILE.CTL,ECU$(61,6)
6680 REFRESH_FLG=1
6690 RETURN 
6700 ! String Position
6710 STRING_POSITION:
6715 XX=NUM(STRING_POS$,ERR=*NEXT),STRING_POS$=STR(XX:"000"); GOTO 6718
6717 STRING_POS$=PAD(STRING_POS$,3," ")
6720 DIM ZZF$(300); ZZF$(13,40)="**User Defined Data Reference**"
6730 READ (Z[4],KEY=SOURCE_FILE$+RADIO_DATA_TYPE$+"  "+STRING_POS$,DOM=*NEXT)ZZF$; KEY_4$=SOURCE_FILE$+RADIO_DATA_TYPE$+STRING_POS$; GOTO 6750
6740 KEY_4$=KEY(Z[4],END=6760); READ (Z[4],KEY=KEY_4$)ZZF$
6750 IF KEY_4$(1,7)<>SOURCE_FILE$+MID(RADIO_DATA_TYPE$,1,1) THEN DIM ZZF$(300); ZZF$(13,40)="**User Defined Data Reference**" ! SSP225735 jdf
6760 DESC_OR_TEXT$=ZZF$(13,40); LENGTH$=ZZF$(56,3); DATA_TYPE$=ZZF$(53,3); ACCOUNT_TYPE$=ZZF$(59,9)
6770 DATA_TYPE$="S"+DIM(4); ACCOUNT_TYPE$(1,4)=DIM(4)
6780 REFRESH_FLG=1
6790 RETURN 
6800 ! Description / Text
6810 ENT_DESCRIPTION:
6820 DESC_OR_TEXT$=PAD(DESC_OR_TEXT$,40)
6880 REFRESH_FLG=1
6890 RETURN 
6900 ! Init the length...
6910 INIT_LEN:
6920 IF NUM(LENGTH$)=0 THEN VV=LEN(STP(DESC_OR_TEXT$,1))+1; LENGTH$=STR(VV:"000")
6930 REFRESH_FLG=1
6940 RETURN 
6950 ! Set the Length
6960 SET_LENGTH:
6970 LENGTH$=PAD(LENGTH$,3,0,"0")
6980 REFRESH_FLG=1
6990 RETURN 
7000 ! Get The Data Type      
7010 GET_DATA_TYPE:
7020 IF STP(DATA_TYPE$)="R" THEN DATA_TYPE$="SR"
7090 RETURN 
7200 ! Change screen when loading the Ship Via option.
7210 SHIP_CHANGE:
7220 CALL "*WINGRP;HIDE",ELEM_1.GRP$
7230 CALL "*WINGRP;SHOW",SHIP_1.GRP$
7280 REFRESH_FLG=1
7290 RETURN 
7300 ! Change screen when loading the Element option.
7310 ELEM_CHANGE:
7320 CALL "*WINGRP;HIDE",SHIP_1.GRP$
7330 CALL "*WINGRP;SHOW",ELEM_1.GRP$
7380 REFRESH_FLG=1
7390 RETURN 
8000 ! Messages
8010 TERM_MSG:
8020 MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"?,YesNo",ANS$
8030 IF ANS$="NO" THEN IF RADIO_GLOBAL$="1" THEN NEXT_ID=SHIP_VIA.CTL; SHIP_VIA$=""; REFRESH_FLG=1; EXIT ELSE NEXT_ID=ELEMENT_NUM.CTL; ELEMENT_NUM$=""; ANS$=""; REFRSH_FLG=1; EXIT ! SSP225735 jdf                
8040 DIM ECV$(121); IF NOT(NUL(SHIP_VIA$)) THEN NEXT_ID=DESC_OR_TEXT.CTL; DESC_OR_TEXT$=%SHIP_VIA_DESC$ ELSE NEXT_ID=RADIO_DATA_TYPE.CTL
8050 CALL "*wingrp;Enable",FIELDS.GRP$; CALL "*WINGRP;ENABLE",BUTTONS.GRP$
8060 ENABLE_FLG=1
8070 IF RADIO_GLOBAL$="1" AND STP(SHIP_VIA$)<>"" THEN GOSUB WRITE_REC ! SSP225735 jdf
8075 IF RADIO_GLOBAL$="2" AND STP(SHIP_VIA$)<>"" AND SORT_CODE$(1,8)="SHIP_VIA" THEN GOSUB LOAD_SELECTION; GOSUB WRITE_REC ! SSP225735 jdf
8090 RETURN 
8100 ! No First Record
8110 NO_FIRST:
8120 MSGBOX MSG("REC_NO_1ST"),"FYI","!"
8130 RETURN 
8200 ! No Last Record
8210 NO_LAST:
8220 MSGBOX MSG("REC_NO_LST$"),"FYI","!"
8230 RETURN 
8300 ! Start of File
8310 START_FILE:
8320 MSGBOX MSG("START_FILE"),"FYI","!"
8330 RETURN 
8400 ! End of File
8410 END_FILE:
8420 MSGBOX MSG("END_OF_FIL"),"FYI","!"
8430 RETURN 
8500 ! Invalid Day Message
8510 INVALID_MSG:
8520 MSGBOX MSG("INVALID_D"),"FYI","!"
8590 RETURN 
8800 ! Invalid Item Code Message
8810 INVALID_ITM:
8820 MSGBOX MSG("INVA_ITEM"),MSG("WARNING"),"STOP"
8830 ITEM_CODE$=""; NEXT_ID=ITEM_CODE.CTL
8890 RETURN 
9000 ! Parameters
9010 GET_SYS_PARAM:
9090 RETURN 
9200 ! ENABLE FUNCTIONS
9210 ENABLE_FUNCTIONS:
9230 IF FILT_EXCL_ITEM_STOCK$<>"N" THEN CALL "*WINGRP;ENABLE",FUNCTIONS.GRP$ ELSE CALL "*WINGRP;DISABLE",FUNCTIONS.GRP$
9280 REFRESH_FLG=1
9290 RETURN 
9999 END 
10000 SEL_FILE_DESC: REFRESH_FLG=1
10010 FIND (Z(7),KEY=SOURCE_FILE$,DOM=NO_FILE)ZZE$
10020 FILE_DESC$=ZZE$(7,39)
10030 RETURN 
10100 NO_FILE:RETURN 
11000 ! Item Query
11010 ELEMENT_QRY:
11020 VAL$=""
11030 IF ECU$(419,1)="C" THEN PROCESS "QRY_ECV_1","../ECG/EC.EN",VAL$ ELSE PROCESS "QRY_ECV_2","../ECG/EC.EN",VAL$; END_IF ; IF NUL(VAL$) THEN RETURN ELSE QRY_VAL$=VAL$
11070 REFRESH_FLG=1
11080 RETURN 
11500 QUERY: VAL$=""
11510 PROCESS "QRY_ECV","../ECG/EC.EN",VAL$
11530 CUSTOMER$=VAL$(1,10),%CUSTOMER$=CUSTOMER$
11540 SORT_CODE$=VAL$(11,10)
11550 ELEMENT_NUM$=VAL$(21,3)
11560 GOSUB GET_SORT_CODE
11570 GOSUB GET_ELEMENT
11580 GOTO FIND_REC
56000 ! "190554-Getting program exception in order entry. Tried deleting the
56001 ! "       OS_BYDATE and reentering but still stuck in error handler
56003 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
56005 REM "225735-buyer specific ship via's coming through with ship via's    
