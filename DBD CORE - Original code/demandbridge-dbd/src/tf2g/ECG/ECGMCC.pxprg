0010 ! EGCMCC - Catalog ID Setup
0035 REM "5.6 - 08/05/08 - 17.584722 - crg - SSP# 221551
0037 REM "221551-Oracle related changes broke some GUI maintanance pgms.     
0040 REM "Copyright 2008 DemandBridge, Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 PROCESS "ECGMCC","../ECG/EC.EN"
0060 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 ! 
0130 X0$="EC3MCC",X1$="Catalog CATALOG Setup"
0140 ! 
0150 X0=-1,X2=-1
0160 CALL "ZZCOMP",X0$,X11$,X2$,X3$,X4$,X5$,X0,X1,X2; IF X1>0 THEN CMD_STR$="END"; EXIT 
0170 ! 
0190 DIM Z[NUM(X3$(60,3))],EDA[12]
0200 ! Files
0210 Z$="01O EDA...  02O ECH...  03O ECQ...  05O EDI...  06O ZZPARM  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
0300 ! IOList Section
0310 IOLIST EDA$
0400 ! System Parameters
0410 GOSUB GET_SYS_PARAM
0430 CALL "*wingrp;disable",FIELDS.GRP$; ENABLE_FLG=0
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0550 ! 
0790 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0970 RETURN 
1000 ! READ RECORD
1010 FIND_REC:
1015 IF NUL(CATALOG_ID$) THEN RETURN 
1018 GOSUB FORMAT_CODE
1020 KEY_1$=CATALOG_ID$
1030 FIND (Z[1],KEY=KEY_1$,DOM=TERM_MSG)IOL=0310
1040 ! 
1050 GOSUB LOAD_INFO
1060 CALL "*wingrp;Enable",BUTTONS.GRP$; CALL "*wingrp;Enable",FIELDS.GRP$
1070 B_EDA$=EDA$
1185 REFRESH_FLG=1,NEXT_ID=TF_DESC.CTL
1190 RETURN 
1300 ! FORMAT CATALOG CODE
1310 FORMAT_CODE:
1320 IF NUL(CATALOG_ID$) THEN RETURN 
1330 XX=NUM(CATALOG_ID$,ERR=*NEXT); CATALOG_ID$=PAD(CATALOG_ID$,10,0,"0"); GOTO 1380
1340 CATALOG_ID$=PAD(CATALOG_ID$,10,1," ")
1380 REFRESH_FLG=1
1390 RETURN 
1500 ! Write Record
1510 WRITE_REC:
1540 WRITE (Z[1],KEY=KEY_1$)IOL=0310
1550 ! MSGBOX "Record Updated!","FYI","!"
1570 IF EDA$(73,1)="Y" THEN PRINT 'DIALOGUE'(15,15,70,10,"Update Catalog with item descriptions"); CALL "EC3UTK",X3$,X4$,KEY_1$,"","*",UTKOPTIONS$; WAIT 3; PRINT 'POP',
1575 GOSUB CLEAR_REC; DIM EDA$(75)
1580 B_EDA$=EDA$
1590 RETURN 
2000 ! Load Information
2010 LOAD_INFO:
2013 IF NUL(EDA$) THEN RETURN 
2015 CATALOG_ID$=EDA$(1,10)
2020 TF_DESC$=EDA$(11,30)
2040 CATALOG_TYPE$=EDA$(41,1); CATALOG_DDL_DESC$=EDA$(52,20); RESTRICT_REPT$=EDA$(72,1)
2050 SORT_BY_ITEM_DESC$=EDA$(73,1); RESTRICT_PC_DDL$=EDA$(74,1)
2185 B_EDA$=EDA$
2190 REFRESH_FLG=1
2195 CALL "*wingrp;Enable",BUTTONS.GRP$; CALL "*wingrp;Enable",FIELDS.GRP$; ENABLE_FLG=1
2199 RETURN 
2300 ! Load Slections   
2310 LOAD_SELECTION:
2313 IF NUL(CATALOG_ID$) THEN RETURN ELSE CATALOG_ID$=PAD(CATALOG_ID$,10)
2315 EDA$(1,10)=CATALOG_ID$
2320 EDA$(11,30)=TF_DESC$
2340 EDA$(41,1)=CATALOG_TYPE$; EDA$(52,20)=CATALOG_DDL_DESC$; EDA$(72,1)=RESTRICT_REPT$
2350 EDA$(73,1)=SORT_BY_ITEM_DESC$; EDA$(74,1)=RESTRICT_PC_DDL$
2490 RETURN 
2500 ! Delete Record
2510 DELETE_REC:
2520 MSGBOX MSG("DELETE_REC"),MSG("CONFIRM"),"?,YesNo",ANS$
2530 IF ANS$="NO" THEN RETURN 
2540 REMOVE (Z[1],KEY=KEY_1$,DOM=*NEXT)
2550 ! MSGBOX "Record Deleted!","FYI","!"
2560 GOSUB DELETE_CATALOG
2570 GOSUB CLEAR_REC
2590 RETURN 
3000 ! First Record
3010 FIRST_REC:
3020 KEY_1$=KEF(Z[1],END=NO_FIRST)
3030 READ (Z[1],KEY=KEY_1$)IOL=0310
3040 GOSUB LOAD_INFO
3090 RETURN 
3200 ! Last Record
3210 LAST_REC:
3220 KEY_1$=KEL(Z[1],ERR=NO_LAST)
3230 READ (Z[1],KEY=KEY_1$)IOL=0310
3240 GOSUB LOAD_INFO
3290 RETURN 
3400 ! Prior Record
3410 PRIOR_REC:
3420 KEY_1$=KEP(Z[1],END=START_FILE)
3430 READ (Z[1],KEY=KEY_1$)IOL=0310
3440 GOSUB LOAD_INFO
3490 RETURN 
3600 ! Next Record
3610 NEXT_REC:
3620 KEY_1$=KEY(Z[1],END=END_FILE)
3630 READ (Z[1],KEY=KEY_1$)IOL=0310
3640 GOSUB LOAD_INFO
3690 RETURN 
5000 ! Check Changes
5010 CHECK_CHANGES:
5020 IF EDA$=B_EDA$ THEN RETURN 
5030 MSGBOX MSG("REC_ALTERD"),MSG("UPDATE"),"?,YesNo",ANS$
5040 IF ANS$="NO" THEN CMD_STR$="END"; EXIT 
5050 GOSUB WRITE_REC
5090 RETURN 
5500 ! CLEAR RECORD
5510 CLEAR_REC:
5515 CATALOG_ID$=""
5520 TF_DESC$=""
5540 CATALOG_TYPE$=""; CATALOG_DDL_DESC$=""; RESTRICT_REPT$=""
5550 SORT_BY_ITEM_DESC$=""; RESTRICT_PC_DDL$=""
5685 REFRESH_FLG=1; NEXT_ID=CATALOG_ID.CTL
5690 RETURN 
7600 DELETE_CATALOG:! "delete catalog
7610 ! PRINT @(0,20),'CL',"Deleting catalog",
7612 READ (Z[2],KEY=KEY_1$,DOM=7613)
7620 ECHKEY$=KEY(Z[2],END=7650)
7622 IF ECHKEY$(1,LEN(KEY_1$))<>KEY_1$ THEN GOTO 7650
7625 REMOVE (Z[5],KEY=ECHKEY$(1,20),DOM=7626)
7630 REMOVE (Z[2],KEY=ECHKEY$); GOTO 7620
7650 REMOVE (Z[3],KEY=KEY_1$,DOM=7651); REM "remove loading parameters C3
7690 ! PRINT @(0,20),'CL',
7695 RETURN 
8000 ! Messages
8010 TERM_MSG:
8020 MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"?,YesNo",ANS$
8030 IF ANS$="NO" THEN CATALOG_ID$=""; NEXT_ID=CATALOG_ID.CTL; EXIT 
8040 DIM EDA$(75); NEXT_ID=TF_DESC.CTL
8050 CATALOG_TYPE$="C"
8070 CALL "*wingrp;Enable",FIELDS.GRP$; CALL "*wingrp;Enable",BUTTONS.GRP$
8080 REFRESH_FLG=1
8090 RETURN 
8100 ! No First Record
8110 NO_FIRST:
8120 MSGBOX MSG("REC_NO_1ST"),"FYI","!"
8130 RETURN 
8200 ! No Last Record
8210 NO_LAST:
8220 MSGBOX MSG("REC_NO_LST$"),"FYI","!"
8230 RETURN 
8300 ! Start of File
8310 START_FILE:
8320 MSGBOX MSG("START_FILE"),"FYI","!"
8330 RETURN 
8400 ! End of File
8410 END_FILE:
8420 MSGBOX MSG("END_OF_FIL"),"FYI","!"
8430 RETURN 
8500 ! Invalid Day Message
8510 INVALID_MSG:
8520 MSGBOX MSG("INVALID_D"),"FYI","!"
8590 RETURN 
8800 ! Invalid Item Code Message
8810 INVALID_ITM:
8820 MSGBOX MSG("INVA_ITEM"),MSG("WARNING"),"STOP"
8830 ITEM_CODE$=""; NEXT_ID=ITEM_CODE.CTL
8890 RETURN 
9000 ! Parameters
9010 GET_SYS_PARAM:
9020 DIM ECPARM$(1000); READ (Z[6],KEY=X3$(9,3)+"E/C",DOM=*NEXT)ECPARM$
9090 RETURN 
9999 END 
10100 QUERY:
10110 PROCESS "QRY_EDA","../EDA/EC.EN",VAL$; IF STP(VAL$)="" THEN RETURN 
10120 BUYER_ID$=VAL$(1,10); ITEM_CODE$=VAL$(11,10)
10150 GOSUB FIND_REC
10160 NEXT_ID=DEF_CATALOG.CTL
10180 REFRESH_FLG=1
10190 RETURN 
11000 ! Item Query
11010 ITEM_QUERY:
11020 VAL$=""
11030 IF NUL(BUYER_ID$) THEN %ITEM_CUST$="          "; PROCESS "QRY_IC0.1.4","../ICG/IC.EN",VAL$; IF NUL(VAL$) THEN RETURN ELSE QRY_VAL$=VAL$
11040 IF NOT(NUL(BUYER_ID$)) THEN %BUYER_ID$=BUYER_ID$; PROCESS "QRY_FM1_ITEM","../FMG/FM.EN",VAL$; IF NUL(VAL$) THEN RETURN ELSE QRY_VAL$=VAL$
11070 REFRESH_FLG=1
11080 RETURN 
56000 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
56001 REM "221551-Oracle related changes broke some GUI maintanance pgms.     
