0010 ! ECGSTL - WebEC Server Log Viewer
0020 SETESC 9300; SETERR 9000
0035 REM "5.5 - 05/02/07 - 10.371666 - jir - SSP# 205761
0036 ! "5.1 - 11/11/02 - 09.300000 - JIR - GUI MODE
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0105 CURR_DIR$=LWD
0110 X0$="EC3STL",X1$="WebEC Server Log Viewer"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1,M0$="#,###,##0",FIRST_LINE=5,SPACING=5
0135 C9=-1
0136 X1$="",X2$=""
0200 REM "
0205 ! PRINT 'WINDOW'(0,0,80,10),'CS'
0206 DIM IDX[100]
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,-1; IF ML_SERVER_ID.CTL=0 THEN PRINT 'POP' END_IF ; IF X1>0 THEN GOTO 9920
0241 BBB=HFN; OPEN (BBB)FID(0); BBB$=FIN(BBB); CLOSE (BBB); BBB=DEC(BBB$(10,1)); IF BBB<>XXX THEN PRINT 'POP'
0242 ! PRINT 'POP'
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=ESC+"BG"+Z0$+ESC+"EG"
0300 REM "IOLISTS
0310 IOLIST ECY$
0320 IOLIST ECZ$,ECZ[0],ECZ[1],ECZ[2],ECZ[3],ECZ[4],ECZ[5],ECZ[6],ECZ[7],ECZ[8],ECZ[9],ECZ[10],ECZ[11],ECZ[12],ECZ[13],ECZ[14],ECZ[15],ECZ[16],ECZ[17],ECZ[18]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O ECY...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0610 GOSUB 6000
1000 REM "Get server to view
1005 ! DIM ECY$(512),ECY[1]; PRINT @(0,5),'CE',; IF LOG_FILE>0 THEN CLOSE (LOG_FILE) END_IF ; MONITOR_MODE=0
1006 DIM ECY[1]; IF LOG_FILE>0 THEN CLOSE (LOG_FILE) END_IF ; MONITOR_MODE=0
1010 C9=0 ! CALL "ZZENTR","SUZX",ECY{ALL},ECY$,X4$,X3$,15,3,1,15,C0,"",X$,"","EC3STL02","ECY","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1010,1011
1011 IF ABS(C0)>1 THEN GOTO 9900
1015 FIND (Z[1],KEY=ECY$(1,15),DOM=1005)IOL=0310
1020 ! PRINT @(31,3),ECY$(16,35),
1025 GOSUB 7700; REM "Clear filters
1030 LOG_FILE_NAME$=STP(ECY$(56,60),1)+"/"+STP(ECY$(196,60),1)
1035 CURR_DIR$=LWD
1036 REM "*OLD TBRED* SET DIR STP(ECY$(56,60),1)
1100 REM "Open log and show a page at a time
1102 REM "Assumes TF format, fields seperated by '|'s, 1st=date, 2nd=time, 3rd=message type
1103 REM "*OLD TBRED * SET DIR STP(ECY$(56,60),1)
1104 CURR_DATE$="",START_LINE=5,CURR_LINE=START_LINE,MAX_LINE=9999999; CLOSE (LOG_FILE,ERR=*NEXT)
1105 LOG_FILE=UNT; OPEN (LOG_FILE,OPT="TEXT",ERR=*NEXT)LOG_FILE_NAME$; GOTO 1110
1106 MSGBOX "Log File : "+LOG_FILE_NAME$+" No Exist","!Error","!"; NEXT_ID=ML_SERVER_ID.CTL; IF ARG_1$<>"" THEN CMD_STR$="E" END_IF ; EXIT 
1110 READ (LOG_FILE,END=1180)LOG$
1115 GOSUB 7500; REM "Get LOG_DATE$, LOG_TIME$, MESS_TYPE$, REST$
1120 GOSUB 7600; IF SKIPIT THEN GOTO 1110; REM "do any filtering we need
1150 GOSUB 2000 ! "display the line
1175 GOTO 1110
1180 IF SAVE_DATE$="" THEN SAVE_DATE$=DB_DATE$; CALL "*WINGRP;SHOW",DAT.GRP$
1181 REFRESH_FLG=1; NEXT_ID=BUTTON_5.CTL; PRINT 'MESSAGE'('F8'+'BLACK',0); IDX=0; CALL "$CTL-10;AGENT_MSG","Ready",1; EXIT ; REM "After file read loop
1182 IF MONITOR_MODE THEN PRINT @(0,20),"Press I to interrupt monitoring ",; READ RECORD (0,SIZ=1,TIM=2,ERR=1110)IN$; IF POS(IN$="Ii")<>0 THEN MONITOR_MODE=0; GOTO 1182 ELSE GOTO 1110
1185 CALL "ZZPROM","X1EC3STL",X3$,Z,"","","",0
1190 ON Z GOTO 1000,9900,1192,1193,1191
1191 GOSUB 7750; GOTO 1100; REM "Adjust filter settings
1192 CLOSE (LOG_FILE); GOTO 1100; REM "Redisplay
1193 GOTO 2200; REM "Turn on monitor mode
2000 REM "Display 1 entry
2010 IF LOG_DATE$<>CURR_DATE$ THEN GOSUB 2150
2015 IF CURR_LINE+REST_LINES>MAX_LINE THEN GOSUB 2100
2051 ML_LOG$=ML_LOG$+ID$+" " ! LIST_BOX LOAD ML_LOG.CTL,CURR_LINE-5,ID$+" "
2055 ! FOR I=1 TO REST_LINES; PRINT @(1+ID_LENGTH,CURR_LINE+I-1),REST$[I]; NEXT I
2056 FOR I=1 TO REST_LINES; IF I>1 THEN ML_LOG$=ML_LOG$+PAD(XX$,ID_LENGTH," "); XX=39 ELSE XX=16
2057 ML_LOG$=ML_LOG$+REST$[I]+SEP+PAD(XX$,ID_LENGTH," "); IDX+=1; LIST_BOX LOAD ML_LOG.CTL,IDX,DIM(XX)+ID$+" "+REST$[I]; ID$=""
2060 NEXT I
2065 CURR_LINE=CURR_LINE+REST_LINES
2070 IF MOD((CURR_LINE-5),100)=0 THEN SET_FOCUS 0; PRINT 'MESSAGE'('BR'+'_RED'+'WHITE'+"Read records : "+STR(CURR_LINE-5),0),; IF MOD(CURR_LINE-5,1500)=0 THEN CALL "$CTL-10;AGENT_MSG","Please wait, read record : "+STR(CURR_LINE-5)
2095 RETURN 
2100 REM "Start a new page
2104 IF MONITOR_MODE THEN GOTO 2130
2105 CALL "ZZPROM","X0EC3STL",X3$,Z,"","","",0
2110 ON Z GOTO 2111,2112,2113,2114,2115,2119
2111 GOTO 2130; REM "Continue
2112 EXITTO 9900; REM "Exit
2113 CLOSE (LOG_FILE); EXITTO 1100; REM "Redisplay
2114 EXITTO 2200; REM "Turn on Monitor Mode
2115 GOSUB 7750; CLOSE (LOG_FILE); EXITTO 1100; REM "Filter
2130 PRINT @(0,START_LINE),'CE',
2135 CURR_LINE=START_LINE; GOSUB 2150
2145 RETURN 
2150 REM "New date
2155 IF CURR_LINE+2>MAX_LINE THEN GOSUB 2100; REM "start new page if close to the bottom
2157 IF CURR_LINE>6 THEN ML_LOG$=ML_LOG$+SEP+LOG_DATE$+SEP+"          "; CURR_LINE=CURR_LINE+1; GOTO 2180
2160 ! PRINT @(0,CURR_LINE),'BR',LOG_DATE$,'ER',; CURR_LINE=CURR_LINE+1
2161 ! IF CURR_LINE>5 THEN ML_LOG$=ML_LOG$(1,LEN(ML_LOG$)-20)
2162 CURR_LINE=CURR_LINE+1,ML_LOG$=ML_LOG$+LOG_DATE$+SEP+"          "
2180 CURR_DATE$=LOG_DATE$,DD$='BR'+'WHITE'+LOG_DATE$,DD$=PAD(DD$,86," ")
2185 IDX+=1; LIST_BOX LOAD ML_LOG.CTL,IDX,DD$
2186 DB_DATE$+=LOG_DATE$+"/",CC+=1,IDX[CC]=IDX
2195 RETURN 
2200 REM "Turn on monitor and position file near the end
2205 TMP$=FN%XFD$(LOG_FILE,0); LOG_SIZE=DEC($00$+TMP$(38,4))
2210 IF LOG_SIZE>2000 THEN READ (LOG_FILE,IND=LOG_SIZE-1000)
2220 MONITOR_MODE=1
2245 GOTO 1110
6000 REM "BACKGROUND
6005 ! PRINT (0,ERR=6016)'SB',
6020 ! PRINT @(1,3),"WebEC Server:",@(0,4),Z0$,
6165 ! PRINT (0,ERR=6166)'SF',
6190 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6410 GOSUB 6000
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6442 PRINT @(15,3),ECY$(1,15),@(32,3),ECY$(16,40),
6445 RETURN 
7500 REM ""GET LOG_DATE$, LOG_TIME$, MESS_TYPE$, REST$, ID$
7503 REM "Assumes TopForm format log with date, time, and mess_type as first 3 fields
7505 LOG_DATE$="",LOG_TIME$="",MESS_TYPE$="",REST$="",ID$="",ID_LENGTH=0,REST_LENGTH=0
7510 P1=POS("|"=LOG$),P2=POS("|"=LOG$,1,2),P3=POS("|"=LOG$,1,3)
7511 IF P1+P2+P3=0 THEN GOTO *RETURN
7515 LOG_DATE$=MID(LOG$,1,P1-1),LOG_TIME$=MID(LOG$,P1+1,P2-P1-1),MESS_TYPE$=MID(LOG$,P2+1,P3-P2-1),REST$=STP(LOG$(P3+1),1)
7520 ID$=LOG_TIME$+" "+MESS_TYPE$,ID_LENGTH=LEN(ID$),REST_LENGTH=113-ID_LENGTH
7525 TEMP=LEN(REST$)/REST_LENGTH; IF FPT(TEMP)<>0 THEN REST_LINES=INT(TEMP)+1 ELSE REST_LINES=TEMP
7527 DIM REST$[REST_LINES]; FOR I=1 TO REST_LINES; REST$[I]=DIM(REST_LENGTH); REST$[I](1)=REST$((I-1)*REST_LENGTH+1); NEXT I
7545 RETURN 
7550 REM "Set Filters
7595 RETURN 
7600 REM "Apply any filtering, set SKIPIT to 1 if we want to skip this one
7605 SKIPIT=0
7610 IF LOG_DATE$<START_DATE$ OR LOG_DATE$>END_DATE$ THEN SKIPIT=1; GOTO 7690
7615 IF LOG_TIME$<START_TIME$ OR LOG_TIME$>END_TIME$ THEN SKIPIT=1; GOTO 7690
7620 IF FMESS_TYPE$<>"    " THEN IF MESS_TYPE$<>FMESS_TYPE$ THEN SKIPIT=1; GOTO 7690
7625 IF FCONTAIN$<>"" THEN IF POS(FCONTAIN$=LOG$)=0 THEN SKIPIT=1; GOTO 7690
7695 RETURN 
7700 CLEAR_FILTER_VARIABLES:
7710 DIM START_DATE$(10),END_DATE$(10,$7E$)
7715 DIM START_TIME$(8),END_TIME$(8,$7E$)
7720 DIM FMESS_TYPE$(4)
7725 FCONTAIN$=""
7740 FILTERING=0; GOSUB 7800
7745 RETURN 
7750 REM "Set Filters
7760 CALL "EC3STF",X3$,X4$,START_DATE$,END_DATE$,START_TIME$,END_TIME$,FMESS_TYPE$,FCONTAIN$
7765 IF START_DATE$<>DIM(10) OR END_DATE$<>DIM(10,$7E$) OR START_TIME$<>DIM(8) OR END_TIME$<>DIM(8,$7E$) OR FMESS_TYPE$<>DIM(4) OR FCONTAIN$<>"" THEN FILTERING=1
7770 GOSUB 7800
7795 RETURN 
7800 REM "Indicate if filtering is on or off
7815 ! IF FILTERING THEN PRINT @(70,3),"Filter ON ", ELSE PRINT @(70,3),"Filter OFF",
7845 RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 3005,3105
9900 REM "END PROGRAM
9901 END_PROG:
9905 IF LOG_FILE>0 THEN CLOSE (LOG_FILE)
9907 REM "*OLD TBRED * IF CURR_DIR$<>"" THEN SET DIR CURR_DIR$
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
10000 FILTER_OPTIONS:
10005 GOSUB CLEAR_FILTER_VARIABLES; ML_LOG$="",IDX=0; LIST_BOX LOAD ML_LOG.CTL,""; IF CB_FILTER$="0" THEN GOTO 1100
10010 PROCESS "ECGSTS.3.1","../ECG/EC.EN",START_DATE$,END_DATE$,START_TIME$,END_TIME$,FMESS_TYPE$,FCONTAIN$
10015 IF START_DATE$="" THEN DIM START_DATE$(10) ELSE CALL "ZZWDTE;VALIDATE",START_DATE$,JJ$,JJ$; START_DATE$=START_DATE$(1,4)+"-"+START_DATE$(5,2)+"-"+START_DATE$(7)
10016 IF START_TIME$="" THEN DIM START_TIME$(8)
10017 IF END_DATE$="" THEN DIM END_DATE$(10,$7E$) ELSE CALL "ZZWDTE;VALIDATE",END_DATE$,JJ$,JJ$; END_DATE$=END_DATE$(1,4)+"-"+END_DATE$(5,2)+"-"+END_DATE$(7)
10018 IF END_TIME$="" THEN DIM END_TIME$(8,$7E$)
10019 IF FMESS_TYPE$="" THEN FMESS_TYPE$="    "
10020 IF START_DATE$<>DIM(10) OR END_DATE$<>DIM(10,$7E$) OR START_TIME$<>DIM(8) OR END_TIME$<>DIM(8,$7E$) OR FMESS_TYPE$<>DIM(4) OR FCONTAIN$<>"" THEN FILTERING=1
10030 ML_LOG$="",REFRESH_FLG=1
10040 GOTO 1100
10100 END_FILTER:RETURN 
11000 ASSIGN_PANEL:
11010 ARG_1$=ML_FROM_DATE$,ARG_2$=ML_END_DATE$,ARG_3$=ML_START_TIME$,ARG_4$=ML_END_TIME$,ARG_5$=ML_MESSAGE$,ARG_6$=ML_CONTAINS$,CMD_STR$="END"
11020 RETURN 
11030 ML_LOG$=""
11100 SEL_DATE:
11110 IF SAVE_DATE$="" THEN SAVE_DATE$=DB_DATE$
11120 DB_DATE$=SAVE_DATE$
11130 BB$=DB_DATE$,OPT_MNU$="[&Dates=1000D,,",CNT=0; DIM DAT$[100]
11140 XX=POS("/"=BB$); IF XX=0 THEN GOTO NXT_OPT
11150 T$=BB$(1,XX-1),BB$=BB$(XX+1)
11160 CNT+=1,OPT_MNU$+=T$+"="+STR(CNT)+",",DAT$[CNT]=T$
11170 GOTO 11140
11180 NXT_OPT:
11190 OPT_MNU$(LEN(OPT_MNU$))="]"
11200 POPUP_MENU OPT_MNU$,OPT
11210 IF OPT>0 THEN LIST_BOX LOAD ML_LOG.CTL,""; START_DATE$=DAT$[OPT],END_DATE$=START_DATE$; GOTO 10030
11220 EXIT 
56000 ! "205761-Server monitor buttons must always be selected twice        
