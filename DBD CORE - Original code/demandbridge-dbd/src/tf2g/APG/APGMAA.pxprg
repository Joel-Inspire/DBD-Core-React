0010 ! APGMAA - Vendor Setup/Inquiry
0035 REM "5.7 - 02/06/18 - 14.971944 - crg - SSP# 263584
0037 REM "263584-Providex version upgrade from Pvx 7.1 to PxPlus 2017        
0040 REM "Copyright 2018 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! 
0060 PROCESS "APGMAA","../APG/AP.EN"
0070 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 ! 
0130 X0$="APGMAA"
0140 X0=-1,X2=-1
0150 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
0160 ! 
0170 ! Dim Variables
0180 DIM Q$[20],P$[20]
0190 DIM Z[NUM(X3$(60,3))],AP4[2]
0200 ! Open Files
0210 Z$="01O AP4...  05O AP8...  10O AP0...  13O ZZPARM  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
0300 ! iolist
0310 IOLIST AP4$,AP4{ALL}
0320 IOLIST AP8$
0400 ! get system parameters
0410 GOSUB GET_PARM
0420 %TEMP$=""
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 GOSUB BUILD_OPTIONS
0570 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0930 %VENDOR$=""
0940 %VEND_NAME$=""
0970 RETURN 
1000 ! Process Vendor
1010 PROCESS_VENDOR:
1012 IF STP(VENDOR$)="" THEN VENDOR$=VENDOR.CTL'VALUE$ ! SSP242471 jdf
1015 IF NUL(VENDOR$) THEN RETURN 
1020 CALL "ZZWLKU;parse_vend",VENDOR$,VEND_DIV$,VEND_CODE$
1030 VENDOR$=VEND_DIV$+VEND_CODE$
1040 READ (Z[1],KEY=VENDOR$,DOM=NEW_VENDOR)IOL=0310
1050 VEND_NAME$=AP4$(11,35)
1060 CALL "*wingrp;enable",GRID.GRP$; CALL "*wingrp;Disable",TMP.GRP$
1070 %VENDOR$=VENDOR$; VEND_NAME$=VEND_NAME$
1075 GOSUB VENDOR_MESSAGE
1080 REFRESH_FLG=1
1090 RETURN 
2000 ! new vendor
2010 NEW_VENDOR:
2030 MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"YesNo",ANS$
2040 IF ANS$="NO" THEN VENDOR$="",NEXT_ID=VENDOR.CTL,REFRESH_FLG=1; RETURN ! SSP242471 jdf
2045 %VENDOR$=VENDOR$
2047 IF SEC$(46,1)="Y" THEN MSGBOX MSG("NO_NEW_VEN"),MSG("ACCESS_DEN"),"STOP"; NEXT_ID=VENDOR.CTL; RETURN 
2050 CALL "APGMAB"
2055 CALL "*wingrp;enable",GRID.GRP$; CALL "*wingrp;Disable",TMP.GRP$
2056 IF VENDOR$<>MID(AP4$,1,10) THEN DIM AP4$(427),AP4[2]; CALL "*wingrp;disable",GRID.GRP$ ! SSP242471 jdf
2057 REFRESH_FLG=1
2060 RETURN 
3000 ! vendor message
3010 VENDOR_MESSAGE:
3020 FIND (Z[5],KEY=VENDOR$,DOM=*RETURN)IOL=0320
3025 IF NUL(AP8$(371,120)) THEN RETURN 
3030 MSG_1$=AP8$(371,60)+SEP+AP8$(431,60)
3040 MSGBOX MSG_1$,"Vendor Message"
3050 RETURN 
5000 ! Build options on the Vendor Screen
5010 BUILD_OPTIONS:
5020 MAX_OPT=1
5030 Q$[MAX_OPT]=MSG("VENDOR")+" Constant Data",P$[MAX_OPT]="APGMAB",MAX_OPT+=1 ! SSP#227949
5040 Q$[MAX_OPT]="Current Status Inquiry",P$[MAX_OPT]="APGMAC",MAX_OPT+=1
5050 Q$[MAX_OPT]="Open Invoice Inquiry",P$[MAX_OPT]="APGMAI",MAX_OPT+=1
5055 Q$[MAX_OPT]="Open P/O Inquiry",P$[MAX_OPT]="APGOPA",MAX_OPT+=1
5060 Q$[MAX_OPT]="Invoice History Inquiry",P$[MAX_OPT]="APGMAJ",MAX_OPT+=1
5070 Q$[MAX_OPT]="Statistical Data Inquiry",P$[MAX_OPT]="APGMDA",MAX_OPT+=1
5080 IF F0$(70,1)="Y" THEN Q$[MAX_OPT]="Salesperson Statistical Data Inquiry",P$[MAX_OPT]="APGMDS",MAX_OPT+=1
5090 IF F0$(67,1)="Y" THEN Q$[MAX_OPT]=MSG("VENDOR")+" Message Setup",P$[MAX_OPT]="APGMAD",MAX_OPT+=1 ! SSP#227949
5100 Q$[MAX_OPT]=MSG("VENDOR")+" Plant Setup/Inquiry",P$[MAX_OPT]="FMGSKA",MAX_OPT+=1 ! SSP#227949
5110 Q$[MAX_OPT]="Delete/Reactivate",P$[MAX_OPT]="DELETE",MAX_OPT+=1
5120 GOSUB CREATE_GRID
5130 REFRESH_FLG=1
5140 RETURN 
5500 ! Clear the Screen
5510 CLEAR_REC:
5520 VENDOR$=""
5530 VEND_NAME$=""
5535 DIM AP4$(427),AP4[2] ! SSP242471 jdf
5540 CALL "*wingrp;Disable",GRID.GRP$; CALL "*wingrp;enable",TMP.GRP$
5570 NEXT_ID=VENDOR.CTL
5580 REFRESH_FLG=1
5590 RETURN 
6000 ! Inactivate of Reactivate Vendor...
6010 DELETE_VENDOR:
6020 IF SEC$(46,1)="Y" THEN MSGBOX "Access Denied!  You may not delete a vendor","FYI","INFO,OK"; RETURN 
6030 IF X3$(330,1)="1" AND POS(VENDOR$(1,2)="0001")>0 THEN {
6040 MSG_1$="You may not delete this Vendor (RL)"
6050 MSGBOX MSG_1$,"FYI","INFO,OK"
6060 RETURN  }
6065 IF MID(AP4$,246,1)="I" THEN DEL_DESC$="Re-activate" ELSE DEL_DESC$="Delete" ! SSP#219953
6070 MSG_1$="Do you wish to "+DEL_DESC$+" this vendor?"
6080 MSGBOX MSG_1$,MSG("CONFIRM"),"?,YesNo",ANS$
6090 IF ANS$="NO" AND MID(AP4$,246,1)<>"I" THEN RETURN ! SSP#219953
6100 ! Yes to delete
6110 IF ANS$="YES" THEN {
6120 IF MID(AP4$,246,1)="I" THEN AP4$(246,1)="A"; WRITE (Z[1],KEY=VENDOR$)IOL=0310; IF POS(X3$(30,1)="M")>0 AND POS(VENDOR$(1,2)="0001",2)>0 THEN CALL "CM2AAA",X3$,Z[1],1,VENDOR$,"",X9; MSGBOX "Record Update!","FYI","INFO,OK"; RETURN ELSE RETURN ! SSP#219953
6130  }
6140 Q2$="",Q0$="A/P",Q1$=VENDOR$,Q3$="M"
6150 IF POS(X3$(30,1)="M")>0 THEN IF POS(VENDOR$(1,2)="0001",2)>0 THEN MSG_1$="Record set to Inactivate for Later Removal."; GOTO 6190
6155 ! SSP#219953
6158 READ (Z[1],KEY=VENDOR$,DOM=6170)AP4$ ! SSP#219953!SSP#271673
6160 CALL "AP2UAB",ERR=*NEXT,X3$,X4$,Q0$,Q1$,Q2$,Q3$
6170 IF Q2$="Y" THEN MSG_1$="Record Deleted!"; GOTO 6220
6180 MSG_1$="Activity Detected. "+Q2$+" Record set to Inactive for Later Removal." ! SSP#240339
6190 IF X3$(30,1)="M" THEN IF POS(A1$(1,2)="0001",2)>0 THEN GOSUB CHANGE_PROFORMA
6200 AP4$(246,1)="I"; WRITE (Z[1],KEY=VENDOR$)IOL=0310
6210 IF POS(X3$(30,1)="M")>0 THEN IF POS(VENDOR$(1,2)="0001",2)>0 THEN CALL "CM2AAA",X3$,Z[1],1,VENDOR$,"",X9
6220 MSGBOX MSG_1$,"FYI","INFO,OK"
6225 GOSUB CLEAR_REC ! SSP#219953
6230 RETURN 
6400 ! Change lookup code for Performa Vendors
6410 CHANGE_PROFORMA:
6420 Q$=AP4$(145,10)+AP4$(1,10)
6430 CLOSE (14); OPEN (14)"AP6"+X3$(9,3); REMOVE (14,KEY=Q$,DOM=*NEXT)
6440 AP4$(145,10)="\"+AP4$(45,10); WRITE (14,KEY=Q$)
6450 RETURN 
9000 ! get system parameters
9010 GET_PARM:
9020 DIM SEC$(255); READ (Z[13],KEY="osec"+X3$(40,3),DOM=*NEXT)SEC$
9030 READ (Z[13],KEY=X3$(9,3)+"A/P",DOM=WRAPUP)F0$
9090 RETURN 
9999 END 
40000 ! !40000,10
40010 SEL_GRID:
40012 REFRESH_FLG=1
40020 GRID SELECT READ X,COL,ROW
40030 GRID FIND X,COL,ROW,VAL$
40035 IF STP(VAL$)="" THEN RETURN 
40036 IF AROW+ACOL<>0 THEN X'COLUMN=ACOL,X'ROW=AROW,X'TEXTCOLOR$="Black",X'BACKCOLOR$="Gray",X'BITMAP$=""
40038 X'COLUMN=COL,X'ROW=ROW,X'TEXTCOLOR$="Light Blue",X'BITMAP$="!Arrow_Right" ! ,X'BACKCOLOR$="White"
40040 AROW=ROW,ACOL=COL
40045 OPT=INT(POS(STP(VAL$)=ALL_MESS$)/40)+1; %GRID=0; GOSUB PROCESS_OPT; GRID GOTO X,COL,ROW; %GRID=1; RETURN 
40050 CREATE_GRID:
40060 GOSUB CREATE_OPT
40070 X=X.CTL,BOPT$=OPT$,ROW=1,COLUMN=0; GRID DELETE X,-1,-1
40080 HIDE CONTROL X
40082 HI=OPT_LN/3; IF FPT(HI)<>0 THEN HI=INT(HI)+1
40085 ! GRID REMOVE X; GRID X,@(2,9.6,75,HI)
40090 XX=POS(SEP=OPT$); IF XX=0 THEN GOTO END_LOAD
40100 COLUMN=COLUMN+1; IF COLUMN>3 THEN COLUMN=1,ROW=ROW+1; GRID ADD X,0,ROW; GRID LOCK X,0,ROW
40110 TX$=OPT$(1,XX-1),OPT$=OPT$(XX+1),X'ROW=ROW,X'COLUMN=COLUMN,X'ALIGN$="L",X'COLUMNWIDTH=25,X'VALUE$=TX$,X'BACKCOLOR$="Gray",X'CELLTYPE$="Button",X'LOCK=1,X'ROWHEIGHT=1.6
40120 ! GRID LOAD X,ROW,COLUMN,TX$
40130 GOTO 40090
40140 END_LOAD: HIG=HI*X'HEIGHT/5,X'HEIGHT=HIG; SHOW CONTROL X; RETURN 
40150 CREATE_OPT:
40160 SE$="123456789ABCDEFG"
40170 FOR A=1 TO MAX_OPT-1
40182 MESS$=STP(Q$[A]),MESS$=PAD(MESS$,40," "),ALL_MESS$=ALL_MESS$+MESS$
40183 IF A=2 THEN MESS$=MESS$+"       "
40190 OPT$=OPT$+STP(MESS$)+SEP,OPT_LN=OPT_LN+1
40191 NXT_OPT:
40195 NEXT A
40200 RETURN 
42000 PROCESS_OPT:
42005 IF P$[OPT]="DELETE" THEN GOSUB DELETE_VENDOR; RETURN 
42010 %SAVE_VENDOR$=%VENDOR$; CALL P$[OPT]; %VENDOR$=%SAVE_VENDOR$,%SAVE_VENDOR$="" ! SSP#237483
42020 RETURN 
50000 ! 50000 - Browsing mode
50010 ! - Next
50020 NEXT_REC:
50040 VENDOR$=KEY(Z(1),END=END_OF_FILE)
50050 READ (Z(1),KEY=VENDOR$,ERR=*NEXT); GOSUB PROCESS_VENDOR; GOTO DISP_REC
50060 CHK_ERR_NXT:IF ERR<>0 THEN EXIT ERR
50070 _D$=_MSG_NEXT$; GOSUB BUSY_CHK
50080 VENDOR$=KEN(Z(1),KEY=VENDOR$,END=END_OF_FILE); READ (Z(1),KEY=VENDOR$,ERR=CHK_ERR_NXT); GOSUB PROCESS_VENDOR; GOTO DISP_REC
50090 END_OF_FILE: MSGBOX "End of File","Eof","!"; EXIT 
50100 ! 2200 - Prior record
50110 PRIOR_REC:
50130 VENDOR$=KEP(Z(1),END=START_OF_FILE); READ (Z(1),KEY=VENDOR$,ERR=*NEXT); GOSUB PROCESS_VENDOR; GOTO DISP_REC
50140 CHK_ERR_PRE:IF ERR<>0 THEN EXIT ERR
50150 _D$=_MSG_PRECEDING$; GOSUB BUSY_CHK
50160 VENDOR$=KEP(Z(1),KEY=VENDOR$,END=START_OF_FILE); READ (Z(1),KEY=VENDOR$,ERR=CHK_ERR_PRE); GOSUB PROCESS_VENDOR; GOTO DISP_REC
50170 START_OF_FILE: MSGBOX "Start of File","Info","!,tim=3"; EXIT 
50180 ! 2400 - First record
50190 FIRST_REC:
50210 VENDOR$=KEF(Z(1),ERR=NO_FIRST)
50220 READ (Z(1),KEY=VENDOR$,ERR=CHK_ERR_NXT); GOSUB PROCESS_VENDOR; GOTO DISP_REC
50230 NO_FIRST: MSGBOX _MSG_REC_NO_1ST$,_FYI$,"!"; EXIT 
50240 ! 2600 - Last record
50250 LAST_REC:
50270 VENDOR$=KEL(Z(1),ERR=NO_LAST)
50280 READ (Z(1),KEY=VENDOR$,ERR=CHK_ERR_PRE); GOSUB PROCESS_VENDOR; GOTO DISP_REC
50290 NO_LAST: MSGBOX _MSG_REC_NO_LST$,_FYI$,"!"; EXIT 
50300 BUSY_CHK:
50310 MSGBOX _MSG_REC_LOCKED$+SEP+SEP+_MSG_REC_VIEW1$+_D$+_MSG_REC_VIEW2$,_MSG_REC_ACCESS$,"YESNO,!",_D$
50320 IF _D$="NO" THEN EXIT 
50330 RETURN 
50340 DISP_REC:RETURN 
56001 REM "219953-Program APGMAA. Setup a new vendor. Delete the vendor before
56002 REM "237483-When A/P vendor setup constant data, then go to the plant   
56003 REM "240339-Request from ssp to add the list of where activity detected 
56004 REM "242471-TEMP in Vendor doesn't appear to work                       
56005 REM "263584-Grid size issues Upgrade from Pvx 7.1 to PxPlus 2017        
