0010 ! APGMAB - File Maintenance Program for AP4-Vendor Masterfile (="AP4"+%C$)
0020 ! Generated by ProvideX® NOMADS II File Maintenance on Sep. 15, 2001 12:24
0030 ! ************************************************************************
0035 REM "5.7 - 10/17/22 - 12.462593 - tma - SSP# 307422
0037 REM "307422-DBSPT-149953 Increase DIM for array from 50 to 100 APGMAB   
0040 REM "Copyright 2022 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 _VENDOR$=%VENDOR$
0060 PROCESS "APGMAB","../APG/AP.EN"
0070 %VENDOR$=_VENDOR$
0080 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 ! 
0130 X0$="AP2MAB",X1$="Vendor Constant Data"
0140 X0=-1,X2=-1
0150 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2; IF X1>0 THEN CMD_STR$="END"; EXIT 
0160 ! 
0190 DIM Z[NUM(X3$(60,3))],AP4$(426),AP4[2],B_AP4[2],ASV[3]
0200 ! Files
0210 Z$="01X AP4...  02O GL1...  03O ZYB...  04O AP2...  05O AP3...  20O AP1...  06O ZZPARM  07O AP6...  08O AP7...  09O AP1...  10O AP0...  11O ASV...  12O APB...  21O APF...  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; IF Z0>0 THEN CMD_STR$="END"; RETURN 
0300 ! IOList
0310 IOLIST AP4$,AP4{ALL}
0380 IOLIST ASV$,ASV{ALL}
0400 ! Get system Parameters...
0410 GOSUB GET_PARAM
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0515 PRINT 'CI', ! SSP#229836
0520 IF %VENDOR$<>"" THEN {
0530 VENDOR$=%VENDOR$
0540 DISABLE CONTROL VENDOR.CTL
0545 GOSUB CHECK_PARAM
0547 CALL "*wingrp;hide",BUTTON_1.GRP$
0550 GOTO FIND_REC }
0560 NEXT_ID=VENDOR.CTL
0570 GOSUB CHECK_PARAM
0790 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0930 %TEMP$=""
0970 RETURN 
1000 ! find Vendor record
1010 FIND_REC:
1020 KEY_1$=VENDOR$
1030 READ (Z[1],KEY=KEY_1$,DOM=NEW_VENDOR)IOL=0310
1040 GOSUB LOAD_SCREEN
1050 B_AP4$=AP4$,B_AP4[0]=AP4[0],B_AP4[1]=AP4[1],B_AP4[2]=AP4[2]
1080 CALL "*wingrp;Enable",BUTTONS.GRP$
1090 RETURN 
1500 ! load vendor screen
1510 LOAD_SCREEN:
1520 ! First Folder
1530 VEND_ADDR_1$=AP4$(46,30); VEND_ADDR_2$=AP4$(76,30)
1540 VEND_CITY$=AP4$(106,16); VEND_ST$=AP4$(122,2),VEND_ZIP$=AP4$(124,9)
1550 VEND_FAX$=AP4$(133,12); VEND_PHONE$=AP4$(195,14)
1555 VEND_COUNTRY$=AP4$(424,2) ! SSP 307210
1560 VEND_LOOKUP$=AP4$(145,10); VEND_CONT_PERSON$=AP4$(155,20); VEND_GREETING$=AP4$(175,20)
1570 CARTON_LABELS$=AP4$(232,1); DIRECT_SHIP$=AP4$(233,1); SHIP_TO_WHSE$=AP4$(234,1)
1600 ! Second Folder
1610 FILE_OUTPUT_FORMAT$=AP4$(221,2); SPC$=AP4$(223,8); PRINT_PO$=AP4$(231,1); REM SSP 214091 
1620 VEND_STAT$=AP4$(246,1); VEND_CATEGORY$=AP4$(247,9)
1630 STND_INV_COMMENT$=AP4$(256,15); GL_ACCT$=AP4$(271,12)
1640 BANK_CODE$=AP4$(283,3); TERMS_CODE$=AP4$(286,2); PYMNT_PRIORITY_CODE$=AP4$(290,1)
1650 PYMNT_SELECTION_CODE$=AP4$(288,2); VEND_ON_HOLD$=AP4$(291,1)
1660 ALWAYS_TAKE_DISC$=AP4$(293,1); EXC_SPCL_AMT_IN_DISC$=AP4$(294,6)
1670 MAILING_LABEL_TYPE_1$=AP4$(300,3); MAILING_LABEL_TYPE_2$=AP4$(303,3); MAILING_LABEL_TYPE_3$=AP4$(306,3); DEF_DAYS$=AP4$(364,3) ! SSP223467 jdf
1680 OUR_ACCT_NUM$=AP4$(309,15); WEB_ADDRESS$=AP4$(324,40)
1690 STND_AMT=AP4[0]; EXTRA_DUE_DAYS=AP4[1]
1700 INTERCOMPANY_GL_ACCT$=AP4$(209,12)
1705 JOB_CARD_OVERRIDE$=AP4$(367,15)
1710 CUST_PO_TP$=AP4$(402,1); REM SSP 198988
1715 XML_PO_OPT$=AP4$(382,20) ! [193783]
1716 COMM_SUB_CODE$=AP4$(235,1) ! SSP 217566
1720 VEND_RATING$=AP4$(236,6),VEND_MWDB$=AP4$(242,1),VEND_GREEN$=AP4$(243,1) ! WO237937
1725 EC_PRINT_FLAGS$=AP4$(413,5),EC_PRINT_FLAGS_1$=AP4$(413,5),EC_WHSE_CODE$=AP4$(418,4) ! WO253441
1730 XML_ITEM_SPECS$=AP4$(422,1),XML_SPECIAL_SHIP$=AP4$(423,1) ! 259010
1780 REFRESH_FLG=1
1790 RETURN 
2000 ! Load the information
2010 LOAD_INFO:
2020 AP4$(1,10)=VENDOR$; AP4$(11,35)=%VEND_NAME$
2030 AP4$(46,30)=VEND_ADDR_1$; AP4$(76,30)=VEND_ADDR_2$
2040 AP4$(106,16)=VEND_CITY$; AP4$(122,2)=VEND_ST$,AP4$(124,9)=VEND_ZIP$
2045 AP4$(424,2)=VEND_COUNTRY$ ! SSP 307210
2050 AP4$(133,12)=VEND_FAX$; AP4$(195,14)=VEND_PHONE$
2060 AP4$(145,10)=VEND_LOOKUP$; AP4$(155,20)=VEND_CONT_PERSON$; AP4$(175,20)=VEND_GREETING$
2070 AP4$(221,2)=FILE_OUTPUT_FORMAT$; AP4$(223,8)=SPC$; AP4$(231,1)=PRINT_PO$; REM SSP 214091
2075 AP4$(232,1)=CARTON_LABELS$; AP4$(233,1)=DIRECT_SHIP$; AP4$(234,1)=SHIP_TO_WHSE$
2080 AP4$(246,1)=VEND_STAT$; AP4$(247,9)=VEND_CATEGORY$
2090 AP4$(256,15)=STND_INV_COMMENT$; AP4$(271,12)=GL_ACCT$
2100 AP4$(283,3)=BANK_CODE$; AP4$(286,2)=TERMS_CODE$; AP4$(290,1)=PYMNT_PRIORITY_CODE$
2110 AP4$(288,2)=PYMNT_SELECTION_CODE$; AP4$(291,1)=VEND_ON_HOLD$
2120 AP4$(293,1)=ALWAYS_TAKE_DISC$; AP4$(294,6)=EXC_SPCL_AMT_IN_DISC$
2130 AP4$(300,3)=MAILING_LABEL_TYPE_1$; AP4$(303,3)=MAILING_LABEL_TYPE_2$; AP4$(306,3)=MAILING_LABEL_TYPE_3$; AP4$(364,3)=DEF_DAYS$ ! SSP223467 jdf
2140 AP4$(309,15)=OUR_ACCT_NUM$; AP4$(324,40)=WEB_ADDRESS$
2150 AP4[0]=STND_AMT; AP4[1]=EXTRA_DUE_DAYS
2160 AP4$(209,12)=INTERCOMPANY_GL_ACCT$
2162 AP4$(402,1)=CUST_PO_TP$; REM SSP 198988
2165 AP4$(367,15)=JOB_CARD_OVERRIDE$
2170 AP4$(382,20)=XML_PO_OPT$ ! [193783]
2175 AP4$(235,1)=COMM_SUB_CODE$ ! SSP 217566
2180 AP4$(422,1)=XML_ITEM_SPECS$,AP4$(423,1)=XML_SPECIAL_SHIP$ ! 259010
2185 AP4$(413,5)=EC_PRINT_FLAGS$,EC_PRINT_FLAGS_1$=EC_PRINT_FLAGS$,AP4$(418,4)=EC_WHSE_CODE$ ! WO253441
2187 AP4$(236,6)=VEND_RATING$,AP4$(242,1)=VEND_MWDB$,AP4$(243,1)=VEND_GREEN$ ! SSP#265701
2290 RETURN 
3000 ! Write out the Record...
3010 WRITE_REC:
3015 IF NUL(TERMS_CODE$)<>0 OR NUL(VEND_CATEGORY$)<>0 OR NUL(BANK_CODE$)<>0 THEN GOSUB MANDATORY_FIELDS; RETURN ! [202367]
3020 MSGBOX MSG("UPDATE_REC"),MSG("CONFIRM"),"?,YESNO",ANS$
3030 IF ANS$="NO" THEN RETURN 
3040 WRITE (Z[1],KEY=KEY_1$)IOL=0310
3050 GOSUB WRITE_SORT_FILE_CHUI
3080 B_AP4$=AP4$; B_AP4[0]=AP4[0]; B_AP4[1]=AP4[1]
3090 RETURN 
4000 ! Delete the Record
4010 DELETE_REC:
4020 MSGBOX MSG("DELETE_REC"),MSG("VERIFY"),"?,YesNo",ANS$
4030 IF ANS$="NO" THEN RETURN 
4040 REMOVE (Z[1],KEY=KEY_1$)
4190 RETURN 
5000 ! Check Changes
5010 CHECK_CHANGES:
5015 IF NUL(TERMS_CODE$)<>0 OR NUL(VEND_CATEGORY$)<>0 OR NUL(BANK_CODE$)<>0 THEN GOSUB MANDATORY_FIELDS; RETURN ! [202367]
5020 IF B_AP4$=AP4$ AND B_AP4[0]=AP4[0] AND B_AP4[1]=AP4[1] THEN RETURN 
5030 MSGBOX MSG("REC_ALTERD"),MSG("UPDATE"),"?,YESNO",ANS$
5040 IF ANS$="NO" THEN CMD_STR$="END"; RETURN 
5060 GOSUB WRITE_REC
5090 RETURN 
8000 ! Setup New Information...
8010 NEW_VENDOR:
8020 DIM AP4$(427)
8030 NEXT_ID=%VEND_NAME.CTL
8040 BANK_CODE$=F0$(17,3); IF %TEMP$="*" THEN VEND_STAT$="T"; CALL "*wingrp;Disable",V_STAT.GRP$ ELSE VEND_STAT$="A"
8050 PYMNT_PRIORITY_CODE$="G"; PYMNT_SELECTION_CODE$="00"
8060 VEND_ON_HOLD$="N"
8065 IF %TEMP$="*" THEN VEND_LOOKUP$="|TEMPORARY"; CALL "*WINGRP;DISABLE",LOOKUP.GRP$
8070 CALL "*wingrp;Enable",BUTTONS.GRP$
8080 REFRESH_FLG=1
8085 CHANGE_FLG=0,NEXT_FOLDER=FLDR.APGMAB.1.CTL,NEXT_ID=VEND_ADDR_1.CTL,%GRID=0 ! SSP#229836
8090 RETURN 
8300 ! Mandatory Fields
8310 MANDATORY_FIELDS:
8320 IF NUL(TERMS_CODE$) THEN MSGBOX MSG("TERM_C_M")+SEP+MSG("CANN_U_R"),MSG("FYI"),"!"; RETURN 
8330 IF NUL(VEND_CATEGORY$) THEN MSGBOX MSG("VEND_C_M")+SEP+MSG("CANN_U_R"),MSG("FYI"),"!"; RETURN 
8340 IF NUL(BANK_CODE$) THEN MSGBOX MSG("BANK_CODE")+SEP+MSG("CANN_U_R"),MSG("FYI"),"!"; RETURN ! [202367]
8390 RETURN 
9000 ! Get all system parameters....
9010 GET_PARAM:
9020 DIM SEC$(255); FIND (Z[6],KEY="osec"+X3$(40,3),DOM=*NEXT)SEC$
9030 FIND (Z[6],KEY=X3$(9,3)+"A/P")F0$
9040 READ (Z[6],KEY=X3$(9,3)+"AP2A",DOM=*NEXT)F1$
9070 ! IF SEC$(46,1)="Y" THEN MSGBOX MSG("NO_NEW_VEN"),MSG("ACCESS_DEN"),"STOP"; CMD_STR$="END"; EXIT
9080 REFRESH_FLG=1
9090 RETURN 
9100 ! check parameters
9110 CHECK_PARAM:
9120 IF POS(X3$(9,3)="500538118130",3)>0 THEN CALL "*wingrp;Show",FILE_OUT.GRP$
9130 IF F0$(16,1)<>"Y" THEN CALL "*wingrp;Hide",BANK_C.GRP$
9140 IF F0$(66,1)<>"Y" THEN CALL "*wingrp;Hide",DISC.GRP$
9150 IF F0$(78,1)<>"Y" OR SEC$(62,1)<>"Y" THEN CALL "*wingrp;Hide",INTER.GRP$
9190 RETURN 
9999 END 
21040 ! 
21100 LOAD_GRID:! ^100,5 - Load user defined fields and vendor data
21105 _CHANGE_FLG=CHANGE_FLG
21110 GRID CLEAR USER_GRID.CTL,0,0; _ROW=0; DIM ROW_ARRAY[100],ASV_ARRAY$[100] ! ssp 196604!SSP#307422 DBSPT-149953
21115 GRID_ROWS=NUM(FIN(Z[11],"NUMREC"))
21120 IF NOT(GRID_ROWS) THEN {
21125 NEXT_FOLDER=FLDR.APGMAB.1.CTL,REFRESH_FLG=1,CHANGE_FLG=0
21130 RETURN 
21135  }
21140 USER_GRID.CTL'COLUMN=-1,USER_GRID.CTL'COLUMNWIDTH=0
21145 USER_GRID.CTL'ROW=-1,USER_GRID.CTL'ROWHEIGHT=0
21150 USER_GRID.CTL'ROWSHIGH=GRID_ROWS
21155 USER_GRID.CTL'COLUMNSWIDE=2
21160 GRID LOCK USER_GRID.CTL,1,0
21165 USER_GRID.CTL'COLUMN=2
21170 USER_GRID.CTL'COLUMNWIDTH=51
21175 USER_GRID.CTL'COLUMN=1
21180 USER_GRID.CTL'COLUMNWIDTH=25
21183 READ (Z[11],KEY="V",DOM=*NEXT) ! ssp 196604
21185 KEY_ROW$=KEY(Z[11],END=21250); READ (Z[11],KEY=KEY_ROW$)ASV$,FLD_LEN,FLD_DESC_LEN ! ssp 196604
21187 IF NUL(ASV$) THEN GOTO 21185
21190 USER_GRID.CTL'COLUMN=1
21197 IF ASV$(1,1)<>"V" THEN GOTO 21185
21198 _ROW+=1; ROW_ARRAY[_ROW]=ROW,ASV_ARRAY$[_ROW]=ASV$ ! ssp 196604
21200 USER_GRID.CTL'ROW=_ROW,USER_GRID.CTL'VALUE$=STP(ASV$(6,75),2)+":",USER_GRID.CTL'CELLTYPE$="ellipsis",USER_GRID.CTL'ALIGN$="Right"
21205 APB$=""
21210 READ (Z[12],KEY=VENDOR$+ASV$(2,4),DOM=21245)APB$
21220 IF STP(APB$,2)<>"" THEN FIELD_DATA$=APB$(15,75)
21225 IF STP(FIELD_DATA$,2)<>"" THEN {
21230 USER_GRID.CTL'COLUMN=2,USER_GRID.CTL'LEN=FLD_LEN ! ssp 196604
21235 USER_GRID.CTL'VALUE$=STP(FIELD_DATA$,2),USER_GRID.CTL'CELLTYPE$="ellipsis"
21240  }
21245 GOTO 21185
21250 USER_GRID.CTL'ROWSHIGH=_ROW
21255 RETURN 
21260 ! 
21300 UPDATE_FIELD:! ^100,5 - User defined field has been accessed
21305 EDIT_ROW=USER_GRID.ROW ! SSP218945 jdf         
21310 GRID FIND USER_GRID.CTL,2,EDIT_ROW,FIELD_DATA$
21315 ASV$=ASV_ARRAY$[EDIT_ROW]; GOTO 21352 ! READ (Z[11],IND=ROW_ARRAY[EDIT_ROW])IOL=0380 ! ssp 196604
21317 FIELD_LENGTH=ASV[0]
21320 IF LEN(FIELD_DATA$)>FIELD_LENGTH THEN {
21325 MSGBOX MSG("MAX_LEN",STR(FIELD_LENGTH)),MSG("ATTN"),"!"
21330 FIELD_DATA$=PAD(FIELD_DATA$,FIELD_LENGTH)
21335 USER_GRID.CTL'COLUMN=2
21340 USER_GRID.CTL'ROW=EDIT_ROW
21345 USER_GRID.CTL'VALUE$=FIELD_DATA$
21350  }
21352 DIM APB$(126); APB$(1,10)=VENDOR$; APB$(11,4)=ASV$(2,4); APB$(15,75)=FIELD_DATA$
21355 WRITE (Z[12],KEY=VENDOR$+ASV$(2,4))APB$
21358 WRITE (Z(21),KEY=APB$(11,4)+APB$(1,10)) ! SSP172947
21360 REFRESH_FLG=1,NEXT_ID=USER_GRID.CTL
21362 TAB_GRID: ARROW_EDIT_ROW=EDIT_ROW; EDIT_ROW=USER_GRID.CTL'CURRENTROW; IF USER_GRID.CTL'CURRENTROW>=ARROW_EDIT_ROW THEN EDIT_ROW+=1 ! SSP218945 jdf
21363 IF EDIT_ROW-ARROW_EDIT_ROW=2 THEN RETURN ! SSP218945 jdf
21364 IF EDIT_ROW>_ROW THEN %GRID=0 ELSE GRID GOTO USER_GRID.CTL,2,EDIT_ROW
21365 RETURN 
21370 ! 
21400 LOOKUP_ZIP:! Lookup zipcode, set city & state if found, set NEW_ZIP for later use
21405 NEW_ZIP=0
21410 CALL "ZYGMBA;CHECK_ZIP",VEND_ZIP$,ZIP_FOUND,ZIP_CITY$,ZIP_ST$,"",ZIP_AREA_CODE$,""
21415 IF LEN(STP(VEND_PHONE$))<7 THEN VEND_PHONE$=PAD(VEND_PHONE$,14," ") ! SSP 191185
21420 IF ZIP_FOUND THEN VEND_CITY$=ZIP_CITY$,VEND_ST$=ZIP_ST$; REFRESH_FLG=1,NEXT_ID=VEND_PHONE.CTL ELSE NEW_ZIP=1
21430 IF VEND_PHONE$(1,3)="   " THEN VEND_PHONE$(1,3)=ZIP_AREA_CODE$ ! SSP 191185
21495 RETURN 
21499 ! 
21500 UPDATE_ZIP:! If new_zip then update the zip code table
21505 IF NUL(VEND_PHONE$) THEN DIM VEND_PHONE$(10)
21510 IF NEW_ZIP THEN CALL "ZYGMBA;UPDATE_ZIP",VEND_ZIP$,VEND_CITY$,VEND_ST$,"",VEND_PHONE$(1,3),VEND_CITY$ ! get area code from phone,long city from regular city
21520 NEW_ZIP=0
21595 RETURN 
21599 ! 
22000 WRITE_SORT_FILE_CHUI:
22010 REMOVE (Z(7),KEY=MID(B_AP4$,145,10)+MID(B_AP4$,1,10),DOM=*NEXT)
22020 REMOVE (Z(8),KEY=MID(B_AP4$,247,9)+MID(B_AP4$,1,10),DOM=*NEXT)
22030 IF POS(%X3$(9,3)="060338363364365366476",3)<>0 THEN REMOVE (Z[20],KEY=MID(B_AP4$,195,12)+MID(B_AP4$,1,10),DOM=*NEXT) ELSE REMOVE (Z[20],KEY=MID(B_AP4$,195,10)+MID(B_AP4$,1,10),ERR=*NEXT)
22040 WRITE (Z(7),KEY=AP4$(145,10)+AP4$(1,10))
22050 WRITE (Z(8),KEY=AP4$(247,9)+AP4$(1,10))
22060 IF POS(%X3$(9,3)="060338363364365366476",3)<>0 THEN WRITE (Z[20],KEY=AP4$(195,12)+AP4$(1,10)) ELSE WRITE (Z[20],KEY=AP4$(195,10)+AP4$(1,10))
22070 RETURN 
22100 FORMAT_VEND_RATING:! WO237937
22110 IF NUM(VEND_RATING$,ERR=BAD_RATING_DATA)=0 THEN VEND_RATING$=DIM(6); GOTO DONE_VEND_RATING
22115 IF NUM(VEND_RATING$)>100 THEN MSGBOX MSG("VEND_RT_MX"),MSG("FYI"),"!"; VEND_RATING$=DIM(6),NEXT_ID=VEND_RATING.CTL; GOTO DONE_VEND_RATING
22125 VEND_RATING$=STR(NUM(VEND_RATING$):"###.00")
22130 DONE_VEND_RATING:
22135 REFRESH_FLG=1
22140 RETURN 
22145 ! 
22150 BAD_RATING_DATA:! Must be numeric, field set as such on panel, but just in case
22160 VEND_RATING$=DIM(6)
22165 NEXT_ID=VEND_RATING.CTL
22170 REFRESH_FLG=1
22190 RETURN 
22195 ! 
56000 REM "186670-Add ability to output purchase orders by a Vendor Set       
56001 ! "196604-User defined fields for vendor (APGMAB - tab 3) don't allow 
56002 ! "       the full length set in the user defined fields screen.
56003 REM 198988-Manage Customer Supplied Inventory. Item Setup, Purch ord
56006 REM "193783-189102;Added new field, options, to Vendor Setup, AP4 file  
56008 REM "202367- APGMAB.2 - Vendor Setup - This does not require you to enter
56009 REM "214091-Email from Brenda requesting ability to have another  
56010 REM "229836-When setting up a new vendor, you are not being positioned   at the vendor name
56012 REM "237937-Add vendor rating (max 100.00), MWDB? Y/N, Green? Y/N fields
56014 REM "253441-Add Vendor fields Print Flags and Whse for PL printing.     
56016 REM "259010-Ability to export item specifications
56017 REM "265701-APGMAB-I am not able to save changes to the following fields vendor rating, Minority/woman/disabled, Green
56018 REM "307210-International shipping address - add country code           
56019 REM "307422-DBSPT-149953 Increase DIM for array from 50 to 100 APGMAB   
