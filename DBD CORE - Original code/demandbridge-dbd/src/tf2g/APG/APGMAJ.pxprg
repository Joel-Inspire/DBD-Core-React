0010 ! APGMAJ - A/P Invoice History Inquiry
0035 REM "5.7 - 08/20/14 - 17.868888 - crg - SSP# 272099
0037 REM "272099-Vendor ZZ10 Image Library issues                            
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 _VENDOR$=%VENDOR$
0060 PROCESS "APGMAJ","../APG/AP.EN"
0070 %VENDOR$=_VENDOR$
0080 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 ! 
0130 X0$="APGMAJ"
0140 X0=-1; X2=-1
0150 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
0160 ! 
0170 ! Dim Variables
0180 DIM Z[NUM(X3$(60,3))],APQ[27],AP4[2],APJ[2],APK[7]
0190 ! 
0200 ! Open Files
0210 Z$="01O APQ...  02O AP4...  04O APJ...  05O GL1...  06O ZZPARM  07O APK...  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
0300 ! Iolist Section
0310 IOLIST APQ$,APQ{ALL}
0315 RANGE_IOL:IOLIST START_INV$:[LEN(10,SEP=SEP)],END_INV$:[LEN(10,SEP=SEP)],START_DATE$:[LEN(6,SEP=SEP)],END_DATE$:[LEN(6,SEP=SEP)],START_PO$:[LEN(15,SEP=SEP)],END_PO$:[LEN(10,SEP=SEP)] ! SSP 200464
0320 IOLIST AP4$,AP4{ALL}
0330 IOLIST IDX1,APJ$,APJ{ALL}
0340 IOLIST APJ$,APJ{ALL}
0350 IOLIST IDX2,APK$,APK{ALL}
0360 DIM R$:IOL=RANGE_IOL ! SSP 200464
0370 R0=-99999999,R1=99999999; GOSUB GET_DATE ! SSP 200464
0400 ! 
0460 CALL "ZZINFO",Z[4],STATUS,X3$,REC_USED,TOT_REC,KEY_SZ,BYTE,DISC,TYPE,TOT_SEC,FILE_NM$
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 IF %VENDOR$<>"" THEN {
0530 VENDOR$=%VENDOR$
0535 PROCESS "APGMAJ.3","../APG/AP.EN",INVO$; INVO$=STP(INVO$) ! SSP 200464
0540 DISABLE CONTROL VENDOR.CTL
0550 GOSUB PROCESS_VENDOR
0560 GOSUB LOAD_INVOICE }
0570 NEXT_ID=VENDOR.CTL
0590 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0970 RETURN 
1000 ! Process Vendor invoice information
1010 PROCESS_VENDOR:
1020 CALL "ZZWLKU;Parse_vend",VENDOR$,VEND_DIV$,VEND_CODE$
1030 VENDOR$=VEND_DIV$+VEND_CODE$
1040 READ (Z[2],KEY=VENDOR$,DOM=*RETURN)IOL=0320
1280 REFRESH_FLG=1
1290 RETURN 
2000 ! load the invoices
2010 LOAD_INVOICE:
2015 OPEN_INV.LOAD=-1,LI_TOTAL_COUNT=0
2020 KEY_1$=VENDOR$+INVO$ ! SSP 200464
2024 CALL "*WINGRP;HIDE",PAGEINV.GRP$
2025 LIST_BOX LOAD OPEN_INV.CTL,"" ! SSP 200464
2030 READ (Z[1],KEY=KEY_1$,DOM=*NEXT)
2035 LOAD_INVOICE_BKG: LI_COUNT=0
2040 LI_COUNT++; IF LI_COUNT>500 THEN RETURN END_IF ; IF LI_TOTAL_COUNT>32000 THEN INVO$=MID(KEY_1$,11,10); LIST_BOX LOAD OPEN_INV.CTL,0,""+SEP+SEP+'RED'+"MORE INVOICES AVAILABLE"+SEP+SEP+SEP+SEP; CALL "*WINGRP;SHOW",PAGEINV.GRP$; GOTO LOAD_INVOICE_DONE ! Next batch of invoices loaded, so stop now; Or 32000 limit has been reached, prepare to continue if required
2045 KEY_1$=KEY(Z[1],END=LOAD_INVOICE_DONE); READ (Z[1])IOL=0310
2050 IF KEY_1$(1,10)<>VENDOR$ THEN GOTO LOAD_INVOICE_DONE
2052 GOSUB RANGE; IF YES THEN GOTO 2045
2055 GOSUB CHECK_MESSAGE
2060 D1$=APQ$(21,6); CALL "ZZWDTE;Display",D1$,A1$,A2$
2070 D2$=APQ$(29,6); CALL "ZZWDTE;Display",D2$,B1$,B2$
2075 NUM1$=STR(APQ[10]:"###,###,###.00-"); IF APQ[10]<0 THEN NUM1$='RED'+NUM1$
2077 NUM2$=STR(APQ[13]:"###,###,###.00-"); IF APQ[13]<0 THEN NUM2$='RED'+NUM2$
2080 LI_TOTAL_COUNT++; LIST_BOX LOAD OPEN_INV.CTL,0,APQ$(11,10)+SEP+D1$+SEP+MSG_1$+SEP+D2$+SEP+NUM1$+SEP+NUM2$+SEP+APQ$(66,3) ! WO251293
2085 IF KEY_SZ THEN GOSUB READ_DIRECT; GOTO 2040
2090 KEY_4=APQ[O]
2100 IF KEY_4<=0 THEN LI_TOTAL_COUNT++; LIST_BOX LOAD OPEN_INV.CTL,0," "; GOTO 2040
2110 READ (Z[4],IND=KEY_4,ERR=2100)IOL=0330
2120 GOSUB LOAD_CHECK
2160 KEY_4=IDX1
2170 IF KEY_4<=0 THEN LI_TOTAL_COUNT++; LIST_BOX LOAD OPEN_INV.CTL,0," "; GOTO 2040
2180 GOTO 2110
2190 RETURN 
2191 LOAD_INVOICE_DONE:
2192 OPEN_INV.LOAD=1 ! Loading complete
2195 RETURN 
2300 ! load check Information
2310 LOAD_CHECK:
2320 D3$=APJ$(49,6); CALL "ZZWDTE;Display",D3$,A3$,B3$
2330 NUM3$=STR(APJ[0]:"###,###,###.00-"); IF APJ(0)<0 THEN NUM3$='RED'+NUM3$
2340 AC_NUM$="AC# "+APJ$(43,4)+"."+APJ$(47,2)
2350 LI_TOTAL_COUNT++; LIST_BOX LOAD OPEN_INV.CTL,0,""+SEP+D3$+SEP+"("+APJ$(36,1)+") "+APJ$(21,15)+SEP+AC_NUM$+SEP+NUM3$+SEP+CLEARED$ ! SSP#189432
2390 RETURN 
2500 ! Read Direct File
2510 READ_DIRECT:
2520 KEY_4$=VENDOR$+APQ$(11,10)
2530 READ (Z[4],KEY=KEY_4$,DOM=*NEXT)
2540 KEY_4$=KEY(Z[4],END=*RETURN); READ (Z[4],KEY=KEY_4$)IOL=0340
2550 IF KEY_4$(1,20)<>VENDOR$+APQ$(11,10) THEN GOTO 2580
2555 IF APJ$(58,1)<>"H" THEN GOTO 2540 ! SSP#209664
2556 CLEARED$=""; IF APJ$(59,6)<>DIM(6) THEN CLEARED$='BLUE'+"Cleared: "+APJ$(59,4)+"/"+APJ$(63,2) ! SSP#189432
2560 GOSUB LOAD_CHECK
2570 GOTO 2540
2580 LI_TOTAL_COUNT++; LIST_BOX LOAD OPEN_INV.CTL,0," " ! GOTO 2040
2590 RETURN 
3000 ! Get the invoice detail information
3010 LOAD_DETAIL:
3015 DIM APQ[27]
3020 INV_NUM$=ARG_1$,Z1=NUM(ARG_2$)
3030 VENDOR$=%VENDOR$
3040 KEY_1$=VENDOR$+INV_NUM$
3050 FIND (Z1,KEY=KEY_1$,DOM=*RETURN)IOL=0310
3060 INV_DATE$=APQ$(21,6); INV_DUE_DATE$=APQ$(29,6)
3070 INV_COMMENT$=APQ$(41,15); INV_TYPE$=APQ$(83,1)
3080 INV_HOLD$=APQ$(81,1); APPLY_TO$=APQ$(84,10)
3090 REF_NUM$=APQ$(56,10); INV_SP_CODE$=APQ$(82,1)
3100 DISC_EXPIRE$=APQ$(35,6); DISC_EXPIRE_2$=APQ$(125,6)
3110 TOT_DISC=APQ[7]; DISC_AMT_2=APQ[22]
3120 TERM_CODE$=APQ$(27,2); CAT_CODE$=APQ$(69,9)
3130 PAYMENT_SELEC$=APQ$(78,2); PAYMENT_P$=APQ$(80,1)
3140 BANK_ACCT$=APQ$(66,3)
3150 FIS_YR$=APQ$(94,4); ACCT_PD$=APQ$(98,2)
3160 TRANS_INDEX=APQ[0]; GL_INDEX=APQ[1]
3170 EXT_DUE_DATE=APQ[9]; WORKER_COMP=APQ[8]
3180 GROSS_INV_AMT=APQ[2]; INV_AMT=APQ[10]
3190 DISC_TAKEN=APQ[11]; INV_BAL=APQ[13]
3200 PAYMENT_APPLIED=APQ[12]
3210 AUD_CONT_NO$=APQ$(106,4)+"."+APQ$(110,2)
3380 IF APQ[1]<>0 THEN CALL "*WINGRP;ENABLE",GL_BTN.GRP$
3970 REFRESH_FLG=1
3990 RETURN 
4000 ! select invoice line
4010 SELECT_LINE:
4015 IDX=-1,INV_LINE$=""
4020 LIST_BOX READ OPEN_INV.CTL,IDX,ERR=4033 ! 272099
4030 LIST_BOX FIND OPEN_INV.CTL,IDX,INV_LINE$,ERR=*NEXT ! SSP#225956
4033 IF NUL(INV_LINE$) OR IDX=-1 THEN MSGBOX "Invalid selection or invoice not found!"; RETURN 
4035 IF NUL(MID(INV_LINE$,1,POS(SEP=INV_LINE$)-1)) THEN RETURN 
4040 INV_LN$=INV_LINE$(1,POS(SEP=INV_LINE$)-1)
4050 PROCESS "APGMAJ.1","../APG/AP.EN",INV_LN$,STR(Z[1]),X3$,STR(Z[5]),STR(Z[7])
4090 RETURN 
5000 ! Check the Reference, and Invoice Messages.
5010 CHECK_MESSAGE:
5020 MSG_1$=""
5030 IF NOT(NUL(APQ$(56,10))) AND NOT(NUL(APQ$(41,15))) THEN {
5040 MSG_1$=FN%ZZDISP$(APQ$(56,10),"P/O")+" / "+APQ$(41,15) ! SSP 200464
5050  } ELSE {
5060 IF NOT(NUL(APQ$(41,15))) THEN MSG_1$=APQ$(41,15)
5070 IF NOT(NUL(APQ$(56,10))) THEN MSG_1$=FN%ZZDISP$(APQ$(56,10),"P/O") ! SSP 200464
5080  }
5090 RETURN 
7000 ! GENERAL LEDGER INFORMATION
7010 GL_INFO:
7020 DIM APK[7]; _APK=NUM(ARG_5$),_GL1=NUM(ARG_4$)
7030 READ (_APK,IND=APQ[1],DOM=*RETURN)IOL=0350
7035 IF NUL(APK$) THEN APK$=DIM(60)
7040 LINE=1
7050 DIM GL1$(52); READ (_GL1,KEY=APK$(40,12),DOM=*NEXT)GL1$
7055 DEBIT_AMT=0,CREDIT_AMT=0
7060 IF APK[3]>0 THEN DEBIT_AMT=APK[3] ELSE CREDIT_AMT=APK[3]
7070 IF DEBIT_AMT=0 THEN DEB$="" ELSE DEB$=STR(DEBIT_AMT:"###,###.00-")
7080 IF CREDIT_AMT=0 THEN CRED$="" ELSE CRED$=STR(CREDIT_AMT:"###,###.00-")
7090 LIST_BOX LOAD LB_LINE.CTL,0,STR(LINE:"###")+SEP+FN%ZZDISP$(APK$(40,12),"G/L")+SEP+GL1$(13,35)+SEP+DEB$+SEP+CRED$+SEP+""
7100 LINE+=1
7110 IF IDX2=-1 OR IDX2=0 THEN RETURN 
7120 READ (_APK,IND=IDX2,DOM=*RETURN)IOL=0350
7130 GOTO 7050
9999 END 
10000 SELECT_INVOICE:! SSP 56000
10010 INVOICE_NUM$=PAD(INVOICE_NUM$,10," ")
10020 READ (Z[1],KEY=%VENDOR$+INVOICE_NUM$,DOM=END_SEL_INVOICE)
10050 PROCESS "APGMAJ.1","../APG/AP.EN",INVOICE_NUM$,STR(Z[1]),X3$,STR(Z[5]),STR(Z[7])
10100 END_SEL_INVOICE:
10110 CALL "*WINGRP;HIDE",INVO.GRP$
10120 NEXT_ID=BT_INVOICE.CTL,INVOICE_NUM$="",REFRESH_FLG=1
10130 RETURN 
10200 STARTING_POINT: INVO$="" ! SSP 200464
10210 PROCESS "APGMAJ.3","../APG/AP.EN",INVO$ ! SSP 200464
10215 INVO$=STP(INVO$)
10220 GOTO LOAD_INVOICE
10230 RETURN 
10500 RANGE: YES=0
10510 IF NOT(NUL(R.START_INV$)) OR NOT(NUL(R.END_INV$)) THEN IF APQ$(11,10)<R.START_INV$ OR APQ$(11,10)>R.END_INV$ THEN YES=1
10520 IF NOT(NUL(R.START_DATE$)) OR NOT(NUL(R.END_DATE$)) THEN IF APQ$(21,6)<R.START_DATE$ OR APQ$(21,6)>R.END_DATE$ THEN YES=1
10530 IF NOT(NUL(R.START_PO$)) OR NOT(NUL(R.END_PO$)) THEN IF APQ$(56,10)<R.START_PO$ OR APQ$(56,10)>R.END_PO$ THEN YES=1
10540 IF APQ[10]<R0 OR APQ[10]>R1 THEN YES=1
10550 RETURN 
10600 SEL_RANGE:
10610 REPLACEMENT_SCRN$="APGMAJ.4"
10620 PERFORM "*winproc;overlay_screen"
10630 RANGE_FLG=1
10640 GOTO LOAD_INVOICE
11000 GET_DATE:
11010 R.END_DATE$=DTE(0:"%Yl%Mz%Dz")
11020 R.END_DATE$=CHR(NUM(R.END_DATE$(1,3))-125)+R.END_DATE$(4)
11030 RETURN 
56000 ! "200464-Cannot searched for an invoice in AP Open Invoice for       
56001 REM "225956-Getting error when trying to view invoices for vendor       
56002 REM "251293-Bank Code option for P-Card file generation during CD update
56003 REM "189432-Ehancement to Bank rec - tag Monthly Disb and Detail trans  
