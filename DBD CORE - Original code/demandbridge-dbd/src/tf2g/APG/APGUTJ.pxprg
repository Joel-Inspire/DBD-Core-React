0010 ! APGUTJ - A/P Change Bank Code on Invoices/Vendors
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 06/08/09 - 7.665833 - tma - SSP# 230362
0037 REM "230362-Some of the AP rebuilds - why are there smiley faces when   
0040 REM "Copyright 2009 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 PROCESS "APGUTJ","../APG/AP.EN"
0060 EXIT 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0105 INIT:
0106 PRINT 'CURSOR'(0) ! SSP#230362 - changed from 8 to 0 to get rid of smiley 
0110 X0$="AP2UTJ",X1$="A/P Change Bank Code on Invoices/Vendors"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01L AP4...  02O API...  03O ZYB...  04O AP0...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0521 READ (Z[13],KEY=X3$(9,3)+"A/P")AP_PARMS$; IF AP_PARMS$<>"Y" THEN CALL "*WINGRP;HIDE",DIV.GRP$
0525 _ZZP=Z(13),_ZYB=Z(3); GOSUB LOAD_DIVISION; PERFORM "BRGMAA;LOAD_BANK_CODE"
0600 REM "
0610 GOSUB 6000
0620 GOSUB 8100
0621 PRINT 'CURSOR'(0); NEXT_ID=DB_DIV.CTL; RETURN 
0622 DIM DIV$(2); IF AP_PARMS$(13,1)<>"Y" THEN GOTO 0625
0623 DIM DIV$(2); CALL "ZZENTR","SZXUX",A{ALL},DIV$,X4$,X3$,12,11,1,2,C0,"","{1"+X$,"","AP2UTJ04","AP0","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 0623,0624
0624 DIM AP0$(33); TITLE$="All"; IF DIV$<>DIM(2) THEN READ (Z[4],KEY=DIV$,DOM=0623)AP0$; TITLE$=AP0$(3,30) END_IF ; PRINT @(16,11),TITLE$,
0625 DIM CODE$(3),A[1]; C0=0; CALL "ZZENTR","SZXUX",A{ALL},CODE$,X4$,X3$,12,12,1,3,C0,"","{1"+X$,"","AP2UTJ02","","",""
0626 IF ABS(C0)=4 THEN GOTO 9900
0630 READ (Z[3],KEY=CODE$,DOM=0625)BANK_CODE$
0635 PRINT @(16,12),BANK_CODE$(4,30),
0640 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0650 PROCEED:
0660 DIM DIV$(2); IF DB_DIV$<>"All" THEN DIV$(1,2)=MID(DB_DIV$,1,2) ELSE DIV$="  "
0670 DIM CODE$(3); CODE$(1,3)=BANK_CODE$(1,3)
0900 READ (Z[1],KEY="",DOM=0901)
1000 REM 
1010 K$=KEY(Z[1],END=5000); READ RECORD (Z[1],KEY=K$)AP4$
1015 R0=R0+1; IF MOD(R0,T0)=1 THEN C=R0; GOSUB 8150
1017 IF DIV$<>DIM(2) THEN IF AP4$(1,2)<>DIV$ THEN GOTO 1010
1020 AP4$(283,3)=CODE$
1025 WRITE RECORD (Z[1],KEY=K$)AP4$
1050 READ (Z[2],KEY=AP4$(1,10),DOM=1051)
1055 K1$=KEY(Z[2],END=1010); READ RECORD (Z[2],KEY=K1$)API$
1060 IF API$(1,10)<>AP4$(1,10) THEN GOTO 1010
1065 API$(66,3)=CODE$
1070 WRITE RECORD (Z[2],KEY=K1$)API$
1075 GOTO 1055
5000 REM "EOJ
5005 C=T; GOSUB 8150
5007 READ (Z[2],KEY="",DOM=*NEXT)
5010 PROCESS "APGUT9","../APG/AP.EN","NO_STOP" ! CALL "AP2UT9",X3$,X4$,"","*"
5020 MSGBOX "Process complete!!",MSG("CONFIRMING"),"INFO,TIM=3"
5040 GOTO 9900
6000 REM "BACKGROUND
6001 RETURN 
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This program will process the Vendor Masterfile 'AP4' and the Accounts Payable  Open Invoice File 'API' and change the bank code to the entered bank code, then rebuild the Invoice Bank Code Sort file 'APP'."
6025 PRINT @(0,8),"If A/P Divisions are used, then a specific division can be entered or blank for all."
6030 REM "PRINT 'LF',"REPORTING EVERY 100 ENTRIES...",
6090 IF AP_PARMS$(13,1)="Y" THEN PRINT @(2,11),"Division:",
6100 PRINT @(1,12),"Bank code:",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6410 GOSUB 6000
6430 GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6460 IF AP_PARMS$(13,1)="Y" THEN PRINT @(12,11),DIV$,@(15,11),TITLE$,
6465 PRINT @(0,14),"There are "+STR(T)+" records to process"
6490 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[1],T9,X3$,T,T0,K,B,D,S0,S1,F$
8115 PRINT 'MESSAGE'("There are "+STR(T)+" records to process",0)
8129 REM "Set T0, we make sure T0 is > 1, because later on we MOD and look for avalue of 1. IF T0 is 1, then nothing would get reported. We look for a result of 1 because this causes the first record to automatically start the reporting instead of waiting until the T0'th record to get the first report
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",10.2,1,28,T1,T,C
8195 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9901 END_PROC: CMD_STR$="E"
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
10000 LOAD_DIVISION: DD$="All/",YY=0
10010 READ (Z(4),END=END_LOAD,DOM=*NEXT) ! ssp#203379
10020 READ (Z(4),END=END_LOAD)AP0$
10030 DD$=DD$+AP0$(1,2)+" - "+STP(AP0$(3))+"/",YY+=1
10035 IF YY=1 THEN DFT$=AP0$(1,2)+" - "+STP(AP0$(3))
10040 GOTO 10020
10050 END_LOAD:
10060 DROP_BOX LOAD DB_DIV.CTL,DD$
10070 DROP_BOX WRITE DB_DIV.CTL,"All"
10080 RETURN 
56000 REM "203379-A/P change bank code utility is getting error 2 - end of    
