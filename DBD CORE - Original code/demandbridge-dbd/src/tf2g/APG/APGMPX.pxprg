0010 ! APGMPX - G/L Distribution Maintenance
0035 REM "5.7 - 11/16/10 - 10.530555 - tma - SSP# 241260
0037 REM "241260-Would like to have an APX query in AP-GL interface similar  
0040 REM "Copyright 2010 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 PROCESS "APGMPX","../APG/AP.EN"
0060 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 ! 
0130 X0$="AP2MPX",X1$="G/L Interface Setup"
0140 ! 
0150 X0=-1,X2=-1,_KCNT=0 ! SSP#241260
0160 CALL "ZZCOMP",X0$,X11$,X2$,X3$,X4$,X5$,X0,X1,X2; IF X1>0 THEN GOSUB WRAPUP
0170 ! 
0190 DIM Z[NUM(X3$(60,3))],APX$(200),_KEYS$[1:1] ! SSP#241029
0200 ! Files
0210 Z$="01X APX...  02O GL1...  03O AP0...  04O AP3...  06O ZZPARM  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
0300 ! IOList Section
0310 IOLIST APX$
0320 IOLIST GL1$
0330 IOLIST VEND$
0400 ! System Parameters
0410 GOSUB GET_SYS_PARAM
0420 _NUMKEYS=1 ! SSP#241260
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 IF F0$(20,1)<>"Y" THEN IF F0$(13,1)="Y" THEN CALL "*wingrp;show",DIV.GRP$; _KEYS$[1]="DIV_OR_DIST_CODE$" ! SSP#241029
0525 IF F0$(20,1)="N" AND F0$(13,1)="N" THEN APX$(1,2)="00",DIV_OR_DIST_CODE$="00",_EOM$=$0D$; REFRESH_FLG=1; CALL "*wingrp;show",DIV.GRP$; CALL "*wingrp;disable",DIV.GRP$; _KEYS$[1]="DIV_OR_DIST_CODE$" ! SSP#241029
0530 IF F0$(20,1)="Y" THEN CALL "*wingrp;show",VEND.GRP$; _KEYS$[1]="VEND_CAT$" ! SSP#241029
0540 IF F0$(36,1)="Y" THEN CALL "*wingrp;show",RET.GRP$
0550 _ENABLE_FLG=1; GOSUB ENABLE_GROUPS ! SSP#241029
0600 ! Special Amount
0610 IF F1$(24,1)="Y" THEN SPEC_TXT1$=F1$(8,16)+":"; CALL "*wingrp;show",SPC1.GRP$
0620 IF F1$(44,1)="Y" THEN SPEC_TXT2$=F1$(28,16)+":"; CALL "*wingrp;show",SPC2.GRP$
0630 IF F1$(64,1)="Y" THEN SPEC_TXT3$=F1$(48,16)+":"; CALL "*wingrp;show",SPC3.GRP$
0640 IF F1$(84,1)="Y" THEN SPEC_TXT4$=F1$(68,16)+":"; CALL "*wingrp;show",SPC4.GRP$
0700 ! 
0710 IF F0$(20,1)<>"Y" AND F0$(13,1)<>"Y" THEN GOSUB FIND_REC
0780 REFRESH_FLG=1
0790 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0970 RETURN 
1000 ! find Record
1010 FIND_REC:
1012 IF F0$(20,1)="Y" THEN QRY_VAL$=PAD(QRY_VAL$,9,"0") ! SSP#241029
1015 CHANGE_FLG=0; IF POS(_EOM$=$000102090D$)=0 THEN RETURN ELSE GOSUB GET_CURKEY; IF _EOM$=$00$ THEN IF QRY_VAL$<>"" AND QRY_VAL$=EVS(_KEYS$[_CURKEY]) THEN QRY_VAL$="" ELSE RETURN ! SSP#241029      
1020 IF F0$(20,1)="Y" THEN KEY_1$=VEND_CAT$ ELSE IF F0$(13,1)="Y" THEN KEY_1$=DIV_OR_DIST_CODE$ ELSE KEY_1$="00"
1030 FIND (Z[1],KEY=KEY_1$,DOM=ADD_REC)IOL=0310 ! SSP#241029
1040 _ENABLE_FLG=-1; GOSUB ENABLE_GROUPS ! SSP#241029
1050 AP_GL_CODE$=APX$(33,12)
1060 DISC_TAKEN$=APX$(45,12)
1070 RETAINAGE$=APX$(57,12)
1080 SPECIAL_AMT_1$=APX$(81,12)
1090 SPECIAL_AMT_2$=APX$(93,12)
1100 SPECIAL_AMT_3$=APX$(105,12)
1110 SPECIAL_AMT_4$=APX$(117,12)
1160 CALL "*wingrp;Enable",BUTTON_1.GRP$
1170 B_APX$=APX$
1175 NEXT_ID=AP_GL_CODE.CTL ! SSP#241260
1180 REFRESH_FLG=1
1190 RETURN 
1300 ! Get the division Information
1310 GET_DIV:
1320 KEY_DIV$=DIV_OR_DIST_CODE$
1330 READ (Z[F0],KEY=KEY_DIV$,DOM=DIV_MSG)IOL=0330
1340 DIV_DESC$=VEND$(3,30)
1380 REFRESH_FLG=1
1390 RETURN 
1500 ! Write Record
1510 WRITE_REC:
1540 WRITE (Z[1],KEY=KEY_1$)IOL=0310
1550 GOSUB CLEAR_FIELDS
1580 B_APX$=APX$; IF F0$(20,1)<>"Y" THEN NEXT_ID=DIV_OR_DIST_CODE.CTL ELSE NEXT_ID=VEND_CAT.CTL ! SSP#241029
1585 GOSUB 10010; _ENABLE_FLG=1; GOSUB ENABLE_GROUPS; REFRESH_FLG=1 ! SSP#241029
1586 B_APX$="",APX$=""
1590 RETURN 
1810 CLEAR_REC:! SSP#241029     
1830 _CLR_FLG$="R"; GOSUB CLEAR_FIELDS
1840 _ENABLE_FLG=1; GOSUB ENABLE_GROUPS ! SSP#241029
1850 CHANGE_FLG=0; REFRESH_FLG=1; IF F0$(20,1)<>"Y" THEN NEXT_ID=DIV_OR_DIST_CODE.CTL ELSE NEXT_ID=VEND_CAT.CTL ! SSP#241029     
1860 RETURN ! SSP#241029     
2000 ! Load Information
2010 LOAD_INFO:
2015 IF F0$(20,1)<>"Y" THEN DIV_OR_DIST_CODE$=APX$(1,2) ELSE VEND_CAT$=APX$(1,9)
2020 AP_GL_CODE$=APX$(33,12)
2030 DISC_TAKEN$=APX$(45,12)
2040 RETAINAGE$=APX$(57,12)
2050 SPECIAL_AMT_1$=APX$(81,12)
2060 SPECIAL_AMT_2$=APX$(93,12)
2070 SPECIAL_AMT_3$=APX$(105,12)
2080 SPECIAL_AMT_4$=APX$(117,12)
2160 CALL "*wingrp;Enable",BUTTON_1.GRP$
2170 B_APX$=APX$
2180 REFRESH_FLG=1
2190 RETURN 
2300 ! Load Slections
2310 LOAD_SELECTION:
2315 IF APX$="" THEN RETURN 
2320 IF F0$(20,1)<>"Y" THEN APX$(1,2)=DIV_OR_DIST_CODE$ ELSE APX$(1,9)=VEND_CAT$
2330 APX$(33,12)=AP_GL_CODE$
2340 APX$(45,12)=DISC_TAKEN$
2350 APX$(57,12)=RETAINAGE$
2360 APX$(81,12)=SPECIAL_AMT_1$
2370 APX$(93,12)=SPECIAL_AMT_2$
2380 APX$(105,12)=SPECIAL_AMT_3$
2390 APX$(117,12)=SPECIAL_AMT_4$
2490 RETURN 
2500 ! Delete Record
2510 DELETE_REC:
2520 MSGBOX MSG("DELETE_REC"),MSG("CONFIRM"),"?,YesNo",ANS$
2530 IF ANS$="NO" THEN RETURN 
2540 REMOVE (Z[1],KEY=KEY_1$,DOM=*NEXT)
2550 REFRESH_FLG=1,B_APX$=APX$; IF F0$(20,1)<>"Y" THEN NEXT_ID=DIV_OR_DIST_CODE.CTL ELSE NEXT_ID=VEND_CAT.CTL ! SSP#241029   
2555 GOSUB 10010
2558 B_APX$=""
2590 RETURN 
3000 ! First Record
3010 FIRST_REC:
3020 KEY_1$=KEF(Z[1],END=NO_FIRST)
3030 READ (Z[1],KEY=KEY_1$)IOL=0310
3040 GOSUB LOAD_INFO
3090 RETURN 
3200 ! Last Record
3210 LAST_REC:
3220 KEY_1$=KEL(Z[1],ERR=NO_LAST)
3230 READ (Z[1],KEY=KEY_1$)IOL=0310
3240 GOSUB LOAD_INFO
3290 RETURN 
3400 ! Prior Record
3410 PRIOR_REC:
3420 KEY_1$=KEP(Z[1],END=START_FILE)
3430 READ (Z[1],KEY=KEY_1$)IOL=0310
3440 GOSUB LOAD_INFO
3490 RETURN 
3600 ! Next Record
3610 NEXT_REC:
3620 KEY_1$=KEY(Z[1],END=END_FILE)
3630 READ (Z[1],KEY=KEY_1$)IOL=0310
3640 GOSUB LOAD_INFO
3690 RETURN 
4010 GET_CURKEY:! SSP#241029     
4020 FOR _CURKEY=1 TO _NUMKEYS ! SSP#241029     
4030 IF _KEYS$[_CURKEY]=ID$+"$" OR _KEYS$[_CURKEY]=ID$ THEN EXITTO *RETURN ! SSP#241029     
4040 NEXT _CURKEY ! SSP#241029     
5000 ! Check Changes
5010 CHECK_CHANGES:
5020 IF APX$=B_APX$ OR B_APX$="" THEN RETURN 
5023 IF CHANGE_FLG=0 THEN RETURN ! SSP#241029     
5030 MSGBOX MSG("REC_ALTERD"),MSG("UPDATE"),"?,YesNo",ANS$
5040 IF ANS$="NO" THEN RETURN 
5050 GOSUB WRITE_REC
5090 RETURN 
8000 ! Messages
8010 DIV_MSG:
8020 MSGBOX "Division Not Found!","NOT FOUND","INFO,OK"
8030 NEXT_ID=DIV_OR_DIST_CODE.CTL
8040 REFRESH_FLG=1
8090 RETURN 
8100 ! No First Record
8110 NO_FIRST:
8120 MSGBOX MSG("REC_NO_1ST"),"FYI","!"
8130 RETURN 
8200 ! No Last Record
8210 NO_LAST:
8220 MSGBOX MSG("REC_NO_LST"),"FYI","!"
8230 RETURN 
8300 ! Start of File
8310 START_FILE:
8320 MSGBOX MSG("START_FILE"),"FYI","!"
8330 RETURN 
8400 ! End of File
8410 END_FILE:
8420 MSGBOX MSG("END_OF_FIL"),"FYI","!"
8430 RETURN 
8500 ! Add New Record
8510 ADD_REC:
8520 MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"?,YESNO",_YESNO$ ! SSP#241029
8530 IF _YESNO$="NO" THEN NEXT_ID=_FIRST_KEY,CHANGE_FLG=0; EXIT ! SSP#241029
8540 DIM APX$(200); B_APX$=APX$
8560 _ENABLE_FLG=-1; GOSUB ENABLE_GROUPS ! SSP#241029
8580 REFRESH_FLG=1
8590 RETURN 
9000 ! Parameters
9010 GET_SYS_PARAM:
9020 READ (Z[6],KEY=X3$(9,3)+"A/P",DOM=WRAPUP)F0$
9030 DIM F1$(120); READ (Z[6],KEY=X3$(9,3)+"AP2A",DOM=*NEXT)F1$
9040 IF F0$(20,1)="Y" THEN F0=4 ELSE F0=3
9090 RETURN 
9999 END 
10000 CLEAR_FIELDS:
10005 ! RETURN !SSP#241029     
10007 IF _CLR_FLG$="R" THEN DIV_OR_DIST_CODE$="",VEND_CAT$="" ! SSP#241029
10010 AP_GL_CODE$="",%GL_ACCT_DESC2$="",DISC_TAKEN$="",%GL_ACCT_DESC3$=""
10020 RETAINAGE$="",%GL_ACCT_DESC4$="",SPECIAL_AMT_1$="",%GL_ACCT_DESC5$="",SPECIAL_AMT_2$="",SPECIAL_AMT_3$="",SPECIAL_AMT_4$="",VEND_CAT$="" ! SSP#241029
10030 AP_GL_CODE$=""
10090 RETURN 
14010 ENABLE_GROUPS:! SSP#241029
14020 IF _ENABLE_FLG=0 THEN GOTO *RETURN ! SSP#241029
14970 IF _ENABLE_FLG>0 THEN CALL "*wingrp;Disable",FIELDS.GRP$; CALL "*wingrp;Disable",BUTTONS.GRP$ ! SSP#241029
14980 IF _ENABLE_FLG<0 THEN CALL "*wingrp;Enable",FIELDS.GRP$; CALL "*wingrp;Enable",BUTTONS.GRP$ ! SSP#241029
14990 RETURN ! SSP#241029
20150 LOOKUP:! ssp#241260
20160 PROCESS "QRY_APX.1","../APG/AP.EN",X$ ! SSP#241260
20165 READ DATA FROM X$ TO QRY_VAL$ ! SSP#241260
20170 IF F0$(20,1)="Y" THEN VEND_CAT$=PAD(QRY_VAL$,9,"0") ELSE DIV_OR_DIST_CODE$=QRY_VAL$ ! SSP#241260
20175 _EOM$=$0D$,_ENABLE_FLG=1,_KCNT=_NUMKEYS ! SSP#241260
20190 LOOKUP_END:GOTO FIND_REC ! SSP#241260
56000 REM "199367-APS S6 in CHUI, the APGMPX screen in GUI does not pull up   
56001 REM "241029-G/L Interface panel APGMPX:  Doesn't have the Do you want   
