0010 ! APGCBA - A/P Specific invoice selection
0035 REM "5.7 - 12/28/10 - 9.138333 - tma - SSP# 243112
0037 REM "243112-Have a blank bank code on the suggestive payment report.    
0040 REM "Copyright 2010 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 PROCESS "APGCBA","../APG/AP.EN"
0060 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 ! 
0130 X0$="AP2CAA"
0140 X0=-1; X2=-1
0150 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
0160 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1) ! SSP240024 jdf
0165 M0$="-###,###,###.00" ! SSP240024 jdf
0170 CALL "ZGBATC",X3$,X4$,X0$,X9; IF X9=1 THEN CMD_STR$="END"; EXIT 
0175 %APGCBA$=MSG("APGCBA"); IF STP(X3$(174,4))<>"" THEN %APGCBA$+=" - "+"Batch No: "+X3$(174,4)
0177 ! 
0180 DIM Z[NUM(X3$(60,3))],APG[5],AP4[1],AP5[5],API[27]
0200 ! Open File Section
0210 Z$="01O APG...  02O AP4...  03O API...  04O ZYB...  05O AP5...  06O ZZPARM  12O AP8...  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
0300 ! IO List Section
0310 IOLIST APG$,APG{ALL}
0320 IOLIST AP4$,AP4{ALL}
0330 IOLIST AP5$,AP5{ALL}
0340 IOLIST AP8$
0350 IOLIST API$,API{ALL}
0360 IOLIST ZYB$,ZYB{ALL} ! SSP240024 jdf
0400 ! Parameters
0410 GOSUB GET_PARAM
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 ! get Info
0530 CALL "ZZINFO",Z[1],X0,X3$,R0,R1,0,0,0,0,0,""
0540 GOSUB CHECK_PRINT
0550 IF R0>0 THEN GOSUB CLEAR_FILE
0570 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0970 EXIT 
1000 ! get the vendor information
1010 GET_VENDOR_INFO:
1020 CALL "ZZWLKU;PARSE_VEND",VENDOR$,VEND_DIV$,VEND_CODE$
1030 VENDOR$=VEND_DIV$+VEND_CODE$
1040 READ (Z[2],KEY=VENDOR$,DOM=*RETURN)IOL=0320
1050 VEND_NAME$=AP4$(11,35); %VEND_DIV$=VEND_DIV$; %VEND_CODE$=VEND_CODE$
1060 FIND (Z[5],KEY=VENDOR$,DOM=NO_ACT)IOL=0330 ! <><><><>  Remember to change back to DOM=NO_ACT....after testing
1070 VEND_BAL=AP5[0]
1080 GOSUB VEND_TOT
1090 IF AP4$(291,1)="Y" THEN GOSUB VEND_ONHOLD
1100 GOSUB VENDOR_MESSAGE
1120 CALL "*wingrp;Enable",INV_NUM.GRP$
1130 NEXT_ID=INV_NUM.CTL; REFRESH_FLG=1
1190 RETURN 
1500 ! No Activity...
1510 NO_ACT:
1520 MSGBOX MSG("VEND_NO_A"),MSG("FYI"),"!"
1530 VENDOR$=""; VEND_NAME$=""
1540 NEXT_ID=VENDOR.CTL
1550 REFRESH_FLG=1
1590 RETURN 
1800 ! check for existing info
1810 INVOICE_CHECK:
1813 DIM B_APG$(64),B_APG[5]; B_KEY_1$="" ! SSP240024 jdf
1815 IF STP(INV_NUM$)="DELETE" OR STP(INV_NUM$)="ALL" THEN GOSUB VENDOR_INVOICE; RETURN 
1820 KEY_1$=VENDOR$+INV_NUM$,FOUND=0 ! SSP#230502
1830 READ (Z[1],KEY=KEY_1$,DOM=NON_EXSIST)IOL=0310
1835 FOUND=1 ! 230502
1838 B_APG$=APG$,B_KEY_1$=KEY_1$; FOR I=0 TO 5; B_APG[I]=APG[I]; NEXT I ! SSP240024 jdf
1840 GOSUB VENDOR_INVOICE
1845 CALL "*WINGRP;DISABLE",INV_NUM.GRP$ ! SSP240319 jdf
1850 CALL "*WINGRP;HIDE",UPDT_1.GRP$; CALL "*WINGRP;SHOW",UPDT_2.GRP$
1880 NEXT_ID=GROSS_AMT.CTL; REFRESH_FLG=1
1890 RETURN 
1900 ! non exsistant
1910 NON_EXSIST:
1920 MSGBOX MSG("REC_NOTFND")+"!"+SEP+MSG("WOULD_ADD")+"?",MSG("CONFIRM"),"?,YESNO",ANS$
1930 IF ANS$="NO" THEN NEXT_ID=INV_NUM.CTL; INV_NUM$=""; REFRESH_FLG=1; RETURN 
1940 GOSUB VENDOR_INVOICE
1990 RETURN 
2000 ! get the invoice information
2010 VENDOR_INVOICE:
2015 DIM ZYB$(151),ZYB[2] ! SSP240024 jdf
2020 IF STP(INV_NUM$)="DELETE" THEN GOSUB DELETE_INVOICE; REFRESH_FLG=1; RETURN 
2025 IF STP(INV_NUM$)="ALL" THEN GOSUB ADD_INVOICE; REFRESH_FLG=1; GOSUB CLEAR_INV_FIELD; GOSUB CHECK_RUN_TOTAL; GOSUB VEND_TOT; RETURN 
2030 KEY_3$=VENDOR$+INV_NUM$
2040 READ (Z[3],KEY=KEY_3$,DOM=INVALID_INV)IOL=0350
2050 INV_DATE$=API$(21,6); INV_DUE_DATE$=API$(29,6); DISC_EXP$=API$(35,6),CHECK_CODE$="" ! SSP#230502
2060 INV_HOLD$=API$(81,1); DISC_TYPE$=""; IF NOT(FOUND) THEN BANK_CODE$=API$(66,3) ELSE BANK_CODE$=APG$(22,3) ! SSP240024 jdf
2070 ORIG_BAL=API[10]; INV_BAL=API[13]
2080 REM_AMT=API[7]-API[11]; IF FOUND THEN REM_AMT=APG[1] ! SSP#230502
2085 IF FOUND THEN CHECK_CODE$=MID(APG$,21,1) ! SSP#230502
2090 GROSS_AMT=API[13]; IF FOUND THEN GROSS_AMT=APG[0] ! SSP#230502
2100 IF NOT(NUL(API$(125,6))) AND DISC_EXP$<%X3_TODAY$ THEN DISC_EXP$=API$(125,6),REM_AMT=API[22]-API[11],DISC_TYPE$="2nd DISC"; IF FOUND THEN REM_AMT=APG[1] ! SSP#230502
2110 IF DISC_EXP$>=%X3_TODAY$ OR AP4$(293,1)="Y" OR FOUND THEN DISC_TAKEN=REM_AMT ELSE DISC_TAKEN=0 ! SSP#228968
2120 NET_INV_PAID=GROSS_AMT-DISC_TAKEN
2130 IF DISC_TYPE$="" AND AP4$(293,1)="Y" THEN DISC_TYPE$="Take Disc"
2170 IF INV_HOLD$="Y" THEN ONHOLD_DESC$="Invoice on Hold" ELSE ONHOLD_DESC$=""
2180 REFRESH_FLG=1; CALL "*wingrp;Enable",INV_1.GRP$; CALL "*wingrp;Enable",BUTTON_1.GRP$
2185 NEXT_ID=GROSS_AMT.CTL
2190 RETURN 
2400 ! Delete All Invoices for this vendor
2410 DELETE_INVOICE:
2420 READ (Z[1],KEY=VENDOR$,DOM=*NEXT)
2430 KEY_1$=KEY(Z[1],END=2480); READ (Z[1],KEY=KEY_1$)IOL=0310
2440 IF KEY_1$(1,10)<>VENDOR$ THEN GOTO 2480
2450 REMOVE (Z[1],KEY=KEY_1$)
2460 T1=-1; CHECK_RUN_TOT+=(APG[0]-APG[1])*T1; VEN_TOT+=(APG[0]-APG[1])*T1 ! VEND_BAL+=APG[0]-APG[1] SSP#230672
2470 GOTO 2430
2480 CALL "*wingrp;Disable",INV_NUM.GRP$; VENDOR$=""; GOSUB CLEAR_INV_FIELD; NEXT_ID=VENDOR.CTL; REFRESH_FLG=1
2490 RETURN 
2500 ! Add  All Invoices for this Vendor
2510 ADD_INVOICE:
2520 READ (Z[3],KEY=VENDOR$,DOM=*NEXT)
2530 READ (Z[3],END=2680)IOL=0350
2540 IF API$(1,10)<>VENDOR$ THEN GOTO 2680
2550 IF API[13]=0 THEN GOTO 2530
2560 USING_2ND$="N"
2570 DIM APG$(64); APG$(1,20)=API$(1,20),APG$(21,1)=" ",APG$(22,3)=API$(66,3),APG[0]=API[13],APG[2]=0
2580 DISC_DATE$=API$(35,6),DISCOUNT=API[7]; IF NOT(NUL(API$(125,6))) THEN IF API$(35,6)<%X3_TODAY$ THEN DISC_DATE$=API$(125,6),DISCOUNT=API[22],USING_2ND$="Y" ! ssp 198202
2585 IF AP4$(293,1)="Y" THEN APG[1]=DISCOUNT-API[11]; APG[3]=API[19]-API[21]; GOTO 2595 ! SSP 198202
2590 IF DISC_DATE$>=%X3_TODAY$ THEN APG[1]=DISCOUNT-API[11]; APG[3]=API[19]-API[21] ELSE APG[1]=0 ! SSP 198202
2595 IF USING_2ND$="Y" THEN APG$(28,1)="Y" ! SSP 198202
2596 IF API$(81,1)="Y" THEN MSGBOX MSG("INVOICE")+": "+API$(11,10)+"is on hold.  "+MSG("ADD_RECORD"),MSG("CONFIRM"),"?,YesNo",ANS$; IF ANS$="NO" THEN GOTO 2530 ! SSP#231105
2600 WRITE (Z[1],KEY=API$(1,20))IOL=0310
2610 CHECK_RUN_TOT+=(APG[0]-APG[1]); VEN_TOT+=APG[0]-APG[1] ! ,VEND_BAL-=APG[0]-APG[1] !SSP#230672
2630 GOTO 2530
2680 CALL "*wingrp;Disable",INV_NUM.GRP$; VENDOR$=""; GOSUB CLEAR_INV_FIELD; NEXT_ID=VENDOR.CTL; REFRESH_FLG=1
2690 RETURN 
2800 ! Calculate Net Invoice Amount
2810 CALC_NET_AMT:
2820 NET_INV_PAID=GROSS_AMT-DISC_TAKEN
2880 REFRESH_FLG=1
2890 RETURN 
3000 ! update Invoice Information
3010 UPDATE_RECORD:
3015 MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"?,YesNo",ANS$; IF ANS$="NO" THEN RETURN 
3020 KEY_1$=VENDOR$+INV_NUM$
3030 DIM APG$(35)
3040 APG$(1,10)=VENDOR$; APG$(11,10)=INV_NUM$
3050 APG$(21,1)=STP(CHECK_CODE$,3); APG$(22,3)=BANK_CODE$
3060 APG$(28,1)=USING_2ND$
3070 APG[0]=GROSS_AMT
3080 APG[1]=DISC_TAKEN
3100 WRITE (Z[1],KEY=KEY_1$)IOL=0310
3120 CHECK_RUN_TOT+=APG[0]-APG[1]; VEN_TOT+=APG[0]-APG[1] ! VEND_BAL-=APG[0]-APG[1] SSP#230672
3140 GOSUB CLEAR_INV_FIELD; NEXT_ID=INV_NUM.CTL
3180 REFRESH_FLG=1
3190 RETURN 
3200 ! UPDATE RECORD
3210 UPDATE_RECORD2:
3215 MSGBOX MSG("REC_ALTERD"),MSG("UPDATE"),"?,YesNo",ANS$; IF ANS$="NO" THEN RETURN 
3220 KEY_1$=VENDOR$+INV_NUM$
3230 DIM APG$(35)
3240 APG$(1,10)=VENDOR$; APG$(11,10)=INV_NUM$
3250 APG$(21,1)=STP(CHECK_CODE$,3); APG$(22,3)=BANK_CODE$ ! SSP#230502!SSP#230502
3260 APG$(28,1)=USING_2ND$
3262 CHECK_RUN_TOT-=(B_GR_AMT-B_DISC_TAKEN); VEN_TOT-=(B_GR_AMT-B_DISC_TAKEN) ! VEND_BAL+=(B_GR_AMT-B_DISC_TAKEN) SSP#230672
3270 APG[0]=GROSS_AMT
3280 APG[1]=DISC_TAKEN
3300 WRITE (Z[1],KEY=KEY_1$)IOL=0310
3320 CHECK_RUN_TOT+=APG[0]-APG[1]; VEN_TOT+=APG[0]-APG[1] ! VEND_BAL-=APG[0]-APG[1] SSP#230672
3330 CALL "*WINGRP;ENABLE",INV_NUM.GRP$ ! SSP240319 jdf
3340 GOSUB CLEAR_INV_FIELD; NEXT_ID=INV_NUM.CTL
3370 CALL "*WINGRP;HIDE",UPDT_2.GRP$; CALL "*WINGRP;SHOW",UPDT_1.GRP$
3380 REFRESH_FLG=1
3390 RETURN 
4000 ! Delete Invoice Selected
4010 DELETE_REC:
4020 MSGBOX MSG("DELETE_REC"),MSG("VERIFY"),"?,YesNo",ANS$
4030 IF ANS$="NO" THEN RETURN 
4040 KEY_1$=VENDOR$+INV_NUM$
4045 READ (Z[1],KEY=KEY_1$,DOM=NO_REC)
4050 REMOVE (Z[1],KEY=KEY_1$)
4060 CHECK_RUN_TOT-=GROSS_AMT-DISC_TAKEN ! SSP 211693
4070 VEN_TOT-=GROSS_AMT-DISC_TAKEN ! VEND_BAL+=GROSS_AMT-DISC_TAKEN ! SSP 211693 SSP#230672
4075 CALL "*WINGRP;ENABLE",INV_NUM.GRP$ ! SSP#243112
4080 GOSUB CLEAR_INV_FIELD; NEXT_ID=INV_NUM.CTL
4085 REFRESH_FLG=1
4090 RETURN 
4100 ! No Record Found
4110 NO_REC:
4120 MSGBOX MSG("VEND_I_NF"),MSG("WARNING"),"STOP"
4190 RETURN 
5000 BANK_ACCOUNT:! SSP240024 jdf
5005 DIM D0$(50) ! SSP240024 jdf
5010 IF PARAM$(16,1)<>"Y" THEN GOTO 5100 ! SSP240024 jdf                
5020 BANK_CODE$=PAD(BANK_CODE$,3) ! SSP240024 jdf
5030 FIND (Z[4],KEY=BANK_CODE$,DOM=5100)IOL=0360 ! SSP240024 jdf
5040 %BANK_ACCT_DESC$=ZYB$(4,30); REFRESH_FLG=1 ! SSP240024 jdf
5050 IF STP(APG$)<>"" THEN APG$(25,3)=ZYB$(105,3) ! SSP240024 jdf
5060 IF POS(" "<>ZYB$(105,3))=0 THEN GOTO 5100 ! SSP240024 jdf
5070 IF ZYB$(105,3)<>D0$(4,3) THEN FIND (Z[6],KEY="CUR"+ZYB$(105,3),DOM=5100)D0$ ! SSP240024 jdf
5080 D1$=FNS$(D0$(7,20)) ! SSP240024 jdf
5090 MSGBOX "in "+D1$+":"+STR(API[18]:M0$)+SEP+"in "+D1$+":"+STR(API[18]-API[20]:M0$)+SEP+"in "+D1$+":"+STR(API[19]-API[21]:M0$)+SEP+"Gross amount in "+D1$+":"+STR(APG[2]:M0$)+SEP+"Discount taken in "+D1$+":"+STR(APG[3]:M0$)+SEP+"Net Invoice Amount Paid in "+D1$+STR(APG[2]-APG[3]:M0$)+DIM(20),"Alternate Currency" ! SSP240024 jdf
5100 NEXT_ID=GROSS_AMT.CTL; REFRESH_FLG=1 ! SSP240024 jdf
5200 RETURN ! SSP240024 jdf
5300 ! UPDATE RECORD on Exit ! SSP240024 jdf
5305 UPDATE_RECORD3:! SSP240024 jdf
5310 KEY_1$=VENDOR$+INV_NUM$ ! SSP240024 jdf
5315 DIM APG$(LEN(B_APG$)) ! SSP240024 jdf
5320 APG$(1,10)=VENDOR$; APG$(11,10)=INV_NUM$ ! SSP240024 jdf
5325 APG$(21,1)=STP(CHECK_CODE$,3); APG$(22,3)=BANK_CODE$ ! SSP240024 jdf             
5330 APG$(28,1)=USING_2ND$ ! SSP240024 jdf
5335 CHECK_RUN_TOT-=(B_GR_AMT-B_DISC_TAKEN); VEN_TOT-=(B_GR_AMT-B_DISC_TAKEN) ! VEND_BAL+=(B_GR_AMT-B_DISC_TAKEN) SSP240024 jdf
5340 APG[0]=GROSS_AMT ! SSP240024 jdf
5345 IF (KEY_1$=B_KEY_1$) OR (STP(B_KEY_1$)="") THEN {! SSP240024 jdf
5350 IF B_APG$<>APG$ THEN GOTO 5365 ! SSP240024 jdf
5355 FOR I=0 TO 5; IF B_APG[I]<>APG[I] THEN EXITTO 5365; END_IF ; NEXT I ! SSP240024 jdf
5360 RETURN  } ELSE RETURN ! SSP240024 jdf
5365 MSGBOX MSG("REC_ALTERD"),MSG("UPDATE"),"?,YesNo",ANS$; IF ANS$="NO" THEN RETURN ! SSP240024 jdf
5370 WRITE (Z[1],KEY=KEY_1$)IOL=0310 ! SSP240024 jdf
5375 CHECK_RUN_TOT+=APG[0]-APG[1]; VEN_TOT+=APG[0]-APG[1] ! VEND_BAL-=APG[0]-APG[1] SSP240024 jdf
5380 GOSUB CLEAR_INV_FIELD; NEXT_ID=INV_NUM.CTL ! SSP240024 jdf
5385 CALL "*WINGRP;HIDE",UPDT_2.GRP$; CALL "*WINGRP;SHOW",UPDT_1.GRP$ ! SSP240024 jdf
5390 REFRESH_FLG=1 ! SSP240024 jdf
5395 RETURN ! SSP240024 jdf
7000 ! check if printing is in progress
7010 CHECK_PRINT:
7020 FIND (Z[6],KEY=X3$(9,3)+"APC"+X3$(174,4),DOM=*RETURN)
7030 MSGBOX MSG("CHECK_PRIN"),MSG("FYI"),"INFO"
7040 CMD_STR$="END"
7090 EXIT 
7700 ! vendor message display
7710 VENDOR_MESSAGE:
7720 IF PARAM$(67,1)<>"Y" THEN RETURN 
7730 FIND (Z[12],KEY=VENDOR$,DOM=*RETURN)IOL=0340
7740 IF POS(" "<>AP8$(131,120))>0 THEN {
7750 MSG_1$=AP8$(131,60)+SEP+AP8$(191,60)
7760 MSGBOX MSG_1$,MSG("VEND_NOTE"),"INFO" }
7790 RETURN 
8000 ! this  will clear the file.
8010 CLEAR_FILE:
8020 MSG_1$="You have already selected invoices for payment either"+SEP+"through Automatic Invoice Selection, Specific Invoice Selection"+SEP+"or the Manual Check Entry program."+SEP+SEP+"The [YES] option will remove ALL of the selected invoices from the payment file."+SEP+SEP+"The [NO] option will allow additional selection of invoices for payment."
8030 MSGBOX MSG_1$,"Clear File","2,YesNoCancel",ANS$ ! ssp 226320 jdf
8040 IF ANS$="CANCEL" THEN CMD_STR$="END"; RETURN 
8050 IF ANS$="NO" THEN {
8060 REMOVE (Z[1],KEY="",DOM=*NEXT)
8065 GOSUB CHECK_RUN_TOTAL
8070 RETURN  }
8080 PRINT 'DIALOGUE'(10,5,80,5,"Files now being cleared.... Do not Interrupt!!!")
8090 Q$=STR(Z[1]:"00"); CALL "ZZINIT",Q$
8100 PRINT 'POP'
8105 GOSUB CHECK_RUN_TOTAL
8110 REMOVE (Z[1],KEY="",DOM=*NEXT)
8190 RETURN 
8300 ! Invalid Invoice
8310 INVALID_INV:
8320 MSGBOX MSG("INVA_INV"),MSG("FYI"),"!"
8390 EXIT 
8500 ! Calculate Check Run Totals
8510 CHECK_RUN_TOTAL:
8520 CHECK_RUN_TOT=0
8530 READ (Z[1],KEY="",DOM=*NEXT)
8540 READ (Z[1],END=8590)IOL=0310
8550 T1=1
8560 CHECK_RUN_TOT+=(APG[0]-APG[1])*T1
8570 VEN_TOT+=(APG[0]-APG[1])*T1
8580 GOTO 8540
8590 VEN_TOT=0
8600 RETURN 
8700 ! Vendor Total....
8710 VEND_TOT:
8720 VEN_TOT=0
8730 READ (Z[1],KEY=VENDOR$,DOM=*NEXT)
8740 READ (Z[1],END=*RETURN)IOL=0310
8750 IF APG$(1,10)<>VENDOR$ THEN RETURN 
8760 T1=1
8770 VEN_TOT+=(APG[0]-APG[1])*T1
8780 GOTO 8740
8790 RETURN 
9000 ! vendor oh hold
9010 VEND_ONHOLD:
9020 MSGBOX MSG("VEND_H"),MSG("FYI"),"!"
9030 RETURN 
9200 ! Clear Invoices
9210 CLEAR_INV_FIELD:
9220 INV_NUM$=""; INV_DATE$=""; INV_HOLD$=""; ONHOLD_DESC$=""
9230 CHECK_CODE$=""; BANK_CODE$=""; %BANK_ACCT_DESC$=""
9240 INV_DUE_DATE$=""; DISC_EXP$=""; DISC_TYPE$=""; CHECK_CODE$="" ! SSP#230502
9250 REM_AMT=0,ORIG_BAL=0,INV_BAL=0 ! SSP#243112
9260 GROSS_AMT=0; DISC_TAKEN=0; NET_INV_PAID=0
9280 REFRESH_FLG=1
9290 RETURN 
9500 ! get the parameters
9510 GET_PARAM:
9520 READ (Z[6],KEY=X3$(9,3)+"A/P",DOM=WRAPUP)PARAM$
9590 RETURN 
9999 END 
18013 IF NUL(INV_NUM$) THEN RETURN 
56000 ! "198202-AP- APGCBA  - Taking discount when the discount should not  
56001 ! "       be taken when they select ALL invoices for that vendor
56002 ! "211693-APGCBA - Specific Invoice Selection, if deleting any invoice
56003 REM "240024-Program APGCBA Cannot change the bank code on an invoice    
56004 REM "240319-APGCBA-Manual Invoice selection.  You have access to change 
56005 REM "243112-Have a blank bank code on the suggestive payment report.    
