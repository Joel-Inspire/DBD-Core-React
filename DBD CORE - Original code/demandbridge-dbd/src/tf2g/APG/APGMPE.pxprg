0010 ! APGMPE - Terms Code Setup
0035 REM "5.7 - 07/21/09 - 15.605 - tma - SSP# 231633
0037 REM "231633-Can add a "  " terms code in Terms Code Masterfile          
0040 REM "Copyright 2009 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 PROCESS "APGMPE","../APG/AP.EN"
0060 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 ! 
0130 X0$="AP2MPE",X1$="Terms Code Setup"
0140 ! 
0150 X0=-1,X2=-1
0160 CALL "ZZCOMP",X0$,X11$,X2$,X3$,X4$,X5$,X0,X1,X2; IF X1>0 THEN GOSUB WRAPUP
0170 ! 
0190 DIM Z[NUM(X3$(60,3))],AP2[1],B_AP2[1]
0200 ! Files
0210 Z$="01O AP2...  06O ZZPARM  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
0300 ! IOList Section
0310 IOLIST AP2$,AP2{ALL}
0320 IOLIST GL1$
0330 IOLIST VEND$
0400 ! System Parameters
0410 GOSUB GET_SYS_PARAM
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 IF APS$(80,1)="Y" THEN CALL "*wingrp;show",SEC.GRP$
0780 REFRESH_FLG=1
0790 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0970 RETURN 
1000 ! find Record
1010 FIND_REC:
1012 IF STP(TERMS_CODE$)="" THEN NEXT_ID=TERMS_CODE.CTL; RETURN ! SSP#231633
1015 TERMS_CODE$=PAD(TERMS_CODE$,2,0,"0"),TERMS_CODE.CTL'VALUE$=TERMS_CODE$
1020 KEY_1$=TERMS_CODE$
1030 FIND (Z[1],KEY=KEY_1$,DOM=TERM_MSG)IOL=0310
1040 ! 
1050 GOSUB LOAD_INFO
1180 REFRESH_FLG=1,NEXT_ID=TERMS_DESC.CTL
1190 RETURN 
1500 ! Write Record
1510 WRITE_REC:
1520 ! MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"?,YesNo",ANS$
1530 ! IF ANS$="NO" THEN RETURN 
1540 WRITE (Z[1],KEY=KEY_1$)IOL=0310
1550 ! MSGBOX "Record Updated!","FYI","!"
1580 B_AP2$=AP2$; B_AP2[0]=AP2[0]; B_AP2[1]=AP2[1]
1585 GOSUB CLEAR_REC
1590 RETURN 
2000 ! Load Information
2010 LOAD_INFO:
2015 TERMS_CODE$=AP2$(1,2)
2020 TERMS_DESC$=AP2$(3,30)
2025 TERMS_TYPE$=AP2$(33,1)
2027 IF TERMS_TYPE$="P" THEN CALL "*wingrp;Enable",DAY.GRP$; CALL "*wingrp;Disable",PER.GRP$ ELSE CALL "*wingrp;Disable",DAY.GRP$; CALL "*wingrp;Enable",PER.GRP$
2030 DAYS_TILL_DUE$=AP2$(34,3); DAYS_IN_PERIOD$=AP2$(37,2)
2035 CUTOFF_DAY_IN_MONTH$=AP2$(39,2); DUE_DATE_2ND$=AP2$(41,2)
2040 DISC_DUE_DATE_TYPE$=AP2$(43,1); DAYS_TILL_DUE_2ND$=AP2$(44,3)
2043 IF DISC_DUE_DATE_TYPE$="P" THEN CALL "*wingrp;Disable",PER2.GRP$; CALL "*wingrp;Enable",DAY2.GRP$; CALL "*wingrp;Enable",SEC.GRP$; CALL "*wingrp;Enable",SEC2.GRP$ ELSE CALL "*wingrp;Disable",DAY2.GRP$; CALL "*wingrp;Enable",PER2.GRP$; CALL "*wingrp;Enable",SEC.GRP$; CALL "*wingrp;Disable",SEC2.GRP$
2045 DUE_DAY$=AP2$(47,2); CUTOFF_DAY_2ND$=AP2$(49,2)
2050 DAYS_IN_PERIOD_2ND$=AP2$(51,2); DAYS_DUE_2ND$=AP2$(53,3)
2055 PRCNT_DISC_ALLOWED=AP2[0]; PERCENTAGE_2ND=AP2[1]
2160 CALL "*wingrp;Enable",BUTTON_1.GRP$; CALL "*wingrp;Enable",TERM.GRP$
2170 B_AP2$=AP2$; B_AP2[0]=AP2[0]; B_AP2[1]=AP2[1]
2180 REFRESH_FLG=1
2190 RETURN 
2300 ! Load Slections
2310 LOAD_SELECTION:
2315 IF LEN(AP2$)=0 THEN DIM AP2$(56)
2320 AP2$(1,2)=TERMS_CODE$
2330 AP2$(3,30)=TERMS_DESC$
2340 AP2$(33,1)=TERMS_TYPE$
2350 AP2$(34,3)=DAYS_TILL_DUE$; AP2$(37,2)=DAYS_IN_PERIOD$
2360 AP2$(39,2)=CUTOFF_DAY_IN_MONTH$; AP2$(41,2)=DUE_DATE_2ND$
2370 AP2$(43,1)=DISC_DUE_DATE_TYPE$; AP2$(44,3)=DAYS_TILL_DUE_2ND$
2380 AP2$(47,2)=DUE_DAY$; AP2$(49,2)=CUTOFF_DAY_2ND$
2390 AP2$(51,2)=DAYS_IN_PERIOD_2ND$; AP2$(53,3)=DAYS_DUE_2ND$
2400 AP2[0]=PRCNT_DISC_ALLOWED; AP2[1]=PERCENTAGE_2ND
2410 ! B_AP2$=AP2$
2490 RETURN 
2500 ! Delete Record
2510 DELETE_REC:
2520 MSGBOX MSG("DELETE_REC"),MSG("CONFIRM"),"?,YesNo",ANS$
2530 IF ANS$="NO" THEN RETURN 
2540 REMOVE (Z[1],KEY=KEY_1$,DOM=*NEXT)
2550 GOSUB CLEAR_REC
2590 RETURN 
2600 ! Term Type 1 Options
2610 TERM_OPT_1:
2613 IF NUL(TERMS_TYPE$) THEN NEXT_ID=TERMS_TYPE.CTL; RETURN 
2615 IF STP(TERMS_TYPE$)="P" THEN {
2620 CALL "*WINGRP;DISABLE",PER.GRP$
2625 CALL "*WINGRP;ENABLE",DAY.GRP$
2630  } ELSE {
2635 CALL "*WINGRP;DISABLE",DAY.GRP$
2640 CALL "*WINGRP;ENABLE",PER.GRP$
2650  }
2680 REFRESH_FLG=1
2690 RETURN 
2700 ! Term type 2 options
2710 TERM_OPT_2:
2713 IF NUL(DISC_DUE_DATE_TYPE$) THEN RETURN 
2715 IF STP(DISC_DUE_DATE_TYPE$)="P" THEN {
2720 CALL "*WINGRP;DISABLE",PER2.GRP$
2725 CALL "*WINGRP;ENABLE",DAY2.GRP$
2730 CALL "*WINGRP;ENABLE",SEC.GRP$; CALL "*WINGRP;ENABLE",SEC2.GRP$
2735  } ELSE {
2740 CALL "*WINGRP;DISABLE",DAY2.GRP$
2745 CALL "*WINGRP;ENABLE",PER2.GRP$
2750 CALL "*WINGRP;ENABLE",SEC.GRP$; CALL "*WINGRP;DISABLE",SEC2.GRP$
2755  }
2780 REFRESH_FLG=1
2790 RETURN 
3000 ! First Record
3010 FIRST_REC:
3020 KEY_1$=KEF(Z[1],END=NO_FIRST)
3030 READ (Z[1],KEY=KEY_1$)IOL=0310
3040 GOSUB LOAD_INFO
3090 RETURN 
3200 ! Last Record
3210 LAST_REC:
3220 KEY_1$=KEL(Z[1],ERR=NO_LAST)
3230 READ (Z[1],KEY=KEY_1$)IOL=0310
3240 GOSUB LOAD_INFO
3290 RETURN 
3400 ! Prior Record
3410 PRIOR_REC:
3420 KEY_1$=KEP(Z[1],END=START_FILE)
3430 READ (Z[1],KEY=KEY_1$)IOL=0310
3440 GOSUB LOAD_INFO
3490 RETURN 
3600 ! Next Record
3610 NEXT_REC:
3620 KEY_1$=KEY(Z[1],END=END_FILE)
3630 READ (Z[1],KEY=KEY_1$)IOL=0310
3640 GOSUB LOAD_INFO
3690 RETURN 
5000 ! Check Changes
5010 CHECK_CHANGES:
5020 IF AP2$=B_AP2$ AND AP2[0]=B_AP2[0] AND AP2[1]=B_AP2[1] OR B_AP2$="" THEN RETURN 
5030 MSGBOX MSG("REC_ALTERD"),MSG("UPDATE"),"?,YesNo",ANS$
5040 IF ANS$="NO" THEN RETURN 
5050 GOSUB WRITE_REC
5090 RETURN 
8000 ! Messages
8010 TERM_MSG:
8020 MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"?,YesNo",ANS$
8030 IF ANS$="NO" THEN TERMS_CODE$=""; NEXT_ID=TERMS_CODE.CTL; RETURN 
8040 NEXT_ID=TERM_DESC.CTL
8080 CALL "*wingrp;Enable",BUTTON_1.GRP$; CALL "*wingrp;Enable",TERM.GRP$; REFRESH_FLG=1; CALL "*WINGRP;ENABLE",TYPE_P.GRP$
8090 RETURN 
8100 ! No First Record
8110 NO_FIRST:
8120 MSGBOX MSG("REC_NO_1ST"),"FYI","!"
8130 RETURN 
8200 ! No Last Record
8210 NO_LAST:
8220 MSGBOX MSG("REC_NO_LST$"),"FYI","!"
8230 RETURN 
8300 ! Start of File
8310 START_FILE:
8320 MSGBOX MSG("START_FILE"),"FYI","!"
8330 RETURN 
8400 ! End of File
8410 END_FILE:
8420 MSGBOX MSG("END_OF_FIL"),"FYI","!"
8430 RETURN 
8500 ! Invalid Day Message
8510 INVALID_MSG:
8520 MSGBOX MSG("INVALID_D"),"FYI","!"
8590 RETURN 
9000 ! Parameters
9010 GET_SYS_PARAM:
9020 READ (Z[6],KEY=X3$(9,3)+"A/P",DOM=WRAPUP)APS$
9090 RETURN 
9999 END 
10000 CLEAR_REC:
10010 TERMS_CODE$="",TERMS_DESC$="",TERMS_TYPE$="",DAYS_TILL_DUE$=""
10020 DAYS_IN_PERIOD$="",CUTOFF_DAY_IN_MONTH$="",DUE_DATE_2ND$=""
10030 DISC_DUE_DATE_TYPE$="",DAYS_TILL_DUE_2ND$=""
10040 DUE_DAY$="",CUTOFF_DAY_2ND$="",DAYS_IN_PERIOD_2ND$=""
10050 DAYS_DUE_2ND$="",PRCNT_DISC_ALLOWED=0,PERCENTAGE_2ND=0
10065 NEXT_ID=TERMS_CODE.CTL
10070 B_AP2$="",AP2$="",B_AP2{ALL}=0,AP2{ALL}=0
10075 CALL "*WINGRP;DISABLE",FIELDS.GRP$; CALL "*WINGRP;DISABLE",TYPE_P.GRP$
10080 CALL "*WINGRP;DISABLE",SEC.GRP$; CALL "*WINGRP;DISABLE",SEC2.GRP$
10085 REFRESH_FLG=1,CHANGE_FLG=0
10090 RETURN 
