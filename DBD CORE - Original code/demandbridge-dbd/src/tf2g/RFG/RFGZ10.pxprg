0010 ! RFGZ10 - WMS Label Printing             
0035 REM "5.7 - 08/16/19 - 16.686388 - crg - SSP# 307352-DBD-176
0037 REM "307352-DBD-176-WMS Label printing - spaces in installation folder path will
0040 REM "Copyright 2019 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 IF TCB(13)=1 THEN BEGIN 
0052 IF NUL(%WDX$) THEN MSGBOX "You must be using Windx to access this program"; GOTO 0075 ! SSP#232409
0055 IF NOT(%GUI) THEN CHUI_MODE=1; %GUI=1
0060 PROCESS "RFGZ10","../RFG/RF.EN"
0065 IF CHUI_MODE THEN %GUI=0; RUN "ZMENU"
0075 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0099 ! **************************************************
0110 INIT:
0115 %FLMAINT_MSG$="./lib/_tfmsglib.en"
0120 _SV_KR=PRM('KR'); SET_PARAM 'KR'=0 ! Ensure native handling of KEP()
0125 CALL "ZZWMX3"
0130 GOSUB SETUP_LIBRARY; IF %FLMAINT_MSG$<>"" THEN MESSAGE_LIB %FLMAINT_MSG$
0140 CHANGE_FLG=0,_ENABLE_FLG=0,_KCNT=1,_FIRST_KEY=0,_FIRST_FIELD=0
0160 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; DIM Z[NUM(X3$(60,3))]
0165 Z$="01O RFL... 02O RFQ... 03O AR1...  04O IC0...  13O ZZPARM "
0170 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
0175 IC0=Z[4]
0186 LENGTH_OF_TIMEOUT=5,SHOW_ALL_QUES=1
0191 IF TCB(88)<>0 THEN %WDX$="[wdx]" ELSE %WDX$=""
0192 %LABEL_PTR_DESC$=""
0195 RETURN 
0299 ! **************************************************
0300 ! 300 - Set up the message library
0310 SETUP_LIBRARY:
0320 _SV_MSGLIB$=MSG(*)
0330 _LANG$=LCS(%NOMAD_DEF_SFX$); IF _LANG$="" THEN _LANG$=LCS(ENV("PVXLANG")); IF _LANG$="" THEN _LANG$=LCS(ENV("LANG")); IF _LANG$="" THEN _LANG$="en"
0340 MESSAGE_LIB "*msglib."+_LANG$,ERR=*NEXT; GOTO 0360
0350 MESSAGE_LIB "*msglib.en"
0360 SET_MSG:GOSUB SETUP_MESSAGES
0370 RETURN 
0399 ! **************************************************
0400 ! ^100 - Retrieve messages from the *msglib.xxx library
0405 SETUP_MESSAGES:
0410 _FYI$=MSG("FYI"),_ERROR$=MSG("ERROR")
0415 _MSG_DIRECTORY$=MSG("DIRECTORY")
0420 _MSG_END_OF_FIL$=MSG("END_OF_FIL")
0425 _MSG_FILOPNERR1$=MSG("FILOPNERR1")
0430 _MSG_JUST_CHECK$=MSG("JUST_CHECK")
0435 _MSG_MANDATORY$=MSG("MANDATORY")
0440 _MSG_NEXT$=MSG("NEXT")
0445 _MSG_NON_NUMER$=MSG("NON_NUMER")
0450 _MSG_NOT_FOUND$=MSG("NOT_FOUND")
0455 _MSG_OVRWRT_CHG$=MSG("OVRWRT_CHG")
0460 _MSG_PRECEDING$=MSG("PRECEDING")
0465 _MSG_PREFIX$=MSG("PREFIX")
0470 _MSG_REC_ACCESS$=MSG("REC_ACCESS")
0475 _MSG_REC_ALTERD$=MSG("REC_ALTERD")
0480 _MSG_REC_CR_NEW$=MSG("REC_CR_NEW")
0485 _MSG_REC_LOCKED$=MSG("REC_LOCKED")
0490 _MSG_X$=MSG("REC_MISS","^"),P=POS("^"=_MSG_X$),_MSG_REC_MISS1$=_MSG_X$(1,P-1),_MSG_REC_MISS2$=_MSG_X$(P+1)
0495 _MSG_REC_NOTFND$=MSG("REC_NOTFND")
0500 _MSG_REC_NO_1ST$=MSG("REC_NO_1ST")
0505 _MSG_REC_NO_LST$=MSG("REC_NO_LST")
0510 _MSG_REC_REMOVE$=MSG("REC_REMOVE")
0515 _MSG_REC_UPDADD$=MSG("REC_UPDADD")
0520 _MSG_X$=MSG("REC_VFYDEL","^"),P=POS("^"=_MSG_X$),_MSG_REC_VFYDEL1$=_MSG_X$(1,P-1),_MSG_REC_VFYDEL2$=_MSG_X$(P+1)
0525 _MSG_X$=MSG("REC_VIEW","^"),P=POS("^"=_MSG_X$),_MSG_REC_VIEW1$=_MSG_X$(1,P-1),_MSG_REC_VIEW2$=_MSG_X$(P+1)
0530 _MSG_REQ_FIELDS$=MSG("REQ_FIELDS")
0535 _MSG_START_FILE$=MSG("START_FILE")
0540 _MSG_UPDATE$=MSG("UPDATE")
0545 _MSG_UPD_OTHER1$=MSG("UPD_OTHER1")
0550 _MSG_UPD_OTHER2$=MSG("UPD_OTHER2")
0555 _MSG_UPD_SAME$=MSG("UPD_SAME")
0560 _MSG_WARNING$=MSG("WARNING")
0565 RETURN 
0595 RETURN 
0599 ! **************************************************
0600 MAIN_POST_DISPLAY:
0605 RESPCTL=0,PRT=0
0630 PRINT 'MESSAGE'('_RED'+'WHITE'+"WAITING "+STR(LENGTH_OF_TIMEOUT)+" SEC",0)
0633 GOSUB UPDATE_STATUS ! 268710
0635 INPUT (0,LEN=1,TIM=LENGTH_OF_TIMEOUT,ERR=*NEXT)WAITPROMPT$
0636 PRINT 'MESSAGE'('F8'+'BLACK'+"",0),
0637 FCOUNT=0,PRT=0
0640 RESPCTL=CTL
0642 IF RESPCTL=10004 THEN CMD_STR$="END"; GOTO 0695
0650 GOSUB PRINT_DOCUMENTS
0680 GOTO 0600
0695 RETURN 
0699 ! **************************************************
0900 ! 900 - Wrapup
0910 WRAPUP:
0912 %NOMAD_TIMEOUT=0
0915 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0950 MESSAGE_LIB _SV_MSGLIB$
0960 SET_PARAM 'KR'=_SV_KR
0965 ! INVOKE %WDX$+APP$+" /X" ! closes application
0970 %LABEL_PTR_DESC$=""
0990 RETURN 
1100 PRINT_DOCUMENTS:! Scan the directories of active documents & print them
1101 PRINT 'MESSAGE'('_GREEN'+'WHITE'+"CHECKING FOR LABELS TO PRINT",0); WAIT 1
1102 OK_TO_PRINT=0
1105 CALL "UPDRFQ;READBYKEY",Z[2],0,LABEL_PTR$,SEG2$,SEG3$,SEG4$,SEG5$,RFQ$,RFQ{ALL},FOUNDRFQ
1110 CALL "UPDRFQ;READNEXT",Z[2],RFQ$,RFQ{ALL},FOUNDRFQ; IF FOUNDRFQ THEN IF RFQ$(1,10)<>LABEL_PTR$ THEN FOUNDRFQ=0
1120 IF FOUNDRFQ THEN {
1125 F$=STP(RFL$(111,60),2)
1130 SERIAL %WDX$+F$,ERR=*PROCEED
1135 TMP_CHAN=HFN; OPEN PURGE (TMP_CHAN,OPT="TEXT",ERR=1195)%WDX$+F$
1140 OK_TO_PRINT=1
1145  }
1150 WHILE FOUNDRFQ
1160 GOSUB CREATE_TEXT
1175 CALL "UPDRFQ;READNEXT",Z[2],RFQ$,RFQ{ALL},FOUNDRFQ; IF FOUNDRFQ THEN IF RFQ$(1,10)<>LABEL_PTR$ THEN FOUNDRFQ=0
1180 WEND 
1190 IF OK_TO_PRINT THEN GOSUB CALL_PROGRAM
1195 RETURN 
1199 ! **************************************************
1200 CREATE_TEXT:
1210 DIM CUST_NAME$(35); CALL "UPDAR1;READBYKEY",Z[3],0,RFQ$(27,10),SEG2$,SEG3$,SEG4$,SEG5$,AR1$,AR1{ALL},FOUNDAR1; IF FOUNDAR1 THEN CUST_NAME$=AR1$(11,35)
1215 DIM CTN_PACK$(6); CALL "UPDIC0;READBYKEY",IC0,0,RFQ$(27,20),SEG2$,SEG3$,SEG4$,SEG5$,IC0$,IC0{ALL},FOUNDIC0; IF FOUNDIC0 AND IC0[19]>0 AND RFQ$(167,10)=DIM(10) THEN CTN_PACK$=STR(IC0[19])
1217 IF RFQ$(167,10)<>DIM(10) THEN CTN_PACK$=STR(NUM(RFQ$(167,10),ERR=*NEXT))
1251 PRINT 'MESSAGE'('_BLUE'+'WHITE'+"NOW PRINTING ",0)
1252 ! LOT NUM/ITEMNUM/ITEMDESC/RECPTDATE/LOCATION/UOM
1255 J$=RFQ$(19,8)+"~"+RFQ$(27,20)+"~"+RFQ$(47,40)+"~"+FN%PRINT_DATETIME$(FN%GET_DATETIME(RFQ$(87,6),0),"MM/DD/YYYY")+"~"+RFQ$(93,4)+"~"+RFQ$(124,4)+"~"+RFQ$(152,15)+"~"+RFQ$(132,20)+"~"+CUST_NAME$+"~"+RFQ$(97,9)+"~"+CTN_PACK$+"~"+RFQ$(27,10)+"~"+RFQ$(37,10) ! 269617
1260 COUNT=1; IF NUM(RFQ$(128,4),ERR=*NEXT)>0 THEN COUNT=NUM(RFQ$(128,4))
1265 FOR J=1 TO COUNT
1270 PRINT (TMP_CHAN)J$
1275 NEXT J
1277 CALL "UPDRFQ;DELETE",Z[2],RFQ$,RFQ{ALL},FOUNDRFQ2,BUSYRFQ2
1290 RETURN 
1298 ! 
1299 ! 
9999 END 
10000 SELECT_LABEL_PTR:
10010 IF LABEL_PTR$<>"" THEN LABEL_PTR$=PAD(LABEL_PTR$,10); CALL "UPDRFL;READBYKEY",RFL,0,LABEL_PTR$,SEG2$,SEG3$,SEG4$,SEG5$,RFL$,RFL{ALL},FOUNDRFL; IF NOT(FOUNDRFL) THEN MSGBOX MSG("NOT_FOUND"); NEXT_ID=LABEL_PTR.CTL; GOTO *RETURN
10020 IF FOUNDRFL THEN {
10030 APP$=STP(RFL$(171,60),2),PRD_FMT$=STP(RFL$(51,60))
10070 ! INVOKE %WDX$+APP$+" "+PRD_FMT$
10080 ! GOSUB MAIN_POST_DISPLAY
10082 REFRESH_FLG=1
10084 CALL "*wingrp;enable",BEGIN.GRP$
10085  }
10090 RETURN 
10098 ! 
10099 ! 
10100 SELECT_BEGIN:
10150 GOSUB MAIN_POST_DISPLAY
10190 RETURN 
11022 PRINT 'MESSAGE'("Checking files",0)
15000 CALL_PROGRAM:
15010 CLOSE (TMP_CHAN)
15100 CASE LABEL_OVER$
15200 ! product label
15300 ! carton label
15400 ! shipping label
15500 ! order liscense plate
15800 DEFAULT 
15805 ! INVOKE WAIT %WDX$+APP$+" /P"
15810 IF STP(APP$,2)<>"" THEN INVOKE WAIT %WDX$+QUO+APP$+QUO+" "+QUO+PRD_FMT$+QUO+" /P /X"
15990 RETURN 
15998 ! 
15999 ! 
20000 REFRESH_PDF_MONITOR:
20103 CURR_GRID_INDEX=GRID_INDEX,GRID_INDEX=0
20105 LBL_MONITOR_IOL:IOLIST PRINTER_ID$,LBL_TYPE$,LOT_NUMBER$,ITEM_CUST_CODE$,ITEM_CODE$
20106 CALL "UPDRFQ;READBYKEY",Z[2],0,LABEL_PTR$,SEG2$,SEG3$,SEG4$,SEG5$,RFQ$,RFQ{ALL},FOUNDRFQ
20107 CALL "UPDRFQ;READNEXT",Z[2],RFQ$,RFQ{ALL},FOUNDRFQ; IF FOUNDRFQ THEN IF RFQ$(1,10)<>LABEL_PTR$ THEN FOUNDRFQ=0
20112 PRINT 'MESSAGE'("Checking files",0)
20115 WHILE FOUNDRFQ
20124 READ DATA FROM R$ TO IOL=LBL_MONITOR_IOL
20170 GRID LOAD PDF_MONITOR.CTL,0,++GRID_INDEX,REC(IOL=LBL_MONITOR_IOL)
20174 GRID LOCK PDF_MONITOR.CTL,0,GRID_INDEX,ERR=*PROCEED
20178 CALL "UPDRFQ;READNEXT",Z[2],RFQ$,RFQ{ALL},FOUNDRFQ; IF FOUNDRFQ THEN IF RFQ$(1,10)<>LABEL_PTR$ THEN FOUNDRFQ=0
20179 WEND 
20190 IF GRID_INDEX<CURR_GRID_INDEX THEN FOR II=CURR_GRID_INDEX TO GRID_INDEX+1 STEP -1; GRID DELETE PDF_MONITOR.CTL,0,II,ERR=*PROCEED; NEXT II
20195 RETURN 
20199 ! **************************************************
21000 UPDATE_STATUS:
21020 CALL "UPDSV0;UPDATE_STATUS",ERR=*NEXT,"WMSLABELPR",LABEL_PTR$,"","WMS Label Printer - "+LABEL_PTR$,"RFGZ10",STR(TCB(89))
21045 RETURN 
21049 ! 
56000 REM "180032-Wireless Warehousing                                        
56002 REM "203846-Modify WMS Label Monitor to user Bartender Commander        
56004 REM "207587-Inventory Label Print cannot be used as delivered           
56005 REM "232409-All WMS menu options.  Need to take out TopForm from msg    
56006 REM "268710-Server status reporting framework - new file for servers to 
56007 REM "269617-Installation and Implementation DB Warehouse Management     
56008 REM "307352-DBD-176-WMS Label printing - spaces in installation folder path will
