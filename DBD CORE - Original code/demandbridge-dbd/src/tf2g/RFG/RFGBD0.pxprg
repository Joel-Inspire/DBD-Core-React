0010 REM "WMS Quick Bin Move  Detail Display<RFGBD0"
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 08/12/11 - 17.584166 - crg - SSP# 248515
0037 REM "248515-Incomplete info being recorded for WMS Bin moves to RFA
0040 REM "Copyright 2011 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT
0100 SETERR 9000
0110 X0$="RFGBD0",X1$="WMS Quick Bin Move Detail Display"
0120 EOL$=$0D0A$; REM EOL$=ATH(STP(EC_PARM$(57,8),1))
0127 HTML_TEXT$="Content-type: text/html"
0140 DIM MESS_INFO$[20] ! ,EC_PARM$(1275)(1275)
0230 DIM ESY$(512)
0240 DIM ECC$(1984),ECC[3]
0250 DIM ESZ$(128),ESZ[18]
0330 IOLIST ESY$
0340 IOLIST ECC$,ECC[0],ECC[1],ECC[2]
0350 IOLIST ESZ$,ESZ[0],ESZ[1],ESZ[2],ESZ[3],ESZ[4],ESZ[5],ESZ[6],ESZ[7],ESZ[8],ESZ[9],ESZ[10],ESZ[11],ESZ[12],ESZ[13],ESZ[14],ESZ[15],ESZ[16],ESZ[17],ESZ[18]
0370 IOLIST ZW2$,ZW2[0],ZW2[1],ZW2[2]
0380 IOLIST ZW9A$,ZW9B$
0400 IOLIST ZW7A$,ZW7B$
0410 IOLIST ZW8A$
0500 REM "Open files
0502 CLOSE (13); OPEN (13)"ZZPARM"
0505 DIM Z[NUM(X3$(60,3))],BLGRBNKS$(100)
0510 Z$="01O IC0...  02O IC1...  03O ICF...  04O FM1...  05O RFA...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0550 IC0=Z[1],IC1=Z[2],ICF=Z[3]
0555 FM1=Z[4],RFA=Z[5]
0900 PRINT_DOC=0
0910 FIRST_IND=0,MX_QTY=0 ! SSP 212487
1000 REM "Process DATA array
1010 FOR I=1 TO NUM_ENTRIES
1015 TAG$=DATA$[I,0],TF_VALUE$=DATA$[I,1]
1050 SWITCH TAG$ ! 1185 end switch
1055 CASE "print_doc"
1057 PRINT_DOC=1; BREAK
1058 CASE "show_tags"
1059 SHOW_TAGS=1; BREAK
1080 CASE "template"
1082 TEMPLATE$=TF_VALUE$; BREAK
1085 CASE "last_item_desc"
1087 LAST_ITEM_DESC$=TF_VALUE$; BREAK
1090 CASE "last_item_qty"
1092 LAST_ITEM_QTY$=TF_VALUE$; BREAK
1095 CASE "barcode"
1097 BARCODE$=TF_VALUE$; BREAK
1100 CASE "last_item"
1102 LAST_ITEM$=TF_VALUE$; BREAK
1110 CASE "whs_loc"
1112 WHS$=TF_VALUE$; BREAK
1200 CASE "bin_loc"
1205 BIN_LOC$=PAD(TF_VALUE$,10); BREAK
1985 END SWITCH ! 1050
1990 NEXT I
2000 ! process data
2020 SESSION_ID$=EC$[1](1,7)
2025 BUYER$=EC$[2](1,15),BASIC$="RFGBD0"
2027 TEMPLATE$="rfgbd0.htm"
2030 MSG$="",FOUNDITEM=0,LOT$="N"
2500 ! barcode
2510 IF BARCODE$<>"" THEN CALL "RFGBAR;BARCODE",ICF,IC0,FM1,RFI,PO2,RF1,BARCODE$,TF_ITEM$,FOUNDBAR,LOT,LOT_NUM$
2550 IF NOT(FOUNDBAR) THEN {! 2590                                         
2555 IF BARCODE$<>"" THEN {
2560 MSG$="Item "+BARCODE$+" Cannot Be Found",FOUNDITEM=0
2565 WRONG_SCAN=1
2570 CALL "UPDRFA;ADD_BM",RFA,X3$,PAD(BARCODE$,20),BUYER$,PAD(BIN_LOC$,10),DIM(10),PAD(WHS$,4),DIM(8),0,WRONG_SCAN
2585  }
2590  } ! 2550
2600 IF FOUNDBAR AND NOT(LOT) THEN {! 2990 }
2610 LOT$="N"; CALL "UPDIC0;READBYKEY",Z[5],0,TF_ITEM$,SEG2$,SEG3$,SEG4$,SEG5$,IC0$,IC0{ALL},FOUNDIC0; IF FOUNDIC0 THEN IF IC0$(112,1)="Y" THEN LOT$="Y"
2615 IF FOUNDIC0 THEN {! 2950 
2620 IF LOT$="N" THEN {! 2690
2625 MSG$="Only Lot Items can be moved"
2627 WRONG_SCAN=1
2630 CALL "UPDRFA;ADD_BM",RFA,X3$,IC0$(1,20),BUYER$,PAD(BIN_LOC$,10),DIM(10),PAD(WHS$,4),DIM(8),0,WRONG_SCAN
2690  } ! 2620
2700 IF LOT$="Y" THEN {! 2790
2710 MSG$="Lot Serial Number MUST be Scanned for Lot Items"
2715 WRONG_SCAN=1
2720 CALL "UPDRFA;ADD_BM",RFA,X3$,IC0$(1,20),BUYER$,PAD(BIN_LOC$,10),DIM(10),PAD(WHS$,4),DIM(8),0,WRONG_SCAN
2790  } ! 2700
2950  } ! 2615
2960 IF NOT(FOUNDIC0) THEN {! 2980}
2965 MSG$="Item is not setup as an inventory item"
2967 WRONG_SCAN=1
2970 CALL "UPDRFA;ADD_BM",RFA,X3$,PAD(BARCODE$,20),BUYER$,PAD(BIN_LOC$,10),DIM(10),PAD(WHS$,4),DIM(8),0,WRONG_SCAN
2980  } ! 2960
2990  } ! 2600
3000 IF FOUNDBAR AND LOT THEN {! 3800
3002 CALL "UPDIC0;READBYKEY",Z[5],0,TF_ITEM$,SEG2$,SEG3$,SEG4$,SEG5$,IC0$,IC0{ALL},FOUNDIC0
3005 IF FOUNDIC0 THEN {! 3397
3010 CALL "UPDIC1;READBYKEY",IC1,0,TF_ITEM$,WHS$,SEG3$,SEG4$,SEG5$,IC1$,IC1{ALL},FOUNDIC1
3050 IF NOT(FOUNDIC1) THEN {! 3090
3055 CALL "UPDIC1;READBYKEY",IC1,0,TF_ITEM$,"9",WHS$(2),SEG4$,SEG5$,IC1_2$,IC1_2{ALL},FOUNDIC1_2
3060 IF NOT(FOUNDIC1) THEN IF FOUNDIC1_2 THEN MSG$="Item is not in whs "+WHS$+" But is in Whs 9"+WHS$(2) ELSE MSG$="Item is not in whs "+WHS$
3065 WRONG_SCAN=1
3070 CALL "UPDRFA;ADD_BM",RFA,X3$,IC0$(1,20),BUYER$,PAD(BIN_LOC$,10),DIM(10),PAD(WHS$,4),DIM(8),0,WRONG_SCAN
3090  } ! 3050
3100 IF FOUNDIC1 THEN {! 3395
3105 FOUND_BIN=0,TOT_QTY=0,OK_ICF=0
3110 CALL "UPDICF;READBYKEY",ICF,4,LOT_NUM$,SEG2$,SEG3$,SEG4$,SEG5$,ICF$,ICF{ALL},FOUNDICF
3120 CALL "UPDICF;READNEXT",ICF,ICF$,ICF{ALL},FOUNDICF; IF FOUNDICF THEN IF ICF$(234,8)<>LOT_NUM$ THEN FOUNDICF=0 ELSE OK_ICF=1; IF BIN_LOC$<>ICF$(100,10) THEN GOTO *SAME ELSE FOUND_BIN=1; IF ICF$(110,11)<>DIM(11) THEN GOTO *SAME ELSE TOT_QTY=TOT_QTY+ICF[4],ITEM_UOM$=ICF$(121,4); GOSUB QTY_DISPLAY; GOTO *SAME ! SSP 212487
3121 ! CALL "UPDICF;READNEXT",ICF,ICF$,ICF{ALL},FOUNDICF; IF FOUNDICF THEN IF ICF$(234,8)<>LOT_NUM$ THEN FOUNDICF=0 ELSE OK_ICF=1; IF BIN_LOC$<>ICF$(100,10) THEN GOTO *SAME ELSE FOUND_BIN=1; IF ICF$(110,11)<>DIM(11) THEN GOTO *SAME ELSE TOT_QTY=TOT_QTY+ICF[4]; GOTO *SAME ! SSP 212487
3200 IF OK_ICF THEN {! 3290
3205 FOUNDICF=1
3220 IF FOUND_BIN AND TOT_QTY>0 THEN BASIC$="RFGBD1",TEMPLATE$="rfgbd1.htm",ITEM_DESC$=IC0$(21,40),ITEM_ON_HAND$=STR(TOT_QTY),OLD_BIN$=BIN_LOC$; IF MX_QTY=1 THEN ITEM_QTY_PER_UNIT$="*** Mixed Packaging ***" ! SSP 212487
3221 ! IF FOUND_BIN AND TOT_QTY>0 THEN BASIC$="RFGBD1",TEMPLATE$="rfgbd1.htm",ITEM_DESC$=IC0$(21,40),ITEM_UOM$=ICF$(121,4),ITEM_QTY_PER_UNIT$=STR(ICF[3]),ITEM_ON_HAND$=STR(TOT_QTY),OLD_BIN$=BIN_LOC$
3225 IF FOUND_BIN AND TOT_QTY<=0 THEN {! SSP 212487
3226 MSG$="No Items to transfer from this Bin Location" ! ssp 212487
3227 WRONG_SCAN=1 ! ssp 212487
3228 CALL "UPDRFA;ADD_BM",RFA,X3$,MID(IC0$,1,20),BUYER$,PAD(BIN_LOC$,10),DIM(10),PAD(WHS$,4),LOT_NUM$,0,WRONG_SCAN ! ssp 212487, 248515
3229  } ! SSP 212487
3230 IF NOT(FOUND_BIN) THEN {! 3280                                           
3235 MSG$="Item should not be in this Bin Location"
3237 WRONG_SCAN=1
3240 CALL "UPDRFA;ADD_BM",RFA,X3$,PAD(BARCODE$,20),BUYER$,PAD(BIN_LOC$,10),DIM(10),PAD(WHS$,4),DIM(8),0,WRONG_SCAN
3280  }
3290  } ! 3200
3300 IF NOT(FOUNDICF) THEN {! 3390
3310 MSG$="Lot Record cannot be found",WRONG_SCAN=1
3315 CALL "UPDRFA;ADD_BM",RFA,X3$,IC0$(1,20),BUYER$,PAD(BIN_LOC$,10),DIM(10),PAD(WHS$,4),PAD(LOT_NUM$,8),0,WRONG_SCAN
3320 BASIC$="RFGBD0",TEMPLAT$="rfgbd0.htm"
3390  } ! 3300
3395  } ! 3100
3397  } ! 3005
3400 IF NOT(FOUNDIC0) THEN {! 3490}
3405 MSG$="Item is not set up in inventory",WRONG_SCAN=1
3410 CALL "UPDRFA;ADD_BM",RFA,X3$,PAD(BARCODE$,20),BUYER$,PAD(BIN_LOC$,10),DIM(10),PAD(WHS$,4),DIM(8),0,WRONG_SCAN
3490  } ! 3400
3800  } ! 3000
3999 DIM BYBINFO$[5]; BYBINFO$[1]=PGN,BYBINFO$[2]="180032.20060308",BYBINFO$[3]=TEMPLATE$,BYBOPTIONS$=""
4000 SWITCH TEMPLATE$
4010 CASE "rfgbd1.htm"
4020 PERFORM "RFGBDT;PROCESS_TEMPLATE_RFGBD1"
4030 BREAK
4100 DEFAULT 
4110 PERFORM "RFGBDT;PROCESS_TEMPLATE_RFGBD0"
4200 END SWITCH 
5000 GOTO 9900
5010 LINE$=TAG$+" "+TAGDESC$
5090 RETURN 
5100 ADD_MSG_TO_LOG:! Add message to log file
5110 MOUT$="SESSION ID|"+EC$[1](1,7)+"|FILE|"+%WEBEC_FILE_NAME$+"|WORKSHEET|"+ECD$(1,10)+"|LINE|"+ECE$(25,3)+"|MESSAGE NO|"+STR(MESSAGE_NO:"00000")+"|MESSAGE|"+MESSAGE$+"|"
5135 MX=FN%_LOG_MESSAGE("MESG",MOUT$)
5145 RETURN 
5149 ! 
5490 RETURN ! 5200
5500 REM create description html
5590 RETURN 
6000 QTY_DISPLAY:! SSP 212487 To determine if different qty per units. If so, message of "Mixed Packaging" will be displayed
6010 IF FIRST_IND=0 THEN FIRST_IND=1,ITEM_QTY_PER_UNIT$=STR(ICF[3]) ! SSP 212487
6020 IF STR(ICF[3])<>ITEM_QTY_PER_UNIT$ THEN MX_QTY=1 ! SSP 212487
6050 RETURN ! SSP 212487
8150 BYB_PROCESSING:! Send LINE$ to EC3BYB for generic fields processing
8160 CALL "EC3BYB",ERR=8161,X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT,LINE$,BYBINFO${ALL},BYBOPTIONS$
8195 RETURN 
8910 DEF FND$(Z9$)=Z9$(3,2)+"/"+Z9$(5,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
9000 REM "ERROR PROCESSING
9002 J=ERR,K=TCB(5); SETERR 0000; ENDTRACE 
9010 LAST_WEBEC_ERR$="ERR: "+STR(J)+" AT: "+STR(K); LAST_WEBEC_ERR$=GBL("LAST_WEBEC_ERR",LAST_WEBEC_ERR$); REM "131928
9090 GOTO 9900
9900 REM "End
9905 CLOSE (100)
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9915 CLOSE (13)
9950 EXIT 
9999 END 
56000 REM "180032-Wireless Warehousing                                        
56002 REM "197953-WMS. Quick bin change                                       
56004 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
56006 REM "212487-WMS. Need to know how to split Lots when doing quick bin    
56008 REM "248515-Incomplete info being recorded for WMS Bin moves to RFA
