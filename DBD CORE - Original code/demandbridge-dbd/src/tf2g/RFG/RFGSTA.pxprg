0010 REM "Start up Program </usr/lib/pvx/tf2g/RFG/RFGSTA>
0030 GOSUB INITIALIZE; GOSUB EVAL_ARGUMENTS; GOSUB TF_CONFIG
0035 REM "5.4 - 09/23/05 - 15.636944 - jme - SSP# 180032
0040 REM "Copyright 2005 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 SET_PARAM 'FF'=1 ! Set TBD Fid Format
0060 SET_PARAM 'EG' ! Ignore Error 29s
0070 SET_PARAM 'NL' ! Suppress LET Directives in listings
0080 SET_PARAM 'NE' ! Return error in call program
0090 SET_PARAM 'FI' ! Ignore Format Mask errors (#43)
0100 SET_PARAM 'TL' ! Thoroughbred Like handling
0110 SET_PARAM 'XT'=0 ! ProvideX Exits to OS
0120 SET_PARAM 'XF' ! Extended file channels (>127)
0130 REM SET_PARAM 'TU' ! WindX Turbo Mode
0140 SET_PARAM 'OR'=0 ! Full OS Path for Rename
0142 SET_PARAM 'PZ'=1 ! Don't warn about program size > 64k
0143 SET_PARAM 'MB'=1900 ! Multi-Segmented Files ssp#169180 segments at 1.9GB
0144 JAV_SYS$=MSE
0145 IF TCB(82)=2 AND JAV_SYS$(32,1)<>"J" AND TCB(88)>0 THEN SET_PARAM 'DW'=2 ! NT Systems Only
0150 SETESC DISABLE 
0160 PRINT 'DIALOGUE'(0,0,60,20)
0165 PRINT 'WINDOW'(0,0,60,20)
0200 ! ^100 Global Variables
0210 %LOGO$="!TOPFORM"
0220 %NOMAD_QRY_BTN$="{!LOOKUP}"
0230 %NOMAD_ENTER_TAB=1
0240 %NOMAD_QRY_WIDE=3
0255 %FLMAINT_LIB$="./tf2g/ZZW/tfscrnlib.en"
0260 IF TCB(88)=0 THEN %WDX$="" ELSE %WDX$="[WDX]"
0297 C$=%C$,NOMAD_QRY_BTN$=%NOMAD_QRY_BTN$,NOMAD_ENTER_TAB=%NOMAD_ENTER_TAB,NOMAD_QRY_WIDE=%NOMAD_QRY_WIDE,FLMAIN_MSG$=%FLMAINT_MSG$,FLMAIN_LIB$=%FLMAINT_LIB$,DATAPATH$=%DATAPATH$
0298 ! 
0299 ! 
0310 USER_LEX "DIR"="LWD"
0410 GOSUB SET_PREFIX_FILE
0500 ! ^100 Call Standard Global Functions
0510 PERFORM "*conv.tbd/function.def"
0520 PERFORM "*conv.tbd/tffunction.def"
0530 PERFORM "ZZ2PID"
0540 GOSUB SET_DEVICES
0550 ! 
0600 ! ^100 End of Startup
0610 GOTO WRAP_UP
0620 ! 
2000 INITIALIZE:! 2000,5 Initialize to default case
2001 CWDIR "/usr/lib/pvx"; HWD$="/usr/lib/pvx"
2005 PREFIX "tf2d"+DLM+" "+HWD$+DLM ! data files in tf2d
2010 PREFIX PROGRAM "nomads"+DLM+" tf2g"+DLM+"==="+DLM+" tf2g"+DLM+"USR"+DLM+" tf2p"+DLM+"==="+DLM+" tf2p"+DLM+"USR"+DLM+" tf2y"+DLM+" tf2z"+DLM+"==="+DLM+" tf2z"+DLM+"USR"+DLM+" UTILS"+DLM+" "+DLM+"usr"+DLM+"lib"+DLM+"pvx"+DLM ! programs in the normal places
2015 %DATAPATH$=HWD$+DLM+"tf2d"
2020 END_INITIALIZE:RETURN 
2025 ! 
2100 TF_CONFIG:! 2100,5  TopForm Configuration
2105 TF0_IOLIST:IOLIST COMP_CODE$,DATA_PREFIX$,PROG_PREFIX$,LIC_COMP$,CONFIG_DESC$
2110 PREFIX PROGRAM "nomads"+DLM+" tf2g"+DLM+"==="+DLM+" tf2g"+DLM+"USR"+DLM+" tf2p"+DLM+"==="+DLM+" tf2p"+DLM+"USR"+DLM+" tf2y"+DLM+" tf2z"+DLM+"==="+DLM+" tf2z"+DLM+"USR"+DLM+" UTILS"+DLM+" "+DLM+"usr"+DLM+"lib"+DLM+"pvx"+DLM ! default program locations
2115 PREFIX "tf2d"+DLM+" "+HWD$+DLM ! default data locations
2120 COMP_CODE$="",DATA_PREFIX$=PFX(0),PROG_PREFIX$=PFX(PGN),LIC_COMP$="Y",CONFIG_DESC$="Default Configuration"
2125 OPEN (1,ERR=*NEXT)HWD$+DLM+"TF0"; GOTO SET_PREFIX
2130 ! if TF0 not found, then setup in home directory
2135 KEYED HWD$+DLM+"TF0",[1:1:6:"CU"],[4:1:1:"C"]+[1:1:6:"C"],,-512 ! primary key is 6 char comp code, secondary is Liscensed company? + comp code
2140 OPEN (1,ERR=END_TF_CONFIG)HWD$+DLM+"TF0" ! if still error then skip this
2145 PRINT 'WINDOW'(1,10,78,9,2,"Create TopForm Configuration Data",OPT="cCx"),'CS',
2150 PRINT "TopForm Configuration file was not found!",'LF',"Please enter the following data:"; INPUT "Company code: ",COMP_CODE:"######"; COMP_CODE$=STR(COMP_CODE:"000000")
2155 INPUT EDIT (0,IND=LEN(STP(DATA_PREFIX$,1)),LEN=128,SIZ=58)"Data Prefix: ",DATA_PREFIX$
2160 INPUT EDIT (0,IND=LEN(STP(PROG_PREFIX$,1)),LEN=128,SIZ=58)"Program Prefix: ",PROG_PREFIX$
2165 LIC_COMP$="Y",CONFIG_DESC$="Default Configuration"
2170 PRINT 'WD'(2),
2175 WRITE (1,ERR=END_TF_CONFIG)IOL=TF0_IOLIST
2180 SET_PREFIX:! Set prefix information here
2185 READ (1,KNO=1,KEY="Y",DOM=2188)
2190 READ (1,END=2195)IOL=TF0_IOLIST; GOTO 2200
2195 INPUT "No liscensed company found!!",*; RELEASE 
2200 IF LIC_COMP$<>"Y" THEN GOTO 2195 ELSE %LIC_COMP$=STR(NUM(COMP_CODE$,ERR=*NEXT):"000"); IF %C$="" THEN %C$=%LIC_COMP$
2205 ! set prefixes now that we are here
2210 IF %CONFIG_OVERRIDE$<>"" THEN READ (1,KNO=0,KEY=PAD(%CONFIG_OVERRIDE$,6,1),DOM=END_TF_CONFIG)IOL=TF0_IOLIST ELSE READ (1,KNO=0,KEY=STR(NUM(%C$):"000000"),DOM=END_TF_CONFIG)IOL=TF0_IOLIST
2215 IF DATA_PREFIX$<>"" THEN PREFIX DATA_PREFIX$; TMPP=POS(DLM+" "=DATA_PREFIX$+" "); IF TMPP<>0 THEN %DATAPATH$=DATA_PREFIX$(1,TMPP-1)
2220 IF %DATAPATH$(1,1)<>DLM AND POS(HWD$=%DATAPATH$)=0 AND POS(":"=%DATAPATH$)=0 THEN %DATAPATH$=HWD$+DLM+%DATAPATH$
2225 IF PROG_PREFIX$<>"" THEN PREFIX PROGRAM PROG_PREFIX$
2230 END_TF_CONFIG:RETURN 
2235 ! 
2500 EVAL_ARGUMENTS:! 2500,10 Evaluate the arguments passed in
2510 ! Supported arguments are (after -arg )
2520 ! c=comp_code -> sets %c$ and can set prefixes based on TF0 data
2530 ! cf = Config key, will override normal company prefixes
2535 ! rpc_port = port number for rpc, call PVX rpc server & then release
2540 IF NAR<1 THEN GOTO END_EVAL_ARGUMENTS
2550 FOR LOCAL I=1 TO NAR
2560 TMP$=ARG(I)
2570 P=POS("="=TMP$); IF P=0 THEN ARG_NAME$=TMP$,ARG_VALUE$="" ELSE ARG_NAME$=TMP$(1,P-1),ARG_VALUE$=TMP$(P+1)
2580 SWITCH ARG_NAME$
2590 CASE "c","C"; %C$=ARG_VALUE$; BREAK
2600 CASE "CF","cf"; %CONFIG_OVERRIDE$=ARG_VALUE$; BREAK
2605 CASE "rpc_port"; %RPC_PORT=NUM(ARG_VALUE$,ERR=*BREAK); BREAK
2610 DEFAULT 
2620 END SWITCH 
2630 NEXT I
2640 END_EVAL_ARGUMENTS:RETURN 
2650 ! 
3000 ! 3000,10 SET TSK LIST FOR Thoroughbred compatibility
3010 SET_DEVICES:
3020 LOCAL TERM_ID$,DEVCNT,ITEMX,LNKNAME$
3030 %TSK_DEVICES$=TSK(0,ERR=*NEXT)
3040 IF %TSK_DEVICES$<>"" THEN GOTO SET_DEVICES_END
3050 %TSK_DEVICES$=FN%GET_FILES$(%DATAPATH$,"??","0","N","N",ERR=*NEXT)
3060 DEVCNT=LEN(%TSK_DEVICES$)
3070 IF DEVCNT=0 THEN GOTO 3130
3080 FOR ITEMX=1 TO DEVCNT STEP 3
3090 LNKNAME$=MID(%TSK_DEVICES$,ITEMX,2)
3100 CALL "ZZ2FNC;CHECK_LINK_NAME",LNKNAME$,IS_LNK
3110 IF IS_LNK<>1 THEN %TSK_DEVICES$(ITEMX,3)="   "
3120 NEXT ITEMX
3130 TERM_ID$=FID(0)
3140 %TSK_DEVICES$=STP(TERM_ID$,2)+"2"+STP(%TSK_DEVICES$,3," ")
3150 LNKNAME$="",TERM_ID$=""
3160 SET_DEVICES_END:RETURN 
3170 ! 
5000 SET_PREFIX_FILE:
5010 CC=HFN; OPEN (CC,ERR=*RETURN)"PFX"+C$; CLOSE (CC); PREFIX FILE "PFX"+C$
5090 RETURN 
9000 WRAP_UP:! 9000,10 Cleanup before we leave
9010 CLOSE (1,ERR=*NEXT)
9020 CLEAR 
9030 RUN "ZSTART.RF"
9999 END 
