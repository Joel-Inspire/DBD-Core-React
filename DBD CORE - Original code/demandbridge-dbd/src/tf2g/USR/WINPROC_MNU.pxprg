0010 ! *winproc.mnu - Build menu string
0020 ! (c) Copyright 2001-2005 Sage Software Canada Ltd
0100 ! 100 - Build menu
0110 BUILD_MENU:
0112 READ DATA FROM REC(IOL=7080),REC=_ORIG$ TO IOL=7080
0115 LOCAL X$,K$,_MEM_FL
0120 GOSUB LD_TEMPFL
0130 GOSUB CREATE_MENU
0135 READ DATA FROM REC(IOL=7080,REC=_ORIG$) TO IOL=7080
0136 CLOSE (_MEM_FL)
0140 RETURN 
0200 ! 200 - Load temporary file
0210 LD_TEMPFL:
0220 _MNU_STR$="",_Z=0,INDX=0,INDX_STR$="",_MENU_LIB$=PTH(_P); DIM SV_KEY$[100]
0230 _MENU_LIB$=STP(_MENU_LIB$,1,DLM); __P=POS(DLM=_MENU_LIB$,-1); IF __P<>0 THEN _MENU_LIB$=UCS(STP(_MENU_LIB$(__P+1),2))
0240 POPUP$="",EMPTY_CONTROL=0; IF _MENU_PNL$<>"" THEN READ (_P,KEY=PAD(UCS(_MENU_PNL$),12)+"0000",DOM=*NEXT)IOL=7080
0245 IF _MENU_PNL$="" THEN POPUP$="P" ! NO MENU JUST DISPLAY CUSTOMIZE ITEM
0250 IF OBJ_TYPE$="P" OR (STP(OBJ_TYPE$,2)="" AND MID(OBJ_TXT$,1,5)="Popup") THEN POPUP$="P"
0270 IF POPUP$="P" THEN _MEM_FL=0; _MEM_FL=HFN; OPEN (_MEM_FL)"*memory*"; GOTO 0340
0280 CUR_SCRN_LIB$=PTH(_P)
0300 _M=HFN; OPEN (_M,BSZ=8192,ERR=*RETURN)"*memory*"
0310 _MEM_FL=_M
0340 IF _CONTROL_VAL$="" AND POPUP$="P" THEN EMPTY_CONTROL=1
0350 READ DATA FROM "" TO IOL=7080
0360 OBJ_TXT$="<Top Level>",OBJ_SEL$="G",MNU_HELP$="",MENU_TEXTBACKGROUND_CLR$="",MENU_LEFTEDGE_CLR$=""
0380 IF _MENU_PNL$<>"" THEN READ (_P,KEY=PAD(UCS(_MENU_PNL$),12)+"M000",DOM=*NEXT)IOL=7080
0390 OBJ_TYPE$="",SV_IDX=0,SV_MNU_TYPE$=""
0400 MNU_HELP$=OBJ_MSG$
0403 IF OBJ_COLOR$>"" THEN READ DATA FROM OBJ_COLOR$,SEP=";" TO MENU_TEXTBACKGROUND_CLR$,MENU_LEFTEDGE_CLR$
0404 NEXT_INDX=0; READ (_MEM_FL,KEY=UCS(CUR_SCRN_LIB$),DOM=*NEXT)NEXT_INDX
0405 START_INDX=0,INDX=0; READ (_MEM_FL,KEY=UCS(CUR_SCRN_LIB$)+PAD(MAIN_SCRN_K$,12),DOM=*NEXT)START_INDX
0407 IF START_INDX>0 THEN INDX=START_INDX-1 ELSE INDX=NEXT_INDX,START_INDX=NEXT_INDX+1
0420 FIRST_READ=1
0430 NEXT_K:
0435 IF _MENU_PNL$="" THEN GOTO END_TMPLOAD ! NO POPUP MENU FOR PANEL (JUST CREATE CUSTOMIZE ITEM)
0440 K$=KEY(_P,END=END_TMPLOAD)
0450 IF K$>PAD(UCS(_MENU_PNL$),12)+"M999" THEN GOTO END_TMPLOAD
0460 READ (_P,KEY=K$)IOL=7080
0470 _Z=0
0480 IF OBJ_DEF$="L" THEN GOSUB PROCESS_EXTERNAL_MNU; FIRST_READ=0; GOTO NEXT_K
0490 GOSUB WRITE_TEMPREC
0500 FIRST_READ=0
0510 GOTO NEXT_K
0520 END_TMPLOAD:
0522 IF _POPUP_CONTROL$<>"" THEN RETURN ! only display if right-clicked on grey area of panel
0523 IF %NOMAD_NO_CUSTOMIZE=0 AND PRM('!9')=0 AND POPUP$="P" THEN GOSUB SETUP_CUSTOMIZER_ITEM
0525 WRITE (_MEM_FL,KEY=UCS(CUR_SCRN_LIB$))INDX; WRITE (_MEM_FL,KEY=UCS(CUR_SCRN_LIB$)+PAD(MAIN_SCRN_K$,12))START_INDX,INDX
0530 RETURN 
1000 ! 1000 - Write temp record
1010 WRITE_TEMPREC:
1030 GOSUB EXTERNAL_PROG
1040 __P=POS("&"=OBJ_TXT$),SV_KEY$=""
1050 MNU_ITM$=UCS(OBJ_TXT$(__P+1,1))
1060 IF OBJ_VAL$="I" THEN OBJ_VAL$=""
1070 IF NOT(FIRST_READ) THEN IF OBJ_IDX=0 AND OBJ_DEF$<>"L" THEN SV_KEY$[_Z]=""
1080 FOR _J=0 TO _Z
1090 SV_KEY$=SV_KEY$+SV_KEY$[_J]
1100 NEXT 
1110 IF OBJ_IDX=0 OR OBJ_IDX<SV_IDX THEN SV_VAL$=OBJ_VAL$
1115 IF SV_IDX<>0 AND OBJ_IDX<>0 AND (OBJ_SEL$="G" OR OBJ_DEF$="L") THEN IF OBJ_IDX=SV_IDX AND SV_MNU_TYPE$="G" THEN SV_KEY$=SV_KEY$(1,MAX(OBJ_IDX,1)); IF SV_KEY$[_Z]<>"" THEN V$=SV_KEY$[_Z],V$=PAD(V$,MAX(OBJ_IDX,1)),SV_KEY$[_Z]=STP(V$,1); GOTO 1130
1120 IF SV_IDX<>0 AND OBJ_IDX<>0 THEN IF OBJ_IDX<SV_IDX OR (OBJ_IDX=SV_IDX AND SV_MNU_TYPE$="G") THEN IF LEN(SV_KEY$)>=OBJ_IDX THEN SV_KEY$=SV_KEY$(1,MAX(OBJ_IDX,1)); IF SV_KEY$[_Z]<>"" THEN V$=SV_KEY$[_Z],V$=PAD(V$,MAX(OBJ_IDX,1)),SV_KEY$[_Z]=STP(V$,1)
1130 IF EMPTY_CONTROL THEN IF OBJ_VAL$="S" OR SV_VAL$="S" THEN GOTO 1190 ! popup control
1140 IF OBJ_DSP$<>"" THEN IF OBJ_VAL$="S" OR SV_VAL$="S" THEN GOTO 1190 ! popup control
1150 IF POPUP$="" THEN IF OBJ_VAL$="S" OR SV_VAL$="S" THEN GOTO 1190
1160 NEW_KEY$=SV_KEY$+MNU_ITM$
1180 _MNU_STR$=_MNU_STR$+NEW_KEY$+SEP
1190 IF OBJ_SEL$="G" OR OBJ_DEF$="L" THEN SV_KEY$[_Z]=SV_KEY$[_Z]+MNU_ITM$
1200 IF EMPTY_CONTROL THEN IF SV_VAL$="S" OR OBJ_VAL$="S" THEN SV_IDX=OBJ_IDX; RETURN ! popup control
1210 IF OBJ_DSP$<>"" THEN IF SV_VAL$="S" OR OBJ_VAL$="S" THEN SV_IDX=OBJ_IDX; RETURN ! popup control
1220 IF POPUP$="" THEN IF SV_VAL$="S" OR OBJ_VAL$="S" THEN SV_IDX=OBJ_IDX; RETURN 
1230 IF OBJ_VAL$="S" THEN OBJ_VAL$=""
1235 INDX++; IF INDX>999 THEN INDX=1
1240 WRITE (_MEM_FL,KEY=STR(INDX:"000"),DOM=1235)IOL=7080
1247 INDX_STR$=INDX_STR$+STR(INDX:"000")+SEP
1250 SV_IDX=OBJ_IDX,SV_MNU_TYPE$=OBJ_SEL$; IF OBJ_SEL$="L" THEN SV_MNU_TYPE$="G"
1260 RETURN 
2000 ! 2000 - Create menu string
2010 CREATE_MENU:
2020 MAIN_LEVEL$="[",_MENU_DEF$="",_CUSTOMIZE_ITEM_EXISTS=0; DIM SUB_LEVELS$[1:10]
2030 _SV_KR=PRM('KR'); SET_PARAM 'KR'=0
2040 NEXT_MNU_KEY:
2050 __P=POS(SEP=_MNU_STR$)
2060 IF __P=0 THEN GOTO DONE_MENU_BUILD
2065 _Q=POS(SEP=INDX_STR$); IF _Q=0 THEN INDX=1 ELSE INDX=NUM(INDX_STR$(1,_Q-1)),INDX_STR$=INDX_STR$(_Q+1)
2070 MNU_OPT_KEY$=_MNU_STR$(1,__P-1),_MNU_STR$=_MNU_STR$(__P+1)
2080 READ (_MEM_FL,KEY=STR(INDX:"000"),DOM=NEXT_MNU_KEY)IOL=7080
2083 ITEM_TEXTBACKGROUND_CLR$=""
2084 IF OBJ_COLOR$>"" THEN ITEM_TEXTBACKGROUND_CLR$=OBJ_COLOR$; IF MID(ITEM_TEXTBACKGROUND_CLR$,1,1)="=" THEN ITEM_TEXTBACKGROUND_CLR$=EVS(MID(ITEM_TEXTBACKGROUND_CLR$,2),ERR=*NEXT)
2085 IF UCS(ITEM_TEXTBACKGROUND_CLR$)="MENU DEFAULT" THEN ITEM_TEXTBACKGROUND_CLR$=""
2086 IF UCS(ITEM_TEXTBACKGROUND_CLR$)="SYSTEM DEFAULT" THEN ITEM_TEXTBACKGROUND_CLR$="Default"
2087 IF ITEM_TEXTBACKGROUND_CLR$>"" THEN ITEM_TEXTBACKGROUND_CLR$="FILL("+ITEM_TEXTBACKGROUND_CLR$+")"
2090 ITM_FUNC$=""
2110 ERROR=0
2113 IF _CUSTOMIZE_ITEM_EXISTS AND STP(OBJ_TXT$,3,"&")="Customize Panel" THEN GOTO NEXT_MNU_KEY
2115 IF STP(OBJ_TXT$,3,"&")="Customize Panel" THEN _CUSTOMIZE_ITEM_EXISTS=1
2130 IF OBJ_VAL$="D" AND POPUP$>"" THEN IF NOT(EMPTY_CONTROL) AND OBJ_DSP$="" THEN OBJ_VAL$=""
2140 MNU_BITMAP$=""; IF OBJ_TBL$<>"" THEN MNU_BITMAP$="{"+OBJ_TBL$+"}"
2150 IF LEN(MNU_OPT_KEY$)=1 THEN IF OBJ_SEL$<>"G" AND OBJ_SEL$<>"" THEN ITM_FUNC$="="+OBJ_VAL$+"17"+STR(_ID_CTL_INCREMENT+INDX:"000")+ITEM_TEXTBACKGROUND_CLR$; GOTO 2160 ! TOP LEVEL
2160 IF ITM_FUNC$="" AND POS(OBJ_VAL$:"IS")=0 THEN ITM_FUNC$="="+OBJ_VAL$+ITEM_TEXTBACKGROUND_CLR$
2170 IF LEN(MNU_OPT_KEY$)=1 THEN MAIN_LEVEL$=MAIN_LEVEL$+MNU_BITMAP$+OBJ_TXT$+ITM_FUNC$+","+STP(OBJ_MSG$,2); GOTO NEXT_MNU_KEY
2180 __Y=LEN(MNU_OPT_KEY$),__Y=__Y-1
2190 IF OBJ_SEL$<>"G" AND OBJ_SEL$<>"" THEN ITM_FUNC$="="+OBJ_VAL$+"17"+STR(_ID_CTL_INCREMENT+INDX:"000")+ITEM_TEXTBACKGROUND_CLR$
2195 IF OBJ_SEL$="G" THEN ITM_FUNC$="="+OBJ_VAL$+ITEM_TEXTBACKGROUND_CLR$
2200 _Q=POS(MNU_OPT_KEY$(1,__Y)+":"=SUB_LEVELS$[__Y])
2210 IF _Q=0 THEN SUB_LEVELS$[__Y]=STP(SUB_LEVELS$[__Y],1)+"],"+MNU_OPT_KEY$(1,__Y)+":["+MNU_BITMAP$+OBJ_TXT$+ITM_FUNC$+OBJ_MSG$ ELSE SUB_LEVELS$[__Y]=STP(SUB_LEVELS$[__Y],1)+","+MNU_BITMAP$+OBJ_TXT$+ITM_FUNC$+OBJ_MSG$
2220 GOTO NEXT_MNU_KEY
2230 DONE_MENU_BUILD:
2240 SET_PARAM 'KR'=_SV_KR
2250 IF MENU_TEXTBACKGROUND_CLR$="" THEN MENU_TEXTBACKGROUND_CLR$=%NOMAD_MENU_TEXTBACKGROUND_CLR$
2252 IF MENU_LEFTEDGE_CLR$="" THEN MENU_LEFTEDGE_CLR$=%NOMAD_MENU_LEFTEDGE_CLR$
2253 IF MID(MENU_TEXTBACKGROUND_CLR$,1,1)="=" THEN MENU_TEXTBACKGROUND_CLR$=EVS(MID(MENU_TEXTBACKGROUND_CLR$,2),ERR=*NEXT)
2254 IF MID(MENU_LEFTEDGE_CLR$,1,1)="=" THEN MENU_LEFTEDGE_CLR$=EVS(MID(MENU_LEFTEDGE_CLR$,2),ERR=*NEXT)
2255 IF UCS(MENU_TEXTBACKGROUND_CLR$)="SYSTEM DEFAULT" THEN MENU_TEXTBACKGROUND_CLR$="Default"
2256 IF UCS(MENU_TEXTBACKGROUND_CLR$)="MENU DEFAULT" THEN MENU_TEXTBACKGROUND_CLR$=""
2257 IF MENU_TEXTBACKGROUND_CLR$>"" THEN MENU_TEXTBACKGROUND_CLR$="FILL("+MENU_TEXTBACKGROUND_CLR$+"),"
2258 IF MENU_LEFTEDGE_CLR$>"" THEN MENU_LEFTEDGE_CLR$="LEFT("+MENU_LEFTEDGE_CLR$+"),"
2260 MAIN_LEVEL$=MENU_TEXTBACKGROUND_CLR$+MENU_LEFTEDGE_CLR$+MAIN_LEVEL$
2265 IF MNU_HELP$="-" THEN MAIN_LEVEL$=MNU_HELP$+MAIN_LEVEL$
2270 IF MAIN_LEVEL$="-[" OR MAIN_LEVEL$="[" THEN RETURN 
2280 __Y=LEN(MAIN_LEVEL$); IF MAIN_LEVEL$(__Y-1)=",," THEN MAIN_LEVEL$=MAIN_LEVEL$(1,__Y-1) ELSE MAIN_LEVEL$=STP(MAIN_LEVEL$,2,",")
2290 MAIN_LEVEL$=MAIN_LEVEL$+"]",SUB_LEVELS$=""
2300 FOR __I=1 TO 10
2310 IF SUB_LEVELS$[__I]<>"" THEN SUB_LEVELS$[__I]=STP(SUB_LEVELS$[__I],0,"]"),SUB_LEVELS$[__I]=STP(SUB_LEVELS$[__I],0,","),SUB_LEVELS$=SUB_LEVELS$+","+SUB_LEVELS$[__I]+"]"
2320 NEXT 
2330 _MENU_DEF$=MAIN_LEVEL$+SUB_LEVELS$
2340 RETURN 
2400 ! ^100 - Run external program. (program can alter menu logic and control the existence, disabling and check marking of a menu item)
2410 EXTERNAL_PROG:
2420 IF OBJ_DSP$="" THEN RETURN 
2425 IF OBJ_FCS$="" THEN OBJ_FCS$="P"
2430 _MNU_OPTION$=OBJ_VAL$,_MNU_FUNCTION$="",_MNU_DESC$=OBJ_TXT$,GROUP_ITEM=0,_MNU_EMPTY=EMPTY_CONTROL
2440 __P=POS("&"=OBJ_TXT$); IF __P=0 THEN RETURN 
2450 MENU_ALT_KEY$=UCS(OBJ_TXT$(__P,2))
2455 IF OBJ_FCS$="P" AND MID(OBJ_DSP$,1,1)<>QUO THEN OBJ_DSP$=QUO+OBJ_DSP$+QUO
2460 IF OBJ_SEL$<>"G" THEN _MNU_FUNCTION$=OBJ_SEL$ ELSE GROUP_ITEM=1
2461 _DEFAULT_PROGRAM$=FLDR_DEFAULT_PROG$
2462 IF _DEFAULT_PROGRAM$="" THEN _DEFAULT_PROGRAM$=DEFAULT_PROG$
2463 IF OBJ_FCS$="P" AND MID(OBJ_DSP$,1,2)=QUO+";" THEN OBJ_DSP$=QUO+_DEFAULT_PROGRAM$+OBJ_DSP$(2)
2465 IF OBJ_FCS$="I" THEN RETURN 
2467 IF OBJ_FCS$="P" THEN PERFORM OBJ_DSP$,ERR=*NEXT; GOTO 2480
2470 IF OBJ_FCS$="P" THEN PERFORM EVS(OBJ_DSP$),ERR=EXTERNAL_LOGIC_ERR ELSE _C$=OBJ_DSP$; PERFORM "*winproc.xeq",ERR=EXTERNAL_LOGIC_ERR
2480 IF POS(_MNU_OPTION$:"BCDIS")=0 THEN _MNU_OPTION$=""
2490 IF STP(_MNU_DESC$,2)="" THEN _MNU_DESC$=OBJ_TXT$ ! assign the original value
2500 ! LET __P=POS("&"=_MNU_DESC$)
2510 ! IF P<>0 THEN IF UCS(_MNU_DESC$(P,2))=MENU_ALT_KEY$ THEN LET OBJ_TXT$=_MNU_DESC$
2520 OBJ_VAL$=_MNU_OPTION$,OBJ_TXT$=_MNU_DESC$
2530 IF NOT(GROUP_ITEM) THEN OBJ_SEL$=_MNU_FUNCTION$
2540 RETURN 
2550 EXTERNAL_LOGIC_ERR: MSGBOX MSG("_WP_MNU_EXTERNAL",OBJ_DSP$)+SEP+MSG("_WP_MNU_ITEM",_MNU_DESC$),MSG("_WP_MNU_ERROR")
2560 RETURN 
5500 ! 5500 - Process external menu
5510 PROCESS_EXTERNAL_MNU:
5520 _Y=0,_K=1; DIM _EXTERNAL_FN[1:20],_EXTERNAL_LIB$[1:20],_EXTERNAL_PNL$[1:20]
5530 GOSUB PARSE_EXTERNAL_MNU
5540 IF _DUPLICATE_MENU THEN GOTO GET_NEXT_LIB
5550 GOSUB OPEN_EXTERNAL_LIB
5560 IF _OPEN_ERR THEN GOTO GET_NEXT_LIB
5570 ! IF _Z<>0 THEN LET OBJ_IDX=1
5580 ! make sure panel exists before first item is written
5590 READ (_EXTERNAL_FN[_Y],KEY=PAD(UCS(_EXTERNAL_PNL$[_Y]),12)+"M000",DOM=*NEXT)
5600 _EXTERNAL_MNU_K$=KEY(_EXTERNAL_FN[_Y],END=GET_NEXT_LIB)
5610 IF _EXTERNAL_MNU_K$>PAD(UCS(_EXTERNAL_PNL$[_Y]),12)+"M999" THEN GOTO GET_NEXT_LIB
5613 IF OBJ_IDX=0 THEN SV_KEY$[_Z]=""
5615 IF _Z<>0 THEN OBJ_IDX=0
5617 ! IF OBJ_IDX=0 THEN LET SV_KEY$[_Z]=""
5620 ! 
5630 GOSUB WRITE_TEMPREC
5640 _Z=_Y,MNULEVEL=0
5643 FOR _Q=0 TO _Z
5645 MNULEVEL+=LEN(SV_KEY$[_Q])
5647 NEXT 
5650 READ (_EXTERNAL_FN[_Y],KEY=PAD(UCS(_EXTERNAL_PNL$[_Y]),12)+"M000",DOM=*NEXT)
5660 _EXTERNAL_MNU_K$=KEY(_EXTERNAL_FN[_Y],END=GET_NEXT_LIB)
5670 IF _EXTERNAL_MNU_K$>PAD(UCS(_EXTERNAL_PNL$[_Y]),12)+"M999" THEN GOTO GET_NEXT_LIB
5680 READ (_EXTERNAL_FN[_Y],KEY=_EXTERNAL_MNU_K$)IOL=7080
5690 IF OBJ_DEF$="L" THEN GOTO 5530
5700 IF OBJ_IDX=0 THEN SV_KEY$[_Z]=""
5710 IF OBJ_IDX<>0 THEN IF OBJ_SEL$="G" AND SV_KEY$[_Z]<>"" THEN V$=SV_KEY$[_Z],SV_KEY$[_Z]=PAD(V$,OBJ_IDX)
5720 OBJ_IDX=OBJ_IDX+MNULEVEL ! was +2
5730 GOSUB WRITE_TEMPREC
5740 GOTO 5660
5750 GET_NEXT_LIB:
5760 _Y=_Y-1,_Z=_Y; IF _Y<1 THEN GOSUB CLOSE_LIBS; RETURN 
5770 GOTO 5660
5900 ! 5900 - Close libraries
5910 CLOSE_LIBS:
5920 FOR _I=1 TO 20
5930 CLOSE (_EXTERNAL_FN[_I],ERR=*NEXT)
5940 NEXT 
5950 RETURN 
6000 ! 6000 - Build external menu
6010 PARSE_EXTERNAL_MNU:
6020 _DUPLICATE_MENU=0,_EXTERNAL_LIB$="",_EXTERNAL_PNL$=""
6030 IF POS("="=OBJ_NULL$)=1 THEN OBJ_NULL$=EVS(OBJ_NULL$(2),ERR=*NEXT)
6040 IF OBJ_NULL$="" THEN RETURN 
6050 _X=POS(",;":OBJ_NULL$); IF _X=0 THEN _EXTERNAL_LIB$="",_EXTERNAL_PNL$=OBJ_NULL$ ELSE _EXTERNAL_LIB$=OBJ_NULL$(_X+1),_EXTERNAL_PNL$=OBJ_NULL$(1,_X-1)
6060 IF _EXTERNAL_LIB$="" THEN IF UCS(STP(_MENU_PNL$,2))=UCS(STP(_EXTERNAL_PNL$,2)) THEN _DUPLICATE_MENU=1; RETURN 
6070 IF UCS(STP(_EXTERNAL_LIB$,2))=_MENU_LIB$ AND UCS(STP(_MENU_PNL$,2))=UCS(STP(_EXTERNAL_PNL$,2)) THEN _DUPLICATE_MENU=1; RETURN 
6080 IF _Y=0 THEN GOTO 6140
6090 FOR _K=1 TO _Y
6100 IF STP(_EXTERNAL_LIB$,2)="" THEN IF UCS(STP(_EXTERNAL_PNL$,2))=UCS(STP(_EXTERNAL_PNL$[_K],2)) THEN _DUPLICATE_MENU=1; BREAK
6110 IF UCS(STP(_EXTERNAL_LIB$,2))=UCS(STP(_EXTERNAL_LIB$[_K],2)) AND UCS(STP(_EXTERNAL_PNL$,2))=UCS(STP(_EXTERNAL_PNL$[_K],2)) THEN _DUPLICATE_MENU=1; BREAK
6120 NEXT 
6130 IF _DUPLICATE_MENU THEN RETURN 
6140 _Y=_Y+1
6150 _EXTERNAL_LIB$[_Y]=_EXTERNAL_LIB$,_EXTERNAL_PNL$[_Y]=_EXTERNAL_PNL$
6160 RETURN 
7000 ! 7000 - Open external library to get menu records
7010 OPEN_EXTERNAL_LIB:
7020 _OPEN_ERR=0
7030 IF _EXTERNAL_LIB$[_Y]="" THEN _EXTERNAL_LIB$[_Y]=PTH(_P)
7040 _EXTERNAL_FN[_Y]=HFN; OPEN (_EXTERNAL_FN[_Y],ERR=7060)_EXTERNAL_LIB$[_Y]
7050 RETURN 
7060 _OPEN_ERR=1
7070 GOTO GET_NEXT_LIB
7080 SCRN_IOLIST:IOLIST OBJ_NME$,OBJ_C,OBJ_L,OBJ_W,OBJ_H,OBJ_TYPE$,OBJ_TXT$,OBJ_VAL$,OBJ_TAB,OBJ_DEF$,OBJ_DSP$,OBJ_FCS$,OBJ_SEL$,OBJ_MSG$,OBJ_HLP$,OBJ_ATTR$,OBJ_IDX,OBJ_HOTKEY$,OBJ_QRY$,OBJ_SEC$,OBJ_STS$,OBJ_GRP$,OBJ_NULL$,OBJ_TAG$,OBJ_TBL$,OBJ_INP$,OBJ_OUT$,OBJ_VALID$,OBJ_CLASS$,OBJ_TIP$,OBJ_ORIG$,OBJ_FONT$,OBJ_COLOR$,OBJ_LISTBOX_TYPE$,OBJ_SEP$,OBJ_SCRATCH$,OBJ_POPUP$,OBJ_SIZING$,OBJ_LOGIC1$,OBJ_LOGIC2$,OBJ_POPUP_LOGIC$,OBJ_QRY_BITMAP$,OBJ_QRY_WIDTH,OBJ_QRY_TIP$,OBJ_QRY_ATTR$,OBJ_EXTENSION$,OBJ_DEMAND$,OBJ_DEMAND_LOGIC$,OBJ_BACKGROUND_LOGIC$,OBJ_RFU1$,OBJ_RFU2$,OBJ_RFU3$,OBJ_RFU4$,OBJ_RFU5$
8100 ! 8100 - Create customizer record (menu item)
8110 SETUP_CUSTOMIZER_ITEM:
8120 READ DATA FROM "" TO IOL=7080
8125 IF _MENU_PNL$="" THEN LINE_SEP$="" ELSE LINE_SEP$=","
8130 OBJ_TXT$=LINE_SEP$+MSG(170),OBJ_SEL$="XPREINPUT NEXT %NOMAD_CUSTOM_DEFINE",_MNU_STR$=_MNU_STR$+"Z"+SEP
8140 INDX++; INDX_STR$=INDX_STR$+STR(INDX:"000")+SEP
8150 WRITE (_MEM_FL,KEY=STR(INDX:"000"))IOL=7080
8160 RETURN 
