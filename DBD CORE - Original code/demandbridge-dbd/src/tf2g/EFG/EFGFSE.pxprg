0010 ! EFGFSE - Utility to update E/F setups from standard FSE file
0030 ! ************************************************************************
0035 REM "5.7 - 06/24/13 - 13.409722 - jvv - SSP# 263821
0037 REM "263821-Ability to copy a user defined spec from standard release   
0040 REM "Copyright 2013 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0060 IF TCB(88)=0 THEN MSGBOX "You must be using Windx to access this program" ELSE PROCESS "EFGFSE","../EFG/EF.EN"
0080 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0130 X0$="EFGFSE",X1$="Utility to maintain standard user defined specs"
0140 X0=-1,X2=-1
0150 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2; IF X1>0 THEN CMD_STR$="END"; EXIT 
0160 DIM Z[NUM(X3$(60,3))]
0170 OPEN (HFN,IOL=*)"FSE"; Z[1]=LFO
0180 OPEN (HFN,IOL=*)"FSE"+%C$; Z[2]=LFO
0190 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CLOSE (Z[1]),(Z[2])
0970 RETURN 
1000 ! Populate User Def Spec codes into given list box
1005 LOAD_UDF_CODES:
1010 UDFIDX=0; LIST_BOX LOAD UDF_LB.CTL,"" ! Clear UDF codes in list box
1012 READ (UDFCHAN,KEY="",DOM=*NEXT)
1015 NEXT_UDF_CODE:! Get first record of next spec code
1020 KUDF$=KEY(UDFCHAN,END=DONE_LSC); READ (UDFCHAN,KEY=KUDF$)UDF$
1025 LIST_BOX LOAD UDF_LB.CTL,(++UDFIDX),MID(UDF$,1,3)+SEP+MID(UDF$,7,20)+SEP
1040 READ (UDFCHAN,KEY=MID(UDF$,1,3)+$FFFFFF$,DOM=*NEXT)
1050 GOTO NEXT_UDF_CODE
1090 DONE_LSC:
1095 RETURN 
1099 ! 
1200 ! Populate standard user defined spec list box
1210 LOAD_STD_CODES:
1220 UDF_LB.CTL=STD_UDF.CTL,UDFCHAN=Z[1]
1230 GOSUB LOAD_UDF_CODES
1245 RETURN 
1249 ! 
1250 ! Populate company specific user defined spec list box
1255 LOAD_CUST_CODES:
1260 UDF_LB.CTL=CUST_UDF.CTL,UDFCHAN=Z[2]
1270 GOSUB LOAD_UDF_CODES
1295 RETURN 
1299 ! 
2000 ! Compare user defined spec codes
2010 COMPARE:
2020 IF NUL(STD_UDF$) THEN GOTO ERR_SEL_STD_UDF
2025 IF NUL(CUST_UDF$) THEN GOTO ERR_SEL_CUST_UDF
2030 ANS$=""; MSGBOX "Would you like to compare the following codes?"+SEP+"Standard code: "+STD_UDF$+SEP+"Company specific code: "+CUST_UDF$,MSG("CONFIRM"),"YESNO,2",ANS$
2035 IF ANS$<>"YES" THEN RETURN 
2040 PROCESS "EFGFSE.1","",STR(Z[1]),STD_UDF$,STR(Z[2]),CUST_UDF$
2045 RETURN 
2049 ! 
2050 COMPARE_ALL:
2055 ANS$=""; MSGBOX "Would you like to compare all available codes?",MSG("CONFIRM"),"YESNO,2",ANS$
2060 IF ANS$<>"YES" THEN RETURN 
2065 PREV_STD_UDF$=""
2070 READ (Z[1],KEY=PREV_STD_UDF$,DOM=*NEXT)
2075 STD_UDF$=MID(KEY(Z[1],END=*RETURN),1,3),CUST_UDF$=STD_UDF$
2080 PROCESS "EFGFSE.1","",STR(Z[1]),STD_UDF$,STR(Z[2]),CUST_UDF$
2085 PREV_STD_UDF$=STD_UDF$+$FF$; GOTO 2070
2095 RETURN 
2096 ! 
2097 ERR_SEL_STD_UDF: MSGBOX "Please select standard user defined spec code!"; RETURN 
2098 ERR_SEL_CUST_UDF: MSGBOX "Please select company specific user defined spec code!"; RETURN 
2099 ! 
3300 PROCESS_COMPARE:
3305 SDONE=0,CDONE=0; GOSUB INIT_GRID
3310 SCHN=NUM(ARG_1$),CCHN=NUM(ARG_3$)
3320 STDUDF$=MID(ARG_2$,1,3),CUSTUDF$=MID(ARG_4$,1,3)
3330 READ (SCHN,KEY=STDUDF$,DOM=*NEXT)
3340 READ (CCHN,KEY=CUSTUDF$,DOM=*NEXT)
3350 NEXT_RECS:
3360 READ (SCHN,END=SREC_DONE,REC=SUDF$); IF SUDF.TEMPLATE_CODE$<>STDUDF$ THEN GOTO SREC_DONE
3370 READ (CCHN,END=CREC_DONE,REC=CUDF$); IF CUDF.TEMPLATE_CODE$<>CUSTUDF$ THEN GOTO CREC_DONE
3375 GOSUB LOAD_COMP_GRID_LINE
3395 GOTO NEXT_RECS
3399 ! 
3400 SREC_DONE:
3405 SDONE=1; IF CDONE=1 THEN GOTO BOTH_DONE
3410 DIM SUDF$:IOL(SCHN); READ DATA FROM DIM(LEN(SUDF$)),REC=SUDF$ TO IOL=IOL(SUDF$)
3420 READ (CCHN,END=CREC_DONE,REC=CUDF$); IF CUDF.TEMPLATE_CODE$<>CUSTUDF$ THEN GOTO CREC_DONE
3430 GOSUB LOAD_COMP_GRID_LINE; GOTO 3420
3440 ! 
3445 GOTO BOTH_DONE
3449 ! 
3450 CREC_DONE:
3455 CDONE=1; IF SDONE=1 THEN GOTO BOTH_DONE
3460 DIM CUDF$:IOL(CCHN); READ DATA FROM DIM(LEN(CUDF$)),REC=CUDF$ TO IOL=IOL(CUDF$)
3465 READ (SCHN,END=SREC_DONE,REC=SUDF$); IF SUDF.TEMPLATE_CODE$<>STDUDF$ THEN GOTO SREC_DONE
3470 GOSUB LOAD_COMP_GRID_LINE; GOTO 3465
3489 ! 
3490 BOTH_DONE:
3495 RETURN 
3499 ! 
3500 LOAD_COMP_GRID_LINE:
3510 LEFT_GRID_IOL$="",LEFT_COLOR$=""; LEFT_GRID_IOL$=SUDF.TEMPLATE_CODE$+SEP+SUDF.SCREEN_NUM$+SEP+SUDF.FIELD_NUM$+SEP+SUDF.FIELD_DESC$+SEP+SUDF.F1_HDG_TEXT_1$+SEP+SUDF.F1_HDG_TEXT_2$+SEP+SUDF.F1_COLUMN_POS_$+SEP+SUDF.F1_LINE_NUM$+SEP+SUDF.F1_OUTPUT_TYPE$+SEP
3520 RIGHT_GRID_IOL$="",RIGHT_COLOR$=""; RIGHT_GRID_IOL$=CUDF.TEMPLATE_CODE$+SEP+CUDF.SCREEN_NUM$+SEP+CUDF.FIELD_NUM$+SEP+CUDF.FIELD_DESC$+SEP+CUDF.F1_HDG_TEXT_1$+SEP+CUDF.F1_HDG_TEXT_2$+SEP+CUDF.F1_COLUMN_POS_$+SEP+CUDF.F1_LINE_NUM$+SEP+CUDF.F1_OUTPUT_TYPE$+SEP
3530 GOSUB CHECK_IS_DIFFERENT
3535 COMP_GRID_IOL$=LEFT_COLOR$+LEFT_GRID_IOL$+SEP+RIGHT_COLOR$+RIGHT_GRID_IOL$
3540 GLNS++; GRID LOAD GRID_CMP.CTL,1,0,COMP_GRID_IOL$
3541 IF IS_DIFFERENT THEN GRID_CMP.CTL'ROW=GLNS; GRID_CMP.CTL'COLUMN=0; GRID_CMP.CTL'BACKCOLOUR$="Light Yellow"
3545 RETURN 
3549 ! 
3600 INIT_GRID:
3605 GLNS=0
3610 GRID LOAD GRID_CMP.CTL,0,0,""
3645 RETURN 
3649 ! 
3650 CHECK_IS_DIFFERENT:
3661 IS_DIFFERENT=0
3662 IF SUDF.TEMPLATE_CODE$<>CUDF.TEMPLATE_CODE$ THEN IS_DIFFERENT=1; GOTO *RETURN
3663 IF SUDF.SCREEN_NUM$<>CUDF.SCREEN_NUM$ THEN IS_DIFFERENT=1; GOTO *RETURN
3664 IF SUDF.FIELD_NUM$<>CUDF.FIELD_NUM$ THEN IS_DIFFERENT=1; GOTO *RETURN
3665 IF SUDF.FIELD_DESC$<>CUDF.FIELD_DESC$ THEN IS_DIFFERENT=1; GOTO *RETURN
3666 IF SUDF.F1_HDG_TEXT_1$<>CUDF.F1_HDG_TEXT_1$ THEN IS_DIFFERENT=1; GOTO *RETURN
3667 IF SUDF.F1_HDG_TEXT_2$<>CUDF.F1_HDG_TEXT_2$ THEN IS_DIFFERENT=1; GOTO *RETURN
3668 IF SUDF.F1_COLUMN_POS_$<>CUDF.F1_COLUMN_POS_$ THEN IS_DIFFERENT=1; GOTO *RETURN
3669 IF SUDF.F1_LINE_NUM$<>CUDF.F1_LINE_NUM$ THEN IS_DIFFERENT=1; GOTO *RETURN
3670 IF SUDF.F1_OUTPUT_TYPE$<>CUDF.F1_OUTPUT_TYPE$ THEN IS_DIFFERENT=1; GOTO *RETURN
3695 RETURN 
3699 ! 
4000 COPY_TO_CUST:
4020 IF NUL(STD_UDF$) THEN GOTO ERR_SEL_STD_UDF
4030 ANS$=""; MSGBOX "Would you like to copy "+SEP+"Standard code: "+STD_UDF$+SEP+"to Company specific code: "+STD_UDF$,MSG("CONFIRM"),"YESNO,2",ANS$
4035 IF ANS$<>"YES" THEN RETURN 
4040 FROM_LIST=1,FROM_KEY$=MID(STD_UDF$,1,3)
4050 ! IND (CUSTCHL,KEY=MID(STD_UDF$,1,3),DOM=*NEXT); GOTO UDF_EXISTS
4060 GOSUB COPY_UDF
4070 GOSUB LOAD_CUST_CODES
4145 RETURN 
4149 ! 
4170 GOSUB LOAD_STD_CODES
4200 COPY_TO_STD:
4220 IF NUL(CUST_UDF$) THEN GOTO ERR_SEL_STD_UDF
4230 ANS$=""; MSGBOX "Would you like to copy "+SEP+"CompanySpecific code: "+CUST_UDF$+SEP+"to Standard code: "+CUST_UDF$,MSG("CONFIRM"),"YESNO,2",ANS$
4235 IF ANS$<>"YES" THEN RETURN 
4240 FROM_LIST=2,FROM_KEY$=MID(CUST_UDF$,1,3)
4260 GOSUB COPY_UDF
4290 RETURN 
4295 ! 
5000 COPY_UDF:
5010 OLD_KY$=FROM_KEY$,NEW_KY$=OLD_KY$
5020 IF FROM_LIST=1 THEN FROM_FILE=Z[1],TO_FILE=Z[2] ELSE FROM_FILE=Z[2],TO_FILE=Z[1]
5030 CHECK_EXISTING:
5040 READ (TO_FILE,KEY=NEW_KY$+"",DOM=*NEXT)*
5045 KK$=KEY(TO_FILE,END=5095)
5050 IF MID(KK$,1,3)=NEW_KY$ THEN GOTO UDF_EXISTS
5100 WRITE_REC:
5110 READ (FROM_FILE,KEY=OLD_KY$,DOM=*NEXT)
5120 KY$=KEY(FROM_FILE,END=5190); K0$=KY$
5130 IF KY$(1,3)<>OLD_KY$(1,3) THEN GOTO 5190
5140 READ RECORD (FROM_FILE,KEY=KY$)OREC$
5150 NKEY$=KY$,NKEY$(1,3)=NEW_KY$,OREC$(1,3)=NEW_KY$
5160 WRITE RECORD (TO_FILE,KEY=NKEY$)OREC$
5170 GOTO 5120
5190 MSGBOX "User Defined Code has been copied"
5195 RETURN 
5199 ! 
5200 UDF_EXISTS:
5210 REC_EXISTS$="USER DEFINED SPEC "+NEW_KY$+" ALREADY EXISTS"
5215 MSGBOX REC_EXISTS$+SEP+"DO YOU WISH TO OVERWRITE",MSG("CONFIRM"),"?,YESNO",ANSWER$
5220 IF ANSWER$="YES" THEN GOTO WRITE_REC
5230 UDF_SEL$=OLD_KY$
5240 SELECTED$=""
5250 PROCESS "EFGFSC","../EFG/EF.EN",SELECTED$,UDF_SEL$
5260 IF STP(SELECTED$,2)="" THEN GOTO *RETURN
5270 NEW_KY$=SELECTED$
5280 GOTO CHECK_EXISTING
5290 RETURN 
9999 END 
56002 REM "245495-GUI utility to manage user defined spec definitions, FSE file
56004 REM "263821-Ability to copy a user defined spec from standard release   
