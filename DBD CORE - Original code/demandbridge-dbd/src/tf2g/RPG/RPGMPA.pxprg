0010 ! RPG Parameter Setup <RPGMPA>
0020 ! "5.0 - 12/16/02 - 10.913055 - JIR
0030 ! "Copyright 2002 Computer Software Inc.; Norcross, Georgia
0035 REM "5.7 - 12/14/12 - 12.351388 - crg - SSP# 257953"
0037 REM "279707-DBRS sign-on/authentication mechanism"
0040 REM "Copyright 2012 Demand Bridge, LLC.; Norcross, Georgia"
0041 REM "        Licensed Software - All Rights Reserved."
0050 IF TCB(13)=1 THEN BEGIN 
0052 IF NUL(%WDX$) THEN MSGBOX "You must be using Windx to access TopForm to maintain the PDF parameters"; GOTO 0075
0055 IF NOT(%GUI) THEN CHUI_MODE=1; %GUI=1; PRINT 'SHOW'(0)
0060 IF FN%HAS_MODULE("RP") THEN PROCESS "RPGMPA","../RPG/RP.EN"
0065 IF CHUI_MODE THEN %GUI=0; PRINT 'SHOW'(1); RUN "ZMENU"
0075 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0080 INIT:
0510 _ZZPARM=HFN; OPEN (_ZZPARM)"ZZPARM"
0520 CALL "ZZCOMP",X0$,X1$,X3$,X3$,X4$,M9$,X0,-1,X2
0540 RP_KEY$=X3$(9,3)+"EXTREPORT"
0595 RETURN 
0599 ! *************************************************************
1000 ! ^1000,10 Read in parameters
1010 READ_REC:
1020 DIM RP_REC$(800)
1030 EXTRACT (_ZZPARM,ERR=NO_READ,KEY=RP_KEY$,DOM=*NEXT)RP_REC$(1); GOTO 1050
1040 DIM RP_REC$(800); RP_REC$(1)=RP_KEY$
1050 RPT_BASE_URL$=RP_REC$(13,156)
1060 DB_BASE_URL$=RP_REC$(169,200)
1070 RPT_SERVER_KEY$=RP_REC$(369,64)
1080 REFRESH_FLG=1
1090 RETURN 
1100 ! 
1200 ! ^1200,10 Write out parameters
1210 WRITE_REC:
1220 RP_REC$(13,156)=RPT_BASE_URL$
1230 RP_REC$(169,200)=DB_BASE_URL$
1240 RP_REC$(369,64)=RPT_SERVER_KEY$
1250 WRITE (_ZZPARM,KEY=RP_KEY$)RP_REC$
1260 CMD_STR$="END"
1270 RETURN 
1290 ! 
5000 ! ^5000,10 Check for error on file
5010 NO_READ:
9900 ! ^9900,10 - Wrap up
9910 WRAP_UP:
9920 CLOSE (_ZZPARM)
9930 RETURN 
9999 END 
10000 ! 
10005 EXPORT_XML_FILE:
10010 READ (_ZZPARM,KEY=X3$(9,3)+"ZZP",DOM=*NEXT)A$
10020 B$=A$(7)
10030 IF NUL(B$) THEN MSGBOX MSG("RPGMPA_ZZP"),MSG("WARNING"),"!,TIM=8"; GOTO EXF_DONE
10040 CALL "[wdx]*OCX;BROWSE_FOR_FOLDER","Export XML file to this folder...",XML_PATH$
10050 IF NUL(XML_PATH$) THEN GOTO EXF_DONE
10060 SERIAL %WDX$+XML_PATH$+"\dbbirt.xml",ERR=*NEXT
10070 OPEN PURGE (HFN)%WDX$+XML_PATH$+"\dbbirt.xml"; EFMCHAN=LFO
10078 PRINT (EFMCHAN)"<?xml version=""1.0"" encoding=""UTF-8""?>"
10079 PRINT (EFMCHAN)"<DB-BIRT-XML>"
10080 PRINT (EFMCHAN)"<FormatMasks>"
10090 FOR I=1 TO LEN(B$) STEP 12
10095 I$=MID(B$,I,12); IF NUL(MID(I$,1,3)) THEN GOTO *CONTINUE
10100 ACCOUNT_TYPE$=MID(I$,1,3),FORMAT_MASK$=STP(MID(I$,4),1)
10110 PRINT (EFMCHAN)"<Format accountType="+QUO+ACCOUNT_TYPE$+QUO+" mask="+QUO+FORMAT_MASK$+QUO+" />"
10120 NEXT I
10130 PRINT (EFMCHAN)"</FormatMasks>"
10132 GOSUB EXPORT_GL_INFO
10179 PRINT (EFMCHAN)"</DB-BIRT-XML>"
10180 EXF_DONE:
10185 IF EFMCHAN<>0 THEN CLOSE (EFMCHAN); EFMCHAN=0
10188 MSGBOX MSG("COMPLETED")+"!",_FYI$,"!"
10190 RETURN 
10199 ! 
10200 EXPORT_GL_INFO:
10205 FIND (_ZZPARM,KEY=X3$(9,3)+"G/L",DOM=*RETURN)GL$
10210 PRINT (EFMCHAN)"<GL>"
10215 PRINT (EFMCHAN)"<Params>"
10217 PRINT (EFMCHAN)"<AcctLen>"+MID(GL$,11,1)+"</AcctLen>"
10220 PRINT (EFMCHAN)"<Revenue>"+GL$(89,12)+"</Revenue>"
10221 PRINT (EFMCHAN)"<CostOfGoodsSold>"+GL$(101,12)+"</CostOfGoodsSold>"
10222 PRINT (EFMCHAN)"<Expenses>"+GL$(113,12)+"</Expenses>"
10223 PRINT (EFMCHAN)"<OtherIncome>"+GL$(125,12)+"</OtherIncome>"
10224 PRINT (EFMCHAN)"<OtherExpenses>"+GL$(137,12)+"</OtherExpenses>"
10225 PRINT (EFMCHAN)"<GLDeptEnabled>"+TBL(MID(GL$,149,1)="Y","N","Y")+"</GLDeptEnabled>"
10226 PRINT (EFMCHAN)"<GLDeptDesc>"+STP(MID(GL$,150,20),2)+"</GLDeptDesc>"
10227 PRINT (EFMCHAN)"<GLDeptCodeOffset>"+STR(NUM(MID(GL$,170,2),ERR=*PROCEED))+"</GLDeptCodeOffset>"
10228 PRINT (EFMCHAN)"<GLDeptCodeLength>"+STR(NUM(MID(GL$,172,1),ERR=*PROCEED))+"</GLDeptCodeLength>"
10229 PRINT (EFMCHAN)"<GLSubAcctEnabled>"+TBL(MID(GL$,240,1)="Y","N","Y")+"</GLSubAcctEnabled>"
10230 PRINT (EFMCHAN)"<GLSubAcctCodeOffset>"+STR(NUM(MID(GL$,241,2),ERR=*PROCEED))+"</GLSubAcctCodeOffset>"
10231 PRINT (EFMCHAN)"<GLSubAcctCodeLength>"+STR(NUM(MID(GL$,243,1),ERR=*PROCEED))+"</GLSubAcctCodeLength>"
10255 PRINT (EFMCHAN)"</Params>"
10290 PRINT (EFMCHAN)"</GL>"
10295 RETURN 
56000 REM "279707-DBRS sign-on/authentication mechanism
