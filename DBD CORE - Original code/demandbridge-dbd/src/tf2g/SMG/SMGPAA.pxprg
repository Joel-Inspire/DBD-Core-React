0010 REM "Shipment Detail Report  <SMGPAA>
0035 REM "5.7 - 09/25/18 - 12.783888 - dmm - SSP# 301912
0037 REM "301912-Customer Shipment Confirmation Email                        
0040 REM "Copyright 2018 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 9000
0095 PRECISION 14; T2=TIM; PRECISION 2
0105 SETERR 9000; SETESC 9300
0110 X0$="SMGPAA",X1$="Shipment Detail Listing"
0120 DIM A$(397),A[15],B$(40),C$(54),C[3],D$(37),D[2]
0140 Z0$="##,###,##0-",Z1$="###.00-"
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0170 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
0210 T=1,T0=1,T1=1
0220 W3=119
0222 SEG2$="",SEG3$="",SEG4$="",SEG5$=""
0225 W=999
0230 DIM T1$(W3,"-"),T2$(W3,"="),T3$(W3,"*"),Y5$(W3),Y6$(W3)
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,"",-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "I/O lists
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12]
0390 IOLIST P3$,P[0],P[1]
0420 IOLIST X3$,X4$,V1$,V3$,V2$,V0$
0500 REM "Files
0505 DIM Z[NUM(X3$(60,3))]; GOSUB 7400
0510 Y$="01O SMD...  04O AR1...  06O PO3...  09O SMD...  10O FM0...  11O FT5...  14O ZZPARM  " ! SSP301912, remove files not used, add new one
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Y$,Z{ALL},Z0,Z1
0700 REM "Open Printer
0720 REM 
0800 REM "Alternate Sort Info & total dim"
0805 DIM U0$(4)
0820 DIM U[1]; U=10,U0=1,U[0]=0,U[1]=10,T0=0,U0$="    ",T5$="                "
0850 DIM T[T0,T1]
0860 V5=V1+1,V6=V5+V2,V7=V6+V3
0895 CALL "ZZPRIN",X0$,X3$,Y0$,V3$,Z{ALL},W8$,W9$,W0,W9,W2,W3,W4; ON W4 GOTO 0896,9920
0900 REM " Position read"
0910 BEG1$="",BEG2$="",BEG3$="",BEG4$="",BEG5$=""
0915 IF STP(V2$(15,15),2)<>"" THEN KEYNO=1,BEG1$=V2$(15,15) ELSE IF V2$(30,10)<>DIM(10) THEN BEG1$=V2$(30,10),KEYNO=2 ELSE IF V2$(14,1)="Y" THEN BEG1$=" ",KEYNO=3 ELSE BEG1$=V2$(1,6),KEYNO=0
0950 CALL "UPDSMD;READBYKEY",Z[1],KEYNO,BEG1$,BEG2$,BEG3$,SEG4$,SEG5$,A$,A{ALL},FOUND
0980 CALL "UPDSMD;READNEXT",Z[1],A$,A{ALL},FOUND; GOSUB CHK_FOUND
1000 WHILE FOUND ! 1900 wend
1010 OK_TO_PRINT=1
1015 IF A$(165,6)>V2$(7,6) THEN OK_TO_PRINT=0
1020 IF A$(165,6)<V2$(1,6) THEN OK_TO_PRINT=0
1025 IF A$(135,15)<>V2$(15,15) AND V2$(15,15)<>DIM(15) THEN OK_TO_PRINT=0
1030 IF A$(22,10)<V2$(30,10) THEN OK_TO_PRINT=0
1032 IF A$(22,10)>V2$(40,10) THEN OK_TO_PRINT=0
1035 IF V2$(14,1)="Y" AND A$(134,1)<>" " THEN OK_TO_PRINT=0
1040 IF STP(V2$(50,4),2)<>"" AND V2$(50,4)<>A$(15,4) THEN OK_TO_PRINT=0
1090 IF OK_TO_PRINT THEN GOSUB UPDATE
1100 IF OK_TO_PRINT THEN {! 1970 }
1400 REM "Subtotals"
1415 IF LINV$<>A$(7,8)+A$(15,4) THEN GOSUB 3100
1490 T=0
1500 REM "Print Data
1510 IF W+14>W0 THEN GOSUB 6000
1515 DIM P3$(500),P[1]; READ (Z[14],KEY="U/M"+A$(88,4),DOM=*NEXT)IOL=0390
1520 IF MID(P3$,20,1)="Y" THEN D7=P[1] ELSE D7=A[1]
1525 IF D7=0 THEN D7=1
1530 SHIPPED=A[0]/D7
1540 Y5$(1)=A$(32,10)+" "+A$(42,10),Y5$(23)=A$(52,40),Y5$(63)=STR(SHIPPED:"####,###.00"),Y5$(75)=A$(92,4)
1550 GOSUB 7300
1970  } ! 1100
1980 CALL "UPDSMD;READNEXT",Z[1],A$,A{ALL},FOUND; GOSUB CHK_FOUND
1990 WEND 
1995 GOTO 9900
3100 REM "High order break
3110 IF LINV$<>A$(7,8)+A$(15,4) AND LINV$<>"" THEN GOSUB 3200 ! subtotal
3120 CALL "UPDAR1;READBYKEY",Z[4],0,A$(22,10),SEG2$,SEG3$,SEG4$,SEG5$,AR1$,AR1{ALL},FOUNDAR1; IF FOUNDAR1 THEN CUST_NAME$=MID(AR1$,11,35) ELSE DIM CUST_NAME$(35)
3125 CALL "ZZDISP","AX",A$(22,10),"A/R",X3$,X$,"",0,0,X4$; CUST$=X$
3130 INVDT$=FND$(A$(1,6))
3135 INV$=A$(7,8)
3140 SHIP1$="",SHIP2$="",SHIP3$="",ATTN$=""; CALL "UPDFM0;READBYKEY",Z[10],0,"C",A$(22,10),A$(15,4),SEG4$,SEG5$,FM0$,FM0{ALL},FOUNDFM0; IF FOUNDFM0 THEN SHIP1$=FM0$(51,30),SHIP2$=FM0$(81,30),SHIP3$=STP(FM0$(111,16),2)+" "+FM0$(127,2)+" "+STP(FN%ZZDISP$(FM0$(129,9),"ZIP"),1),ATTN$=STP(FM0$(160,20),2) ! SSP301912, display zip correctly, get attention
3145 FOUNDPO3=0; DIM PO3$(428); FIND (Z[6],KEY=A$(96,8)+DIM(5),DOM=*NEXT)PO3$(1); IF STP(PO3$(46,87))<>"" THEN SHIP1$=PO3$(46,30),SHIP2$=PO3$(76,30),SHIP3$=STP(PO3$(106,16))+", "+PO3$(122,2)+"  "+STP(FN%ZZDISP$(PO3$(124,9),"ZIP"),1),ATTN$=STP(PO3$(16,30)),PO3FOUND=1 ! SSP301912, get PO3 general shipping address if available
3150 IF NOT(FOUNDPO3) THEN DIM FT5$(428); FIND (Z[11],KEY=A$(96,8)+DIM(5),DOM=*NEXT)FT5$(1); IF STP(FT5$(46,87))<>"" THEN SHIP1$=FT5$(46,30),SHIP2$=FT5$(76,30),SHIP3$=STP(FT5$(106,16))+", "+FT5$(122,2)+"  "+STP(FN%ZZDISP$(FT5$(124,9),"ZIP"),1),ATTN$=STP(FT5$(16,30)) ! SSP301912, if no PO3 then try for general shipping in FT5
3180 GOSUB 6000
3185 LINV$=A$(7,8)+A$(15,4)
3190 RETURN 
3198 ! 
3199 ! 
3200 REM "Second highest order break"
3290 RETURN 
3298 ! 
3299 ! 
3380 RETURN 
3480 RETURN 
5000 END_OF_RPT:
5020 IF W8=0 THEN GOTO 5290
5030 T0$="END"
5040 IF V1>0 THEN GOSUB 3120
5050 T=0,T$="Report"; GOSUB 7000
5200 REM "
5290 RETURN 
5298 ! 
5299 ! 
6000 REM "Page header
6035 IF T0$="END" THEN W0$=""
6040 CALL "ZZHEAD",X0$,X1$,X2$,X3$,W0$,W1$,W2$,W9$,W3,W,W9,W8,W0,W5,W2,W8$,W5$; IF T0$="END" THEN GOTO 6060 ELSE ON W5 GOTO 6041,9910
6045 ON W5 GOTO 6046,9900
6060 GOSUB 6100
6070 GOSUB 7300
6090 RETURN 
6100 REM "
6105 GOSUB 7300 ! SSP301912, print blank line for UnForm output issue
6115 Y5$(1)="Order Number: "+STP(FN%ZZDISP$(A$(96,8),"O/P"),1),Y5$(26)="Customer: "+CUST$+" "+CUST_NAME$,Y5$(83)=SHIP1$ ! SSP301912, display order number correctly and move customer over to left and address to right
6120 GOSUB 7300
6125 Y5$(1)="Invoice Number: "+INV$,Y5$(35)="Invoice Date: "+INVDT$,Y5$(83)=SHIP2$ ! SSP301912, address over to right
6130 GOSUB 7300
6140 Y5$(1)="Carrier: "+A$(150,15),Y5$(35)="Attn: "+ATTN$,Y5$(83)=SHIP3$ ! SSP 257952 SSP301912, address over to right, output attention
6145 GOSUB 7300
6150 Y5$(1)="Tracking Number: "+A$(171,60); GOSUB 7300 ! SSP 257952, SSP275380, was 168,60 change to 171,60
6155 GOSUB 7300; GOSUB 7300
6180 Y5$(8)="Item Code",Y5$(23)="Description",Y5$(65)="Shipped",Y5$(75)="UOM"
6185 GOSUB 7300; GOSUB 7300
6195 IF FID(W9)=FID(0) THEN PRINT 'SF',
6199 RETURN 
7000 REM "Totals logic
7001 IF T=T0 THEN IF T[T0,0]<2 THEN GOSUB 7300; GOTO 7100
7002 IF W+3>W0 THEN GOSUB 6000
7010 Y5$(1)=T3$(1,T0+1-T)+" "
7015 IF T$<>"" THEN Y5$(T0+2-T)=T$+" Total ("+STR(T[T,0]:"0")+"):"
7090 IF T0$="END" THEN IF T=0 THEN GOSUB 7200; GOTO 7190
7095 GOSUB 7300
7100 REM "Accumulate sub totals
7110 IF T=0 THEN GOTO 7190
7120 FOR X=0 TO T1
7130 T[T-1,X]=T[T-1,X]+T[T,X],T[T,X]=0
7140 NEXT X
7190 RETURN 
7200 REM "Elapsed time routine"
7205 GOSUB 7300; Y5$(1)="Number of items printed: "+STR(T[0,0]); GOSUB 7300
7210 PRECISION 14; T2=TIM-T2
7220 IF INT(T2)>0 THEN T4$=STR(INT(T2))+" Hours " ELSE T4$=""
7230 T3=INT(FPT(T2)*3600)
7240 T3=T3/60
7250 T4$=T4$+STR(T3:"##.##")+" Minutes"
7260 Y5$(1)="Elapsed time: "+T4$; GOSUB 7300
7280 PRECISION 2
7290 RETURN 
7300 REM "Output line Y5$ to output device
7310 IF POS("V"=W8$)>0 THEN CALL "ZZVIEW",X3$,0,0,W3,W9,W,W9$,Y5$; GOTO 7390
7320 W=W+1; IF POS(" "<>Y5$)=0 THEN PRINT (W9)"" ELSE PRINT (W9)Y5$; Y5$(1)=""
7330 REM 
7390 IF W>=W0 THEN GOSUB 6000
7395 RETURN 
7400 REM "Read report selection parameters"
7410 Z$="12O ZZP     "
7420 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0; ON Z0 GOTO 7421,9900
7430 Y3$=X3$(1,6),Y4$=X3$(178,7)
7450 READ (Z[12],KEY=X3$(1,8),DOM=7451)IOL=0420
7480 X3$(178,7)=Y4$,V0=NUM(V0$(71,1)),W3$=V0$(19,POS("   "=V0$(19,40)+"  ")-1)
7490 RETURN 
7500 REM "CUSTOM PROGRAMMING ROUTINES
7525 REM "Modified stmts:
7550 REM "Added stmts:
7575 REM "Deleted stmts:
9000 REM "ERROR PROCESSING
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 1000
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9900 REM "End program
9905 REM IF POS("V"=W8$)>0 THEN GOSUB 6000
9910 CALL "ZZERPT",X3$,X4$,X0$,Y3$,Y4$,W9,W2,W5,W,W0,W8,T3,V3$
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 IF Y4$<>"" THEN RUN Y4$,ERR=9931
9940 SETERR 9950; EXIT 
9950 RUN "ZMENU"
9999 END 
10000 CHK_FOUND:
10010 IF FOUND THEN {
10015 IF STP(V2$(15,15),2)<>"" AND A$(135,15)<>V2$(15,15) THEN FOUND=0
10020 IF A$(22,10)>V2$(40,10) THEN FOUND=0
10030 IF V2$(14,1)="Y" AND KEYNO=3 AND A$(134,1)<>" " THEN FOUND=0
10080  }
10090 RETURN 
10098 ! 
10099 ! 
10100 UPDATE:
10150 CALL "UPDSMD;EXTRACTBYKEY",Z[9],0,A$(1,6),A$(7,8),A$(15,4),A$(19,3),A$(96,8),A$,A{ALL},FOUNDSMD,BUSYSMD
10160 IF FOUNDSMD AND NOT(BUSYSMD) THEN A$(134,1)="Y"; CALL "UPDSMD;UPDATE",Z[9],A$,A{ALL},BUSYSMD2 ELSE OK_TO_PRINT=0
10190 RETURN 
10198 ! 
10199 ! 
56000 REM "186852-Email Order Shipping Confirmations                          
56002 REM "192290-Shipping confirmation email was not sent to buyer.          
56003 REM "257952-Need larger tracking number field to replace 20 character   
56004 REM "275380-Buyer Shipment Confirmation Email not going out as expected.
56006 REM "301912-Customer Shipment Confirmation Email shipping address
