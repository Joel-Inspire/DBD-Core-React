0010 REM "SMGSTP - Search and strip target strings from input
0035 REM "5.7 - 07/26/11 - 11.370555 - crg - SSP# 248154
0037 REM "248154-CIG; Program to enter strings of data to remove from input  
0040 REM "Copyright 2011 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! 
0070 IF TCB(88)=0 THEN MSGBOX "You must be using Windx to access this program" ELSE PROCESS "SMGSTP","../SMG/SM.EN"
0080 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 X0=-1
0160 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2; IF X1>0 THEN CMD_STR$="END"; EXIT 
0190 DIM Z[NUM(X3$(60,3))]
0200 ! Files
0210 Z$="13O ZZPARM  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
0300 ! IOList Section
0310 IOL_SMGSTP:IOLIST SMGSTP$
0320 ! 
0410 _FYI$=MSG("FYI"),_ERROR$=MSG("ERROR")
0420 CTAB$=DIM(2)
0430 K9$="S/MSTRIP"+%C$
0440 SAMPLE_STRING$=FN%ZZDISP$("00123456","O/P")
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 GOSUB CLEAR_REC
0530 GOSUB FIND_REC
0540 CHANGE_FLAG=0
0570 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0970 RETURN 
1000 ! 1000 - Start of maintenance only code - Find/Add/Delete/Clear record
1010 FIND_REC:
1020 READ RECORD (Z[13],KEY=K9$,DOM=*RETURN)SMGSTP$
1030 GOSUB LOAD_STRINGS
1110 EXIT 
1120 ! 
1130 ADD_STRING:
1131 IF SEARCH_STRING$="" THEN RETURN 
1133 EXAMPLE_STR$=SEARCH_STRING$+SAMPLE_STRING$
1136 GRID LOAD STP_GRID.CTL,1,0,SEP+SEARCH_STRING$+SEP+EXAMPLE_STR$+SEP; ++STR_CNT
1140 CHANGE_FLAG=1
1180 RETURN 
1800 ! 1800 - Clear record
1810 CLEAR_REC:
1830 GRID LOAD STP_GRID.CTL,0,0,""; STR_CNT=0
1850 REFRESH_FLG=1
1860 RETURN 
1899 ! 
2000 LOAD_STRINGS:
2005 GRID LOAD STP_GRID.CTL,0,0,""; STR_CNT=0
2010 LOAD_STR$=MID(SMGSTP$,LEN(K9$)+1)
2020 NEXT_STRING:
2030 IF LEN(LOAD_STR$)<=0 THEN GOTO END_LOAD_STRINGS
2040 NEXT_POS=POS(SEP=LOAD_STR$)
2050 IF NEXT_POS THEN NEXT_STR$=MID(LOAD_STR$,1,NEXT_POS-1),LOAD_STR$=MID(LOAD_STR$,NEXT_POS+1) ELSE GOTO END_LOAD_STRINGS
2095 LOAD_STRING_LINE:
2097 EXAMPLE_STR$=NEXT_STR$+SAMPLE_STRING$
2100 GRID LOAD STP_GRID.CTL,1,0,SEP+NEXT_STR$+SEP+EXAMPLE_STR$+SEP; ++STR_CNT
2110 GOTO NEXT_STRING
2120 END_LOAD_STRINGS:
2140 REFRESH_FLG=1
2150 RETURN 
2199 ! 
2200 CHECK_CHANGES:
2205 IF NOT(CHANGE_FLAG) THEN RETURN 
2210 MSGBOX MSG("REC_ALTERD"),MSG("UPDATE"),"?,YesNo",ANS$
2220 IF ANS$="NO" THEN RETURN 
2230 GOSUB SAVE_STRINGS
2245 RETURN 
2249 ! 
4000 ! Delete selected strings
4010 DELETE_STRINGS:
4012 MSGBOX MSG("SMGSTP_03"),MSG("CONFIRM"),"YESNO,2",ANS$
4014 IF ANS$<>"YES" THEN GOTO END_DELETE_STRINGS
4020 FOR I=STP_GRID.CTL'ROWSHIGH TO 1 STEP -1
4030 STP_GRID.CTL'ROW=I
4035 READ DATA FROM STP_GRID.CTL'ROWDATA$ TO IOL=STP_GRID.CTL'LOADIOLIST$
4040 IF C_DELETE$="1" THEN {
4050 GRID DELETE STP_GRID.CTL,0,I
4090  }
4100 NEXT I
4105 END_DELETE_STRINGS:
4110 CHANGE_FLAG=1
4195 RETURN 
4199 ! 
4200 ! Save specified strings
4205 IOL_GRD:IOLIST C_DELETE$,C_STRING$,C_EXAMPLE$
4210 SAVE_STRINGS:
4215 SMGSTP$=K9$
4220 FOR I=1 TO STP_GRID.CTL'ROWSHIGH
4230 STP_GRID.CTL'ROW=I
4235 READ DATA FROM STP_GRID.CTL'ROWDATA$ TO IOL=IOL_GRD
4240 IF NOT(NUL(C_STRING$)) THEN {
4250 SMGSTP$+=C_STRING$+SEP
4260  }
4270 NEXT I
4280 WRITE RECORD (Z[13],KEY=K9$)SMGSTP$
4290 CHANGE_FLAG=0
4295 RETURN 
4299 ! 
9999 END 
56002 REM "248154-CIG; Program to enter strings of data to remove from input  
