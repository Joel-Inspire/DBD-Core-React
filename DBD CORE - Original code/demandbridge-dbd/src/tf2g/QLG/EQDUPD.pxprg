0010 REM "Data Replication Bulk Setup <DRGUTB>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 08/10/18 - 15.308333 - jvv - SSP# 301133
0037 REM "301133-Controls on some selections for the Data Rep Target setup   
0040 REM "Copyright 2018 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM "This utility is intended for use in setting up a predefined set of
0051 REM "files for replication to an external target database and possibly 
0052 REM "generating sQL statements to create the database table objects
0069 ! 
0070 IF TCB(88)=0 THEN MSGBOX "You must be using Windx to access this program" ELSE PROCESS "DRGUTB","../DRG/DR.EN"
0080 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0085 EXIT 
0090 ! 
0095 INIT:
0110 X0$="DRGUTB",X1$="Data Replication Bulk Setup"
0130 K0=20,K1=1
0135 C9=-1
0140 X0=-1
0150 SAVE_PERCENT_GUI=%GUI; %GUI=1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "IOLISTS
0310 IOLIST A$
0320 IOLIST B$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O DR0...  02O DR1...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0525 DDF=HFN; OPEN (DDF,ERR=9900)"providex.ddf"
0540 IF MEMCHN<>0 THEN CLOSE (MEMCHN,ERR=*PROCEED); MEMCHN=0
0595 RETURN 
0599 ! 
0600 PROCEED:
0610 IF NUM(FIN(MEMCHN,"NUMREC"))<=0 THEN GOTO *RETURN
0630 IF DO_DATAREP$="Y" AND NUL(DR_TARGET$) THEN MSGBOX MSG("DRGUTB_05"),MSG("WARNING"),"OK,TIM=2"; RETURN 
0640 IF DO_SQL$="Y" THEN GOSUB GET_SQLFILE_PATH; IF NOT(SQLCHAN) THEN MSGBOX MSG("DRGUTB_06"),MSG("WARNING"),"OK"
0660 GOSUB 8100
0950 GOSUB 8150
0960 READ (MEMCHN,KEY="",DOM=*NEXT) ! 237322 Reset to beginning of file list
0970 ! 
1000 REM "BEGIN MAIN PROCESS
1010 DIM DR1$(502)
1011 FKEY$=KEY(MEMCHN,END=5000); READ (MEMCHN,KEY=FKEY$)LOGICAL_FILE$ ! 237322
1015 IF MID(FKEY$,1,2)="DR" THEN MSGBOX MSG("DRGUTB_11",STP(FKEY$,2)),MSG("FYI"),"!"; GOTO 1000 ! SSP 301133
1020 C=C+1; SEQ_NUMBER=SEQ_NUMBER+1; IF MOD(C,T0)=1 THEN GOSUB 8150 ! 237322
1021 DO_DATAREP:
1022 IF DO_DATAREP$<>"Y" THEN GOTO DO_SQL
1025 PHY_FILE$=UCS(MID(LOGICAL_FILE$,1,3))
1030 DR1$(1,8)=PAD(PHY_FILE$+%C$,8)
1040 DR1$(9,2)=SEQ_NUM$
1050 DR1$(11,30)=""+LOGICAL_FILE$
1060 DR1$(41,20)=DR0$(1,20)
1070 GOSUB SETUP_DATAPATH
1080 DR1$(301,1)="Y"
1090 DR1$(346,1)="Y" ! Enable logging
1100 DR1$(347,1)=DR0$(181,1)
1101 DR1$(348,3)=PAD(QUE_CODE$,3)
1110 DR1$(351,1)="N"
1120 WRITE (Z[2],KEY=DR1$(1,10),DOM=DR1_EXISTS)DR1$
1130 IF DR1$(301,1)="Y" THEN {
1140 IO_PROG$=STP(DR0$(171,10))
1150 OPT$=DR1$(351,1)
1160 CALL "DR2PFX",X3$,X4$,STP(DR1$(314,32))+"|"+STP(DR1$(1,10),1),STP(DR1$(61,120)),IO_PROG$,OPT$
1170  }
1199 ! 
1200 DO_SQL:
1201 IF DO_SQL$<>"Y" THEN GOTO 1000
1205 ERRM$="",SQLSTR$=""; CALL "DBSQLGEN",SQLSTR$,ERRM$,STP(LOGICAL_FILE$,2),"*providex.ddf","","SQL",DB_TYPE$,TCASE_TYPE$ ! 249336,285801
1210 IF ERRM$<>"" THEN MSGBOX ERRM$ END_IF ; IF NOT(NUL(SQLSTR$)) THEN PRINT (SQLCHAN)SQLSTR$ ! 249336
1600 GOTO 1000
1601 ! 
1610 DR1_EXISTS:
1620 MSGBOX "DR1 entry exists: "+LOGICAL_FILE$
1630 GOTO DO_SQL
1640 ! 
5000 REM "EOJ
5010 C=T; GOSUB 8150
5020 MSGBOX MSG("COMPLETED")+"!",_FYI$,"!"
5025 GOSUB 8100; GOSUB 8150
5030 GOSUB CLEAR_FIELDS
5040 RETURN 
5099 ! 
6000 REM "BACKGROUND
6445 RETURN 
7500 FILE_OPEN_ERR:! Error opening file
7505 FOE=ERR
7510 CALL "ZZPROM",".4",X3$,Z,"Error "+STR(FOE)+" occurred on file "+STP(DR1$(1,10))+"!","","",0
7545 GOTO 1600
7549 ! 
7550 SETUP_DATAPATH:! Get datapath from Target and replace tokens
7560 DR1$(61,120)=DR0$(51,120) ! if changed then default to model, 271011
7565 DR1$(61,120)=SUB(DR1$(61,120),"&TF",STP(DR1$(1,8))) ! &TF replaced with file name
7570 GOSUB SET_DD_NAME; DR1$(61,120)=SUB(DR1$(61,120),"&DD",STP(DR1$(314,32))) ! &DD replaced with data dictionary name
7595 SD_DONE:RETURN 
7599 ! 
7600 SET_DD_NAME:! Set the data dictionary name from the file name
7605 TMP$=PHY_FILE$
7615 READ (DDF,KNO=1,KEY=TMP$,DOM=*NEXT)
7620 TMPKEY$=KEY(DDF,KNO=1,END=SDN_DONE); READ (DDF,KEY=TMPKEY$,KNO=1)DDFNAME$; IF MID(TMPKEY$,1,3)<>TMP$ THEN GOTO SDN_DONE ! 212004
7625 DR1$(314,32)=DDFNAME$ ! 212004
7640 SDN_DONE:! done
7645 RETURN 
7649 ! 
7950 ! 
8100 REM "Initilaize graph
8110 T=NUM(FIN(MEMCHN,"NUMREC")),T0=2
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Update graph
8155 CALL "ZZBARG",X3$,"HG",23,10,50,T1,T,C
8195 RETURN 
8950 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"     ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9515 GOTO 0640
9520 ON C9 GOTO 1140,2040
9900 WRAPUP:REM "END PROGRAM
9905 %GUI=SAVE_PERCENT_GUI
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 CLOSE (DDF)
9925 IF MEMCHN THEN CLOSE (MEMCHN); MEMCHN=0
9926 IF SQLCHAN<>0 THEN CLOSE (SQLCHAN); SQLCHAN=0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
10000 ! 
10005 SELECT_FILES:
10010 CALL "ZZGFLS",MEMCHN
10020 FILES_SELECTED.CTL'VALUE$=FIN(MEMCHN,"NUMREC")
10030 IF NUM(FIN(MEMCHN,"NUMREC"))=0 THEN GOSUB CLEAR_FIELDS
10045 RETURN 
10049 ! 
10050 GET_TARGET:
10060 DIM DR0$(234); IF NOT(NUL(DR_TARGET$)) THEN FIND (Z[1],KEY=PAD(DR_TARGET$,20),DOM=ERR_TARGET)DR0$ ELSE GOTO ERR_TARGET
10070 TARGET_DESC$=DR0$(21,30); REFRESH_FLG=1
10075 RETURN 
10079 ! 
10080 ERR_TARGET:
10085 DR_TARGET$=""; NEXT_ID=DR_TARGET.CTL; REFRESH_FLG=1
10090 RETURN 
10099 ! 
10100 CLEAR_FIELDS:
10110 DO_DATAREP$="",DO_SQL$="",DR_TARGET$="",SEQ_NUM$="",QUE_CODE$="",DB_TYPE$="O"
10112 FILES_SELECTED.CTL'VALUE$="0"
10115 IF MEMCHN THEN CLOSE (MEMCHN); MEMCHN=0
10116 IF SQLCHAN THEN CLOSE (SQLCHAN); SQLCHAN=0
10120 REFRESH_FLG=1
10145 RETURN 
10149 ! 
10150 GET_SQLFILE_PATH:
10155 CALL "[wdx]*OCX;BROWSE_FOR_FOLDER","Save SQL file to this folder...",SQL_PATH$
10160 IF NUL(SQL_PATH$) THEN RETURN 
10165 SERIAL %WDX$+SQL_PATH$+"\drsql.txt",ERR=*NEXT
10170 OPEN PURGE (HFN)%WDX$+SQL_PATH$+"\drsql.txt"; SQLCHAN=LFO
10195 RETURN 
10199 ! 
56001 ! 
56002 REM "249336-Data rep bulk setup utility isssue exporting SQL for AR5
56003 REM "271011-Datarep target model is being truncated  
56004 REM "285801-Ability to control tablename case in CREATE statements   
56005 REM "301133-Controls on some selections for the Data Rep Target setup   
