0010 REM "Purchase Order Special Shipping Receiving <PO2MAF>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 10/25/13 - 15.985833 - tma - SSP# 265590
0037 REM "265590-Display issue in Special shipping during PO recvg and AR    
0040 REM "Copyright 2013 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 INIT:
0105 X3$=ARG_1$,X4$=ARG_2$,Q0$=ARG_3$,Q1$=ARG_4$
0110 X0$="PO2MAF",X1$="P/O Special Shipping Receiving",K9$="",K9=0
0120 _SV_KR=PRM('KR'); SET_PARAM 'KR'=0 ! Ensure native handling of KEP()
0125 PERFORM "POGMAG;SETUP_LIBRARY"; IF %FLMAINT_MSG$<>"" THEN MESSAGE_LIB %FLMAINT_MSG$
0130 K0=6,K1=1
0131 DIM Z0$(80,"-"),C0$(12),S9$(40)
0135 C9=-1
0140 M0$="##,###,###.00-",M1$="##,##0"
0145 GOSUB 8950
0175 DEF FNS$(Z9$)=Z9$(1,POS("   "=Z9$+"   ")-1)
0180 DEF FNN(Z9)=Z9/M
0185 DEF FNR$(Z9$)=Z9$(POS(" "<>Z9$))
0200 REM "
0230 IF Q1$<>"" THEN X2=-1
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)),Z1$(LEN(M0$),X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE'),Z1$=MNM('GS')+Z1$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST A2$
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19]
0330 IOLIST C$,C[0],C[1],C[2],C[3]
0340 IOLIST D$
0350 IOLIST E$
0370 IOLIST W$,W[0],W[1],W[2],W[3],W[4],W[5],W[6],W[7],W[8],W[9],W[10],W[11],W[12]
0372 IOLIST S$,S[0],S[1],S[2],S[3],S[4],S[5],S[6],S[7],S[8],S[9],S[10],S[11],S[12]
0400 IOLIST I$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01X PO1...  02O PO2...  03O PO3...  04O AP4...  05O FM0...  06O ZZPARM  07O PO7...  08O AR1...  09O POA...  10O IC0...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0580 READ (Z[6],KEY=X3$(9,3)+"I/C")P9$
0585 P8=NUM(P9$(40,2)),P9=NUM(P9$(42,1))
0590 READ (Z[1],KEY=K9$,DOM=0591)
0600 REM "
0630 IF POS("P/O"=X4$(7),12)=0 THEN X4$=X4$+"P/O"+" z2Z6A1  "; WRITE (Z[6],KEY=X3$(9,3)+"ZZP")X4$
0800 REM 
0990 END_INIT:
0995 RETURN 
0999 ! ****************************************************
1000 MAIN_POST_DISPLAY:
1010 ! CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
1030 DIM B$(227),B[19],C$(171),C[3],D$(397),D[15],E$(53),E[1],W[12],S[12],I$(300)
1035 DIM A$(22)
1045 A$(1,0+K9)=K9$+A1$
1050 IF Q1$<>"" THEN GOTO 1100
1060 ! PRINT (0,ERR=1070)'CF',; GOSUB 6450; GOTO 1100
1100 REM "KEY SEQ
1110 X0=-1,C0=0,X1=0,C9=0,Z9=0
1120 C0=ABS(C0),X$=""; IF C0=2 AND X0>0 THEN X0=X0-1 ELSE IF C0<>2 THEN X0=X0+1; IF C0=4 THEN GOTO 9700
1140 ON X0 GOTO 1200,1250,1300,END_MAIN_POST_DISPLAY
1200 REM "P/O number
1202 A$(1,9)=Q1$,PORDER$=A$(1,9); GOTO 1220
1210 ! CALL "ZZENTR","AXUX",A{ALL},A$,X4$,X3$,39,3+V0,1,9,C0,"P/O","{1"+X$,"","PO2MAF00","PO1","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1210,1211
1215 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1210,1210,9700,6970
1220 READ (Z[1],KEY=A$(1,9),DOM=ERR_IN_Q1)IOL=0310
1225 FIND (Z[4],KEY=A2$(7,10),DOM=1226)IOL=0340
1230 IF POS(" "<>A2$(17,4))<>0 THEN FIND (Z[5],KEY="P"+A2$(7,14),DOM=1231)IOL=0350
1235 GOSUB 7600
1236 GOSUB 7900
1240 GOTO 1120
1250 REM "Invoice number
1254 A$(10,10)=MID(Q1$,10),AP_INV_NUM$=A$(10,10); GOTO 1266
1259 A8$=A$(1,9)
1260 ! CALL "ZZENTR","SXU",A{ALL},A$,X4$,X3$,17,6,10,10,C0,"P/O","{2"+X$,A8$,"PO2MAF02","PO7","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1260,1261
1265 IF C0<0 THEN ON INT(ABS(C0)-2) GOTO 1266,6800,9800
1290 GOTO 1120
1300 REM "Line number
1304 A$(20,3)=MID(Q1$,20,3),LINE_NUM$=A$(20,3); GOTO 1320
1306 Q$=A$(1,9)
1310 ! CALL "ZZENTR","ZUX",A{ALL},A$,X4$,X3$,10,7,20,3,C0,"","{2"+X$,Q$,"PO2MAF04","PO2","",""; IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 1310,1311
1312 IF ABS(C0)=2 AND LEN(A1$)>10 THEN A1$=A1$(1,10)
1315 IF C0<0 THEN IF -C0=1 THEN GOTO 1310 ELSE C0=ABS(C0)
1320 READ (Z[2],KEY=A$(1,9)+A$(20,3),DOM=ERR_IN_Q1)IOL=0320
1325 READ (Z[7],KEY=A$(1,22)+" ",DOM=1326)IOL=0370
1330 GOSUB 7700
1335 GOSUB 7750
1340 GOTO 1120
1400 END_MAIN_POST_DISPLAY:
1405 REFRESH_FLG=1
1450 GOSUB LOAD_GRID
1495 RETURN 
1499 ! **************************************
1500 LOAD_GRID:! Read PO3 records and set values from PO7 records if avail. then display
1505 T=0
1506 GRID CLEAR R_GRID.CTL,0,0,ERR=*NEXT
1507 ROW_NO=0
1510 Y=9,L$="",L=0,E0=0,F0$="*"; DIM L[250]
1520 IF A2$(173,1)="C" THEN A1$=A$(1,8)+" "+A$(20,3) ELSE A1$=A$(1,9)+A$(20,3)
1530 READ (Z[3],KEY=A1$,DOM=1531)
1540 IF POS(B$(10,1)="IC")<>0 THEN READ (Z[10],KEY=B$(91,20),DOM=1541)IOL=0400
1550 READ (Z[3],END=1800)IOL=0330
1560 IF C$(166,9)+C$(7,3)<>A1$ THEN GOTO 1800
1600 REM 
1610 C[3]=0
1620 READ (Z[7],KEY=A$(1,22)+C$(10,1),DOM=1630)IOL=0372
1625 C[3]=S[0]
1630 IF C[3]=0 AND B[11]<>0 THEN C[3]=C[0]*W[0]/B[11]
1700 REM "Display
1705 IF C$(225,35)<>S9$(1,35) THEN C0$=C$(225,28)
1720 L$=L$+A$(1,22)+C$(10,1),L=L+1,T=T+C[3],L[L]=C[3]
1730 GOSUB 7500
1740 IF L[L]<>0 THEN X0=L; GOSUB 7850
1790 GOTO 1550
1800 REM "Input values for a page
1810 GOSUB 7800
1820 GRID GOTO R_GRID.CTL,4,1,ERR=*PROCEED
1895 RETURN 
1899 ! *************************************************
1900 WRAPUP:! Finish up
1960 REM "If end check balance, and leave
1970 IF T<>W[0] THEN MSGBOX MSG("POGMAF_07"),MSG("MB_ATTN"),"!"; NEXT_ID=R_GRID.CTL; IGNORE_EXIT=1; EXIT 
1975 IF POS(B$(10,1)="IC")<>0 THEN P$=B$(82,9)+B$(7,3),P1$=A2$(173,1); CALL "POGMAC",X3$,X4$,P$,P1$,I$
1980 IF P9$(44,1)="Y" THEN IF I$(112,1)="Y" AND T<>0 THEN CALL "PO2MAU",X3$,X4$,A$(1,22),B{ALL},""," ",8800,"",S{ALL},""; PROCESS "ICGLAB","../ICG/IC.EN",X3$,X4$,"",A$(1,22); J9=C9,C9=-1; C9=J9 ! CALL "PO2MAU",X3$,X4$,A$(1,22),B{ALL},""," ",8800,"",S{ALL},"" ! SSP 239950 SSP 242707
1990 GOTO 9900
2000 REM "DATA SEQ
2100 SEL_RECEIVE_NOW:
2105 X0=R_GRID.ROW
2110 T=MAX(0,T-L[X0])
2115 L[X0]=NUM(R_GRID$,ERR=*NEXT)*M
2120 T=T+L[X0]; GOSUB 7800
2130 GOSUB 7850
2135 GRID LOAD R_GRID.CTL,4,X0,STR(FNN(L[X0]):M0$)+ESC,ERR=*PROCEED
2137 IF X0<ROW_NO THEN GRID GOTO R_GRID.CTL,4,X0+1,ERR=*PROCEED
2140 RETURN 
3000 REM "Run shipto setup
3005 U2$="PO2MAF"+STR(LEN(Q1$):"00")+Q1$
3010 IF A2$(173,1)="C" THEN U1$="FM2ODC11"+A$(1,8)+A$(20,3)+U2$+U1$ ELSE U1$="PO2MAD12"+A$(1,9)+A$(20,3)+U2$+U1$
3020 GOTO 9710
6600 REM "ALT KEY DATA READS
6690 RETURN 
7300 REM 
7310 ON FPT(C0)*10 GOTO 7390,7325,7330,7390
7325 IF V8$="" THEN X0=X0+1; GOTO 7390 ELSE X0=NUM(V8$(POS(STR(X0:"00")<=V8$,2),2)); GOTO 7390
7330 IF V8$="" THEN X0=X0-2; GOTO 7335 ELSE X0=POS(STR(X0-1:"00")<=V8$,2)-2; IF X0<0 THEN X0=0; GOTO 7390 ELSE X0=NUM(V8$(X0,2)); GOTO 7390
7335 IF X0<0 THEN X0=0; GOTO 7390
7390 C0=3; RETURN 
7500 REM "Display a ship to record
7505 G_ROW$=""
7510 G_ROW$=C$(11,1)+"-"+C$(12,4)+" "+C0$+SEP+STR(FNN(C[0]):M0$)+SEP+STR(FNN(C[2]):M0$)+SEP+STR(FNN(C[3]):M0$)+"&"
7512 ! IF Y=9 THEN PRINT @(9,Y),C0$,
7515 ! IF C$(11,1)="D" THEN PRINT @(2,Y+1),"Our warehouse in "+FNS$(C$(106,16))+","+C$(122,2), ELSE PRINT @(2,Y+1),FNS$(C$(46,30))+"  "+FNS$(C$(106,16))+","+C$(122,2),
7530 GRID LOAD R_GRID.CTL,1,++ROW_NO,G_ROW$,ERR=*NEXT
7535 FOR J=1 TO 3; GRID LOCK R_GRID.CTL,J,ROW_NO,ERR=*PROCEED; NEXT J
7540 RETURN 
7600 REM "Print P/O stuff
7610 VENDOR$=A2$(7,10)
7615 VEND_NAME$=D$(11,35),PLANT_CODE$=A2$(17,4),PLANT_NAME$=A2$(21,35)
7640 RETURN 
7700 REM "Set U/M divisor M
7705 DIM M$(21),M[1]
7710 READ (Z[13],KEY="U/M"+B$(77,4),DOM=7711)M$,M[0],M[1]
7715 IF M$(20,1)="Y" THEN M=M[1] ELSE M=B[2]
7720 IF M=0 THEN M=1
7740 RETURN 
7750 REM "Print desc from B$
7760 H9$=FNS$(B$(17,8)); IF H9$<>"" THEN H9$=H9$+QUO; IF POS(" "<>B$(111,8))<>0 THEN H9$=H9$+"x"
7770 H9$=H9$+FNS$(B$(111,8)); IF H9$<>"" THEN IF POS(" "<>B$(111,8))<>0 THEN H9$=H9$+QUO+" " ELSE H9$=H9$+" "
7775 IF NUM(B$(15,2))<>0 THEN H9$=H9$+B$(15,2)+" Ply "
7780 H9$=H9$+FNS$(B$(28,40))
7785 LINE_DESC$=H9$,LINE_QTY$=STR(FNN(W[0]):M0$)
7786 UM$=STP(B$(77,4),1)+"/"+STP(STR(B[2]:M1$),2) ! SSP#265590
7790 RETURN 
7800 REM "Print total
7805 T=0; FOR XX=0 TO ROW_NO; T+=L[XX]; NEXT XX
7810 GRID LOAD R_GRID.CTL,1,ROW_NO+1,SEP+SEP+MSG("TOTAL")+SEP+STR(FNN(T):M0$)+ESC,ERR=*NEXT
7815 FOR J=1 TO 4; GRID LOCK R_GRID.CTL,J,ROW_NO+1,ERR=*PROCEED; NEXT J
7840 RETURN 
7850 REM "Write amount out to PO7 file
7855 DIM S[12]; S$=W$,S$(1,23)=L$((X0-1)*23+1,23),S[0]=L[X0],S[1]=W[1]; GOSUB 7950
7860 IF S$(23,1)=" " THEN GOTO 7890
7865 IF S[0]<>0 THEN GOTO 7870
7866 REMOVE (Z[7],KEY=S$(1,23),DOM=7867)
7867 REMOVE (Z[9],KEY=S$(10,10)+S$(1,9)+S$(20,4),DOM=7868)
7868 GOTO 7890
7870 GOSUB 8000
7872 S$(39,6)=ARG_6$,S$(45,6)=ARG_5$; WRITE (Z[7],KEY=S$(1,23))IOL=0372
7875 WRITE (Z[9],KEY=S$(10,10)+S$(1,9)+S$(20,4))
7890 RETURN 
7900 REM "Set C0$ to customer name if it exists
7905 C0$=""
7910 IF A2$(173,1)<>"C" THEN GOTO 7940
7920 READ (Z[8],KEY=A2$(179,10),DOM=7940)A3$
7930 C0$=A3$(11,28)
7940 RETURN 
7950 REM "Figure extension S(2)
7955 IF M$(21,1)<>"Y" THEN GOTO 7975
7960 PRECISION 14
7965 IF W[0]=0 THEN S[2]=0 ELSE S[2]=W[1]*L[X0]/W[0]
7970 PRECISION 2; GOTO 7990
7975 IF M=0 THEN S[2]=0 ELSE S[2]=S[0]/M*S[1]; IF M[0]<>0 THEN S[2]=S[2]/M[0]
7990 RETURN 
8000 REM "Check for stock order and need to change inv G/L acct number
8005 IF P9$(39,1)<>"Y" THEN GOTO 8090
8010 IF A2$(173,1)="C" AND A2$(351,1)<>"B" THEN GOTO 8090
8012 IF F0$<>"*" THEN READ (Z[3],KEY=S$(1,8)+" "+S$(20,4),DOM=2126)IOL=0330
8013 GOSUB 8100
8015 H9$=S$(27,12)
8020 H9$(P8,P9)=E$(214,P9)
8030 S$(27,12)=H9$
8090 RETURN 
8100 REM "Read warehouse record
8105 ON POS(C$(11,1)="DP") GOTO 8150,8110,8120
8110 DIM D9$(10); READ (Z[5],KEY=C$(11,1)+D9$+C$(12,4),DOM=8150)IOL=0350; GOTO 8140
8120 READ (Z[5],KEY=C$(11,1)+D$(1,10)+C$(12,4),DOM=8150)IOL=0350; GOTO 8140
8140 RETURN 
8150 EXITTO 8090
8900 ERR_IN_Q1:! Error in the data passed in via q1$
8910 MSGBOX MSG("POGMAF_01","|"+Q1$+"|"),MSG("MB_ATTN"),"!"
8940 CMD_STR$="END"
8945 EXIT 
8949 ! *************************************************
8950 REM "Program flow logic
8951 RETURN 
8960 DIM U0$(6); U0$(1)=FID(0),U0$=U0$+"PF"
8970 CLOSE (14); OPEN (14)"ZZPARM"; FIND (14,KEY=U0$,DOM=8990)U0$,U1$
8975 IF U1$(1,6)<>X0$ THEN Q1$=""; GOTO 8990
8980 Q1=NUM(U1$(7,2)),Q1$=U1$(9,Q1),U1$=U1$(Q1+9)
8985 IF LEN(Q1$)<=9 THEN F9$="" ELSE IF Q1$(10)="*" THEN Q1$=Q1$(1,9),F9$="*"
8990 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000
9520 ON C9 GOTO 1140,2040
9700 REM "PROG FLOW
9710 IF U1$="" THEN REMOVE (Z[13],KEY=U0$,DOM=9900); GOTO 9900
9715 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9720 WRITE (Z[13],KEY=U0$)U0$,U1$
9730 RUN U1$(1,6)
9800 REM "EXIT PROGRAM
9900 ! END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9915 %CONT$=C$(11,1)+C$(12,4) ! SSP 197923
9920 MESSAGE_LIB _SV_MSGLIB$
9922 SET_PARAM 'KR'=_SV_KR
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 ! "197923-Issue with vendor costs. Does the cost come from PO         
56001 ! "       receiving or the vendor invoice"
56002 REM "239950-P/O Receiving-Multiple Vendor bill and Receivings in batch  
56003 REM "242707-Cannot delete a PO Lot record after receiving has been      
56004 REM "265590-Display issue in Special shipping during PO recvg and AR    
