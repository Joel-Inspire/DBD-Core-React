0010 ! POGPAS - Purchase order printing
0035 REM "5.7 - 07/05/16 - 15.858333 - crg - SSP# 285703
0037 REM "285703-Comp 589:  Unform does not give warehouse delivery option   
0040 REM "Copyright 2016 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 PROCESS "POGPAS","../POG/PO.EN"
0060 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 _SV_KR=PRM('KR'); SET_PARAM 'KR'=0 ! Ensure native handling of KEP()
0130 X0=-1,X2=-1
0140 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2
0150 ! 
0155 DIM ZZS$(100),PRT$(125),RNG[4],B.RNG[4]
0160 ! iolist
0170 IOLIST ZZS$(1)
0180 IOLIST PRT$(1),RNG$,RNG{ALL}
0300 ! Open Files
0310 DIM Z[NUM(X3$(60,3))]
0320 Z$="01O ZZS...  06O ZZPARM  07O ZZP     "
0330 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
0400 ! 
0410 RPT_SELEC$="            "; VIEW$=""
0480 GOSUB GET_PARAM
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 KEY_1$="PO2PAA"+RPT_SELEC$; %K9$="PO2PAA"
0530 READ (Z[1],KEY=KEY_1$,DOM=ADD_OPT)IOL=0170,IOL=0180
0570 B_ZZS$=ZZS$; B_PRT$=PRT$; B_RNG$=RNG$; CHG_FLG=0
0580 GOSUB SELECT_OPTIONS
0585 IF P0$(185,1)="Y" THEN CALL "*wingrp;show",SUFF.GRP$
0590 RETURN 
0600 ! selection Options
0610 SELECT_OPTIONS:
0630 PRG_NAME$=ZZS$(1,6); RPT_SELEC$=ZZS$(7,12); RPT_DESC$=ZZS$(19,40)
0640 RPT_OPT$=ZZS$(59,12); RPT_SEQ$=ZZS$(71,1)
0650 SUFF_CODE$=ZZS$(72,10); RPT_PER$=ZZS$(82,2); NOT_USED$=ZZS$(84,12)
0660 RPT_DFLT$=ZZS$(96,2)
0665 EN_JC_OV$=ZZS$(98,1)
0670 EN_SPLIT_SHIP$=ZZS$(99,1) ! 285703
0800 ! Output Options
0810 PRT_MODE$=PRT$(1,1); RPT_OUTDEV$=PRT$(2,6)
0820 RPT_VERT$=PRT$(9,1); RPT_HORIZ$=PRT$(10,1)
0830 RPT_AUX1$=PRT$(11,1); RPT_AUX2$=PRT$(12,1); RPT_AUX3$=PRT$(13,1); RPT_AUX4$=PRT$(14,1)
0840 RPT_LINES$=PRT$(15,2); RPT_FORMCODE$=PRT$(17,1)
0850 RPT_JOBCARD$=PRT$(65,60); RPT_JOBCARD_FLG$=PRT$(125,1)
0860 GOSUB LOAD_RANGE
0870 NEXT_ID=RPT_VEIW.CTL
0880 REFRESH_FLG=1
0890 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0960 SET_PARAM 'KR'=_SV_KR
0970 EXIT 
1000 ! load the sequence range
1010 LOAD_RANGE:
1020 SWITCH RPT_SEQ$
1030 CASE "1"
1040 IF NUL(RNG$(3,9)) THEN ML_PONUM_START$="" ELSE ML_PONUM_START$=RNG$(3,9)
1050 IF RNG$(12,3)="~~~" THEN ML_PONUM_END$="" ELSE ML_PONUM_END$=RNG$(12,9)
1055 NEXTFOLDER_FLG=FLDR.PO1.1.CTL
1058 RETURN 
1060 CASE "2"
1070 IF NUL(RNG$(1,10)) THEN VEND_START$="" ELSE VEND_START$=RNG$(1,10)
1080 IF RNG$(11,10)="~~~~~~~~~~" THEN VEND_END$="" ELSE VEND_END$=RNG$(11,10) ! SSP#262970
1090 IF NUL(RNG$(21,4)) THEN PLANT_START$="" ELSE PLANT_START$=RNG$(21,4)
1100 IF RNG$(25,3)="~~~" THEN PLANT_END$="" ELSE PLANT_END$=RNG$(25,4)
1110 IF NUL(RNG$(29,9)) THEN ML_PONUM_START$="" ELSE ML_PONUM_START$=RNG$(29,9)
1120 IF RNG$(38,3)="~~~" THEN ML_PONUM_END$="" ELSE ML_PONUM_END$=RNG$(38,9); REM "SSP#204829
1130 NEXTFOLDER_FLG=FLDR.PO1.2.CTL
1135 RETURN 
1490 END SWITCH 
1500 RETURN 
3800 CHECK_CHANGES:
3805 GOSUB LOAD_OPTIONS
3810 IF CHG_FLG=0 THEN RETURN 
3820 MSGBOX MSG("UPD_RPT_1")+SEP+MSG("UPD_RPT_2"),MSG("CONFIRM"),"?,YESNO",ANS$
3840 IF ANS$="NO" THEN RETURN 
3850 WRITE (Z[1],KEY=KEY_1$)IOL=0170,IOL=0180
3860 B_ZZS$=ZZS$; B_PRT$=PRT$; B_RNG$=RNG$; CHG_FLG=0
3870 RETURN 
3880 ! 
8000 ! Messages
8010 ADD_OPT:
8020 MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"?,YesNo",ANS$
8030 IF ANS$="NO" THEN CMD_STR$="END"; RETURN 
8040 KEY_1$="PO2PAA"+RPT_SELEC$
8050 DIM ZZS$(100),RNG$(100); ZZS$(1,18)=KEY_1$,ZZS$(71,1)="1"
8060 GOSUB SELECT_OPTIONS
8070 REFRESH_FLG=1; NEXT_ID=RPT_DESC.CTL
8090 RETURN 
9000 ! Get system Parameters
9010 GET_PARAM:
9020 READ (Z[6],KEY=X3$(9,3)+"F/M")P0$ ! TOD_PO=0; IF MID(P0$,318,1)="Y" THEN TOD_PO=1
9090 RETURN 
9999 END 
17000 ! 
20000 LOAD_OPTIONS:
20010 ! View Report Routine.
20020 ! 
20030 ZZS$(1,6)=PRG_NAME$
20040 ZZS$(7,12)=RPT_SELEC$; ZZS$(19,40)=RPT_DESC$
20050 ZZS$(59,12)=RPT_OPT$; ZZS$(71,1)=RPT_SEQ$
20060 ZZS$(72,10)=SUFF_CODE$; ZZS$(82,2)=RPT_PER$; ZZS$(84,12)=NOT_USED$
20070 ZZS$(96,2)=RPT_DFLT$
20080 ZZS$(98,1)=EN_JC_OV$ ! jobcard override
20085 ZZS$(99,1)=EN_SPLIT_SHIP$ ! SSP#285703-Enable separate jobs for split ships
20100 ! 
20110 PRT$(1,1)=PRT_MODE$; PRT$(2,2)=RPT_OUTDEV$
20120 PRT$(9,1)=RPT_VERT$; PRT$(10,1)=RPT_HORIZ$
20130 PRT$(11,1)=RPT_AUX1$; PRT$(12,1)=RPT_AUX2$
20140 PRT$(13,1)=RPT_AUX3$; PRT$(14,1)=RPT_AUX4$
20150 PRT$(15,2)=RPT_LINES$; PRT$(17,1)=RPT_FORMCODE$
20160 PRT$(65,60)=RPT_JOBCARD$; PRT$(125,1)=RPT_JOBCARD_FLG$
20170 ! 
20175 RNG$=""
20180 ! 
20190 IF RPT_SEQ$="1" THEN {
20200 T1$="PO6..."
20205 RNG$="PP"
20210 GOSUB GET_PO_RANGE
20215 RNG$+="   999" }
20220 IF RPT_SEQ$="2" THEN {
20230 T1$="PO4..."
20235 GOSUB GET_VEND_RANGE
20240 GOSUB GET_PLANT_RANGE
20250 GOSUB GET_PO_RANGE }
20300 IF B_ZZS$=ZZS$ AND B_PRT$=PRT$ AND B_RNG$=RNG$ THEN CHG_FLG=0 ELSE CHG_FLG=1
20390 RETURN 
25000 ! 
25010 PRINT_REPORT:
25020 IF NOT(NUL(VIEW$)) THEN PRT$(2,2)=VIEW$
25030 K$=X3$(1,8)
25040 %B_WINDATE_FMT$=%WINDATE_FMT$,%WINDATE_FMT$="YYYYMMDD",JJ$=DTE(0:"%Yl%Mz%Dz"); CALL "ZZWDTE;VALIDATE",JJ$,JJJ$,JJJJ$; X3$(21,6)=JJ$
25050 WRITE (Z[7],KEY=K$)X3$,X4$,T1$,PRT$,RNG$,ZZS$
25055 GOSUB GET_PROGRAM
25060 CALL K9$
25065 PRINT (0,ERR=*NEXT)'GOTO'(200),'POP'
25070 CMD_STR$="END"
25080 RETURN 
26000 ! Determine which program to run 
26010 GET_PROGRAM:
26020 K9$="PO2PAA"
26030 IF MID(RPT_OPT$,1,1)="2" THEN K9$="PO2PAB"
26040 IF MID(RPT_OPT$,1,1)="3" THEN K9$="PO2PAF"
26050 ! IF TOD_PO THEN K9$="PO2PAR" ! SSP212097
26090 RETURN 
28000 ! 
28010 GET_PO_RANGE:
28020 IF NUL(ML_PONUM_START$) THEN RNG$+="         " ELSE RNG$+=ML_PONUM_START$
28030 IF NUL(ML_PONUM_END$) THEN RNG$=RNG$+"~~~~~~~~~" ELSE RNG$=RNG$+ML_PONUM_END$
28040 RETURN 
28050 ! 
28060 GET_PLANT_RANGE:
28070 IF NUL(PLANT_START$) THEN RNG$+="    " ELSE RNG$+=PLANT_START$
28080 IF NUL(PLANT_END$) THEN RNG$+="~~~~" ELSE RNG$+=PLANT_END$
28090 RETURN 
28100 ! 
28110 GET_VEND_RANGE:
28120 IF NUL(VEND_START$) THEN RNG$+="          " ELSE RNG$=RNG$+VEND_START$
28130 IF NUL(VEND_END$) THEN RNG$=RNG$+"~~~~~~~~~~" ELSE RNG$=RNG$+VEND_END$; REM "SSP#204829
28140 RETURN 
28150 ! 
28160 GET_SHIPTO_RANGE:
28170 IF NUL(SHIPTO_START$) THEN RNG$=RNG$+"    " ELSE RNG$=RNG$+SHIPTO_START$
28180 IF NUL(SHIPTO_END$) THEN RNG$=RNG$+"~~~~" ELSE RNG$=RNG$+SHIPTO_END$
28190 RETURN 
30180 LOAD_FISCAL_YEAR:
30190 ZZPARM=HFN
30200 OPEN (ZZPARM)"ZZPARM"
30210 READ (ZZPARM,KEY=%C$+"A/R",DOM=*NEXT)GL_REC$; CR_YEAR$=GL_REC$(7,4),CR_PD$=GL_REC$(11,2)
30215 IF NUL(RPT_FISYR$)=0 THEN CR_YEAR$=RPT_FISYR$
30220 KE$=%C$+"G/LYE",RPT_FISYR$=""
30230 READ (ZZPARM,KEY=KE$,DOM=*NEXT)
30240 K$=KEY(ZZPARM,END=30280); READ (ZZPARM)Z9$
30250 IF LEN(K$)<8 OR KE$<>K$(1,8) THEN GOTO END_LOAD_FISCAL_YEAR
30260 RPT_FISYR$=RPT_FISYR$+K$(9)+"/"
30265 IF K$(9,4)=CR_YEAR$ THEN CC$=Z9$(21)
30270 GOTO 30240
30280 END_LOAD_FISCAL_YEAR:
30290 DROP_BOX LOAD RPT_FISYR.CTL,RPT_FISYR$
30300 DROP_BOX WRITE RPT_FISYR.CTL,CR_YEAR$
30310 DROP_BOX LOAD END_FISCAL.CTL,RPT_FISYR$
30320 DROP_BOX WRITE END_FISCAL.CTL,CR_YEAR$
30321 RPT_FISYR$=CR_YEAR$
30322 IF NUL(RPT_PER$)=0 THEN CR_PD$=RPT_PER$
30325 RPT_PER$=""
30330 FOR ACCT=1 TO 12; RPT_PER$=RPT_PER$+STR(ACCT:"00")+"/"; NEXT ACCT
30340 DROP_BOX LOAD RPT_PER.CTL,RPT_PER$
30350 DROP_BOX WRITE RPT_PER.CTL,CR_PD$
30355 RPT_PER$=CR_PD$
30360 END_ACCTPD$=RPT_PER$
30370 DROP_BOX LOAD END_ACCTPD.CTL,RPT_PER$
30380 DROP_BOX WRITE END_ACCTPD.CTL,CR_PD$
30390 RETURN 
32000 LOAD_ENDING_DATE:
32005 IF LEN(RPT_FISYR$)>4 THEN VAL$=CR_YEAR$ ELSE VAL$=RPT_FISYR$
32010 FIND (ZZPARM,KEY=KE$+VAL$)Z9$; CC$=Z9$(21)
32011 IF LEN(RPT_PER$)>2 THEN VAL=NUM(CR_PD$) ELSE VAL=NUM(RPT_PER$)
32020 CC$=CC$(VAL*6-5,6),RPT_AGEDATE$=CC$,REFRESH_FLG=1
32030 RETURN 
40000 SEL_QRY:ENTER PRIME_KEY$
40010 IF NUL(MID(PRIME_KEY$,7,6)) THEN EXIT 123
40020 EXIT 
56000 REM "186670-Add ability to output purchase orders by a Vendor Set       
56001 REM "204829-Getting error in PO printing                                
56002 REM "212097-TOD Purchase Order Printing                                 
56003 REM "285703-Comp 589:  Unform does not give warehouse delivery option   
