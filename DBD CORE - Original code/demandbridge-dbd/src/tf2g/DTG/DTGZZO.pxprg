0010 REM "DTGZZO - On-demand DB/d Scheduled Reports runner
0035 REM "5.7 - 10/04/16 - 13.106111 - crg - SSP# 287534
0037 REM "287534-On-demand execution of scheduled DB/d reports 
0040 REM "Copyright 2016 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! 
0070 IF TCB(88)=0 THEN MSGBOX "You must be using Windx to access this program" ELSE PROCESS "DTGZZO","../DTG/DT.EN"
0080 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 X0=-1
0160 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2; IF X1>0 THEN CMD_STR$="END"; EXIT 
0190 DIM Z[NUM(X3$(60,3))]
0200 ! Files
0210 Z$="22O ZZK...  23O DTK...  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
0300 ! IOLISTS
0310 IOL_ZZK_S:IOLIST ZZK_KEY_S$,RPT_PGM_NAME$,RPT_DESC$,SCHD_MM$,SCHD_HH$,SCHD_DOM$,SCHD_MTH$,SCHD_DOW$ ! Scheduling Options
0320 IOL_DTK:IOLIST DTK_KEY$,DTK_SCHD_DATE$,DTK_SCHD_TIME$,DTK_RUN_DATE$,DTK_RUN_TIME$ ! Report Run History
0400 ! 
0410 _FYI$=MSG("FYI"),_ERROR$=MSG("ERROR")
0420 CTAB$=DIM(2)
0480 GOSUB DEFINE_LOG_FUNCTION
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0530 GOSUB CLEAR_REC
0570 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0970 RETURN 
1800 ! 1800 - Clear record
1810 CLEAR_REC:
1820 GOSUB LOAD_RPTS
1850 REFRESH_FLG=1
1860 RETURN 
1899 ! 
2000 LOAD_RPTS:
2005 GRID LOAD RPT_GRID.CTL,0,0,""; RPT_CNT=0
2007 IF FCHAN THEN CLOSE (FCHAN); FCHAN=0
2010 READ (Z[22],KEY="",DOM=*NEXT)
2020 NEXT_RPT:
2030 SJK$=KEY(Z[22],END=END_LOAD_RPTS); READ (Z[22],KEY=SJK$)IOL=IOL_ZZK_S
2040 IF MID(SJK$,32,1)<>"S" THEN GOTO NEXT_RPT
2095 LOAD_GRID_LINE:
2100 GRID LOAD RPT_GRID.CTL,1,0,SEP+SJK$+SEP+CTAB$+MID(SJK$,1,6)+SEP+CTAB$+MID(SJK$,7,6)+SEP+CTAB$+MID(SJK$,13,4)+SEP+CTAB$+MID(SJK$,17,3)+SEP+CTAB$+MID(SJK$,20,3)+SEP+CTAB$+MID(SJK$,23,2)+SEP+CTAB$+RPT_PGM_NAME$+SEP+CTAB$+SCHD_MM$+SEP+CTAB$+SCHD_HH$+SEP+CTAB$+SCHD_DOM$+SEP+CTAB$+SCHD_MTH$+SEP+CTAB$+SCHD_DOW$+SEP; ++RPT_CNT
2110 GOTO NEXT_RPT
2120 END_LOAD_RPTS:
2130 IF NOT(RPT_CNT) THEN GRID LOAD RPT_GRID.CTL,1,0,SEP
2135 GRID LOCK RPT_GRID.CTL,0,0; IF RPT_CNT THEN GRID UNLOCK RPT_GRID.CTL,1,0
2140 REFRESH_FLG=1
2150 RETURN 
2199 ! 
4200 ! Replicate selected filters
4210 RUN_RPTS:
4212 MSGBOX MSG("DTGZZO_01"),MSG("CONFIRM"),"YESNO,2",ANS$
4214 IF ANS$<>"YES" THEN GOTO END_RUN_RPTS
4286 GOSUB KICKOFF_RPTS
4290 END_RUN_RPTS:
4293 GOSUB LOAD_RPTS; REFRESH_FLG=1
4294 MSGBOX MSG("COMPLETED")+"!",_FYI$,"!"
4295 RETURN 
4299 ! 
4300 SET_READONLY:! SSP#253613   
4310 SET_PARAM 'XI'
4320 SET_READONLY_END:RETURN 
4330 CLEAR_READONLY:! SSP#253613             
4340 SET_PARAM -'XI'
4350 CLEAR_READONLY_END:RETURN 
5000 REM "Event handler - OnSelect
5005 SELECT_RPT:
5010 READ DATA FROM RPT_GRID.CTL'ROWDATA$ TO IOL=RPT_GRID.CTL'LOADIOLIST$
5015 IF NOT(FN%ISOPEN(FCHAN)) THEN FCHAN=HFN; OPEN (FCHAN)"*MEMORY*"
5020 IF C_SELECT$="1" THEN {
5030 WRITE (FCHAN,KEY=C_RPT_KEY$,ERR=*NEXT)C_RPT_KEY$
5040  } ELSE {
5050 REMOVE (FCHAN,KEY=C_RPT_KEY$,ERR=*NEXT)
5060  }
5095 RETURN 
5099 ! 
9999 END 
10000 KICKOFF_RPTS:
10010 REM "Iterate over selected ZZK records
10020 READ (FCHAN,KEY="",DOM=*NEXT)
10030 NEXT_SCHD_JOB_REC:
10040 SJK$=KEY(FCHAN,END=DONE_CSJ); READ (FCHAN,KEY=SJK$)
10050 PERFORM "DTGZZK;LOAD_SCHEDULED_JOB"
10080 GOTO NEXT_SCHD_JOB_REC
10090 DONE_CSJ:
10095 RETURN 
10999 ! 
50100 DEFINE_LOG_FUNCTION:! Short-circuited stub for global log function that is used by Output Director
50110 DEF FN%TOD_LOG(LOCAL MESSAGE_TYPE$, LOCAL MESSAGE$, LOCAL LOG_IT, LOCAL SHOW_IT)
50185 RETURN 0 ! Do nothing
50190 END DEF
50195 RETURN 
50199 ! 
56001 REM "287534-On-demand execution of scheduled DB/d reports 
