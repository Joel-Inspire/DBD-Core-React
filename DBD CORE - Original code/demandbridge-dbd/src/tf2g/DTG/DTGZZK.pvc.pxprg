0010 ! DTGZZK - Output Director Object for ZZK
0030 ! 
0035 REM "5.5 - 09/24/07 - 10.571388 - jme - SSP# 203099
0037 REM "203099-Enhancements to Output Director.  Provide ability for
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0100 DEF CLASS "DTGZZK" CREATE REQUIRED DELETE REQUIRED 
0110 LIKE "FILE"
0115 LIKE "DTGYES"
0120 LIKE "DTGDT0"
0200 ! start variable list
0210 LOCAL ZZK,DT6
0215 LOCAL TYPE$=PAD("SCHEDULED_DBD_RPT",20)
0220 PROPERTY ZZK$,DT6$,CURRENT_QUEUE$,IL_DT1$,IL_DT2$
0999 ! 
1000 FUNCTION GETFIRSTREC(TRANS_NUM$)
1005 ENTER QUEUE_NUM$
1020 FILENO=DT6
1030 X=_OBJ'READBYKEY(0,QUEUE_NUM$,"")
1040 IF X THEN X=_OBJ'EXTRACT()
1045 IF X THEN IF OUTPUT_TYPE$<>TYPE$ OR OUTPUT_QUEUE$<>QUEUE_NUM$ THEN X=0
1050 IF X THEN {
1053 IOLIST DT6$
1060 READ DATA FROM REC(IOL(FILENO)) TO IOL=1053
1070 CURRENT_QUEUE$=DT6$
1080  }
1090 RETURN X
1099 ! 
1100 FUNCTION FINDDT0MATCH()
1120 SEG1$=MID(DT6$,31,2)
1125 SEG2$=MID(DT6$,33,20)
1130 SEG3$=PAD(MID(DT6$,53,9),10)
1135 SEG4$=DIM(5)
1140 SEG5$="",SEG6$="",SEG7$="",SEG8$=""
1150 OK=_OBJ'GETDT0KEY(TYPE$,SEG1$,SEG2$,SEG3$,SEG4$,SEG5$,SEG6$,SEG7$,SEG8$)
1190 RETURN OK
1199 ! 
1200 FUNCTION SETYES()
1290 RETURN 1
1299 ! 
1300 FUNCTION DELYES()
1390 RETURN 1
1399 ! 
1400 FUNCTION CHECK(X$)
1490 RETURN 1
1499 ! 
1500 FUNCTION PRT_RPT(X)
1510 ENTER PRINTER_CHAN
1515 REPORT$=MID(CURRENT_QUEUE$,31,31)
1520 X=_OBJ'PRINT_REPORT(PRINTER_CHAN,REPORT$)
1590 RETURN 
1599 ! 
1600 FUNCTION GETNEXTDT0SEQ()
1605 FILENO=DT0
1620 X=_OBJ'READNEXT(); IF X THEN IF OUTPUT_TYPE$<>TYPE$ OR STP(DIVISION$+KEY1$+KEY2$+KEY3$,1)<>MID(DT0_KEY$,1,LEN(STP(DIVISION$+KEY1$+KEY2$+KEY3$,1))) THEN X=0
1630 IOLIST DT0$
1640 READ DATA FROM REC(IOL(FILENO)) TO IOL=1630
1690 RETURN X
1699 ! 
1800 FUNCTION PRINT_REPORT(X,X$)
1810 ENTER PRINTER_CHAN,REPORT_ID$
1815 ! 
1850 TMPPTH$=OPT(PRINTER_CHAN); P=POS("FILE="=TMPPTH$); IF P>0 THEN TMPPTH$=TMPPTH$(P+5); P=0; P=POS(".pdf"=TMPPTH$); IF P>0 THEN TMPPTH$=TMPPTH$(1,P-1)
1860 TODMSG$=_OBJ'_CLASS$+"|EXECUTE|Using file basename|"+TMPPTH$; MX=FN%TOD_LOG("INFO",TODMSG$,1,0)
1870 %SCHD_RPT_PATH$=TMPPTH$+"_&TITLE_NS"
1880 CLOSE (PRINTER_CHAN)
1885 ERASE TMPPTH$+".pdf",ERR=*NEXT
1899 ! 
1900 IOL_ZZK_P:IOLIST ZZK_KEY_P$,X3$,X4$,T1$,PRT$,RNG$,ZZS$,RPT_PGM$,RPT_SPLIT$
1901 IOL_ZZK_P_RO:IOLIST ZZK_KEY_P$,*,*,T1$,PRT$,RNG$,ZZS$,RPT_PGM$,RPT_SPLIT$ ! Do not update X3$, X4$
1902 IOL_ZZK_O:IOLIST ZZK_KEY_O$,%FROM_POS,%LAST_POS,%FIELD_LEN,%KEY_POS,%THE_KEY$,%FILE_TO_PROCESS$,%FROM_POS1,%LAST_POS1,%FIELD_LEN1,%BREAK$,%ACCTPD,%BREAK_FM0_LOC
1904 IOL_ZZK_S:IOLIST ZZK_KEY_S$,RPT_PGM_NAME$,RPT_DESC$,SCHD_EXP1$,SCHD_EXP2$,SCHD_EXP3$,SCHD_EXP4$,SCHD_EXP5$ ! Scheduling Options
1906 IOL_ZZK_D:IOLIST ZZK_KEY_D$,RPT_OPTIONS_DESC$,RPT_OPTIONS_TYPE$,RPT_OPTIONS_OFFSET$,RPT_OPTIONS_VAR$,RPT_OPTIONS_INITVAL$,RPT_OPTIONS_STEP$,RPT_OPTIONS_INCR$,RPT_OPTIONS_DOMAIN$
1908 IOL_ZZP:IOLIST %X3$,%X4$,T1$,PRT$,RNG$,ZZS$
1915 TODMSG$=_OBJ'_CLASS$+"|EXECUTE|Opening report|"+REPORT_ID$+"|"; MX=FN%TOD_LOG("INFO",TODMSG$,1,0)
1919 ! 
1920 READ (ZZK,KEY=REPORT_ID$+"P")IOL=IOL_ZZK_P_RO
1930 IF RPT_SPLIT$="Y" THEN READ (ZZK,KEY=REPORT_ID$+"O")IOL=IOL_ZZK_O
1940 REM "Get date fields and update report data with real-time values
1945 GOSUB LOAD_FISCAL_YEAR
1950 FOR D=1 TO 9
1960 READ (ZZK,KEY=REPORT_ID$+STR(D),DOM=NEXT_ZZK_D)IOL=IOL_ZZK_D
1970 GOSUB UPDATE_DATA_FIELD
1980 NEXT_ZZK_D:
1990 NEXT D
1999 ! 
2000 TODMSG$=_OBJ'_CLASS$+"|EXECUTE|Printing report"; MX=FN%TOD_LOG("INFO",TODMSG$,1,0)
2010 OPEN (HFN)"ZZP"; ZZP=LFO
2020 WRITE (ZZP,KEY=MID(%X3$,1,8))IOL=IOL_ZZP
2030 CLOSE (ZZP)
2035 CLOSE (ZZPARM,ERR=*NEXT)
2040 IF RPT_SPLIT$="Y" THEN %SCHD_RPT_PATH$+="_&BREAK_" ELSE %SCHD_RPT_PATH$+="_"
2045 ERASE %SCHD_RPT_PATH$,ERR=*NEXT
2050 IF RPT_SPLIT$="Y" THEN CALL "ZZ2DRV",RPT_PGM$ ELSE CALL RPT_PGM$
2200 TODMSG$=_OBJ'_CLASS$+"|EXECUTE|Closing report"; MX=FN%TOD_LOG("INFO",TODMSG$,1,0)
2390 %SCHD_RPT_PATH$=""
2400 RETURN 
8999 END DEF
9999 END 
10000 ON_CREATE:
10005 OPEN_STATUS=0
10007 FILENAME$="ZZK"
10010 COMPANY$=%C$
10051 OPEN (HFN,ERR=*RETURN)FILENAME$+COMPANY$
10060 ZZK=LFO
10061 FILENAME$="DT6"
10063 OPEN (HFN,IOL=*,ERR=*RETURN)FILENAME$+COMPANY$
10065 DT6=LFO
10070 STATIC IOL=IOL(DT6)
10080 OPEN_STATUS=1
10090 RETURN 
10099 ! 
10100 ON_DELETE:
10110 IF OPEN_STATUS THEN CLOSE (ZZK),(DT6)
10190 RETURN 
10199 ! 
15000 UPDATE_DATA_FIELD:
15010 UPDATE_FIELD$=RPT_OPTIONS_INITVAL$
15020 IF RPT_OPTIONS_STEP$="Use Initial Value" OR STP(RPT_OPTIONS_STEP$,2)="" THEN GOTO SET_VALUE ELSE GOTO *NEXT
15100 YYMM_STEP:
15110 PRD_INCR=0,PRD_INCR=NUM(RPT_OPTIONS_INCR$)
15115 FTYPE$="",FTYPE$=RPT_OPTIONS_DOMAIN$; IF POS(FTYPE$="ARAP")<>0 THEN GOSUB OTHER_FISCAL_YEAR
15120 ON POS(RPT_OPTIONS_TYPE$="246") GOTO 15590,15130,15200,15250
15130 ! Set Current Period
15140 CURR_PRD=NUM(CR_PD$),CURR_IPRD=CURR_PRD+PRD_INCR,CURR_PRD=CURR_IPRD
15150 IF CURR_IPRD>NUM_PERIODS THEN CURR_PRD=CURR_IPRD-NUM_PERIODS ELSE IF CURR_IPRD=0 THEN CURR_PRD=NUM_PERIODS ELSE IF CURR_IPRD<0 THEN CURR_PRD=CURR_IPRD+NUM_PERIODS
15180 UPDATE_FIELD$=STR(CURR_PRD:"00")
15190 GOTO SET_VALUE
15200 ! Set Current Year
15210 CURR_YR=NUM(CR_YEAR$),CURR_YR=CURR_YR+PRD_INCR
15220 UPDATE_FIELD$=STR(CURR_YR:"0000")
15240 GOTO SET_VALUE
15250 ! Set Current Date
15252 IF FTYPE$="" THEN GOTO *NEXT ELSE UPDATE_FIELD$=RPT_AGEDATE$; GOTO SET_VALUE
15255 DDY$=DTE(0:"%Ys%Mz%Dz")
15260 ! F LEN(Q$)=8 THEN GOTO 7520 ELSE IF LEN(Q$)=6 THEN D1$=STR(NUM(Q$(1,2)):"00") ELSE GOTO 7549
15265 ! F D1$<"50" THEN Q$="20"+Q$ ELSE Q$="19"+Q$
15270 Q0$=CHR(NUM(DDY$(1,3),ERR=SET_VALUE)-125)+DDY$(4,1)+DDY$(5,2)+DDY$(7,2)
15285 CALL "ZZDATE",%X3$,"",Q0$,REQUIRED_DATE$,PRD_INCR,0,0,0,0,0,0,"","",""
15290 UPDATE_FIELD$=REQUIRED_DATE$
15300 SET_VALUE:
15355 ST_POS=NUM(RPT_OPTIONS_OFFSET$),ST_LEN=NUM(RPT_OPTIONS_TYPE$)
15360 IF RPT_OPTIONS_VAR$="ZZS" THEN ZZS$(ST_POS,ST_LEN)=UPDATE_FIELD$ ELSE RNG$(ST_POS,ST_LEN)=UPDATE_FIELD$
15590 RETURN 
15599 ! 
30180 LOAD_FISCAL_YEAR:
30190 ZZPARM=HFN
30200 OPEN (ZZPARM)"ZZPARM"
30210 READ (ZZPARM,KEY=%C$+"G/L",DOM=*NEXT)GL_REC$; CR_YEAR$=GL_REC$(34,4),CR_PD$=GL_REC$(38,2)
30215 LOAD_ENDING_DATE:
30220 KE$=%C$+"G/LYE"
32005 VAL$=CR_YEAR$
32010 FIND (ZZPARM,KEY=KE$+VAL$)Z9$; CC$=Z9$(21)
32011 VAL=NUM(CR_PD$)
32020 CC$=CC$(VAL*6-5,6),RPT_AGEDATE$=CC$
32030 NUM_PERIODS=0,NUM_PERIODS=NUM(MID(Z9$,13,2),ERR=*NEXT)
32050 RETURN 
32100 OTHER_FISCAL_YEAR:
32110 IF FTYPE$="AR" THEN KTYPE$="A/R" ELSE IF FTYPE$="AP" THEN KTYPE$="A/P"
32120 READ (ZZPARM,KEY=%C$+KTYPE$,DOM=*NEXT)GL_REC$; CR_YEAR$=GL_REC$(7,4),CR_PD$=GL_REC$(11,2)
32130 GOSUB LOAD_ENDING_DATE
32150 RETURN 
