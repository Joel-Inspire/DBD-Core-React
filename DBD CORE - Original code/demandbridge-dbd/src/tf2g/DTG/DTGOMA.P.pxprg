0010 REM + Customer Output Control Maintenance  <DTGOMA.P>
0035 REM "5.7 - 08/20/14 - 16.9825 - tma - SSP# 272144
0037 REM "272144-Output control maintenance opens another window             
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 REM + Begin Program Notes
0052 REM "This program is called from DTGOMA. It cannont be run
0053 REM "stand alone.
0099 REM + End Program Notes
0100 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0310 IOLIST DT0$
0320 IOLIST AP4$,AP4[0],AP4[1]
0330 IOLIST ARH$,ARH[0]
0340 IOLIST FMP$
9000 ! 9000 - Retrieve messages from the *msglib.xxx library
9010 SETUP_MESSAGES:
9020 _FYI$=MSG("FYI"),_ERROR$=MSG("ERROR")
9030 _MSG_DIRECTORY$=MSG("DIRECTORY")
9040 _MSG_END_OF_FIL$=MSG("END_OF_FIL")
9050 _MSG_FILOPNERR1$=MSG("FILOPNERR1")
9060 _MSG_JUST_CHECK$=MSG("JUST_CHECK")
9070 _MSG_MANDATORY$=MSG("MANDATORY")
9080 _MSG_NEXT$=MSG("NEXT")
9090 _MSG_NON_NUMER$=MSG("NON_NUMER")
9100 _MSG_NOT_FOUND$=MSG("NOT_FOUND")
9110 _MSG_OVRWRT_CHG$=MSG("OVRWRT_CHG")
9120 _MSG_PRECEDING$=MSG("PRECEDING")
9130 _MSG_PREFIX$=MSG("PREFIX")
9140 _MSG_REC_ACCESS$=MSG("REC_ACCESS")
9150 _MSG_REC_ALTERD$=MSG("REC_ALTERD")
9160 _MSG_REC_CR_NEW$=MSG("REC_CR_NEW")
9170 _MSG_REC_LOCKED$=MSG("REC_LOCKED")
9180 _MSG_X$=MSG("REC_MISS","^"),P=POS("^"=_MSG_X$),_MSG_REC_MISS1$=_MSG_X$(1,P-1),_MSG_REC_MISS2$=_MSG_X$(P+1)
9190 _MSG_REC_NOTFND$=MSG("REC_NOTFND")
9200 _MSG_REC_NO_1ST$=MSG("REC_NO_1ST")
9210 _MSG_REC_NO_LST$=MSG("REC_NO_LST")
9220 _MSG_REC_REMOVE$=MSG("REC_REMOVE")
9230 _MSG_REC_UPDADD$=MSG("REC_UPDADD")
9240 _MSG_X$=MSG("REC_VFYDEL","^"),P=POS("^"=_MSG_X$),_MSG_REC_VFYDEL1$=_MSG_X$(1,P-1),_MSG_REC_VFYDEL2$=_MSG_X$(P+1)
9250 _MSG_X$=MSG("REC_VIEW","^"),P=POS("^"=_MSG_X$),_MSG_REC_VIEW1$=_MSG_X$(1,P-1),_MSG_REC_VIEW2$=_MSG_X$(P+1)
9260 _MSG_REQ_FIELDS$=MSG("REQ_FIELDS")
9270 _MSG_START_FILE$=MSG("START_FILE")
9280 _MSG_UPDATE$=MSG("UPDATE")
9290 _MSG_UPD_OTHER1$=MSG("UPD_OTHER1")
9300 _MSG_UPD_OTHER2$=MSG("UPD_OTHER2")
9310 _MSG_UPD_SAME$=MSG("UPD_SAME")
9320 _MSG_WARNING$=MSG("WARNING")
9330 _MSG_CANNOT_WRITE$=MSG("CANT_WRITE")
9340 _MSG_DUP_UNIQUE$=MSG("DUP_UNIQUE")
9350 RETURN 
9999 END 
12000 DT0_LOOKUP:
12002 REM "This routine will call a Query window to display all of the
12004 REM "DT0 records for the Output Type 
12010 VAR$=""
12015 QRY_USED=0
12020 PROCESS "QRY_DT0_PL","../DTG/DT.EN",VAR$
12050 IF VAR$<>"" THEN {
12055 QRY_USED=1
12060 TMP_OUTPUT_TYPE$=SUB(VAR$(1,20),"~"," ")
12065 DIVISION$=SUB(VAR$(21,2),"~"," ")
12070 CUSTOMER$=SUB(VAR$(23,20),"~"," ") ! 234689 - Was WHSE$
12075 WHSE$=SUB(VAR$(43,10),"~"," ") ! 234689 - Was CUSTOMER$
12080 SHIP_VIA_CODE$=SUB(VAR$(53,1),"~"," ")
12085 GOSUB SELECT_DIVISION
12090 GOSUB SELECT_WHSE
12095 GOSUB SELECT_CUSTOMER
12100 GOSUB SELECT_SHIP_VIA
12105 QRY_USED=0
12110  }
12190 REFRESH_FLG=1
12195 EXIT 
18000 SELECT_WHSE:
18010 IF LEN(STP(WHSE$))=0 THEN {
18015 WHS_DESC$="All"
18020  } ELSE {
18025 WHSE$=STR(NUM(WHSE$,ERR=*NEXT):"0000")
18030 CALL "UPDFM0;READBYKEY",FM0,0,"D",DIM(10),WHSE$,SEG4$,SEG5$,FM0$,FM0{ALL},FOUNDFM0
18035 IF FOUNDFM0 THEN {
18040 WHS_DESC$=FM0$(16,35)
18045  } ELSE {
18050 IF QRY_USED=1 THEN {
18055 WHS_DESC$=MSG("NOT_FOUND")
18060  } ELSE {
18065 MSGBOX MSG("NOT_FOUND")
18067 WHS_DESC$=""
18070 WHSE$=""
18075 WHSE.CTL'VALUE$=""
18080 NEXT_ID=WHSE.CTL
18085  }
18090  }
18095  }
18190 REFRESH_FLG=1
18195 RETURN 
18200 SELECT_CUSTOMER:
18210 IF LEN(STP(CUSTOMER$))=0 THEN {
18215 CUST_NAME$="All"
18220  } ELSE {
18225 CALL "ZZWLKU;parse_cust",CUSTOMER$,CUST_DIV$,CUST_CODE$
18230 CALL "UPDAR1;READBYKEY",Z[4],0,CUST_DIV$,CUST_CODE$,SEG3$,SEG4$,SEG5$,AR1$,AR1{ALL},FOUNDAR1
18235 IF FOUNDAR1 THEN {
18240 CUST_NAME$=AR1$(11,35)
18245  } ELSE {
18250 IF QRY_USED=1 THEN {
18255 CUST_NAME$=MSG("NOT_FOUND")
18260  } ELSE {
18265 MSGBOX MSG("NOT_FOUND")
18267 CUST_NAME$=""
18270 CUSTOMER$=""
18275 CUSTOMER.CTL'VALUE$=""
18280 NEXT_ID=CUSTOMER.CTL
18285  }
18290  }
18295  }
18327 CUSTOMER$=CUST_DIV$+CUST_CODE$
18390 REFRESH_FLG=1
18395 RETURN 
18400 SELECT_DIVISION:
18410 IF LEN(STP(DIVISION$))=0 THEN {
18415 DIV_NAME$="All"
18420  } ELSE {
18425 DIVISION$=STR(NUM(DIVISION$,ERR=*NEXT):"00")
18430 READ (ARH,KEY=DIVISION$,DOM=*NEXT)IOL=0330
18435 IF LEN(STP(ARH$))>0 AND DIVISION$=ARH$(1,2) THEN {
18440 DIV_NAME$=ARH$(3,30)
18445  } ELSE {
18450 IF QRY_USED=1 THEN {
18455 DIV_NAME$=MSG("NOT_FOUND")
18460  } ELSE {
18465 MSGBOX MSG("NOT_FOUND")
18467 DIV_NAME$=""
18470 DIVISION$=""
18475 DIVISION.CTL'VALUE$=""
18480 NEXT_ID=DIVISION.CTL
18485  }
18490  }
18495  }
18590 REFRESH_FLG=1
18595 RETURN 
18600 SELECT_SHIP_VIA:
18610 IF LEN(STP(SHIP_VIA_CODE$))=0 THEN {
18615 SHIP_VIA_DESC$="All"
18617 GOSUB SELECT_FLEX_FORMAT
18620  } ELSE {
18625 READ (FMP,KEY="S"+SHIP_VIA_CODE$,DOM=*NEXT)IOL=0340
18635 IF LEN(STP(FMP$))>0 AND SHIP_VIA_CODE$=FMP$(2,1) THEN {
18640 SHIP_VIA_DESC$=FMP$(3,15)
18642 GOSUB SELECT_FLEX_FORMAT
18645  } ELSE {
18650 IF QRY_USED=1 THEN {
18655 SHIP_VIA_DESC$=MSG("NOT_FOUND")
18657 GOSUB SELECT_FLEX_FORMAT
18660  } ELSE {
18665 MSGBOX MSG("NOT_FOUND")
18667 SHIP_VIA_DESC$=""
18670 SHIP_VIA_CODE$=""
18675 SHIP_VIA_CODE.CTL'VALUE$=""
18680 NEXT_ID=SHIP_VIA_CODE.CTL
18685  }
18690  }
18695  }
18790 REFRESH_FLG=1
18795 RETURN 
18800 SELECT_FLEX_FORMAT:
18810 REFRESH_FLG=1
18900 EN_GRID$="Y"
18905 EN_INPUT$="N"
18910 GOSUB ENABLE_GROUPS
18920 DIM DT0_SEG1$(2,"~"),DT0_SEG2$(20,"~"),DT0_SEG3$(10,"~"),DT0_SEG4$(5,"~")
18940 IF STP(DIVISION$)<>"" THEN DT0_SEG1$(1,LEN(DIVISION$))=DIVISION$
18942 IF STP(WHSE$)<>"" THEN DT0_SEG3$(1,LEN(WHSE$))=WHSE$ ! 234689  - Was SEG2$
18944 TMP_CUST$=CUST_DIV$+CUST_CODE$
18946 IF STP(TMP_CUST$)<>"" THEN DT0_SEG2$(1,LEN(TMP_CUST$))=TMP_CUST$ ! 234689 - Was SEG3$
18948 TMP_SEG4$=SHIP_VIA_CODE$+FLEX_CODE$
18950 IF STP(TMP_SEG4$)<>"" THEN DT0_SEG4$(1,LEN(TMP_SEG4$))=TMP_SEG4$
18960 PERFORM "UPDDT0;BUILD_GRID"
18995 RETURN 
21000 INIT:
21001 X0$="DTGOMA.P",X1$="Output Control Maintenance"
21002 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOSUB WRAPUP; GOTO *RETURN
21003 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=*NEXT)
21004 DIM Z[NUM(X3$(60,3))]
21005 DIM AP4[1],A[1],ARH[1]
21006 SEG2$="",SEG3$="",SEG4$="",SEG5$=""
21010 %NO_X=1
21015 GOSUB CLEAR_FIELDS
21040 CHANGE_FLG=0,_ENABLE_FLG=0,_KCNT=1,_FIRST_KEY=0,_FIRST_FIELD=0
21070 Z$="01X DT0...  03O FM0...  04O AR1...  05O DTR...  06O ARH...  07O FMP...  "
21080 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; IF Z0>0 THEN GOTO OPEN_ERR
21081 DT0=Z[1],FM0=Z[3],DTR=Z[5],ARH=Z[6],FMP=Z[7]
21082 TYPE_DESC$=%TYPE_DESC$; %TYPE_DESC$="",VTYPE$=%VTYPE$; %VTYPE$=""
21085 DTT$=%DTT$; %DTT$=""
21100 CALL "UPDDTT;GET_VAL_OUT_DROP",DTT$,VAL_OUT_DROP$
21110 CALL "UPDDTR;GET_VAL_OUT_REPT",DTR,DTT$,VAL_OUT_REPT$
21190 RETURN 
21200 OPEN_ERR:
21205 MSGBOX _MSG_FILOPNERR1$+QUO+_FILE_NAME$+QUO+SEP+_MSG_DIRECTORY$+LWD+SEP+_MSG_PREFIX$+PFX,MSG(ERR),"!"
21210 CMD_STR$="END"
21290 RETURN 
22000 MAIN_POST_DISPLAY:
22020 NEXT_ID=DIVISION.CTL
22090 RETURN 
22100 WRAPUP:
22110 %NO_X=0
22120 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
22150 REPLACEMENT_SCRN$="DTGOMA"
22190 RETURN 
22500 SELECT_EXIT:
22510 DIVISION$="",CUST_DIV$="",CUST_CODE$="",WHSE$="",SHIP_VIA_CODE$="",FLEX_FORMAT$=""
22570 CMD_STR$="END"
22590 RETURN 
22600 SELECT_CLEAR:
22605 GOSUB CLEAR_FIELDS
22630 PERFORM "UPDDT0;DELETE_GRID"
22640 EN_INPUT$="Y"; EN_GRID$="N"; GOSUB ENABLE_GROUPS
22650 NEXT_ID=DIVISION.CTL,REFRESH_FLG=1
22690 RETURN 
22800 CLEAR_FIELDS:
22810 DIVISION$="",DIV_NAME$=""
22815 WHSE$="",WHS_DESC$=""
22820 CUSTOMER$="",CUST_NAME$="",CUST_CODE$="",CUST_DIV$=""
22825 SHIP_VIA_CODE$="",SHIP_VIA_DESC$=""
22830 FLEX_CODE$="",FLEX_DESC$=""
22895 RETURN 
55000 ENABLE_GROUPS:
55020 IF EN_INPUT$="Y" THEN CALL "*wingrp;enable",INPUT_FIELDS.GRP$; EN_INPUT$=""
55030 IF EN_INPUT$="N" THEN CALL "*wingrp;disable",INPUT_FIELDS.GRP$; EN_INPUT$=""
55060 IF EN_GRID$="Y" THEN CALL "*wingrp;enable",GRID.GRP$; EN_GRID$=""
55070 IF EN_GRID$="N" THEN CALL "*wingrp;disable",GRID.GRP$; EN_GRID$=""
55290 RETURN 
56000 REM + Modification History
56002 REM "201588-Create XML output of orders to remote warehouse location    
56003 REM "234689-Create new Output Director report for the WHS_PACKING_LIST  
56004 REM "272144-Output control maintenance opens another window             
