0010 ! DTGTOD - OutPut Director central objects
0035 REM "5.7 - 12/27/18 - 16.103055 - crg - SSP# 305008
0037 REM "305008-Some KeyBank Orders not going to CTL warehouse.             
0040 REM "Copyright 2018 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! FILENO - CHANNEL OF OPEN FILE
0051 ! KEYNO  - KEY NUMBER
0052 ! SEG1-SEG5$ : KEY SEGMENTS . CAN BE NULL
0053 ! A$ - RECORD A$
0054 ! FOUND - IF RECORD FOUND, THEN 1 ELSE 0
0055 ! BUSY - IF RECORD BUSY THEN 1 ELSE 0
0056 ! OK - IF RECORD ADDED OK=1 ELSE 0
0310 IOLIST A$
0500 INIT:
0505 DIM Z[255]; OPENED=0
0507 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
0510 Z$="01S "+OPEN_FILE_NAME$+"...  "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; FILENO=Z[1],OPENED=1
0590 RETURN 
0598 ! 
0599 ! 
0600 WRAPUP:
0610 IF OPENED THEN CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0690 RETURN 
0698 ! 
0699 ! 
9999 END 
10000 GET_FILENAME:
10010 ENTER DTR,X3$,OUTPUT_TYPE$,REPORT_NAME$,TOD_FILE_NAME$,FILE_NAME$,XFER_TYPE$,FILE_TYPE$ ! 235764
10011 WAIT 1 ! 241296
10015 SUFFIX$=".txt"; IF FILE_TYPE$="P" THEN SUFFIX$=".pdf" ELSE IF FILE_TYPE$="X" THEN SUFFIX$=".xml" ELSE IF FILE_TYPE$="H" THEN SUFFIX$=".html" ELSE IF FILE_TYPE$="C" THEN SUFFIX$=".csv" ELSE IF FILE_TYPE$="L" THEN SUFFIX$=".xls" ELSE IF FILE_TYPE$="1" THEN SUFFIX$=".xlsx" ! 291420
10020 IF TOD_FILE_NAME$<>"" THEN FILE_NAME$=TOD_FILE_NAME$+"_"+DTE(0:"%Y%Mz%Dz")+STR(TIM*100:"0000")+MID(STR(RND),3)+SUFFIX$ ! 235764
10022 IF TOD_FILE_NAME$="" THEN FILE_NAME$=DTE(0:"%Y%Mz%Dz")+STR(TIM*100:"0000")+MID(STR(RND),3)+SUFFIX$ ! 235764
11000 IF TOD_FILE_NAME$="" THEN {! only add prefix if not image lib file
11025 CALL "UPDDTR;READBYKEY",DTR,0,OUTPUT_TYPE$,SEG2$,SEG3$,SEG4$,SEG5$,DTR$,DTR{ALL},FOUNDDTR
11030 CALL "UPDDTR;READNEXT",DTR,DTR$,DTR{ALL},FOUNDDTR; IF FOUNDDTR THEN IF DTR$(1,20)<>OUTPUT_TYPE$ THEN FOUNDDTR=0
11040 WHILE FOUNDDTR
11050 IF DTR$(24,20)=REPORT_NAME$ AND DTR$(44,10)<>DIM(10) THEN FILE_NAME$=STP(DTR$(44,10),2)+FILE_NAME$,FOUNDDTR=0
11070 IF FOUNDDTR THEN CALL "UPDDTR;READNEXT",DTR,DTR$,DTR{ALL},FOUNDDTR; IF FOUNDDTR THEN IF DTR$(1,20)<>OUTPUT_TYPE$ THEN FOUNDDTR=0
11080 WEND 
11085  } ! 11000
11087 MSG$="DTGTOD|FILETYPE|"+FILE_TYPE$+"|XFERTYPE|"+XFER_TYPE$+"|FILENAME|"+FILE_NAME$; MX=FN%TOD_LOG("INFO",MSG$,1,0)
11090 EXIT 
11098 ! 
11099 ! 
11500 CREATE_FILE:
11510 ENTER DT0,DT0$,FILE_NAME$,FULL_PATH$,DIR$,SERVER_NUM$
11515 DT0_KEY$=DT0$(1,60),XFER_TYPE$=DT0$(61,1)
11520 KEY1$=STP(DT0_KEY$(1,20)),KEY2$=SUB(DT0_KEY$(21,2)," ","~"),KEY3$=STP(DT0_KEY$(23,20),3," "),KEY4$=STP(DT0_KEY$(43,10),3," "),KEY5$=DT0_KEY$(58,3)
11522 KEY2$=SUB(KEY2$,"~","0"),KEY3$=SUB(KEY3$,"~","0"),KEY4$=SUB(KEY4$,"~","0")
11530 BASE$=CVS(PTH(DT0),2); XD=POS("DT0"=BASE$); BASE$=BASE$(1,XD-1)
11540 IF POS("\"=BASE$)<>0 THEN SEP$="\" ELSE SEP$="/"
11550 DIR$=BASE$+KEY1$+"/"+SERVER_NUM$+"/"; MX=FN%DIRECTORY(DIR$,ERR=*NEXT) ! 272901
11560 IF KEY2$<>"" THEN DIR$=DIR$+KEY2$
11565 IF KEY3$<>"" THEN DIR$=DIR$+"_"+KEY3$
11570 IF KEY4$<>"" THEN DIR$=DIR$+"_"+KEY4$
11575 IF KEY5$<>"" THEN DIR$=DIR$+"_"+KEY5$
11580 MX=FN%DIRECTORY(DIR$,ERR=*NEXT) ! 272901
11620 FULL_PATH$=DIR$+SEP$+FILE_NAME$
11650 IF DT0$(61,1)="1" AND DT0$(427,1)<>"P" THEN FULL_PATH$=STP(DT0$(62,60),2) ELSE IF DT0$(427,1)<>"P" THEN SERIAL FULL_PATH$ ! if printer or pdf file dont create file
11660 MSG$="DTGTOD|CREATE FILE|DIR|"+DIR$+"|FILENAME|"+FILE_NAME$; MX=FN%TOD_LOG("INFO",MSG$,1,0)
11690 EXIT 
11698 ! 
11699 ! 
11700 BUILD_DIRECTORY:
11710 ENTER DT0,DT0$,DIR$,SERVER_NUM$
11715 DT0_KEY$=DT0$(1,60),XFER_TYPE$=DT0$(61,1)
11720 KEY1$=STP(DT0_KEY$(1,20)),KEY2$=SUB(DT0_KEY$(21,2)," ","~"),KEY3$=STP(DT0_KEY$(23,20),3," "),KEY4$=STP(DT0_KEY$(43,10),3," "),KEY5$=DT0_KEY$(58,3)
11722 KEY2$=SUB(KEY2$,"~","0"),KEY3$=SUB(KEY3$,"~","0"),KEY4$=SUB(KEY4$,"~","0")
11730 BASE$=CVS(PTH(DT0),2); XD=POS("DT0"=BASE$); BASE$=BASE$(1,XD-1)
11740 IF POS("\"=BASE$)<>0 THEN SEP$="\" ELSE SEP$="/"
11750 DIR$=BASE$+KEY1$+SEP$+SERVER_NUM$+SEP$; MX=FN%DIRECTORY(DIR$,ERR=*NEXT) ! 272901
11760 IF KEY2$<>"" THEN DIR$=DIR$+KEY2$
11765 IF KEY3$<>"" THEN DIR$=DIR$+"_"+KEY3$
11770 IF KEY4$<>"" THEN DIR$=DIR$+"_"+KEY4$
11775 IF KEY5$<>"" THEN DIR$=DIR$+"_"+KEY5$
11780 MX=FN%DIRECTORY(DIR$,ERR=*NEXT) ! 272901
11781 MSG$="DTGTOD|CREATE DIR|DIR|"+DIR$; MX=FN%TOD_LOG("INFO",MSG$,1,0)
11790 EXIT 
11800 OPEN_FILE:
11810 ENTER FULL_PATH$,DT0$,FILE_CHAN,RPT_FILE$,OPEN_ERR
11820 FILE_CHAN=HFN,OPEN_ERR=1
11840 RPT_FILE$=FULL_PATH$
11850 IF DT0$(427,1)="P" AND STP(DT0$(1,20))<>"PURCHASE_ORDER" THEN RPT_FILE$="*PDF*;FILE="+FULL_PATH$
11852 IF DT0$(427,1)="P" AND STP(DT0$(1,20))="PURCHASE_ORDER" THEN RPT_FILE$="*PDF*;ORIENTATION=LANDSCAPE; FILE="+FULL_PATH$
11860 IF DT0$(427,1)="P" THEN OPEN (FILE_CHAN,ERR=*NEXT)RPT_FILE$; OPEN_ERR=0 ELSE IF DT0$(61,1)="1" THEN OPEN (FILE_CHAN,ERR=*NEXT)RPT_FILE$; OPEN_ERR=0 ELSE OPEN PURGE (FILE_CHAN,ERR=*NEXT)FULL_PATH$; OPEN_ERR=0
11870 MSG$="DTGTOD|OPEN FILE|FULL_PATH|"+FULL_PATH$; MX=FN%TOD_LOG("INFO",MSG$,1,0)
11872 MSG$="DTGTOD|OPEN FILE|RPT_FILE|"+RPT_FILE$; MX=FN%TOD_LOG("INFO",MSG$,1,0)
11890 EXIT 
12000 OUTPUT_FILES:
12010 ENTER X3$,DT0$,DIRECTORY$
12020 XFER_TYPE$=DT0$(61,1)
12030 MSG$="DTGTOD|OUTPUT FILES|DIR|"+DIRECTORY$+"|XFERTYPE|"+XFER_TYPE$; MX=FN%TOD_LOG("INFO",MSG$,1,0)
12050 SWITCH XFER_TYPE$
12100 CASE "1" ! printer
12150 IF DT0$(427,1)="P" THEN CALL "ZZ2PRT;SEND_FULL_DIR",ERR=*NEXT,X3$,X4$,DT0$,DIRECTORY$
12190 BREAK
12200 CASE "2" ! email
12205 ZZPARM=HFN; OPEN (ZZPARM)"ZZPARM"
12207 SMTP_SERVER$=""; READ (ZZPARM,KEY=X3$(9,3)+"EML",DOM=*NEXT)EML_P$; SMTP_SERVER$=MID(EML_P$,7,60); SMTP_SERVER$=STP(SMTP_SERVER$,3," ")
12208 READ (ZZPARM,KEY="CMP"+%C$,ERR=*NEXT)CC$; EML_FROM$=STP(CC$(7,40),3," ")
12209 CLOSE (ZZPARM)
12210 EML_TO$=DT0$(62,60),SEND_FROM_DIR$=DT0$(157,70),EML_SUBJ$=DT0$(297,50)
12212 IF STP(EML_SUBJ$,2)="" THEN EML_SUBJ$="(NONE)"
12215 EML_FROM$=DT0$(347,60)
12280 CALL "ZZ2EML;SEND_FULL_DIR",ERR=*NEXT,SMTP_SERVER$,EML_TO$,DIRECTORY$,EML_SUBJ$,EML_FROM$
12290 BREAK
12300 CASE "3" ! ftp
12310 LOGIN$=DT0$(122,15),PASSWORD$=DT0$(137,20)
12315 FTP_TO_DIR$=DT0$(227,70),SERVER$=DT0$(62,60)
12380 CALL "ZZ2FTP;SEND_FULL_DIR",ERR=*NEXT,SERVER$,LOGIN$,PASSWORD$,FTP_TO_DIR$,DIRECTORY$
12390 BREAK
12400 CASE "4" ! http
12410 URL$=STP(DT0$(62,60),2),VAL_RESP$=STP(DT0$(428,40),2)
12480 CALL "ZZ2SND;SEND_FULL_DIR",ERR=*NEXT,X3$,X4$,URL$,DIRECTORY$,"H",VAL_RESP$
12490 BREAK
12500 CASE "5" ! https
12510 URL$=STP(DT0$(62,60),2),VAL_RESP$=STP(DT0$(428,40),2)
12580 CALL "ZZ2SND;SEND_FULL_DIR",ERR=*NEXT,X3$,X4$,URL$,DIRECTORY$,"S",VAL_RESP$
12590 BREAK
12600 CASE "6" ! fax
12610 CALL "ZZ2FAX;SEND_FULL_DIR",ERR=*NEXT,X3$,X4$,DT0$,DIRECTORY$
12690 BREAK
12700 CASE "7" ! webec display
12790 BREAK
12800 CASE "8" ! hold for attachments
12810 CALL "ZZ2HLD;SEND_FULL_DIR",ERR=*NEXT,X3$,X4$,DT0$,DIRECTORY$ ! 234689
12890 BREAK
12900 CASE "9" ! image lib/archive
12910 OUTPUT_TYPE$=DT0$(1,20),OUTPUT_LOCATION$=STP(DT0$(62,60))
12980 CALL "ZZ2ILG;SEND_FULL_DIR",ERR=*NEXT,OUTPUT_TYPE$,DIRECTORY$,OUTPUT_LOCATION$
12990 BREAK
13900 END SWITCH ! 12100
13990 EXIT 
13998 ! 
13999 ! 
20000 SET_Y:
20010 ENTER FILENO,UPD$,FIRST_REC$,Y_POS
20015 FIRST$=PAD(FIRST_REC$,30)
20020 CALL UPD$+";READBYKEY",FILENO,0,FIRST$,SEG2$,SEG3$,SEG4$,SEG5$,R$,R{ALL},FOUND
20030 CALL UPD$+";READNEXT",FILENO,R$,R{ALL},FOUND; IF FOUND THEN IF R$(1,30)<>FIRST$ THEN FOUND=0
20040 WHILE FOUND
20050 R$(Y_POS,1)="Y"; CALL UPD$+";UPDATE",FILENO,R$,R{ALL},BUSY
20060 CALL UPD$+";READNEXT",FILENO,R$,R{ALL},FOUND; IF FOUND THEN IF R$(1,30)<>FIRST$ THEN FOUND=0
20070 WEND 
20090 EXIT 
20098 ! 
20099 ! 
20100 DELETE_Y:
20110 ENTER FILENO,UPD$,FIRST_REC$,KEYNUM
20120 CALL UPD$+";READBYKEY",FILENO,KEYNUM,"Y",SEG2$,SEG3$,SEG4$,SEG5$,R$,R{ALL},FOUND
20130 KY$=KEY(FILENO,END=*NEXT); IF KY$(1,1)="Y" THEN CALL UPD$+";READNEXT",FILENO,R$,R{ALL},FOUND; CALL UPD$+";DELETE",FILENO,R$,R{ALL},FOUND,BUSY; GOTO *SAME
20190 EXIT 
20198 ! 
20199 ! 
56000 REM "198612-TopForm Output director
56002 REM "203516-Duplicate file name for 806 xml
56003 REM "230554-Output Director fails every other day
56004 REM "235764-Pier1 outgoing XML file naming convention broken.           
56005 REM "234689-Create new Output Director report for the WHS_PACKING_LIST  
56006 REM "241296-Pier 1 complained that not all 806 xml files were received  
56007 REM "261434-DBRS Development - Next Phase - Ability to schedule BIRT rpt
56008 REM "272901-See 272617/074; error 12 DTGTOD 11650 first time Output Dir 
56009 REM "305008-Some KeyBank Orders not going to CTL warehouse.             
