0010 ! DTGRPK - Output Director Object for RPK
0030 ! 
0035 REM "5.7 - 02/25/16 - 10.398888 - crg - SSP# 279707
0037 REM "279707-DBRS - Single sign-on/authentication mechanism for DB/d user
0040 REM "Copyright 2016 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0100 DEF CLASS "DTGRPK" CREATE REQUIRED DELETE REQUIRED 
0110 LIKE "FILE"
0115 LIKE "DTGYES"
0120 LIKE "DTGDT0"
0200 ! start variable list
0210 LOCAL RPK,DT6
0215 LOCAL TYPE$=PAD("SCHEDULED_DBRS_RPT",20)
0220 PROPERTY RPK$,DT6$,CURRENT_QUEUE$,IL_DT1$,IL_DT2$
0999 ! 
1000 FUNCTION GETFIRSTREC(TRANS_NUM$)
1005 ENTER QUEUE_NUM$
1020 FILENO=DT6
1030 X=_OBJ'READBYKEY(0,QUEUE_NUM$,"")
1040 ! X=_OBJ'READNEXT()
1045 IF X THEN IF OUTPUT_TYPE$<>TYPE$ OR OUTPUT_QUEUE$<>QUEUE_NUM$ THEN X=0
1050 IF X THEN {
1053 IOLIST DT6$
1060 READ DATA FROM REC(IOL(FILENO)) TO IOL=1053
1070 CURRENT_QUEUE$=DT6$
1080  }
1090 RETURN X
1099 ! 
1100 FUNCTION FINDDT0MATCH()
1120 SEG1$=MID(DT6$,31,2)
1125 SEG2$=MID(DT6$,33,20)
1130 SEG3$=PAD(MID(DT6$,53,9),10)
1135 SEG4$=DIM(5)
1140 SEG5$="",SEG6$="",SEG7$="",SEG8$=""
1150 OK=_OBJ'GETDT0KEY(TYPE$,SEG1$,SEG2$,SEG3$,SEG4$,SEG5$,SEG6$,SEG7$,SEG8$)
1190 RETURN OK
1199 ! 
1200 FUNCTION SETYES()
1290 RETURN 1
1299 ! 
1300 FUNCTION DELYES()
1390 RETURN 1
1399 ! 
1400 FUNCTION CHECK(X$)
1490 RETURN 1
1499 ! 
1500 FUNCTION PRT_RPT(X)
1510 ENTER PRINTER_CHAN
1515 REPORT$=MID(CURRENT_QUEUE$,31,25)
1520 X=_OBJ'PRINT_REPORT(PRINTER_CHAN,REPORT$)
1590 RETURN 
1599 ! 
1600 FUNCTION GETNEXTDT0SEQ()
1605 FILENO=DT0
1620 X=_OBJ'READNEXT(); IF X THEN IF OUTPUT_TYPE$<>TYPE$ OR STP(DIVISION$+KEY1$+KEY2$+KEY3$,1)<>MID(SUB(DT0_KEY$,"~"," "),1,LEN(STP(DIVISION$+KEY1$+KEY2$+KEY3$,1))) THEN X=0
1630 IOLIST DT0$
1640 READ DATA FROM REC(IOL(FILENO)) TO IOL=1630
1690 RETURN X
1699 ! 
1800 FUNCTION PRINT_REPORT(X,X$)
1810 ENTER PRINTER_CHAN,REPORT_ID$
1815 ! 
1850 TMPPTH$=OPT(PRINTER_CHAN); IF NUL(TMPPTH$) THEN TMPPTH$=PTH(PRINTER_CHAN) END_IF ; P=POS("FILE="=TMPPTH$); IF P>0 THEN TMPPTH$=TMPPTH$(P+5); P=0; P=POS(".pdf"=TMPPTH$); IF P>0 THEN TMPPTH$=TMPPTH$(1,P+3) ! Get the file reserved for this job
1860 TODMSG$=_OBJ'_CLASS$+"|EXECUTE|Using file basename|"+TMPPTH$; MX=FN%TOD_LOG("INFO",TODMSG$,1,0)
1880 CLOSE (PRINTER_CHAN)
1899 ! 
1904 IOL_RPK_S:IOLIST RPK_KEY_S$,RPT_DESC$,RPT_IS_STALE$,SCHD_EXP1$,SCHD_EXP2$,SCHD_EXP3$,SCHD_EXP4$,SCHD_EXP5$ ! Scheduling Options
1906 IOL_RPK_P:IOLIST RPK_KEY_P$,RPT_PARAM_VAR$,RPT_PARAM_DESC$,RPT_PARAM_TYPE$,RPT_PARAM_VAL$,RPT_PARAM_DEFVAL$,RPT_PARAM_ACCTMASK$,RPT_PARAM_IS_LOCKED$,RPT_PARAM_IS_REQD$,RPT_PARAM_STEP$,RPT_PARAM_INCR$ ! Parameters data
1915 TODMSG$=_OBJ'_CLASS$+"|EXECUTE|Opening report|"+REPORT_ID$+"|"; MX=FN%TOD_LOG("INFO",TODMSG$,1,0)
1919 ! 
1920 READ (RPK,KEY=REPORT_ID$+"S  ")IOL=IOL_RPK_S
1925 DIM RPP$[999,1]; PARAM_CNT=0
1930 READ (RPK,KEY=REPORT_ID$,DOM=*NEXT)
1935 NEXT_PARAM:
1940 NPKEY$=KEY(RPK,END=NP_DONE); READ (RPK,KEY=NPKEY$)IOL=IOL_RPK_P
1945 IF MID(RPK_KEY_P$,1,LEN(REPORT_ID$))<>REPORT_ID$ OR MID(RPK_KEY_P$,LEN(REPORT_ID$)+1,3)="S  " THEN GOTO NP_DONE
1950 PARAM_CNT++; RPP$[PARAM_CNT,0]=RPT_PARAM_VAR$; RPP$[PARAM_CNT,1]=RPT_PARAM_VAL$
1955 TODMSG$=_OBJ'_CLASS$+"|EXECUTE|Report parameter|"+RPT_PARAM_VAR$+"|Value|"+RPT_PARAM_VAL$+"|"; MX=FN%TOD_LOG("DEBUG",TODMSG$,1,0)
1970 GOSUB UPDATE_DATA_FIELD
1990 GOTO NEXT_PARAM
1995 NP_DONE:
1996 PARAM_CNT++; RPP$[PARAM_CNT,0]="__format"; RPP$[PARAM_CNT,1]=MID(TMPPTH$,POS("."=TMPPTH$,-1)+1)
1999 ! 
2000 TODMSG$=_OBJ'_CLASS$+"|EXECUTE|Printing report"; MX=FN%TOD_LOG("INFO",TODMSG$,1,0)
2010 CALL "RPGVWM;RUN_REPORT",MID(REPORT_ID$,1,10),RPP${ALL},PARAM_CNT,TMPPTH$
2050 IF NOT(NUL(RETURN$)) THEN TODMSG$=_OBJ'_CLASS$+"|EXECUTE|Error during report execution|"+RETURN$+"|"+STR(RETURN_CODE)+"|"; MX=FN%TOD_LOG("WARN",TODMSG$,1,0)
2200 TODMSG$=_OBJ'_CLASS$+"|EXECUTE|Closing report"; MX=FN%TOD_LOG("INFO",TODMSG$,1,0)
2400 RETURN 
8999 END DEF
9999 END 
10000 ON_CREATE:
10005 OPEN_STATUS=0
10007 FILENAME$="RPK"
10010 COMPANY$=%C$
10051 OPEN (HFN,ERR=*RETURN)FILENAME$+COMPANY$
10060 RPK=LFO
10061 FILENAME$="DT6"
10063 OPEN (HFN,IOL=*,ERR=*RETURN)FILENAME$+COMPANY$
10065 DT6=LFO
10070 STATIC IOL=IOL(DT6)
10080 OPEN_STATUS=1
10090 RETURN 
10099 ! 
10100 ON_DELETE:
10110 IF OPEN_STATUS THEN CLOSE (RPK),(DT6)
10190 RETURN 
10199 ! 
15000 UPDATE_DATA_FIELD:
15010 UPDATE_FIELD$=RPT_PARAM_VAL$
15020 IF RPT_PARAM_STEP$="Use Initial Value" OR STP(RPT_PARAM_STEP$,2)="" THEN GOTO SET_VALUE ELSE GOTO *NEXT
15100 YYMM_STEP:
15110 PRD_INCR=0,PRD_INCR=NUM(STP(RPT_PARAM_INCR$,3,*$0D0A$)) ! 300353 - strip newline or carr returns
15115 ! FTYPE$="",FTYPE$=RPT_OPTIONS_DOMAIN$; IF POS(FTYPE$="ARAP")<>0 THEN GOSUB OTHER_FISCAL_YEAR
15120 ON POS(RPT_PARAM_TYPE$(2,1)="246") GOTO 15590,15130,15200,15250
15130 ! Set Current Period
15135 CR_PD$=DTE(0:"%Mz")
15140 CURR_PRD=NUM(CR_PD$),CURR_IPRD=CURR_PRD+PRD_INCR,CURR_PRD=CURR_IPRD
15150 IF CURR_IPRD>NUM_PERIODS THEN CURR_PRD=CURR_IPRD-NUM_PERIODS ELSE IF CURR_IPRD=0 THEN CURR_PRD=NUM_PERIODS ELSE IF CURR_IPRD<0 THEN CURR_PRD=CURR_IPRD+NUM_PERIODS
15180 UPDATE_FIELD$=STR(CURR_PRD:"00")
15190 GOTO SET_VALUE
15200 ! Set Current Year
15205 CR_YEAR$=DTE(0:"%Ys")
15210 CURR_YR=NUM(CR_YEAR$),CURR_YR=CURR_YR+PRD_INCR
15220 UPDATE_FIELD$=STR(CURR_YR:"0000")
15240 GOTO SET_VALUE
15250 ! Set Current Date
15252 IF FTYPE$="" THEN GOTO *NEXT ELSE UPDATE_FIELD$=RPT_AGEDATE$; GOTO SET_VALUE
15255 DDY$=DTE(0:"%Ys%Mz%Dz")
15270 Q0$=CHR(NUM(DDY$(1,3),ERR=SET_VALUE)-125)+DDY$(4,1)+DDY$(5,2)+DDY$(7,2)
15285 CALL "ZZDATE",%X3$,"",Q0$,REQUIRED_DATE$,PRD_INCR,0,0,0,0,0,0,"","",""
15290 UPDATE_FIELD$=FN%FMT_TFDATE$(REQUIRED_DATE$,"YYYY-MM-DD")
15300 SET_VALUE:
15360 RPP$[PARAM_CNT,1]=UPDATE_FIELD$
15590 RETURN 
15599 ! 
