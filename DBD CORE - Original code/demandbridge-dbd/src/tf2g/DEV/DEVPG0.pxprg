0010 ! DEVPG0 - Topform Program History
0035 REM "5.5 - 10/18/07 - 12.393611 - jdf - SSP# 211380
0037 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
0040 REM "Copyright 2007 Demandbridge Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0070 IF TCB(88)=0 THEN MSGBOX "You must be using Windx to access this program" ELSE PROCESS "HISLST","../DEV/DEV.EN"
0080 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0090 GOTO 9900
0100 ! Initialization
0110 INIT:
0120 GOSUB CLEAR_LIST
0130 ARCH_DIR$="/usr/lib/pvx/rel/archive"
0190 RETURN 
0500 ! Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 REFRESH_FLG=1
0570 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0970 RETURN 
0999 ! 
1000 REM "Clear list
1005 CLEAR_LIST:
1010 LIST_BOX LOAD HIST_LIST.CTL,""; IDX=0
1020 FIRSTVAL$="",SECONDVAL$="",PGC_OK=0
1030 BUTTON HIDE SHOW_PGC.CTL
1090 REFRESH_FLG=1
1095 RETURN 
1099 ! 
1100 REM "Display list for program/call #
1110 SHOW_LIST:
1120 GOSUB CLEAR_LIST
1125 GOSUB GET_CMD; IF NUL(UNIX_CMD$) THEN GOTO 1195
1130 CHAN=HFN; OPEN (CHAN,ERR=1190)UNIX_CMD$
1135 READ (CHAN,END=1190,BSY=*SAME)ROWVAL$
1136 ROWVAL$=SUB(ROWVAL$,"     "," - ")
1137 ROWVAL$=SUB(ROWVAL$," ",SEP)
1140 IF POS(SEP+SEP=ROWVAL$)>0 THEN ROWVAL$=SUB(ROWVAL$,SEP+SEP,SEP); GOTO *SAME
1155 IDX+=1; LIST_BOX LOAD HIST_LIST.CTL,IDX,ROWVAL$
1180 GOTO 1135
1190 CLOSE (CHAN)
1195 REFRESH_FLG=1
1199 RETURN 
1200 ! 
1205 GET_CMD:
1210 IF NUL(STP(TF_REF$,3)) THEN UNIX_CMD$=""; GOTO *RETURN
1230 UNIX_CMD$="< (cat "+ARCH_DIR$+"/tflog.pvx | grep "+TF_REF$+")"
1295 RETURN 
1299 ! 
1300 REM "Get selection
1305 GET_USER_SELECTION:
1310 LIST_BOX READ HIST_LIST.CTL,SELVAL$
1320 LIST_BOX READ HIST_LIST.CTL,SELVAL
1322 LIST_BOX LOAD HIST_LIST.CTL,SELVAL,*
1325 LIST_BOX LOAD HIST_LIST.CTL,SELVAL,'SB'+'_GREEN'+SUB(SELVAL$,SEP,SEP+'SB'+'_GREEN')
1330 IF SECONDVAL$<>"" THEN FIRSTVAL$=SECONDVAL$ END_IF ; BEGINPOS=POS(SEP=SELVAL$,1,3)+1,ENDPOS=POS(SEP=SELVAL$,1,4); SECONDVAL$=STP(SELVAL$(BEGINPOS,ENDPOS-BEGINPOS),2)
1340 ! MSGBOX "First: "+SECONDVAL$+$0A$+"Second: "+FIRSTVAL$+$0A$+"Selected:"+SELVAL$
1350 IF FIRSTVAL$<>"" OR SECONDVAL$<>"" THEN PGC_OK=1; BUTTON SHOW SHOW_PGC.CTL ELSE PGC_OK=0; BUTTON HIDE SHOW_PGC.CTL
1395 RETURN 
1399 ! 
1400 REM "Show program compare
1410 SHOW_PGC:
1415 IF PGC_OK<>1 THEN GOTO *RETURN
1420 PROG1$=ARCH_DIR$+"/"+FIRSTVAL$,PROG2$=ARCH_DIR$+"/"+SECONDVAL$
1422 IF NUL(FIRSTVAL$) THEN PROG1$=PROG2$; PROG2$=MID(SECONDVAL$,MSK(SECONDVAL$,"[^\.]*.",ERR=*NEXT),MSL-1,ERR=1450)
1430 CALL PGN+";PGC",ERR=SHOW_PGC_ERR,PROG1$,PROG2$
1440 GOTO 1495
1450 SHOW_PGC_ERR:
1460 MSGBOX MSG(ERR)
1495 RETURN 
1510 ! Compare two source files
1520 PGC:
1530 ENTER (PROG1$),(PROG2$)
1540 CALL PGN+";LST2TXT",PROG1$,FILE1$
1550 CALL PGN+";LST2TXT",PROG2$,FILE2$
1560 INVOKE "[WDX]"+QUO+ENV("TF_CMP_PGN")+QUO+" "+SUB(FILE1$,"[wdx]","")+" "+SUB(FILE2$,"[wdx]","")
1570 EXIT 
2010 ! List code from an external program to a text file
2020 LST2TXT:
2030 ENTER (PROG$),FILE$
2035 IF NUL(FILE$) THEN FILE$="[wdx]c:\temp\"+SUB(PROG$,"/","_"); ERASE FILE$,ERR=*NEXT
2040 CHANNEL=HFN; IF CHANNEL=-1 THEN EXIT 14 ! No free channels
2050 TF_TYPE$="?"; OPEN INPUT (CHANNEL,ERR=BADPGM)PROG$
2060 TF_TYPE$=MID(FIB(CHANNEL),19,1)
2070 BADPGM:CLOSE (CHANNEL); IF TF_TYPE$<>"P" THEN EXIT 12 ! Not a pgm
2080 SERIAL FILE$,ERR=BADFILE; GOTO LISTPGM
2090 BADFILE:EXIT 12
2100 LISTPGM:PRINT 'WINDOW'(0,-8,80,5),'CI',
2110 PRM_XT=PRM('XT'),PRM_NE=PRM('NE'),PRM_LE=PRM('LE'),OKAY=0
2120 SET_PARAM -'XT','NE' ! No exit in console mode, error in subprogram
2130 EXEC$="LOAD "+QUO+PROG$+QUO+" ; " ! Load PGM to de-list
2135 EXEC$+="OPEN LOCK ("+STR(CHANNEL)+")"+QUO+FILE$+QUO+" ; "
2140 EXEC$+="LIST("+STR(CHANNEL)+"); OKAY=1 ; " ! LIST line
2145 EXEC$+="CLOSE("+STR(CHANNEL)+") "
2150 PREIN$="LOAD "+QUO+PGN+QUO ! Load the current pgm
2160 ! The following commands MUST be on the same line !!!
2170 GO$="GOTO "+STR(1+TCB(4)); PREINPUT PREIN$; PREINPUT GO$; PREINPUT "RUN"; EXECUTE EXEC$+";"+PREIN$+";"+GO$,ERR=*ESCAPE
2180 SET_PARAM 'XT'=PRM_XT,'NE'=PRM_NE; PRINT 'CI','POP',
2190 IF OKAY=0 THEN EXIT 18 ! Protected or empty program file
2200 ! IF POS(line$=code$)<>1 THEN LET code$="" ! Only return SAME line
2210 EXIT 
9999 END 
56000 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
