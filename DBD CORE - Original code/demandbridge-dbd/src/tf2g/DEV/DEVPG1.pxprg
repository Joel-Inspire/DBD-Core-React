0001 BEGIN 
0011 SETERR 0021; ENTER CALLNUM$; GOTO 0041
0021 INPUT "Enter call number:",CALLNUM$
0025 ERASE "[WDX]C:\temp\"+CALLNUM$+".TXT",ERR=*NEXT
0031 SETERR 9105
0041 IOL_ZWL:IOLIST ZWL$
0051 OPEN (1,ERR=9100)"ZWL"
0061 READ (1,KEY="",DOM=*NEXT)
0071 NEXT_ZWL:
0081 COUNT=0
0091 READ (1,END=DONE_ZWL)IOL=IOL_ZWL
0101 IF STP(ZWL$(12,6))<>CALLNUM$ THEN GOTO NEXT_ZWL
0111 ! 
0121 INFO$="Program compare utility"
0131 WK.LFN=HFN,PR.LFN=0
0141 WRK.1$="",WRK.2$=""
0151 X$=FIB(0),SV_MODE$=X$(21,3); PRINT 'CS' ! 'RM','_BLACK','WHITE','SN','DF','CS',
0161 SV_SD=PRM('SD'); SET_PARAM -'SD'
0171 ! 100 - First program definition
0181 ! IF LAST.1$<>"" THEN RESP$=LAST.1$
0191 QUEST$="Original program file/directory:"; GOSUB 5500
0201 IF RESP$="" THEN GOTO 9000 ELSE LAST.1$=RESP$
0211 OPEN INPUT (WK.LFN,ERR=0251)RESP$; A$=FIB(WK.LFN),PTH.1$=PTH(WK.LFN)
0221 CLOSE (WK.LFN); IF A$(19,1)="D" THEN GOTO 0500 ELSE IF A$(19,1)<>"P" THEN GOTO 0261
0231 OPEN INPUT (WK.LFN,ISZ=256,ERR=0251)RESP$; READ RECORD (WK.LFN)X$; CLOSE (WK.LFN); IF LEN(X$)<>256 THEN GOTO 0261
0241 INFO$="Original program file:"+PTH.1$; GOTO 0300
0251 INFO$=MSG(RET); PRINT 'RB',; GOTO 0171
0261 INFO$="'"+RESP$+"' is not a program file"; PRINT 'RB',; GOTO 0171
0271 INFO$="'"+RESP$+"' is password protected"; PRINT 'RB',; GOTO 0171
0300 ! 300 - Second program definition
0310 RESP$=PTH.1$
0320 QUEST$="Revised program file:"; GOSUB 5500
0330 IF RESP$="" THEN GOTO 0071 ELSE LAST.2$=RESP$
0340 IF LEN(RESP$)=2 THEN IF LEN(PTH.1$)>2 AND RESP$(2,1)=":" AND UCS(RESP$(1,1))>="A" AND UCS(RESP$(1,1))<="Z" THEN RESP$=RESP$(1,1)+PTH.1$(2)
0350 OPEN INPUT (WK.LFN,ERR=0390)RESP$; A$=FIB(WK.LFN),PTH.2$=PTH(WK.LFN)
0360 CLOSE (WK.LFN); IF A$(19,1)<>"P" THEN GOTO 0400
0370 OPEN INPUT (WK.LFN,ISZ=256,ERR=0390)RESP$; READ RECORD (WK.LFN)X$; CLOSE (WK.LFN); IF LEN(X$)<>256 THEN GOTO 0400
0380 FL.TYP$="P"; GOTO 0620
0390 INFO$=MSG(RET); PRINT 'RB',; GOTO 0300
0400 INFO$="'"+RESP$+"' is not a program file"; PRINT 'RB',; GOTO 0300
0410 INFO$="'"+RESP$+"' is password protected"; PRINT 'RB',; GOTO 0300
0500 ! 500 - Second program definition
0510 IF LAST.2$<>"" THEN RESP$=LAST.2$
0520 INFO$="Directory containing original programs:"+PTH.1$
0530 QUEST$="Revised program Directory:"; GOSUB 5500
0540 IF LEN(RESP$)=2 THEN IF LEN(PTH.1$)>2 AND RESP$(2,1)=":" AND UCS(RESP$(1,1))>="A" AND UCS(RESP$(1,1))<="Z" THEN RESP$=RESP$(1,1)+PTH.1$(2)
0550 IF RESP$="" THEN GOTO 0071 ELSE LAST.2$=RESP$
0560 OPEN INPUT (WK.LFN,ERR=0590)RESP$; A$=FIB(WK.LFN),PTH.2$=PTH(WK.LFN)
0570 CLOSE (WK.LFN); IF A$(19,1)<>"D" THEN GOTO 0600
0580 FL.TYP$="D"; GOTO 0610
0590 INFO$=MSG(RET); PRINT 'RB',; GOTO 0500
0600 INFO$="'"+RESP$+"' is not a directory"; PRINT 'RB',; GOTO 0500
0610 GOSUB GET_FILES; IF PG.MSK$="" THEN GOTO 0500
0620 ! 600 - Changes only flag
0630 INFO$="Print only the lines which have been changed"
0640 OPTS$="Yes No"
0650 ! GOSUB 5000
0660 CHG.FLG$="Y"
0670 ! 
0680 INFO$="Output results to"
0690 OPTS$="Screen Printer"
0700 ! GOSUB 5000
0710 PRT_OPT$="P"; IF PRT_OPT$="Q" THEN GOTO 9000
0720 ! 
0730 PR.LFT=0,PR.PAGE=0
0740 PR.TTL$="Program comparision for "+WHO
0750 IF PRT_OPT$="S" THEN PR.LFN=HFN; OPEN (PR.LFN)FID(0); GOTO 0790
0760 PR.LFN=HFN; __XX$="[WDX]C:\temp\"+CALLNUM$+".TXT" ! ERASE __XX$,ERR=*NEXT ! CALL "*pr.opn",PR.LFN,20
0770 SERIAL __XX$,ERR=*PROCEED; OPEN LOCK (PR.LFN)__XX$
0780 ON CTL GOTO 0790,0620,0760,0760,9000
0790 MX_COL=MXC(PR.LFN); IF MX_COL<1 THEN MX_COL=79 ! 
0800 MX_LINE=MXL(PR.LFN)-5; IF MX_LINE<0 AND PRT_OPT$<>"S" THEN MX_LINE=55 ! Allow for hdr (-5)
0810 MX_MID=MAX(31,INT((MX_COL-6)/2)-1) ! 
0820 ! 800 - Compare
0830 TTL.1$=PTH.1$,TTL.2$=PTH.2$
0840 IF FL.TYP$="P" THEN GOSUB 1110; GOTO 1090
0850 ! 
0860 IF FL_LST=0 THEN GOTO 0920
0870 ! 
0880 F$=KEY(FL_LST,END=*NEXT); READ (FL_LST); F$=F$(1,LEN(F$)-1); GOTO 0940
0890 X$=PTH(FL_LST); CLOSE (FL_LST); IF POS("*memory*"=LCS(X$))=0 THEN ERASE X$,ERR=*NEXT
0900 GOTO 1090
0910 ! 
0920 X=POS(SEP=FL.LST$); IF X=0 THEN GOTO 1090
0930 F$=FL.LST$(1,X-2),FL.LST$=FL.LST$(X+1)
0940 X=HFN; OPEN INPUT (X,ERR=0860)TTL.1$+DLM+F$; X$=FIB(X); PTH.1$=PTH(X); CLOSE (X)
0950 IF X$(19,1)<>"P" THEN GOTO 0860
0960 OPEN INPUT (X,ISZ=256,ERR=0860)TTL.1$+DLM+F$; READ RECORD (X)X$; CLOSE (X); IF LEN(X$)<>256 THEN GOTO 0860 ELSE IF X$(1,1)+X$(5,4)<>"[prg]" THEN GOTO 0860
0970 PTH.1X$=X$
0980 COMP$="",TTL.3$=""; IF FID(0)=FID(PR.LFN) THEN PR.LFT=0,PR.PAGE=0
0990 X=HFN; OPEN INPUT (X,ERR=1040)TTL.2$+DLM+F$; X$=FIB(X); PTH.2$=PTH(X); CLOSE (X)
1000 IF X$(19,1)<>"P" THEN GOTO 0860
1010 OPEN INPUT (X,ISZ=256,ERR=0860)TTL.2$+DLM+F$; READ RECORD (X)X$; CLOSE (X); IF LEN(X$)<>256 THEN GOTO 0860 ELSE IF X$(1,1)+X$(5,4)<>"[prg]" THEN GOTO 0860
1020 IF PTH.1X$=X$ THEN GOTO 0860
1030 GOSUB 1110; GOTO 0860
1040 IF PR.LFT<1 THEN GOSUB 4500; GOTO 1060
1050 PRINT (PR.LFN)DIM(MX_COL,"-"); PR.LFT=PR.LFT-1
1060 PRINT (PR.LFN)"Cannot find:",TTL.2$+DLM+F$; PR.LFT=PR.LFT-1
1070 IF FID(0)=FID(PR.LFN) THEN INFO$="Finished comparing these two programs.  Continue?",OPTS$="Continue"; GOSUB 5000; IF OPT$="Q" THEN GOTO 9000
1080 GOTO 0860
1090 CALL "*pr.cls",PR.LFN; PR.LFN=0
1100 GOTO 9000
1110 ! 1000 - Convert the files
1120 PRINT @(0,2),'CE','SB','CYAN',"Comparing:",@(0,4),"Original:",@(16,4),PTH.1$,@(0,6),"Revised:",@(16,6),PTH.2$,'RM',
1130 TITLE1$=PTH.1$; IF LEN(TITLE1$)>MX_MID THEN TITLE1$=TITLE1$(1,2)+"~~"+TITLE1$(LEN(TITLE1$)-MX_MID+5)
1140 TITLE2$=PTH.2$; IF LEN(TITLE2$)>MX_MID THEN TITLE2$=TITLE2$(1,2)+"~~"+TITLE2$(LEN(TITLE2$)-MX_MID+5)
1150 ERR_MSG$=""
1160 COMP$=PAD(TITLE1$,MX_MID,2)+" Line#  "+PAD(TITLE2$,MX_MID,2),TTL.3$=COMP$
1170 CALL "*pg.cnv",ERR=1180,PTH.1$,WRK.1$; GOTO 1190
1180 ERR_MSG$=PTH.1$+" is password protected"; PRINT @(0,20),ERR_MSG$,'CL',; GOTO 2900
1190 CALL "*pg.cnv",ERR=1200,PTH.2$,WRK.2$; GOTO 1210
1200 ERR_MSG$=PTH.2$+" is password protected"; PRINT @(0,20),ERR_MSG$,'CL',; GOTO 2900
1210 LFN.1=HFN; OPEN (LFN.1)WRK.1$
1220 LFN.2=HFN; OPEN (LFN.2)WRK.2$
1230 L.1$="",L.2$=""
1240 NO.ADD=0,NO.CHG=0,NO.DEL=0
2000 ! 2000 - Compare records
2010 IF L.1$="" THEN READ RECORD (LFN.1,END=2050)L.1$
2020 L1=NUM(L.1$(1,5),ERR=2030); GOTO 2060
2030 L1=NUM(L.1$(1,4),ERR=*NEXT); GOTO 2060
2040 MSGBOX "Cannot compare programs without line numbers",PTH.1$; GOTO 2900
2050 L1=65000
2060 IF L.2$="" THEN READ RECORD (LFN.2,END=2090)L.2$
2070 L2=NUM(L.2$(1,5),ERR=2080); GOTO 2100
2080 L2=NUM(L.2$(1,4)); GOTO 2100
2090 L2=65000
2100 IF L1+L2=130000 THEN GOTO 2900
2110 IF FID(0)=FID(PR.LFN) AND NO.ADD+NO.DEL+NO.CHG+(CHG.FLG$="N")<>0 THEN GOTO 2130
2120 PRINT @(10,4),L1:"#0000",@(10,6),L2:"#0000",
2130 IF L1<L2 THEN GOTO 2160
2140 IF L1>L2 THEN GOTO 2170
2150 REC.1$=L.1$,REC.2$=L.2$,L.1$="",L.2$=""; GOSUB 4000; GOTO 2000
2160 REC.1$=L.1$,REC.2$="",L.1$=""; GOSUB 4000; GOTO 2000
2170 REC.1$="",REC.2$=L.2$,L.2$=""; GOSUB 4000; GOTO 2000
2900 ! 2900 - End of report
2910 IF LFN.1<>0 THEN CLOSE (LFN.1)
2920 IF LFN.2<>0 THEN CLOSE (LFN.2)
2930 IF WRK.1$<>"" THEN ERASE WRK.1$,ERR=2940; WRK.1$=""
2940 IF WRK.2$<>"" THEN ERASE WRK.2$,ERR=2950; WRK.2$=""
2950 ! 
2960 IF NO.ADD+NO.DEL+NO.CHG=0 AND ERR_MSG$="" AND FL.TYP$<>"P" THEN GOTO 3030
2970 IF PR.LFT<1 THEN GOSUB 4500
2980 PR.LFT=PR.LFT-1
2990 IF ERR_MSG$<>"" THEN PRINT (PR.LFN)ERR_MSG$; ERR_MSG$=""; GOTO 3020
3000 IF NO.ADD+NO.DEL+NO.CHG=0 THEN PRINT (PR.LFN)" No differences between:",PTH.1$," and ",PTH.2$; GOTO 3020
3010 PRINT (PR.LFN)NO.ADD," additions,",NO.DEL," deletions,",NO.CHG," changes"
3020 IF FID(0)=FID(PR.LFN) THEN INFO$="Finished comparing these two programs.  Continue?",OPTS$=""; GOSUB 5000; IF OPT$="Q" THEN EXITTO 9000
3030 RETURN 
4000 ! 4000 - Output line
4010 IF REC.1$=REC.2$ THEN CD$="" ELSE IF REC.1$="" THEN CD$="Add",NO.ADD=NO.ADD+1 ELSE IF REC.2$="" THEN CD$="Del",NO.DEL=NO.DEL+1 ELSE CD$="Chg",NO.CHG=NO.CHG+1
4020 IF CD$="" AND CHG.FLG$="Y" THEN RETURN 
4030 O1$=REC.1$,O2$=REC.2$
4040 LNUM$=""
4050 IF O1$<>"" THEN LNUM$=O1$(1,POS(" "=O1$+" ")-1)
4060 X=LEN(LNUM$); IF X<4 OR X>5 THEN IF O2$<>"" THEN LNUM$=O2$(1,POS(" "=O2$+" ")-1)
4070 IF O1$<>"" THEN O1$=O1$(LEN(LNUM$)+2)
4080 IF O2$<>"" THEN O2$=O2$(LEN(LNUM$)+2)
4090 LNUM$=PAD(LNUM$,5,0),O1$=" "+O1$,O2$=" "+O2$
4100 IF PR.LFT<1 THEN GOSUB 4500 ELSE IF COMP$<>"" THEN GOSUB SUB_HEADING
4110 PR.LFT=PR.LFT-1
4120 IF LEN(O1$)<=MX_MID THEN LINE$=LINE$+O1$+DIM(MX_MID+1)+LNUM$; PRINT (PR.LFN)O1$,@(MX_MID+1),LNUM$,; O1$=""; GOTO 4150
4130 PRINT (PR.LFN)O1$(1,MX_MID),@(MX_MID+1),LNUM$,; LINE$=LINE$+O1$(1,MX_MID)+" "+LNUM$
4140 O1$=":"+O1$(MX_MID+1)
4150 LNUM$=""
4160 IF LEN(O2$)<=MX_MID THEN LINE$=LINE$+DIM(MX_MID+8)+O2$+$0A$; PRINT (PR.LFN)@(MX_MID+8),O2$; O2$=""; GOTO 4190
4170 PRINT (PR.LFN)@(MX_MID+8),O2$(1,MX_MID); LINE$=LINE$+" "+O2$(1,MX_MID)+$0A$
4180 O2$=":"+O2$(MX_MID+1)
4190 IF O2$+O1$<>"" THEN GOTO 4100
4200 PRINT (PR.LFN)'LF',; PR.LFT=PR.LFT-1
4210 IF CD$="" THEN RETURN 
4220 IF FID(0)<>FID(PR.LFN) THEN PRINT @(0,20),NO.ADD," Additions, ",NO.DEL," deletions, ",NO.CHG,", changes",
4230 RETURN 
4500 ! 4500 - Print page header
4510 PR.PAGE=PR.PAGE+1
4520 IF PR.PAGE<=1 THEN GOTO 4550
4530 IF FID(0)<>FID(PR.LFN) THEN GOTO 4550
4540 INFO$="More output to follow.  Continue?",OPTS$="Continue"; GOSUB 5000; IF OPT$="Q" THEN EXITTO 9000
4550 IF FID(0)=FID(PR.LFN) THEN PRINT (PR.LFN)'CS' ! ELSE PRINT (PR.LFN)'FF',
4560 IF NOT(HEADER_DONE) THEN HEADER_DONE=1; PRINT (PR.LFN)'LF',DAY,INT(TIM):"##0:",FPT(TIM)*60:"00",@(INT((MX_COL+1-LEN(PR.TTL$))/2)),PR.TTL$,'LF',DIM(MX_COL,"-")
4570 PRINT (PR.LFN)'LF',TTL.3$,'LF',DIM(MX_COL,"-")
4580 PR.LFT=MX_LINE,COMP$=""
4590 HEADER$=TTL.3$
4600 RETURN 
4610 ! 4600 - Sub Heading
4620 SUB_HEADING:
4630 IF PR.LFT<3 THEN COMP$=""; GOSUB 4500; GOTO 4670
4640 PRINT (PR.LFN)'LF',COMP$
4650 PRINT (PR.LFN)DIM(MX_COL,"-")
4660 PR.LFT=PR.LFT-3,COMP$=""
4670 RETURN 
5000 ! 5000 - Display title/options
5010 X$=" "+PAD(OPTS$+" Quit",79)
5020 PRINT @(0,0),'RM','BR',PAD(INFO$,80),@(0,1),X$,'RM',
5030 O=0; IF DEF_OPT$<>"" THEN O=POS(" "+DEF_OPT$=X$),DEF_OPT$=""
5040 IF O=0 THEN O=1
5050 OPT$=X$(O+1,1),L=POS(" "=X$(O+1))+1
5060 MSE_DOWN=0
5100 ! 5100 - Get option
5110 PRINT 'YELLOW',@(O-1,1),X$(O,L),'RM',
5120 COUNT+=1; OBTAIN (0,SIZ=1,HLP=X$(O+1,1))@(O-1,1),'BI',O$,'EI'
5130 ANT=CTL
5140 ! IF CTL=5 THEN GOSUB CLIP
5150 IF CTL=-1080 THEN GOTO 5450
5160 PRINT 'BR',@(O-1,1),X$(O,L),'RM',
5170 IF CTL>=0 THEN ON CTL GOTO 5400,5350,5300,5250,5250,5260
5180 IF CTL=-1081 THEN IF MSE_DOWN=0 THEN GOTO 5100 ELSE GOTO 5400
5190 IF CTL>-1000 THEN CALL "*control"; GOTO 5100
5200 IF CTL=-1003 OR CTL=-1004 OR CTL=-1016 THEN GOTO 5350
5210 IF CTL=-1005 OR CTL=-1015 THEN GOTO 5300
5220 IF CTL=-1010 THEN O=1; GOTO 5330
5230 IF CTL=-1018 THEN GOTO 5370
5240 PRINT 'RB',; GOTO 5100
5250 O$="Q"
5260 IF O$=" " THEN GOTO 5300 ELSE O$=UCS(O$)
5270 P=POS(" "+O$=X$); IF P=0 THEN PRINT 'RB',; GOTO 5100
5280 OPT$=O$,O=P,L=POS(" "=X$(O+1))+1; GOTO 5400
5300 ! 5300 - Forward
5310 OPT$=X$(O+L,1),O=O+L-1
5320 IF OPT$=" " THEN O=1,OPT$=X$(2,1)
5330 L=POS(" "=X$(O+1))+1; GOTO 5100
5350 ! 5350 - Backward
5360 O=POS(" "=X$(1,O-1),-1); IF O<>0 THEN L=POS(" "=X$(O+1))+1,OPT$=X$(O+1,1); GOTO 5100
5370 O=LEN(STP(X$,2)); GOTO 5360
5400 ! 5400 - Return - selection made
5410 PRINT 'GREEN',@(O-1,1),X$(O,L),'RM'
5420 IF OPT$="Q" THEN EXITTO 9000 ELSE RETURN 
5450 ! 5450 - Mouse follow logic
5460 MSE_DOWN=1,O$=MSE; O__C=DEC(O$(2,1))+1,O1=MAX(1,POS(" "=STP(X$(1,O__C),1),-1))
5470 IF O=O1 THEN GOTO 5120 ELSE PRINT 'BR',@(O-1,1),X$(O,L),'RM',; O=O1,OPT$=X$(O+1,1)
5480 IF OPT$=" " THEN GOTO 5370 ELSE GOTO 5330
5500 ! 5500 - Get input
5510 PRINT @(0,0),'RM','BR',PAD(INFO$,80),@(0,1),PAD(QUEST$,80),'RM',
5520 COUNT+=1
5530 IF COUNT=1 THEN RESP$=ZWL$(1,8) ! INPUT EDIT @(LEN(QUEST$),1),'YELLOW',RESP$,'RM'
5540 IF COUNT=2 THEN RESP$=SUB(RESP$,"/pvx/","/pvx/rel/"); COUNT=0
5550 IF CTL<>0 THEN EXITTO 9000 ELSE RESP$=STP(RESP$,2)
5560 RETURN 
7000 ! 7000 - Error logic
7010 PRINT IOL=8120,MSG(RET),; GOTO 0171
7020 PRINT IOL=8130,MSG(RET),; GOTO 0300
7030 PRINT IOL=8120,MSG(RET),; GOTO 0246
7040 PRINT IOL=8120,MSG(RET),; GOTO 0385
7800 ! 7800
7810 GET_FILES:
7820 DR$=PTH.1$,PG.MSK$="*",FL.LST$="",FL_LST=0,FL_TMP=0
7830 CALL "*gflst",DR$,PG.MSK$,FL.LST$,"N"
7840 IF CTL=4 AND PG.MSK$<>"" THEN GOTO 7860
7850 IF CTL>0 AND CTL<>2 AND CTL<>100 THEN EXITTO 9000
7860 IF PG.MSK$="" THEN GOTO 7970
7870 PRINT @(29,12),"(Scanning directory)",
7880 DR$=PTH.1$,FL.LST$=""; CALL "*file.lst;list_to_file",FL.LST$,DR$,PG.MSK$,"P"
7890 DR$=PTH.2$,FL.TMP$=""; CALL "*file.lst;list_to_file",FL.TMP$,DR$,PG.MSK$,"P"
7900 IF POS("FILNUM="=FL.TMP$) THEN GOTO 8000
7910 ! 
7920 X=POS(SEP=FL.TMP$); IF X=0 THEN GOTO 7950
7930 X$=FL.TMP$(1,X),FL.TMP$=FL.TMP$(X+1),X=POS(SEP+X$=SEP+FL.LST$); IF X=0 THEN FL.LST$=FL.LST$+X$
7940 GOTO 7920
7950 FL.LST$=SRT(FL.LST$)
7960 PRINT @(29,12),'CL',
7970 RETURN 
8000 ! ^100
8010 X=POS("FILNUM="=FL.LST$); IF X=0 THEN GOTO 7910 ELSE FL_LST=NUM(FL.LST$(8))
8020 X=POS("FILNUM="=FL.TMP$); IF X=0 THEN GOTO 7910 ELSE FL_TMP=NUM(FL.TMP$(8))
8030 READ (FL_TMP,KEY="",ERR=*NEXT)
8040 READ (FL_TMP,END=8070)X$
8050 WRITE (FL_LST,KEY=X$,DOM=*NEXT)X$
8060 GOTO 8040
8070 ! 
8080 X$=PTH(FL_TMP); CLOSE (FL_TMP); IF POS("*memory*"=LCS(X$))=0 THEN ERASE X$,ERR=*NEXT
8090 READ (FL_LST,KEY="",ERR=*NEXT)
8100 RETURN 
8110 ! 8000 - Iolists
8120 IOLIST @(0,20),'RB','CL'
8130 IOLIST @(0,20),'CL'
9000 ! 9000 - Wrapup
9010 PRINT @(0,22),'CL',"End of comparision",'LF','MODE'(SV_MODE$),'DF','CE',
9020 SET_PARAM 'SD'=SV_SD
9030 IF PR.LFN<>0 THEN CALL "*pr.abt",PR.LFN
9040 CLOSE (WK.LFN)
9050 IF LFN.1<>0 THEN CLOSE (LFN.1)
9060 IF LFN.2<>0 THEN CLOSE (LFN.2)
9070 IF WRK.1$<>"" THEN ERASE WRK.1$
9080 IF WRK.2$<>"" THEN ERASE WRK.2$
9090 GOTO NEXT_ZWL
9100 DONE_ZWL:
9110 IF NOT(NUL(%WDX$)) THEN %USE_NOTEPAD=1; CALL "[WDX]*Windx.utl;Get_Val","ENV("+QUO+"TEMP"+QUO+")",%ENV_TEMP$; CALL "[WDX]*Windx.utl;Get_Val","ENV("+QUO+"COMSPEC"+QUO+")",%ENV_COMSPEC$ ELSE %USE_NOTEPAD=0
9120 IF %USE_NOTEPAD THEN Q$=%WDX$+%ENV_COMSPEC$+" /c NOTEPAD.EXE "+SUB(__XX$,"[WDX]",""); INVOKE HIDE Q$ ELSE INVOKE Q$
9140 END 
