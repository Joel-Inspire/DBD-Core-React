0010 REM "Cleared Check data Update from Text File <BRGTCC>
0020 ! SETESC 9300; SETERR 9000
0035 REM "5.6 - 09/18/08 - 11.185277 - jvv - SSP# 217397
0037 REM "217397-Ability to import a text file from the bank containing      
0040 REM "Copyright 2008 DemandBridge, Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0052 IF TCB(88)=0 THEN {
0055 MSGBOX "You must be using Windx to access this program" } ELSE {
0060 CLEAR 
0070 SETERR 0080; ENTER X3$,X4$,Q0$,Q1$
0080 PROCESS "BRGTCC","../BRG/BR.EN"
0082  }
0090 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0105 INIT:
0110 X0$="BRGTCC",X1$="Cleared Check Update from Text File"
0120 DIM Z0$(80,"-")
0150 DIM FL(11); FL(1)=3,FL(2)=10,FL(3)=10,FL(4)=1,FL(5)=6,FL(6)=10,FL(7)=15,FL(8)=6,FL(9)=3,FL(10)=10,FL(11)=6
0160 COUNT=0
0165 FILE_FOUND=0
0195 ! 
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,-1,-1; IF X1>0 THEN GOSUB WRAPUP; GOTO *RETURN
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0299 ! 
0310 IOLIST BR0$,BR0[0],BR0[1]
0320 IOLIST ZYB$
0395 ! 
0500 REM "Open files
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O BR0... 02O ZYB... 03O BR1... 13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; IF Z0>0 THEN GOSUB WRAPUP; GOTO *RETURN
0525 GOSUB LOAD_BANK_CODE
0530 GOSUB 7900; REM "Create if necessary, open BR3_LOG file
0595 ! 
0600 RETURN 
0900 ! Display screen, prompt for file format
0915 GOSUB 6000
0950 DIM FILE_FMT$(2); FILE_FMT$="01",X$="Valid format: 01"
0955 CALL "ZZENTR","SZ",TMP{ALL},FILE_FMT$,X4$,X3$,34,13,1,2,C0,"","{3"+X$,"","FM2UPA00","","",""; IF ABS(C0)=4 THEN GOTO 9900
0960 IF POS(FILE_FMT$="1")=0 THEN CALL "ZZPROM",".4",X3$,S3,"This is not a valid file format, please try again.","","",0; GOTO 0955
0965 DIM BCODE$(3); X$="Bank Code"
0970 CALL "ZZENTR","AXUF",TMP{ALL},BCODE$,X4$,X3$,34,14,1,3,C0,"","{1"+X$,"","AR2MAA00","","",""; IF ABS(C0)=4 THEN GOTO 9900 ELSE IF ABS(C0)>4 THEN GOSUB 6400; ON C0 GOTO 0970,0971
0975 DIM ZYB$(50); READ (Z[2],KEY=BCODE$,DOM=0970)ZYB$
0978 PRINT @(46,14),ZYB$(4,30),
0980 FIND (Z[3],KEY=BCODE$,DOM=0985)*; GOTO 0990
0985 CALL "ZZPROM",".4",X3$,S3,"The Bank Recon for this code has not been start.","","",0; GOTO 0955
0990 IF Q1$="" THEN CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0991,9900
0995 ! 
0998 REM "Get list of BR3 files to process
1000 PROCEED:
1002 BCODE$=BANK_CODE$,FILE_FMT$=FORMAT$
1005 READ (Z[3],KEY=BCODE$,DOM=1008)*; GOTO 1010
1008 MSGBOX "No Bank Recon Available!!",MSG("CONFIRMING"),"INFO,TIM=3"; RETURN 
1010 CALL "ZZ2BLS","D0:BR3*","S",F$
1012 IF X3$(77,1)="U" THEN CALL "ZZ2BLS","D0:br3*","S",F_LOWER$; F$=F$+F_LOWER$
1015 P=POS(":"=F$); IF P=0 THEN IF FILE_FOUND=1 THEN GOTO 5500 ELSE GOTO 5900
1020 F1$=F$(P+1); P1=POS(":"=F1$); IF P1>0 THEN F$=F1$(P1-2),F1$=F1$(1,P1-3) ELSE F$=""
1025 FILE_FOUND=1
1050 Z$="10CU 10O "+F1$+" "
1055 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 1056,1995
1060 CLOSE (Z[10]); OPEN LOCK (Z[10],OPT="TEXT")F1$; REM "Open for reading raw file
1065 CLOSE (13); OPEN (13)"ZZPARM"; REM "Re-open ZZPARM closed in ZZ2BLS
1075 BR_MESS.CTL'VALUE$="Processing: "+F1$
1080 IF Q1$<>"G" THEN GOSUB 8100
1085 L0=80; IF FILE_FMT$="2" THEN L0=999 ! Length of file format 1 records, change accordingly as file formats are added.  WO166044, format 02 
1090 END_MESSAGE$="BR3|STAT|"+FN%CDS$+"|"+X3$(40,3)+"|"+X3$(9,3)+"|"+FID(0)+"|"+F1$+"|"+STR(T:"0000"); GOSUB SYSTEM_CALL
1095 ! 
1100 REM "Process the file
1102 I9=0,RECORD$="",RCOUNT$="",ERR_TYPE=0; DIM RCOUNT[50]
1104 BAD_REC_COUNT=0 ! Records not processed, ERR_TYPE=3
1105 UPDATE_COUNT=0 ! Records that match by amount
1106 DISC_COUNT=0 ! Records cleared but discrepency, ERR_TYPE=1
1108 NOTFOUND_COUNT=0 ! Records not found, ERR_TYPE=2
1110 READ (Z[10],END=7750)RECORD$
1113 I9=I9+1
1115 P=POS("BR3"=RCOUNT$,3); IF P=0 THEN RCOUNT$=RCOUNT$+"BR3"; GOTO 1115; REM "Used to write to /usr/lib/pvx/BR_LOG
1120 P1=(P+2)/3; RCOUNT[P1]=RCOUNT[P1]+1
1122 COUNT=COUNT+1
1130 REM PRINT @(0,3),'CE',@(0,4),RECORD$; INPUT *
1145 IF LEN(RECORD$)<L0 THEN I9$=RECORD$; DIM RECORD$(L0); RECORD$(1)=I9$; REM "Correct length if short
1195 ! 
1500 REM "Process RECORD$
1510 DIM BR0$(127),BR0[2]; LET_CODE=0,MESSAGE$="",BR0$(1,3)=BCODE$; REM "preset Bank code
1520 ON POS(FILE_FMT$="1") GOTO TYPE_NOT_FOUND,PROCESS_FILE_01
1595 ! 
1900 REM "End of file
1901 REM "Close & Rename to BR4 for archiving. Erase an existing BR4 file, if same name
1905 Z$="10CU"+F1$+" "
1906 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
1910 F2$=F1$; IF F2$ LIKE "[Bb][Rr]3*" THEN F2$(1,3)="BR4" ELSE GOTO 1995
1915 ERASE F2$,ERR=1916
1919 REM "Get disk DIRectory of data files
1925 J$=%DATAPATH$
1930 REM "Move the file
1936 ORIGINAL$=J$+DLM+F1$,ARCHIVE$=J$+DLM+F2$; RENAME ORIGINAL$,ARCHIVE$,ERR=*NEXT
1995 GOTO 1015
1999 ! 
2000 TYPE_NOT_FOUND:REM "Record type sent in not found
2005 ERR_TYPE=4
2015 GOSUB BAD_RECORD
2090 TYPE_NOT_FOUND_END:GOSUB WRAPUP; GOTO *RETURN
2099 ! 
2100 PROCESS_FILE_01:REM "Process file format 01 record (WO217397/603)
2110 FIRST=1,KEY$=BCODE$+"1"+STR(NUM(RECORD$(14,10)):"000000000") ! input len=10, output = 9
2115 READ (Z[1],KEY=KEY$,DOM=*NEXT)
2120 KY$=KEY(Z[1],END=2190)
2125 READ (Z[1],KEY=KY$)IOL=0310
2128 IF FIRST AND KY$(1,13)<>KEY$(1,13) THEN ERR_TYPE=2; GOSUB BAD_RECORD; GOTO 1110
2132 FIRST=0
2135 IF BR0$(1,13)=KEY$ THEN FIND_KEY$=KY$; GOTO 2120 ELSE GOSUB FILE_01_UPDATE
2190 PROCESS_FILE_01_END:GOTO 1110
2199 ! 
2200 FILE_01_UPDATE:REM "Existing record, extract BR0 and update with current info
2205 DIM BR0$(127); EXTRACT (Z[1],KEY=FIND_KEY$,DOM=*NEXT)IOL=0310; GOTO UPDATE_BR0
2210 ERR_TYPE=2; GOSUB BAD_RECORD; GOTO PROCESS_FILE_01_END; REM "Was set to update existing record but got DOM when trying EXTRACT, write out err type=2 for log
2215 UPDATE_BR0:REM "Have BR0 record, update fields
2220 BR3_CHK_AMT=NUM(RECORD$(31,10),ERR=FILE_01_ERROR)*.01
2225 IF BR0[0]<=0 THEN ERR_TYPE=3; GOSUB BAD_RECORD; GOTO 2280
2230 IF BR3_CHK_AMT=BR0[0] THEN BR0[1]=BR0[0],BR0$(79,1)="Y",BR0$(107,1)="N",UPDATE_COUNT=UPDATE_COUNT+1 ELSE BR0[1]=BR3_CHK_AMT,BR0$(79,1)="D",BR0$(107,1)="Y",ERR_TYPE=1; GOSUB BAD_RECORD
2245 ! 
2270 GOSUB WRITE_RECORDS
2280 FILE_01_UPDATE_END:RETURN 
2299 ! 
2300 FILE_01_ERROR:REM "Create new BR0 record
2305 ERR_TYPE=3
2310 GOSUB BAD_RECORD
2320 GOTO PROCESS_FILE_01_END
4100 WRITE_RECORDS:
4110 WRITE (Z[1],KEY=FIND_KEY$)IOL=0310
4130 ! IF POS(OP_PARM$(279,1)="YF")<>0 THEN WRITE (Z[9],KEY=BR0$(1,11)+BR0$(382,20)+BR0$(12,4)) ! FTD
4145 WRITE_RECORDS_END:RETURN 
4199 ! 
5000 ! END OF FILE MESSAGES"
5040 IF UPDATE_COUNT<>0 THEN END_MESSAGE$=STR(UPDATE_COUNT:"0000")+" BR0 records were updated - exact match"; GOSUB SYSTEM_CALL
5045 IF DISC_COUNT<>0 THEN END_MESSAGE$=STR(DISC_COUNT:"0000")+" BR0 records were updated with Discrepencies"; GOSUB SYSTEM_CALL
5050 IF NOTFOUND_COUNT<>0 THEN END_MESSAGE$=STR(NOTFOUND_COUNT:"0000")+" records were not found in BR0"; GOSUB SYSTEM_CALL
5055 IF BAD_REC_COUNT<>0 THEN END_MESSAGE$=STR(BAD_REC_COUNT:"0000")+" records were not processed"; GOSUB SYSTEM_CALL
5070 RETURN 
5099 ! 
5100 SYSTEM_CALL:REM "Write out to Product Catalog Gateway Log
5120 IF BRL>0 THEN PRINT (BRL)END_MESSAGE$
5140 SYSTEM_CALL_END:RETURN 
5199 ! 
5500 ! EOJ
5510 END_MESSAGE$=FN%CDS$+", "+STR(COUNT:"0000")+" records processed"
5520 GOSUB SYSTEM_CALL
5525 END_MESSAGE$=""; GOSUB SYSTEM_CALL
5530 IF POS(Q1$="GD")=0 THEN PRINT @(0,15),'CE',
5540 MSGBOX "Process complete!!",MSG("CONFIRMING"),"INFO,TIM=3"
5550 GOTO 9900
5900 MSGBOX "No Input Text File Found!!",MSG("CONFIRMING"),"INFO,TIM=3"; GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(12,4),"This program will read the BR3 text file in the format",@(12,5),"entered below, then for each record read,",
6025 PRINT @(12,6),"if an existing Bank Recon record is found it will be",@(12,7),"cleared."
6050 PRINT @(12,13),"BR3 text file format:",
6060 PRINT @(19,14),"Bank Code:",
6165 PRINT (0,ERR=6066)'SF',
6190 RETURN 
6195 ! 
7100 LOAD_BANK_CODE:
7110 DIM PARAM$(200)
7120 READ (Z[13],KEY=%C$+"A/R",DOM=*NEXT)PARAM$
7130 BANK_CODE$=PARAM$(43,3)
7140 GOSUB FIND_REC
7150 RETURN 
7200 FIND_REC:
7210 BCODE$=PAD(BANK_CODE$,3)
7215 IF STP(BANK_CODE$,2)="" THEN GOTO 7230
7220 DIM ZYB$(50); READ (Z[2],KEY=BCODE$,DOM=7230)ZYB$; GOTO 7240
7230 MSGBOX "Invalid Bank Code!!",MSG("CONFIRMING"),"INFO,TIM=3"; RETURN 
7240 REFRESH_FLG=1
7260 RETURN 
7749 ! 
7750 REM "End of file
7752 IF LEN(RCOUNT$)<=0 THEN GOTO 7795
7755 FOR X=1 TO LEN(RCOUNT$)-2 STEP 3
7756 S$="BR3|STAT|"+FN%CDS$+"|"+X3$(40,3)+"|"+X3$(9,3)+"|"+FID(0)+"|"+F1$+"|"+RCOUNT$(X,3)+"|"+STR(RCOUNT[(X+2)/3]:"0000")
7758 IF BRL>0 THEN PRINT (BRL)S$
7759 NEXT X
7770 GOSUB 5000
7795 GOTO 1900
7799 ! 
7800 BAD_RECORD:REM "Write message to br3_LOG, based on ERR_TYPE
7805 ON ERR_TYPE GOTO 7890,7810,7820,7830,7840,7850,7890
7810 DISC_COUNT=DISC_COUNT+1
7815 MESSAGE$="File: "+F1$+" Reason: "+"Record Cleared but Discrepency"; GOTO 7880
7820 NOTFOUND_COUNT=NOTFOUND_COUNT+1
7825 MESSAGE$="File: "+F1$+" Reason: "+"Record Not Found"; GOTO 7880
7830 BAD_REC_COUNT=BAD_REC_COUNT+1
7835 MESSAGE$="File: "+F1$+" Reason: "+"Invalid BR3 Record"; GOTO 7880
7840 MESSAGE$="File layout format "+FILE_FMT$+" not found, can't process.",BAD_REC$=""; GOTO 7885
7880 BAD_REC$=RECORD$(14,10)+"|"+RECORD$(25,6)+"|"+RECORD$(31,10)+"|"+RECORD$(56,6)
7885 IF BRL>0 THEN PRINT (BRL)MESSAGE$; PRINT (BRL)BAD_REC$
7890 BAD_RECORD_END: ERR_TYPE=0; RETURN 
7899 ! 
7900 REM "Open BR_LOG file, create if not there
7905 BRL=14
7910 CLOSE (BRL); OPEN LOCK (BRL,ERR=*NEXT)"BR_LOG"; GOTO 7940
7915 IF ERR=12 THEN SERIAL "BR_LOG"; GOTO 7910 ELSE BRL=0
7920 MSGBOX "Log file could not be opened- contact Support",MSG("CONFIRMING"),"INFO,TIM=3"; GOSUB WRAPUP; GOTO *RETURN
7940 RETURN 
7949 ! 
7995 ! 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8107 T=0
8113 CALL "ZZ2FNC;SERIALRECCNT",Z[10],T
8115 BR_MESS.CTL'VALUE$="There are "+STR(T)+" records to process"
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8135 T1=0
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 ! CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,I9
8195 RETURN 
8199 ! 
8910 DEF FND$(Z9$)=Z9$(1*2+1,2)+"/"+Z9$(7-1*2,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "End
9902 CMD_STR$="E"; GOTO *RETURN
9905 WRAPUP:
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9915 CLOSE (BRL)
9950 RETURN 
9999 END 
56000 REM "217397-Ability to import a text file from the bank containing      
