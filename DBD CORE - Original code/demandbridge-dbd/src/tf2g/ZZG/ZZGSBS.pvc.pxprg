0010 ! Definition of Class: XLSubstring
0035 REM "5.5 - 09/04/07 - 16.922777 - crg - SSP# 203699
0037 REM "203699-Ability to apply expressions to XML data
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0100 DEF CLASS "ZZGSBS" DELETE REQUIRED 
0110 LIKE "ZZGXPR"
0120 ! 
0130 ! Local:
0140 LOCAL STARTVAL
0150 LOCAL LENGTHVAL
0160 ! 
0170 ! Operations:
0180 ! 
0190 FUNCTION EVAL$(INPUT_STR$)
0200 ENTER (INPUT_STR$)
0210 ! PRINT _OBJ," ",OPDESC$,"("+STR(STARTVAL)+","+STR(LENGTHVAL)+")"
0220 IF SUBEXPR<>0 THEN {
0230 SUBRETVAL$=SUBEXPR'EVAL$(INPUT_STR$)
0240  } ELSE {
0250 SUBRETVAL$=INPUT_STR$
0260  }
0270 IF SIDEEXPR<>0 THEN {
0280 SIDERETVAL$=SIDEEXPR'EVAL$(INPUT_STR$)
0290  }
0300 IF LENGTHVAL>=0 THEN RETVAL$=MID(SUBRETVAL$,STARTVAL,LENGTHVAL) ELSE RETVAL$=MID(SUBRETVAL$,STARTVAL)
0310 EXPRVAL$=RETVAL$+SIDERETVAL$
0320 RETURN EXPRVAL$
0330 ! 
0340 END DEF
0350 ! 
0360 ON_CREATE:
0370 ENTER (_STARTVAL),(_LENGTHVAL)
0380 OPTYPE=2
0390 OPDESC$="XLSubstring"
0400 STARTVAL=MAX(_STARTVAL,1)
0410 LENGTHVAL=_LENGTHVAL
0420 RETURN 
0430 ! 
0440 ON_DELETE:
0450 IF SUBEXPR<>0 THEN DROP OBJECT SUBEXPR; SUBEXPR=0
0460 IF SIDEEXPR<>0 THEN DROP OBJECT SIDEEXPR; SIDEEXPR=0
0470 RETURN 
9999 END 
56001 REM "203699-Ability to apply expressions to XML data
