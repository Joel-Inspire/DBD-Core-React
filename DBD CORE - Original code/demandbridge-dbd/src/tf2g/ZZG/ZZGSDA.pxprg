0010 ! ZZGMDA - A/R Statistical Data Inquiry (="ARA"+%C$)
0035 REM "5.7 - 08/02/15 - 15.576944 - jvv - SSP# 278906
0037 REM "278906-Issue with adding quote on Salesperson Stat Panel, if you   
0040 REM "Copyright 2015 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 ! 
0093 _SLSMAN$=%SLSMAN$
0095 PROCESS "ZZGSDA","../ARG/AR.EN"
0097 %SLSMAN$=_SLSMAN$
0099 EXIT 
0100 ! 100 - Initialization
0110 INIT:
0120 ! 
0125 %PGM_NAME$="*zzgmda*"
0130 X0=-1; X2=-1
0140 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
0150 ! 
0190 DIM Z[NUM(X3$(60,3))],ARA[13],B_ARA[13]
0200 ! Files
0210 Z$="01O ARA...  06O ZZPARM  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; IF Z0=1 THEN CMD_STR$="END"; GOTO WRAPUP
0295 ! 
0300 ! Iolist
0310 IOLIST ARA$,ARA{ALL}
0400 ! 
0410 GOSUB GET_PARAM
0415 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0480 ! CALL "*wingrp;Show",FLDS.GRP$; CALL "*wingrp;Show",FIELDS.GRP$
0490 RETURN 
0500 MAIN_POST_DISPLAY:! 500 - Main panel post_display logic
0520 FY$=F0$(34,4) ! Preset current fiscal year
0525 IF %SLSMAN$<>"" THEN {
0530 SLSMAN$=%SLSMAN$
0535 DISABLE CONTROL SLSMAN.CTL
0540 GOSUB PROCESS_SLSMAN
0545  }
0550 NEXT_ID=SLSMAN.CTL,REFRESH_FLG=1
0560 RETURN 
0595 ! 
0900 ! 900 - Wrapup
0910 WRAPUP:
0930 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0970 EXIT 
0995 ! 
1000 PROCESS_SLSMAN:
1010 ! CALL "ZZWLKU;Parse_cust",SLSMAN$,CUST_DIV$,CUST_CODE$
1020 SLSMAN$=PAD(SLSMAN$,4)
1030 NEXT_ID=FY.CTL
1040 REFRESH_FLG=1
1090 PROCESS_SLSMAN_END:RETURN 
1095 ! 
2000 ! LOAD CHANGES
2010 LOAD_INFO:
2015 ARA[0]=OPEN_BAL
2020 FOR ZZ9=1 TO 13
2030 EXECUTE "ARA["+STR(ZZ9)+"]=PERIOD_AMT_"+STR(ZZ9)
2040 NEXT ZZ9
2050 FOR XX9=0 TO 13
2060 IF ARA[XX9]<>B_ARA[XX9] THEN CHG_FLG=1
2070 NEXT XX9
2090 RETURN 
3000 ! Write Record
3010 WRITE_REC:
3040 WRITE (Z[1],KEY=SLSMAN$+CR_FY$+OPTION$)IOL=0310
3080 CHG_FLG=0
3090 RETURN 
5000 ! DELETE RECORD
5010 DELETE_REC:
5020 MSGBOX MSG("DELETE_REC"),MSG("VERIFY"),"?,YESNO",ANS$
5030 IF ANS$="NO" THEN RETURN 
5040 REMOVE (Z[1],KEY=SLSMAN$+CR_FY$+OPTION$)
5080 CHG_FLG=0
5090 RETURN 
7000 ! Add New Options
7010 ADD_OPT:
7013 IF F1$(POS(OPTION$=F1$,5)+1,1)<>"Y" THEN RETURN 
7015 GOSUB RESET
7020 MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"?,YESNO",ANS$
7030 IF ANS$="NO" THEN EXIT 
7040 READ (Z[6],KEY=%C$+"G/LYE"+CR_FY$,DOM=*NEXT)GL_DATES$
7050 ARA$="",ARA$+=SLSMAN$; ARA$+=CR_FY$; ARA$+=OPTION$ ! ssp 278906
7060 CALL "*wingrp;Unlock",FIELDS.GRP$; CALL "*wingrp;Show",FLDS.GRP$; CALL "*wingrp;Show",FIELDS.GRP$; CALL "*wingrp;Enable",BUTTONS.GRP$
7070 FOR I=1 TO NUM(GL_DATES$(13,2))
7080 EXECUTE "ML_DATE"+STR(I)+"$="+""""+STR(I:"00")+"- "+""""+"+FND$(GL_DATES$(15+I*6,6))"
7090 NEXT I
7100 NEXT_ID=OPEN_BAL.CTL
7110 OPTIONS$="R"; REST=1; GOSUB BORDERLESS
7180 REFRESH_FLG=1
7190 RETURN 
9000 GET_PARAM:
9010 DIM SEC$(255); READ (Z[6],KEY="osec"+%X3_OP_ID$,DOM=*NEXT)SEC$
9020 READ (Z[6],KEY=%C$+"G/L",DOM=*NEXT)F0$
9030 MSG_1$=MSG("ARG1_033")+SEP+MSG("ARG1_034")+"."
9040 IF NUL(F0$) THEN MSGBOX MSG_1$,MSG("ATTN"),"!"; CMD_STR$="END"; GOTO WRAPUP
9050 READ (Z[6],KEY="STATAR3")F9$,F0
9060 FOR XX=0 TO 9; F1$=F1$+F9$(49+XX*17,5),F2$=F2$+F9$(54+XX*17,12); NEXT XX
9070 FIND (Z[6],KEY="PATH"+%X3$(40,3),ERR=NO_PATH)EXCEL$,WORDPAD$ ! SSP 188541
9090 RETURN 
9999 END 
14000 QUOTA:
14010 MSGBOX MSG("QUOTA_AMT"),MSG("CONFIRMING"),"?,YesNo",OP$
14020 IF OP$="NO" THEN GOTO *RETURN
14030 FOR I=2 TO 13; EXECUTE "PERIOD_AMT_"+STR(I)+"=PERIOD_AMT_1"; NEXT I
14040 GOSUB GEN_YTD
14050 REFRESH_FLG=1
14060 RETURN 
21000 ! 21000 - Read and load line details
21010 PROCESS_OPTION:
21013 GOSUB RESET
21015 ! IF SEC$(63,1)="Y" AND POS(OPTION$="PD")>0 THEN MSGBOX MSG("ARG1_035"),MSG("ACCESS_DEN"),"!"; RETURN 
21020 READ (Z[1],KEY=SLSMAN$+CR_FY$+OPTION$,DOM=ADD_OPT)IOL=0310
21030 READ (Z[6],KEY=%C$+"G/LYE"+CR_FY$,DOM=*NEXT)GL_DATES$
21040 B_ARA$=ARA$; FOR XX=0 TO 13; B_ARA[XX]=ARA[XX]; NEXT XX; CHG_FLG=0
21050 CALL "*wingrp;Unlock",FIELDS.GRP$; CALL "*wingrp;Show",FLDS.GRP$; CALL "*wingrp;Show",FIELDS.GRP$; CALL "*wingrp;Enable",BUTTONS.GRP$
21060 CALL "*wingrp;Enable",BT.GRP$
21070 IF F1$(POS(OPTION$=F1$,5)+1,1)="N" THEN CALL "*wingrp;lock",FIELDS.GRP$; CALL "*WINGRP;DISABLE",BUTTONS.GRP$; NEXT_ID=OPTION.CTL; OPTIONS$="R"; REST=0; GOSUB BORDERLESS ELSE OPTIONS$="R"; REST=1; GOSUB BORDERLESS
21075 OPEN_BAL=ARA[0]
21080 GOSUB LOAD_LINE_DETAILS
21085 NEXT_ID=BT_EXIT.CTL; REFRESH_FLG=1
21090 RETURN 
25000 ! 25000 - Load items into listbox
25010 LOAD_LINE_DETAILS:! COLUMN SEPARATOR IS SEP
25020 FOR I=1 TO NUM(GL_DATES$(13,2))
25030 EXECUTE "ML_DATE"+STR(I)+"$="+""""+STR(I:"00")+"- "+""""+"+FND$(GL_DATES$(15+I*6,6))"
25034 EXECUTE "PERIOD_AMT_"+STR(I)+"=ARA["+STR(I)+"]"
25036 EXECUTE "TOTAL_YTD=TOTAL_YTD+PERIOD_AMT_"+STR(I)
25038 EXECUTE "YTD_AMT"+STR(I)+"=TOTAL_YTD"
25040 NEXT I
25050 FOR I=NUM(GL_DATES$(13,2))+1 TO 13
25060 EXECUTE "MULTI_LINE HIDE ML_DATE"+STR(I)+".CTL;MULTI_LINE HIDE PERIOD_AMT_"+STR(I)+".CTL;MULTI_LINE HIDE YTD_AMT"+STR(I)+".CTL"
25070 NEXT I
25080 %PER_LEN=NUM(GL_DATES$(13,2))
25090 RETURN 
25095 ! 
26000 ! Load
26010 RESET:
26015 FOR AA=1 TO 13
26020 B_ARA[AA]=0; ARA[AA]=0
26025 EXECUTE "PERIOD_AMT_"+STR(AA)+"=0,YTD_AMT"+STR(AA)+"=0"
26030 NEXT AA
26040 TOTAL_YTD=OPEN_BAL
26050 DIM ARA(16),B_ARA(16)
26090 CHG_FLG=0
27000 ! 
27010 CALL "*wingrp;Lock",FIELDS.GRP$; CALL "*wingrp;Hide",FLDS.GRP$; CALL "*wingrp;Hide",FIELDS.GRP$; CALL "*wingrp;Disable",BUTTONS.GRP$
27020 CALL "*wingrp;Disable",BT.GRP$
27090 RETURN 
27500 SET_BUTTON: BT$="SPCNQBDGL" ! SSP243104 jdf
27510 FOR AA=1 TO LEN(BT$)
27515 AAA=EVN("BT_TYPE_"+BT$(AA,1)+".CTL")
27520 AAA'TEXTCOLOUR$="DEFAULT",AAA'HEIGHT=25,BBB$=AAA'TEXT$,XXX=POS("}"=BBB$); IF XXX<>0 THEN BBB$=BBB$(XXX+1),AAA'TEXT$=BBB$
27530 NEXT AA
27535 AAA=EVN("BT_TYPE_"+OPTION$+".CTL")
27540 AAA'TEXTCOLOUR$="Light Blue",AAA'HEIGHT=40,BBB$=AAA'TEXT$,AAA'TEXT$="{!Arrow_Down}"+BBB$
27550 RETURN 
28000 ! GENERATE YTD
28010 GEN_YTD:
28020 TOT_AMT=OPEN_BAL
28030 FOR XX9=1 TO 13
28040 EXECUTE "TOT_AMT+=PERIOD_AMT_"+STR(XX9)+", YTD_AMT"+STR(XX9)+"=TOT_AMT"
28050 NEXT XX9
28080 REFRESH_FLG=1
28090 RETURN 
30000 LOAD_FISCAL_YEAR:
30010 READ (Z[6],KEY=%C$+"A/R",DOM=*NEXT)GL_REC$; CR_YEAR$=GL_REC$(7,4),CR_PD$=GL_REC$(11,2)
30020 KE$=%C$+"G/LYE",CR_FY$=""
30030 READ (Z[6],KEY=KE$,DOM=*NEXT)
30040 K$=KEY(Z[6],END=30100); READ (Z[6])Z9$
30050 IF LEN(K$)<8 OR KE$<>K$(1,8) THEN GOTO END_LOAD_FISCAL_YEAR
30060 ! IF POS(Z9$(99,1)="CR")>0 THEN GOTO 30040
30070 CR_FY$=CR_FY$+K$(9)+"/"
30080 IF K$(9,4)=CR_YEAR$ THEN CC$=Z9$(21)
30090 GOTO 30040
30100 END_LOAD_FISCAL_YEAR:
30110 CC$=CC$(NUM(CR_PD$)*6-5,6),END_DATE$=CC$,REFRESH_FLG=1
30120 DROP_BOX LOAD CR_FY.CTL,CR_FY$
30130 DROP_BOX WRITE FY.CTL,CR_YEAR$,ERR=*NEXT
30140 DROP_BOX LOAD END_FISCAL.CTL,CR_FY$,ERR=*NEXT
30150 DROP_BOX WRITE END_FISCAL.CTL,CR_YEAR$,ERR=*NEXT
30155 CR_FY$=CR_YEAR$
30160 CR_ACCTPD$=""
30170 FOR ACCT=1 TO 12; CR_ACCTPD$=CR_ACCTPD$+STR(ACCT:"00")+"/"; NEXT ACCT
30180 DROP_BOX LOAD CR_ACCTPD.CTL,CR_ACCTPD$,ERR=*NEXT
30190 DROP_BOX WRITE CR_ACCTPD.CTL,CR_PD$,ERR=*NEXT
30200 END_ACCTPD$=CR_ACCTPD$
30210 DROP_BOX LOAD END_ACCTPD.CTL,CR_ACCTPD$,ERR=*NEXT
30220 DROP_BOX WRITE END_ACCTPD.CTL,CR_PD$,ERR=*NEXT
30230 CR_ACCTPD$=CR_PD$
30240 RETURN 
33000 BORDERLESS:
33005 IF REST1=1 AND REST2=REST THEN GOTO *RETURN
33060 ! CALL "BORDERLESS",0,OPTIONS$,REST,FIELDS1.GRP$
33070 REST1=1,REST2=REST
33080 RETURN 
33090 LOAD_GRAP:
33100 VAR$=MSG("CURR_PER")+"=",MTHS$=MSG("MTHS"),VAL1$="",VAR$="",TOTAL_PIE=0
33110 FOR XX=1 TO %PER_LEN; TOTAL_PIE=TOTAL_PIE+ABS(EVN("PERIOD_AMT_"+STR(XX))); NEXT XX
33120 FOR XX=1 TO %PER_LEN; DTE$=EVS("ML_DATE"+STR(XX)+"$"),MM=NUM(DTE$(1,2)),DTE$=MTHS$(NUM(DTE$(1,2))*3-2,1),VAR2$=VAR2$+DTE$+"/",VAR$=VAR$+STR(EVN("PERIOD_AMT_"+STR(XX)))+","; VAL_1=ABS(EVN("PERIOD_AMT_"+STR(XX))); IF VAL_1<>0 THEN PRECISION 3; VAL1$=VAL1$+MTHS$(MM*3-2,3)+" "+STR((VAL_1/TOTAL_PIE)*100:"###.0-%")+"="+STR(EVN("PERIOD_AMT_"+STR(XX)))+"/"; PRECISION 2 END_IF ; NEXT XX
33130 VAR$=VAR$(1,LEN(VAR$)-1)+"/"; DIM OPT$[9]; OPT$[1]=MSG("SALES"),OPT$[2]=MSG("PROFIT"),OPT$[3]=MSG("COMM"),OPT$[4]=MSG("NUM_OF_INV"),OPT$[5]=MSG("QUOTA"),OPT$[6]=MSG("BOOKINGS"),OPT$[7]=MSG("BK_PROFIT"),OPT$[8]=MSG("ADJ_GP"),OPT$[9]=MSG("GROSS_P"),OP$=OPT$[POS(OPTION$="SPCNQBDGL")] ! SSP243104 jdf
33140 PROCESS "GLGMDA.1","../ARG/AR.EN",VAR$,%CUST_NAME$,VAL1$,VAR2$,OP$
33150 RETURN 
33160 LOAD_GRAPHIC:
33170 VAR$=ARG_1$,CHART_1.CTL'TITLE1$=ARG_2$,VAL1$=ARG_3$,VAR2$=ARG_4$,XAXISTITLE$=ARG_5$
33180 GOSUB DEFAULTS
33190 FOR XX=1 TO %PER_LEN; VAL3$=VAL3$+" =0/"; NEXT XX
33200 IF CB_3D$="0" THEN CHART_1.CTL'TITLE2$=ARG_2$,CHART_1.CTL'TITLE1$="" ELSE CHART_1.CTL'TITLE1$=ARG_2$,CHART_1.CTL'TITLE2$=""
33210 MTHS$=MSG("MTHS")
33220 FOR XX=1 TO LEN(MTHS$) STEP 3; MONTHS$=MONTHS$+MTHS$(XX,1)+"="+MTHS$(XX,3)+" "; NEXT XX
33230 GOSUB SELECT_GRAF ! CHART LOAD CHART_1.CTL,VAR$
33240 REFRESH_FLG=1,B_GRAF$=""
33250 CHART ENABLE AA
33260 RETURN 
33270 SELECT_GRAF:
33280 DIM GRA$[8]; GRA$[1]="AREA",GRA$[2]="BAR",GRA$[3]="COLUMN",GRA$[4]="LINE",GRA$[5]="PIE",GRA$[6]="RIBBON",GRA$[7]="SCATTER",GRA$[8]="STACK"
33290 GRAF$=GRA$[NUM(LB_GRAP$)]
33300 IF GRAF$="PIE" THEN BB$=VAL1$ ELSE BB$=VAR$
33310 IF CB_3D$="0" THEN EFECT$="2D" ELSE EFECT$="3D"
33320 IF CB_3D$="0" THEN CHART_1.CTL'TITLE2$=ARG_2$,CHART_1.CTL'TITLE1$="" ELSE CHART_1.CTL'TITLE1$=ARG_2$,CHART_1.CTL'TITLE2$=""
33330 AA=EVN("CHART_1.CTL"),FOREGROUND.CTL'TEXTCOLOR$=COLORS$,BACKGROUND.CTL'TEXTCOLOR$=BACKCOLOR$; IF FONT$<>"" THEN REFRESH_FLG=1,AA'FONT$=FONT$,FONTD$="Font:",FONTD.CTL'FONT$=FONT$ ! ,COLORS.CTL'TEXTCOLOR$=COLORS$,COLORS.CTL'BACKCOLOR$=BACKCOLOR$,BACKCOLOR.CTL'TEXTCOLOR$=COLORS$,BACKCOLOR.CTL'BACKCOLOR$=BACKCOLOR$
33340 CHART HIDE AA
33350 IF B_GRAF$="PIE" THEN CHART DELETE AA
33360 ! AA'FMT$=EFECT$+GRAF$,AA'TEXTCOLOR$=COLORS$,AA'BACKCOLOR$=BACKCOLOR$
33370 CHART LOAD CHART_1.CTL,BB$
33380 AA'FMT$=EFECT$+GRAF$,AA'TEXTCOLOR$=COLORS$,AA'BACKCOLOR$=BACKCOLOR$,REFRESH_FLG=1,AA'POINTTEXT$=VAR2$,AA'LEGENDLOCATION$="TOPRIGHT"
33390 IF CB_3D$="0" THEN CHART_1.CTL'TITLE2$=ARG_2$,CHART_1.CTL'TITLE1$="" ELSE CHART_1.CTL'TITLE1$=ARG_2$,CHART_1.CTL'TITLE2$=""
33400 IF FOOTER$<>"" THEN AA'FOOTER$=FOOTER$
33410 ! IF GRAF$="PIE" THEN AA'FONT$="Arial Narrow,-10"
33420 CHART SHOW AA
33430 IF CB_3D$="0" AND ARG_5$<>"" THEN AA'TITLE2$=ARG_2$+"      "+ARG_5$,AA'XAXISTITLE$="",XAXISTITLE$="" ELSE AA'XAXISTITLE$=ARG_5$,XAXISTITLE$=ARG_5$
33440 IF XAXISLOCATION$<>"" THEN AA'XAXISLOCATION$=XAXISLOCATION$
33450 IF XAXISTITLE$<>"" THEN AA'XAXISTITLE$=XAXISTITLE$
33460 B_GRAF$=GRAF$
33470 RETURN 
33480 ! Check for records on GL6
33490 CHECK_GL6:
33500 NUM_REC$=FN%FIN$(_GL6,"NUMREC"); IF NUM(NUM_REC$)=0 THEN RETURN 
33510 MSGBOX MSG("GL6_MSG"),"!Warning","!,OK,CANCEL",OP$
33520 IF OP$="CANCEL" THEN CMD_STR$="END",REFRSH_FLG=1
33530 RETURN 
33540 ASSIGN_PRG:
33550 FOR I=1 TO 13
33560 EXECUTE "AMT_TBL$[I+2]=STR(PERIOD_AMT_"+STR(I)+")"
33570 NEXT I
33580 AMT_TBL$[1]=GL_ACCT$+FY$+OPTION$,AMT_TBL$[2]=STR(ML_BAL_FORW)
33590 RETURN 
33600 CHECK_CHANGES:
33605 GOSUB LOAD_INFO
33610 IF CHG_FLG=0 THEN GOTO *RETURN
33620 MSGBOX MSG("REC_ALTERD"),MSG("UPDATE"),"?,Yesno",_YESNO$
33630 IF _YESNO$="YES" THEN CHG_FLG=0; GOSUB WRITE_REC
33640 CHG_FLG=0,NEXT_ID=BUTTON_3.CTL
33650 RETURN 
33660 DEFAULTS: CB_3D$="1",BACKCOLOR$="Dark Cyan",COLORS$="Light Cyan",GRAF$="RIBBON",LB_GRAP$="6",FONT$="Letter Gothic MT"
33670 RETURN 
34000 EXPORT:
34005 IF DEC(MID(MSE,22,1))>3 THEN WINDX=1,WDX$="[wdx]" ELSE WINDX=0,WDX$=""
34010 ON NUM(EXPORT$) GOSUB *NEXT,EXCEL,WORD
34020 CALL "*wingrp;hide",EXPORT.GRP$
34030 NEXT_ID=BT_EXPORT.CTL
34040 CLOSE (EXCEL,ERR=*NEXT)
34045 CALL "[WDX]*CMDWORD;CLOSE",ERR=*NEXT,OB_EXCEL
34050 RETURN 
34100 EXCEL:
34110 R=0
34115 CALL "[WDX]*CMDWORD;GET_EXCEL",ERR=*NEXT,OB_EXCEL,TF_NAME$; IF TF_NAME$<>"" THEN GOTO 34140
34130 IF WINDX THEN EXECUTE "[wdx]invoke "+QUO+EXCEL$+QUO ELSE INVOKE EXCEL$
34140 WAIT 2
34150 DATES$=""; FOR AA=1 TO %PER_LEN; TMP$=EVS("ml_date"+STR(AA)+"$"),YY=NUM(TMP$(11)),MM=NUM(TMP$(5,2)),DD=NUM(TMP$(8,2)),TMP=JUL(YY,MM,DD),DATES$=DATES$+DTE(TMP:"%Ms-%Yl")+$09$; NEXT AA
34155 TF_VALUE$=""; FOR AA=1 TO 12; TF_VALUE$=TF_VALUE$+STR(EVN("PERIOD_AMT_"+STR(AA)))+$09$; NEXT AA
34160 EXCEL=HFN; IF TF_NAME$="" THEN OPEN (EXCEL)"[dde]excel;" ELSE OPEN (EXCEL)"[DDE]Excel;"+TF_NAME$
34170 R=R+1; IF R>2 THEN GOTO 34220
34180 K$="R"+STR(R)+"C1:R"+STR(R)+"C13"
34200 IF R=2 THEN WRITE RECORD (EXCEL,KEY=K$)TF_VALUE$ ELSE WRITE RECORD (EXCEL,KEY=K$)DATES$
34210 GOTO 34170
34220 X$="R"+STR(R-1)
34225 DIM GRA$[8],VA[8]; GRA$[1]="area",GRA$[2]="bar",GRA$[3]="column",GRA$[4]="line",GRA$[5]="pie",GRA$[6]="line",GRA$[7]="scatter",GRA$[8]="stack"
34230 WRITE RECORD (EXCEL)"[SELECT(""R2C1:"+X$+"C12"","""+X$+"C12"")]"
34235 WRITE RECORD (EXCEL,ERR=*NEXT)"[Zoom(95)]"
34240 WRITE RECORD (EXCEL)"[NEW(2,1)]"
34245 WRITE RECORD (EXCEL,ERR=*NEXT)"[Zoom(50)]"; GOSUB DEFAULTS
34247 IF CB_3D$="1" THEN D3$="3d." ELSE D3$=""
34248 GR$=GRA$[NUM(LB_GRAP$)]; IF GR$="pie" THEN VA$="6" ELSE VA$="5"
34250 WRITE RECORD (EXCEL,ERR=*NEXT)"[gallery."+D3$+GR$+"("+VA$+")]"
34260 WRITE RECORD (EXCEL,ERR=*NEXT)"[Zoom(75)]"
34270 DIM OPT$[7]; OPT$[1]=MSG("SALES"),OPT$[2]=MSG("GROSS_PFT"),OPT$[3]=MSG("NUM_OF_INV"),OPT$[4]=MSG("BOOKINGS"),OPT$[5]=MSG("BK_PROFIT"),OPT$[6]=MSG("ADJ_GP"),OPT$[7]=MSG("GROSS_P"),OP$=OPT$[POS(OPTION$="SPNBDGL")] ! SSP243104 jdf
34280 BB$=""
34290 WRITE RECORD (EXCEL)OP$+" - "+BB$+" "+%CUST_NAME$
34300 RETURN 
35000 WORD:
36000 START_FISCAL_FOCUS:
36010 CALL "*WINGRP;HIDE",FLDS.GRP$; CALL "*WINGRP;HIDE",FLDS2.GRP$; CALL "*WINGRP;DISABLE",BUTTON.GRP$; CALL "*WINGRP;DISABLE",BUTT2.GRP$
36020 FOR AA=1 TO 13; EXECUTE "ML_DATE"+STR(AA)+"$="+QUO+QUO; NEXT AA
36030 REFRESH_FLG=1
36040 RETURN 
37000 NO_PATH:
37080 PROCESS "ZWGPAT","../ZZG/ZZ.EN"
37090 FIND (Z[6],KEY="PATH"+%X3$(40,3),ERR=NO_PATH)EXCEL$,WORDPAD$ ! SSP 188541
37100 RETURN 
37110 HARD_COPY:
37120 PRINT 'DIALOGUE'(20,15,60,8,"Enter Comment"),'CS'
37130 ! CH_PR=0; CALL "*pr.opn",CH_PR,0
37140 PRINT 'TEXT'(55,60,1000,100,"Comment/Title:"); MULTI_LINE 100,@(15,1,40,1); SET_FOCUS 100; OBTAIN *; MULTI_LINE READ 100,TF_COMMENT$
37150 PRINT 'POP',
37160 IF CTL=4 THEN GOTO *RETURN
37170 PRINT 'CURSOR'(6); CH_PR=HFN; OPEN (CH_PR,ERR=END_HARD)"*windev*" ! CH_PR=0; CALL "*pr.opn",CH_PR,0
37180 IF CH_PR=0 THEN GOTO *RETURN
37190 TF_NAME$=STP(X3$(86,40)),TIL$=MSG("ZZGMDA"); DIM BAL_T$[7]; BAL_T$[1]="SALES",BAL_T$[2]="GROSS_PFT",BAL_T$[3]="NUM_OF_INV",BAL_T$[4]="BOOKINGS",BAL_T$[5]="BK_PROFIT",BAL_T$[6]="ADJ_GP",BAL_T$[7]="GROSS_P",OP=POS(BAL_TYPE$="SPNBDGL"),BAL_T$=BAL_T$[OP],BAL_T$=MSG(BAL_T$) ! SSP243104 jdf
37200 PRINT (CH_PR)@(0),"Date/Time:",DTE(0:"%Mz/%Dz/%Yl %hz:%mz %p")
37210 PRINT (CH_PR)@(0)," Terminal:",FID(0)
37220 PRINT (CH_PR)@(0),"  User ID:",%X3$(40,3),'LF','LF','LF','LF' ! SSP 188541
37230 IF TF_COMMENT$<>"" THEN PRINT (CH_PR)@(INT((80-LEN(TF_COMMENT$))/2)),TF_COMMENT$,'LF','LF'
37240 PRINT (CH_PR)"  Program: ZZGMDA",@(INT((80-LEN(TF_NAME$))/2)),TF_NAME$
37250 PRINT (CH_PR)"     Date: ",DTE(0:"%Ml %Dz, %Yl"),@(INT((80-LEN(TIL$))/2)),TIL$,@(66),"Time: ",DTE(0:"%hz:%mz %p")
37260 PRINT (CH_PR)PAD(RAY$,80,"_")
37270 IT$=MSG("SLSMAN")+":"; PRINT (CH_PR)@(22-LEN(IT$)),IT$," ",@(40),%CUST_NAME$
37280 CU$=MSG("FY")+":"; PRINT (CH_PR)@(22-LEN(CU$)),CU$," ",START_FISCAL$,
37290 PRINT (CH_PR)@(40),MSG("BAL_TYPE")+": ",BAL_T$
37300 PRINT (CH_PR)PAD("",80,"_")
37310 BAL_F$=MSG("BAL_FORW")+":"; PRINT (CH_PR)@(55-LEN(BAL_F$)),BAL_F$," ",ML_BAL_FORW:"#,###,###,###.00CR"
37320 PRINT (CH_PR)@(3),MSG("PERIOD_E"),@(30),MSG("CURR_PER"),@(60),MSG("YTD")
37330 PRINT (CH_PR)PAD("",80,"_")
37340 FOR A=1 TO %PER_LEN
37350 EXECUTE "PRINT (CH_PR)@(5),ML_DATE"+STR(A)+"$,@(28),PERIOD_AMT_"+STR(A)+":"+QUO+"#,###,###,###.00CR"+QUO+",@(56),YTD_AMT"+STR(A)+":"+QUO+"#,###,###,###.00CR"+QUO
37360 NEXT A
37370 PRINT (CH_PR)'FF'
37380 CLOSE (CH_PR)
37390 END_HARD:
37400 PRINT 'CURSOR'(0)
37410 RETURN 
56000 ! "195686-problem with setting path in                                
56001 ! "       feature like the budget feature in the General Ledger.
56003 REM "205460-Oracle - FIN(CHANNEL,"NUMREC") changed to use FN%FIN$()     
56005 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
56006 REM "278906-Issue with adding quote on Salesperson Stat Panel, if you   
