0010 ! Definition of Class: XLCondition
0035 REM "5.5 - 09/04/07 - 16.909166 - crg - SSP# 203699
0037 REM "203699-Ability to apply expressions to XML data
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0100 DEF CLASS "ZZGCON" DELETE REQUIRED 
0110 LIKE "ZZGXPR"
0120 ! Local:
0130 LOCAL LEFTCONDEXPR OBJECT 
0140 LOCAL RIGHTCONDEXPR OBJECT 
0150 LOCAL TRUECONDEXPR OBJECT 
0160 LOCAL FALSECONDEXPR OBJECT 
0170 LOCAL COND_EQUALS
0180 ! 
0190 ! Operations:
0200 ! 
0210 FUNCTION EVAL$(INPUT_STR$)
0220 ENTER (INPUT_STR$)
0230 ! PRINT _OBJ," ",OPDESC$
0240 IF SUBEXPR<>0 THEN {
0250 SUBRETVAL$=SUBEXPR'EVAL$(INPUT_STR$)
0260  } ELSE {
0270 SUBRETVAL$=INPUT_STR$
0280  }
0290 IF SIDEEXPR<>0 THEN {
0300 SIDERETVAL$=SIDEEXPR'EVAL$(INPUT_STR$)
0310  }
0320 LEFTCON_VAL$=LEFTCONDEXPR'EVAL$(SUBRETVAL$)
0330 RIGHTCON_VAL$=RIGHTCONDEXPR'EVAL$(SUBRETVAL$)
0340 IF LEFTCON_VAL$=RIGHTCON_VAL$ THEN COND_EVAL=1 ELSE COND_EVAL=0
0350 IF (COND_EVAL AND COND_EQUALS) OR (NOT(COND_EVAL) AND NOT(COND_EQUALS)) THEN {
0360 RETVAL$=TRUECONDEXPR'EVAL$(SUBRETVAL$)
0370  } ELSE {
0380 RETVAL$=FALSECONDEXPR'EVAL$(SUBRETVAL$)
0390  }
0400 EXPRVAL$=RETVAL$+SIDERETVAL$
0410 RETURN EXPRVAL$
0420 ! 
0430 END DEF
0440 ! 
0450 ON_CREATE:
0460 ENTER _LEFTCONDEXPR,_RIGHTCONDEXPR,_TRUECONDEXPR,_FALSECONDEXPR,_COND_EQUALS
0470 OPTYPE=3
0480 OPDESC$="XLCondition"
0490 LEFTCONDEXPR=_LEFTCONDEXPR
0500 RIGHTCONDEXPR=_RIGHTCONDEXPR
0510 TRUECONDEXPR=_TRUECONDEXPR
0520 FALSECONDEXPR=_FALSECONDEXPR
0530 COND_EQUALS=_COND_EQUALS
0540 RETURN 
0550 ! 
0560 ON_DELETE:
0570 IF SUBEXPR<>0 THEN DROP OBJECT SUBEXPR; SUBEXPR=0
0580 IF SIDEEXPR<>0 THEN DROP OBJECT SIDEEXPR; SIDEEXPR=0
0590 IF LEFTCONDEXPR<>0 THEN DROP OBJECT LEFTCONDEXPR; LEFTCONDEXPR=0
0600 IF RIGHTCONDEXPR<>0 THEN DROP OBJECT RIGHTCONDEXPR; RIGHTCONDEXPR=0
0610 IF TRUECONDEXPR<>0 THEN DROP OBJECT TRUECONDEXPR; TRUECONDEXPR=0
0620 IF FALSECONDEXPR<>0 THEN DROP OBJECT FALSECONDEXPR; FALSECONDEXPR=0
0630 RETURN 
9999 END 
56001 REM "203699-Ability to apply expressions to XML data
