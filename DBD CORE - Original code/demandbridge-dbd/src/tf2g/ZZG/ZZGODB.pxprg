0010 ! PxPlus ODBC Interface Parameter Setup <ZZGODB>
0035 REM "5.7 - 08/07/24 - 13.060005 - crg - SSP# 307492
0037 REM "307492-DBD-466:PxPlus ODBC interface utility for DBD use           
0040 REM "Copyright 2024 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 IF TCB(13)=1 THEN BEGIN 
0052 IF NUL(%WDX$) THEN MSGBOX "You must be using Windx to access this panel"; GOTO 0075
0055 IF NOT(%GUI) THEN CHUI_MODE=1; %GUI=1; PRINT 'SHOW'(0)
0060 PROCESS "ZZGODB","../ZZG/ZZ.EN"
0065 IF CHUI_MODE THEN %GUI=0; PRINT 'SHOW'(1); RUN "ZMENU"
0075 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0080 INIT:
0510 _ZZPARM=HFN; OPEN (_ZZPARM)"ZZPARM"
0520 CALL "ZZCOMP",X0$,X1$,X3$,X3$,X4$,M9$,X0,-1,X2
0540 ODB_KEY$=X3$(9,3)+"PXPODBC"
0595 RETURN 
0599 ! *************************************************************
1000 ! ^1000,10 Read in parameters
1010 READ_REC:
1020 DIM ODB_REC$(800)
1030 EXTRACT (_ZZPARM,ERR=NO_READ,KEY=ODB_KEY$,DOM=*NEXT)ODB_REC$(1); GOTO 1050
1040 DIM ODB_REC$(800); ODB_REC$(1)=ODB_KEY$
1050 CMDSTR$=ODB_REC$(11,20)
1051 ODBCDSN$=ODB_REC$(31,40)
1052 ALLOW_EXPORT$=ODB_REC$(71,1)
1080 REFRESH_FLG=1
1090 RETURN 
1100 ! 
1200 ! ^1200,10 Write out parameters
1210 WRITE_REC:
1220 ODB_REC$(11,20)=CMDSTR$
1221 ODB_REC$(31,40)=ODBCDSN$
1222 ODB_REC$(71,1)=ALLOW_EXPORT$
1250 WRITE (_ZZPARM,KEY=ODB_KEY$)ODB_REC$
1260 CMD_STR$="END"
1270 RETURN 
1290 ! 
5000 ! ^5000,10 Check for error on file
5010 NO_READ:
9900 ! ^9900,10 - Wrap up
9910 WRAP_UP:
9920 CLOSE (_ZZPARM)
9930 RETURN 
9999 END 
10000 ! 
10005 EXEC_SQL:
10010 ENTER X3$,X4$,SQL_QUERY$,RESULTS_FILE$,DISPLAY_OUTPUT,ALLOW_QRYMOD,NO_PROGRESS,ERR=*PROCEED
10015 GOSUB GET_ODB_PARAMS
10020 IF NUL(RESULTS_FILE$) THEN {
10025 CALL "[WDX]*Windx.utl;Get_Val","ENV("+QUO+"TEMP"+QUO+")",ENV_TEMP$ ! Get env values we need
10030 RESULTS_FILE$=ENV_TEMP$+"\"+STP(CMDSTR$,3)+"_"+DTE(0:"%Y%Mz%Dz_%Hz%mz%sz_")+SUB(STR(RND),".","")+".txt"
10035  }
10040 IF ALLOW_QRYMOD THEN CALL "ZZGODB;GET_SQL_QUERY",SQL_QUERY$
10045 ZZ2CMD_OPTS$="TNR"; IF NOT(NO_PROGRESS) THEN ZZ2CMD_OPTS$+="P" ! Default is to display file transfer progress bar; allow silent mode upon request
10050 RESP$="",RETURN_CODE=-9999,OLD_GUI=%GUI,%GUI=0
10055 IF %DEBUG_ODBC THEN MSGBOX "SQL Query: "+SQL_QUERY$,"SQL Query","OK,TIM=5,TOP,INFO"
10060 CALL "ZZ2CMD",%X3$,%X4$,"{"+CMDSTR$+"}",ODBCDSN$+SEP+SQL_QUERY$,ZZ2CMD_OPTS$+" {"+"[wdx]"+RESULTS_FILE$+"}",RETURN_CODE,RESP$
10070 %GUI=OLD_GUI
10080 IF DISPLAY_OUTPUT THEN INVOKE "[wdx]excel "+RESULTS_FILE$ ! Display output only if requested
10095 EXIT 
10099 ! 
10100 GET_SQL_QUERY:
10105 ENTER SQL_QUERY$
10120 PROCESS "ZZINPT","../ZZG/ZZ.EN",SQL_QUERY$,MSG("ZZGODB_003"),MSG("ZZGODB_004"),MSG("ZZGODB_006"),MSG("ZZGODB_005"),""
10195 EXIT 
10199 ! 
11000 GET_ODB_PARAMS:
11010 OPEN (HFN)"ZZPARM"; _ZZPARM=LFO
11020 CMDSTR$=""; FIND (_ZZPARM,KEY=%C$+"PXPODBC",DOM=*NEXT)ODB_REC$; CMDSTR$=STP(MID(ODB_REC$,11,20)); ODBCDSN$=STP(MID(ODB_REC$,31,40))
11090 CLOSE (_ZZPARM)
11095 RETURN 
11099 ! 
11100 TEST_SETTINGS:
11110 SQL$=MSG("ZZGODB_007")
11120 CALL "ZZGODB;EXEC_SQL",%X3$,%X4$,SQL$,RESULTS_FILE$,1,1
11195 RETURN 
11199 ! 
