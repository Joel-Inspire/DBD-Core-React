0010 ! ZZGPRIN - Select graphical output device
0015 ! ^15,5 - Call enter string
0020 SETERR 0030
0025 ENTER X0$,X3$,Y0$,V3$,H{ALL},H0$,H1$,H0,H9,H2,H3,H4
0030 SETERR 0000
0035 ENTER_IOL:IOLIST H0$,H0,H9,H2,H3,H4
0040 ARG_1$=REC(IOL=ENTER_IOL)
0050 ! ^50,5 - process the panel
0056 PROCESS "ZZGPRIN","tf2g/ZZG/ZZ.EN",ARG_1$
0060 READ DATA FROM ARG_1$ TO IOL=ENTER_IOL
0061 V3$=PAD(V3$,8)
0065 EXIT 
0070 ! 
0100 INIT:! ^100,5 - Initialization routine
0105 READ DATA FROM ARG_1$ TO IOL=ENTER_IOL
0110 WINPRT_SETUP READ DEF_PRINTER$
0115 REFRESH_FLG=1
0120 ! 
0200 POST_DISPLAY:! ^100,5 - Determine if screen button should be displayed
0205 IF NOT(POS("H"=H0$)) THEN ENABLE CONTROL SCREEN.CTL
0210 REFRESH_FLG=1
0215 EXIT 
0220 ! 
0300 BROWSE_PRINTER:! ^100,5 - Display available printers
0305 WINPRT_SETUP INPUT QRY_VAL$
0310 EXIT 
0315 ! 
1000 GO2SCREEN:! ^1000,5 - Open *viewer*
1005 ! First, determine point size for potential print to printer
1010 WINPRT_SETUP WRITE PROPERTIES "ORIENTATION=PORTRAIT"
1015 H9=HFN; OPEN (H9)%WDX$+"*winprt*;asis"
1020 GOSUB GET_POINT_SIZE
1025 PRINT (H9)'AB', ! Abort the print job
1030 CLOSE (H9)
1035 OPEN (H9,OPT="ONCLOSE")"*viewer*"
1040 PRINT (H9)'FONT'("Courier New",POINT_SIZE,"B"),'DF',
1045 PRINT (H9)DIM(H3)
1046 H0=58 ! Force number of lines to 58 for Form Feed
1050 CMD_STR$="E"
1055 EXIT 
1060 ! 
1100 GO2PRINTER:! ^100,5 - Open *winprt*
1105 H9=HFN; OPEN (H9)%WDX$+"*winprt*;asis"
1106 GOSUB GET_POINT_SIZE
1107 CMD_STR$="E"
1108 EXIT 
1110 ! 
2000 GET_POINT_SIZE:! ^1000,5
2005 POINT_SIZE=-10
2010 LOOP:PRINT (H9)'FONT'("Courier New",POINT_SIZE,"B"),'DF',
2015 IF MXC(H9)<H3 THEN POINT_SIZE+=1; GOTO LOOP
2020 H0=MXL(H9)-3,H2=0
2025 RETURN 
2030 ! 
9700 WRAPUP:! 9700,5 - Transfer data back to arg_1$
9705 ARG_1$=REC(IOL=ENTER_IOL)
9710 EXIT 
