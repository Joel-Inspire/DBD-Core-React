0001 ! EXCELCE - OUTPUT TO EXCEL - @ FILTER"
0002 REM 0
0004 ! Computerease Limited, Ed Jack
0005 SETERR EXIT; SETESC EXIT
0010 ! Data will be parsed into columns if reports contains @ positioning.
0030 ! Name of File/Device: /tmp (Directory name on Server)
0040 ! Type of link: FILE
0050 ! select driver: excelce (the name of this device driver)
0060 ! See line 1058 for name of Excel file on local PC
0070 ! See line 1080 for location of Excel program
0080 ! GET_FILE_BOX WRITE %EXCEL_FILE$,"c:\tmp","Excel output file","Excel Files (*.xls *.txt)|*.xls;*.txt,All files|*.*,","txt"
0090 A$=PTH(LFO); F=LFO
0100 CLOSE (F); OPEN (F,ERR=*NEXT)A$; CLOSE (F); GOTO 0110
0105 PRINT @(0,22),'CL','RB',"Unable to access Directory: ",F$," <Enter> ",; INPUT *; GOTO EXIT
0110 F$=A$+DLM+"VIEW"+FID(0)+".TXT"
0120 ERASE F$,ERR=*NEXT
0130 SERIAL F$,ERR=*NEXT; GOTO 0140
0135 PRINT @(0,22),'CL','RB',"Unable to create file: ",F$," <Enter> ",; INPUT *; GOTO EXIT
0140 OPEN (F)F$; LOCK (F,ERR=*NEXT); GOTO 0150
0145 PRINT @(0,22),'CL','RB',"Unable to LOCK File: ",F$," <Enter> ",; INPUT *; GOTO EXIT
0150 REM "Define Mnemonics
0160 ! DEFPRT (F)132,60
0170 ! MNEMONIC (F)'*C'=$0D0A$
0180 MNEMONIC (F)'FF'=$0C$ ! <formfeed>
0190 ! MNEMONIC (F)'CR'=$0D$ ! <cr>
0200 ! MNEMONIC (F)'LF'=$0D0A$ ! <cr><lf>
0210 PRINT (F)'BO',
0220 MNEMONIC (F)'*X'="*dev/excelce;COPY_FILE;"+F$
0300 EXIT:
0310 EXIT 
1000 ! 
1010 COPY_FILE:
1018 OS$=SYS; IF POS("WIN"=OS$)<>0 THEN OS$="WINDOWS"
1020 WINDX$=MSE; IF WINDX$(22,1)>$00$ AND WINDX$(22,1)<$FF$ THEN WINDX$="[wdx]" ELSE WINDX$=""
1025 ! PRINT 'WINDOW'(10,10,60,3,""),"Copying file to local disk. Please wait.",; WAIT 1
1030 SOURCE_FILE$=PTH(LFA),PAGE_BREAK$="------------PAGE BREAK------------"
1040 TEXT=UNT; OPEN (TEXT,ERR=*NEXT)SOURCE_FILE$; GOTO 1046
1045 PRINT 'POP',@(0,22),'CL','RB',"Unable to access file: ",SOURCE_FILE$," <Enter> ",; INPUT *; GOTO EXIT_ERR
1046 IF %EXCEL_FILE$<>"" THEN EXCEL_FILE$=%EXCEL_FILE$,%EXCEL_FILE$=""; GOTO 1060
1050 LOCAL_PATH$=ENV("temp"); IF LOCAL_PATH$="" THEN LOCAL_PATH$="C:\TMP"
1051 EXCEL=UNT; OPEN (EXCEL,ERR=1052)WINDX$+LOCAL_PATH$; GOTO 1055
1052 DIRECTORY WINDX$+LOCAL_PATH$,ERR=*NEXT; GOTO 1055
1054 PRINT 'POP',@(0,22),'CL','RB',"Unable to access local directory: ",LOCAL_PATH$," <Enter> ",; INPUT *; GOTO EXIT_ERR
1058 EXCEL_FILE$=LOCAL_PATH$+"\report.txt"
1060 ERASE WINDX$+EXCEL_FILE$,ERR=*NEXT
1070 SERIAL WINDX$+EXCEL_FILE$,ERR=*NEXT; GOTO 1080
1075 PRINT 'POP',@(0,22),'CL','RB',"Unable to create file: ",EXCEL_FILE$," <Enter> ",; INPUT *; GOTO EXIT_ERR
1080 CALL WINDX$+"*win/registry",ERR=*NEXT,"HKEY_LOCAL_MACHINE","SOFTWARE\MICROSOFT\WINDOWS\CURRENTVERSION\APP PATHS\EXCEL.EXE","",EXCEL_LOCATION$,0
1082 IF EXCEL_LOCATION$="" THEN EXCEL_LOCATION$="C:\PROGRA~1\MICROS~1\OFFICE\EXCEL.EXE"
1083 EXCEL=UNT; OPEN (EXCEL,ERR=*NEXT)WINDX$+EXCEL_LOCATION$; GOTO 1085
1084 PRINT 'POP',@(0,22),'CL','RB',"Unable to Locate Excel Program in: ",EXCEL_LOCATION$," <Enter> ",; INPUT *; GOTO EXIT_ERR
1087 CLOSE (EXCEL); OPEN (EXCEL)WINDX$+EXCEL_FILE$; LOCK (EXCEL)
1090 READ (TEXT,END=EXIT_COPY)VALUE$; VALUE$=HTA(VALUE$)
1095 IF VALUE$="0C" THEN VALUE$=PAGE_BREAK$; GOTO 1170
1096 IF LEN(VALUE$)>1 AND VALUE$(1,2)="0C" THEN PRINT (EXCEL)PAGE_BREAK$; VALUE$=VALUE$(3)
1100 P=POS("1B"=VALUE$); IF P=0 THEN GOTO 1155
1110 IF VALUE$="1B4350" OR VALUE$="1B4646" THEN VALUE$=""; GOTO 1090
1120 IF LEN(VALUE$)<9 THEN GOTO 1155
1130 IF P=1 THEN VALUE$=VALUE$(P+8); GOTO 1100
1140 IF LEN(VALUE$(P))<9 THEN VALUE$=VALUE$(1,P-1); GOTO 1155
1150 VALUE$=VALUE$(1,P-1)+"09"+VALUE$(P+8); GOTO 1100
1160 VALUE$=ATH(VALUE$)
1170 PRINT (EXCEL)VALUE$; GOTO 1090
1200 EXIT_COPY:
1210 MNEMONIC (EXCEL)'*R'=WINDX$+EXCEL_LOCATION$+" "+EXCEL_FILE$
1220 CLOSE (EXCEL); CLOSE (TEXT)
1230 PRINT (0,ERR=1231)'POP',
1240 ERASE SOURCE_FILE$,ERR=1241
1270 EXIT 
7000 EXIT_ERR:
7010 PRINT (0,ERR=7011)'POP',
7020 IF EXCEL>0 THEN CLOSE (EXCEL)
7030 IF TEXT>0 THEN CLOSE (TEXT)
7040 EXIT 
9999 END 
