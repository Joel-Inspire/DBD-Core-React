0010 ! ZYGCAL - Memo and Special Date Setup
0020 ! "6.0 - 06/25/03 - 11.626666 - jir
0030 ! "Copyright 2003 Computer Software Inc.; Norcross, Georgia
0040 ! "        Licensed Software - All Rights Reserved.
0100 ! 100,10
0110 INIT:
0112 IF %GRID THEN SET_GRID=1,%GRID=0
0120 _ZZY5=HFN; OPEN (_ZZY5,IOL=*,REC=_ZY5$)"ZY5"
0130 _ZZYM=HFN; OPEN (_ZZYM,IOL=*,REC=_ZYM$)"ZYM"
0140 XX$=DTE(*); DIM WK_LONG$[7]
0145 TD$=DTE(0:"%Yl%Mz%Dz"),TODAY=JUL(NUM(TD$(1,4)),NUM(TD$(5,2)),NUM(TD$(7))),ST=JUL(NUM(TD$(1,4)),1,1),LL=JUL(NUM(TD$(1,4)),12,31),HH=TODAY-ST+1,EN=LL-TODAY
0150 X=POS(","=XX$,1,12); IF X<>0 THEN MTHS_LONG$=XX$(1,X),XX$=XX$(X+1)
0160 P=POS(","=XX$,1,7); IF P<>0 THEN WEEK_DYS$=XX$(1,P-1)
0170 Q=POS(","=WEEK_DYS$,-1),WKLONG$=WEEK_DYS$+","
0180 IF Q<>0 THEN WEEK_DYS$=","+WEEK_DYS$(Q+1)+","+WEEK_DYS$(1,Q)
0190 WEEK_DYS$=STP(WEEK_DYS$,1,",")
0200 L=POS(","=WKLONG$); IF L<>0 THEN WKL=WKL+1,WK_LONG$[WKL]=WKLONG$(1,L-1),WKLONG$=WKLONG$(L+1); GOTO *SAME
0210 L=POS(","=WEEK_DYS$); IF L<>0 THEN WK_DYS$=WK_DYS$+WEEK_DYS$(L+1,3),WEEK_DYS$=WEEK_DYS$(L+2); GOTO *SAME
0220 IF LEN(ARG_1$)=8 THEN GOTO PARSE_IT
0230 FORCE_IT: ARG_1$=DTE(0:"%Yl%Mz%Dz")
0240 PARSE_IT:SETERR FORCE_IT
0250 READ DATA FROM ARG_1$ TO Y:[NUM(4)],M:[NUM(2)],D:[NUM(2)]
0260 X=JUL(Y,M,D,ERR=FORCE_IT)
0270 MONTH_ID$=MID("123456789ABC",M,1)
0280 YEAR_ID$=STR(Y:"0000")
0290 DAY_ID$=STR(D)
0300 SETERR 0000
0310 SETERR 0000
0320 ! Position window
0330 MSE$=MSE
0340 X=DEC(MID(MSE$,20,2)); IF X=0 THEN GOTO 0380
0350 X$=OBJ(X,ERR=*RETURN); IF X$="" THEN GOTO *RETURN
0360 CTRL_X=DEC(X$(21,2)),CTRL_W=DEC(X$(25,2)),CTRL_H=DEC(X$(27,2)),CTRL_Y=DEC(X$(23,2))+CTRL_H ! Get bottom
0370 _OBJ_C=-100 ! Draw window off screen
0380 ! _OBJ_STS$+="h"
0390 RETURN 
0400 ! ^100 - Move window
0410 POST_INIT:
0420 PRINT 'FILL'(1,15),'PEN'(0,0,0),'RECTANGLE'(@X(2),@Y(3),@X(27.5),@Y(13.75)),'FRAME'(@X(2),@Y(3),@X(27.5),@Y(13.75),0),
0430 IF CTRL_Y=0 THEN GOTO 0490
0440 X$=OBJ(0),MSE$=MSE,WNDW_W=DEC(X$(25,2)),WNDW_H=DEC(X$(27,2))
0450 TOT_W=DEC($00$+MID(MSE$,27,2)),TOT_H=DEC($00$+MID(MSE$,29,2))
0460 IF CTRL_X+WNDW_W>TOT_W THEN CTRL_X=CTRL_X-WNDW_W+CTRL_W
0470 IF CTRL_Y+WNDW_H>TOT_H THEN CTRL_Y=CTRL_Y-WNDW_H-CTRL_H
0480 ! IF DEC(MID(MSE$,22,1))>0 THEN CALL "[WDX]*win/calendar;call_dll",CTRL_X,CTRL_Y ELSE GOSUB DO_DLL
0490 GOSUB DRAW_MONTH; RETURN 
0500 ! ^100 - Draw month
0510 DRAW_MONTH:
0520 IF FPT(NUM(YEAR_ID$)/4)=0 THEN LEAP_YEAR.CTL'VISIBLE=1 ELSE LEAP_YEAR.CTL'VISIBLE=0
0530 SV_BY=PRM('BY')
0540 SET_PARAM 'BY'=1797
0545 GOSUB CHECK_HOLIDAY
0550 Y=NUM(YEAR_ID$),M=POS(MONTH_ID$="123456789ABC")
0560 CUR_L=3.5
0570 IF JUL(Y,M,1)|7<5 THEN CUR_L+=.75
0580 PRINT 'SCROLL'(2,4,25,6*1.5),'DO','SR',
0590 MAX_DY=0
0600 FOR N=1 TO 31
0610 COL=(JUL(Y,M,N,ERR=*BREAK))|7
0612 IF POS(STR(Y:"0000")+STR(M:"00")+STR(N:"00")=FULL_MOON$,8)<>0 THEN FULL$=CHR(186) ELSE FULL$=""
0615 IF (M=6 AND N=14) OR (M=7 AND N=4) OR (M=9 AND N=11) THEN RADIO_BUTTON DAY_ID.CTL:N,@(COL*3.5+2.5,CUR_L+1,3.5,1.5)="{!US_FLAG}"+STR(N)+FULL$,MSG=MSG("PRESS_S")+" "+MSG("DAY")+" "+STR(N); GOTO 0621
0618 IF FULL$<>"" THEN RADIO_BUTTON DAY_ID.CTL:N,@(COL*3.5+2.5,CUR_L+1,3.5,1.5)="{}"+STR(N)+FULL$,MSG=MSG("PRESS_S")+" "+MSG("DAY")+" "+STR(N),FNT="MS Sans Serif,-11,B"; GOTO 0621
0620 RADIO_BUTTON DAY_ID.CTL:N,@(COL*3.5+2.5,CUR_L+1,3.5,1.5)="{}"+STR(N)+FULL$,MSG=MSG("PRESS_S")+" "+MSG("DAY")+" "+STR(N)
0621 ! RADIO_BUTTON DAY_ID.CTL:N,@(COL*3.5+2.5,CUR_L+1,3.5,1.5)="{}"+STR(N),TIP=MSG("PRESS_S")+" "+MSG("DAY")+" "+STR(N),MSG=MSG("PRESS_S")+" "+MSG("DAY")+" "+STR(N)
0625 IF FULL$<>"" THEN DAY_ID.CTL'ID=N,DAY_ID.CTL'TIP$="Full Moon"
0627 IF PFM$=STR(Y:"0000")+STR(M:"00")+STR(N:"00") THEN DAY_ID.CTL'ID=N,DAY_ID.CTL'TIP$=MSG("PFM")
0628 %B_WINDATE_FMT$=%WINDATE_FMT$,%WINDATE_FMT$="YYYYMMDD"
0630 JJ$=STR(Y:"0000")+STR(M:"00")+STR(N:"00"); CALL "ZZWDTE;VALIDATE",JJ$,JJJ$
0640 READ (_ZZYM,KEY=PAD(WHO,3," ")+JJ$,DOM=0660)
0650 DAY_ID.CTL'ID=N,DAY_ID.CTL'BACKCOLOUR$="Light Cyan"
0660 READ (_ZZY5,KEY=JJ$,DOM=0700)
0670 IF _ZY5.HIGHLIGHT$="1" THEN DAY_ID.CTL'ID=N,DAY_ID.CTL'BACKCOLOUR$="Light Red"
0680 IF _ZY5.BANK_HOLIDAY$="1" THEN DAY_ID.CTL'ID=N,DAY_ID.CTL'BACKCOLOUR$="Light Blue",DAY_ID.CTL'TEXTCOLOR$="White"
0690 IF _ZY5.NORMALLY_WORK_OF$="1" THEN DAY_ID.CTL'ID=N,DAY_ID.CTL'BACKCOLOUR$="White"
0696 IF _ZY5.NORMALLY_WORK_OF$="3" THEN DAY_ID.CTL'ID=N,DAY_ID.CTL'BACKCOLOUR$="Light Magenta"; IF JJ$(3,4)="0317" THEN DAY_ID.CTL'BACKCOLOUR$="Green"; IF JJ$(3,4)="0317" THEN DAY_ID.CTL'BACKCOLOUR$="Light Green"
0697 IF _ZY5.NORMALLY_WORK_OF$="2" THEN DAY_ID.CTL'ID=N,DAY_ID.CTL'BACKCOLOUR$="Light Yellow"
0698 BEFORE$="",BEFORE$=DAY_ID.CTL'TIP$; IF STP(BEFORE$)<>"" THEN BEFORE$=BEFORE$+" / "
0699 IF STP(_ZY5.TF_DESCRIPTION$)<>"" THEN DAY_ID.CTL'TIP$=BEFORE$+STP(_ZY5.TF_DESCRIPTION$)
0700 MAX_DY=N
0705 %WINDATE_FMT$=%B_WINDATE_FMT$
0710 IF COL=6 THEN CUR_L+=1.5
0720 NEXT 
0730 SET_PARAM 'BY'=SV_BY
0740 D=NUM(DAY_ID$)
0750 IF D>MAX_DY THEN D=MAX_DY
0760 DAY_ID$=STR(D)
0770 REFRESH_FLG=1
0780 ARG_1$=YEAR_ID$+STR(M:"00")+STR(D:"00")
0790 REFRESH_FLG=1,CHANGE_FLG=0; IF FOLDER_ID$<>"" THEN PERFORM "../ZZG/"+FOLDER_ID$+";MAIN_POST_DISPLAY",ERR=*NEXT
0800 RETURN 
0900 ! ^100 - Check EOM terminator
0910 SET_DATE:
0920 ARG_1$=YEAR_ID$+STR(POS(MONTH_ID$="123456789ABC"):"00")+STR(NUM(DAY_ID$):"00")
0930 YY=NUM(YEAR_ID$),MM=NUM(ARG_1$(5,2)),DD=NUM(DAY_ID$),RESUL=JUL(YY,MM,DD)
0935 IF DD=4 AND MM=7 THEN _SOUND_NAME$="us_anthem.mid"; PERFORM "$CTL-10;Sound_play"; WAIT 2
0940 DAY_ID.CTL'ID=NUM(DAY_ID$); IF (DAY_ID.CTL'BACKCOLOUR$="GRAY" OR DAY_ID.CTL'BACKCOLOUR$="DEFAULT") THEN GOTO MAIN_POST
0950 DAY_ID.CTL'ID=NUM(DAY_ID$); IF (DAY_ID.CTL'BACKCOLOUR$="LIGHT CYAN" AND FOLDER_ID$<>"ZZGCAL") THEN NEXT_FOLDER=FLDR.ZZGCAL.CTL
0960 DAY_ID.CTL'ID=NUM(DAY_ID$); IF (DAY_ID.CTL'BACKCOLOUR$<>"LIGHT CYAN" AND FOLDER_ID$<>"ZYGDAA") THEN NEXT_FOLDER=FLDR.ZYGDAA.CTL
0970 MAIN_POST:
0980 REFRESH_FLG=1,CHANGE_FLG=0; PERFORM "../ZZG/"+FOLDER_ID$+";MAIN_POST_DISPLAY",ERR=*NEXT
0990 RETURN 
1000 ! ^1000 - Call the DLL
1010 CALL_DLL:
1020 ENTER CTRL_X,CTRL_Y
1030 GOSUB DO_DLL
1040 EXIT 
1100 ! ^100
1110 DO_DLL:
1120 X$=OBJ(0),WNDW_W=DEC(X$(25,2)),WNDW_H=DEC(X$(27,2))
1130 IF POS("PVXWIN32"=UCS(ARG(0))) THEN L$="USER32.dll" ELSE L$="USER.EXE"
1140 A=DLL(L$,"MoveWindow",INT(DEC(X$(17,4))),INT(CTRL_X),INT(CTRL_Y),INT(WNDW_W),INT(WNDW_H),INT(1))
1150 RETURN 
1160 CLOSE_FILES:
1170 IF _ZZY5<>0 THEN CLOSE (_ZZY5); _ZZY5=0
1180 IF _ZZYM<>0 THEN CLOSE (_ZZYM); _ZZYM=0
1185 IF SET_GRID THEN %GRID=1
1190 RETURN 
2000 CHECK_HOLIDAY:
2004 %B_WINDATE_FMT$=%WINDATE_FMT$,%WINDATE_FMT$="YYYYMMDD"
2005 PERFORM "../ZZG/ZYGDAB;Paschal_full_moon"
2008 FULL_MOON$=""; FOR AA=1 TO 13; FULL_MOON$=FULL_MOON$+DE$[AA]; NEXT AA
2010 NOPRINT=1,JJ$=YEAR_ID$+"0101",B_WINDATE_FMT$=%WINDATE_FMT$,%WINDATE_FMT$="YYYYMMDD"; CALL "ZZWDTE;VALIDATE",JJ$,JJJ$; %WINDATE_FMT$=B_WINDATE_FMT$
2020 READ (_ZZY5,KEY=JJ$,DOM=*NEXT); GOTO END_HOLIDAYS
2030 PERFORM "../ZZG/ZYGDAB;INIT"
2040 PERFORM "../ZZG/ZYGDAB;WRITE_DATES"
2050 NOPRINT=0
2055 END_HOLIDAYS:
2058 %WINDATE_FMT$=%B_WINDATE_FMT$
2060 RETURN 
3000 SELECT_HS:
3010 IF HS=0 THEN HS=NUM(MONTH_ID$),HS.CTL'VALUE$=MONTH_ID$
3020 H_SCROLLBAR READ HS.CTL,CUR,12,RC,AR
3040 MONTH_ID$=STR(CUR)
3050 GOSUB DRAW_MONTH
3060 REFRESH_FLG=1
3070 RETURN 
4000 FULL_MOON:
4010 PRINT 'PEN'(1,1,0),'FILL'(1,0),'CIRCLE'(@X(COL*3.5+2.5),@Y(CUR_L+1),@X(1),1)
4020 RETURN 
5000 CHECK_HSB:
5500 SEL_HSB:
5501 H_SCROLLBAR READ HSB.CTL,VAL,13,1,1
5502 IF VAL=13 THEN YEAR_ID$=STR(NUM(YEAR_ID$)+1:"0000"),VAL=1
5503 IF VAL=1 AND B_VAL=1 THEN YEAR_ID$=STR(NUM(YEAR_ID$)-1:"0000"),VAL=12
5504 IF VAL=1 THEN B_VAL=1 ELSE B_VAL=0
5510 VAL$="123456789ABC",MONTH_ID$=VAL$(VAL,1)
5540 GOSUB DRAW_MONTH
5545 H_SCROLLBAR WRITE HSB.CTL,VAL,13
5550 RETURN 
