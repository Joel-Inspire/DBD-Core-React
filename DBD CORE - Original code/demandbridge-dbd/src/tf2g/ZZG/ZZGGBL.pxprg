0010 ! Set GUI Global Variables <ZZGGBL>
0035 REM "5.7 - 08/18/24 - 15.092825 - jvv - SSP# 307491
0037 REM "307491-Generic File Transfer utility                               
0040 REM "Copyright 2024 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! PERFORMED from START_UP
0130 ! 
0150 SET_GLOBALS:
0151 GOSUB CONTROL_KEY
0152 IF TCB(88)=0 THEN %WDX$="" ELSE %WDX$="[wdx]"
0153 IF X3$="" THEN X3$=%X3$
0154 IF X3$="" THEN Z=HFN; OPEN (Z,ERR=*NEXT)"ZZPARM"; READ (Z,KEY=FID(0)+DIM(6),DOM=*PROCEED)X3$,*,X4$; CLOSE (Z)
0155 IF X4$="" THEN X4$="500ZZPG/L0Z3Z2Z2  ZIP A5S4    A/R0Z2A5    AR5 S2S4S4  ARG0Z3      AR6 Z7S1    A/P0z2A6    AP20A2      SSN0Z3Z2Z4  SER0Z1Z8    P/O0A2A6S1  I/C aASA    ARV0Z3      ICE SA      X/X0Z1Z1Z1Z1O/P0A2A6    T/M0z0A6    FMC A4S5    S/S0z2Z6Z2  FMA A4a5    FMB a4S5    C/E0A5Z2Z2  GLN0Z3Z2Z2  "; Z=HFN; OPEN (Z,ERR=*NEXT)"ZZPARM"; READ (Z,KEY=FID(0)+DIM(6),DOM=*PROCEED)X3$,*,X4$; CLOSE (Z)
0160 IF X4$="" THEN EXIT 
0190 ! In the following sections: FN_GET_MASK_DEF returns a 9 char string of the form 122334455 where 1 is fill character (ignored for our purposes) 22 33 44 55 are the segments, 1st char if upper case then visible segment, if lower case then invisible segment, 2nd char is length, 0-9, A=10
0200 ! ^200,5 A/R Section
0215 %CUST_MASK$=FN_GET_MASK$("A/R","%AR_DIV_LEN","%CUST_LEN","","")
0225 %CUST_CAT_MASK$=FN_GET_MASK$("ARG","%CUST_CAT_LEN","","","")
0235 %SALES_DIST_MASK$=FN_GET_MASK$("ARV","%SALES_DIST_LEN","","","")
0240 %AR_INV_MASK$=FN_GET_MASK$("AR6","%AR6_LEN","%AR6_SUB_LEN","","") ! 
0300 ! ^300,5 A/P Section
0310 %VEND_MASK$=FN_GET_MASK$("A/P","%AP_DIV_LEN","%VEND_LEN","","")
0325 %VEND_CAT_MASK$=FN_GET_MASK$("AP2","%VEND_CAT_LEN","","","")
0390 ! 
0400 ! ^400,5 O/P Section
0420 %ORD_MASK$=FN_GET_MASK$("O/P","%OP_DIV_LEN","%ORDER_LEN","","")
0425 ! 
0500 ! ^500,5 G/L Section
0510 %GLACCT_MASK$=FN_GET_MASK$("G/L","%GL_ACCT_LEN","%GL_SUBACCT_LEN","%GL_DEPT_LEN","")
0535 ! 
0600 ! ^600,5 P/O Section
0620 %PORDER_MASK$=FN_GET_MASK$("P/O","%PO_DIV_LEN","%PO_LEN","%PO_CODE_LEN","")
0625 ! 
0700 ! ^700,5 F/M Section
0705 %COST_CTR_MASK$=FN_GET_MASK$("FMA","%FMA_LEN","","","")
0710 %CUST_DEPT_MASK$=FN_GET_MASK$("FMB","","%FMB_LEN","","")
0715 %COST_CODE_MASK$=FN_GET_MASK$("FMC","","","","")
0810 %ZIP_MASK$=FN_GET_MASK$("ZIP","","","","") ! CALL "ZZWLKU;ZIP_GLOBAL",ERR=*NEXT
0820 %SALES_TAX_MASK$=FN_GET_MASK$("AR5","%SALES_TAX_LEN","","","")
0900 ! Get length of Item code in %ITEM_CODE_LEN
0905 %ITEM_CODE_LEN=10
0910 ZTMP=HFN; OPEN (ZTMP,ERR=ICL_END)"ZZPARM"
0915 FIND (ZTMP,KEY=MID(X3$,9,3)+"I/C",DOM=ICL_END)ZTMP$
0920 %ITEM_CODE_LEN=MIN(MAX(NUM(MID(ZTMP$,13,2),ERR=ICL_END),5),10) ! Minimum 5 wide, maximum 10
0925 ICL_END:
1000 ! ^1000,5 Phone number length/mask
1005 %PHONE_LEN=ASC(X3$(310,1))-100,%FAX_LEN=%PHONE_LEN
1010 IF X3$(311,1)>CHR(100) THEN %FAX_LEN=10
1015 DIM X$(%PHONE_LEN,"9")
1020 CALL "ZZDISP","PX",X$,"   ",X3$,%PHONE_MASK$,"",0,0,X4$
1025 DIM X$(%FAX_LEN,"9")
1030 CALL "ZZDISP","POX",X$,"   ",X3$,%FAX_MASK$,"",0,0,X4$
1035 IF POS("Ext:"=%PHONE_MASK$)>0 THEN %PHONE_MASK$=STP(%PHONE_MASK$,3),%PHONE_MASK$=SUB(%PHONE_MASK$,"Ext:",".")
1040 IF %FAX_LEN=10 THEN %FAX_MASK$=STP(%FAX_MASK$,3)
1045 %PHONE_MASK$=SUB(%PHONE_MASK$,"9","X")
1050 %FAX_MASK$=SUB(%FAX_MASK$,"9","X")
1055 ! 
1100 ! SSP268441, check for WMS module
1105 %HAS_WMS=0; IF (FN%HAS_MODULE("WW")) THEN %HAS_WMS=1
9900 DONE:
9910 %GLOBALS_DONE=1
9999 END 
10000 DEF FN_GET_MASK_DEF$(LOCAL S$)=PAD(TBL(SGN(POS(S$=X4$(7),12)),DIM(9),MID(X4$,POS(S$=X4$(7),12)+9,9)),9,1)
10005 ! Given S$ return the definition in form ABBCCDDEE where A is fill character and BB CC DD EE are (1,1)=type (2,1)=length. if not in list return 9 blanks, if S$ is not three chars long then unpredictable things may occur; makes sure the answer is 9 long
10049 ! 
10050 DEF FN_LEN_OF_SEG(LOCAL L, LOCAL D$) ! Find the visible length of segment L from Account type mask definition D$
10055 LOCAL VISIBLE_LENGTH
10060 VISIBLE_LENGTH=0
10065 IF L<1 OR L>4 THEN GOTO *RETURN
10070 IF D$(L*2,1)>="a" THEN VISIBLE_LENGTH=0 ELSE VISIBLE_LENGTH=NUM(D$((L*2)+1,1),ERR=*NEXT)
10090 RETURN VISIBLE_LENGTH
10095 END DEF
10099 ! 
10100 DEF FN_GET_MASK$(LOCAL S$,SEG1$,SEG2$,SEG3$,SEG4$) ! Given Account type return the mask for it and return the length of each segment
10105 LOCAL MASK$,MDEF$
10110 MASK$=""; VIA SEG1$=0,ERR=*PROCEED; VIA SEG2$=0,ERR=*PROCEED; VIA SEG3$=0,ERR=*PROCEED; VIA SEG4$=0,ERR=*PROCEED
10115 CALL "ZZDISP","AX",DIM(100,"X"),S$,X3$,MASK$,"",0,0,X4$
10117 MASK$="~"+S$ ! Changes to *winproc, etc now, need only this part of mask
10120 MDEF$=FN_GET_MASK_DEF$(S$)
10125 VIA SEG1$=FN_LEN_OF_SEG(1,MDEF$),ERR=*PROCEED; VIA SEG2$=FN_LEN_OF_SEG(2,MDEF$),ERR=*PROCEED; VIA SEG3$=FN_LEN_OF_SEG(3,MDEF$),ERR=*PROCEED; VIA SEG4$=FN_LEN_OF_SEG(4,MDEF$),ERR=*PROCEED
10140 GM_DONE:RETURN MASK$
10145 END DEF
10149 ! 
13000 ! ^13000,10 G/L Section
13005 GL_FMT:
13006 %GLACCT_MASK$=FN_GET_MASK$("G/L","%GL_ACCT_LEN","%GL_SUBACCT_LEN","%GL_DEPT_LEN","")
13008 IF %GL_FMT$="" THEN GLTMP$=FN_GET_MASK_DEF$("G/L"); %GL_FMT$=GLTMP$(3)
13010 GL_DEF$=%GL_FMT$ ! (1,1)=Acct len, (2,1)=Zero (3,1)=Subacct len, (5,1)=Department len
13020 %GL_ACCT_LEN=NUM(GL_DEF$(1,1))
13030 %GL_SUBACCT_LEN=0; IF LEN(GL_DEF$)>2 THEN %GL_SUBACCT_LEN=NUM(GL_DEF$(3,1))
13040 %GL_DEPT_LEN=0; IF LEN(GL_DEF$)>4 THEN %GL_DEPT_LEN=NUM(GL_DEF$(5,1))
13045 %GL_ADD=NUM(MID(GL_DEF$,7,1))
13050 %GLACCT_MASK$=DIM(%GL_ACCT_LEN,"0"); IF %GL_SUBACCT_LEN THEN %GLACCT_MASK$+="-"+DIM(%GL_SUBACCT_LEN,"0")
13060 IF %GL_DEPT_LEN THEN %GLACCT_MASK$+="-"+DIM(%GL_DEPT_LEN,"0")
13062 IF %GL_ADD THEN %GLACCT_MASK$+="-"+DIM(%GL_ADD,"0")
13065 DIM %DEPT_MASK$(%GL_DEPT_LEN,"0")
13070 RETURN 
13100 ! ^13100, 10,Sales Tax Section
13110 STAX_FMT:
13120 %SALES_TAX_MASK$=FN_GET_MASK$("AR5","%SALES_TAX_LEN","","","")
13130 RETURN 
14000 ! 14000,10
14010 ! 
14020 CONTROL_KEY:
14030 ! 
14040 DEFCTL $000270$=-14 ! F1 - help
14050 DEFCTL $000271$=-18 ! F2 - Calendar
14055 DEFCTL $000272$=-11 ! F3 - View Program Call Stack
14060 DEFCTL $000273$=-17 ! F4 - Item Assistant
14070 DEFCTL $000274$=-19 ! F5 - Calculator
14080 DEFCTL $000275$=-15 ! F6 - Customer, Vendor, Item Custom Notes
14090 DEFCTL $000276$=-13 ! F7 - Options:Files/Window/Send,Check Mail/On_Doc
14100 DEFCTL $000277$=-12 ! F8 - Memo and Special Date Setup
14110 DEFCTL $000278$=-16 ! F9 - Customer Assistant
14112 DEFCTL $000370$=-22 ! F10 - Customer Support
14115 DEFCTL $04$=-25 ! Delete Record on report
14116 DEFCTL $15$=-26 ! Update Record on report
14117 DEFCTL $11$=-20 ! CTRL+Q - Query Creator
14118 DEFCTL $000279$=-21 ! CTRL+F10 - Query Generator
14119 DEFCTL $00027B$=-23 ! Print Screen
14120 ! 
14500 ! 14500,10 Company Message
14510 ! 
14520 _ZZP=HFN; OPEN (_ZZP)"ZZPARM"; _ZZR=HFN; OPEN (_ZZR)"ZZROLE"
14521 _SYO=HFN; OPEN (_SYO,ERR=*NEXT)"SYO"+%C$; READ (_SYO,KEY=X3$(40,3),DOM=*NEXT)SYO$; %DR_SYO$=SYO$; ADDR "UPDSYO"; CLOSE (_SYO) ! SSP 192978
14522 READ (_ZZP,KEY=FID(0)+"      ",DOM=*RETURN)X3$,*,X4$
14523 DIM RIGHT$(11); READ (_ZZP,KEY="*right*"+X3$(40,3),DOM=*NEXT)RIGHT$,%RIGHT
14524 READ (_ZZP,KEY=%C$+"A/R",DOM=*NEXT)AR_PARAM$; IF AR_PARAM$(254,1)="Y" THEN %BYPASS_R4_REPORT=1 ELSE %BYPASS_R4_REPORT=0 ! SSP 228852
14525 READ (_ZZP,KEY="*viewer*",DOM=*NEXT)*,*,HLP_PTH$,CB_WORD$; %HELP_PATH$=HLP_PTH$; IF STP(CB_WORD$)="" THEN %CB_WORD=1 ELSE %CB_WORD=NUM(CB_WORD$)
14528 READ (_ZZP,KEY="o"+X3$(40,3),DOM=*NEXT)OPER$; %OPER=NUM(OPER$(58,1))
14530 READ (_ZZP,KEY="CMP"+X3$(9,3),DOM=*NEXT)CMP$; %_LOG_HISTORY=NUM(MID(CMP$,197,1))
14531 HLP_PTH$=""; READ (_ZZP,KEY="s"+X3$(40,3),DOM=*NEXT)SEC$,%SOUND_PLAY,HLP_PTH$,USER_CB_WORD$,CB_FAV$,CB_EXPORT_EXCEL$,CB_VIEW_FILE$,CB_CUST_QRY$,CB_CUST_PNL$,CB_TRANSFER_FILE$,; IF LEN(SEC$)>=5 THEN %ROLE$=STP(MID(SEC$,5)),SEC$="" ! SSP 208045 SSP 224088 SSP 224256 SSP 307491
14532 IF X3$(48,1)="1" THEN TODAY$=DTE(0:"%Dl, %Ml %D, %Yl") ELSE IF X3$(48,1)="2" THEN TODAY$=DTE(0:"%Dl, %D %Ml, %Yl") ELSE TODAY$=DTE(0:"%Dl, %Yl, %Ml %D")
14533 IF HLP_PTH$<>"" THEN %HELP_PATH$=HLP_PTH$
14534 %SOUND_PLAY=0,%CB_WORD=0 ! IF POS(USER_CB_WORD$="01")<>0 THEN %CB_WORD=NUM(USER_CB_WORD$) ! 307475
14535 PRINT 'MESSAGE'(DEF 0)
14540 PRINT 'MESSAGE'(DEF 25)
14550 PRINT 'MESSAGE'('_BLUE'+'WHITE'+X3$(9,3)+"-"+X3$(86,40),1)
14551 PRINT 'MESSAGE'(DEF 30,-35)
14552 PRINT 'MESSAGE'('_BLUE'+'WHITE'+"Today's Date: "+TODAY$+" - User:"+X3$(40,3),2)
14560 CLOSE (_ZZP),(_ZZR)
14565 %YESNO$="YesNo "
14566 %CB_FAV=NUM(CB_FAV$),%EXPORT_TO_EXCEL=NUM(CB_EXPORT_EXCEL$),%VIEW_FILE=NUM(CB_VIEW_FILE$),%CB_CUST_QRY=NUM(CB_CUST_QRY$),%NOMAD_CUSTOM_SKIP_DEFINITION=NUM(CB_CUST_PNL$),%NOMAD_NO_CUSTOMIZE=NUM(CB_CUST_PNL$),%CB_TRANSFER_FILE=NUM(CB_TRANSFER_FILE$) ! SSP 208045 224088 224256  SSP 307491
14567 %QRY_MAINT_DISABLE=0; IF UCS(MID(OPER$,94,2))<>"Z2" THEN %QRY_MAINT_DISABLE=1 ! 245025
14570 RETURN 
14990 CALL "CIA",ERR=*NEXT
14999 RETURN 
15000 ! 15000,10 Play Sound
15005 RETURN ! 198585 - kills performance
15010 DEF FN%PLAY_SOUND$(LOCAL SOUND_NAME$)
15020 PRINT SOUND_NAME$
15030 MULTI_MEDIA "close all"
15040 MULTI_MEDIA "open \\"+NID+"\pvx\tf2g\ZZG\"+SOUND_NAME$+" alias topform"
15050 MULTI_MEDIA "play topform"
15060 END DEF
15070 RETURN 
16000 SEL_PHONE:
16010 VAL$=EVS(ID$+"$")
16020 VAL=NUM(VAL$,ERR=NO_NUM)
16025 IF POS("."=VAL$)<>0 OR POS(","=VAL$)<>0 OR POS("-"=VAL$)<>0 OR POS("+"=VAL$)<>0 THEN GOTO NO_NUM
16030 RETURN 
16040 NO_NUM:
16050 VAL$=VAL$(1,LEN(VAL$)-1)
16060 EXECUTE ID$+"$=VAL$"
16070 REFRESH_FLG=1
16080 RETURN 
56000 ! "189263-Program to copy role from one user to another               
56001 ! "196661-The suggestion was made to provide for the search criteria  
56002 REM "198585-test windows/topform performance                            
56003 ! "208045-Requirements for security to not allow export to excel      
56010 ! "224088-Security - If I login as a salesrep, I can play around to   
56015 ! "224256-Need to setup security of the "Panel Customizer" feature.   
56016 ! "192978-Put 191822 w/o in GUI order Entry                           
56017 ! "228852-Remove the print alignment questions in all areas since they
56018 REM "245025-Set security on the maintenance folder in the query screens
56020 REM "268441-WMS - Picking                                               
56021 REM "307475-DBD-443:Disable MSW Errors, Sound Play                      
56022 REM "307491-Generic File Transfer utility                               
