0010 ! ZZGDFL - Select disk file list, user interface
0030 ! ************************************************************************
0035 REM "5.7 - 04/03/13 - 9.446666 - crg - SSP# 260723
0037 REM "260723-Import Module/Cash Receipts GW/CIG Plus; Allow selection of
0040 REM "Copyright 2013 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0049 ! 
0050 ENTER CHANNEL,FROM_DIR$,FILE_MASK$,ERRMSG$,ERR=*NEXT
0052 IF NUL(%WDX$) THEN MSGBOX MSG("WARN_WINDX"),MSG("ERROR"),"OK,STOP"; GOTO 0075
0055 ARG_1$=STR(CHANNEL)
0060 PROCESS "ZZGDFL","../ZZG/ZZ.EN",ARG_1$,FROM_DIR$,FILE_MASK$,ERRMSG$
0065 CHANNEL=NUM(ARG_1$,ERR=*NEXT)
0075 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 FILENAME_LEN=120,DEF_MAX_FILES=999
0154 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; DIM Z[NUM(X3$(60,3))]
0160 GOSUB SET_INIT_VARS
0190 RETURN 
0199 ! 
0200 ! 200 - Main panel post_display logic
0210 MAIN_POST_DISPLAY:
0290 RETURN 
0299 ! 
0500 ! 500 - Initialize panel variables
0510 SET_INIT_VARS:
0520 MEMCHN=NUM(ARG_1$,ERR=*NEXT)
0530 IF MEMCHN=0 THEN OPEN (HFN)"*MEMORY*"; MEMCHN=LFO
0540 FROM_DIR$=ARG_2$,FILE_MASK$=ARG_3$
0570 RETURN 
0599 ! 
0900 ! 900 - Wrapup
0910 WRAPUP:
0950 ARG_1$=STR(MEMCHN)
0960 ARG_4$=ERRMSG$
0970 RETURN 
0999 ! 
1000 REM "Event handler - OnSelect
1005 SELECT_FILE:
1010 READ DATA FROM FILE_GRID.CTL'ROWDATA$ TO IOL=FILE_GRID.CTL'LOADIOLIST$
1020 IF C_SEL$="1" THEN {
1030 WRITE (MEMCHN,KEY=PAD(STP(C_FILENAME$,2),FILENAME_LEN),ERR=*NEXT)C_FILENAME$
1040  } ELSE {
1050 REMOVE (MEMCHN,KEY=PAD(STP(C_FILENAME$,2),FILENAME_LEN),ERR=*NEXT)
1060  }
1095 RETURN 
1099 ! 
9999 END 
10000 ! 
10010 REM "Loads given control with the files present in providex.ddf
10015 REM "FILE_LIST - NOMADS Control or memory channel to be loaded with list of files present in specified dir
10020 REM "CTLTYPE$ - indicates type Of control: L - Listbox, D - Dropbox, G - Grid, M - Memory file channel
10025 REM "COL - required for G - Grid type control, indicates column used for the filename in grid
10026 REM "PRESEL - Memory channel containing preselected files for grid display, SELCOL - Grid column containing "selected" indicator
10027 REM "FROM_DIR - Source directory, FILE_MASK - eg: *.txt or RP1*, MAX_FILES - Limit of number of files returned (default 999)
10030 LOAD_FILE_LIST:
10032 SETERR ERR_CATCH
10035 FILENAME_LEN=120,DEF_MAX_FILES=999
10040 ENTER FILE_LIST,CTLTYPE$,COL,PRESEL,SELCOL,FROM_DIR$,FILE_MASK$,MAX_FILES,ERRMSG$,ERR=*NEXT
10050 IF COL=0 THEN COL=1
10051 IF NUL(FROM_DIR$) THEN FROM_DIR$=HWD
10052 IF NUL(FILE_MASK$) THEN FILE_MASK$="*"
10053 IF MAX_FILES<=0 THEN MAX_FILES=DEF_MAX_FILES
10120 I=0
10140 CLEAR_CONTROL:ON POS(CTLTYPE$="LDGM") GOTO ERR_CONTROL,CLEAR_LISTBOX,CLEAR_DROPBOX,CLEAR_GRID,CLEAR_MEM_CHANNEL,ERR_CONTROL
10142 CLEAR_LISTBOX: LIST_BOX LOAD FILE_LIST,""; GOTO CLEAR_DONE
10144 CLEAR_DROPBOX: DROP_BOX LOAD FILE_LIST,""; GOTO CLEAR_DONE
10146 CLEAR_GRID: GRID LOAD FILE_LIST,0,0,""; GOTO CLEAR_DONE
10148 CLEAR_MEM_CHANNEL:IF FILE_LIST THEN CLOSE (FILE_LIST,ERR=*PROCEED) ELSE FILE_LIST=HFN END_IF ; OPEN (FILE_LIST)"*MEMORY*"; GOTO CLEAR_DONE
10150 CLEAR_DONE:
10165 SELECT F_NAME$ FROM STP(FROM_DIR$) WHERE F_NAME$ LIKE FILE_MASK$
10210 IF F_NAME$="." OR F_NAME$=".." THEN CONTINUE
10230 I++
10240 LOAD_CONTROL:ON POS(CTLTYPE$="LDGM") GOTO ERR_CONTROL,LOAD_LISTBOX,LOAD_DROPBOX,LOAD_GRID,LOAD_MEM_CHANNEL,ERR_CONTROL
10244 LOAD_LISTBOX: LIST_BOX LOAD FILE_LIST,0,F_NAME$; GOTO LOAD_DONE
10246 LOAD_DROPBOX: DROP_BOX LOAD FILE_LIST,0,F_NAME$; GOTO LOAD_DONE
10248 LOAD_GRID: GRID LOAD FILE_LIST,1,0,SEP; GRID WRITE FILE_LIST,COL,I,F_NAME$; IF SELCOL<>0 THEN IS_SEL$="0"; GOSUB CHECK_SELECTED; GRID WRITE FILE_LIST,SELCOL,I,IS_SEL$ END_IF ; GOTO LOAD_DONE
10250 LOAD_MEM_CHANNEL:WRITE (FILE_LIST,KEY=STR(I:DIM(10,"0")))F_NAME$; GOTO LOAD_DONE
10255 LOAD_DONE:
10257 IF I>=MAX_FILES THEN GOTO *BREAK
10260 NEXT RECORD ! GOTO LFL_NEXT_FILE
10270 LFL_END:
10290 EXIT 
10300 ! 
10310 ERR_CONTROL: ERRMSG$="Error loading file list: Invalid or Unknown control provided: "+CTLTYPE$; GOTO LFL_END
10320 ERR_CATCH:
10325 XX=ERR; YY=TCB(5); ERRMSG$="Error "+STR(XX)+" at line "+STR(YY)+": Stack:"+FN%GET_STACK$; GOTO LFL_END
10500 ! 
10505 CHECK_SELECTED:
10510 FIND (PRESEL,KEY=PAD(STP(F_NAME$,2),FILENAME_LEN),DOM=*NEXT); IS_SEL$="1"
10545 RETURN 
10549 ! 
11000 REM "Output the presented channel containing filenames in char delimted string form
11010 REM "CHAN - Open file channel containing list of files as data strings
11015 REM "MODE - 0 - Default, uses only data portion of rec
11020 REM "SEP_CHAR$ - String to be applied as suffix to all entries
11025 REM "PREFIX$ - String to be prefixed to all entries
11030 REM "RETVAL$ - Concatenated string of char sperated, prefixed file entries
11035 FORMAT:
11040 ENTER CHAN,MODE,SEP_CHAR$,PREFIX$,RETVAL$,ERRMSG$,ERR=*NEXT
11100 IF NOT(FN%ISOPEN(CHAN)) THEN ERRMSG$="Invalid channel"; RETURN 
11115 RETVAL$="",ERRMSG$=""
11120 READ (CHAN,KEY="",DOM=*NEXT)
11125 NEXT_FMT_FILE:
11130 K$=KEY(CHAN,END=FMT_DONE); READ (CHAN,KEY=K$)F$
11140 RETVAL$+=PREFIX$+STP(F$,2)+SEP_CHAR$
11190 GOTO NEXT_FMT_FILE
11280 FMT_DONE:
11295 EXIT 
11299 ! 
