0010 ! ZZWCAL - Calendar
0030 INIT:
0035 XX$=DTE(*)
0037 X=POS(","=XX$,1,12); IF X<>0 THEN MTHS_LONG$=XX$(1,X),XX$=XX$(X+1)
0038 P=POS(","=XX$,1,7); IF P<>0 THEN WEEK_DYS$=XX$(1,P-1)
0039 Q=POS(","=WEEK_DYS$,-1)
0040 IF Q<>0 THEN WEEK_DYS$=","+WEEK_DYS$(Q+1)+","+WEEK_DYS$(1,Q)
0041 WEEK_DYS$=STP(WEEK_DYS$,1,",")
0043 L=POS(","=WEEK_DYS$); IF L<>0 THEN WK_DYS$=WK_DYS$+WEEK_DYS$(L+1,1),WEEK_DYS$=WEEK_DYS$(L+2); GOTO *SAME
0045 IF LEN(ARG_1$)=8 THEN GOTO PARSE_IT
0050 FORCE_IT: ARG_1$=DTE(0:"%Yl%Mz%Dz")
0060 PARSE_IT:SETERR FORCE_IT
0070 READ DATA FROM ARG_1$ TO Y:[NUM(4)],M:[NUM(2)],D:[NUM(2)]
0080 X=JUL(Y,M,D,ERR=FORCE_IT)
0090 MONTH_ID$=MID("123456789ABC",M,1)
0100 YEAR_ID$=STR(Y:"0000")
0110 DAY_ID$=STR(D)
0120 SETERR 0000
0130 SETERR 0000
0140 ! Position window
0141 MSE$=MSE
0150 X=DEC(MID(MSE$,20,2)); IF X=0 THEN GOTO 0180
0160 X$=OBJ(X,ERR=0180),CTRL_X=DEC(X$(21,2)),CTRL_W=DEC(X$(25,2)),CTRL_H=DEC(X$(27,2)),CTRL_Y=DEC(X$(23,2))+CTRL_H ! Get bottom
0170 _OBJ_C=-100 ! Draw window off screen
0180 _OBJ_STS$+="h"
0190 RETURN 
0200 ! ^100 - Move window
0210 POST_INIT:
0220 PRINT 'FILL'(1,15),'PEN'(0,0,0),'RECTANGLE'(@X(2),@Y(3),@X(27.5),@Y(13.75)),'FRAME'(@X(2),@Y(3),@X(27.5),@Y(13.75),0),
0230 IF CTRL_Y=0 THEN GOTO 0250
0231 X$=OBJ(0),MSE$=MSE,WNDW_W=DEC(X$(25,2)),WNDW_H=DEC(X$(27,2))
0232 TOT_W=DEC($00$+MID(MSE$,27,2)),TOT_H=DEC($00$+MID(MSE$,29,2))
0233 IF CTRL_X+WNDW_W>TOT_W THEN CTRL_X=CTRL_X-WNDW_W+CTRL_W
0234 IF CTRL_Y+WNDW_H>TOT_H THEN CTRL_Y=CTRL_Y-WNDW_H-CTRL_H
0240 IF DEC(MID(MSE$,22,1))>0 THEN CALL "[WDX]*win/calendar;call_dll",CTRL_X,CTRL_Y ELSE GOSUB DO_DLL
0250 GOSUB DRAW_MONTH; RETURN 
0300 ! ^100 - Draw month
0310 DRAW_MONTH:
0315 IF FPT(NUM(YEAR_ID$)/4)=0 THEN LEAP_YEAR.CTL'VISIBLE=1 ELSE LEAP_YEAR.CTL'VISIBLE=0
0320 SV_BY=PRM('BY')
0330 SET_PARAM 'BY'=1797
0335 GOSUB CHECK_HOLIDAY
0340 Y=NUM(YEAR_ID$),M=POS(MONTH_ID$="123456789ABC")
0350 CUR_L=3.5
0360 IF JUL(Y,M,1)|7<5 THEN CUR_L+=.75
0370 PRINT 'SCROLL'(2,4,25,6*1.5),'DO','SR',
0380 MAX_DY=0
0390 FOR N=1 TO 31
0400 COL=(JUL(Y,M,N,ERR=*BREAK))|7
0403 IF POS(STR(Y:"0000")+STR(M:"00")+STR(N:"00")=FULL_MOON$,8)<>0 THEN FULL$=CHR(186) ELSE FULL$=""
0405 IF (M=6 AND N=14) OR (M=7 AND N=4) OR (M=9 AND N=11) THEN RADIO_BUTTON DAY_ID.CTL:N,@(COL*3.5+2.5,CUR_L+1,3.5,1.5)="{!US_FLAG}"+STR(N)+FULL$,MSG=MSG("PRESS_S")+" "+MSG("DAY")+" "+STR(N); GOTO 0415
0410 RADIO_BUTTON DAY_ID.CTL:N,@(COL*3.5+2.5,CUR_L+1,3.5,1.5)="{}"+STR(N)+FULL$
0412 IF FULL$<>"" THEN DAY_ID.CTL'ID=N,DAY_ID.CTL'TIP$="Full Moon"
0413 IF PFM$=STR(Y:"0000")+STR(M:"00")+STR(N:"00") THEN DAY_ID.CTL'ID=N,DAY_ID.CTL'TIP$=MSG("PFM")
0415 GOSUB CHECK_HOLIDAYS
0420 MAX_DY=N
0430 IF COL=6 THEN CUR_L+=1.5
0440 NEXT 
0450 SET_PARAM 'BY'=SV_BY
0460 D=NUM(DAY_ID$)
0470 IF D>MAX_DY THEN D=MAX_DY
0480 DAY_ID$=STR(D)
0490 REFRESH_FLG=1
0500 RETURN 
0600 ! ^100 - Check EOM terminator
0610 SET_DATE:
0620 ARG_1$=YEAR_ID$+STR(POS(MONTH_ID$="123456789ABC"):"00")+STR(NUM(DAY_ID$):"00"),Y=NUM(YEAR_ID$),M=NUM(ARG_1$(5,2)),N=NUM(ARG_1$(7))
0627 IF PFM$=STR(Y:"0000")+STR(M:"00")+STR(N:"00") THEN DAY_ID.CTL'ID=N,DAY_ID.CTL'TIP$=MSG("PFM")
0630 CMD_STR$="END"
0640 IF _ZZY5<>0 THEN CLOSE (_ZZY5); _ZZY5=0
0650 IF _ZZYM<>0 THEN CLOSE (_ZZYM); _ZZYM=0
0699 RETURN 
1000 ! ^1000 - Call the DLL
1010 CALL_DLL:
1020 ENTER CTRL_X,CTRL_Y
1030 GOSUB DO_DLL
1040 EXIT 
1100 ! ^100
1110 DO_DLL:
1120 X$=OBJ(0),WNDW_W=DEC(X$(25,2)),WNDW_H=DEC(X$(27,2))
1130 IF POS("PVXWIN32"=UCS(ARG(0))) THEN L$="USER32.dll" ELSE L$="USER.EXE"
1140 A=DLL(L$,"MoveWindow",INT(DEC(X$(17,4))),INT(CTRL_X),INT(CTRL_Y),INT(WNDW_W),INT(WNDW_H),INT(1))
1150 RETURN 
9000 ! 9000 - Notes on usage
9010 ! .
9020 ! This query is designed to work with 8 digit dates of the form
9030 ! YYYYMMDD.  Since most applications would prefer to work with
9040 ! with a more readable date you will need to develop a small interface
9050 ! program to convert the date.
9060 ! .
9070 ! By using the Run-program option in NOMADS (first character of query is
9080 ! is an asterisk) you can have nomads call your interface program which can
9090 ! then use this calendar program.
9100 ! .
9110 ! Assuming you want to use a date MM/DD/YYYY...
9120 ! Also assume your interface program is called MYDATE...
9130 ! .
9140 ! Set the query on date fields to *MYDATE.  Nomads will then perform MYDATE when a
9150 ! query is invoked and pass the query value in QRY_VAL$
9160 ! .
9170 ! Create MYDATE as follows:
9180 ! 0010 SETERR TRUE_QRY
9190 ! 0020 X$=QRY_VAL$(7,4)+QRY_VAL$(1,2)+QRY_VAL$(4,2)
9200 ! 0030 TRUE_QRY: PROCESS "calendar","*win/calendar.en",X$
9210 ! 0040 IF X$<>"" QRY_VAL$=X$(5,2)+"/"+X$(7,2)+"/"+X$(1,4)
9220 ! 0050 EXIT
10000 ! 10000,10 Check For Holidays
10010 CHECK_HOLIDAYS:
10015 %B_WINDATE_FMT$=%WINDATE_FMT$,%WINDATE_FMT$="YYYYMMDD"
10020 ! 
10030 IF _ZZY5=0 THEN _ZZY5=HFN; OPEN (_ZZY5,IOL=*,REC=_ZY5$)"ZY5"
10040 IF _ZZYM=0 THEN _ZZYM=HFN; OPEN (_ZZYM,IOL=*,REC=_ZYM$)"ZYM"
10050 JJ$=STR(Y:"0000")+STR(M:"00")+STR(N:"00"); CALL "ZZWDTE;VALIDATE",JJ$,JJJ$
10055 ALL_DESC$="",DES$="",%WINDATE_FMT$=%B_WINDATE_FMT$
10060 READ (_ZZYM,KEY=%X3$(40,3)+JJ$,DOM=HOLIDAYS)
10070 DAY_ID.CTL'ID=N,DAY_ID.CTL'BACKCOLOUR$="Light Cyan"
10075 ALL_DESC$="",DES$=""; FOR AA=1 TO 9
10076 DES$=STP(EVS("_ZYM.MEMO_DESC_LINE_"+STR(AA)+"$"))
10077 IF DES$<>"" THEN ALL_DESC$=ALL_DESC$+DES$+"  "
10078 NEXT AA
10079 IF ALL_DESC$<>"" THEN DAY_ID.CTL'ID=N,DAY_ID.CTL'TIP$=ALL_DESC$
10080 HOLIDAYS:READ (_ZZY5,KEY=JJ$,DOM=END_HOLIDAYS)
10090 IF _ZY5.HIGHLIGHT$="1" THEN DAY_ID.CTL'ID=N,DAY_ID.CTL'BACKCOLOUR$="Light Red"
10100 IF _ZY5.BANK_HOLIDAY$="1" THEN DAY_ID.CTL'ID=N,DAY_ID.CTL'BACKCOLOUR$="Light Blue",DAY_ID.CTL'TEXTCOLOR$="White"
10110 IF _ZY5.NORMALLY_WORK_OF$="1" THEN DAY_ID.CTL'ID=N,DAY_ID.CTL'BACKCOLOUR$="White"
10120 IF _ZY5.NORMALLY_WORK_OF$="2" THEN DAY_ID.CTL'ID=N,DAY_ID.CTL'BACKCOLOUR$="Light Yellow"
10130 IF _ZY5.NORMALLY_WORK_OF$="3" THEN DAY_ID.CTL'ID=N,DAY_ID.CTL'BACKCOLOUR$="Light Magenta"; IF MID(JJ$,3,4)="0317" THEN DAY_ID.CTL'BACKCOLOUR$="Light Green"
10140 IF STP(_ZY5.TF_DESCRIPTION$)<>"" THEN DAY_ID.CTL'ID=N,SSS$="",SSS$=" / "+DAY_ID.CTL'TIP$,DAY_ID.CTL'TIP$=STP(_ZY5.TF_DESCRIPTION$); IF LEN(SSS$)>8 THEN DAY_ID.CTL'TIP$=STP(_ZY5.TF_DESCRIPTION$)+" "+SSS$
10145 END_HOLIDAYS: %WINDATE_FMT$=%B_WINDATE_FMT$
10150 RETURN 
10500 ! 10500,10 Check Holidays on File
10510 CHECK_HOLIDAY:
10511 %B_WINDATE_FMT$=%WINDATE_FMT$,%WINDATE_FMT$="YYYYMMDD"
10512 PERFORM "../ZZG/ZYGDAB;Paschal_full_moon"
10513 FULL_MOON$=""; FOR AA=1 TO 13; FULL_MOON$=FULL_MOON$+DE$[AA]; NEXT AA
10515 IF _ZZY5=0 THEN _ZZY5=HFN; OPEN (_ZZY5,IOL=*,REC=_ZY5$)"ZY5"
10516 %B_WINDATE_FMT$=%WINDATE_FMT$,%WINDATE_FMT$="YYYYMMDD"
10520 NOPRINT=1,JJ$=YEAR_ID$+"0101"; CALL "ZZWDTE;VALIDATE",JJ$,JJJ$
10530 READ (_ZZY5,KEY=JJ$,DOM=*NEXT); GOTO END_HOLIDAYS
10540 PERFORM "../ZZG/ZYGDAB;INIT"
10550 PERFORM "../ZZG/ZYGDAB;WRITE_DATES"
10560 NOPRINT=0
10565 %WINDATE_FMT$=%B_WINDATE_FMT$
10570 RETURN 
11000 SEL_HSB:! 11000,10
11010 H_SCROLLBAR READ HSB.CTL,VAL,13,1,1
11020 IF VAL=13 THEN YEAR_ID$=STR(NUM(YEAR_ID$)+1:"0000"),VAL=1
11030 IF VAL=1 AND B_VAL=1 THEN YEAR_ID$=STR(NUM(YEAR_ID$)-1:"0000"),VAL=12
11040 IF VAL=1 THEN B_VAL=1 ELSE B_VAL=0
11050 VAL$="123456789ABC",MONTH_ID$=VAL$(VAL,1)
11060 GOSUB DRAW_MONTH
11070 H_SCROLLBAR WRITE HSB.CTL,VAL,13
11080 RETURN 
