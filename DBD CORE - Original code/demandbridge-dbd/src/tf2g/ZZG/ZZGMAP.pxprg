0010 ! MAP Services <ZZGMAP>
0035 REM "5.6 - 03/17/09 - 10.9275 - tma - SSP# 228234
0037 REM "228234-EURO zip code fails with PE in contact mgmt                 
0040 REM "Copyright 2009 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 IF TCB(13)=1 THEN BEGIN 
0060 PROCESS "MAPURLMPA","tf2g/MAPURL/AP.EN"
0070 END 
0080 INIT:
0200 ! ^200,10 Setup
0210 DIM MAPURL$:"PARM_KEY:C(9),MAP_URL:C(120)"
0300 ! ^300,10 - Iolist
0310 MAPURL_IOL:IOLIST MAPURL$
0320 ! 
0500 ! ^ 500,10 - Open Files
0510 _ZZPARM=HFN; OPEN (_ZZPARM)"ZZPARM"
0520 RETURN 
0530 ! 
1000 ! ^1000,10 Read in parameters
1010 READ_REC:
1020 PARM_KEY$=%C$+"MAPURL",KEY_SIZE=LEN(PARM_KEY$)
1030 READ DATA FROM "" TO IOL=MAPURL_IOL
1040 MAPURL$(1,KEY_SIZE)=PARM_KEY$
1050 READ (_ZZPARM,KEY=PARM_KEY$,DOM=*NEXT)IOL=MAPURL_IOL
1060 REFRESH_FLG=1
1070 RETURN 
1080 ! 
1200 ! ^1200,10 Write out parameters
1210 WRITE_REC:
1220 WRITE (_ZZPARM,KEY=PARM_KEY$)IOL=MAPURL_IOL
1230 CMD_STR$="E"
1240 RETURN 
1250 ! 
2000 GET_MAP:! given ADDR1$,ADDR2$,CITY$,TF_STATE$,ZIP$ get MAPURL, substitute into template, URL encode then call system_help to display in the browser
2005 ENTER ADDR1$,ADDR2$,CITY$,TF_STATE$,ZIP$,ERR=*NEXT
2010 SELECT MAP$ FROM "ZZPARM" BEGIN %C$+"MAPURL" END %C$+"MAPURL"
2015 MAP_URL$=STP(MID(MAP$,10))
2020 NEXT RECORD 
2023 IF MAP$="" THEN MAP$="http://www.mapquest.com/maps/map.adp?address=TFADDR_1 TFADDR_2&city=TFCITY&state=TFSTATE&zip=TFZIP&country=us&zoom=8&",MAP_URL$=MAP$
2024 MAP_URL$=SUB(MAP_URL$,"&zip","&zipcode") ! ssp 
2025 MAP_URL$=SUB(MAP_URL$,"TFADDR_1",STP(ADDR1$))
2026 MAP_URL$=SUB(MAP_URL$,"TFADDR_2",STP(ADDR2$))
2027 MAP_URL$=SUB(MAP_URL$,"TFCITY",STP(CITY$))
2028 MAP_URL$=SUB(MAP_URL$,"TFSTATE",STP(TF_STATE$))
2029 MAP_URL$=SUB(MAP_URL$,"TFZIP",STP(ZIP$))
2040 MAP_URL$=FN%URLENCODE$(MAP_URL$)
2050 MAP_URL$=FN%URLENCODE$(MAP_URL$)
2055 IF MID(MAP_URL$,1,2)="w." THEN MAP_URL$="http://ww"+MAP_URL$
2075 SYSTEM_HELP MAP_URL$
2095 EXIT 
2099 ! 
2100 DEF FN%URLENCODE$(LOCAL DAT$)
2105 LOCAL X,ODAT$
2110 IF LEN(DAT$)=0 THEN RETURN ""
2115 FOR X=1 TO LEN(DAT$)
2120 IF POS(DAT$(X,1)=" 0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz$-_.+!*'(),/:?=&",1)=0 THEN ODAT$+="%"+HTA(DAT$(X,1)) ELSE ODAT$+=DAT$(X,1)
2125 NEXT 
2130 TRANSLATE ODAT$," "+$01$+"+"
2135 RETURN ODAT$
2140 END DEF
2149 ! 
7600 CLIPBOARD:! ^100,5 - Write name and address information to clipboard
7603 ENTER ADDR_1$,ADDR_2$,CITY$,ST$,ZIP_CODE$,CONTACT$,CONTACT_GREETING$,TF_NAME$,ERR=*NEXT
7605 CLIP_BOARD WRITE ""
7608 IF STP(CONTACT$,2)<>"" THEN REC$=CONTACT$+$0A$
7610 IF STP(TF_NAME$)<>"" THEN REC$+=TF_NAME$+$0A$
7615 IF STP(ADDR_1$,2)<>"" THEN REC$+=ADDR_1$+$0A$
7620 IF STP(ADDR_2$,2)<>"" THEN REC$+=ADDR_2$+$0A$
7622 ZIP$=ZIP_CODE$; IF LEN(STP(ZIP_CODE$))>5 THEN ZIP$=FN%ZZDISP$(ZIP_CODE$,"ZIP") ! SSP#228234
7625 REC$+=STP(CITY$)+", "+ST$+" "+STP(ZIP$)+$0A$
7630 REC$+=$0A$
7640 REC$+=$0A$
7645 IF STP(CONTACT_GREETING$,2)<>"" THEN REC$+=MSG("DEAR")+" "+CONTACT_GREETING$+$0A$
7650 CLIP_BOARD WRITE REC$
7652 REC2$=REC$
7653 X=POS($0A$=REC2$); IF X<>0 THEN REC2$(X,1)=SEP; GOTO 7653
7654 MSGBOX REC2$+SEP+SEP+SEP+MSG("WISH")+" Start Word?","Clipboard","info,tim=10,yesno",OPT$; IF OPT$="YES" THEN GOSUB START_WORD
7655 ! MSGBOX REC2$,"Clipboard","info,tim=10",OPT$
7656 ! IF WHO="jir" THEN GOTO 10000
7657 EXIT 
7660 ! 
8000 TRACKING:
8010 ENTER _KEY$,TRACK_NUM$
8020 _FMP=HFN; OPEN (_FMP,ERR=*RETURN)"FMP"+%C$
8030 READ (_FMP,KEY="S"+_KEY$,DOM=NO_RECORD)FMP$
8040 HHTP$=MID(FMP$,19),XX=POS("="=HHTP$,-1); IF XX<>0 THEN HHTP$=HHTP$(1,XX)
8050 HHTP$+=TRACK_NUM$
8060 SYSTEM_HELP HHTP$
8070 NO_RECORD:CLOSE (_FMP); EXIT 
9900 ! ^9900,10 - Wrap up
9910 WRAP_UP:
9920 CLOSE (_ZZPARM)
9930 RETURN 
9999 END 
10000 START_WORD:! 10000,10
10010 SYSTEM_HELP "winword.exe"
10020 WAIT 2
10030 _WORD=HFN; OPEN (_WORD)"[wdx][dde]winword;" ! ssp 199670
10032 FOR A=1 TO 3; WRITE RECORD (_WORD)"[Insertpara]"; NEXT A
10035 WRITE RECORD (_WORD)"[Insert "+QUO+DTE(0:"%Ml %Dz, %Yl")+QUO+"]"
10038 FOR A=1 TO 3; WRITE RECORD (_WORD)"[Insertpara]"; NEXT A
10040 WRITE RECORD (_WORD)"[Editpaste]"
10050 WRITE RECORD (_WORD)"[InsertPara]"
10060 WRITE RECORD (_WORD)"[Insert "" ""]"
10070 CLOSE (_WORD)
10080 RETURN 
10500 ! 10500,10
10510 XX=POS($0A$=REC$); IF XX=0 THEN RETURN 
10520 BB$=REC$(1,XX-1),REC$=REC$(XX+1)
10530 WRITE RECORD (_WORD)"[Insert "+QUO+BB$+QUO+"]"
10540 WRITE RECORD (_WORD)"[Insertpara]"
10550 GOTO 10510
56000 ! "195226-I noticed that the link out to mapquest is not sending the  
56001 ! "       zip code."
56002 ! "199670-Copying an address to clip board generates an error         
56004 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
56005 REM "228234-EURO zip code fails with PE in contact mgmt                 
