0010 REM "Platinum Issues <DBG003>                                     
0020 SETESC 9300; SETERR 9000
0035 REM "5.5 - 06/14/06 - 9.719722 - kmc - SSP# 196716
0037 REM "196716-Dashboards                                                  
0040 REM "Copyright 2006 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,CTL_LIST,GADGET_NUMBER,OPTIONS{ALL},RETURN$
0100 SETERR 9000
0110 X0$="DBG003",X1$="Platinum Issues"
0200 REM "                                                                 
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "IOLISTS                                                          
0500 REM "FILES                                                            
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O SS1...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0599 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
0600 ! Initialize anything needed
0605 GADGET_FLAG$="%GADGET_"+%CURR_GADGET.DB_GADGET_INDEX$ ! will be 0 if first time through
0610 FONT$="Arial,-16,B"
0620 UP_LEFT_X_PIXEL=@X(%CURR_GADGET.UP_LEFT_COL-1),UP_LEFT_Y_PIXEL=@Y(%CURR_GADGET.UP_LEFT_ROW-1) ! -1 To get top left of value
0625 WIDTH_PIXEL=@X(%CURR_GADGET.WIDTH),HEIGHT_PIXEL=@Y(%CURR_GADGET.HEIGHT) ! no minus 1, to get bottom and right edge
0626 WIDTH_COL=%CURR_GADGET.WIDTH,HEIGHT_ROW=%CURR_GADGET.HEIGHT
0630 UP_LEFT_COL=%CURR_GADGET.UP_LEFT_COL,UP_LEFT_ROW=%CURR_GADGET.UP_LEFT_ROW
0635 RIGHT_COL=%CURR_GADGET.UP_LEFT_COL+%CURR_GADGET.WIDTH,BOTTOM_ROW=%CURR_GADGET.UP_LEFT_ROW+%CURR_GADGET.HEIGHT
0665 CUR_CTL=15000+(100*GADGET_NUMBER)
0670 TITLE.CTL=++CUR_CTL,PLATINUM_LIST.CTL=++CUR_CTL
0680 MASK1$="#,##0"
0685 BT_EXCEL.CTL=++CUR_CTL
0699 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
1000 REM "BEGIN MAIN PROCESS                                               
1005 IF VIN(GADGET_FLAG$)=0 THEN {! First time through
1020 ALERT=0; GOSUB BORDER
1035 GOSUB SETUP_FIELDS
1090 VIA GADGET_FLAG$=1
1095  }
1100 ! normal processing
1110 GOSUB GET_INFO
1200 ! print info
1210 LIST_BOX LOAD PLATINUM_LIST.CTL,""
1215 READ (PLATINUM,KEY="",DOM=*NEXT)
1220 WHILE 1
1225 READ (PLATINUM,END=*BREAK)COMP$,TODAY,TOTAL
1230 SET_PARAM 'XI'; DIM SS1$(1000); FIND (Z[1],KEY="00000"+COMP$+"00",DOM=*NEXT)SS1$
1231 SET_PARAM -'XI'
1240 LIST_BOX LOAD PLATINUM_LIST.CTL,0,""+SEP+STP(STR(TODAY:MASK1$),2)+SEP+STP(STR(TOTAL:MASK1$),2)+SEP+COMP$+SEP+STP(SS1$(11,35))
1280 WEND 
1295 CLOSE (PLATINUM)
1995 GOTO 9900
2000 GET_INFO:! Records for today
2005 PLATINUM=HFN; OPEN (PLATINUM)"*memory*"
2010 READ (%SSP_OPEN,KEY="",DOM=*NEXT)
2015 TODAY$=DTE(JUL(0,0,0):"YYYYMMDD"),TODAY$=CHR(NUM(TODAY$(1,3))-125)+TODAY$(4)
2020 READ_NEXT_ZW2: K$=KEY(%SSP_OPEN,END=READING_DONE); READ (%SSP_OPEN,KEY=K$)ZW2$
2025 IF ZW2$(359,1)="A" THEN {
2030 READ (PLATINUM,KEY=ZW2$(245,3),DOM=*NEXT)COMP$,TODAY,TOTAL
2035 TOTAL+=1; IF ZW2$(7,6)=TODAY$ THEN TODAY+=1
2040 WRITE (PLATINUM,KEY=ZW2$(245,3))ZW2$(245,3),TODAY,TOTAL
2045  }
2075 GOTO READ_NEXT_ZW2
2080 READING_DONE:
2095 RETURN 
2099 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
3000 QRY_BUTTON:! Query button pressed
3005 ENTER X3$,X4$,OPTIONS$
3010 WDW_WIDE=100,WDW_HIGH=25; GOSUB CENTER_WDW; PRINT 'DIALOGUE'(UP_LEFT_X,UP_LEFT_Y,WDW_WIDE,WDW_HIGH,4,"Platinum Issues",'B?',OPT="c"),'CS'
3014 FMT$="[Issue]L6 [Client]L6 [Date]MDYL8 [Time]L5 [Assigned]C8 [Status]C7 [Program]C8 [Description]L65 "
3015 LIST_BOX 1110,@(0,2,WDW_WIDE-2,WDW_HIGH-4),FNT="Arial,-12",OPT="r",FMT=FMT$
3016 BUTTON BT_EXCEL.CTL,@(WDW_WIDE-14,0,12,1.25)="{!EXCEL} Export",FNT="Arial,-12",OPT="S"
3017 SETCTL BT_EXCEL.CTL:EXP_EXCEL
3030 SET_PARAM 'XI'
3031 SETCTL 1110:NOTES_PREVIEW
3035 TODAY$=DTE(JUL(0,0,0):"YYYYMMDD"),TODAY$=CHR(NUM(TODAY$(1,3))-125)+TODAY$(4); READ (%SSP_OPEN,KEY="999999",DOM=*PROCEED)
3040 RN_ZW2: K$=KEP(%SSP_OPEN,END=READ_DONE); READ (%SSP_OPEN,KEY=K$)ZW2$
3045 IF MID(ZW2$,359,1)="A" THEN COLOR$="" ELSE GOTO RN_ZW2
3050 CALL "ZZDISP","DX",ZW2$(7,6),"",%X3$,TF_DATE$,"",0,0,X4$
3065 LIST_BOX LOAD 1110,0,COLOR$+ZW2$(1,6)+SEP+COLOR$+DIM(3)+ZW2$(245,3)+SEP+COLOR$+TF_DATE$+SEP+COLOR$+ZW2$(13,2)+":"+STR(INT(NUM(ZW2$(15,2))*60/100):"00")+SEP+ZW2$(191,3)+SEP+ZW2$(195,1)+SEP+ZW2$(175,8)+SEP+STP(ZW2$(53,60))+" "+STP(ZW2$(113,60))
3070 GOTO RN_ZW2
3075 READ_DONE:
3085 SET_PARAM -'XI'
3090 OBTAIN (0)'CO',IN$; CC=CTL; IF CTL<>4 THEN GOTO *SAME ELSE CALL "SSG000;DROP_PREVIEW_WINDOW",ERR=*NEXT,WIN_HANDLE; PRINT 'POP',
3095 EXIT 
3099 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
4000 EXP_EXCEL:
4005 LB_VAR=1110
4010 PERFORM "UTGEXP;EXPORT",ERR=*NEXT
4095 RETURN 
4099 ! 
5000 SETUP_FIELDS:! Setup fields in the Gadget
5005 MULTI_LINE TITLE.CTL,@(FN_COLUMN(0),FN_ROW(0),WIDTH_COL-.3-%NOMAD_QRY_WIDE,1),OPT="BL",FNT=FONT$; TITLE.CTL'BACKCOLOR$="DARK GRAY",TITLE.CTL'TEXTCOLOR$="WHITE",TITLE.CTL'VALUE$=" Open Platinum Issues"
5010 FMT$="[]L1 [Today]C5 [Total]C5 [Cmp]C5 [Name]L20"
5020 LIST_BOX PLATINUM_LIST.CTL,@(FN_COLUMN(0)+.35,FN_ROW(2.5),WIDTH_COL-.8,HEIGHT_ROW-2.8),FNT="Arial,-12",OPT="r",FMT=FMT$
5080 QRY_BUTTON.CTL=FN_ADD_CTL(PGN+";QRY_BUTTON",""); BUTTON QRY_BUTTON.CTL,@(FN_COLUMN(WIDTH_COL-%NOMAD_QRY_WIDE-.3),FN_ROW(.1),%NOMAD_QRY_WIDE,1.2)=%NOMAD_QRY_BTN$,OPT="SF"
5095 RETURN 
5099 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
5100 BORDER:! Border around Gadget, Alert =0 for normal, 1 for Attention, 2 for Alert
5105 SWITCH ALERT
5106 CASE 0; PEN_COLOR=8; BREAK ! Dark Gray
5107 CASE 1; PEN_COLOR=12; BREAK ! Dark Blue
5108 CASE 2; PEN_COLOR=1; BREAK ! Light Red
5114 DEFAULT ; PEN_COLOR=8 ! Dark Gray
5115 END SWITCH 
5120 IF VIN(GADGET_FLAG$)=0 THEN FILL_IT=1 ELSE FILL_IT=0 ! Only fill rectangle if first time through
5150 PRINT 'FILL'(FILL_IT,15),
5155 PRINT 'PEN'(1,2,PEN_COLOR),
5160 PRINT 'RECTANGLE'(UP_LEFT_X_PIXEL,UP_LEFT_Y_PIXEL,UP_LEFT_X_PIXEL+WIDTH_PIXEL,UP_LEFT_Y_PIXEL+HEIGHT_PIXEL),
5195 RETURN 
5199 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
8800 DEF FN_ADD_CTL(LOCAL ACTION$, LOCAL OPTIONS$)
8801 ! Add next control to CTL_LIST, if CTL_LIST blank then start at 5000
8810 LK$="5000"; LK$=KEL(CTL_LIST,ERR=*NEXT)
8815 NEW_CTL=NUM(LK$)+1
8820 WRITE (CTL_LIST,KEY=STR(NEW_CTL))ACTION$,OPTIONS$
8840 RETURN NEW_CTL
8845 END DEF
8849 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
8900 DEF FN_COLUMN(LOCAL COLUMN)
8905 ! Correct column including UP_LEFT_COL                  
8910 IF UP_LEFT_COL+COLUMN<1 THEN RETURN UP_LEFT_COL+COLUMN ELSE RETURN UP_LEFT_COL+COLUMN-1
8915 END DEF
8916 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
8950 DEF FN_ROW(LOCAL ROW)
8955 ! Correct Row including UP_LEFT_ROW                 
8960 IF UP_LEFT_ROW+ROW<1 THEN RETURN UP_LEFT_ROW+ROW ELSE RETURN UP_LEFT_ROW+ROW-1
8965 END DEF
8966 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
9000 REM "ERROR PROCESSING                                                 
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL                                
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC                                       
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 3050,2040
9900 REM "END PROGRAM                                      
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
14000 CENTER_WDW:
14005 GOSUB GET_XYINFO
14010 UP_LEFT_X=INT((_XMAX/_XCHAR-WDW_WIDE)/2)
14015 UP_LEFT_Y=INT((_YMAX/_YCHAR-WDW_HIGH)/2)
14020 RETURN 
14049 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
14050 GET_XYINFO:
14055 IF _XCHAR<>0 THEN RETURN 
14060 _X$=MSE
14065 _XCHAR=DEC($00$+_X$(10,1))
14070 _YCHAR=DEC($00$+_X$(11,1))
14075 _XMAX=DEC(_X$(27,2))
14080 _YMAX=DEC(_X$(29,2))
14085 _WIN_VER=TCB(25)
14090 RETURN 
14099 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
15000 NOTES_PREVIEW:
15005 LIST_BOX READ 1110,A$,ERR=*RETURN
15010 CALL "SSG000;SHOW_NOTES_PREVIEW",A$,WIN_HANDLE,RET_CODE
15090 RETURN 
