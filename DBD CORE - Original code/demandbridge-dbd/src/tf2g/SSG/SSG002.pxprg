0010 REM "Today's Issues <DBG002>                                     
0020 SETESC 9300; SETERR 9000
0035 REM "5.5 - 06/14/06 - 9.719722 - kmc - SSP# 196716
0037 REM "196716-Dashboards                                                  
0040 REM "Copyright 2006 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,CTL_LIST,GADGET_NUMBER,OPTIONS{ALL},RETURN$
0100 SETERR 9000
0110 X0$="DBG002",X1$="Today's Issues"
0200 REM "                                                                 
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "IOLISTS                                                          
0500 REM "FILES                                                            
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O ZW2...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0599 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
0600 ! Initialize anything needed
0605 GADGET_FLAG$="%GADGET_"+%CURR_GADGET.DB_GADGET_INDEX$ ! will be 0 if first time through
0610 FONT$="Arial,-16,B"
0620 UP_LEFT_X_PIXEL=@X(%CURR_GADGET.UP_LEFT_COL-1),UP_LEFT_Y_PIXEL=@Y(%CURR_GADGET.UP_LEFT_ROW-1) ! -1 To get top left of value
0625 WIDTH_PIXEL=@X(%CURR_GADGET.WIDTH),HEIGHT_PIXEL=@Y(%CURR_GADGET.HEIGHT) ! no minus 1, to get bottom and right edge
0626 WIDTH_COL=%CURR_GADGET.WIDTH,HEIGHT_ROW=%CURR_GADGET.HEIGHT
0630 UP_LEFT_COL=%CURR_GADGET.UP_LEFT_COL,UP_LEFT_ROW=%CURR_GADGET.UP_LEFT_ROW
0635 RIGHT_COL=%CURR_GADGET.UP_LEFT_COL+%CURR_GADGET.WIDTH,BOTTOM_ROW=%CURR_GADGET.UP_LEFT_ROW+%CURR_GADGET.HEIGHT
0665 CUR_CTL=15000+(100*GADGET_NUMBER)
0670 TITLE.CTL=++CUR_CTL,ISSUES_TODAY_TEXT.CTL=++CUR_CTL,UNASSIGNED_TEXT.CTL=++CUR_CTL,OPEN_STILL_TEXT.CTL=++CUR_CTL
0675 ISSUES_TODAY.CTL=++CUR_CTL,UNASSIGNED.CTL=++CUR_CTL,OPEN_STILL.CTL=++CUR_CTL
0680 MASK1$="#,##0"
0699 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
1000 REM "BEGIN MAIN PROCESS                                               
1005 IF VIN(GADGET_FLAG$)=0 THEN {! First time through
1020 ALERT=0; GOSUB BORDER
1035 GOSUB SETUP_FIELDS
1090 VIA GADGET_FLAG$=1
1095  }
1100 ! normal processing
1110 GOSUB GET_INFO
1200 ! print info
1210 ISSUES_TODAY.CTL'VALUE$=STR(ISSUES_TODAY)
1215 UNASSIGNED.CTL'VALUE$=STR(UNASSIGNED)
1220 OPEN_STILL.CTL'VALUE$=STR(OPEN_STILL)
1995 GOTO 9900
2000 GET_INFO:! Records for today
2005 ISSUES_TODAY=0,OPEN_STILL=0,NOT_TODAY=0,UNASSIGNED=0
2006 SET_PARAM 'XI'
2010 READ (Z[1],KEY="40000",DOM=*NEXT) ! Only look at numbered issues
2015 TODAY$=DTE(JUL(0,0,0):"YYYYMMDD"),TODAY$=CHR(NUM(TODAY$(1,3))-125)+TODAY$(4)
2020 READ_NEXT_ZW2: K$=KEP(Z[1],END=READING_DONE); READ (Z[1],KEY=K$)ZW2$
2024 IF POS("9"<ZW2$(1,6))<>0 OR ZW2$(191,3)="WZE" OR POS(ZW2$(173,2)="INWODVEH",2)<>0 THEN GOTO READ_NEXT_ZW2
2025 IF ZW2$(7,6)<>TODAY$ THEN NOT_TODAY+=1; IF NOT_TODAY>100 THEN GOTO READING_DONE ELSE GOTO READ_NEXT_ZW2
2030 ISSUES_TODAY+=1; IF POS(ZW2$(195,1)="CFLZYGU")=0 THEN OPEN_STILL+=1
2035 IF NUL(ZW2$(191,3)) THEN UNASSIGNED+=1
2075 GOTO READ_NEXT_ZW2
2080 READING_DONE:
2085 SET_PARAM -'XI'
2095 RETURN 
2099 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
3000 QRY_BUTTON:! Query button pressed
3005 ENTER X3$,X4$,OPTIONS$
3006 JUL_TODAY=JUL(0,0,0); CUR_CTL=10000; BT_EXCEL.CTL=++CUR_CTL,ML_FILTER.CTL=++CUR_CTL,BT_FILTER.CTL=++CUR_CTL
3007 BT_PREV.CTL=++CUR_CTL,BT_NEXT.CTL=++CUR_CTL
3010 DIM Z[NUM(X3$(60,3))]; Z$="01O ZW2...  13O ZZPARM  "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO *NEXT,9900
3015 WDW_WIDE=100,WDW_HIGH=25; GOSUB CENTER_WDW; PRINT 'DIALOGUE'(UP_LEFT_X,UP_LEFT_Y,WDW_WIDE,WDW_HIGH,4,"Issues Today - "+DTE(0:"MM/DD/YYYY"),'B?',OPT="c"),'CS'
3020 FMT$="[Issue]L6 [Client]L6 [Time]L5 [Assigned]C8 [Status]C7 [Program]C8 [Description]L65 "
3025 LIST_BOX 1110,@(0,2,WDW_WIDE-2,WDW_HIGH-4),FNT="Arial,-12",OPT="r",FMT=FMT$
3026 BUTTON BT_EXCEL.CTL,@(WDW_WIDE-14,0,12,1.75)="{!EXCEL} Export",FNT="Arial,-12",OPT="S"
3027 SETCTL BT_EXCEL.CTL:EXP_EXCEL
3028 MULTI_LINE ML_FILTER.CTL,@(15,.5,8,1),FNT="Arial,-12",OPT="S"; BUTTON BT_FILTER.CTL,@(23,.4,3,1.00)="{!Repeat}",FNT="Arial,-12",OPT="S"
3029 BUTTON BT_PREV.CTL,@(3,0,5,1.75)="{!Arrow_left}",FNT="ARIAL,-12",OPT="S"; BUTTON BT_NEXT.CTL,@(8,0,5,1.75)="{!Arrow_right}",FNT="ARIAL,-12",OPT="S"
3030 SET_PARAM 'XI'
3031 SETCTL 1110:NOTES_PREVIEW
3033 SETCTL BT_FILTER.CTL:FILTER_RESULTS
3034 SETCTL BT_PREV.CTL:LOAD_PREVIOUS; SETCTL BT_NEXT.CTL:LOAD_NEXT
3035 TODAY$=DTE(JUL_TODAY:"YYYYMMDD"),TODAY$=CHR(NUM(TODAY$(1,3))-125)+TODAY$(4); READ (Z[1],KEY="40000",DOM=*PROCEED)
3036 GOTO RN_ZW2
3039 FILTER_RESULTS: MULTI_LINE READ ML_FILTER.CTL,FILTER$; IF NOT(NUL(FILTER$)) THEN LIST_BOX LOAD 1110,""; TODAY$=CHR(NUM(FILTER$(7,3))-125)+FILTER$(10,1)+FILTER$(1,2)+FILTER$(4,2); NOT_TODAY=0; READ (Z[1],KEY="40000",DOM=*PROCEED); SET_PARAM 'XI'
3040 RN_ZW2: K$=KEP(Z[1],END=READ_DONE); READ (Z[1],KEY=K$)ZW2$
3045 IF POS("9"<ZW2$(1,6))<>0 OR ZW2$(191,3)="WZE" OR POS(ZW2$(173,2)="INIDWODVEH",2)<>0 THEN GOTO RN_ZW2
3050 IF ZW2$(7,6)<>TODAY$ THEN NOT_TODAY+=1; IF NOT_TODAY>500 THEN GOTO READ_DONE ELSE GOTO RN_ZW2
3055 IF MID(ZW2$,359,1)="A" THEN COLOR$='_YELLOW'+'WHITE' ELSE COLOR$=""
3060 LIST_BOX LOAD 1110,0,COLOR$+ZW2$(1,6)+SEP+COLOR$+DIM(3)+ZW2$(245,3)+SEP+COLOR$+ZW2$(13,2)+":"+STR(INT(NUM(ZW2$(15,2))*60/100):"00")+SEP+ZW2$(191,3)+SEP+ZW2$(195,1)+SEP+ZW2$(175,8)+SEP+STP(ZW2$(53,60))+" "+STP(ZW2$(113,60))
3065 GOTO RN_ZW2
3070 READ_DONE:
3085 SET_PARAM -'XI'
3090 OBTAIN (0)'CO',IN$; CC=CTL; IF CTL<>4 THEN GOTO *SAME ELSE CALL "SSG000;DROP_PREVIEW_WINDOW",ERR=*NEXT,WIN_HANDLE; PRINT 'POP',
3091 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
3095 EXIT 
3099 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
4000 EXP_EXCEL:
4005 LB_VAR=1110
4010 PERFORM "UTGEXP;EXPORT",ERR=*NEXT
4095 RETURN 
4099 ! 
4100 LOAD_PREVIOUS:
4105 IF JUL_OFFSET<-3 THEN MSGBOX "Sorry, can't go back more than four days here! That's too much work!" ELSE JUL_OFFSET--
4195 GOTO RELOAD_LIST
4199 ! 
4200 LOAD_NEXT:
4205 IF JUL_OFFSET>=0 THEN MSGBOX "Sorry, can't go beyond today here!" ELSE JUL_OFFSET++
4295 GOTO RELOAD_LIST
4299 ! 
4300 RELOAD_LIST:
4310 MULTI_LINE WRITE ML_FILTER.CTL,DTE(JUL_TODAY+JUL_OFFSET:"MM/DD/YYYY")
4320 PRINT 'CAPTION'("Issues for "+DTE(JUL_TODAY+JUL_OFFSET:"MM/DD/YYYY"))
4395 GOTO FILTER_RESULTS
4399 ! 
5000 SETUP_FIELDS:! Setup fields in the Gadget
5005 MULTI_LINE TITLE.CTL,@(FN_COLUMN(0),FN_ROW(0),WIDTH_COL-1.3-%NOMAD_QRY_WIDE,1),OPT="BL",FNT=FONT$; TITLE.CTL'BACKCOLOR$="DARK GRAY",TITLE.CTL'TEXTCOLOR$="WHITE",TITLE.CTL'VALUE$="  Issues Today"
5020 MULTI_LINE ISSUES_TODAY_TEXT.CTL,@(FN_COLUMN(0),FN_ROW(2.75),14,1),OPT="BRL",FNT=FONT$; ISSUES_TODAY_TEXT.CTL'BACKCOLOR$="LIGHT GRAY",ISSUES_TODAY_TEXT.CTL'TEXTCOLOR$="BLACK",ISSUES_TODAY_TEXT.CTL'VALUE$="Entered Today:"
5025 MULTI_LINE ISSUES_TODAY.CTL,@(FN_COLUMN(14),FN_ROW(2.75),5,1),OPT="BRL",FNT=FONT$,FMT=MASK1$; ISSUES_TODAY.CTL'BACKCOLOR$="LIGHT GRAY",ISSUES_TODAY.CTL'TEXTCOLOR$="BLACK",ISSUES_TODAY.CTL'VALUE$="0"
5030 MULTI_LINE UNASSIGNED_TEXT.CTL,@(FN_COLUMN(0),FN_ROW(4.50),14,1),OPT="BRL",FNT=FONT$; UNASSIGNED_TEXT.CTL'BACKCOLOR$="LIGHT GRAY",UNASSIGNED_TEXT.CTL'TEXTCOLOR$="BLACK",UNASSIGNED_TEXT.CTL'VALUE$="Unassigned:"
5035 MULTI_LINE UNASSIGNED.CTL,@(FN_COLUMN(14),FN_ROW(4.50),5,1),OPT="BRL",FNT=FONT$,FMT=MASK1$; UNASSIGNED.CTL'BACKCOLOR$="LIGHT GRAY",UNASSIGNED.CTL'TEXTCOLOR$="BLACK",UNASSIGNED.CTL'VALUE$="0"
5040 MULTI_LINE OPEN_STILL_TEXT.CTL,@(FN_COLUMN(0),FN_ROW(6.25),14,1),OPT="BRL",FNT=FONT$; OPEN_STILL_TEXT.CTL'BACKCOLOR$="LIGHT GRAY",OPEN_STILL_TEXT.CTL'TEXTCOLOR$="BLACK",OPEN_STILL_TEXT.CTL'VALUE$="Still Open:"
5045 MULTI_LINE OPEN_STILL.CTL,@(FN_COLUMN(14),FN_ROW(6.25),5,1),OPT="BRL",FNT=FONT$,FMT=MASK1$; OPEN_STILL.CTL'BACKCOLOR$="LIGHT GRAY",OPEN_STILL.CTL'TEXTCOLOR$="BLACK",OPEN_STILL.CTL'VALUE$="0"
5080 QRY_BUTTON.CTL=FN_ADD_CTL(PGN+";QRY_BUTTON",""); BUTTON QRY_BUTTON.CTL,@(FN_COLUMN(WIDTH_COL-%NOMAD_QRY_WIDE-.3),FN_ROW(.1),%NOMAD_QRY_WIDE,1.2)=%NOMAD_QRY_BTN$,OPT="SF"
5095 RETURN 
5099 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
5100 BORDER:! Border around Gadget, Alert =0 for normal, 1 for Attention, 2 for Alert
5105 SWITCH ALERT
5106 CASE 0; PEN_COLOR=8; BREAK ! Dark Gray
5107 CASE 1; PEN_COLOR=12; BREAK ! Dark Blue
5108 CASE 2; PEN_COLOR=1; BREAK ! Light Red
5114 DEFAULT ; PEN_COLOR=8 ! Dark Gray
5115 END SWITCH 
5120 IF VIN(GADGET_FLAG$)=0 THEN FILL_IT=1 ELSE FILL_IT=0 ! Only fill rectangle if first time through
5150 PRINT 'FILL'(FILL_IT,15),
5155 PRINT 'PEN'(1,2,PEN_COLOR),
5160 PRINT 'RECTANGLE'(UP_LEFT_X_PIXEL,UP_LEFT_Y_PIXEL,UP_LEFT_X_PIXEL+WIDTH_PIXEL,UP_LEFT_Y_PIXEL+HEIGHT_PIXEL),
5195 RETURN 
5199 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
8800 DEF FN_ADD_CTL(LOCAL ACTION$, LOCAL OPTIONS$)
8801 ! Add next control to CTL_LIST, if CTL_LIST blank then start at 5000
8810 LK$="5000"; LK$=KEL(CTL_LIST,ERR=*NEXT)
8815 NEW_CTL=NUM(LK$)+1
8820 WRITE (CTL_LIST,KEY=STR(NEW_CTL))ACTION$,OPTIONS$
8840 RETURN NEW_CTL
8845 END DEF
8849 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
8900 DEF FN_COLUMN(LOCAL COLUMN)
8905 ! Correct column including UP_LEFT_COL                  
8910 IF UP_LEFT_COL+COLUMN<1 THEN RETURN UP_LEFT_COL+COLUMN ELSE RETURN UP_LEFT_COL+COLUMN-1
8915 END DEF
8916 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
8950 DEF FN_ROW(LOCAL ROW)
8955 ! Correct Row including UP_LEFT_ROW                 
8960 IF UP_LEFT_ROW+ROW<1 THEN RETURN UP_LEFT_ROW+ROW ELSE RETURN UP_LEFT_ROW+ROW-1
8965 END DEF
8966 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
9000 REM "ERROR PROCESSING                                                 
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL                                
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC                                       
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 3050,2040
9900 REM "END PROGRAM                                      
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
14000 CENTER_WDW:
14005 GOSUB GET_XYINFO
14010 UP_LEFT_X=INT((_XMAX/_XCHAR-WDW_WIDE)/2)
14015 UP_LEFT_Y=INT((_YMAX/_YCHAR-WDW_HIGH)/2)
14020 RETURN 
14049 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
14050 GET_XYINFO:
14055 IF _XCHAR<>0 THEN RETURN 
14060 _X$=MSE
14065 _XCHAR=DEC($00$+_X$(10,1))
14070 _YCHAR=DEC($00$+_X$(11,1))
14075 _XMAX=DEC(_X$(27,2))
14080 _YMAX=DEC(_X$(29,2))
14085 _WIN_VER=TCB(25)
14090 RETURN 
14099 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
15000 NOTES_PREVIEW:
15005 LIST_BOX READ 1110,A$,ERR=*RETURN
15010 CALL "SSG000;SHOW_NOTES_PREVIEW",A$,WIN_HANDLE,RET_CODE
15090 RETURN 
