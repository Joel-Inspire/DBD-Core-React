0010 REM "All Workorders<DBG005>                                     
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 10/17/13 - 15.676388 - tma - SSP# 266303
0037 REM "266303-Rejected WO's showing up in dash board                      
0040 REM "Copyright 2013 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,CTL_LIST,GADGET_NUMBER,OPTIONS{ALL},RETURN$
0100 SETERR 9000
0110 X0$="DBG005",X1$="All Workorders"
0200 REM "                                                                 
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "IOLISTS                                                          
0500 REM "FILES                                                            
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O ZW2...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0599 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
0600 ! Initialize anything needed
0605 GADGET_FLAG$="%GADGET_"+%CURR_GADGET.DB_GADGET_INDEX$ ! will be 0 if first time through
0610 FONT$="Arial,-16,B",LARGE_FONT$="Arial,-48,B"
0620 UP_LEFT_X_PIXEL=@X(%CURR_GADGET.UP_LEFT_COL-1),UP_LEFT_Y_PIXEL=@Y(%CURR_GADGET.UP_LEFT_ROW-1) ! -1 To get top left of value
0625 WIDTH_PIXEL=@X(%CURR_GADGET.WIDTH),HEIGHT_PIXEL=@Y(%CURR_GADGET.HEIGHT) ! no minus 1, to get bottom and right edge
0626 WIDTH_COL=%CURR_GADGET.WIDTH,HEIGHT_ROW=%CURR_GADGET.HEIGHT
0630 UP_LEFT_COL=%CURR_GADGET.UP_LEFT_COL,UP_LEFT_ROW=%CURR_GADGET.UP_LEFT_ROW
0635 RIGHT_COL=%CURR_GADGET.UP_LEFT_COL+%CURR_GADGET.WIDTH,BOTTOM_ROW=%CURR_GADGET.UP_LEFT_ROW+%CURR_GADGET.HEIGHT
0665 CUR_CTL=15000+(100*GADGET_NUMBER)
0670 TITLE.CTL=++CUR_CTL,OPEN_ISSUES_TEXT.CTL=++CUR_CTL
0675 OPEN_ISSUES.CTL=++CUR_CTL
0680 MASK1$="#,##0"
0685 BT_EXCEL.CTL=++CUR_CTL
0699 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
1000 REM "BEGIN MAIN PROCESS                                               
1005 IF VIN(GADGET_FLAG$)=0 THEN {! First time through
1020 ALERT=0; GOSUB BORDER
1035 GOSUB SETUP_FIELDS
1090 VIA GADGET_FLAG$=1
1095  }
1099 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
1100 ! normal processing
1110 GOSUB GET_INFO
1200 ! print info
1210 OPEN_ISSUES.CTL'VALUE$=STR(OPEN_ISSUES)
1995 GOTO 9900
1999 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
2000 GET_INFO:! Records for today
2005 IF %WKO_OPEN THEN CLOSE (%WKO_OPEN) END_IF ; %WKO_OPEN=HFN; OPEN (%WKO_OPEN)"*memory*"
2006 SET_PARAM 'XI'
2010 READ (Z[1],KNO=4,KEY="",DOM=*NEXT)
2020 READ_NEXT_ZW2: K$=KEY(Z[1],KNO=4,END=READING_DONE)
2025 IF POS(K$(1,1)="CDFLZYGUI") THEN READ (Z[1],KNO=4,KEY=K$(1,1)+$FE$,DOM=*PROCEED); GOTO READ_NEXT_ZW2 ELSE READ (Z[1],KNO=4,KEY=K$)ZW2$ ! SSP#266303
2030 IF POS(ZW2$(173,2)="WO",2)=0 THEN GOTO READ_NEXT_ZW2
2035 WRITE (%WKO_OPEN,KEY=ZW2$(1,6))ZW2$
2075 GOTO READ_NEXT_ZW2
2080 READING_DONE:
2085 SET_PARAM -'XI'
2090 OPEN_ISSUES=0; OPEN_ISSUES=NUM(FIN(%WKO_OPEN,"NUMREC"),ERR=*NEXT)
2095 RETURN 
2099 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
3000 QRY_BUTTON:! Query button pressed
3005 ENTER X3$,X4$,OPTIONS$
3010 WDW_WIDE=100,WDW_HIGH=25; GOSUB CENTER_WDW; PRINT 'DIALOGUE'(UP_LEFT_X,UP_LEFT_Y,WDW_WIDE,WDW_HIGH,4,"All Workorders",'B?',OPT="c"),'CS'
3014 FMT$="[Issue]L6 [Client]L6 [Date]MDYL8 [Time]L5 [Assigned]C8 [Status]C7 [Program]L8 [Description]L65 "
3015 LIST_BOX 1110,@(0,2,WDW_WIDE-2,WDW_HIGH-4),FNT="Arial,-12",OPT="r",FMT=FMT$
3016 BUTTON BT_EXCEL.CTL,@(WDW_WIDE-14,0,12,1.25)="{!EXCEL} Export",FNT="Arial,-12",OPT="S"
3017 SETCTL BT_EXCEL.CTL:EXP_EXCEL
3018 SETCTL 1110:NOTES_PREVIEW
3020 READ (%WKO_OPEN,KEY=$FE$,DOM=*NEXT,END=*NEXT)
3040 RN_ZW2: K$=KEP(%WKO_OPEN,END=READ_DONE); READ (%WKO_OPEN,KEY=K$)ZW2$
3045 IF POS(ZW2$(173,2)="WO",2)=0 THEN GOTO RN_ZW2
3050 CALL "ZZDISP","DX",ZW2$(7,6),"",%X3$,TF_DATE$,"",0,0,X4$
3060 IF MID(ZW2$,359,1)="A" THEN COLOR$='_YELLOW'+'WHITE' ELSE COLOR$=""
3065 LIST_BOX LOAD 1110,0,COLOR$+ZW2$(1,6)+SEP+COLOR$+DIM(3)+ZW2$(245,3)+SEP+COLOR$+TF_DATE$+SEP+COLOR$+ZW2$(13,2)+":"+STR(INT(NUM(ZW2$(15,2))*60/100):"00")+SEP+ZW2$(191,3)+SEP+ZW2$(195,1)+SEP+ZW2$(175,8)+SEP+STP(ZW2$(53,60))+" "+STP(ZW2$(113,60))
3070 GOTO RN_ZW2
3075 READ_DONE:
3085 SET_PARAM -'XI'
3090 OBTAIN (0)'CO',IN$; CC=CTL; IF CTL<>4 THEN GOTO *SAME ELSE CALL "SSG000;DROP_PREVIEW_WINDOW",ERR=*NEXT,WIN_HANDLE; PRINT 'POP',
3095 EXIT 
3099 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
4000 EXP_EXCEL:
4005 LB_VAR=1110
4010 PERFORM "UTGEXP;EXPORT",ERR=*NEXT
4095 RETURN 
4099 ! 
5000 SETUP_FIELDS:! Setup fields in the Gadget
5005 MULTI_LINE TITLE.CTL,@(FN_COLUMN(0),FN_ROW(0),WIDTH_COL-1.2-%NOMAD_QRY_WIDE,1),OPT="BL",FNT=FONT$; TITLE.CTL'BACKCOLOR$="DARK GRAY",TITLE.CTL'TEXTCOLOR$="WHITE",TITLE.CTL'VALUE$=" All Workorders"
5025 MULTI_LINE OPEN_ISSUES.CTL,@(FN_COLUMN(0),FN_ROW(2.75),14,1),OPT="BRL",FNT=LARGE_FONT$,FMT=MASK1$; OPEN_ISSUES.CTL'BACKCKCOLOR$="LIGHT GRAY",OPEN_ISSUES.CTL'TEXTCOLOR$="BLACK",OPEN_ISSUES.CTL'VALUE$="0"
5080 QRY_BUTTON.CTL=FN_ADD_CTL(PGN+";QRY_BUTTON",""); BUTTON QRY_BUTTON.CTL,@(FN_COLUMN(WIDTH_COL-%NOMAD_QRY_WIDE-.3),FN_ROW(.1),%NOMAD_QRY_WIDE,1.2)=%NOMAD_QRY_BTN$,OPT="SF"
5095 RETURN 
5099 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
5100 BORDER:! Border around Gadget, Alert =0 for normal, 1 for Attention, 2 for Alert
5105 SWITCH ALERT
5106 CASE 0; PEN_COLOR=8; BREAK ! Dark Gray
5107 CASE 1; PEN_COLOR=12; BREAK ! Dark Blue
5108 CASE 2; PEN_COLOR=1; BREAK ! Light Red
5114 DEFAULT ; PEN_COLOR=8 ! Dark Gray
5115 END SWITCH 
5120 IF VIN(GADGET_FLAG$)=0 THEN FILL_IT=1 ELSE FILL_IT=0 ! Only fill rectangle if first time through
5150 PRINT 'FILL'(FILL_IT,15),
5155 PRINT 'PEN'(1,2,PEN_COLOR),
5160 PRINT 'RECTANGLE'(UP_LEFT_X_PIXEL,UP_LEFT_Y_PIXEL,UP_LEFT_X_PIXEL+WIDTH_PIXEL,UP_LEFT_Y_PIXEL+HEIGHT_PIXEL),
5195 RETURN 
5199 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
8800 DEF FN_ADD_CTL(LOCAL ACTION$, LOCAL OPTIONS$)
8801 ! Add next control to CTL_LIST, if CTL_LIST blank then start at 5000
8810 LK$="5000"; LK$=KEL(CTL_LIST,ERR=*NEXT)
8815 NEW_CTL=NUM(LK$)+1
8820 WRITE (CTL_LIST,KEY=STR(NEW_CTL))ACTION$,OPTIONS$
8840 RETURN NEW_CTL
8845 END DEF
8849 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
8900 DEF FN_COLUMN(LOCAL COLUMN)
8905 ! Correct column including UP_LEFT_COL                  
8910 IF UP_LEFT_COL+COLUMN<1 THEN RETURN UP_LEFT_COL+COLUMN ELSE RETURN UP_LEFT_COL+COLUMN-1
8915 END DEF
8916 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
8950 DEF FN_ROW(LOCAL ROW)
8955 ! Correct Row including UP_LEFT_ROW                 
8960 IF UP_LEFT_ROW+ROW<1 THEN RETURN UP_LEFT_ROW+ROW ELSE RETURN UP_LEFT_ROW+ROW-1
8965 END DEF
8966 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
9000 REM "ERROR PROCESSING                                                 
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL                                
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC                                       
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 3050,2040
9900 REM "END PROGRAM                                      
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
14000 CENTER_WDW:
14005 GOSUB GET_XYINFO
14010 UP_LEFT_X=INT((_XMAX/_XCHAR-WDW_WIDE)/2)
14015 UP_LEFT_Y=INT((_YMAX/_YCHAR-WDW_HIGH)/2)
14020 RETURN 
14049 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
14050 GET_XYINFO:
14055 IF _XCHAR<>0 THEN RETURN 
14060 _X$=MSE
14065 _XCHAR=DEC($00$+_X$(10,1))
14070 _YCHAR=DEC($00$+_X$(11,1))
14075 _XMAX=DEC(_X$(27,2))
14080 _YMAX=DEC(_X$(29,2))
14085 _WIN_VER=TCB(25)
14090 RETURN 
14099 ! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
15000 NOTES_PREVIEW:
15005 LIST_BOX READ 1110,A$,ERR=*RETURN
15010 CALL "SSG000;SHOW_NOTES_PREVIEW",A$,WIN_HANDLE,RET_CODE
15090 RETURN 
56000 REM "266303-Rejected WO's showing up in dash board                      
