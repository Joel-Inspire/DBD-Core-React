0010 ! ZRGZ10 - Proxy Printing - User specific
0035 REM "5.7 - 04/30/15 - 14.006388 - jvv - SSP# 276928
0037 REM "276928-User Specific Proxy Printing - does not print all copies of 
0040 REM "Copyright 2015 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 IF TCB(13)=1 THEN BEGIN 
0052 IF NUL(%WDX$) THEN MSGBOX "You must be using Windx to access DB Distributor to access the User Specific Proxy Printing"; GOTO 0075 ! SSP245712
0055 IF NOT(%GUI) THEN CHUI_MODE=1; %GUI=1
0060 PROCESS "ZRGZ10","../ZRG/ZR.EN"
0065 IF CHUI_MODE THEN %GUI=0; RUN "ZMENU"
0075 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0099 ! **************************************************
0110 INIT:
0120 _SV_KR=PRM('KR'); SET_PARAM 'KR'=0 ! Ensure native handling of KEP()
0125 CALL "ZZWMX3"
0130 GOSUB SETUP_LIBRARY; IF %FLMAINT_MSG$<>"" THEN MESSAGE_LIB %FLMAINT_MSG$
0140 CHANGE_FLG=0,_ENABLE_FLG=0,_KCNT=1,_FIRST_KEY=0,_FIRST_FIELD=0
0160 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; DIM Z[NUM(X3$(60,3))]
0165 Z$="01O ZRY... 02O ZRZ... 13O ZZPARM "
0170 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
0180 DIM ZR_PARM$(1000); FIND (Z[13],KEY=%X3$(9,3)+"PDF",DOM=*NEXT)ZR_PARM$(1)
0185 OPERATOR$=%X3_OP_ID$
0186 LENGTH_OF_TIMEOUT=5,SHOW_ALL_QUES=1
0190 GOSUB SETUP_DOCUMENTS
0191 IF TCB(88)<>0 THEN %WDX$="[wdx]" ELSE %WDX$=""
0195 RETURN 
0299 ! **************************************************
0300 ! 300 - Set up the message library
0310 SETUP_LIBRARY:
0320 _SV_MSGLIB$=MSG(*)
0330 _LANG$=LCS(%NOMAD_DEF_SFX$); IF _LANG$="" THEN _LANG$=LCS(ENV("PVXLANG")); IF _LANG$="" THEN _LANG$=LCS(ENV("LANG")); IF _LANG$="" THEN _LANG$="en"
0340 MESSAGE_LIB "*msglib."+_LANG$,ERR=*NEXT; GOTO 0360
0350 MESSAGE_LIB "*msglib.en"
0360 SET_MSG:GOSUB SETUP_MESSAGES
0370 RETURN 
0399 ! **************************************************
0400 ! ^100 - Retrieve messages from the *msglib.xxx library
0405 SETUP_MESSAGES:
0410 _FYI$=MSG("FYI"),_ERROR$=MSG("ERROR")
0415 _MSG_DIRECTORY$=MSG("DIRECTORY")
0420 _MSG_END_OF_FIL$=MSG("END_OF_FIL")
0425 _MSG_FILOPNERR1$=MSG("FILOPNERR1")
0430 _MSG_JUST_CHECK$=MSG("JUST_CHECK")
0435 _MSG_MANDATORY$=MSG("MANDATORY")
0440 _MSG_NEXT$=MSG("NEXT")
0445 _MSG_NON_NUMER$=MSG("NON_NUMER")
0450 _MSG_NOT_FOUND$=MSG("NOT_FOUND")
0455 _MSG_OVRWRT_CHG$=MSG("OVRWRT_CHG")
0460 _MSG_PRECEDING$=MSG("PRECEDING")
0465 _MSG_PREFIX$=MSG("PREFIX")
0470 _MSG_REC_ACCESS$=MSG("REC_ACCESS")
0475 _MSG_REC_ALTERD$=MSG("REC_ALTERD")
0480 _MSG_REC_CR_NEW$=MSG("REC_CR_NEW")
0485 _MSG_REC_LOCKED$=MSG("REC_LOCKED")
0490 _MSG_X$=MSG("REC_MISS","^"),P=POS("^"=_MSG_X$),_MSG_REC_MISS1$=_MSG_X$(1,P-1),_MSG_REC_MISS2$=_MSG_X$(P+1)
0495 _MSG_REC_NOTFND$=MSG("REC_NOTFND")
0500 _MSG_REC_NO_1ST$=MSG("REC_NO_1ST")
0505 _MSG_REC_NO_LST$=MSG("REC_NO_LST")
0510 _MSG_REC_REMOVE$=MSG("REC_REMOVE")
0515 _MSG_REC_UPDADD$=MSG("REC_UPDADD")
0520 _MSG_X$=MSG("REC_VFYDEL","^"),P=POS("^"=_MSG_X$),_MSG_REC_VFYDEL1$=_MSG_X$(1,P-1),_MSG_REC_VFYDEL2$=_MSG_X$(P+1)
0525 _MSG_X$=MSG("REC_VIEW","^"),P=POS("^"=_MSG_X$),_MSG_REC_VIEW1$=_MSG_X$(1,P-1),_MSG_REC_VIEW2$=_MSG_X$(P+1)
0530 _MSG_REQ_FIELDS$=MSG("REQ_FIELDS")
0535 _MSG_START_FILE$=MSG("START_FILE")
0540 _MSG_UPDATE$=MSG("UPDATE")
0545 _MSG_UPD_OTHER1$=MSG("UPD_OTHER1")
0550 _MSG_UPD_OTHER2$=MSG("UPD_OTHER2")
0555 _MSG_UPD_SAME$=MSG("UPD_SAME")
0560 _MSG_WARNING$=MSG("WARNING")
0565 RETURN 
0595 RETURN 
0599 ! **************************************************
0600 MAIN_POST_DISPLAY:
0605 RESPCTL=0,PRT=0
0610 GOSUB REFRESH_PDF_MONITOR
0620 GOSUB PRINT_DOCUMENTS
0630 PRINT 'MESSAGE'('_RED'+'WHITE'+"WAITING "+STR(LENGTH_OF_TIMEOUT)+" SEC",0)
0633 GOSUB UPDATE_STATUS ! 268710
0635 INPUT (0,LEN=1,TIM=LENGTH_OF_TIMEOUT,ERR=*NEXT)WAITPROMPT$
0636 PRINT 'MESSAGE'('F8'+'BLACK'+"",0),
0637 FCOUNT=0,PRT=0
0640 RESPCTL=CTL
0642 IF RESPCTL=10002 THEN CMD_STR$="END"; GOTO 0695
0680 GOTO 0600
0695 RETURN 
0699 ! **************************************************
0900 ! 900 - Wrapup
0910 WRAPUP:
0912 %NOMAD_TIMEOUT=0
0915 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0920 ARG_1$=KEC(_FIL_NO,ERR=*NEXT)
0940 IF _CUR_FN<>0 THEN CLOSE (_CUR_FN); _CUR_FN=0
0950 MESSAGE_LIB _SV_MSGLIB$
0960 SET_PARAM 'KR'=_SV_KR
0970 RETURN 
1000 SET_VARS:! based on DOC_INDEX, set variables to use for count or printing
1010 DIM ZRY$:CPL(%ZRY_IOL$); READ DATA FROM "" TO ZRY$; FIND (Z[1],KEY=%X3_COMP$+ZRZ.OPERATOR$,DOM=*NEXT,REC=ZRY$)
1015 IF POS(ZRZ.PAUSED$="SU")>0 THEN DOCUMENT_ACTIVE=1 ELSE DOCUMENT_ACTIVE=0
1020 BASE_PRINT_DIR$=STP(ZR_PARM$(7,60),1)
1025 BASE_ARCHIVE_DIR$=STP(ZR_PARM$(67,60),1)
1026 PRTW=0; PRTWI=NUM(ZRY.PRT_WAIT_INTVL$,ERR=*NEXT); PRTW=PRTWI ! SSP 276928
1030 PC_TEMP_DIR$=STP(ZR_PARM$(425,30),1); IF STP(ZRY.USER_TEMP_DIR$,1)<>"" THEN PC_TEMP_DIR$=STP(ZRY.USER_TEMP_DIR$,1)
1032 IF PC_TEMP_DIR$="" THEN PC_TEMP_DIR$="C:/" ELSE IF POS("/\"=PC_TEMP_DIR$(LEN(PC_TEMP_DIR$),1))=0 THEN PC_TEMP_DIR$=PC_TEMP_DIR$+"/"
1035 READER_LOCATION$=STP(ZR_PARM$(335,90),1); IF STP(ZRY.USER_READER_LOCATION$,1)<>"" THEN READER_LOCATION$=STP(ZRY.USER_READER_LOCATION$,1)
1040 PRINTER$=STP(ZRZ.PRINTER$,1)
1042 IF STP(PRINTER$,3)="" THEN WINPRT_SETUP READ PRINTER$; XP=POS(" on"=PRINTER$); IF XP>1 THEN PRINTER$=PRINTER$(1,XP-1)
1045 REM IF DRIVER_NAME$="" THEN GOSUB SET_PRINTER_DRIVER; DRIVER_NAME$=PRINT_DRIVER$
1047 GROUP_CODE_2$=ZRZ.GROUP_CODE$+ZRZ.GROUP_CODE_EXT$ ! SSP245387 jdf
1050 PDF_DIR$=BASE_PRINT_DIR$+%C$+DLM+ZRZ.DOC_CODE$; IF NOT(NUL(GROUP_CODE_2$)) THEN PDF_DIR$+=DLM+STP(GROUP_CODE_2$,1) ! SSP245387 jdf
1055 PRINT_CMD$="< find "+PDF_DIR$+" -name "+QUO+"*.pdf"+QUO+" -print 2> /dev/null | sort 2> /dev/null"
1060 COUNT_CMD$="< find "+PDF_DIR$+" -name "+QUO+"*.pdf"+QUO+" -print 2> /dev/null | wc -l 2> /dev/null"
1095 RETURN 
1099 ! **************************************************
1100 PRINT_DOCUMENTS:! Scan the directories of active documents & print them
1102 DIM FILE_Q$[2000],PRINT_Q$[2000]; FCOUNT=0
1105 READ (Z[2],KEY="",DOM=*NEXT,ERR=*NEXT,REC=TMP$)
1110 READ_NEXT_DOC:DIM ZRZ$:CPL(%ZRZ_IOL$); READ DATA FROM "" TO ZRZ$; KEY_2$=KEY(Z[2],END=DOC_DONE); READ (Z[2],END=DOC_DONE,REC=ZRZ$); IF ZRZ.OPERATOR$=X3$(40,3) AND POS(ZRZ.PAUSED$="SU") THEN ACTIVE=1 ELSE ACTIVE=0
1115 AT_LEAST_ONE=0
1125 IF NOT(DOCUMENT_ACTIVE) OR NOT(ACTIVE) THEN GOTO NEXT_DOCUMENT
1126 IF ZRZ.DOC_CODE$="PL" AND NOT(NUL(ZRZ.GROUP_CODE$+ZRZ.GROUP_CODE_EXT$)) THEN PRT=1; GOSUB CHECK_GROUP_PL; GOTO 1174 ! 270910
1127 GOSUB SET_VARS
1130 CMD_CHAN=HFN; OPEN (CMD_CHAN,ERR=NEXT_DOCUMENT)PRINT_CMD$
1140 READ_NEXT:READ (CMD_CHAN,END=NEXT_DOCUMENT,ERR=NEXT_DOCUMENT)A$
1160 GOSUB PRINT_DOCUMENT
1170 AT_LEAST_ONE+=1 ! update zrz printed count
1172 REM IF ++DPRINT>=5 THEN GOSUB REFRESH_PDF_MONITOR; DPRINT=0 ! Refresh PDF monitor periodically even if we haven't finished all the documents
1174 GOTO READ_NEXT
1175 NEXT_DOCUMENT:CLOSE (CMD_CHAN,ERR=*NEXT)
1176 REM IF AT_LEAST_ONE THEN GOSUB REFRESH_PDF_MONITOR
1180 GOTO READ_NEXT_DOC
1185 DOC_DONE:REM GOSUB REFRESH_PDF_MONITOR
1186 IF NUL(READER_LOCATION$) THEN RETURN ! 271499
1187 GOSUB CALL_ADOBE
1195 RETURN 
1199 ! **************************************************
1200 PRINT_DOCUMENT:! Print document named in A$
1202 FCOUNT=FCOUNT+1
1205 DOC_CHAN=HFN; OPEN INPUT (DOC_CHAN,ISZ=8000,ERR=*RETURN)A$ ! 247551 - if another session has consumed this job we will see an ERR 12 here, ignore it
1207 DUP$=""; IF POS("_"=A$)>0 THEN DUP$="_"
1210 M=MSK(A$,"/[^/]*$"); IF M=0 THEN PDF_NAME$=A$ ELSE PDF_NAME$=MID(A$,M+1)
1214 MASK$=SUB(MSG("DOC_CODES"),"|",")("),MASK$=".*("+MID(MASK$,1,LEN(MASK$)-1)
1215 M=MSK(A$,MASK$); IF M<>1 THEN PDF_BASE$="" ELSE PDF_BASE$=MID(A$,1,MSL)
1220 F$=PC_TEMP_DIR$+"TOPFORM"+%X3$(40,3)+DUP$+STR(FCOUNT)+".PDF"; IF NUL(READER_LOCATION$) THEN F$="*WINDEV*;"+STP(ZRZ.PRINTER$); GOTO 1246 ! 271499
1225 B=POS(BASE_PRINT_DIR$=PDF_BASE$); IF B=1 THEN ARCHIVE_PATH$=BASE_ARCHIVE_DIR$+MID(PDF_BASE$,LEN(BASE_PRINT_DIR$)+1)+DLM+PDF_NAME$ ELSE ARCHIVE_PATH$=BASE_ARCHIVE_DIR$+PDF_NAME$ ! use PDF_BASE which has group code removed and create ARCHIVE_PATH$
1230 ! Remove user name from archive path
1235 SERIAL %WDX$+F$,ERR=*PROCEED
1240 TMP_CHAN=HFN; OPEN PURGE (TMP_CHAN,ISZ=1,ERR=*NEXT)%WDX$+F$; GOTO 1248
1245 GOTO *RETURN
1246 TMP_CHAN=HFN; OPEN (TMP_CHAN,ISZ=1,ERR=*NEXT)%WDX$+F$; GOTO 1248 ! 271499
1247 GOTO *RETURN
1250 IF ZRY.DO_NOT_ARCHIVE$<>"Y" THEN SERIAL ARCHIVE_PATH$,ERR=*PROCEED; ARCHIVE_CHAN=HFN; OPEN PURGE (ARCHIVE_CHAN,ISZ=1)ARCHIVE_PATH$
1252 PRINT 'MESSAGE'("Building file"+STR(FCOUNT),0)
1255 READ RECORD (DOC_CHAN,END=*NEXT)J$; WRITE RECORD (TMP_CHAN,SIZ=LEN(J$))J$; IF ZRY.DO_NOT_ARCHIVE$<>"Y" THEN WRITE RECORD (ARCHIVE_CHAN,SIZ=LEN(J$))J$; GOTO *SAME ELSE GOTO *SAME
1260 CLOSE (TMP_CHAN); IF ZRY.DO_NOT_ARCHIVE$<>"Y" THEN CLOSE (ARCHIVE_CHAN)
1265 PAUSED$=ZRZ.PAUSED$
1275 CLOSE (DOC_CHAN); ERASE A$,ERR=*NEXT
1277 FILE_Q$[FCOUNT]=F$,PRINT_Q$[FCOUNT]=STP(ZRZ.PRINTER$)
1280 RETURN 
1299 ! **************************************************
1300 CALL_ADOBE:
1302 PRINT 'MESSAGE'("PREPARING FILES",0)
1305 IF FCOUNT=0 THEN GOTO *RETURN
1307 IF POS("gswin32"=READER_LOCATION$) THEN GOTO CALL_PDF
1310 FOR ADOBE=1 TO FCOUNT
1315 F$=FILE_Q$[ADOBE]
1320 DUP$="N"; IF POS("_"=F$)>0 THEN DUP$="Y"
1362 PRINT 'MESSAGE'('_GREEN'+'WHITE'+"CALLING ADOBE..",0)
1365 WCMD$=QUO+READER_LOCATION$+QUO+" /h /t "+F$+" "+QUO+PRINT_Q$[ADOBE]+QUO+" "+QUO+DRIVER_NAME$+QUO
1366 IF PAUSED$="S" THEN WCMD$=QUO+READER_LOCATION$+QUO+" "+F$ ! SSP#231318
1367 IF FIRST_CALL=0 AND DUP$="Y" THEN FIRST_CALL=1; INVOKE HIDE %WDX$+QUO+READER_LOCATION$+QUO
1368 IF FIRST_CALL=0 AND PAUSED$="U" THEN FIRST_CALL=1; INVOKE HIDE %WDX$+QUO+READER_LOCATION$+QUO
1369 IF PAUSED$="S" THEN MSGBOX "  ","Press Return to Retrieve Next Document" ! SSP#231318!SSP#236427
1370 INVOKE WAIT %WDX$+WCMD$
1375 NEXT ADOBE
1380 RETURN 
1399 ! **************************************************
1400 CALL_PDF:
1405 PRINT 'MESSAGE'("PREPARING FILES",0)
1410 IF FCOUNT=0 THEN GOTO *RETURN
1415 FOR ADOBE=1 TO FCOUNT
1420 F$=FILE_Q$[ADOBE]
1425 DUP$="N"; IF POS("_"=F$)>0 THEN DUP$="Y"
1430 PRINT 'MESSAGE'('_GREEN'+'WHITE'+"Processing PDF..",0)
1435 IF PAUSED$="S" THEN USE_SYSTEM_HELP=1 ELSE USE_SYSTEM_HELP=0; WCMD$=QUO+READER_LOCATION$+QUO+" -sDEVICE=mswinpr2 -sOutputFile="+QUO+"%printer%"+PRINT_Q$[ADOBE]+QUO+" -dQUIET -dNOPAUSE -dBATCH "+F$ ! 234747
1440 IF USE_SYSTEM_HELP THEN SYSTEM_HELP F$ ELSE IF PRTW THEN WAIT PRTW END_IF ; INVOKE HIDE WAIT %WDX$+WCMD$ ! 245082 - Added WAIT to allow docs to print in order SSP 276928
1445 IF PAUSED$="S" AND DUP$="N" THEN MSGBOX "  ","Press Return to Retrieve Next Document" ! SSP#236427
1450 NEXT ADOBE
1495 RETURN 
1499 ! **************************************************
1500 SET_WAITING:! set number waiting in que for ZRZ data
1515 WAITING=0
1520 GOSUB SET_VARS
1530 CMD_CHAN=HFN; OPEN (CMD_CHAN,ERR=*NEXT)COUNT_CMD$; READ (CMD_CHAN,ERR=*NEXT)CNT$; WAITING=NUM(CNT$,ERR=*NEXT)
1532 PRINT 'MESSAGE'("IN QUEUE: "+STR(WAITING),0)
1555 CLOSE (CMD_CHAN,ERR=*NEXT)
1595 RETURN 
1599 ! **************************************************
9999 END 
20000 REFRESH_PDF_MONITOR:
20103 CURR_GRID_INDEX=GRID_INDEX,GRID_INDEX=0
20105 PDF_MONITOR_IOL:IOLIST ACTIVE,OP_CODE$,DOC_NAME$,WAITING$,GROUP_CODE$,PRINTER$,PRINT_DRIVER$,ZRZ_KEY$,GROUP_CODE_EXT$ ! SSP245387 jdf
20106 READ (Z[2],KEY="",DOM=*NEXT)
20110 READ_NEXT_ZRZ: READ DATA FROM "" TO IOL=PDF_MONITOR_IOL; ACTIVE=0; DIM ZRZ$:CPL(%ZRZ_IOL$); READ DATA FROM "" TO ZRZ$; KEY_2$=KEY(Z[2],END=ZRZ_DONE); READ (Z[2],END=ZRZ_DONE,REC=ZRZ$); IF ZRZ.OPERATOR$=X3$(40,3) AND POS(ZRZ.PAUSED$="SU")<>0 THEN ACTIVE=1
20111 GROUP_CODE_2$=ZRZ.GROUP_CODE$+ZRZ.GROUP_CODE_EXT$ ! SSP245387 jdf
20112 PRINT 'MESSAGE'("Checking files",0)
20115 IF ACTIVE=1 THEN {
20120 READ (DOC_LIST,KEY=ZRZ.DOC_CODE$,DOM=*NEXT)DOC_NAME$
20122 OP_CODE$=ZRZ.OPERATOR$
20124 PRINTER$=ZRZ.PRINTER$
20125 DOC_NAME$=DOC_NAME$ ! DOC_NAMES read above
20130 IF ZRZ.DOC_CODE$="PL" THEN GOSUB CHECK_GROUP_PL; GOTO 20140
20137 GOSUB SET_WAITING; WAITING$=STR(WAITING:MSG("ZRGZ00_07"))
20140 GROUP_CODE$=STP(MID(GROUP_CODE_2$,1,30),1),GROUP_CODE_EXT$=STP(MID(GROUP_CODE_2$,31,90),1) ! SSP245387 jdf
20147 DRIVER_NAME$=PRINT_DRIVER$
20150 REM IF DRIVER_NAME$="" THEN GOSUB SET_PRINTER_DRIVER; DRIVER_NAME$=PRINT_DRIVER$
20155 ZRZ_KEY$=KEY_2$
20170 GRID LOAD PDF_MONITOR.CTL,0,++GRID_INDEX,REC(IOL=PDF_MONITOR_IOL)
20173 REM GRID LOCK PDF_MONITOR.CTL,0,GRID_INDEX,ERR=*PROCEED; GRID UNLOCK PDF_MONITOR.CTL,1,GRID_INDEX,ERR=*PROCEED ! Lock row except for 1st column
20174 GRID LOCK PDF_MONITOR.CTL,0,GRID_INDEX,ERR=*PROCEED
20175 PDF_MONITOR.CTL'ROW=GRID_INDEX,PDF_MONITOR.CTL'COLUMN=0; IF ACTIVE THEN PDF_MONITOR.CTL'BACKCOLOR$="White" ELSE PDF_MONITOR.CTL'BACKCOLOR$="Light Gray"
20179  }
20180 GOTO READ_NEXT_ZRZ
20185 ZRZ_DONE:
20190 IF GRID_INDEX<CURR_GRID_INDEX THEN FOR II=CURR_GRID_INDEX TO GRID_INDEX+1 STEP -1; GRID DELETE PDF_MONITOR.CTL,0,II,ERR=*PROCEED; NEXT II
20195 RETURN 
20199 ! **************************************************
20200 SETUP_DOCUMENTS:! Set arrays for documents to print
20210 MAX_DOC_INDEX=POS(MID(MSG("DOC_NAMES"),-1)=MSG("DOC_NAMES"),1,0) ! # of documents in list
20215 DIM TF_NAME$[MAX_DOC_INDEX],CODE$[MAX_DOC_INDEX]
20220 READ DATA FROM MSG("DOC_NAMES"),SEP=MID(MSG("DOC_NAMES"),-1) TO TF_NAME${ALL}
20225 READ DATA FROM MSG("DOC_CODES"),SEP=MID(MSG("DOC_CODES"),-1) TO CODE${ALL}
20229 ! Load into Doc_List memory file
20230 DOC_LIST=HFN; OPEN (DOC_LIST)"*MEMORY*"
20235 FOR DINDEX=0 TO MAX_DOC_INDEX-1
20240 WRITE (DOC_LIST,KEY=CODE$[DINDEX])TF_NAME$[DINDEX]
20250 NEXT DINDEX
20295 RETURN 
20299 ! **************************************************
20300 SET_PRINTER_DRIVER:! Attempt to get printer driver for the printer
20310 FOUND_PRINTER=0,PRINT_DRIVER$=""
20315 CALL "[wdx]*winapi;gettemppath",TDIR$
20320 INVOKE %WDX$+"regedit /E "+TDIR$+"pd.reg "+QUO+"HKEY_LOCAL_MACHINE\SYSTEM\CURRENTCONTROLSET\CONTROL\PRINT\PRINTERS"+QUO,ERR=GIVE_UP
20325 TCHAN=HFN; OPEN (TCHAN,ERR=GIVE_UP)%WDX$+TDIR$+"pd.reg"
20330 READ_NEXT_TCHAN:READ (TCHAN,END=FILE_DONE)REG_LINE$; REG_LINE$=STP(REG_LINE$,3,$00$) ! read in and remove all nulls
20335 IF NOT(FOUND_PRINTER) THEN IF MID(REG_LINE$,2,4)<>"Name" THEN GOTO READ_NEXT_TCHAN ELSE REG_LINE$=SUB(MID(REG_LINE$,9,LEN(REG_LINE$)-10),"\\","\"); IF REG_LINE$=STP(PRINTER$,1) THEN FOUND_PRINTER=1 END_IF ; GOTO READ_NEXT_TCHAN
20340 IF FOUND_PRINTER THEN IF MID(REG_LINE$,2,14)="Printer Driver" THEN PRINT_DRIVER$=MID(REG_LINE$,19,LEN(REG_LINE$)-20),REFRESH_FLG=1; GOTO FILE_DONE ELSE GOTO READ_NEXT_TCHAN
20360 FILE_DONE:
20390 GIVE_UP:
20395 RETURN 
20399 ! ************************************************ 
20400 POST_ADD_QUE:! Add Que button pressed
20445 %NOMAD_TIMEOUT=0
20450 PROCESS "ZRGPMB",""
20455 %NOMAD_TIMEOUT=LENGTH_OF_TIMEOUT
20457 SET_FOCUS 0
20460 REM GOSUB REFRESH_PDF_MONITOR
20495 RETURN 
20499 ! **************************************************
20500 TOGGLE_PAUSED_DISPLAY:! Toggle from showing all ques to only active ques
20520 IF SHOW_ALL_QUES THEN SHOW_ALL_QUES=0,ALL_QUE_BUTTON.CTL'TEXT$=MSG("ZRGZ00_12"),ALL_QUE_BUTTON.CTL'TIP$=MSG("ZRGZ00_13") ELSE SHOW_ALL_QUES=1,ALL_QUE_BUTTON.CTL'TEXT$=MSG("ZRGZ00_14"),ALL_QUE_BUTTON.CTL'TIP$=MSG("ZRGZ00_15")
20525 SET_FOCUS 0
20530 REM GOSUB REFRESH_PDF_MONITOR
20580 REFRESH_FLG=1
20595 RETURN 
20599 ! *************************************************
20600 PDF_MONITOR_SELECTION:! changed cell in PDF Monitor
20602 REM MSGBOX "CURRENT ROW="+STR(PDF_MONITOR.CTL'CURRENTROW)+" CURRENT COLUMN = "+STR(PDF_MONITOR.CTL'CURRENTCOLUMN)
20605 PDF_MONITOR.CTL'ROW=PDF_MONITOR.CTL'CURRENTROW,PDF_MONITOR.CTL'COLUMN=PDF_MONITOR.CTL'CURRENTCOLUMN
20610 GVALUE$=PDF_MONITOR.CTL'VALUE$,PDF_MONITOR.CTL'COLUMN=8,ZRZ_KEY$=PDF_MONITOR.CTL'VALUE$
20620 READ (Z[2],KEY=ZRZ_KEY$,DOM=PMS_DONE,REC=ZRZ$)
20630 IF GVALUE$="0" THEN ZRZ.PAUSED$="P" ELSE ZRZ.PAUSED$="Y"; REM P=paused, Y=print
20640 WRITE (Z[2],KEY=ZRZ_KEY$,REC=ZRZ$)
20685 PMS_DONE:
20687 SET_FOCUS 0
20690 REM GOSUB REFRESH_PDF_MONITOR
20695 RETURN 
20699 ! *********************************************
20700 POST_PDF_MONITOR:! selected a record from the listbox
20710 LIST_INDEX$=""; LIST_BOX READ PDF_MONITOR.CTL,LIST_INDEX$,ERR=*PROCEED
20715 IF LIST_INDEX$="" THEN RETURN ! nothing picked as far as we can tell
20720 ZRZ_KEY$=MID(LIST_INDEX$,POS(SEP=LIST_INDEX$,1,7)+1,8) ! Get first 8 chars of 7th column for key
20745 %NOMAD_TIMEOUT=0
20750 PROCESS "ZRGPMB","",ZRZ_KEY$
20755 %NOMAD_TIMEOUT=LENGTH_OF_TIMEOUT
20780 REM GOSUB REFRESH_PDF_MONITOR
20795 RETURN 
20799 ! **************************************************
21000 CHECK_GROUP_PL:
21005 GROUP_CODE_2$=ZRZ.GROUP_CODE$+ZRZ.GROUP_CODE_EXT$ ! SSP245387 jdf
21010 B_GROUP$=STP(GROUP_CODE_2$)+" ",SAVE_GROUP$=GROUP_CODE_2$,CC=0 ! SSP245387 jdf
21020 NXT_GROUP:
21030 XX=POS(" "=B_GROUP$); IF XX=0 THEN GOTO END_CHECK_GROUP_PL
21040 GROUP_CODE_2$=B_GROUP$(1,XX-1),B_GROUP$=B_GROUP$(XX+1) ! SSP245387 jdf
21045 ZRZ.GROUP_CODE$=GROUP_CODE_2$,ZRZ.GROUP_CODE_EXT$="" ! SSP245387 jdf
21050 PRINT 'MESSAGE'("Checking Group : "+GROUP_CODE_2$,0), ! SSP245387 jdf
21060 GOSUB SET_WAITING; WAITING$=STR(WAITING:MSG("ZRGZ00_07")),CC+=WAITING
21070 IF PRT THEN GOSUB CHECK_GROUP_PL_PRT; CLOSE (CMD_CHAN,ERR=*NEXT)
21080 GOTO NXT_GROUP
21090 END_CHECK_GROUP_PL:
21100 ZRZ.GROUP_CODE$=SAVE_GROUP$(1,30),ZRZ.GROUP_CODE_EXT$=SAVE_GROUP$(31,90),WAITING=CC,WAITING$=STR(WAITING:MSG("ZRGZ00_07")),CC=0 ! SSP245387 jdf
21110 RETURN 
21120 CHECK_GROUP_PL_PRT:
21130 CMD_CHAN=HFN; OPEN (CMD_CHAN,ERR=*RETURN)PRINT_CMD$
21140 READ (CMD_CHAN,END=*RETURN,ERR=*RETURN)A$
21150 GOSUB PRINT_DOCUMENT
21160 AT_LEAST_ONE+=1 ! update zrz printed count
21170 GOTO 21140
21180 RETURN 
22000 UPDATE_STATUS:
22020 CALL "UPDSV0;UPDATE_STATUS",ERR=*NEXT,"PROXYPRINT","USER "+OPERATOR$,"","Proxy Printing Monitor - "+OPERATOR$,"ZRGZ10",STR(TCB(89))
22045 RETURN 
22049 ! 
56005 REM "192539 & 194087 -PDF printing requires Adobe 6.0, this is a problem          
56007 REM "194087-Changes made at Whitepaper during 2nd week of install       
56009 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
56010 REM "234747-Having an issue with User Specific PDF Printing on Windows 7
56011 REM "245387-User Specific PDF Printing for Packing slips limits you     
56012 REM "245712-The name "PDF Printing" is not accurate or appropriate for  
56013 REM "247551-Two unattended printing sessions run into timing issue   
56014 REM "268710-Server status reporting framework - new file for servers to 
56015 REM "270910-Modify User Specific Proxy Printing to allow printing of PL 
56016 REM "271499-Proxy Printing - Ability to specify and use printer settings
56017 REM "276928-User Specific Proxy Printing - does not print all copies of 
