0010 REM "<CDGUAA> Import Cash Disbursements Gateway Information
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 02/11/14 - 15.256111 - dmm - SSP# 268013
0037 REM "268013-FY/AP validation in Cash Receipts Gateway, other gateways   
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 IF TCB(13)=1 THEN BEGIN 
0052 IF NUL(%WDX$) THEN MSGBOX "You must be using Windx to access this program"; GOTO 0075
0055 IF NOT(%GUI) THEN CHUI_MODE=1; %GUI=1; PRINT 'SHOW'(0)
0060 PROCESS "CDGUAA","../CDG/CD.EN"
0065 IF CHUI_MODE THEN %GUI=0; PRINT 'SHOW'(1); RUN "ZMENU"
0075 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0080 ! 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0101 INIT:
0110 X0$="CDGUAA",X1$="Import Cash Disbursements Gateway Information"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0165 DEF FND$(Z9$)=Z9$(NUM(X3$(48,1))*2+1,2)+X3$(59,1)+Z9$(7-NUM(X3$(48,1))*2,2)+X3$(59,1)+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
0200 REM "
0210 X0=-1
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOL_CD3:IOLIST CD3$(1),CD3{ALL}
0399 ! 
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O CD3... 03O AP4... 04O API... 05O ZYB... 13O ZZPARM "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0595 RETURN 
0599 ! 
1000 PROCEED:
1001 SEQ_NO=1
1005 REM "Get list of CD2 files to process
1010 CALL "ZZ2BLS","D0:CD2*","S",F$
1012 IF X3$(77,1)="U" THEN CALL "ZZ2BLS","D0:cd2*","S",F_LOWER$; F$=F$+F_LOWER$
1015 NEXT_FILE: P=POS(":"=F$); IF P=0 THEN GOTO 5000
1020 F1$=F$(P+1); P1=POS(":"=F1$); IF P1>0 THEN F$=F1$(P1-2),F1$=F1$(1,P1-3) ELSE F$=""
1050 Z$="01CU 01O "+F1$+" "
1055 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 1056,1995
1060 CLOSE (Z[1]); OPEN LOCK (Z[1],OPT="TEXT")F1$; REM "Open for reading raw file
1065 CLOSE (13); OPEN (13)"ZZPARM"; REM "Re-open ZZPARM closed in ZZ2BLS
1075 PRINT 'MESSAGE'('BR'+'_GREEN'+'WHITE'+"Processing: "+F1$,0),
1080 GOSUB 8100; GOSUB 8150
1100 REM "Process the file
1105 I9=0,I0$="",I1$="",F8=0
1110 NEXT_REC:
1115 READ (Z[1],END=END_OF_FILE)I$; I9=I9+1,COUNT=COUNT+1
1120 IF MOD(I9,T0)=1 THEN GOSUB 8150
1125 I$=PAD(I$,77); REM "Correct length if short, should be 77 bytes
1150 REM "Create CD3 record
1160 DIM CD3$(218),CD3[4]
1170 IF STP(I$(1,4),3," ")="" THEN CD3$(1,4)=CR_FY$ ELSE CD3$(1,4)=I$(1,4); REM "Fiscal Year
1172 DIM YEAR$(105); FIND (Z[13],KEY=X3$(9,3)+"G/LYE"+CD3$(1,4),DOM=*NEXT)YEAR$; GOTO 1175
1173 CD3$(90,2)="01"; GOTO 1180; REM "Invalid fiscal year
1175 IF POS(YEAR$(99,1)="CR")<>0 THEN CD3$(90,2)="08"; REM "Closed year
1180 IF STP(I$(5,2),3," ")="" THEN CD3$(5,2)=CR_ACCTPD$ ELSE CD3$(5,2)=I$(5,2); REM "Accounting period
1181 CD3$(5,2)=STR(NUM(CD3$(5,2)):"00",ERR=*NEXT) ! SSP268013
1182 IF CD3$(5,2)<"01" OR CD3$(5,2)>YEAR$(13,2) THEN IF CD3$(90,2)="  " THEN CD3$(90,2)="02"; REM "Invalid accounting period
1190 IF STP(I$(7,3),3," ")="" THEN CD3$(7,3)=BANK_CODE_LIST$ ELSE CD3$(7,3)=I$(7,3); REM "Bank code
1192 FIND (Z[5],KEY=CD3$(7,3),DOM=*NEXT); GOTO 1195
1193 IF CD3$(90,2)="  " THEN CD3$(90,2)="03"; REM "Invalid bank code
1200 CD3$(10,4)=STR(SEQ_NO:"0000"); REM "Sequence number
1210 CD3$(14,10)=I$(10,10); REM "Vendor code
1215 DIM AP4$(430); FIND (Z[3],KEY=CD3$(14,10),DOM=*NEXT)AP4$; GOTO 1220
1216 IF CD3$(90,2)="  " THEN CD3$(90,2)="04"; REM "Invalid Vendor code
1220 CD3$(34,6)=I$(30,6),CD3$(24,10)=I$(20,10); REM "Check reference, invoice number
1222 DATE$=I$(36,8); GOSUB FORMAT_DATE; CD3$(40,6)=DATE_RESULT$; CD3$(46,9)=I$(44,9); REM "Check date, Reference Number
1225 FIND (Z[4],KEY=CD3$(14,10)+CD3$(24,10),DOM=*NEXT); GOTO 1230; REM "Check invoice number in open invoices file
1226 IF CD3$(90,2)="  " THEN CD3$(90,2)="05"; REM "Invalid invoice number
1230 CD3$(55,35)=AP4$(11,35); REM "Vendor name
1233 CD3[0]=NUM(I$(53,12),ERR=*NEXT); GOTO 1235; REM "Gross Amount
1234 IF CD3$(90,2)="  " THEN CD3$(90,2)="06"
1235 CD3[1]=NUM(I$(65,12),ERR=*NEXT); GOTO 1237; REM "Discount Taken
1236 IF CD3$(90,2)="  " THEN CD3$(90,2)="07"
1299 ! 
1300 WRITE (Z[2],KEY=CD3$(1,13),DOM=*NEXT)IOL=IOL_CD3; GOTO 1310
1301 SEQ_NO=SEQ_NO+1,CD3$(10,4)=STR(SEQ_NO:"0000"); GOTO 1300
1310 SEQ_NO=SEQ_NO+1
1795 GOTO NEXT_REC
1799 ! 
1900 END_OF_FILE:! End of file. Close & Rename to CD4 for archiving. Erase an existing CD4 file, if same name
1905 Z$="01CU"+F1$+" "
1906 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
1910 F2$=F1$; IF F2$ LIKE "[Cc][Dd]2*" THEN F2$(1,3)="CD4" ELSE GOTO 1995
1919 REM "Get disk DIRectory of data files
1925 J$=%DATAPATH$
1930 REM "Move the file
1935 ERASE F2$,ERR=*PROCEED; RENAME J$+DLM+F1$ TO J$+DLM+F2$
1995 GOTO NEXT_FILE
1999 ! 
5000 REM "EOJ
5020 DIM TEMP$(50); TEMP$(1)=X3$(9,3)+"CDGUAA"; WRITE (Z[13],KEY=TEMP$(1,9))CR_FY$+CR_ACCTPD$+TEMP$; REM "Put out record with current FY/AP for later transfer during update
5030 MSGBOX "Process complete","F.Y.I.","!"; CMD_STR$="END"
5040 GOTO 9900
5099 ! 
6000 REM "BACKGROUND
6190 RETURN 
7500 FORMAT_DATE:REM "Convert date in YYYYMMDD format into TopForm format
7501 REM "YYYYMMDD in date$,result in DATE_RESULT$
7505 DATE_RESULT$=""
7510 IF LEN(DATE$)<8 THEN GOTO FORMAT_DATE_END
7520 DATE_RESULT$=CHR(NUM(DATE$(1,3),ERR=7521)-125)+DATE$(4)
7545 FORMAT_DATE_END:RETURN 
7550 REM "Convert Q$(HH:MM:SS) format into Q0$(HHMM) format
7555 Q0$=Q$(1,2)+Q$(4,2)
7595 RETURN 
7800 REM "Get G/L Z Mask
7810 CALL "ZZDISP","AX","000000000000","G/L",X3$,W2$,"",0,0,X4$
7815 M2=0; FOR X=1 TO LEN(W2$); M2=M2+POS("0"=W2$(X,1)); NEXT X
7850 REM "Set Fiscal year & period"
7860 FIND (Z[13],DOM=8900,KEY=X3$(9,3)+"A/R")P0$
7870 FIND (Z[13],DOM=8900,KEY=X3$(9,3)+"G/LYE"+P0$(7,4))P1$
7890 RETURN 
7899 ! 
8100 REM "Init feedback variables"
8107 T=0
8113 CALL "ZZ2FNC;SerialRecCnt",Z[1],T
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8135 T1=0
8145 RETURN 
8150 REM "Update bar graph
8155 CALL "ZZBARG",X3$,"HG",9,15,50,T1,T,I9
8195 RETURN 
8910 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"      ")-1)
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56001 REM "254808-New Module - Cash Disbursements Gateway                     
56002 REM "268013-FY/AP validation in Cash Receipts Gateway, other gateways   
