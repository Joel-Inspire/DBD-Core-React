0010 ! GLGCAL - Mortgage Program
0015 PRINT 'SHOW'(-1)
0020 PROCESS "XGLGCAL","../GLG/GL.EN"
0025 RELEASE 
0030 SETERR 0031; EXIT 
0031 PRINT 'CS'; END 
0040 INIT:
0050 DIM LN$(87,"-"); LST_DY$="312931303130313130313031",NAM_MO$="JanFebMarAprMayJunJulAugSepOctNovDec"
0055 PRECISION 14
0060 RETURN 
0080 RATES:RETURN 
0200 RATE_PERIOD:
0205 PRECISION 14
0210 SWITCH RATE_PERIOD$
0220 CASE "A"
0230 RT_YR=RATES/100
0235 EXIT 
0240 BREAK
0250 CASE "S"
0251 I=2
0252 BREAK
0260 CASE "M"
0261 I=12
0262 BREAK
0270 CASE "B"
0271 I=365.25/14
0272 BREAK
0280 CASE "W"
0281 I=365.25/7
0290 BREAK
0300 CASE "D"
0301 I=365.25
0302 BREAK
0310 END SWITCH 
0320 RT_W=1+(RATES/100)/I,RT_YR=1
0330 FOR J=1 TO INT(I)
0340 RT_YR=RT_YR*RT_W
0350 NEXT J
0360 J=J-1
0370 IF I<>J THEN RT_YR=RT_YR*(1+(RT_W-1)*(I-J))
0380 RT_YR=RT_YR-1
0390 ML_DESC$="Annual Interest Rate is: "+STR(RT_YR*100)+"%",REFRESH_FLG=1; RETURN 
0400 PAYMENT_PERIOD:
0410 PRECISION 14
0430 IF PAYMENT_PERIOD$="" THEN PAYMENT_PERIOD$=RATE_PERIOD$ ELSE PAYMENT_PERIOD$=UCS(PAYMENT_PERIOD$(1,1))
0440 IF POS(PAYMENT_PERIOD$="ASMBWDX")=0 THEN PRINT IOL=8010,"Enter A, S, M, B, W or D",; GOTO 0410
0450 IF PAYMENT_PERIOD$="A" THEN PER_YR=1,RT_PER=RT_YR; GOTO 0520
0460 IF PAYMENT_PERIOD$="S" THEN PER_YR=2; GOTO 0510
0470 IF PAYMENT_PERIOD$="M" THEN PER_YR=12; GOTO 0510
0480 IF PAYMENT_PERIOD$="B" THEN PER_YR=365.25/14; GOTO 0510
0490 IF PAYMENT_PERIOD$="W" OR PAYMENT_PERIOD$="X" THEN PER_YR=365.25/7; GOTO 0510
0500 PER_YR=365.25
0510 RT_PER=EXP(LOG(1+RT_YR)/PER_YR)-1,REFRESH_FLG=1
0520 ML_DESC1$="Interest Per Period: "+STR(RT_PER*100)+"%"; RETURN 
0600 MORTGAGE:
0630 IF MORTGAGE=0 THEN MSGBOX "Cannot Be Zero"+SEP+"Or Its Paid Off Already",MSG("WARNING"),"!,TIM=4"; NEXT_ID=MORTAGAGE.CTL; GOTO *RETURN
0640 RETURN 
0700 PAYMENT_AMT:
0730 IF PAYMENT_AMT>0 THEN GOSUB 4000; REFRESH_FLG=1,NEXT_ID=DATES.CTL; RETURN 
0740 RETURN 
0800 NO_PAY:
0830 GOSUB 3000
0840 IF PAYMENT_PERIOD$="X" THEN PAYMENT_AMT=INT(PAYMENT_AMT*100+.5)/100; NEXT_ID=PAYMENT_AMT.CTL,REFRESH_FLG=1; GOTO *RETURN
0845 NEXT_ID=DATES.CTL,NO_PRT=NO_PAY
0850 RETURN 
0900 DATES:
0910 FST_DY$=DATES$(5)+DATES$(1,4),FST_MO=NUM(FST_DY$(1,2)),FST_DY=NUM(FST_DY$(3,2)),FST_YR=NUM(DATES$(1,4))
0960 NO_PRT=NO_PAY,REFRESH_FLG=1; RETURN 
1000 ! 1000 - Number of payment to print
1010 NO_PRT=NO_PAY
1015 NO_PRT:
1040 IF NO_PRT<0 THEN MSGBOX "Cannot Be Negative",MSG("WARNING"),"!,TIM=3"; NEXT_ID=NO_PRT.CTL,NO_PRT=0; GOTO *RETURN
1050 IF NO_PRT=0 AND NO_PAY=0 THEN MSGBOX "Number To Print Must Be Given",MSG("WARNING"),"!,TIM=3"; NEXT_ID=NO_PRT; GOTO *RETURN
1060 ! IF NO_PRT=0 OR NO_PRT=NO_PAY THEN PRINT @(54-5,12),"(End)",
1070 RETURN 
1100 ! 1100 - Get title
1110 INPUT EDIT (0,LEN=40)@(40,13),PR_TTL$,IOL=8020
1120 ON CTL GOTO 1200,1020,1110,1020,9000
1200 VIEW_REPORT:
1210 PR_LFN=HFN; OPEN (PR_LFN)"*viewer*"
1230 PR_DAY$=DAY,PR_TIM$=STR(INT(TME):"00:")+STR(INT(FPT(TME)*60):"00"),PR_PGN=0
1240 IF RATE_PERIOD$="A" THEN PR_RATE_PERIOD$="annually" ELSE IF RATE_PERIOD$="S" THEN PR_RATE_PERIOD$="semi-annually" ELSE IF RATE_PERIOD$="M" THEN PR_RATE_PERIOD$="monthly" ELSE IF RATE_PERIOD$="B" THEN PR_RATE_PERIOD$="bi-weekly" ELSE IF RATE_PERIOD$="W" THEN PR_RATE_PERIOD$="weekly" ELSE PR_RATE_PERIOD$="daily"
2000 ! 2000 - Generate report
2010 M=FST_MO,D=FST_DY,Y=FST_YR,A=MORTGAGE
2020 ! PRINT IOL=8020,"Generating report ...",
2030 L=65
2040 IF Y|4=0 THEN LST_DY$(3,2)="29" ELSE LST_DY$(3,2)="28"
2050 MO_END=NUM(LST_DY$(M*2-1,2))
2100 ! 2100 - Next period
2110 IF L<65 THEN GOTO 2180
2120 IF PR_PGN<>0 THEN PRINT (PR_LFN)'FF',
2130 PR_PGN=PR_PGN+1
2140 PRINT (PR_LFN)"Date: ",DTE(0:"%Ml %Dz, %Dl %Yl"),@(40-INT(LEN(PR_TTL$)/2)),PR_TTL$,@(70),"Page:",PR_PGN,'LF',"Time: ",DTE(0:"%Hz:%mz %P"),@(25),RATES:"##0.00#%"," Compounded ",PR_RATE_PERIOD$,'LF',@(0),LN$
2150 PRINT (PR_LFN)@(5),"    Date",@(23),"Still Owing",@(41),"Payment",@(55),"Interest",@(67),"New Balance",'LF',@(0),LN$
2160 ! PRINT (PR_LFN)@(5),"===========",@(20),"============",@(36),"=========",@(51),"=========",@(66),"============",'LF'
2170 L=6
2180 I=A*RT_PER; IF A+I<PAYMENT_AMT THEN PAYMENT_AMT=A+I
2185 IF Y99<>0 AND Y99<>Y THEN GOSUB TOTAL_YEAR
2190 PRINT (PR_LFN)@(5),NAM_MO$(M*3-2,3)+" "+STR(D:"00")+"/"+STR(Y:"0000"),@(19),A:"###,###,##0.00-",@(34),PAYMENT_AMT:"###,###,##0.00",@(49),I:"###,###,##0.00",@(63),A+I-PAYMENT_AMT:"###,###,##0.00-"
2200 L=L+1,YEAR_PAY=YEAR_PAY+PAYMENT_AMT,YEAR_I=YEAR_I+I,YEAR_T=YEAR_T+A+I-PAYMENT_AMT,Y99=Y
2210 A=A+I-PAYMENT_AMT
2220 IF PAYMENT_PERIOD$="A" THEN Y=Y+1; GOTO 2300
2230 IF PAYMENT_PERIOD$="S" THEN M=M+6; GOTO 2280
2240 IF PAYMENT_PERIOD$="M" THEN M=M+1; GOTO 2280
2250 IF PAYMENT_PERIOD$="B" THEN D=D+14; GOTO 2270
2260 IF PAYMENT_PERIOD$="W" OR PAYMENT_PERIOD$="X" THEN D=D+7 ELSE D=D+1
2270 IF D>MO_END THEN D=D-MO_END,M=M+1
2280 IF M>12 THEN M=M-12,Y=Y+1
2290 MO_END=NUM(LST_DY$(M*2-1,2))
2300 ! IF Y>99 THEN Y=Y-99
2310 IF Y|4=0 THEN LST_DY$(3,2)="29" ELSE LST_DY$(3,2)="28"
2320 IF A<=0 THEN GOTO 2900
2330 IF NO_PRT>0 THEN NO_PRT=NO_PRT-1; IF NO_PRT=0 THEN GOTO 2900
2340 GOTO 2100
2900 ! 2900 - End print
2920 GOSUB TOTAL_YEAR
2930 CLOSE (PR_LFN,ERR=*NEXT)
2940 CMD_STR$="E"; RETURN 
3000 ! 3000 - Determine payment amt
3001 PRECISION 14
3010 Z=0
3020 X=RT_PER,Y=NO_PAY; IF PAYMENT_PERIOD$="X" THEN X=EXP(LOG(1+RT_YR)/12)-1,Y=Y/52*12
3030 FOR I=1 TO Y
3040 Z=(Z+1000)/(1+X)
3050 NEXT I
3060 PAYMENT_AMT=1000*MORTGAGE/Z
3070 IF PAYMENT_PERIOD$="X" THEN PAYMENT_AMT=PAYMENT_AMT/4
3080 REFRESH_FLG=1 ! PRINT @(54-11,9),PAYMENT_AMT:"####,##0.00",
3090 GOTO 4510
4000 ! 4000 - Determine # payments
4010 NO_PAY=0,WK_AMT=MORTGAGE
4020 X=RT_PER; IF PAYMENT_PERIOD$="X" THEN X=EXP(LOG(1+RT_YR)/(365.25/7))-1
4030 INT_PER=WK_AMT*X
4040 IF PAYMENT_AMT<=INT_PER THEN PRINT @(40,10),'CL',"Never",@(0,22),'CL',; RETURN 
4050 WK_AMT=WK_AMT+INT_PER-PAYMENT_AMT,NO_PAY=NO_PAY+1
4060 IF WK_AMT>0 THEN GOTO 4030
4070 PRINT @(54-4,10),NO_PAY:"###0",
4080 GOTO 4500
4500 ! 4500 - Print # years/months
4510 ML_DESC2$="Mortgage Will Be Paid Off After"
4520 NO_YR=INT(NO_PAY/PER_YR)
4530 NO_WRK=NO_PAY-(NO_YR*PER_YR)
4540 NO_MON=(12/PER_YR)*NO_WRK
4550 IF FPT(NO_MON)<>0 THEN NO_MON=INT(NO_MON)+1; IF NO_MON=12 THEN NO_MON=0,NO_YR=NO_YR+1
4560 IF NO_YR<>0 THEN ML_DESC2$=ML_DESC2$+STR(NO_YR)+" Years"
4570 IF NO_MON<>0 THEN ML_DESC2$=ML_DESC2$+STR(NO_MON)+" months"
4580 RETURN 
8000 ! 8000 - IOLIST
8010 IOLIST @(0,22),'CL','RB'
8020 IOLIST @(0,22),'CL'
9000 ! 9000 - WRAPUP
9010 END 
10000 TOTAL_YEAR:
10010 PRINT (PR_LFN)@(38),"-----------",@(53),"-----------",'LF',@(21),"Total Year:",@(34),YEAR_PAY:"###,###,##0.00",@(49),YEAR_I:"###,###,##0.00"
10020 YTD_PAY=YTD_PAY+YEAR_PAY,YEAR_PAY=0,YTD_YEAR_I=YTD_YEAR_I+YEAR_I,YEAR_I=0,YTD_T_YEAR=YTD_T_YEAR+T_YEAR,T_YEAR=0,L=L+4,FFF=FFF+1
10025 IF FFF=1 THEN PRINT (PR_LFN)'LF'; GOTO 10040
10030 PRINT (PR_LFN)@(21),"Total Acum:",@(34),YTD_PAY:"###,###,##0.00",@(49),YTD_YEAR_I:"###,###,##0.00",'LF'
10040 RETURN 
10100 CLEAR_REC:
10110 RATES=0,RATE_PERIOD$="M",PAYMENT_PERIOD$="M",MORTGAGE=0,PAYMENT_AMT=0,NO_PAY=360,DATES$="",PR_TTL$="",ML_DESC$="",ML_DESC1$="",NEXT_ID=RATES.CTL,REFRESH_FLG=1
10120 RETURN 
