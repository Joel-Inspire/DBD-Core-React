0010 REM "SAGE - TopForm Interface <SAGWS1>"
0020 SETESC 9300; SETERR 9000
0035 REM "5.5 - 10/18/07 - 10.965833 - jdf - SSP# 211380
0037 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
0040 REM "Copyright 2007 Demandbridge Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT
0100 SETERR 9000
0110 X0$="SAGWS1",X1$="SAGE - TopForm Interface"
0500 REM "Open files
0505 DIM Z[NUM(X3$(60,3))],BLGRBNKS$(100)
0510 Z$="01O ECR...  13O ZZPARM"
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0950 REM "Check if SAGE module enabled
0960 CALL "ZZ2PRP","SG",SG_ENABLED$,""; IF SG_ENABLED$<>"Y" THEN GOTO 9900
0970 READ (Z[13],KEY=X3$(9,3)+"SAGE",DOM=ERR_NO_PARAMS)SAGE$
1000 REM "Process DATA array
1010 FOR I=1 TO NUM_ENTRIES
1015 TAG$=DATA$[I,0],TF_VALUE$=DATA$[I,1]
1020 T_IN=POS("."=TAG$); IF T_IN THEN TAG_PARM$=MID(TAG$,T_IN+1),TAG$=MID(TAG$,1,T_IN-1) ELSE TAG_PARM$=""
1025 SWITCH TAG$
1035 CASE "template"; TEMPLATE$=TF_VALUE$; BREAK
1104 CASE "pcg_cust_number"; PCG_CUST_CODE$=TF_VALUE$; IF SAGE$(13,1)<>"C" THEN DATA$[I,1]=""; END_IF ; BREAK
1110 CASE "pcg_item_number"; PCG_ITEM_NUM$=TF_VALUE$; BREAK
1120 CASE "pcg_prod_code"; PCG_PROD_CODE$=TF_VALUE$; BREAK
1130 CASE "3p_id"; SAGE_3P_ID$=TF_VALUE$; BREAK
1385 ! 
1386 END SWITCH 
1390 NEXT I
1395 REM "Handle missing or invalid data 
1400 IF NUL(%BASIC2$) THEN NUM_ENTRIES++; %BASIC2$="EC3WS1"; DATA$[NUM_ENTRIES,0]="basic2",DATA$[NUM_ENTRIES,1]="EC3WS1"
1401 IF NUL(PCG_PROD_CODE$) THEN NUM_ENTRIES++; DATA$[NUM_ENTRIES,0]="pcg_prod_code",DATA$[NUM_ENTRIES,1]=MID(SAGE$,10,3)
1410 GOSUB PROCESS_ITEM
1415 GOSUB ADD_ITEM_EC_PARAM
1500 ! 
2990 GOTO 9900
2995 ! ***********************************
3005 ADD_ITEM_EC_PARAM:
3015 DIM ECR$(1000)
3016 IF SAGE$(13,1)="C" THEN ECR$(1,10)=PAD(UCS(PCG_CUST_CODE$),10)
3017 ECR$(11,10)=PAD(UCS(PCG_ITEM_NUM$),10)
3025 EXTRACT (Z[1],KEY=ECR$(1,20),DOM=*NEXT)ECR$
3035 ECR$(253,1)="I",ECR$(255,1)="Y" ! 3rd Party Id, Pricing source, Create PO?
3036 IF NUL(SAGE_3P_ID$) THEN ECR$(243,10)=SAGE$(14,10) ELSE ECR$(243,10)=SAGE_3P_ID$
3045 WRITE (Z[1],KEY=ECR$(1,20))ECR$
3050 MX=FN%_LOG_MESSAGE("INFO","SESSION_ID|"+EC$[1](1,7)+"|FILE|"+%WEBEC_FILE_NAME$+"|SAGWS1|ADDING SAGE ITEM ECPARAMS|"+ECR$(1,20))
3055 RETURN 
3060 ! 
6000 PROCESS_ITEM:! Add/update SAGE item through the Product Catalog Gateway
6020 STATUS=0,STATUS_MSG$="OK"
6030 MX=FN%_LOG_MESSAGE("INFO","SESSION_ID|"+EC$[1](1,7)+"|FILE|"+%WEBEC_FILE_NAME$+"|SAGWS1|PROCESSING SAGE ITEM|")
6040 CALL "EC3PCG",ERR=ERR_PROCESS_ITEM,X3$,X4$,EC_PARM$,EC${ALL},DATA${ALL},NUM_ENTRIES,OUTPUT
6050 RETURN 
6060 ! 
6070 ERR_PROCESS_ITEM:
6080 MX=FN%_LOG_MESSAGE("EXCP","SESSION_ID|"+EC$[1](1,7)+"|FILE|"+%WEBEC_FILE_NAME$+"|SAGWS1|ERROR : PROCESSING SAGE ITEM|")
6095 RETURN 
6100 ! 
6110 ERR_NO_PARAMS:
6120 MX=FN%_LOG_MESSAGE("EXCP","SESSION_ID|"+EC$[1](1,7)+"|FILE|"+%WEBEC_FILE_NAME$+"|SAGWS1|ERROR : SAGE INTERFACE PARAMETERS NOT AVAILABLE|")
6130 GOTO 9900
8199 ! ***********************************
9000 REM "ERROR PROCESSING
9002 J=ERR,K=TCB(5); SETERR 0000; ENDTRACE 
9010 LAST_WEBEC_ERR$="ERR: "+STR(J)+" AT: "+STR(K); LAST_WEBEC_ERR$=GBL("LAST_WEBEC_ERR",LAST_WEBEC_ERR$); REM "131928
9090 GOTO 9900
9900 REM "End
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9950 EXIT 
9999 END 
56000 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
