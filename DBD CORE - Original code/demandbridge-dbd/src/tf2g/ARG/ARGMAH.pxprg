0010 ! ARGMAH - Invoice History Inquiry
0035 REM "5.7 - 09/13/23 - 13.402221 - jvv - SSP# 307457
0037 REM "307457-Display Summary Bill Num in Invoice History panel           
0040 REM "Copyright 2023 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0045 IF TCB(13)=1 THEN BEGIN 
0050 CUSTOMER$=%CUSTOMER$
0060 PROCESS "ARGMAH","../ARG/AR.EN",ST_INV$,EN_INV$,ST_DT$,EN_DT$,ST_PO$,EN_PO$,ST_AMT$,ENAMT$
0070 %CUSTOMER$=CUSTOMER$
0080 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 _SV_KR=PRM('KR'); SET_PARAM 'KR'=0 ! Ensure native handling of KEP()
0130 X0=-1; X2=-1
0140 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2
0150 DIM Z[NUM(X3$(60,3))]
0160 Z$="01O AR8...  02O AR1...  03O AR7...  04O FS6...  05O AP4...  06O ZZPARM  07O IC0...  08O FM1...  09O FMP...  10O FS9...  "
0165 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
0170 ! IO list
0180 DIM AR1[15],AR8[6],FS6[7],AR7[1]
0190 ! iolist for AR1
0200 IOLIST AR1$,AR1{ALL}
0210 ! iolist for AR8
0220 IOLIST AR8$,AR8{ALL}
0230 IOLIST FS6$,FS6{ALL}
0250 IOLIST FM1$,FM1{ALL}
0260 IOLIST AR7_IDX,AR7$,AR7{ALL}
0270 IOLIST AR7$,AR7{ALL}
0400 ! 
0410 %DESC_FILE1$=STR(Z[7]:"##"); %DESC_FILE2$=STR(Z[8]:"##"); %DESC_FILE3$=STR(Z[9]:"##"); %DESC_FILE4$=STR(Z[10]:"##")
0420 %AR4$=STR(Z[5])
0450 ! 
0460 CALL "ZZINFO",Z[3],STATUS,X3$,REC_USED,TOT_REC,KEY_SZ,BYTE,DISC,TYPE,TOT_SEC,FILE_NM$
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 IF %CUSTOMER$<>"" THEN {
0530 CUSTOMER$=%CUSTOMER$
0540 DISABLE CONTROL CUSTOMER.CTL
0550 GOSUB GET_CUST_INFO; GOSUB LOAD_INV_HIST }
0560 NEXT_ID=CUSTOMER.CTL,REFRESH_FLG=1
0570 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0930 %DESC_FILE$=""
0940 %AR4$=""
0947 IF _ASO<>0 THEN CLOSE (_ASO); _ASO=0 ! SSP#269244
0960 SET_PARAM 'KR'=_SV_KR
0970 RETURN 
1000 ! Retrieve Customer Information
1010 GET_CUST_INFO:
1012 CALL "ZZWLKU;Parse_cust",CUSTOMER$,B.CUST_DIV$,B.CUST_CODE$
1015 CUSTOMER$=B.CUST_DIV$+B.CUST_CODE$
1020 KEY_2$=CUSTOMER$
1030 FIND (Z[2],KEY=KEY_2$,DOM=*RETURN)IOL=0200
1040 CUST_NAME$=AR1$(11,35)
1080 REFRESH_FLG=1
1090 RETURN 
1500 ! Load Invoice History info
1510 LOAD_INV_HIST:
1515 TOT_NUM_INV=5000,CUR_NUM_INV=0,BT_MORE.CTL'ENABLED=0,REFRESH_FLG=1 ! SSP 225600 ,SSP 300654
1520 ST_INV_NUM$=ARG_1$; EN_INV_NUM$=ARG_2$
1530 ST_INV_DATE$=ARG_3$; EN_INV_DATE$=ARG_4$
1540 ST_PO_NUM$=ARG_5$; EN_PO_NUM$=ARG_6$
1550 ST_INV_AMT=NUM(ARG_7$); EN_INV_AMT=NUM(ARG_8$)
1555 LIST_BOX LOAD INV_HIST.CTL,"" ! SSP 225600
1556 INV_HIST.CTL'VISIBLE=0 ! ssp 300654
1557 IF TCB(29)>=7100000 THEN INV_HIST.CTL'LOADPOINT=-1 ! SSP300654
1560 IF NUL(KEY_1$) THEN KEY_1$=CUSTOMER$ ! SSP 225600
1570 READ (Z[1],KEY=KEY_1$,DOM=*NEXT)
1580 KEY_1$=KEY(Z[1],END=LIH_DONE); IF CUR_NUM_INV>TOT_NUM_INV THEN BT_MORE.CTL'ENABLED=1,BT_MORE.CTL'VISIBLE=1; REFRESH_FLG=1; GOTO LIH_DONE ! SSP 225600
1585 READ (Z[1],KEY=KEY_1$)IOL=0220
1590 IF KEY_1$(1,10)<>CUSTOMER$ THEN GOTO LIH_DONE
1595 ! IF STP(AR8$(11,8))="ONACCNT" THEN GOTO 1580  !DBSPT-128614/SSP#307382    
1600 IF NOT(NUL(ST_INV_NUM$)) AND NOT(NUL(EN_INV_NUM$)) THEN IF AR8$(11,8)<ST_INV_NUM$ OR AR8$(11,8)>EN_INV_NUM$ THEN GOTO 1580
1610 IF NOT(NUL(ST_INV_DATE$)) AND NOT(NUL(EN_INV_DATE$)) THEN IF AR8$(20,6)<ST_INV_DATE$ OR AR8$(20,6)>EN_INV_DATE$ THEN GOTO 1580
1620 IF NOT(NUL(ST_PO_NUM$)) AND NOT(NUL(EN_INV_NUM$)) THEN IF AR8$(36,15)<ST_PO_NUM$ OR AR8$(36,15)>EN_INV_NUM$ THEN GOTO 1580
1630 IF ST_INV_AMT<>0 AND EN_INV_AMT<>0 THEN IF AR8[4]<ST_INV_AMT OR AR8[4]>EN_INV_AMT THEN GOTO 1580
1635 D1$=AR8$(20,6); CALL "ZZWDTE;Display",D1$,A1$,A2$
1640 LIST_BOX LOAD INV_HIST.CTL,0,AR8$(11,8)+SEP+D1$+SEP+AR8$(36,15)+SEP+AR8$(90,16)+SEP+STR(AR8[4]:"###,###,###.00")+SEP+AR8$(53,2)+"-"+AR8$(55,6)+SEP+FN%ZZDISP$(AR8$(26,2)+AR8$(28,8),"A/R")+SEP+AR8$(112,6); ++CUR_NUM_INV ! SSP 225600!SSP291330
1645 IF POS(" "<>AR8$(124,8))=0 THEN GOTO 1650 ! SSP 307457
1648 LIST_BOX LOAD INV_HIST.CTL,0,DIM(8)+SEP+""+SEP+"Summary Bill: "+AR8$(124,8); ++CUR_NUM_INV ! SSP 307457
1650 IF KEY_SZ THEN GOSUB READ_DIRECT; GOTO 1580 ! goto 1580
1660 IF AR8[0]=-1 OR AR8[0]=0 THEN GOTO 1750
1670 READ (Z[3],IND=AR8[0],DOM=1580)IOL=0260
1680 GOSUB LOAD_CHECK
1690 IF AR7_IDX=0 OR AR7_IDX=-1 THEN GOTO 1580
1700 ART_XX9=AR7_IDX; READ (Z[3],IND=ART_XX9,DOM=1580)IOL=0260
1710 GOSUB LOAD_CHECK
1720 GOTO 1690
1750 LIST_BOX LOAD INV_HIST.CTL,0,""; ++CUR_NUM_INV ! SSP 225600
1760 GOTO 1580
1785 LIH_DONE: INV_HIST.CTL'VISIBLE=1 ! SSP 300654
1786 IF CUR_NUM_INV<=TOT_NUM_INV THEN BT_MORE.CTL'ENABLED=0,BT_MORE.CTL'VISIBLE=0,REFRESH_FLG=1 ! SSP 225600
1790 RETURN 
1800 ! load for the check payments
1810 LOAD_CHECK:
1820 D2$=AR7$(20,6); CALL "ZZWDTE;DISPLAY",D2$,A1$,A2$
1830 TF_TYPE$=""; IF AR7$(19,1)="P" THEN TF_TYPE$="CHK" ELSE IF AR7$(19,1)="A" THEN TF_TYPE$="ADJ" ELSE IF AR7$(19,1)="C" THEN TF_TYPE$=" CM" ELSE IF AR7$(19,1)="D" THEN TF_TYPE$=" DM"
1835 IF AR7[0]<0 THEN PAY_AMT$='RED'+STR(AR7[0]:"#,###,###.00-") ELSE PAY_AMT$=STR(AR7[0]:"#,###,###.00-")
1840 LIST_BOX LOAD INV_HIST.CTL,0,""+SEP+D2$+SEP+TF_TYPE$+": "+AR7$(26,8)+SEP+"AC#: "+AR7$(34,4)+"."+AR7$(38,2)+SEP+PAY_AMT$; ++CUR_NUM_INV ! SSP 225600
1850 IF AR7[1]<>0 THEN LIST_BOX LOAD INV_HIST.CTL,0,""+SEP+""+SEP+""+SEP+"Discount Amount: "+SEP+STR(AR7[1]:"#,###,###.00-"); ++CUR_NUM_INV ! SSP 225600
1880 LIST_BOX LOAD INV_HIST.CTL,0,""; ++CUR_NUM_INV ! SSP 225600
1890 RETURN 
1900 ! Read Direct AR7 File
1910 READ_DIRECT:
1920 KEY_3$=KEY_1$
1930 READ (Z[3],KEY=KEY_3$,DOM=*NEXT)
1940 KEY_3$=KEY(Z[3],END=*RETURN); READ (Z[3])IOL=0270
1950 IF KEY_3$(1,18)<>KEY_1$ THEN RETURN 
1955 IF MID(AR7$,49,1)<>"H" THEN GOTO 1940 ! SSP#209664
1960 GOSUB LOAD_CHECK
1970 GOTO 1940
1990 RETURN 
2000 ! SELECT INVOICE HISTORY LINES
2010 SELECT_LINES:
2020 ! LIST_BOX READ INV_HIST.CTL,IDX
2030 HIST_LINE$=INV_HIST.CTL'VALUE$ ! LIST_BOX FIND INV_HIST.CTL,IDX,HIST_LINE$
2035 IF NUL(HIST_LINE$) THEN RETURN 
2040 INV_NUM$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1)
2045 IF NUL(INV_NUM$) THEN RETURN 
2050 PROCESS "ARGMAH.1","../ARG/AR.EN",INV_NUM$,STR(Z[1]),STR(Z[4])
2090 RETURN 
2200 ! Get the Invoice Number
2210 INVOICE:
2220 REPLACEMENT_SCRN$="ARGMAH_2"
2230 PERFORM "*winproc;overlay_screen"
2235 FIND (Z[1],KEY=CUSTOMER$+INV_NUM$,DOM=*RETURN); REM "SSP#207280
2240 IF NUL(INV_NUM$) THEN RETURN 
2250 PROCESS "ARGMAH.1","../ARG/AR.EN",INV_NUM$,STR(Z[1]),STR(Z[4])
2290 RETURN 
3000 ! INVOICE HISTORY INFORMATION
3010 DISPLAY_HIST_INFO:
3015 GOSUB PARAMS
3020 DIM AR8[6]
3030 INV_NUM$=ARG_1$; Z1=NUM(ARG_2$)
3040 KEY_1$=%CUSTOMER$+INV_NUM$
3050 READ (Z1,KEY=KEY_1$,DOM=*RETURN)IOL=0220
3060 CUSTOMER$=AR8$(1,10)
3065 FOREIGN_CURR_CODE$="",FOR_CURR_EXCH_RATE=0; IF PP$(118,1)="Y" THEN READ (_ASO,KEY=AR8$(106,6)+AR8$(11,8),DOM=*NEXT); CALL "*wingrp;show",FOREIGN_CUR.GRP$; CALL "*wingrp;enable",FOREIGN_CUR.GRP$ ELSE CALL "*wingrp;hide",FOREIGN_CUR.GRP$ ! SSP#269244
3070 INV_TYPE$=AR8$(19,1); INV_DATE$=AR8$(20,6); BILLED_FROM_CUST$=AR8$(26,10)
3080 CUST_PO$=AR8$(36,15); TERMS_CODE$=AR8$(51,2); ORDER_NUM$=AR8$(53,8)
3090 SPER_CODE$=AR8$(61,4); CUST_CATEGORY$=AR8$(81,9); CUST_CMNT$=AR8$(90,16)
3100 FY$=AR8$(106,4); ACCTPD$=AR8$(110,2); AUDIT_NUM$=AR8$(112,6)
3110 GREATEST_FY$=AR8$(118,4); GREATEST_ACCTPD$=AR8$(122,2)
3120 SUMMARY_BILL_NUMBER$=AR8$(124,8)
3130 NET_SALE=AR8[1]; STAX_BILLED=AR8[2]
3140 FRT_BILLED=AR8[3]; INV_AMT=AR8[4]; INV_AMT1=AR8[4]
3150 GP=AR8[5]; TOTAL_COMM=AR8[6]
3160 GOSUB VALIDATE_SECURITY
3180 REFRESH_FLG=1
3190 RETURN 
3500 ! Get the order information
3510 ORDER_INFO:
3520 IF NUL(ORDER_NUM$) THEN MSGBOX MSG("NO_ORD"),MSG("FYI"),"!"; RETURN 
3530 PROCESS "ARGMAH.2","../ARG/AR.EN",ORDER_NUM$,ARG_3$,ARG_2$
3590 RETURN 
4000 ! LIST ORDER INFORMATION
4010 ORDER_LINE:
4015 DIM FS6[7]
4020 FOR ZZ=1 TO 100
4030 LIST_BOX LOAD ORDER_LINE.CTL,1,*,ERR=4060
4040 NEXT ZZ
4050 GOTO 4070
4060 EXITTO 4070
4070 Z4=NUM(ARG_2$); ORDER_NUM$=ARG_1$
4080 KEY_4$=%CUSTOMER$+ORDER_NUM$; CUSTOMER$=%CUSTOMER$; ORDER_NUMBER$=ORDER_NUM$
4090 READ (Z4,KNO=2,KEY=KEY_4$,DOM=*NEXT)
4100 KEY_4$=KEY(Z4,END=*RETURN)
4110 READ (Z4,KEY=KEY_4$)IOL=0230
4120 IF FS6$(22,8)<>ORDER_NUM$ THEN RETURN 
4130 PO_NUM$=FS6$(54,15),HIST_TYPE$=FS6$(11,1)
4140 D1$=FS6$(34,6); CALL "ZZWDTE;DISPLAY",D1$,A1$,A2$
4150 D2$=FS6$(48,6); CALL "ZZWDTE;DISPLAY",D2$,A1$,A2$
4160 ORD_DATE$=D1$; REFRESH_FLG=1
4170 GOSUB GET_ITEM_DESC
4175 IF FS6[2]=0 THEN FS6[2]=1
4180 LIST_BOX LOAD ORDER_HIST.CTL,0,FS6$(30,1)+SEP+FS6$(12,10)+SEP+ITEM_DESC$+SEP+STR(FS6[0]:"###,###,###")+SEP+FS6$(101,4)+SEP+STR(((FS6[0]/FS6[2])*FS6[1]):"###,###,###.00-")+SEP+FS6$(40,2)+"-"+FS6$(42,6)+SEP+D2$+SEP+FS6$(11,1)
4190 ! READ (Z4,KNO=2,KEY=KEY_4$,DOM=*NEXT)
4200 GOTO 4100
4290 RETURN 
5000 ! Select Order Line Information
5010 SELECT_ORDER_LINE:
5020 LIST_BOX READ ORDER_HIST.CTL,IDX
5030 LIST_BOX FIND ORDER_HIST.CTL,IDX,HIST_LINE$
5040 SEQ_NUM$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1)
5050 ITEM_CODE$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1)
5060 ITEM_DESCRIPTION$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1)
5070 ORD_AMT$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1)
5080 U_M$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1)
5090 EXT$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1)
5100 INV_NUM$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1)
5110 INV_DATE$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1)
5115 HIST_TYPE$=HIST_LINE$
5120 PROCESS "FMGIAI","../FMG/FM.EN",CUSTOMER$+HIST_TYPE$+ITEM_CODE$+STP(ORDER_NUMBER$,3,"-")+SEQ_NUM$
5190 RETURN 
6000 ! ITEM DESCRIPTION BY TYPE
6010 GET_ITEM_DESC:
6012 IF %DESC_FILE2$="" THEN Z8=HFN; OPEN (Z8)"FM1"+%C$; %DESC_FILE2$=STR(Z8) ! SSO211474
6015 Z7=NUM(%DESC_FILE1$); Z8=NUM(%DESC_FILE2$); Z9=NUM(%DESC_FILE3$); Z10=NUM(%DESC_FILE4$)
6020 ITEM_CODE$=FS6$(12,10)
6030 DIM FM1$(167),FM1[18]
6040 ON POS(FS6$(11,1)="BDFHJM") GOTO 6900,6100,6200,6400,6500,6600,6500
6100 ! CUSTOM ITEM
6110 FIND (Z8,KEY=CUSTOMER$+ITEM_CODE$,DOM=*NEXT)IOL=0250
6120 ITEM_DESC$=FM1$(42,40); PROD_CODE$=FM1$(21,3); LEFT_RIGHT$=FM1$(24,8); TOP_BOTTOM$=FM1$(32,8); PLY$=FM1$(40,2)
6130 GOTO 6900
6200 ! 
6210 FIND (Z7,KEY=CUSTOMER$+ITEM_CODE$,DOM=6110)IOL=0250
6230 ITEM_DESC$=FM1$(21,40); PROD_CODE$=FM1$(61,3); LEFT_RIGHT$=FM1$(64,8); TOP_BOTTOM$=FM1$(72,8); PLY$=FM1$(80,2)
6240 GOTO 6900
6400 ! 
6410 FIND (Z7,KEY="          "+ITEM_CODE$,DOM=6430)IOL=0250
6420 ITEM_DESC$=FM1$(21,40); PROD_CODE$=FM1$(61,3); LEFT_RIGHT$=FM1$(64,8); TOP_BOTTOM$=FM1$(72,8); PLY$=FM1$(80,2); GOTO 6900
6430 FIND (Z8,KEY="          "+ITEM_CODE$,DOM=*NEXT)IOL=0250
6440 ITEM_DESC$=FM1$(42,40); PROD_CODE$=FM1$(21,3); LEFT_RIGHT$=FM1$(24,8); TOP_BOTTOM$=FM1$(32,8); PLY$=FM1$(40,2)
6450 GOTO 6900
6500 ! 
6510 FIND (Z10,KEY=ORDER_NUMBER$+ITEM_CODE$+FS6$(30,1),DOM=*NEXT)IOL=0250
6520 ITEM_DESC$=FM1$(41,40); PROD_CODE$=FM1$(20,3); LEFT_RIGHT$=FM1$(23,8); TOP_BOTTOM$=FM1$(31,8); PLY$=FM1$(39,2)
6530 GOTO 6900
6600 ! 
6610 FIND (Z9,KEY="X"+ITEM_CODE$,DOM=*NEXT)IOL=0250
6620 ITEM_DESC$=FM1$(12,40)
6900 RETURN 
7000 PARAMS:
7005 _ZZP=FFN("ZZPARM"); IF _ZZP<=0 THEN _ZZP=HFN; OPEN (_ZZP)"ZZPARM"
7010 READ (_ZZP,KEY=%C$+"A/R",DOM=*NEXT)PP$
7015 READ (_ZZP,KEY="osec"+%X3_OP_ID$,DOM=*NEXT)SEC$
7018 IF PP$(118,1)="Y" THEN _FILE_NAME$="ASO"+%C$; _ASO=HFN; OPEN (_ASO,ERR=*NEXT,IOL=*)_FILE_NAME$ ! SSP#269244
7020 RETURN 
9999 END 
12000 REM + Validate Security Settings
12010 VALIDATE_SECURITY:
12050 IF MID(SEC$,40,1)="Y" THEN TOTAL_COMM=0; REM "SSP#207584
12095 RETURN 
13000 FOREIGN_CURR:! SSP#269244
13020 GP=GP*FOR_CURR_EXCH_RATE,FRT_BILLED=FRT_BILLED*FOR_CURR_EXCH_RATE,STAX_BILLED=STAX_BILLED*FOR_CURR_EXCH_RATE,INV_AMT=INV_AMT*FOR_CURR_EXCH_RATE,ORIG_INV_AMT=ORIG_INV_AMT*FOR_CURR_EXCH_RATE,NET_SALE=NET_SALE*FOR_CURR_EXCH_RATE,TOTAL_COMM=TOTAL_COMM*FOR_CURR_EXCH_RATE
13030 REFRESH_FLG=1
13040 CALL "*wingrp;Disable",FOREIGN_CUR.GRP$
13999 RETURN 
56000 REM + Modification History
56002 REM "190140-Invoice Number should be able to be entered in Open Invoice 
56004 REM "206066-Commission amount displays in Open Invoice Display          
56005 REM "207280-PE in Invoice History - Cleared                             
56006 REM "207584-I only get error message when logging in with 1 user ID.
56008 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
56009 REM "225600-Load fixed number of invoices, button to load next screenful 
56010 REM "269244-ARGMAH.1 - AR invoice History.  Add option to display values in foreign currency
56011 REM "291330-Alternate Bill To company does not display correctly in GUI 
56012 REM "300654-Issue with order history queries                            
56014 REM "307382-DBSPT-128614 On accounts do not display in invoice history  
56015 REM "307457-Display Summary Bill Num in Invoice History panel           
