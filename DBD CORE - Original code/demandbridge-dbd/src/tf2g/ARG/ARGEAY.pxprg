0010 REM "Release 3.8 Sales Tax Computation <ARGEAY>
0020 SETESC 9300; SETERR 9000
0035 ! "6.0 - 04/21/03 - 10.486111 - dmm
0040 ! "Copyright 2003 Computer Software Inc.; Norcross, Georgia
0041 ! "        Licensed Software - All Rights Reserved.
0050 REM "A$=ARB string, A0=index to ART
0051 REM "T(I,0)=Taxable Sales, T(I,1)=Non-Taxable Sale, T(I,2)=Tax as Freight
0052 REM "T(I,3)=State Taxable Sale, T(I,4)=State Sales Tax, T(I,5)=County Taxable Amount, T(I,6)=County Sales Tax, T(I,7)=Local Taxable Amount, T(I,8)=Local Sales Tax, T(I,9)=Total Sales Tax   ,T(I,10)=Additonal Freight
0053 REM "Don't worry about deletion, since we will always go thru ARB and thisroutine always removes ASD records before computing tax.
0054 REM "S0=when coming from taxable amount override entry.  4=state, 6=county, 8=local
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,A$,A{ALL},S0
0100 SETERR 9000
0110 X0$="ARGEAY",X1$="Compute Sales Tax"
0120 DIM S$(40),D[4]
0135 C9=-1
0140 M2$="###,###.00-"
0200 REM "
0300 REM "IOLISTS
0310 IOLIST A,B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12]
0340 IOLIST D$,D[0],D[1],D[2],D[3],D[4]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O ART...  03O ASD...  04O AR5...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0525 _ART=Z[2],_ASD=Z[3],_AR5=Z[4]
0900 DIM B[10],C[16]
0910 A0=A[20]
1000 REM 
1010 GOSUB 3000
1020 GOSUB 1100
1040 GOSUB 2000
1060 GOSUB 1440
1090 GOTO 9900
1100 REM "Compute sales tax total T(X,0)=Taxable Sale, T(X,1)=Non-taxable sale, T(X,2)=Tax as Freight
1110 T9$=A$(67,10)
1120 IF A0=0 THEN GOTO 3100 ELSE GOSUB 1300; A=A0
1140 IF A<=0 THEN GOTO 1280
1160 READ (_ART,IND=A)IOL=0310; IF B[4]=0 THEN GOTO 1260 ELSE X=(POS(B$(125,10)=T9$)-1)/10
1180 IF B$(54,1)="Y" OR (B$(54,1)="C" AND B$(125,2)<>"IL") THEN T[X,0]=T[X,0]+B[4]
1200 IF B$(54,1)="C" AND B$(125,2)="IL" THEN T[X,0]=T[X,0]+B[4]*.5,T[X,1]=T[X,1]+B[4]-B[4]*.5
1220 IF B$(54,1)="F" THEN T[X,2]=T[X,2]+B[4]
1240 IF POS(B$(54,1)="YCF")=0 THEN T[X,1]=T[X,1]+B[4]
1260 GOTO 1140
1280 RETURN 
1300 REM "Multiple Sales Tax Codes 3.8
1320 A=A0
1340 IF A<=0 THEN GOTO 1400 ELSE A9=A; READ (_ART,IND=A)IOL=0310; IF POS(" "<>B$(125,10))=0 THEN B$(125,10)=A$(67,10); WRITE (_ART,IND=A9)IOL=0310
1360 IF POS(B$(125,10)=T9$)=0 THEN T9$=T9$+B$(125,10)
1380 GOTO 1340
1400 DIM T[LEN(T9$)/10,10]
1410 IF T9$<>A$(67,10) THEN A$(178,1)="Y" ELSE A$(178,1)="N"
1415 T[0,10]=A[2]; REM "Header STC is always in T(0,), must put additional freight here"
1420 RETURN 
1440 REM "Write results to ASD record
1460 REM PRINT 'CS'
1470 IF S0>0 AND A0>0 AND A$(178,1)<>"Y" THEN GOSUB 3200
1480 FOR X=1 TO LEN(T9$) STEP 10
1500 I=(X-1)/10
1540 DIM C$(44),C[12]
1560 C$(1,14)=A$(1,14),C$(15,10)=T9$(X,10)
1640 FOR J=0 TO 10; C[J]=T[I,J]; NEXT J
1650 C[9]=T[I,4]+T[I,6]+T[I,8]
1660 WRITE (_ASD,KEY=C$(1,24))IOL=0330
1670 REM PRINT C$,"  ",C(0):M2$,C(1):M2$,C(2):M2$,C(3):M2$,C(4):M2$,C(5):M2$,C(6):M2$,C(7):M2$,C(8):M2$,C(9):M2$
1675 FOR J=3 TO 8; IF A$(178,1)="Y" THEN A[J]=A[J]+C[J] ELSE A[J]=C[J]
1676 NEXT J
1680 NEXT X
1685 REM INPUT *,
1690 RETURN 
2000 REM "Compute tax
2010 IF A$(178,1)="Y" THEN FOR X=3 TO 8; A[X]=0; NEXT X
2020 FOR X=1 TO LEN(T9$) STEP 10
2040 I=(X-1)/10,Q$=T9$(X,10)
2080 IF Q$(1,2)<>"  " AND (S0=0 OR S0=3) THEN READ (_AR5,KEY=Q$(1,2)+S$(1,8),DOM=2160)IOL=0340; J=3; GOSUB 2500
2100 IF Q$(3,4)<>S$(1,4) AND (S0=0 OR S0=5) THEN READ (_AR5,KEY=Q$(1,6)+S$(1,4),DOM=2160)IOL=0340; J=5; GOSUB 2500
2120 IF Q$(7,4)<>S$(1,4) AND (S0=0 OR S0=7) THEN READ (_AR5,KEY=Q$,DOM=2160)IOL=0340; J=7; GOSUB 2500
2160 NEXT X
2190 RETURN 
2500 REM "Calculate Tax
2509 IF A$(178,1)="Y" AND (S0>0 OR A0=0) THEN PRINT @(0,22),'CL',"A$(178,1)=",A$(178,1),"  S0=",S0,"  A0=",A0,; INPUT *,
2510 IF S0=0 THEN K=T[I,0] ELSE K=A[J]; GOTO 2540
2520 IF D$(36,1)="Y" AND A$(115,1)<>"Y" THEN K=K+T[I,2]+T[I,10]; REM "Freight on lines/total screen  - additional freight can only be in (0,10)-SSP#108423 only add in if invoice is taxable
2535 IF A$(178,1)<>"Y" AND A$(115,1)="Y" THEN K=0
2540 T[I,J+1]=K*D[0]/100
2560 IF D[1]>0 THEN IF ABS(T[I,J+1])>D[1] THEN T[I,J+1]=D[1]*SGN(T[I,J+1]); REM "Maximum tax
2580 T[I,J]=K
2590 RETURN 
3000 REM "Remove existing records
3020 READ (_ASD,KEY=A$(1,14),DOM=3021)
3040 K$=KEY(_ASD,END=3090)
3060 IF K$(1,14)<>A$(1,14) THEN GOTO 3090
3080 REMOVE (_ASD,KEY=K$); GOTO 3040
3090 RETURN 
3100 REM "no lines
3110 GOSUB 1400
3120 T[0,0]=A[1],T[0,1]=0,T[0,2]=0
3130 FOR I=3 TO 8; T[0,I]=A[I]; NEXT I
3190 RETURN 
3200 REM "Sales Tax override with line items and 1 tax code case
3210 FOR I=3 TO 8 STEP 2; IF S0<>I THEN T[0,I]=A[I],T[0,I+1]=A[I+1]
3220 NEXT I
3240 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
