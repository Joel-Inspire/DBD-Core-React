0010 REM "Customer GUI Delete Routine <ARGUAB>
0035 REM "5.7 - 08/11/10 - 8.518333 - tma - SSP# 240337
0037 REM "240337-Wording of panel ARGDEL needs work                          
0040 REM "Copyright 2010 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$,Q2$,Q3$
0100 ! SETERR 9000
0110 X0$="ARGUAB",X1$="Customer Delete Routine",K9$="",K9=0,CC0=0
0120 DIM A[14],Y[4],F[6],K[14],J[14]
0195 ! 
0300 REM "IOLISTS
0310 IOLIST A$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10],A[11],A[12],A[13],A[14]
0320 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0360 IOLIST F$,F[0],F[1],F[2],F[3],F[4],F[5],F[6]
0410 IOLIST P9$,K[0],K[1],K[2],K[3],K[4],K[5],K[6],K[7],K[8],K[9],K[10],K[11],K[12],K[13],K[14]
0495 ! 
0500 ! 500,5 - Open files
0503 GOSUB SETUP_MESSAGES
0505 _FILE_NAME$="AR0"+%C$,_AR0=HFN; OPEN (_AR0,ERR=OPEN_ERR)_FILE_NAME$
0510 _FILE_NAME$="AR1"+%C$,_AR1=HFN; OPEN (_AR1,ERR=OPEN_ERR)_FILE_NAME$
0515 _FILE_NAME$="FS4"+%C$,_FS4=HFN; OPEN (_FS4,ERR=OPEN_ERR)_FILE_NAME$
0520 _FILE_NAME$="IC0"+%C$,_IC0=HFN; OPEN (_IC0,ERR=OPEN_ERR)_FILE_NAME$
0525 _FILE_NAME$="AR9"+%C$,_AR9=HFN; OPEN (_AR9,ERR=OPEN_ERR)_FILE_NAME$
0530 _FILE_NAME$="AR6"+%C$,_AR6=HFN; OPEN (_AR6,ERR=OPEN_ERR)_FILE_NAME$
0535 _FILE_NAME$="ARE"+%C$,_ARE=HFN; OPEN (_ARE,ERR=OPEN_ERR)_FILE_NAME$
0540 _FILE_NAME$="ARK"+%C$,_ARK=HFN; OPEN (_ARK,ERR=OPEN_ERR)_FILE_NAME$
0545 _FILE_NAME$="FM0"+%C$,_FM0=HFN; OPEN (_FM0,ERR=OPEN_ERR)_FILE_NAME$
0550 _FILE_NAME$="FM1"+%C$,_FM1=HFN; OPEN (_FM1,ERR=OPEN_ERR)_FILE_NAME$
0555 _FILE_NAME$="ARU"+%C$,_ARU=HFN; OPEN (_ARU,ERR=OPEN_ERR)_FILE_NAME$
0560 _FILE_NAME$="ARW"+%C$,_ARW=HFN; OPEN (_ARW,ERR=OPEN_ERR)_FILE_NAME$
0565 _FILE_NAME$="ZZPARM",_ZZPARM=HFN; OPEN (_ZZPARM,ERR=OPEN_ERR)_FILE_NAME$
0570 _FILE_NAME$="FMP"+%C$,_FMP=HFN; OPEN (_FMP,ERR=OPEN_ERR)_FILE_NAME$
0575 _FILE_NAME$="FM4"+%C$,_FM4=HFN; OPEN (_FM4,ERR=OPEN_ERR)_FILE_NAME$
0580 _FILE_NAME$="FT1"+%C$,_FT1=HFN; OPEN (_FT1,ERR=OPEN_ERR)_FILE_NAME$
0585 _FILE_NAME$="ASH"+%C$,_ASH=HFN; OPEN (_ASH,ERR=OPEN_ERR)_FILE_NAME$
0595 ! 
0600 GOTO CONTINUE
0605 ! 
0610 OPEN_ERR: MSGBOX _MSG_FILOPNERR1$+QUO+_FILE_NAME$+QUO+SEP+_MSG_DIRECTORY$+LWD+SEP+_MSG_PREFIX$+PFX,MSG(ERR),"!"
0615 GOSUB WRAPUP
0620 CMD_STR$="E"
0625 GOTO DONE
0630 ! 
0635 CONTINUE:
0640 ! 
0650 ! Read Parameters
0680 READ (_ZZPARM,KEY=%C$+"A/R",DOM=*NEXT)P0$; GOTO 0685
0681 CMD_STR$="E"; GOSUB WRAPUP; GOTO DONE
0685 W9$=""; CALL "RTGPRM",ERR=*NEXT,W9$
0695 ! 
1000 REM "Check for recent activity
1015 PRINT 'CURSOR'(1),
1020 CALL "*progbar;init","Checking customer for activity...",40
1050 COUNTER=0
1095 ! 
1100 REM "Active - balances
1110 READ (_AR1,KEY=Q1$(1,10),DOM=5000)IOL=0310
1120 FOR X=6 TO 13
1130 IF A[X]<>0 THEN Q2$="AR "+MSG("OPEN_BAL")+", "; EXITTO 1210 ! SSP#047384
1140 NEXT X
1145 CALL "*progbar;update_percent",ERR=*NEXT,"",25
1195 ! 
1210 REM "Open invoices?
1220 READ (_AR6,KEY=A$(1,10),DOM=1221)
1230 READ (_AR6,END=1250)B$
1240 IF B$(1,10)=A$(1,10) THEN Q2$=Q2$+MSG("OPEN_INVO")+", "; GOTO 1250 ! SSP#047384
1245 CALL "*progbar;update_percent",ERR=*NEXT,"",50
1295 ! 
1300 REM "Open orders?
1310 READ (_FS4,KEY=A$(1,10),DOM=1311)
1320 K$=KEY(_FS4,END=1350); READ (_FS4)
1330 IF K$(1,10)=A$(1,10) THEN Q2$=Q2$+MSG("OPEN_ORD")+", "; GOTO 1360 ! SSP#047384
1335 CALL "*progbar;update_percent",ERR=*NEXT,"",75
1345 ! 
1350 REM "Inventory?
1360 READ (_IC0,KEY=A$(1,10),DOM=1361)
1370 K$=KEY(_IC0,END=1390); READ (_IC0)
1380 IF K$(1,10)=A$(1,10) THEN Q2$=Q2$+MSG("INV_ACCT"); GOTO 5000 ! SSP#047384
1385 CALL "*progbar;update_percent",ERR=*NEXT,"Activity check complete",100
1390 IF Q2$<>"" THEN GOTO 5000 ! SSP#047384
1395 ! 
2000 REM "Delete Customer Information
2010 CALL "*progbar;wrap_up"
2020 CALL "*progbar;init","Deleting all customer "+A$(1,10)+" data...",40
2095 ! 
2100 REM "Sort files      
2105 REMOVE (_ASH,KEY=A$(134,9)+A$(1,10)+DIM(4),DOM=*NEXT)
2110 REMOVE (_ARE,KEY=A$(155,10)+A$(1,10),DOM=*NEXT)
2115 REMOVE (_ARW,KEY=A$(205,10)+A$(1,10),DOM=*NEXT)
2119 ! 
2120 REM "Sales tax & commission
2130 READ (_ARK,KEY=A$(1,10),DOM=2131)
2140 K$=KEY(_ARK,END=2200); READ (_ARK)
2150 IF K$(1,10)<>A$(1,10) THEN GOTO 2200
2160 REMOVE (_ARK,KEY=K$)
2170 GOTO 2140
2195 ! 
2200 REM "Shipto Loc
2210 READ (_FM0,KEY="C"+A$(1,10),DOM=2220)
2220 K$=KEY(_FM0,END=2250); READ (_FM0)
2230 IF K$(1,11)<>"C"+A$(1,10) THEN GOTO 2250
2240 REMOVE (_FM0,KEY=K$); IF W9$="" THEN GOTO 2220 ELSE CUST$=K$(2,10); CALL "RTGWOC",ERR=2220,CUST$,"FM0...","D",K$; GOTO 2220
2245 ! 
2250 REM "Customer Job Master
2260 READ (_FM1,KEY=A$(1,10),DOM=2261)
2270 DIM FM1$(448); K$=KEY(_FM1,END=2300); READ (_FM1)FM1$
2280 IF K$(1,10)<>A$(1,10) THEN GOTO 2300
2290 REMOVE (_FM1,KEY=K$); IF W9$<>"" THEN CUST$=K$(1,10); CALL "RTGWOC",ERR=2291,CUST$,"FM1...","D",K$
2292 REMOVE (_FT1,KEY=K$(1,10)+FM1$(350,20)+K$(11,10),DOM=2293)
2294 GOTO 2270
2295 ! 
2400 REM "Combine Stats
2430 READ (_AR9,KEY=A$(1,10),DOM=2431)
2440 K$=KEY(_AR9,END=2580)
2445 IF K$(1,10)<>A$(1,10) THEN GOTO 2580
2450 Q1$=K$(1,14),Q0$=K$(15,1),P9$=Q1$+Q0$
2460 DIM K[14],J[14]
2470 EXTRACT (_AR9,KEY=P9$,DOM=*NEXT)IOL=0410
2480 FOR X=0 TO 14; J[X]=K[X]; NEXT X
2500 Q1$(1,10)=A$(1,2)+"z       ",P9$=Q1$+Q0$
2505 GOSUB 6000
2510 DIM K[14]
2520 EXTRACT (_AR9,KEY=P9$,DOM=*NEXT)IOL=0410
2530 FOR X=0 TO 14; K[X]=K[X]+J[X]; NEXT X
2540 WRITE (_AR9,KEY=P9$)IOL=0410; IF W9$<>"" THEN CUST$=P9$(1,10); CALL "RTGWOC",ERR=*NEXT,CUST$,"AR9...","U",P9$
2545 REMOVE (_AR9,KEY=K$); IF W9$<>"" THEN CUST$=K$(1,10); CALL "RTGWOC",ERR=2546,CUST$,"AR9...","D",K$
2547 READ (_AR9,KEY=K$,DOM=2548)
2550 GOTO 2440
2580 CALL "*progbar;update_percent",ERR=*NEXT,"",20
2595 ! 
2600 REM "History
2605 GOSUB CLOSE_GROUP1_FILES
2610 GOSUB OPEN_GROUP2_FILES
2613 ! 
2615 READ (_AR8,KEY=A$(1,10),DOM=2620)
2620 K$=KEY(_AR8,END=2700); READ (_AR8)IOL=0360
2630 IF K$(1,10)<>A$(1,10) THEN GOTO 2700
2640 REMOVE (_AR8,KEY=K$)
2641 IF W9$<>"" THEN CUST$=K$(1,10); CALL "RTGWOC",ERR=2642,CUST$,"AR8...","D",K$
2650 F=_AR7,A0=F[0]; GOSUB 5900
2670 GOTO 2620
2695 ! 
2700 REM "Forms Management
2710 READ (_FM3,KEY=A$(1,10),DOM=2711)
2720 K$=KEY(_FM3,END=2750); READ (_FM3)
2730 IF K$(1,10)<>A$(1,10) THEN GOTO 2750
2740 REMOVE (_FM3,KEY=K$); IF W9$<>"" THEN CUST$=K$(1,10); CALL "RTGWOC",ERR=2741,CUST$,"FM3...","D",K$
2745 GOTO 2720
2747 ! 
2750 REM "Item spec-comp
2760 READ (_FMS,KEY=A$(1,10),DOM=2761)
2770 K$=KEY(_FMS,END=2800); READ (_FMS)
2780 IF K$(1,10)<>A$(1,10) THEN GOTO 2800
2790 REMOVE (_FMS,KEY=K$); GOTO 2770
2795 ! 
2800 REM "Item spec-perm
2810 READ (_FMU,KEY=A$(1,10),DOM=2811)
2820 K$=KEY(_FMU,END=2850); READ (_FMU)
2830 IF K$(1,10)<>A$(1,10) THEN GOTO 2850
2840 REMOVE (_FMU,KEY=K$); GOTO 2820
2845 ! 
2850 REM "Item spec- comment
2860 READ (_FMV,KEY=A$(1,10),DOM=2870)
2870 K$=KEY(_FMV,END=2895); READ (_FMV)
2880 IF K$(1,10)<>A$(1,10) THEN GOTO 2895
2890 REMOVE (_FMV,KEY=K$); GOTO 2870
2895 CALL "*progbar;update_percent",ERR=*NEXT,"",40
2899 ! 
2900 REM "Order History
2905 GOSUB CLOSE_GROUP2_FILES
2910 GOSUB OPEN_GROUP3_FILES
2915 ! 
2920 READ (_FS6,KEY=A$(1,10),DOM=2921)
2930 K$=KEY(_FS6,END=3060); READ (_FS6)C$
2940 IF K$(1,10)<>A$(1,10) THEN GOTO 3060
2950 REMOVE (_FS6,KEY=K$); IF W9$<>"" THEN CUST$=K$(1,10); CALL "RTGWOC",ERR=2951,CUST$,"FS6...","D",K$
2960 REM "Sorts
2970 REMOVE (_FS7,KEY=C$(31,3)+C$(12,10)+C$(11,1)+C$(1,10)+C$(22,8)+C$(30,1),DOM=2971)
2980 REMOVE (_FS8,KEY=C$(1,10)+C$(22,8)+C$(11,1)+C$(12,10)+C$(30,1),DOM=2981)
2990 READ (_FS9,KEY=C$(22,8)+C$(30,1)+C$(31,3),DOM=2991)
3000 K$=KEY(_FS9,END=3050); READ (_FS9)
3010 IF K$(1,12)<>C$(22,8)+C$(30,1)+C$(31,3) THEN GOTO 3050
3020 REMOVE (_FS9,KEY=K$)
3030 GOTO 3000
3050 GOTO 2930
3060 REM "Salesman sort
3070 FOR X=0 TO 3
3075 REMOVE (_ARU,KEY=A$(278+X*4,4)+A$(1,10),DOM=3077)
3080 NEXT X
3090 REMOVE (_AR0,KEY=A$(1,10),DOM=3091)
3095 REMOVE (_AR1,KEY=A$(1,10),DOM=3096)
3099 ! 
3100 REM "Customer Messages
3120 REMOVE (_AS5,KEY=A$(1,10),DOM=3121)
3121 IF W9$<>"" THEN CUST$=A$(1,10); CALL "RTGWOC",ERR=3122,CUST$,"AS5...","D",A$(1,10)
3140 IF P0$(107,1)="Y" THEN CALL "ARGMAT",X3$,X4$,A$(411,20)+A$(1,10),"","D",""
3195 ! 
3200 REM "Remove Defaults --- Customer O/P Defaults "D"
3205 REMOVE (_FMP,KEY="D"+A$(1,10),DOM=3206)
3295 ! 
3350 REM "Customer Contacts 'a','c'
3355 Q33$="a"; GOSUB 7200
3370 Q33$="c"; GOSUB 7200
3390 REM "Count Stops 'd'
3395 REMOVE (_FMP,KEY="d"+A$(1,10),DOM=3396)
3399 ! 
3400 REM "Customer Fiscal Year Records 'E'"
3405 Q33$="E"; GOSUB 7200
3420 REM "Customer Requisition Charges 'R'
3425 Q33$="R"; GOSUB 7200
3440 REM "More Customer Defaults 'A'
3445 REMOVE (_FMP,KEY="A"+A$(1,10),DOM=3446)
3495 ! 
3500 REM "FM4
3510 READ (_FM4,KEY=A$(1,10),DOM=3511)
3520 K$=KEY(_FM4,END=3550); READ (_FM4,KEY=K$)
3530 IF K$(1,10)<>A$(1,10) THEN GOTO 3550
3540 REMOVE (_FM4,KEY=K$)
3545 GOTO 3520
3590 CALL "*progbar;update_percent",ERR=*NEXT,"",60
3595 ! 
3600 REM "SSP122424, add many missing customer related files to routine
3610 GOSUB 7500
3620 GOSUB 7550
3630 GOSUB 7600; GOSUB CLOSE_GROUP6_FILES
3695 ! 
3900 REM "Deleted Return to Overlay
3910 Q2$="Y"
3920 GOSUB CLOSE_GROUP7_FILES
3930 CALL "*progbar;update_percent",ERR=*NEXT,"Customer delete complete",100
3935 CALL "*progbar;wrap_up"
3940 GOTO 9900
3995 ! 
5000 REM "No Delete Return to Overlay
5010 ! Q2$="N"   !SSP#047384
5020 GOSUB CLOSE_GROUP1_FILES
5025 GOSUB CLOSE_GROUP3_FILES
5030 GOSUB CLOSE_GROUP7_FILES
5040 CALL "*progbar;wrap_up"
5090 GOTO 9900
5095 ! 
5900 REM "     REMOVELINES
5910 IF A0=0 THEN GOTO 5990
5920 EXTRACT (F,IND=0,ERR=5960)IOL=0320
5925 IF Y[4]=-2 THEN READ (F,IND=0); WAIT 0; GOTO 5920 ELSE Y[4]=-2; WRITE (F,IND=0)IOL=0320
5930 A1=Y[1],Y[1]=A0,Y[0]=Y[0]-1
5935 READ (F,IND=A0,ERR=5940)A; IF A>0 THEN Y[0]=Y[0]-1,A0=A; GOTO 5935
5940 WRITE (F,IND=A0)A1
5950 Y[4]=-1; WRITE (F,IND=0)IOL=0320
5955 GOTO 5990
5960 IF ERR=0 THEN RETRY ELSE GOTO *RETURN
5990 RETURN 
5995 ! 
6000 REM "Write z record to master & sorts
6010 DIM A2$(599); A2$(1,39)=Q1$(1,10)+"Deleted customer's statistics",A2$(155,1)="z",A2$(278,1)="z"; WRITE (_AR1,KEY=A2$(1,10),DOM=6011)A2$,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
6020 WRITE (_ARE,KEY="z         "+Q1$(1,10),DOM=6021)
6030 WRITE (_ARU,KEY="z   "+Q1$(1,10),DOM=6031)
6050 RETURN 
6095 ! 
7200 REM "Clear FMP records with prefix
7220 READ (_FMP,KEY=Q33$+A$(1,10),DOM=7221)
7225 K$=KEY(_FMP,END=7245); READ (_FMP,KEY=K$,ERR=7226)
7230 IF MID(K$,1,1)<>Q33$ OR MID(K$,1,11)<>Q33$+MID(A$,1,10) THEN GOTO 7245
7235 REMOVE (_FMP,KEY=K$,DOM=7236)
7240 GOTO 7225
7245 RETURN 
7249 ! 
7500 REM "ARZ,ASC,ASI,ASJ,ASL,ASW,ASX,BM0,FMY,FMZ,FO5,FST
7505 GOSUB CLOSE_GROUP3_FILES
7510 GOSUB OPEN_GROUP4_FILES
7512 DIM Z[15]; Z[3]=_ARZ,Z[4]=_ASC,Z[5]=_ASI,Z[6]=_ASJ,Z[7]=_ASL,Z[8]=_ASW,Z[9]=_ASX,Z[10]=_BM0,Z[11]=_FMY,Z[12]=_FMZ,Z[14]=_FO5,Z[15]=_FST
7515 FOR X=3 TO 15
7517 IF X=13 OR X=9 THEN GOTO 7540
7520 READ (Z[X],KEY=A$(1,10),DOM=7521)
7522 K$=KEY(Z[X],END=7540); READ (Z[X])
7525 IF K$(1,10)<>A$(1,10) THEN GOTO 7540
7527 REMOVE (Z[X],KEY=K$); IF X=8 THEN REMOVE (Z[9],KEY=K$(11,4)+K$(1,10),DOM=7528)
7530 GOTO 7522
7540 NEXT X
7545 RETURN 
7549 ! 
7550 REM "Repeat order files, FT3,FT4,FT5,FT6,FT7,FT8,FT9,FTA
7555 GOSUB CLOSE_GROUP4_FILES
7560 GOSUB OPEN_GROUP5_FILES
7565 READ (_FT7,KEY=A$(1,10),DOM=7566)
7567 K$=KEY(_FT7,END=7595); READ (_FT7)
7569 IF K$(1,10)<>A$(1,10) THEN GOTO 7595
7570 REMOVE (_FT7,KEY=K$)
7572 REMOVE (_FT3,KEY=K$(11,8),DOM=7573)
7574 REMOVE (_FT6,KEY=K$(11,8),DOM=7575)
7575 DIM Z[10]; Z[6]=_FT4,Z[7]=_FT5,Z[8]=_FT8,Z[9]=_FT9,Z[10]=_FTA
7576 FOR X=6 TO 10
7577 READ (Z[X],KEY=K$(11,8),DOM=7578)
7580 K1$=KEY(Z[X],END=7590); READ (Z[X])
7582 IF K1$(1,8)<>K$(11,8) THEN GOTO 7590
7585 REMOVE (Z[X],KEY=K1$)
7587 GOTO 7580
7590 NEXT X
7592 GOTO 7567
7595 CALL "*progbar;update_percent",ERR=*NEXT,"",80
7596 RETURN 
7599 ! 
7600 REM "FT0,FT2,FTB,FTC,FTD,FTI,FTL,FTM,KT0,SH5,SH6,AT1
7605 GOSUB CLOSE_GROUP5_FILES
7610 GOSUB OPEN_GROUP6_FILES
7612 DIM Z[11]; Z[3]=_FT0,Z[4]=_FT2,Z[5]=_FTB,Z[6]=_FTC,Z[7]=_FTI,Z[8]=_FTL,Z[9]=_KT0,Z[10]=_SH5,Z[11]=_AT1
7615 FOR X=3 TO 11
7620 READ (Z[X],KEY=A$(1,10),DOM=7621)
7622 K$=KEY(Z[X],END=7640); READ (Z[X])
7625 IF K$(1,10)<>A$(1,10) THEN GOTO 7640
7627 REMOVE (Z[X],KEY=K$); IF X=8 THEN REMOVE (_FTM,KEY=K$(21,4)+K$(1,20),DOM=7628)
7630 GOTO 7622
7640 NEXT X
7650 REMOVE (_SH6,KEY=A$(1,10),DOM=7651)
7660 READ (_FTD,KEY="C"+A$(1,10),DOM=7661)
7662 K$=KEY(_FTD,END=7671); READ (_FTD)
7665 IF K$(2,10)<>A$(1,10) THEN GOTO 7671
7667 REMOVE (_FTD,KEY=K$)
7670 GOTO 7662
7695 RETURN 
7699 ! 
8000 OPEN_GROUP2_FILES:
8005 _FILE_NAME$="FM3"+%C$,_FM3=HFN; OPEN (_FM3,ERR=OPEN_ERR)_FILE_NAME$
8010 _FILE_NAME$="FMS"+%C$,_FMS=HFN; OPEN (_FMS,ERR=OPEN_ERR)_FILE_NAME$
8015 _FILE_NAME$="AR8"+%C$,_AR8=HFN; OPEN (_AR8,ERR=OPEN_ERR)_FILE_NAME$
8020 _FILE_NAME$="AR7"+%C$,_AR7=HFN; OPEN (_AR7,ERR=OPEN_ERR)_FILE_NAME$
8025 _FILE_NAME$="ARO"+%C$,_ARO=HFN; OPEN (_ARO,ERR=OPEN_ERR)_FILE_NAME$
8030 _FILE_NAME$="FMU"+%C$,_FMU=HFN; OPEN (_FMU,ERR=OPEN_ERR)_FILE_NAME$
8035 _FILE_NAME$="FMV"+%C$,_FMV=HFN; OPEN (_FMV,ERR=OPEN_ERR)_FILE_NAME$
8040 OPEN_GROUP2_FILES_END:RETURN 
8045 ! 
8050 OPEN_GROUP3_FILES:
8055 _FILE_NAME$="FS6"+%C$,_FS6=HFN; OPEN (_FS6,ERR=OPEN_ERR)_FILE_NAME$
8060 _FILE_NAME$="FS7"+%C$,_FS7=HFN; OPEN (_FS7,ERR=OPEN_ERR)_FILE_NAME$
8065 _FILE_NAME$="FS8"+%C$,_FS8=HFN; OPEN (_FS8,ERR=OPEN_ERR)_FILE_NAME$
8070 _FILE_NAME$="FS9"+%C$,_FS9=HFN; OPEN (_FS9,ERR=OPEN_ERR)_FILE_NAME$
8075 _FILE_NAME$="AS5"+%C$,_AS5=HFN; OPEN (_AS5,ERR=OPEN_ERR)_FILE_NAME$
8090 OPEN_GROUP3_FILES_END:RETURN 
8095 ! 
8100 OPEN_GROUP4_FILES:
8105 _FILE_NAME$="ARZ"+%C$,_ARZ=HFN; OPEN (_ARZ,ERR=OPEN_ERR)_FILE_NAME$
8110 _FILE_NAME$="ASC"+%C$,_ASC=HFN; OPEN (_ASC,ERR=OPEN_ERR)_FILE_NAME$
8115 _FILE_NAME$="ASI"+%C$,_ASI=HFN; OPEN (_ASI,ERR=OPEN_ERR)_FILE_NAME$
8120 _FILE_NAME$="ASJ"+%C$,_ASJ=HFN; OPEN (_ASJ,ERR=OPEN_ERR)_FILE_NAME$
8125 _FILE_NAME$="ASL"+%C$,_ASL=HFN; OPEN (_ASL,ERR=OPEN_ERR)_FILE_NAME$
8130 _FILE_NAME$="ASW"+%C$,_ASW=HFN; OPEN (_ASW,ERR=OPEN_ERR)_FILE_NAME$
8135 _FILE_NAME$="ASX"+%C$,_ASX=HFN; OPEN (_ASX,ERR=OPEN_ERR)_FILE_NAME$
8140 _FILE_NAME$="BM0"+%C$,_BM0=HFN; OPEN (_BM0,ERR=OPEN_ERR)_FILE_NAME$
8145 _FILE_NAME$="FMY"+%C$,_FMY=HFN; OPEN (_FMY,ERR=OPEN_ERR)_FILE_NAME$
8150 _FILE_NAME$="FMZ"+%C$,_FMZ=HFN; OPEN (_FMZ,ERR=OPEN_ERR)_FILE_NAME$
8155 _FILE_NAME$="FO5"+%C$,_FO5=HFN; OPEN (_FO5,ERR=OPEN_ERR)_FILE_NAME$
8160 _FILE_NAME$="FST"+%C$,_FST=HFN; OPEN (_FST,ERR=OPEN_ERR)_FILE_NAME$
8190 OPEN_GROUP4_FILES_END:RETURN 
8195 ! 
8200 OPEN_GROUP5_FILES:
8205 _FILE_NAME$="FT7"+%C$,_FT7=HFN; OPEN (_FT7,ERR=OPEN_ERR)_FILE_NAME$
8210 _FILE_NAME$="FT3"+%C$,_FT3=HFN; OPEN (_FT3,ERR=OPEN_ERR)_FILE_NAME$
8215 _FILE_NAME$="FT6"+%C$,_FT6=HFN; OPEN (_FT6,ERR=OPEN_ERR)_FILE_NAME$
8220 _FILE_NAME$="FT4"+%C$,_FT4=HFN; OPEN (_FT4,ERR=OPEN_ERR)_FILE_NAME$
8225 _FILE_NAME$="FT5"+%C$,_FT5=HFN; OPEN (_FT5,ERR=OPEN_ERR)_FILE_NAME$
8230 _FILE_NAME$="FT8"+%C$,_FT8=HFN; OPEN (_FT8,ERR=OPEN_ERR)_FILE_NAME$
8235 _FILE_NAME$="FT9"+%C$,_FT9=HFN; OPEN (_FT9,ERR=OPEN_ERR)_FILE_NAME$
8240 _FILE_NAME$="FTA"+%C$,_FTA=HFN; OPEN (_FTA,ERR=OPEN_ERR)_FILE_NAME$
8290 OPEN_GROUP5_FILES_END:RETURN 
8295 ! 
8300 OPEN_GROUP6_FILES:
8305 _FILE_NAME$="FT0"+%C$,_FT0=HFN; OPEN (_FT0,ERR=OPEN_ERR)_FILE_NAME$
8310 _FILE_NAME$="FT2"+%C$,_FT2=HFN; OPEN (_FT2,ERR=OPEN_ERR)_FILE_NAME$
8315 _FILE_NAME$="FTB"+%C$,_FTB=HFN; OPEN (_FTB,ERR=OPEN_ERR)_FILE_NAME$
8320 _FILE_NAME$="FTC"+%C$,_FTC=HFN; OPEN (_FTC,ERR=OPEN_ERR)_FILE_NAME$
8325 _FILE_NAME$="FTI"+%C$,_FTI=HFN; OPEN (_FTI,ERR=OPEN_ERR)_FILE_NAME$
8330 _FILE_NAME$="FTL"+%C$,_FTL=HFN; OPEN (_FTL,ERR=OPEN_ERR)_FILE_NAME$
8335 _FILE_NAME$="KT0"+%C$,_KT0=HFN; OPEN (_KT0,ERR=OPEN_ERR)_FILE_NAME$
8340 _FILE_NAME$="SH5"+%C$,_SH5=HFN; OPEN (_SH5,ERR=OPEN_ERR)_FILE_NAME$
8345 _FILE_NAME$="AT1"+%C$,_AT1=HFN; OPEN (_AT1,ERR=OPEN_ERR)_FILE_NAME$
8350 _FILE_NAME$="SH6"+%C$,_SH6=HFN; OPEN (_SH6,ERR=OPEN_ERR)_FILE_NAME$
8355 _FILE_NAME$="FTD"+%C$,_FTD=HFN; OPEN (_FTD,ERR=OPEN_ERR)_FILE_NAME$
8360 _FILE_NAME$="FTM"+%C$,_FTM=HFN; OPEN (_FTM,ERR=OPEN_ERR)_FILE_NAME$
8390 OPEN_GROUP6_FILES_END:RETURN 
8395 ! 
9000 CLOSE_GROUP1_FILES:
9005 IF _FS4 THEN CLOSE (_FS4)
9010 IF _IC0 THEN CLOSE (_IC0)
9015 IF _AR9 THEN CLOSE (_AR9)
9020 IF _AR6 THEN CLOSE (_AR6)
9025 IF _ARE THEN CLOSE (_ARE)
9030 IF _ARK THEN CLOSE (_ARK)
9035 IF _FM0 THEN CLOSE (_FM0)
9040 CLOSE_GROUP1_FILES_END:RETURN 
9045 ! 
9050 CLOSE_GROUP2_FILES:
9055 IF _FM3 THEN CLOSE (_FM3)
9060 IF _FMS THEN CLOSE (_FMS)
9065 IF _AR8 THEN CLOSE (_AR8)
9070 IF _AR7 THEN CLOSE (_AR7)
9075 IF _ARO THEN CLOSE (_ARO)
9080 IF _FMU THEN CLOSE (_FMU)
9085 CLOSE_GROUP2_FILES_END:RETURN 
9090 ! 
9100 CLOSE_GROUP3_FILES:
9105 IF _FS6 THEN CLOSE (_FS6)
9110 IF _FS7 THEN CLOSE (_FS7)
9115 IF _FS8 THEN CLOSE (_FS8)
9120 IF _FS9 THEN CLOSE (_FS9)
9125 IF _ARU THEN CLOSE (_ARU)
9130 IF _AS5 THEN CLOSE (_AS5)
9135 IF _FMV THEN CLOSE (_FMV)
9140 IF _FM1 THEN CLOSE (_FM1)
9145 IF _ARW THEN CLOSE (_ARW)
9150 IF _FMP THEN CLOSE (_FMP)
9155 IF _FM4 THEN CLOSE (_FM4)
9160 IF _ASH THEN CLOSE (_ASH)
9190 CLOSE_GROUP3_FILES_END:RETURN 
9195 ! 
9200 SETUP_MESSAGES:
9210 _MSG_FILOPNERR1$=MSG("FILOPNERR1")
9220 _MSG_DIRECTORY$=MSG("DIRECTORY")
9230 _MSG_PREFIX$=MSG("PREFIX")
9290 SETUP_MESSAGES_END:RETURN 
9295 ! 
9300 CLOSE_GROUP4_FILES:
9305 IF _ARZ THEN CLOSE (_ARZ)
9310 IF _ASC THEN CLOSE (_ASC)
9315 IF _ASI THEN CLOSE (_ASI)
9320 IF _ASJ THEN CLOSE (_ASJ)
9325 IF _ASL THEN CLOSE (_ASL)
9330 IF _ASW THEN CLOSE (_ASW)
9335 IF _ASX THEN CLOSE (_ASX)
9340 IF _BM0 THEN CLOSE (_BM0)
9390 CLOSE_GROUP4_FILES_END:RETURN 
9395 ! 
9400 CLOSE_GROUP5_FILES:
9405 IF _FT7 THEN CLOSE (_FT7)
9410 IF _FT3 THEN CLOSE (_FT3)
9415 IF _FT6 THEN CLOSE (_FT6)
9420 IF _FT4 THEN CLOSE (_FT4)
9425 IF _FT5 THEN CLOSE (_FT5)
9430 IF _FT8 THEN CLOSE (_FT8)
9435 IF _FT9 THEN CLOSE (_FT9)
9440 IF _FTA THEN CLOSE (_FTA)
9445 IF _FMY THEN CLOSE (_FMY)
9450 IF _FMZ THEN CLOSE (_FMZ)
9455 IF _FO5 THEN CLOSE (_FO5)
9460 IF _FST THEN CLOSE (_FST)
9490 CLOSE_GROUP5_FILES_END:RETURN 
9495 ! 
9500 CLOSE_GROUP6_FILES:
9505 IF _FT0 THEN CLOSE (_FT0)
9510 IF _FT2 THEN CLOSE (_FT2)
9515 IF _FTB THEN CLOSE (_FTB)
9520 IF _FTC THEN CLOSE (_FTC)
9525 IF _FTI THEN CLOSE (_FTI)
9530 IF _FTL THEN CLOSE (_FTL)
9535 IF _KT0 THEN CLOSE (_KT0)
9540 IF _SH5 THEN CLOSE (_SH5)
9545 IF _AT1 THEN CLOSE (_AT1)
9550 IF _SH6 THEN CLOSE (_SH6)
9555 IF _FTD THEN CLOSE (_FTD)
9560 IF _FTM THEN CLOSE (_FTM)
9590 CLOSE_GROUP6_FILES_END:RETURN 
9595 ! 
9600 CLOSE_GROUP7_FILES:
9605 IF _AR0 THEN CLOSE (_AR0)
9610 IF _AR1 THEN CLOSE (_AR1)
9615 IF _ZZPARM THEN CLOSE (_ZZPARM)
9620 IF _FT1 THEN CLOSE (_FT1)
9640 CLOSE_GROUP7_FILES_END:RETURN 
9645 ! 
9700 WRAPUP:
9710 GOSUB CLOSE_GROUP1_FILES
9715 GOSUB CLOSE_GROUP2_FILES
9720 GOSUB CLOSE_GROUP3_FILES
9725 GOSUB CLOSE_GROUP4_FILES
9730 GOSUB CLOSE_GROUP5_FILES
9735 GOSUB CLOSE_GROUP6_FILES
9740 GOSUB CLOSE_GROUP7_FILES
9790 WRAPUP_END:RETURN 
9795 ! 
9900 DONE:
9910 PRINT 'CURSOR'(0),
9930 EXIT 
9999 END 
56000 REM "047384-Show where the activity is when a delete is not allowed.    
