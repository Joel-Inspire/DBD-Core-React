0010 REM "<ARGDBE> DBE Customer Setup in DBD
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 07/23/10 - 14.528333 - dmm - SSP# 237196
0037 REM "237196-Create utility to auto setup DB/e EC buyer, data rep filter,
0040 REM "Copyright 2010 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0045 ! WO237196 - Copied from PC2UAA - PCGUAA panel
0050 _CUSTOMER$=%CUSTOMER$
0060 PROCESS "ARGDBE","../ARG/AR.EN"
0070 %CUSTOMER$=_CUSTOMER$
0080 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0100 SETERR 9000
0101 INIT:
0110 X0$="ARGDBE",X1$="DBE Customer Setup in DBD"
0150 X0=-1,X2=-1
0160 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2; IF X1>0 THEN CMD_STR$="END"; EXIT 
0180 IF NOT(FN%HAS_MODULE("DE")) THEN CMD_STR$="END"; EXIT 
0200 REM "
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0300 REM "IOLISTS
0305 GLOBAL_IOL:IOLIST %CUST_NAME$,%EC_CATALOG_DESC,%DR_TARGET_DESC,%ARGDBE_CUST$
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O ECA... 02O DRF... 03O EDA...  04O DR0...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0605 DIM ARGDBE$(139); FIND (Z[13],KEY=X3$(9,3)+"ARGDBE",DOM=NEW_REC)ARGDBE$(1)
0610 GOSUB FIELDS_PROCEED_EXIT_GROUPS
0613 IF %CUSTOMER$<>"" THEN CUSTOMER$=%CUSTOMER$ ! Coming from ARGMAA
0615 EC_CATALOG_ID$=ARGDBE$(10,10),DATA_REP_TARGET$=ARGDBE$(20,20); GOSUB GET_DESC
0620 REFRESH_FLG=1; EXIT 
0625 ! 
0630 NEW_REC:
0635 MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"?,YesNo",ANS$
0640 IF ANS$="NO" THEN CMD_STR$="END"; EXIT 
0645 DIM ARGDBE$(139); SAVE_ONLY$="Y"; GOSUB SAVE_ONLY; NEXT_ID=EC_CATALOG_ID.CTL,REFRESH_FLG=1; RETURN 
0649 ! 
1000 ! If utility mode, user pressed proceed
1010 PROCEED:
1030 GOSUB SETUP_ECA
1040 GOSUB SETUP_DRF
1050 GOSUB REPLICATE_CUSTOMER
1080 IF %CUSTOMER$<>"" THEN DISABLE CONTROL BT_EXIT1.CTL; NEXT_ID=EXIT.CTL ELSE GOSUB CLEAR_CUST; NEXT_ID=CUSTOMER.CTL
1085 REFRESH_FLG=1
1090 EXIT 
1099 ! 
1100 REM "Process the file
1199 ! 
1900 REM "End of file
1999 ! 
2000 SETUP_ECA:! Create a new ECA record
2010 DIM ECA$(1540); ECA2$="",ECA$(1,10)=CUSTOMER$
2015 FIND (Z[1],KEY=ECA$(1,15),DOM=*NEXT); GOSUB BUYER_EXISTS; GOTO *RETURN
2020 ECA$(16,35)=%CUST_NAME$ ! Buyer name
2025 ECA$(51,10)=ECA$(1,10) ! Customer code
2030 ECA$(65,10)=ECA$(1,10) ! Password
2035 ECA$(480,3)=X3$(9,3),ECA$(80,3)="060" ! Company code, session expires
2040 ECA$(88,1)="Y" ! Allow O/E?
2045 ECA$(325,10)=EC_CATALOG_ID$,ECA$(626,10)=EC_CATALOG_ID$
2080 WRITE (Z[1],KEY=ECA$(1,15),DOM=*NEXT)ECA$,ECA2$; GOTO *RETURN
2081 GOSUB BUYER_EXISTS
2095 RETURN 
2099 ! 
2100 SETUP_DRF:! Create a new DRF record
2110 DIM DRF$(580)
2115 DRF$(1,20)=DATA_REP_TARGET$
2120 DRF$(21,30)="CUST_DIV"
2125 DRF$(51,30)=CUSTOMER$
2180 WRITE (Z[2],KEY=DRF$(1,80),DOM=*NEXT)DRF$; GOTO *RETURN
2181 GOSUB FILTER_EXISTS
2195 RETURN 
2199 ! 
2200 BUYER_EXISTS:! there is already a buyer ID setup, no modification
2210 MSGBOX MSG("ARGDBE_03"),MSG("WARNING","!,TIM=5") ! Buyer already exists, bypassing buyer setup.
2245 RETURN 
2249 ! 
2250 FILTER_EXISTS:! there is already a filter setup, no modification
2260 MSGBOX MSG("ARGDBE_04"),MSG("WARNING","!,TIM=5") ! Filter already exists, bypassing filter setup.
2295 RETURN 
2299 ! 
2300 REPLICATE_CUSTOMER:! call program to replicate this one customer, using pieces of DRGQRF
2305 MSGBOX MSG("ARGDBE_05"),MSG("CONFIRM"),"?,YesNo",ANS$; IF ANS$="NO" THEN GOTO *RETURN ! Proceed with replicating this customer?
2310 IF FCHAN=0 THEN FCHAN=HFN; OPEN (FCHAN)"*MEMORY*"
2315 C_FILTER$=CUSTOMER$; WRITE (FCHAN,KEY=PAD(C_FILTER$,10))C_FILTER$
2330 CALL "DRGQRF;REPLICATE_ALL_FILTERS",X3$,X4$,DATA_REP_TARGET$,"CUST_DIV",10,FCHAN,PROGRESS1.CTL,1 ! Modeled after line 4286 of DRGQRF, PROGRESS1 is a multi-line to display a message concerning the progress of the DR1 file read, and we pass in a 1 to say yes we want a progress bar two lines below this showing the progress of replicating each file found in DR1 to match the criteria - which is that it has a "CUST_DIV".
2340 IF FCHAN THEN CLOSE (FCHAN); FCHAN=0
2380 MSGBOX MSG("ARGDBE_06"),"F.Y.I.","!" ! Replication complete
2395 RETURN 
2399 ! 
6000 ! Must have a valid customer, entry during utility mode
6010 CHECK_CUSTOMER:
6020 IF STP(CUSTOMER$)="" THEN MSGBOX MSG("ARGDBE_07"),MSG("WARNING","!,TIM=5"); NEXT_ID=CUSTOMER.CTL ELSE GOSUB PARSE_CUSTOMER; NEXT_ID=EC_CATALOG_ID.CTL ! Please enter Customer code
6040 REFRESH_FLG=1
6045 RETURN 
6049 ! 
6050 ! Parse customer$ into cust_div and cust_code
6060 PARSE_CUSTOMER:
6070 CALL "ZZWLKU;Parse_cust",CUSTOMER$,CUST_DIV$,CUST_CODE$
6095 RETURN 
6099 ! 
6100 ! SAVE_ONLY accessed or set to "Y" for new parameter record or set to "N" from WRITE_REC, if Y then disable customer, enable SAVE button, if not then enable all fields and proceed
6110 SAVE_ONLY:
6115 IF SAVE_ONLY$="Y" THEN CALL "*wingrp;Enable",FIELDS.GRP$; GOSUB CLEAR_CUST; CALL "*wingrp;disable",CUSTOMER.GRP$; NEXT_ID=EC_CATALOG_ID.CTL; CALL "*wingrp;Disable",PROCEED_EXIT.GRP$; CALL "*wingrp;Enable",SAVE_EXIT.GRP$ ELSE CALL "*wingrp;Enable",FIELDS.GRP$; CALL "*wingrp;Disable",SAVE_EXIT.GRP$; CALL "*wingrp;Enable",PROCEED_EXIT.GRP$; NEXT_ID=CUSTOMER.CTL
6140 REFRESH_FLG=1
6145 RETURN 
6149 ! 
6150 ! Read for descriptions
6155 GET_DESC:
6160 FIND (Z[3],KEY=EC_CATALOG_ID$,DOM=*NEXT)EDA$; %EC_CATALOG_DESC$=EDA$(11,30)
6165 FIND (Z[4],KEY=DATA_REP_TARGET$,DOM=*NEXT)DR0$; %DR_TARGET_DESC$=DR0$(21,30)
6195 RETURN 
6199 ! 
6200 ! Assume utility mode, enable FIELDS and PROCEED_EXIT groups
6205 FIELDS_PROCEED_EXIT_GROUPS:
6210 IF %CUSTOMER$<>"" THEN CALL "*wingrp;Disable",FIELDS.GRP$; NEXT_ID=BT_EXIT1.CTL ELSE CALL "*wingrp;Enable",FIELDS.GRP$; NEXT_ID=SAVE_ONLY.CTL
6215 CALL "*wingrp;Enable",PROCEED_EXIT.GRP$
6245 RETURN 
6249 ! 
6300 ! Must have a valid EC catalog ID
6305 CHECK_CATALOG:
6310 IF STP(EC_CATALOG_ID$)="" THEN MSGBOX MSG("ARGDBE_08"),MSG("WARNING","!,TIM=5"); NEXT_ID=EC_CATALOG_ID.CTL ! Please enter Catalog ID
6345 RETURN 
6349 ! 
6350 ! Must have a valid DR target code
6355 CHECK_TARGET:
6360 IF STP(DATA_REP_TARGET$)="" THEN MSGBOX MSG("ARGDBE_09"),MSG("WARNING","!,TIM=5"); NEXT_ID=DATA_REP_TARGET.CTL ! Please enter Target Code
6395 RETURN 
6399 ! 
6400 ! Clear customer related fields
6405 CLEAR_CUST:
6410 CUSTOMER$=""
6415 CUST_DIV$=""
6420 CUST_CODE$=""
6445 RETURN 
6449 ! 
6700 ! Write parameter record
6705 WRITE_REC:
6710 IF STP(EC_CATALOG_ID$)="" THEN GOSUB CHECK_CATALOG; EXIT 
6715 IF STP(DATA_REP_TARGET$)="" THEN GOSUB CHECK_TARGET; EXIT 
6720 DIM ARGDBE$(139); ARGDBE$(1,9)=X3$(9,3)+"ARGDBE"
6725 ARGDBE$(10,10)=EC_CATALOG_ID$
6730 ARGDBE$(20,20)=DATA_REP_TARGET$
6750 WRITE (Z[13],KEY=ARGDBE$(1,9))ARGDBE$
6780 SAVE_ONLY$="N"; GOSUB SAVE_ONLY
6795 RETURN 
6799 ! 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8107 T=0
8113 CALL "ZZ2FNC;SerialRecCnt",Z[1],T
8115 IF NOT(%GUI) THEN PRINT @(0,7),"There are "+STR(T)+" records to process"
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8135 T1=0
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,I9
8195 RETURN 
8199 ! 
8910 DEF FNS$(Z9$)=Z9$(1,POS("     "=Z9$+"      ")-1)
9000 REM "ERROR PROCESSING
9001 IF POS(Q1$="GD")=0 THEN PRINT @(4,15),ERR,"  ",TCB(5); INPUT *
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1150,2040
9900 REM "END PROGRAM
9905 END_PROG:
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9920 READ DATA FROM "" TO IOL=GLOBAL_IOL
9930 SETERR 9940; Q1$=A1$
9935 EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "237196-Create utility to auto setup DB/e EC buyer, data rep filter,
