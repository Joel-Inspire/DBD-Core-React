0010 ! ARGFAA - File Maintenance Program for APX_GLInterface (="APX"+%C$)
0020 ! Generated by ProvideX® NOMADS II File Maintenance on May. 15, 2003 10:00
0030 ! ************************************************************************
0035 REM "5.7 - 07/10/19 - 12.125 - tma - SSP# 306696
0037 REM "306696-Code Set in Order Processing only allow Uppercase for the   
0040 REM "Copyright 2019 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 PROCESS "ARGFAA","../ARG/AR.EN"
0060 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 ! 
0125 DIM FMP[1]
0130 X0$="AR2FAA",X1$="Set Type Setup"
0140 ! 
0150 X0=-1,X2=-1
0160 CALL "ZZCOMP",X0$,X11$,X2$,X3$,X4$,X5$,X0,X1,X2; IF X1>0 THEN CMD_STR$="END"
0170 ! 
0190 DIM Z[NUM(X3$(60,3))],PARAM[8],PARAM_2[29],B_PARAM[8],B_PARAM_2[29]
0200 ! Files
0209 REM + [202347]-Modified line 210 to open XP4
0210 Z$="01X FMP...  02O ZY0...  06O ZZPARM  07O XP4...  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
0300 ! IOList Section
0310 IOLIST PARAM$
0320 IOLIST FMP$
0330 IOLIST ARG$
0400 ! System Parameters
0420 GOSUB GET_SYS_PARAM
0430 ! 
0440 %TYPE_NAME$=""
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0530 CUST_CAT_SET_TYPE$=""
0540 NEXT_ID=CUST_CAT_SET_TYPE.CTL
0780 REFRESH_FLG=1
0790 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0970 RETURN 
1000 ! Find the current record
1010 FIND_REC:
1020 KEY_1$="e"+CUST_CAT_SET_TYPE$+CUST_CAT_SET_CODE$
1030 ! EXTRACT (Z[1],KEY=KEY_1$,DOM=*NEXT)
1040 READ (Z[1],KEY=KEY_1$,DOM=ADD_MSG)IOL=0320
1050 GOSUB LOAD_SCREEN_1
1053 CALL "*wingrp;enable",FIELDS.GRP$
1055 ! IF STP(CUST_CAT_SET_TYPE$)="P" THEN CALL "*wingrp;disable",INCLUDE.GRP$
1060 NEXT_ID=JUSTTHESE_OR_ALLBUT.CTL
1070 B_FMP$=FMP$
1080 CALL "*wingrp;enable",BUTTONS.GRP$
1090 RETURN 
2000 ! Load Screen with Parameter Information
2010 LOAD_SCREEN_1:
2020 ! CUST_CAT_SET_TYPE$=FMP$(2,1); CUST_CAT_SET_CODE$=FMP$(3,2)
2030 CUST_CAT_SET_DESC$=FMP$(5,30)
2040 JUSTTHESE_OR_ALLBUT$=FMP$(35,1)
2045 GOSUB LOAD_TYPE
2050 FOR I=0 TO 11
2060 IF POS(" "<>FMP$(36+I*9,L))>0 THEN FIND (Z[2],KEY=K1$+FMP$(36+I*9,L),DOM=*NEXT)IOL=0330 ELSE GOTO 2190
2070 ON POS(FMP$(2,1)="ASTCWPV") GOTO 2190,2080,2090,2090,2090,2090,2090,2090,2090,2190
2080 EXECUTE "CUST_CAT_CODE_"+STR(I+1:"00")+"$=FN%ZZDISP$(FMP$(36+I*9,L),"+QUO+"ARG"+QUO+")"; EXECUTE "DESC_"+STR(I+1)+"$=arg$(B0,B1)"; EXECUTE "CODE_HOLD_"+STR(I+1:"00")+"$=FMP$(36+I*9,L)"; GOTO 2190
2090 EXECUTE "CUST_CAT_CODE_"+STR(I+1:"00")+"$=FMP$(36+I*9,L)"; EXECUTE "DESC_"+STR(I+1)+"$=MID(arg$,B0,B1)"; EXECUTE "CODE_HOLD_"+STR(I+1:"00")+"$=FMP$(36+I*9,L)" ! SSP#202687
2190 NEXT I
2193 REFRESH_FLG=1
2195 RETURN 
3000 ! Write the record
3010 WRITE_REC:
3040 WRITE (Z[1],KEY=KEY_1$)IOL=0320
3049 REM + [202347]-Added line 3050 to update XP4
3050 CALL "UPDFMP;DUPLICATE_XP4",Z[7],FMP$,FMP{ALL},"U"
3060 B_FMP$=FMP$
3090 RETURN 
3200 ! Format the Customer Category Set Code
3210 FORMAT_SET_CODE:
3220 IF CUST_CAT_SET_TYPE$="V" THEN FOR I=0 TO 11; EXECUTE "CUST_CAT_CODE_"+STR(I+1:"00")+".CTL'UPPERCASE=0"; NEXT I ! SSP#306696
3225 IF CUST_CAT_SET_TYPE$<>"V" THEN FOR I=0 TO 11; EXECUTE "CUST_CAT_CODE_"+STR(I+1:"00")+".CTL'UPPERCASE=1"; NEXT I ! SSP#306696
3230 XX=NUM(CUST_CAT_SET_CODE$,ERR=*NEXT); CUST_CAT_SET_CODE$=PAD(STR(XX),2,0,"0"); RETURN 
3240 CUST_CAT_SET_CODE$=PAD(CUST_CAT_SET_CODE$,2)
3290 RETURN 
4000 ! load Information into Parameter String
4010 LOAD_INFO_1:
4015 IF NUL(CUST_CAT_SET_CODE$) THEN RETURN 
4020 FMP$(1,1)="e"; FMP$(2,1)=CUST_CAT_SET_TYPE$; FMP$(3,2)=CUST_CAT_SET_CODE$
4030 FMP$(5,30)=CUST_CAT_SET_DESC$
4040 FMP$(35,1)=JUSTTHESE_OR_ALLBUT$
4050 FOR I=0 TO 11
4060 ! IF STP(CUST_CAT_SET_TYPE$)="A" THEN EXECUTE "CUST_CAT_CODE_"+STR(I+1:"00")+"$=FN%ZZDISP$(CUST_CAT_CODE_"+STR(I+1:"00")+"$,"+QUO+"ARG"+QUO+")"
4070 EXECUTE "FMP$(36+I*9,L)=CODE_HOLD_"+STR(I+1:"00")+"$" ! EXECUTE "DESC_"+STR(I+1)+"$=arg$(B0,B1)"
4080 NEXT I
4090 RETURN 
5000 ! Check Changes
5010 CHECK_CHANGES:
5020 GOSUB VERIFY_CHANGE
5030 IF CHG_FLG=0 THEN RETURN 
5040 MSGBOX MSG("REC_ALTERD"),MSG("UPDATED"),"?,YESNO",ANS$
5050 IF ANS$="NO" THEN RETURN 
5060 GOSUB WRITE_REC
5090 RETURN 
5300 ! Delete Record
5310 DELETE_REC:
5330 REMOVE (Z[1],KEY=KEY_1$,ERR=*NEXT)
5334 REM + [202347]-Added line 5335 to also update XP4
5340 GOSUB CLEAR_REC
5345 CALL "UPDFMP;DUPLICATE_XP4",Z[7],FMP$,FMP{ALL},"D"
5380 REFRESH_FLG=1
5390 RETURN 
5500 ! Clear all the Fields
5510 CLEAR_REC:
5520 CUST_CAT_SET_TYPE$=""; %TYPE_NAME$=""
5530 CUST_CAT_SET_CODE$=""; CUST_CAT_SET_DESC$=""
5540 JUSTTHESE_OR_ALLBUT$="N"
5550 FOR ZZ=1 TO 12
5560 EXECUTE "CUST_CAT_CODE_"+STR(ZZ:"00")+"$="+QUO+QUO; EXECUTE "DESC_"+STR(ZZ)+"$="+QUO+QUO; EXECUTE "CODE_HOLD_"+STR(ZZ:"00")+"$="+QUO+QUO
5570 NEXT ZZ
5575 NEXT_ID=CUST_CAT_SET_TYPE.CTL
5580 CALL "*wingrp;disable",FIELDS.GRP$; REFRESH_FLG=1
5590 RETURN 
5700 ! Format the Category Code
5710 CODE_FMT:
5720 CAT_NUM$=PAD(STR(CAT_NUM),2,0,"0")
5730 IF STP(CUST_CAT_SET_TYPE$)="A" THEN {
5740 EXECUTE "CODE_HOLD_"+CAT_NUM$+"$=PAD(CUST_CAT_CODE_"+CAT_NUM$+"$,3,0,"+QUO+"0"+QUO+")+"+QUO+"000000"+QUO
5750 EXECUTE "CUST_CAT_CODE_"+CAT_NUM$+"$=FN%ZZDISP$(CODE_HOLD_"+CAT_NUM$+"$,"+QUO+"ARG"+QUO+")"
5760  } ELSE {
5770 EXECUTE "CUST_CAT_CODE_"+CAT_NUM$+"$=PAD(CUST_CAT_CODE_"+CAT_NUM$+"$,L)"
5780 EXECUTE "CODE_HOLD_"+CAT_NUM$+"$=CUST_CAT_CODE_"+CAT_NUM$+"$"
5790  }
5800 EXECUTE "KEY_FMT$=CODE_HOLD_"+CAT_NUM$+"$"
5810 READ (Z[2],KEY=K1$+KEY_FMT$,DOM=INVALID_CODE)IOL=0330
5820 EXECUTE "DESC_"+STR(CAT_NUM)+"$=MID(ARG$,B0,B1)"
5890 REFRESH_FLG=1
5895 RETURN 
5900 ! Invalid Code entered
5910 INVALID_CODE:
5920 MSGBOX MSG("INVALID_C"),MSG("FYI"),"!"
5950 EXECUTE "CUST_CAT_CODE_"+CAT_NUM$+"$="+QUO+QUO; EXECUTE "DESC_"+STR(CAT_NUM)+"$="+QUO+QUO; EXECUTE "CODE_HOLD_"+CAT_NUM$+"$="+QUO+QUO
5960 EXECUTE "NEXT_ID=CUST_CAT_CODE_"+CAT_NUM$+".CTL"
5980 REFRESH_FLG=1
5990 RETURN 
6000 ! type description
6005 LOAD_TYPE:
6010 DIM %TYPE_NAME$(30)
6020 ON POS(CUST_CAT_SET_TYPE$="ASTVCWP") GOTO 6030,6040,6050,6060,6065,6070,6080,6090; REM "SSP# 137868
6030 %TYPE_NAME$(1)=""; GOTO 6095
6040 %TYPE_NAME$(1)="A/R Category",L=9,T$="AUX",F$="ARG",F0$="ARG",L$="ARG",B0=10,B1=30,M$="Enter Customer Category code",K1$=""; GOTO 6095
6050 %TYPE_NAME$(1)="States",L=2,T$="SU",F$="",F0$="ZY0",L$="ZY0",B0=3,B1=30,M$="Enter state abbreviation",K1$=""; GOTO 6095
6060 %TYPE_NAME$(1)="Contact Management Category",L=3,T$="SU",F$="",F0$="ZP1",L$="ZP1",B0=4,B1=40,M$="Enter Contact Management Category code",K1$=""; GOTO 6095
6065 %TYPE_NAME$="Ship Via Code",L=1,T$="SUZ",F$="",F0$="FMP",L$="FMPS",B0=3,B1=15,M$="Enter Ship Via Code",K1$="S"; GOTO 6095; REM "SSP# 167953
6070 %TYPE_NAME$(1)="Customer Rep/Salesperson",L=4,T$="SZU",F$="",F0$="AR3",L$="AR3",B0=5,B1=35,M$="Enter Customer Rep/Salesperson code",K1$=""; GOTO 6095
6080 %TYPE_NAME$(1)="Warehouse",L=4,T$="Z",F$="",F0$="FM0",L$="FM0D",B0=16,B1=35,M$="Enter Warehouse Location code",K1$="D"+DIM(10); GOTO 6095
6090 %TYPE_NAME$(1)="Product Code",L=3,T$="SUZ",F$="",F0$="FMP",L$="FMPP",B0=5,B1=15,M$="Enter Product Code",K1$="T",JUSTTHESE_OR_ALLBUT$="Y"; GOTO 6095; REM "SSP# 137868
6095 GOSUB OPEN_FILE
6097 GOSUB SET_CODE_MSG ! SSP202689 jdf ADDED SUBROUTINE
6099 REFRESH_FLG=1; RETURN 
6100 ! First Record
6110 FIRST_REC:
6115 KEY_1$="e"; READ (Z[1],KEY=KEY_1$,DOM=*NEXT)
6120 KEY_1$=KEY(Z[1],END=NO_FIRST)
6130 READ (Z[1],KEY=KEY_1$)IOL=0320
6140 GOSUB LOAD_SCREEN_1
6150 RETURN 
6160 ! Last Record
6170 LAST_REC:
6180 KEY_1$=KEL(Z[1],ERR=NO_LAST)
6190 READ (Z[1],KEY=KEY_1$)IOL=0320
6200 GOSUB LOAD_SCREEN_1
6210 RETURN 
6220 ! Prior Record
6230 PRIOR_REC:
6240 KEY_1$=KEP(Z[1],END=START_FILE)
6245 IF KEY_1$(1,1)<>"e" THEN RETURN 
6250 READ (Z[1],KEY=KEY_1$)IOL=0320
6260 GOSUB LOAD_SCREEN_1
6270 RETURN 
6280 ! Next Record
6290 NEXT_REC:
6300 KEY_1$=KEY(Z[1],END=END_FILE)
6305 IF KEY_1$(1,1)<>"e" THEN RETURN 
6310 READ (Z[1],KEY=KEY_1$)IOL=0320
6320 GOSUB LOAD_SCREEN_1
6330 RETURN 
6500 ! Open the corresponding file
6510 OPEN_FILE:
6520 IF NUL(F0$) THEN RETURN 
6530 Z$="02C XXX...  02O "+F0$+"...  "; IF F0$="FMP" THEN Z$=SUB(Z$,"02O ","02OS") ! SSP225229 jdf
6540 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; IF Z1 THEN CMD_STR$="END"; EXIT 
6590 RETURN 
7000 ! Check if tax module is installed.
7010 TAX_MOD:
7020 CALL "ZZ2PRP","TW",TAX_FILE$,""
7090 RETURN 
7100 ! Check if Credit Card Feature is Available
7110 CREDIT_CARD_MOD:
7120 CALL "ZZ2PRP","CC",CREDIT_CARD$,""
7190 RETURN 
8000 ! Add Messages
8010 ADD_MSG:
8020 MSGBOX MSG("ADD_RECORD")+SEP+MSG("AR_REC"),MSG("VERIFY"),"?,YESNO",ANS$
8030 IF ANS$="NO" THEN CMD_STR$="END"; RETURN 
8040 DIM FMP$(144)
8050 NEXT_ID=CUST_CAT_SET_DESC.CTL
8070 CALL "*wingrp;enable",BUTTONS.GRP$; CALL "*wingrp;enable",FIELDS.GRP$
8075 ! IF STP(CUST_CAT_SET_TYPE$)="P" THEN CALL "*wingrp;disable",INCLUDE.GRP$
8080 REFRESH_FLG=1
8090 RETURN 
8200 ! No First Record
8210 NO_FIRST:
8220 MSGBOX MSG("REC_NO_1ST"),"FYI","!"
8230 RETURN 
8240 ! No Last Record
8250 NO_LAST:
8260 MSGBOX MSG("REC_NO_LST$"),"FYI","!"
8270 RETURN 
8280 ! Start of File
8290 START_FILE:
8300 MSGBOX MSG("START_FILE"),"FYI","!"
8310 RETURN 
8320 ! End of File
8330 END_FILE:
8340 MSGBOX MSG("END_OF_FIL"),"FYI","!"
8350 RETURN 
8360 ! SET CODE MESSAGE
8370 SET_CODE_MSG:! SSP202689 jdf ADDED SUBROUTINE
8380 CUST_CAT_CODE_01.CTL'MSG$=M$
8390 CUST_CAT_CODE_02.CTL'MSG$=M$
8400 CUST_CAT_CODE_03.CTL'MSG$=M$
8410 CUST_CAT_CODE_04.CTL'MSG$=M$
8420 CUST_CAT_CODE_05.CTL'MSG$=M$
8430 CUST_CAT_CODE_06.CTL'MSG$=M$
8440 CUST_CAT_CODE_07.CTL'MSG$=M$
8450 CUST_CAT_CODE_08.CTL'MSG$=M$
8460 CUST_CAT_CODE_09.CTL'MSG$=M$
8470 CUST_CAT_CODE_10.CTL'MSG$=M$
8480 CUST_CAT_CODE_11.CTL'MSG$=M$
8490 CUST_CAT_CODE_12.CTL'MSG$=M$
8495 RETURN 
8500 ! CHECK FOR CHANGES
8510 VERIFY_CHANGE:
8520 CHG_FLG=0
8530 IF B_FMP$<>FMP$ THEN CHG_FLG=1
8590 RETURN 
9000 ! Parameters
9010 GET_SYS_PARAM:
9030 READ (Z[6],KEY=X3$(9,3)+"F/M",DOM=*RETURN)IOL=0310
9090 RETURN 
9999 END 
10000 ! QUERY TYPE
10010 EXC_QUERY:
10020 SWITCH CUST_CAT_SET_TYPE$
10030 CASE "A"
10040 PROCESS "Qry_arg","../ARG/AR.EN",VAL$
10050 IF NUL(VAL$) THEN BREAK ELSE QRY_VAL$=VAL$
10060 BREAK
10070 CASE "S"
10080 PROCESS "Qry_zy0","../ARG/AR.EN",VAL$
10090 IF NUL(VAL$) THEN BREAK ELSE QRY_VAL$=VAL$
10100 BREAK
10110 CASE "T"
10120 PROCESS "Qry_zp1","../ZPG/ZP.EN",VAL$
10130 IF NUL(VAL$) THEN BREAK ELSE QRY_VAL$=VAL$
10140 BREAK
10150 CASE "V"
10160 PROCESS "Qry_fmps","../OPG/OP.EN",VAL$ ! SSP222728 jdf
10170 IF NUL(VAL$) THEN BREAK ELSE QRY_VAL$=VAL$
10180 BREAK
10190 CASE "C"
10200 PROCESS "Qry_ar3","../ARG/AR.EN",VAL$
10210 IF NUL(VAL$) THEN BREAK ELSE QRY_VAL$=VAL$
10220 BREAK
10230 CASE "W"
10240 PROCESS "Qry_fm0d.1","../FMG/FM.EN",VAL$
10250 IF NUL(VAL$) THEN BREAK ELSE QRY_VAL$=VAL$
10260 BREAK
10270 CASE "P"
10280 PROCESS "Qry_fmpt","../FMG/FM.EN",VAL$ ! ssp#281938
10290 IF NUL(VAL$) THEN BREAK ELSE QRY_VAL$=VAL$
10300 BREAK
10370 END SWITCH 
10380 REFRESH_FLG=1
10390 RETURN 
56000 REM + Modification Hiostory
56001 REM "5.5 - 11/22/06 - 16.839166 - mhe - SSP# 202347
56002 REM "202347-Modify ARGFAA to also call UPDFMP to update XP4 after       
56004 REM "202689-OPS, C4 in GUI needs work - ARGFAA. *Status info incorrect  
56006 REM "222728-The ship via code set up ( maintenance folder) does not work
56008 REM "225229-Unable to add Code Set for Ship Via in Freight Mark UP      
56009 REM "281938-Unable to use the Qry at the Code Set Option in ARGFAA      
