0010 ! ARGSB0 - Replace Summary Bill Number in Open Invoices
0035 REM "5.7 - 10/08/12 - 9.473333 - dmm - SSP# 258053
0037 REM "258053-Program to read text file and update summary bill num in AR6
0040 REM "Copyright 2012 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0070 IF TCB(88)=0 THEN MSGBOX "You must be using Windx to access this program" ELSE PROCESS "ARGSB0","../ARG/AR.EN"
0080 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0081 ! Copied from ECGBU0, WO258053, will read a tab delimmited text file of SB1 information, used to update AR6 records with replacement Summary Bill Number.
0085 ! 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0101 INIT:
0110 X0$="ARGSB0",X1$="Replace Summary Bill Number in Open Invoices"
0120 DIM Z0$(80,"-")
0140 SAVE_GUI=%GUI; %GUI=1
0195 ! 
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,-1,-1; IF X1>0 THEN GOTO 9920
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0299 ! 
0395 ! 
0500 REM "Open files
0505 DIM Z[NUM(X3$(60,3))]
0509 ! Slot 10 is used for SB1 file opened in 1000's
0510 Z$="01O AR6... 02O AR1...  13O ZZPARM  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0530 GOSUB 7900; REM "Create if necessary, open SB_LOG file
0595 ! 
0950 DIM FILE_FMT$(2); FILE_FMT$="01",X$="Valid formats: 01"
0952 RETURN 
0955 ! 
0992 PROCEED: FILE_FMT$=ML_FORMAT$
0995 ! 
1000 REM "Get list of SB1 files to process
1005 ! Logic copied from FO2UBA
1010 CALL "ZZ2BLS","D0:SB1*","S",F$
1012 IF X3$(77,1)="U" THEN CALL "ZZ2BLS","D0:sb1*","S",F_LOWER$; F$=F$+F_LOWER$
1015 P=POS(":"=F$); IF P=0 THEN GOTO 5000
1020 F1$=F$(P+1); P1=POS(":"=F1$); IF P1>0 THEN F$=F1$(P1-2),F1$=F1$(1,P1-3) ELSE F$=""
1050 Z$="10CU 10O "+F1$+" "
1055 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 1056,1995
1060 CLOSE (Z[10]); OPEN LOCK (Z[10],OPT="TEXT")F1$; REM "Open for reading raw file
1065 CLOSE (13); OPEN (13)"ZZPARM"; REM "Re-open ZZPARM closed in ZZ2BLS
1075 IF Q1$<>"G" THEN PRINT @(15,15),'CE',"Processing: "+F1$,
1080 IF Q1$<>"G" THEN GOSUB 8100; GOSUB 8150
1085 ! L0=227 ! Length of file format 01 records, change accordingly as file formats are added. For SB1, file will be tab delimited, no check for length of record needed.
1095 ! 
1100 REM "Process the file
1105 I9=0,RECORD$="",RCOUNT$=""; DIM RCOUNT[50]
1110 DIM RECORD$[1:100]; READ (Z[10],END=7750)IOL=CPL("IOLIST RECORD$[ALL]:[STR($09$)]")
1112 IF FILE_FMT$="01" THEN IF STP(CVS(RECORD$[1],16),3)="" OR MID(RECORD$[1],1,5)="Buyer" OR STP(RECORD$[1],3)="" THEN GOTO 1110 ! If first five characters indicate this is the headings, then read next record.
1113 I9=I9+1
1115 P=POS("SB1"=RCOUNT$,3); IF P=0 THEN RCOUNT$=RCOUNT$+"SB1"; GOTO 1115; REM "Used to write to /usr/lib/pvx/SB_LOG
1120 P1=(P+2)/3; RCOUNT[P1]=RCOUNT[P1]+1
1125 IF Q1$<>"G" THEN IF MOD(I9,T0)=1 THEN GOSUB 8150
1130 REM PRINT @(0,3),'CE',@(0,4),RECORD$; INPUT *
1195 ! 
1500 REM "Process RECORD$[ALL]
1510 MESSAGE$=""
1520 ON POS(FILE_FMT$="01") GOTO TYPE_NOT_FOUND,PROCESS_FILE_01
1530 GOTO 9900
1595 ! 
1900 REM "End of file
1901 REM "Close & Rename to SB4 for archiving. Erase an existing SB4 file, if same name
1905 Z$="10CU"+F1$+" "
1906 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
1910 F2$=F1$; IF F2$ LIKE "[Ss][Bb]1*" THEN F2$(1,3)="SB4" ELSE GOTO 1995
1915 ERASE F2$,ERR=1916
1919 REM "Get disk DIRectory of data files
1925 J$=%DATAPATH$
1930 REM "Move the file
1936 ORIGINAL$=J$+DLM+F1$,ARCHIVE$=J$+DLM+F2$; RENAME ORIGINAL$,ARCHIVE$,ERR=*NEXT
1995 GOTO 1015
1999 ! 
2000 TYPE_NOT_FOUND:REM "Record type sent in not found
2010 MESSAGE$="File layout format "+FILE_FMT$+" not found, can't process."
2015 GOSUB PRINT_MSG
2090 TYPE_NOT_FOUND_END:GOTO 9900
2099 ! 
2100 PROCESS_FILE_01:REM "Process file format 01 record (WO258053/101)
2110 CUST$=PAD(RECORD$[1],10)
2115 READ (Z[2],KEY=CUST$,DOM=INVALID_CUSTOMER)AR1$
2120 ! 
2125 INVOICE$=PAD(RECORD$[2],8)
2130 DIM AR6$(180); READ (Z[1],KEY=CUST$+INVOICE$,DOM=INVALID_INVOICE)AR6$(1)
2135 ! 
2140 NEW_SUMMARY_BILL_REF$=UCS(PAD(STP(RECORD$[3],3,$0D$),8))
2145 IF STP(NEW_SUMMARY_BILL_REF$,1)="" THEN GOTO BLANK_NEW_SUMMARY_BILL_REF
2150 ! 
2155 IF AR6$(160,8)=DIM(8) THEN GOTO BLANK_AR6_SUMMARY_BILL_REF
2160 ! 
2165 IF AR6$(160,8)="PENDING " THEN GOTO PENDING_AR6_SUMMARY_BILL_REF
2170 ! 
2175 EXTRACT RECORD (Z[1],KEY=CUST$+INVOICE$)AR6$
2180 AR6$(160,8)=NEW_SUMMARY_BILL_REF$
2185 WRITE RECORD (Z[1],KEY=CUST$+INVOICE$)AR6$
2190 PROCESS_FILE_01_END:GOTO 1110
2199 ! 
5000 REM "EOJ 
5020 MSGBOX "Process complete",MSG("CONFIRMING"),"!,TIM=4"
5040 RETURN 
5099 ! 
6000 REM "BACKGROUND
6165 PRINT (0,ERR=6066)'SF',
6190 RETURN 
6195 ! 
6200 REM "Display data
6210 GOSUB 6450
6295 RETURN 
6299 ! 
6400 REM "WHOLE SCREEN
6405 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,0
6420 GOSUB 6000
6430 IF C9>=0 THEN GOSUB 6200 ELSE GOSUB 6450
6440 IF ABS(C0)>4 THEN C0=ABS(C0)-5
6445 RETURN 
6450 REM "DISPLAY KEYS
6455 IF C9<0 THEN GOTO 6445
6458 PRINT @(34,13),FILE_FMT$(1,2),
6460 ! CALL "ZZDISP","A",CUST$(1,10),"A/R",X3$,"","",34,14,X4$
6490 RETURN 
6499 ! 
7550 INVALID_CUSTOMER:
7560 MESSAGE$="Invalid customer "+STP(RECORD$[1],1)+", Invoice "+STP(RECORD$[2],1)
7570 GOSUB PRINT_MSG
7590 INVALID_CUSTOMER_END:GOTO 1110
7595 ! 
7600 INVALID_INVOICE:
7610 MESSAGE$="Invalid invoice "+STP(RECORD$[2],1)+", Customer "+STP(RECORD$[1],1)
7620 GOSUB PRINT_MSG
7640 INVALID_INVOICE_END:GOTO 1110
7645 ! 
7650 BLANK_NEW_SUMMARY_BILL_REF:
7660 MESSAGE$="Blank new summary bill ref for Customer "+STP(RECORD$[1],1)+", Invoice "+STP(RECORD$[2],1)
7670 GOSUB PRINT_MSG
7690 BLANK_NEW_SUMMARY_BILL_REF_END:GOTO 1110
7695 ! 
7700 BLANK_AR6_SUMMARY_BILL_REF:
7710 MESSAGE$="Blank AR6 summary bill ref for Customer "+STP(RECORD$[1],1)+", Invoice "+STP(RECORD$[2],1)
7720 GOSUB PRINT_MSG
7740 BLANK_AR6_SUMMARY_BILL_REF_END:GOTO 1110
7745 ! 
7750 REM "End of file
7752 IF LEN(RCOUNT$)<=0 THEN GOTO 7795
7755 FOR X=1 TO LEN(RCOUNT$)-2 STEP 3
7756 S$="SB1|STAT|"+FN%CDS$+"|"+X3$(40,3)+"|"+X3$(9,3)+"|"+FID(0)+"|"+F1$+"|"+RCOUNT$(X,3)+"|"+STR(RCOUNT[(X+2)/3]:"0000")
7758 PRINT (14)S$
7759 NEXT X
7795 GOTO 1900
7799 ! 
7800 PENDING_AR6_SUMMARY_BILL_REF:
7810 MESSAGE$="PENDING AR6 summary bill ref for Customer "+STP(RECORD$[1],1)+", Invoice "+STP(RECORD$[2],1)
7820 GOSUB PRINT_MSG
7840 PENDING_AR6_SUMMARY_BILL_REF_END:GOTO 1110
7845 ! 
7900 REM "Open SB_LOG file, create if not there
7910 CLOSE (14); OPEN LOCK (14,ERR=*NEXT)"SB_LOG"; GOTO 7940
7915 IF ERR=12 THEN SERIAL "SB_LOG"; GOTO 7910
7920 MSGBOX "Log file could not be opened,"+SEP+SEP+"please contact Demandbridge Support.",MSG("WARNING"),"STOP,TIM=8"; CMD_STR$="E"; EXITTO 9900
7940 RETURN 
7949 ! 
7950 PRINT_MSG:
7960 PRINT (14)MESSAGE$
7990 PRINT_MSG_END:RETURN 
7995 ! 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8107 T=0
8113 CALL "ZZ2FNC;SERIALRECCNT",Z[10],T
8115 ML_MESS.CTL'VALUE$="There are "+STR(T)+" records to process"
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8135 T1=0
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",13,4,50,T1,T,I9
8195 RETURN 
8199 ! 
8910 DEF FND$(Z9$)=Z9$(1*2+1,2)+"/"+Z9$(7-1*2,2)+"/"+STR((ASC(Z9$(1,1))-65)*10+1900+NUM(Z9$(2,1))-1570*POS("  "=Z9$(1,2)):"####")
8915 DEF FNT$(Z9$)=STR(NUM(Z9$(1,2))-12*POS("13"<=Z9$(1,2),2)+12*POS("00"=Z9$(1,2)):"00")+":"+STR(NUM(Z9$(3,2))*.6:"00")+" "+CHR(65+15*POS("12"<=Z9$(1,2),2))+"M"
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "End
9905 END_PROC:
9907 %GUI=SAVE_GUI
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9915 CLOSE (14)
9920 SETERR 9940
9930 Q1$=A1$; EXIT 
9940 BEGIN ; SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "258053-Program to read text file and update summary bill num in AR6
