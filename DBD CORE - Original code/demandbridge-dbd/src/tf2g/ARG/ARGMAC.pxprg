0010 ! ARGMAC - Customer Credit Analysis
0035 REM "5.7 - 08/29/11 - 9.069722 - jdf - SSP# 248904
0037 REM "248904-The AR Terms Code field is not validated when you setup a   
0040 REM "Copyright 2011 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 IF TCB(13)=1 THEN BEGIN 
0055 _CUSTOMER$=%CUSTOMER$
0060 PROCESS "ARGMAC","../ARG/AR.EN"
0061 IF %NEW_CUST THEN PROCESS "FMGSHA","../ARG/AR.EN"
0065 %CUSTOMER$=_CUSTOMER$
0070 ! IF TCB(13)=1 THEN RUN "ZMENU"
0075 END 
0100 INIT:! ^100,5 - Initialization routine
0200 ! ^100,5 - Constants & functions
0205 DEF FND$(LOCAL X$)=MID(X$,3,2)+"/"+MID(X$,5,2)+"/"+STR(DEC(MID(X$,1,1))+125)+MID(X$,2,1)
0300 ! ^100,5 - Iolists and templates
0301 %PGM_NAME$="*argmac*" ! SSP# 187014
0305 GLOBAL_IOL:IOLIST %CUST_NAME$,%TERMS_CODE_DESC$
0310 HISTORY_IOL:IOLIST HIST_BAL1,HIST_BAL2,HIST_BAL3,HIST_BAL4,HIST_BAL5,HIST_BAL6,HIST_BAL7
0315 DIM ARP$:"T1:c(14),T2:c(1),T3:c(32),T4:c(8),T5:c(8),T6:c(8),T7:c(8),T8:c(8),T9:c(38),T10:c(1*),TN0:c(8*),TN1:c(8*),TN2:c(8*),TN3:c(8*),TN4:c(8*),TN5:c(8*),TN6:c(8*),TN7:c(8*)"
0500 ! 500,5 - Open files
0503 GOSUB SETUP_MESSAGES
0505 _FILE_NAME$="AR0"+%C$; _AR0=HFN; OPEN (_AR0,ERR=OPEN_ERR,IOL=*)_FILE_NAME$ ! Customer credit analysis
0510 _FILE_NAME$="AR1"+%C$; _AR1=HFN; OPEN (_AR1,ERR=OPEN_ERR,IOL=*)_FILE_NAME$ ! Customer master
0515 _FILE_NAME$="AR2"+%C$; _AR2=HFN; OPEN (_AR2,ERR=OPEN_ERR,IOL=*)_FILE_NAME$ ! Terms code master
0520 _FILE_NAME$="AR5"+%C$; _AR5=HFN; OPEN (_AR5,ERR=OPEN_ERR,IOL=*)_FILE_NAME$ ! Customer messages
0525 _FILE_NAME$="ARH"+%C$; _ARH=HFN; OPEN (_ARH,ERR=OPEN_ERR,IOL=*)_FILE_NAME$ ! Division master
0530 _FILE_NAME$="ZZPARM"; ZZPARM=HFN; OPEN (ZZPARM,ERR=OPEN_ERR)_FILE_NAME$
0540 ! 
0545 DIM SEC_PARM$(255); FIND (ZZPARM,KEY="osec"+%X3_OP_ID$,DOM=*NEXT)SEC_PARM$ ! SSP 194939
0565 GOTO CONTINUE
0570 OPEN_ERR: MSGBOX _MSG_FILOPNERR1$+QUO+_FILE_NAME$+QUO+SEP+_MSG_DIRECTORY$+LWD+SEP+_MSG_PREFIX$+PFX,MSG(ERR),"!"
0575 CMD_STR$="E"
0580 EXIT 
0585 ! 
0590 CONTINUE:
0595 ! 
0600 ! ^100,5 - Get parameters
0605 IF X1>1 THEN CMD_STR$="E"; EXIT 
0610 READ RECORD (ZZPARM,KEY=%C$+"A/R",DOM=*NEXT)ARP$; GOTO 0620
0615 CMD_STR$="E"; EXIT 
0620 SEC$=""; READ (ZZPARM,KEY="osec"+%X3_OP_ID$,DOM=*NEXT)SEC$
0625 GB$=""; READ (ZZPARM,KEY=%C$+"G/B",DOM=*NEXT)GB$
0630 TM_PARMS$=""; CALL "RTGPRM",ERR=*NEXT,TM_PARMS$
0635 ARP.TN5$=STP(STR(NUM(ARP.TN5$):"##,###,###"),2)+":"
0640 ARP.TN6$=STP(STR(NUM(ARP.TN6$):"##,###,###"),2)+":"
0645 ARP.TN7$=STP(STR(NUM(ARP.TN7$):"##,###,###"),2)+":"
0646 ! ARP.T4$ to ARP.T8$ - aging period descriptions
0647 ! ARP.T10$ - credit requester feature flag
0650 ! ARP.TN5$ to ARP.TN7$ - dollar ranges for summary
0660 ! 
0700 POST_DISPLAY:! ^100,5 - Do stuff after the panel is created
0702 ! IF %NEW_CUST THEN %NEW_CUST=0
0703 IF ARP$(15,1)<>"Y" THEN CALL "*wingrp;Hide",BILLTO.GRP$ ! SSP#231162
0705 IF %CUSTOMER$<>"" THEN {
0710 CUSTOMER$=%CUSTOMER$
0715 DISABLE CONTROL CUSTOMER.CTL
0720 GOSUB PROCESS_CUSTOMER
0721 CALLED$="Y"
0722 ! IF CALLED$="Y" THEN CALL "*wingrp;Hide",SCROLL.GRP$
0725  }
0730 NEXT_ID=CUSTOMER.CTL,REFRESH_FLG=1
0735 RETURN 
0740 ! 
1000 PROCESS_CUSTOMER:! ^1000,5 - Get customer code and associated fields
1005 READ DATA FROM "" TO IOL=IOL(_AR1)
1010 CALL "ZZWLKU;Parse_cust",CUSTOMER$,CUST_DIV$,CUST_CODE$
1015 READ (_AR1,KEY=CUST_DIV$+CUST_CODE$,DOM=*NEXT,ERR=CHECK_ERR); GOTO 1025
1020 READ (_ARH,KEY=CUST_DIV$,DOM=MISSING_DIV)
1025 BILL_TO_CUST$=BILL_TO_CUST_DIV$+BILL_TO_CUST_CODE$
1026 IF PRINT_DUN_LETTER$=" " THEN PRINT_DUN_LETTER$="N"
1030 READ DATA FROM "",REC=AR0$ TO IOL=IOL(_AR0)
1035 READ (_AR0,KEY=CUST_DIV$+CUST_CODE$,DOM=*NEXT,REC=AR0$)
1040 ! let next_id=cust_name.ctl
1045 CALL "*wingrp;enable",FIELDS.GRP$
1050 CALL "*wingrp;disable",KEY.GRP$
1055 CALL "*wingrp;enable",BUTTONS.GRP$
1060 IF MID(SEC$,45,1)="Y" OR (TM_PARMS$>"" AND MID(TM_PARMS$,7,1)="R") THEN CALL "*wingrp;disable",WRITE.GRP$
1065 GOSUB INIT_FOLDER
1070 REFRESH_FLG=1
1075 RETURN 
1080 ! 
1100 MISSING_DIV:! ^100,5 - Can't process this new customer code
1105 MSGBOX MSG("DIV_NOF",CUST_DIV$),MSG("ATTN"),"!"
1110 NEXT_ID=CUSTOMER.CTL,REFRESH_FLG=1,CUSTOMER$="",CUST_DIV$="",CUST_CODE$=""
1115 RETURN 
1120 ! 
1200 CHECK_CHANGES:! ^100,5
1205 IF CHANGE_FLG THEN {
1210 MSGBOX MSG("REC_ALTERD"),MSG("UPDATE"),"?,YESNO",ANSWER$
1215 IF ANSWER$="YES" THEN GOTO WRITE_REC
1220 CHANGE_FLG=0
1225  }
1230 REFRESH_FLG=1,CHANGE_FLG=0; IF STP(TERMS_CODE$)<>"" THEN IGNORE_EXIT=0 ELSE MSGBOX MSG("TERM_C_M"),MSG("ATTN"),"!" ! SSP248904 jdf
1235 RETURN 
1240 ! 
2000 FIND_REC:! ^1000,5 - Lookup record
2005 IF STP(CUSTOMER$,1)="" THEN {
2010 MSGBOX MSG("CUST_CODE")+" "+MSG("IS_MISSING"),MSG("ATTENTION"),"!"
2015 NEXT_ID=CUSTOMER.CTL,REFRESH_FLG=1,CHANGE_FLG=0
2020 EXIT 
2025  }
2030 READ DATA FROM "",REC=AR0$ TO IOL=IOL(_AR0)
2035 READ DATA FROM "" TO IOL=IOL(_AR1)
2040 CALL "ZZGLKU;Parse_cust",CUSTOMER$,CUST_DIV$,CUST_CODE$
2045 READ (_AR1,KEY=CUST_DIV$+CUST_CODE$,DOM=*NEXT); GOTO 2055
2050 READ (_ARH,KEY=CUST_DIV$,DOM=MISSING_DIV)
2055 BILL_TO_CUST$=BILL_TO_CUST_DIV$+BILL_TO_CUST_CODE$
2060 READ (_AR0,KEY=CUST_DIV$+CUST_CODE$,DOM=*NEXT,REC=AR0$)
2065 NEXT_ID=CUST_NAME.CTL,REFRESH_FLG=1,CHANGE_FLG=0
2070 CALL "*wingrp;enable",FIELDS.GRP$
2075 CALL "*wingrp;enable",BUTTONS.GRP$
2080 IF MID(SEC$,45,1)="Y" OR (TM_PARMS$>"" AND MID(TM_PARMS$,7,1)="R") THEN CALL "*wingrp;disable",WRITE.GRP$
2085 CALL "*wingrp;disable",KEY.GRP$
2090 EXIT 
2095 ! 
2100 CLEAR_REC:! ^100,5 - Initialize fields and clear globals
2105 GOSUB CHECK_CHANGES
2110 READ DATA FROM "",REC=AR0$ TO IOL=IOL(_AR0)
2115 READ DATA FROM "" TO IOL=IOL(_AR1)
2120 READ DATA FROM "" TO IOL=GLOBAL_IOL
2125 READ DATA FROM "" TO IOL=HISTORY_IOL
2130 CALL "*wingrp;disable",BUTTONS.GRP$
2135 CALL "*wingrp;disable",FIELDS.GRP$
2140 CALL "*wingrp;enable",KEY.GRP$
2145 CHANGE_FLG=0,BILL_TO_CUST$="",CUSTOMER$="",CREDIT_LEFT=0
2150 REFRESH_FLG=1
2155 EXIT 
2160 ! 
2200 WRITE_REC:! ^100,5 - Write the record
2205 IF STP(TERMS_CODE$)="" THEN {
2210 MSGBOX MSG("TERM_C_M"),MSG("ATTN"),"!"
2215 NEXT_FOLDER=FLDR.ARGMAC.2.CTL,NEXT_ID=TERMS_CODE.CTL,REFRESH_FLG=1
2220 EXIT 
2230  }
2250 WRITE (_AR1,KEY=CUST_DIV$+CUST_CODE$)
2260 AR0.CUST_DIV$=CUST_DIV$,AR0.CUST_CODE$=CUST_CODE$
2270 WRITE (_AR0,KEY=CUST_DIV$+CUST_CODE$,REC=AR0$)
2280 WRITE_FLG=1
2300 ! ^100,5 - Go back
2305 CHANGE_FLG=0
2310 ! SSP# 192975,should not clear sreen while in panel; IF DEC(MID(MSE,20,2))=BUTTON_1.CTL THEN GOTO CLEAR_REC ! Clear screen if write button was pressed
2315 RETURN 
2320 ! 
2500 ! Save Routine Options
2510 SAVE_ROUTINE:
2520 GOSUB WRITE_REC
2530 IF WRITE_FLG AND %NEW_CUST THEN WRITE_FLG=0; CMD_STR$="END"
2580 REFRESH_FLG=1
2590 RETURN 
3000 INIT_FOLDER:! ^1000,5 - Determine folder ID and adjust display
3005 CUSTOMER$=CUST_DIV$+CUST_CODE$
3010 BILL_TO_CUST$=STP(BILL_TO_CUST_DIV$+BILL_TO_CUST_CODE$,2)
3015 IF STP(CUSTOMER$,1)<>"" THEN {
3020 CALL "*wingrp;enable",FIELDS.GRP$
3025 CALL "*wingrp;enable",BUTTONS.GRP$
3030 IF MID(SEC$,45,1)="Y" OR (TM_PARMS$>"" AND MID(TM_PARMS$,7,1)="R") THEN CALL "*wingrp;disable",WRITE.GRP$
3035 CALL "*wingrp;disable",KEY.GRP$
3040  } ELSE {
3045 CALL "*wingrp;disable",FIELDS.GRP$
3050 CALL "*wingrp;disable",BUTTONS.GRP$
3055 CALL "*wingrp;enable",KEY.GRP$
3060  }
3064 NEXT_ID=BUTTON_2.CTL
3065 IF FOLDER_ID$="ARGMAC.1" THEN {
3070 CREDIT_LEFT=CREDIT_LIMIT-CUST_BAL-OPEN_ORDER_AMT
3075 IF CREDIT_LEFT<0 THEN {
3080 CREDIT_LEFT.CTL'TEXTCOLOR$="red"
3085  } ELSE {
3090 CREDIT_LEFT.CTL'TEXTCOLOR$="black"
3095  }
3100 READ DATA FROM "" TO IOL=HISTORY_IOL
3105 IF AR0.CUST_BAL_CNT THEN HIST_BAL1=AR0.CUST_HIST_BAL/AR0.CUST_BAL_CNT
3110 IF AR0.FUTURE_BAL_CNT THEN HIST_BAL2=AR0.FUTURE_HIST_BAL/AR0.FUTURE_BAL_CNT
3115 IF AR0.AGE_BAL_CNT_0 THEN HIST_BAL3=AR0.AGE_HIST_BAL_0/AR0.AGE_BAL_CNT_0
3120 IF AR0.AGE_BAL_CNT_1 THEN HIST_BAL4=AR0.AGE_HIST_BAL_1/AR0.AGE_BAL_CNT_1
3125 IF AR0.AGE_BAL_CNT_2 THEN HIST_BAL5=AR0.AGE_HIST_BAL_2/AR0.AGE_BAL_CNT_2
3130 IF AR0.AGE_BAL_CNT_3 THEN HIST_BAL6=AR0.AGE_HIST_BAL_3/AR0.AGE_BAL_CNT_3
3135 IF AR0.AGE_BAL_CNT_4 THEN HIST_BAL7=AR0.AGE_HIST_BAL_4/AR0.AGE_BAL_CNT_4
3140 IF AR0.SIZE_INV_CNT_1>0 THEN DAY_PAY1=INT(AR0.SIZE_INV_DAYS_1/AR0.SIZE_INV_CNT_1)
3145 IF AR0.SIZE_INV_CNT_2>0 THEN DAY_PAY2=INT(AR0.SIZE_INV_DAYS_2/AR0.SIZE_INV_CNT_2)
3150 IF AR0.SIZE_INV_CNT_3>0 THEN DAY_PAY3=INT(AR0.SIZE_INV_DAYS_3/AR0.SIZE_INV_CNT_3)
3155 IF AR0.SIZE_INV_CNT_4>0 THEN DAY_PAY4=INT(AR0.SIZE_INV_DAYS_4/AR0.SIZE_INV_CNT_4)
3160 TOT_INV=0,AVG_PAY=0
3165 FOR X=1 TO 4
3170 VAR0$="ar0.size_inv_cnt_"+STR(X)
3175 VAR1$="ar0.size_inv_days_"+STR(X)
3180 TOT_INV+=VIN(VAR0$)
3185 AVG_PAY+=VIN(VAR1$)
3190 NEXT X
3195 IF TOT_INV THEN AVG_PAY=INT(AVG_PAY/TOT_INV)
3200  }
3205 IF FOLDER_ID$="ARGMAC.2" THEN {
3210  }
3215 REFRESH_FLG=1
3220 IF STP(CUSTOMER$,2)="" THEN CHANGE_FLG=0
3225 RETURN 
3230 ! 
4000 NEXT_REC:! ^1000,5 - Find next record
4005 IF CHANGE_FLG<>0 THEN GOSUB CHECK_CHANGES
4010 _KEY$=KEY(_AR1,END=4035)
4015 READ (_AR1,KEY=_KEY$,ERR=CHECK_ERR)
4020 READ DATA FROM "",REC=AR0$ TO IOL=IOL(_AR0)
4025 READ (_AR0,KEY=_KEY$,ERR=*NEXT,REC=AR0$)
4030 GOTO INIT_FOLDER
4035 MSGBOX MSG("END_OF_FIL"),MSG("FYI"),"!"
4040 ! 
4100 PRIOR_REC:! ^100,5 - Get prior record
4105 IF CHANGE_FLG<>0 THEN GOSUB CHECK_CHANGES
4110 _KEY$=KEP(_AR1,END=4135)
4115 READ (_AR1,KEY=_KEY$,ERR=CHECK_ERR)
4120 READ DATA FROM "",REC=AR0$ TO IOL=IOL(_AR0)
4125 READ (_AR0,KEY=_KEY$,ERR=*NEXT,REC=AR0$)
4130 GOTO INIT_FOLDER
4135 MSGBOX MSG("START_FILE"),MSG("FYI"),"!"; EXIT 
4140 ! 
4200 FIRST_REC:! ^100,5 - Get first record
4205 IF CHANGE_FLG<>0 THEN GOSUB CHECK_CHANGES
4210 _KEY$=KEF(_AR1,ERR=4235)
4215 READ (_AR1,KEY=_KEY$,ERR=CHECK_ERR)
4220 READ DATA FROM "",REC=AR0$ TO IOL=IOL(_AR0)
4225 READ (_AR0,KEY=_KEY$,ERR=*NEXT,REC=AR0$)
4230 GOTO INIT_FOLDER
4235 MSGBOX MSG("REC_NO_1ST"),MSG("FYI"),"!"; EXIT 
4240 ! 
4300 LAST_REC:! ^100,5 - Last record
4305 IF CHANGE_FLG<>0 THEN GOSUB CHECK_CHANGES
4310 _KEY$=KEL(_AR1,ERR=4335)
4315 READ (_AR1,KEY=_KEY$,ERR=CHECK_ERR)
4320 READ DATA FROM "",REC=AR0$ TO IOL=IOL(_AR0)
4325 READ (_AR0,KEY=_KEY$,ERR=*NEXT,REC=AR0$)
4330 GOTO INIT_FOLDER
4335 MSGBOX MSG("REC_NO_LST"),MSG("FYI"),"!"; EXIT 
4340 ! 
4400 CHECK_ERR:! ^100,5 - Error on file read
4405 IF ERR<>0 THEN EXIT ERR
4410 MSGBOX MSG("REC_BUSY"),MSG("CONFIRM"),"?,YESNO",ANSWER$
4415 IF ANSWER$="NO" THEN EXIT ELSE RETRY 
4420 ! 
7000 ! Folder Operations
7010 FOLDER_OPS:
7020 IF MID(SEC$,45,1)="Y" THEN CALL "*wingrp;disable",SEC_1.GRP$
7080 REFRESH_FLG=1
7090 RETURN 
9200 SETUP_MESSAGES:
9210 _MSG_FILOPNERR1$=MSG("FILOPNERR1")
9220 _MSG_DIRECTORY$=MSG("DIRECTORY")
9230 _MSG_PREFIX$=MSG("PREFIX")
9290 SETUP_MESSAGES_END:RETURN 
9295 ! 
9700 WRAPUP:! 9700,5 - Close files and clean up
9705 READ DATA FROM "" TO IOL=GLOBAL_IOL
9710 IF _AR0 THEN CLOSE (_AR0)
9715 IF _AR1 THEN CLOSE (_AR1)
9720 IF _AR2 THEN CLOSE (_AR2)
9725 IF _AR5 THEN CLOSE (_AR5)
9730 IF _ARH THEN CLOSE (_ARH)
9735 IF ZZPARM THEN CLOSE (ZZPARM)
9736 IF %NEW_CUST THEN PROCESS "FMGSHA","../ARG/AR.EN"
9740 RETURN 
9900 ! END PROCESS
9910 END_PROCESS:
9915 IF FOLDER_ID$="ARGMAC.2" THEN TERMS_CODE$=TERMS_CODE.CTL'VALUE$ ! SSP248904 jdf   
9920 IF NUL(TERMS_CODE$) AND NOT(NUL(CUSTOMER$)) THEN IF CHANGE_FLG=0 AND ANSWER$<>"NO" AND ID$<>"BUTTON_3" THEN MSGBOX MSG("TERM_C_M"),MSG("ATTN"),"!"; END_IF ; NEXT_ID=TERMS_CODE.CTL,IGNORE_EXIT=1,REFRESH_FLG=1; RETURN ELSE IGNORE_EXIT=0 ! SSP248904 jdf
9930 IF %NEW_CUST=0 THEN {
9940 CMD_STR$="END"
9950  } ELSE {
9960 PROCESS "FMGSHA","../ARG/AR.EN"
9970 CMD_STR$="END"
9980  }
9990 EXIT 
9999 END 
20000 ! Load Variables
20010 LOAD_VAR:
20020 ARG_1$=STR(CUST_BAL)+","+STR(FUTURE_BAL)+","+STR(CURR_BAL)+","+STR(AGE_BAL_1)+","+STR(AGE_BAL_2)+","+STR(AGE_BAL_3)+","+STR(AGE_BAL_4)+"/"
20030 ARG_2$="A/R Balance"
20040 ARG_3$=MSG("BAL")+"="+STR(CUST_BAL)+"/"+MSG("FUTURE_BAL")+"="+STR(FUTURE_BAL)+"/"+STP(ARP.T4$,3," ")+"="+STR(CURR_BAL)+"/"+STP(ARP.T5$,3," ")+"="+STR(AGE_BAL_1)+"/"+STP(ARP.T6$,3," ")+"="+STR(AGE_BAL_2)+"/"+STP(ARP.T7$,3," ")+"="+STR(AGE_BAL_3)+"/"+STP(ARP.T8$,3," ")+"="+STR(AGE_BAL_4)+"/"
20050 ARG_4$=MSG("BAL")+"/"+MSG("FUTURE_BAL")+"/"+ARP.T4$+"/"+ARP.T5$+"/"+ARP.T6$+"/"+ARP.T7$+"/"+ARP.T8$+"/"
20060 ARG_5$=""
20080 PROCESS "GLGMDA.1","../ARG/AR.EN",ARG_1$,ARG_2$,ARG_3$,ARG_4$,ARG_5$
20100 RETURN 
25600 LOAD_GRAPHIC:
25610 VAR$=ARG_1$,CHART_1.CTL'TITLE1$=ARG_2$,VAL1$=ARG_3$,VAR2$=ARG_4$,XAXISTITLE$=ARG_5$
25612 GOSUB DEFAULTS
25613 FOR XX=1 TO 12; VAL3$=VAL3$+" =0/"; NEXT XX
25615 IF CB_3D$="0" THEN CHART_1.CTL'TITLE2$=ARG_2$,CHART_1.CTL'TITLE1$="" ELSE CHART_1.CTL'TITLE1$=ARG_2$,CHART_1.CTL'TITLE2$=""
25618 MTHS$=MSG("MTHS")
25619 FOR XX=1 TO LEN(MTHS$) STEP 3; MONTHS$=MONTHS$+MTHS$(XX,1)+"="+MTHS$(XX,3)+" "; NEXT XX
25620 ! PROCESS "GLGMDA.1","../ARG/AR.EN",VAR$,%CUST_NAMES$,VAL1$,VAR2$,OP$
25680 GOSUB SELECT_GRAF
25700 REFRESH_FLG=1,B_GRAF$=""
25705 CHART ENABLE AA
25710 RETURN 
32000 SET_DEFAULTS:
32010 WRITE (_ZZPARM,KEY=%PGM_NAME$+%X3_OP_ID$)CB_3D$,BACKCOLOR$,COLORS$,GRAF$,LB_GRAP$,FONT$ ! SSP# 187014
32011 DEFAULTS: _ZZPARM=FFN("ZZPARM"); IF _ZZPARM<0 THEN _ZZPARM=HFN; OPEN (_ZZPARM)"ZZPARM" ! SSP# 187014
32012 READ (_ZZPARM,KEY=%PGM_NAME$+%X3_OP_ID$,DOM=*NEXT)CB_3D$,BACKCOLOR$,COLORS$,GRAF$,LB_GRAP$,FONT$; GOTO *RETURN ! SSP# 187014
32015 CB_3D$="1",BACKCOLOR$="Light White",COLORS$="Light Yellow",GRAF$="AREA",LB_GRAP$="1",FONT$="Georgia" ! SSP# 187014
32017 GOTO SET_DEFAULTS ! SSP# 187014
32020 RETURN 
33270 SELECT_GRAF:
33280 DIM GRA$[8]; GRA$[1]="AREA",GRA$[2]="BAR",GRA$[3]="COLUMN",GRA$[4]="LINE",GRA$[5]="PIE",GRA$[6]="RIBBON",GRA$[7]="SCATTER",GRA$[8]="STACK"
33290 GRAF$=GRA$[NUM(LB_GRAP$)]
33300 IF GRAF$="PIE" THEN BB$=VAL1$ ELSE BB$=VAR$
33310 IF CB_3D$="0" THEN EFECT$="2D" ELSE EFECT$="3D"
33320 IF CB_3D$="0" THEN CHART_1.CTL'TITLE2$=ARG_2$,CHART_1.CTL'TITLE1$="" ELSE CHART_1.CTL'TITLE1$=ARG_2$,CHART_1.CTL'TITLE2$=""
33330 AA=EVN("CHART_1.CTL"),FOREGROUND.CTL'TEXTCOLOR$=COLORS$,BACKGROUND.CTL'TEXTCOLOR$=BACKCOLOR$; IF FONT$<>"" THEN REFRESH_FLG=1,AA'FONT$=FONT$,FONTD$="Font:",FONTD.CTL'FONT$=FONT$ ! ,COLORS.CTL'TEXTCOLOR$=COLORS$,COLORS.CTL'BACKCOLOR$=BACKCOLOR$,BACKCOLOR.CTL'TEXTCOLOR$=COLORS$,BACKCOLOR.CTL'BACKCOLOR$=BACKCOLOR$
33340 CHART HIDE AA
33350 IF B_GRAF$="PIE" THEN CHART DELETE AA
33360 ! AA'FMT$=EFECT$+GRAF$,AA'TEXTCOLOR$=COLORS$,AA'BACKCOLOR$=BACKCOLOR$
33370 CHART LOAD CHART_1.CTL,BB$
33380 AA'FMT$=EFECT$+GRAF$,AA'TEXTCOLOR$=COLORS$,AA'BACKCOLOR$=BACKCOLOR$,REFRESH_FLG=1,AA'POINTTEXT$=VAR2$,AA'LEGENDLOCATION$="TOPRIGHT"
33390 IF CB_3D$="0" THEN CHART_1.CTL'TITLE2$=ARG_2$,CHART_1.CTL'TITLE1$="" ELSE CHART_1.CTL'TITLE1$=ARG_2$,CHART_1.CTL'TITLE2$=""
33400 IF FOOTER$<>"" THEN AA'FOOTER$=FOOTER$
33410 ! IF GRAF$="PIE" THEN AA'FONT$="Arial Narrow,-10"
33420 CHART SHOW AA
33430 IF CB_3D$="0" AND ARG_5$<>"" THEN AA'TITLE2$=ARG_2$+"      "+ARG_5$,AA'XAXISTITLE$="",XAXISTITLE$="" ELSE AA'XAXISTITLE$=ARG_5$,XAXISTITLE$=ARG_5$
33440 IF XAXISLOCATION$<>"" THEN AA'XAXISLOCATION$=XAXISLOCATION$
33450 IF XAXISTITLE$<>"" THEN AA'XAXISTITLE$=XAXISTITLE$
33460 B_GRAF$=GRAF$
33470 RETURN 
56000 ! "187014-This problem was in release also.  When clicking on the     
56001 ! "       Exit button
56002 REM "192975-Program ARGMAC Customer Setup/Inquiry, Credit Analysis. SAVE
56010 ! "194939-GUI portion of 187445 to limit access to credit information 
56012 REM "248904-The AR Terms Code field is not validated when you setup a   
