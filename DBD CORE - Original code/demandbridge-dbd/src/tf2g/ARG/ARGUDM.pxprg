0010 REM "Remove Invoice Batch records from files create for ODBC <AR2UDM>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 03/31/15 - 13.470555 - tma - SSP# 259674
0037 REM "259674-Add utility to GUI to clear AZ files.  These are the invoice
0040 REM "Copyright 2015 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 IF TCB(88)=0 THEN MSGBOX "You must be using Windx to access this program" ELSE PROCESS "ARGUDM","../ARG/AR.EN"
0055 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0060 EXIT 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0100 SETERR 9000
0105 INIT:
0110 X0$="AR2UDM",X1$="Remove Records from ODBC Avail Files"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0391 IOLIST AZB$,AZB[0],AZB[1],AZB[2],AZB[3],AZB[4],AZB[5],AZB[6],AZB[7],AZB[8],AZB[9],AZB[10],AZB[11],AZB[12],AZB[13],AZB[14],AZB[15],AZB[16],AZB[17],AZB[18],AZB[19],AZB[20]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O AZB...  02O AZT...  03O AZS... 04O AZD...  05O AZC...  06O AZ3...  13O ZZPARM  "
0511 GOSUB LOAD_FISCAL_YEAR
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0600 REM "
0610 GOSUB 6000
0620 RETURN 
0640 ! CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900!SSP#236056
0700 PROCEED:
0710 DIM A$(6); A$(1)=RPT_FISYR$+RPT_PER$
1000 REM 
1010 K$=KEY(Z[1],END=5000)
1025 IF K$(1,6)>A$(1,6) THEN READ (Z[1]); GOTO 5000
1040 DIM AZB[20]; READ (Z[1],KEY=K$)IOL=0391
1080 REMOVE (Z[1],KEY=K$)
1100 READ (Z[2],KEY=K$,DOM=*NEXT)
1110 AZT_KEY$=KEY(Z[2],END=1200)
1120 IF AZT_KEY$(1,17)<>K$(1,17) THEN GOTO 1200
1130 REMOVE (Z[2],KEY=AZT_KEY$)
1140 GOTO 1110
1200 READ (Z[6],KEY=K$(7,8),DOM=*NEXT)
1210 AZ3_KEY$=KEY(Z[6],END=1300); READ (Z[6],KEY=AZ3_KEY$)AZ3_REC$ ! SSP#259372
1215 IF AZ3_KEY$(1,8)<>K$(7,8) THEN GOTO 1300 ! If not the same invoice we are done !SSP#259372
1220 IF AZ3_REC$(34,6)+AZ3_REC$(1,8)+AZ3_REC$(21,3)<>K$(1,17) THEN READ (Z[6]); GOTO 1210 ! If not the same Year+period & seq # check next record !SSP#259372
1230 REMOVE (Z[6],KEY=AZ3_KEY$)
1240 GOTO 1210
1300 READ (Z[4],KEY=K$,DOM=*NEXT)
1310 AZD_KEY$=KEY(Z[4],END=1400); READ (Z[4],END=1400)
1320 IF AZD_KEY$(1,17)<>K$(1,17) THEN GOTO 1400 ! SSP#259372
1330 REMOVE (Z[4],KEY=AZD_KEY$)
1340 GOTO 1310
1400 READ (Z[3],KEY=K$(1,14),DOM=*NEXT)
1410 AZS_KEY$=KEY(Z[3],END=1500); READ (Z[3],END=1500)
1415 IF AZS_KEY$(1,14)<>K$(1,14) THEN GOTO 1500 ! If not the same invoice we are done !SSP#259372
1420 IF AZS_KEY$(1,14)+AZS_KEY$(25,3)<>K$(1,17) THEN GOTO 1410 ! If not the same sequence read the next record !SSP#259372
1430 REMOVE (Z[3],KEY=AZS_KEY$)
1440 GOTO 1410
1500 READ (Z[5],KEY=K$(1,14),DOM=*NEXT)
1510 AZC_KEY$=KEY(Z[5],END=1600)
1520 IF AZC_KEY$(1,17)<>K$(1,17) THEN GOTO 1600
1530 REMOVE (Z[5],KEY=AZC_KEY$)
4990 C1=C1+1
4999 GOTO 1000
5000 REM "EOJ
5010 X$=STR(C1)+" records removed" ! SSP#259643
5030 MSGBOX "Process complete!!"+SEP+SEP+X$,MSG("CONFIRMING"),"INFO"
5040 GOTO 9900
6000 REM "BACKGROUND
6190 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9901 END_PROC: CMD_STR$="E"
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
30180 LOAD_FISCAL_YEAR:
30190 ZZPARM=HFN
30200 OPEN (ZZPARM)"ZZPARM"
30210 READ (ZZPARM,KEY=%C$+"A/R",DOM=*NEXT)GL_REC$; CR_YEAR$=GL_REC$(7,4),CR_PD$=GL_REC$(11,2)
30215 IF NUL(RPT_FISYR$)=0 THEN CR_YEAR$=RPT_FISYR$
30220 KE$=%C$+"G/LYE",RPT_FISYR$="    "+"/"
30230 READ (ZZPARM,KEY=KE$,DOM=*NEXT)
30240 K$=KEY(ZZPARM,END=30280); READ (ZZPARM)Z9$
30250 IF LEN(K$)<8 OR KE$<>K$(1,8) THEN GOTO END_LOAD_FISCAL_YEAR
30260 RPT_FISYR$=RPT_FISYR$+K$(9)+"/"
30265 IF K$(9,4)=CR_YEAR$ THEN CC$=Z9$(21)
30270 GOTO 30240
30280 END_LOAD_FISCAL_YEAR:
30290 DROP_BOX LOAD RPT_FISYR.CTL,RPT_FISYR$
30300 DROP_BOX WRITE RPT_FISYR.CTL,CR_YEAR$
30310 DROP_BOX LOAD END_FISCAL.CTL,RPT_FISYR$
30320 DROP_BOX WRITE END_FISCAL.CTL,CR_YEAR$
30321 RPT_FISYR$=CR_YEAR$
30322 IF NUL(RPT_PER$)=0 THEN CR_PD$=RPT_PER$
30330 FOR ACCT=1 TO 12; RPT_PER$=RPT_PER$+STR(ACCT:"00")+"/"; NEXT ACCT
30340 DROP_BOX LOAD RPT_PER.CTL,RPT_PER$
30350 DROP_BOX WRITE RPT_PER.CTL,CR_PD$
30355 RPT_PER$=CR_PD$
30360 END_ACCTPD$=RPT_PER$
30370 DROP_BOX LOAD END_ACCTPD.CTL,RPT_PER$
30380 DROP_BOX WRITE END_ACCTPD.CTL,CR_PD$
30390 RETURN 
32000 LOAD_ENDING_DATE:
32005 IF LEN(RPT_FISYR$)>4 THEN VAL$=CR_YEAR$ ELSE VAL$=RPT_FISYR$
32010 FIND (ZZPARM,KEY=KE$+VAL$)Z9$; CC$=Z9$(21)
32011 IF LEN(RPT_PER$)>2 THEN VAL=NUM(CR_PD$) ELSE VAL=NUM(RPT_PER$)
32020 CC$=CC$(VAL*6-5,6),RPT_ENDDATE$=CC$,REFRESH_FLG=1
32025 REFRESH_FLG=1
32030 RETURN 
56001 REM "259372-Needs to purge records from the AZT file.                   
