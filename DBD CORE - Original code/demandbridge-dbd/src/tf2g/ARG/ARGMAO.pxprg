0010 ! ARGMAO - Order History by Order Number
0035 REM "5.7 - 06/22/21 - 19.474075 - jvv - SSP# 307357
0037 REM "307357-Update FS6 to allow for more line numbers                   
0040 REM "Copyright 2021 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0042 IF TCB(13)=1 THEN BEGIN 
0050 _CUSTOMER$=%CUSTOMER$
0060 PROCESS "ARGMAO","../ARG/AR.EN"
0070 %CUSTOMER$=_CUSTOMER$
0080 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 _SV_KR=PRM('KR'); SET_PARAM 'KR'=0 ! Ensure native handling of KEP()
0140 CHANGE_FLG=0,_ENABLE_FLG=0,_KCNT=1,_FIRST_KEY=0,_FIRST_FIELD=0
0150 X2=-1; X0=-1
0155 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2
0160 DIM Z[NUM(X3$(60,3))]
0165 Z$="01O FS6...  02O AR1...  03O FS8...  05O IC0...  06O FM1...  07O FMP...  08O FS1...  09O ASC...  10O AP4... 11O FS9...  13O ZZPARM  14O FTT...  " ! [SSP-200893]-added FTT 
0170 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
0182 FTT_LFN=Z[14] ! [SSP-200893]
0190 ! IO List section
0200 DIM C[15],FS6[6],FM1[18],AP4[1]
0210 ! IOlist for customer
0220 IOLIST C$,C{ALL}
0230 ! IOList for Invoice History
0240 IOLIST FS6$,FS6{ALL}
0250 ! IOList for Forms
0260 IOLIST FM1$,FM1{ALL}
0270 ! iolist for Vendor
0280 IOLIST AP4$,AP4{ALL}
0290 ! iolist for Contracts
0294 DIM FTT[11] ! [SSP-200893]
0295 FTT_IOLIST:IOLIST FTT$,FTT[0],FTT[1],FTT[2],FTT[3],FTT[4],FTT[5],FTT[6],FTT[7],FTT[8],FTT[9],FTT[10],FTT[11] ! [SSP-200893]
0390 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 IF %CUSTOMER$<>"" THEN {
0530 CUSTOMER$=%CUSTOMER$
0540 DISABLE CONTROL CUSTOMER.CTL
0550 GOTO GET_CUSTOMER }
0560 NEXT_ID=CUSTOMER.CTL,REFRESH_FLG=1
0570 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0930 SET_PARAM 'XI'=0 ! SSP249755 jdf
0960 SET_PARAM 'KR'=_SV_KR
0970 RETURN 
2000 ! Get customer Information
2005 GET_CUSTOMER:
2030 READ (Z[2],KEY=CUSTOMER$,DOM=*RETURN)IOL=0220
2040 CUST_NAME$=C$(11,35)
2050 NEXT_ID=ORDER_NUMBER.CTL
2060 REFRESH_FLG=1
2090 RETURN 
2500 ! get invoice Information
2510 GET_INVOICE:
2513 LIST_BOX LOAD ORDER_HIST.CTL,""
2514 NEXT_ID=ORDER_HIST.CTL; REFRESH_FLG=1
2518 IF ORDER_NUMBER$="" THEN RETURN 
2520 KEY_1$="",KEY_1$=CUSTOMER$+ORDER_NUMBER$
2530 READ (Z[1],KNO=2,KEY=KEY_1$,DOM=*NEXT)
2540 KEY_1$=KEY(Z[1],END=*RETURN); READ (Z[1])IOL=0240
2550 IF FS6$(22,8)<>ORDER_NUMBER$ THEN RETURN 
2560 PO_NUM$=FS6$(54,15),HIST_TYPE$=FS6$(11,1)
2570 D1$=FS6$(34,6); CALL "ZZWDTE;DISPLAY",D1$,A1$,A2$
2575 D2$=FS6$(48,6); CALL "ZZWDTE;Display",D2$,A1$,A2$
2577 GOSUB CHECK_ORDER
2578 SET_PARAM 'XI'=0 ! SSP249755 jdf
2580 ORD_DATE$=D1$; REFRESH_FLG=1
2581 FIND (Z(13),KEY="U/M"+FS6$(101,4),DOM=*NEXT)UM$,M0,M1 ! SSP#258906
2585 GOSUB GET_DESCRIPTION
2586 MSG_1$=DIM(10); IF FS6$(111,1)="B" THEN MSG_1$="Bill as Ship"
2587 IF FS6[2]=0 THEN FS6[2]=1
2588 DISP_INV_NUM$=FN%ZZDISP$(FS6$(40,8),"AR6") ! 214033[SSP-200893]-format inv number
2589 IF MID(UM$,21,1)="Y" THEN EXT_PRC$=STR(FS6[1]:"###,###.00-") ELSE EXT_PRC$=STR(((FS6[0]/FS6[2])*FS6[1]):"###,###,###.00-") END_IF ; IF NUM(EXT_PRC$)=0 THEN EXT_PRC$=STR(FS6[1]:"###,###,###.00-") ! SSP#255955!SSP#258906
2590 LIST_BOX LOAD ORDER_HIST.CTL,0,BL$+MID(FS6$,114,3)+SEP+BL$+FS6$(30,1)+SEP+BL$+FS6$(12,10)+SEP+BL$+ITEM_DESC$+SEP+BL$+STR(FN%QTY_IN_UM(FS6[0],FS6$(101,4),FS6(2)):"###,###,###-")+SEP+BL$+FS6$(101,4)+SEP+BL$+EXT_PRC$+SEP+BL$+DISP_INV_NUM$+SEP+BL$+D2$+SEP+BL$+FN%ZZDISP$(FS6$(22,8),"O/P")+SEP+BL$+FS6$(11,1)+SEP+BL$+MSG_1$ ! [SSP-200893]!SSP#255955 ssp 307357
2600 READ (Z[1],KNO=2,KEY=KEY_1$,DOM=*NEXT)
2610 GOTO 2540
2620 RETURN 
2700 ! get invoice Information by Customer's Purchase Order.
2710 GET_INVOICE_PO:
2712 LIST_BOX LOAD ORDER_HIST.CTL,""
2714 NEXT_ID=ORDER_HIST.CTL; REFRESH_FLG=1
2718 IF PO_NUM$="" THEN RETURN 
2719 ORDER_SE$=""; IF QRY_VAL$<>"" THEN PO_NUM.CTL'VALUE$=PO_NUM$,ORDER_SE$=QRY_VAL$(16),QRY_VAL$=""
2720 KEY_1$="",KEY_1$=CUSTOMER$+PO_NUM$
2730 READ (Z[1],KNO=4,KEY=KEY_1$,DOM=*NEXT)
2740 KEY_1$=KEY(Z[1],END=*RETURN); READ (Z[1])IOL=0240
2750 IF FS6$(54,15)<>PO_NUM$ THEN RETURN 
2755 IF ORDER_SE$<>"" AND ORDER_SE$<>FS6$(22,8) THEN GOTO 2740
2760 ORDER_NUMBER$=FS6$(22,8),HIST_TYPE$=FS6$(11,1)
2770 D1$=FS6$(34,6); CALL "ZZWDTE;DISPLAY",D1$,A1$,A2$
2775 D2$=FS6$(48,6); CALL "ZZWDTE;Display",D2$,A1$,A2$
2777 GOSUB CHECK_ORDER
2778 SET_PARAM 'XI'=0 ! SSP249755 jdf
2780 ORD_DATE$=D1$; REFRESH_FLG=1
2781 FIND (Z(13),KEY="U/M"+FS6$(101,4),DOM=*NEXT)UM$,M0,M1 ! SSP#258906
2785 GOSUB GET_DESCRIPTION
2787 IF FS6[2]=0 THEN FS6[2]=1
2788 DISP_INV_NUM$=FN%ZZDISP$(FS6$(40,8),"AR6") ! 214033[SSP-200893]-format inv number
2789 IF MID(UM$,21,1)="Y" THEN EXT_PRC$=STR(FS6[1]:"###,###.00-") ELSE EXT_PRC$=STR(((FS6[0]/FS6[2])*FS6[1]):"###,###,###.00-") END_IF ; IF NUM(EXT_PRC$)=0 THEN EXT_PRC$=STR(FS6[1]:"###,###,###.00-") ! SSP#255955 !SSP#258906
2790 LIST_BOX LOAD ORDER_HIST.CTL,0,BL$+MID(FS6$,114,3)+SEP+BL$+FS6$(30,1)+SEP+BL$+FS6$(12,10)+SEP+BL$+ITEM_DESC$+SEP+BL$+STR(FN%QTY_IN_UM(FS6[0],FS6$(101,4),FS6(2)):"###,###,###-")+SEP+BL$+FS6$(101,4)+SEP+BL$+EXT_PRC$+SEP+BL$+DISP_INV_NUM$+SEP+BL$+D2$+SEP+BL$+FN%ZZDISP$(FS6$(22,8),"O/P")+SEP+BL$+FS6$(11,1) ! [SSP-200893]!SSP#255955 SSP307357
2800 READ (Z[1],KNO=4,KEY=KEY_1$,DOM=*NEXT)
2810 GOTO 2740
3000 ! Select the lines from order history
3010 SELECT_LINE:
3015 IF _EOM$=$01$ THEN RETURN 
3020 LIST_BOX READ ORDER_HIST.CTL,IDX
3030 LIST_BOX FIND ORDER_HIST.CTL,IDX,HIST_LINE$
3035 XX=POS($1B53461B4236$=HIST_LINE$); IF XX<>0 THEN HIST_LINE$=HIST_LINE$(1,XX-1)+HIST_LINE$(XX+6); GOTO 3035
3040 XX=POS($1B53461B4232$=HIST_LINE$); IF XX<>0 THEN HIST_LINE$=HIST_LINE$(1,XX-1)+HIST_LINE$(XX+6); GOTO 3040
3045 LINE_NUM$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1) ! SSP 305357
3050 SEQ_NUM$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1)
3060 ITEM_CODE$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1)
3070 ITEM_DESCRIPTION$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1)
3080 ORD_AMT$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1)
3090 U_M$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1)
3100 EXT$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1)
3110 INV_NUM$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1)
3120 INV_DATE$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1)
3125 ORDER_NUMBER$=HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1),ORDER_NUMBER$=STP(ORDER_NUMBER$,3,"-"); REM "SSP#209559
3130 LINE_TYPE$=HIST_LINE$; LINE_TYPE$=LINE_TYPE$(1,1); REM "HIST_LINE$(1,POS(SEP=HIST_LINE$)-1),HIST_LINE$=HIST_LINE$(POS(SEP=HIST_LINE$)+1)
3140 CALL "ZZWLKU;Parse_ord",ORDER_NUMBER$,B.ORD_DIV$,B.ORD_NUM$; ORDER_NUMBER$=B.ORD_DIV$+B.ORD_NUM$ ! SSP 209461
3180 PROCESS "FMGIAI","../FMG/FM.EN",CUSTOMER$+LINE_TYPE$+ITEM_CODE$+STP(ORDER_NUMBER$,3,"-")+SEQ_NUM$
3190 RETURN 
3300 ! Get Product Information
3310 GET_DESCRIPTION:
3320 ITEM_CODE$=FS6$(12,10)
3325 DIM FM1$(167),FM1[18]
3330 ON POS(FS6$(11,1)="BDFHJM") GOTO 3490,3345,3365,3380,3400,3420,3400
3335 ! custom Item case
3345 FIND (Z[6],KEY=CUSTOMER$+ITEM_CODE$,DOM=*NEXT)IOL=0260
3350 ITEM_DESC$=FM1$(42,40); PROD_CODE$=FM1$(21,3); LEFT_RIGHT$=FM1$(24,8); TOP_BOTTOM$=FM1$(32,8); PLY$=FM1$(40,2)
3355 GOTO 3490
3360 ! 
3365 FIND (Z[5],KEY=CUSTOMER$+ITEM_CODE$,DOM=3345)IOL=0260
3370 ITEM_DESC$=FM1$(21,40); PROD_CODE$=FM1$(61,3); LEFT_RIGHT$=FM1$(64,8); TOP_BOTTOM$=FM1$(72,8); PLY$=FM1$(80,2)
3375 GOTO 3490
3380 ! 
3385 FIND (Z[5],KEY="          "+ITEM_CODE$,DOM=3392)IOL=0260
3390 ITEM_DESC$=FM1$(21,40); PROD_CODE$=FM1$(61,3); LEFT_RIGHT$=FM1$(64,8); TOP_BOTTOM$=FM1$(72,8); PLY$=FM1$(80,2); GOTO 3395
3392 FIND (Z[6],KEY="          "+ITEM_CODE$,DOM=*NEXT)IOL=0260
3394 ITEM_DESC$=FM1$(42,40); PROD_CODE$=FM1$(21,3); LEFT_RIGHT$=FM1$(24,8); TOP_BOTTOM$=FM1$(32,8); PLY$=FM1$(40,2)
3395 GOTO 3490
3400 ! 
3405 FIND (Z[11],KEY=ORDER_NUMBER$+ITEM_CODE$+FS6$(30,1),DOM=*NEXT)IOL=0260
3410 ITEM_DESC$=FM1$(41,40); PROD_CODE$=FM1$(20,3); LEFT_RIGHT$=FM1$(23,8); TOP_BOTTOM$=FM1$(31,8); PLY$=FM1$(39,2)
3415 GOTO 3490
3420 ! 
3425 FIND (Z[7],KEY="X"+ITEM_CODE$,DOM=*NEXT)IOL=0260
3430 ITEM_DESC$=FM1$(12,40)
3490 RETURN 
3500 ! Order History Line Information
3510 HISTORY_LINE_INFO:
3515 DIM FS6[6],AP4[1]
3520 SEQ_NUM$=ARG_1$; ITEM_CODE$=ARG_2$; ITEM_DESCRIPTION$=ARG_3$; CUSTOMER$=ARG_4$
3530 U_M$=ARG_5$; EXT$=ARG_6$; INV_NUM$=ARG_7$; INV_DATE$=ARG_8$
3540 ORD_NUM$=ARG_12$; ORD_DATE$=ARG_15$; PO_NUM$=ARG_14$
3545 PROD_CODE$=ARG_16$; LEFT_RIGHT$=ARG_17$; TOP_BOTTOM$=ARG_18$; PLY$=ARG_19$
3550 KEY_1$=ARG_11$+ARG_13$+ITEM_CODE$+ORD_NUM$+SEQ_NUM$
3560 Z1=NUM(ARG_9$); Z5=NUM(ARG_10$)
3570 READ (Z1,KNO=0,KEY=KEY_1$,DOM=*RETURN)IOL=0240
3580 JOB_NUM$=FS6$(69,12); EST_RO$=FS6$(81,6)
3590 VEND_CODE$=FS6$(87,10); PLANT_CODE$=FS6$(97,4); LAST_THREE_BCKT_FLAG$=FS6$(105,1)
3600 ORD_AMT=FS6[0]; AMT_UNIT=FS6[1]; QTY_UNIT=FS6[2]
3610 EXT=(ORD_AMT/QTY_UNIT)*AMT_UNIT
3620 COST_UNIT=FS6[4]; C_EXT=(ORD_AMT/QTY_UNIT)*COST_UNIT
3630 GROSS_PROFIT=EXT-C_EXT
3635 IF EXT=0 THEN GP_P=100; GOTO 3680
3640 GP_P=(1-(C_EXT/EXT))*100
3680 GOSUB GET_VENDOR_INFO
3690 REFRESH_FLG=1
3700 RETURN 
3900 ! get vendor information
3910 GET_VENDOR_INFO:
3915 DIM AP4[1]
3920 Z10=NUM(ARG_20$)
3930 FIND (Z10,KEY=VEND_CODE$,DOM=*RETURN)IOL=0280
3940 VEND_NAME$=AP4$(11,35); CITY$=AP4$(106,16); TF_STATE$=AP4$(122,2)
3950 RETURN 
3990 RETURN 
4000 ! Re-order Date
4010 REORDER_DATE:
4020 CALL "*wingrp;enable",DATE_1.GRP$
4030 NEXT_ID=EST_RO.CTL
4040 RETURN 
4200 ! Re-order date setup
4210 REORDER_DATE_SETUP:
4220 FS6$(81,6)=EST_RO$
4225 KEY_1_1$=ARG_11$+ARG_13$+ITEM_CODE$+ORD_NUM$+SEQ_NUM$
4230 WRITE (Z1,KEY=KEY_1_1$)IOL=0240
4240 MSGBOX "Re-order Date Updated","Re-Order Date"
4250 NEXT_ID=EXIT_1.CTL
4260 CALL "*wingrp;disable",DATE_1.GRP$
4270 RETURN 
5000 ! check the order status
5010 CHECK_ORDER:
5030 REM BL$=""; READ (Z[8],KEY=ORDER_NUMBER$,DOM=*RETURN)FS1$; IF FS1$(63,1)="Y" THEN BL$='SF'+'_CYAN'; CALL "*WINGRP;SHOW",INVO.GRP$ ELSE BL$='SF'+'_GREEN'; CALL "*WINGRP;SHOW",INVO3.GRP$ ! SSP210101 jdf
5035 BL$=""; SET_PARAM 'XI'; READ (Z[8],KEY=ORDER_NUMBER$,DOM=*RETURN)FS1$; SET_PARAM 'XI'=0; IF FS1$(63,1)="Y" AND (FS6$(1,10)=FS1$(6,10)) THEN BL$='SF'+'_CYAN'; CALL "*WINGRP;SHOW",INVO.GRP$ ELSE IF FS6$(1,10)=FS1$(6,10) THEN BL$='SF'+'_GREEN'; CALL "*WINGRP;SHOW",INVO3.GRP$ ! SSP210101 jdf ! SSP249755 jdf
5040 CONTRACT_NUM$=FS1$(315,20) ! [SSP-200893]
5090 RETURN 
5099 ! 
6000 CLEAR_SCREEN:
6005 LIST_BOX LOAD ORDER_HIST.CTL,""
6010 ORDER_NUMBER$="",PO_NUM$="",CONTRACT_NUM$="" ! ssp#286531     
6090 REFRESH_FLG=1
6095 RETURN 
6099 ! 
6100 CLEAR_FOR_ORDER:
6105 LIST_BOX LOAD ORDER_HIST.CTL,""
6110 PO_NUM$="",CONTRACT_NUM$="" ! Was ORDER_NUMBER$="",PO_NUM$="",CONTRACT_NUM$="" !ssp#286531 
6115 REFERSH_FLG=1
6120 RETURN 
6150 CLEAR_FOR_PO:
6155 LIST_BOX LOAD ORDER_HIST.CTL,""
6160 CONTRACT_NUM$="" ! Was ORDER_NUMBER$="",PO_NUM$="",CONTRACT_NUM$="" !ssp#286531
6165 REFRESH_FLG=1
6170 RETURN 
6200 CLEAR_FOR_CN:
6205 LIST_BOX LOAD ORDER_HIST.CTL,""
6210 ORDER_NUMBER$="",PO_NUM$="" ! ,CONTRACT_NUM$="" !ssp#286531
6215 REFRESH_FLG=1
6220 RETURN 
9999 END 
20000 GET_CONTRACT_NUMBER:! [200893]-added routine
20005 LIST_BOX LOAD ORDER_HIST.CTL,""
20008 FTT_CONTRACT_NUM$="" ! SSP 301066
20010 TMP_CUSTOMER$=CUSTOMER$
20012 TMP_CONTRACT$=PAD(CONTRACT_NUM$,20)
20014 TMP_CONTRACT_DATE$=DIM(6)
20016 TMP_INVOICE_NUM$=DIM(6)
20030 TMP_KEY$=TMP_CONTRACT$+TMP_CUSTOMER$+TMP_CONTRACT_DATE$+TMP_INVOICE_NUM$
20035 READ (FTT_LFN,KEY=TMP_KEY$,DOM=*NEXT)IOL=FTT_IOLIST
20040 FTT_KEY$=KEY(FTT_LFN,END=*NEXT)
20045 IF FTT_KEY$="" THEN FTT_KEY$=DIM(30)
20050 IF TMP_KEY$(1,30)<>FTT_KEY$(1,30) THEN {
20060 GOSUB INVALID_CONTRACT_NUM
20065 GOTO *RETURN
20070  } ELSE {
20075 IF LEN(STP(FTT_CONTRACT_NUM$))=0 THEN GOSUB FTT_QRY
20080 IF LEN(STP(FTT_CONTRACT_NUM$))>0 THEN {
20090 FS6_LFN=Z[1]
20100 FS6_KNO=6
20110 FS6_KEY$=FTT_ORDER$+FTT_CUSTOMER$
20115 READ (FS6_LFN,KNO=FS6_KNO,KEY=FS6_KEY$,ERR=*RETURN,END=*RETURN)IOL=0240
20120 FS6_ORDER$=FS6$(22,8)
20130 FS6_CUSTOMER$=FS6$(1,10)
20140 WHILE FS6_ORDER$=FTT_ORDER$ AND FS6_CUSTOMER$=FTT_CUSTOMER$
20150 ORDER_NUMBER$=FS6$(22,8)
20160 D1$=FS6$(34,6); CALL "ZZWDTE;DISPLAY",D1$,A1$,A2$
20170 D2$=FS6$(48,6); CALL "ZZWDTE;Display",D2$,A1$,A2$
20180 GOSUB CHECK_ORDER
20183 SET_PARAM 'XI'=0 ! SSP249755 jdf
20185 PO_NUM$=FS6$(54,15)
20190 ORD_DATE$=D1$; REFRESH_FLG=1
20200 GOSUB GET_DESCRIPTION
20210 IF FS6[2]=0 THEN FS6[2]=1
20215 DISP_INV_NUM$=FN%ZZDISP$(FS6$(40,8),"AR6") ! 214033[SSP-200893]-format inv number
20220 LIST_BOX LOAD ORDER_HIST.CTL,0,BL$+MID(FS6$,114,3)+SEP_BL$+FS6$(30,1)+SEP+BL$+FS6$(12,10)+SEP+BL$+ITEM_DESC$+SEP+BL$+STR(FS6[0]:"###,###,###")+SEP+BL$+FS6$(101,4)+SEP+BL$+STR(((FS6[0]/FS6[2])*FS6[1]):"###,###,###.00")+SEP+BL$+DISP_INV_NUM$+SEP+BL$+D2$+SEP+BL$+FN%ZZDISP$(FS6$(22,8),"O/P")+SEP+BL$+FS6$(11,1) ! SSP 307357
20230 FS6_KEY$=KEY(FS6_LFN,END=*BREAK)
20240 READ (FS6_LFN)IOL=0240
20250 FS6_ORDER$=FS6$(22,8)
20260 FS6_CUSTOMER$=FS6$(1,10)
20270 WEND 
20280  }
20290  }
20295 NEXT_ID=ORDER_HIST.CTL; REFRESH_FLG=1
20495 RETURN 
20600 FTT_QRY:! [200893]-added routine
20610 VAL$=""
20612 FTT_CONTRACT_NUM$=""
20614 FTT_CUSTOMER$=""
20616 FTT_ORDER$=""
20620 PROCESS "QRY_FTT.1","../ARG/AR.EN",VAL$
20630 IF LEN(STP(VAL$))>0 THEN {
20640 FTT_CONTRACT_NUM$=VAL$(1,20)
20642 FTT_CUSTOMER$=VAL$(21,10)
20644 FTT_CONTRACT_DATE$=VAL$(31,6)
20646 FTT_INVOICE_NUM$=VAL$(37,8)
20648 FTT_ORDER$=VAL$(45,8)
20650 FTT_CUSTOMER_PO$=VAL$(53,15)
20665  }
20675 VAL$=""
20690 REFRESH_FLG=1
20695 RETURN 
20700 SEARCH_FTT:! [200893]-added routine
20720 GOSUB FTT_QRY
20730 IF LEN(STP(FTT_CONTRACT_NUM$))>0 THEN {
20735 CONTRACT_NUM$=FTT_CONTRACT_NUM$
20740 GOSUB GET_CONTRACT_NUMBER
20750  }
20795 RETURN 
20800 INVALID_CONTRACT_NUM:! [200893]-added routine
20810 MSG_TITLE$=MSG("CONTMSG_01")
20815 MSG_PARAM$="i,OK"
20825 MSG_TEXT$=MSG("CONTMSG_03")
20880 MSGBOX MSG_TEXT$,MSG_TITLE$,MSG_PARAM$
20890 NEXT_ID=CONTRACT_NUM.CTL
20895 RETURN 
56005 REM "5.4 - 03/21/06 - 17.149444 - kmc - SSP# 194087
56010 ! "207617-Order status using customer order number, not TF order      
56015 ! "209461-Line numbers are ascii sequence values not numbers          
56016 REM "209559-911-  Order History Index                                   
56018 REM "200893-Create New Contract number field in order processing        
56020 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
56022 REM "249755-Get error 0 when you select option Order history by order   
56023 REM "255955-Cannot see an order line with a negative qty in order       
56024 REM "255955-Cannot see an order line with a negative qty in order       
56025 REM "258906-Display issue in GUI Order History by Order Number when      using LOT U/M
56026 REM "283352-Order History by item and order history by order number     
56027 REM "286531-No longer able to copy the order number when looking up the 
56028 REM "301066-GUI - A/R, order history by order number, call up the order,
56029 REM "307357-Update FS6 to allow for more line numbers                   
