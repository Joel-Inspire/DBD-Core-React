0010 ! APGMAA - slsman Setup/Inquiry
0035 REM "5.7 - 02/06/18 - 15.036944 - crg - SSP# 263584
0037 REM "263584-Providex version upgrade from Pvx 7.1 to PxPlus 2017        
0040 REM "Copyright 2018 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! 
0060 PROCESS "ARGMBA","../ARG/AR.EN"
0070 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 ! 
0130 X0$="AR2MBA"
0140 X0=-1,X2=-1
0150 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2
0160 ! 
0170 ! Dim Variables
0180 DIM Q$[20],P$[20]
0190 DIM Z[NUM(X3$(60,3))],AR3[2]
0200 ! Open Files
0210 Z$="01O AR3...  02O ARF...  13O ZZPARM  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
0300 ! iolist
0310 IOLIST AR3$
0320 IOLIST AP8$
0400 ! get system parameters
0410 GOSUB GET_PARM
0420 %TEMP$=""; %NEW_SLSMAN=0
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 GOSUB BUILD_OPTIONS
0570 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0930 %SLSMAN$=""
0940 %VEND_NAME$=""
0970 RETURN 
1000 ! Process slsman
1010 PROCESS_SLSMAN:
1015 IF NUL(SLSMAN$) THEN RETURN 
1016 IF SLSMAN$="0000" THEN RETURN 
1040 READ (Z[1],KEY=SLSMAN$,DOM=NEW_SLSMAN)IOL=0310
1050 SALES_NAME$=AR3$(5,35)
1060 CALL "*wingrp;enable",GRID.GRP$; CALL "*wingrp;Disable",TMP.GRP$
1070 %SLSMAN$=SLSMAN$
1075 ! GOSUB SLSMAN_MESSAGE
1080 REFRESH_FLG=1
1090 RETURN 
2000 ! new slsman
2010 NEW_SLSMAN:
2030 MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"YesNo",ANS$
2040 IF ANS$="NO" THEN RETURN 
2045 %SLSMAN$=SLSMAN$; %NEW_SLSMAN=1
2050 CALL "ARGMBB"
2060 RETURN 
3000 ! slsman message
3010 SLSMAN_MESSAGE:
3015 IF STP(SLSMAN$)="" THEN RETURN 
3020 FIND (Z[5],KEY=SLSMAN$,DOM=*RETURN)IOL=0320
3025 IF NUL(AP8$(371,120)) THEN RETURN 
3030 MSG_1$=AP8$(371,60)+SEP+AP8$(431,60)
3040 MSGBOX MSG_1$,"slsman Message"
3050 RETURN 
5000 ! Build options on the slsman Screen
5010 BUILD_OPTIONS:
5020 MAX_OPT=1
5030 Q$[MAX_OPT]="Salesperson Constant Data",P$[MAX_OPT]="ARGMBB",MAX_OPT+=1
5040 Q$[MAX_OPT]="Statistical Data Inquiry",P$[MAX_OPT]="ZZGSDA",MAX_OPT+=1
5050 Q$[MAX_OPT]="Detailed Statistical Data",P$[MAX_OPT]="ARGMBC",MAX_OPT+=1
5060 Q$[MAX_OPT]="Delete/Reactivate Salesperson",P$[MAX_OPT]="DELETE",MAX_OPT+=1
5120 GOSUB CREATE_GRID
5130 REFRESH_FLG=1
5140 RETURN 
5500 ! Clear screen
5510 PROCESS_CLEAR:
5530 SLSMAN$=""
5540 NEXT_ID=SLSMAN.CTL
5550 %NEW_SLSMAN=0
5580 REFRESH_FLG=1
5590 RETURN 
6000 ! Inactivate of Reactivate slsman...
6010 DELETE_SLSMAN:
6065 IF AR3$(203,1)="I" THEN DEL_DESC$="Re-activate" ELSE DEL_DESC$="Delete"
6070 MSG_1$="Do you wish to "+DEL_DESC$+" this slsman?"
6080 MSGBOX MSG_1$,MSG("CONFIRM"),"?,YesNo",ANS$
6090 IF ANS$="NO" AND AR3$(203,1)<>"I" THEN RETURN 
6100 ! Yes to delete
6110 IF ANS$="YES" THEN {
6120 IF AR3$(203,1)="I" THEN AR3$(203,1)="A"; WRITE (Z[1],KEY=SLSMAN$)IOL=0310; RETURN 
6130  }
6140 Q2$="",Q0$="A/R",Q1$=SLSMAN$,Q3$="M"
6160 CALL "AR2UAC",ERR=*NEXT,X3$,X4$,Q0$,Q1$,Q2$,Q3$
6170 IF Q2$="Y" THEN MSG_1$="Record Deleted!"; GOTO 6220
6175 Q5$="Customer Sort     Commission PayableCommission BookingOrder Booking     "
6177 MSG_1$="Activity detected. There is "+Q2$ ! SSP#047384
6180 MSG_1$+=" - Record set to Inactive for Later Removal."
6190 ! IF X3$(30,1)="M" THEN IF POS(A1$(1,2)="0001",2)>0 THEN GOSUB CHANGE_PROFORMA
6200 AR3$(203,1)="I"; WRITE (Z[1],KEY=SLSMAN$)IOL=0310
6210 ! IF POS(X3$(30,1)="M")>0 THEN IF POS(SLSMAN$(1,2)="0001",2)>0 THEN CALL "CM2AAA",X3$,Z[1],1,SLSMAN$,"",X9
6220 MSGBOX MSG_1$,"FYI","INFO,OK"
6230 RETURN 
6400 ! Change lookup code for Performa slsmans
6410 CHANGE_PROFORMA:
6420 Q$=AR3$(145,10)+AR3$(1,10)
6430 CLOSE (14); OPEN (14)"AP6"+X3$(9,3); REMOVE (14,KEY=Q$,DOM=*NEXT)
6440 AR3$(145,10)="\"+AR3$(45,10); WRITE (14,KEY=Q$)
6450 RETURN 
9000 ! get system parameters
9010 GET_PARM:
9020 DIM SEC$(255); READ (Z[13],KEY="osec"+X3$(40,3),DOM=*NEXT)SEC$
9030 READ (Z[13],KEY=X3$(9,3)+"A/P",DOM=WRAPUP)F0$
9090 RETURN 
9999 END 
40000 ! !40000,10
40010 SEL_GRID:
40012 REFRESH_FLG=1
40020 GRID SELECT READ X,COL,ROW
40030 GRID FIND X,COL,ROW,VAL$
40035 IF STP(VAL$)="" THEN RETURN 
40036 IF AROW+ACOL<>0 THEN X'COLUMN=ACOL,X'ROW=AROW,X'TEXTCOLOR$="Black"
40038 X'COLUMN=COL,X'ROW=ROW,X'TEXTCOLOR$="Light Blue"
40040 AROW=ROW,ACOL=COL
40045 OPT=INT(POS(STP(VAL$)=ALL_MESS$)/40)+1; GOSUB PROCESS_OPT; GRID GOTO X,COL,ROW; RETURN 
40050 CREATE_GRID:
40060 GOSUB CREATE_OPT
40070 X=X.CTL,BOPT$=OPT$,ROW=1,COLUMN=0; GRID DELETE X,-1,-1
40080 HIDE CONTROL X
40082 HI=OPT_LN; IF FPT(HI)<>0 THEN HI=INT(HI)+1
40085 ! GRID REMOVE X; GRID X,@(2,9.6,75,HI)
40090 XX=POS(SEP=OPT$); IF XX=0 THEN GOTO END_LOAD
40100 COLUMN+=1; IF COLUMN>1 THEN COLUMN=1,ROW=ROW+1; GRID ADD X,0,ROW; GRID LOCK X,0,ROW
40110 TX$=OPT$(1,XX-1),OPT$=OPT$(XX+1),X'ROW=ROW,X'COLUMN=COLUMN,X'ALIGN$="L",X'COLUMNWIDTH=55,X'VALUE$=TX$,X'BACKCOLOR$="Gray",X'CELLTYPE$="Button",X'LOCK=1,X'ROWHEIGHT=1.6
40120 ! GRID LOAD X,ROW,COLUMN,TX$
40130 GOTO 40090
40140 END_LOAD: HIG=HI*X'HEIGHT/4,X'HEIGHT=HIG; SHOW CONTROL X; RETURN 
40150 CREATE_OPT:
40160 SE$="123456789ABCDEFG"
40170 FOR A=1 TO MAX_OPT-1
40182 MESS$=STP(Q$[A]),MESS$=PAD(MESS$,40," "),ALL_MESS$=ALL_MESS$+MESS$
40183 IF A=2 THEN MESS$=MESS$+"       "
40190 OPT$=OPT$+STP(MESS$)+SEP,OPT_LN=OPT_LN+1
40191 NXT_OPT:
40195 NEXT A
40200 RETURN 
42000 PROCESS_OPT:
42005 IF P$[OPT]="DELETE" THEN GOSUB DELETE_SLSMAN; RETURN 
42010 CALL P$[OPT]
42020 RETURN 
50000 ! 50000 - Browsing mode
50010 ! - Next
50020 NEXT_REC:
50040 SLSMAN$=KEY(Z(1),END=END_OF_FILE)
50050 READ (Z(1),KEY=SLSMAN$,ERR=*NEXT); GOSUB PROCESS_SLSMAN; GOTO DISP_REC
50060 CHK_ERR_NXT:IF ERR<>0 THEN EXIT ERR
50070 _D$=_MSG_NEXT$; GOSUB BUSY_CHK
50080 SLSMAN$=KEN(Z(1),KEY=SLSMAN$,END=END_OF_FILE); READ (Z(1),KEY=SLSMAN$,ERR=CHK_ERR_NXT); GOSUB PROCESS_SLSMAN; GOTO DISP_REC
50090 END_OF_FILE: MSGBOX "End of File","Eof","!"; EXIT 
50100 ! 2200 - Prior record
50110 PRIOR_REC:
50130 SLSMAN$=KEP(Z(1),END=START_OF_FILE); READ (Z(1),KEY=SLSMAN$,ERR=*NEXT); GOSUB PROCESS_SLSMAN; GOTO DISP_REC
50140 CHK_ERR_PRE:IF ERR<>0 THEN EXIT ERR
50150 _D$=_MSG_PRECEDING$; GOSUB BUSY_CHK
50160 SLSMAN$=KEP(Z(1),KEY=SLSMAN$,END=START_OF_FILE); READ (Z(1),KEY=SLSMAN$,ERR=CHK_ERR_PRE); GOSUB PROCESS_SLSMAN; GOTO DISP_REC
50170 START_OF_FILE: MSGBOX "Start of File","Info","!,tim=3"; EXIT 
50180 ! 2400 - First record
50190 FIRST_REC:
50210 SLSMAN$=KEF(Z(1),ERR=NO_FIRST)
50220 READ (Z(1),KEY=SLSMAN$,ERR=CHK_ERR_NXT); GOSUB PROCESS_SLSMAN; GOTO DISP_REC
50230 NO_FIRST: MSGBOX _MSG_REC_NO_1ST$,_FYI$,"!"; EXIT 
50240 ! 2600 - Last record
50250 LAST_REC:
50270 SLSMAN$=KEL(Z(1),ERR=NO_LAST)
50280 READ (Z(1),KEY=SLSMAN$,ERR=CHK_ERR_PRE); GOSUB PROCESS_SLSMAN; GOTO DISP_REC
50290 NO_LAST: MSGBOX _MSG_REC_NO_LST$,_FYI$,"!"; EXIT 
50300 BUSY_CHK:
50310 MSGBOX _MSG_REC_LOCKED$+SEP+SEP+_MSG_REC_VIEW1$+_D$+_MSG_REC_VIEW2$,_MSG_REC_ACCESS$,"YESNO,!",_D$
50320 IF _D$="NO" THEN EXIT 
50330 RETURN 
50340 DISP_REC:RETURN 
56000 REM "047384-Show where the activity is when a delete is not allowed.    
56002 REM "263584-Grid size issues Upgrade from Pvx 7.1 to PxPlus 2017        
