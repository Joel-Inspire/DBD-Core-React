0010 ! ARGMAA - Customer Setup & Inquiry
0035 REM "5.7 - 02/06/18 - 14.9675 - crg - SSP# 263584
0037 REM "263584-Providex version upgrade from Pvx 7.1 to PxPlus 2017        
0040 REM "Copyright 2018 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 IF TCB(13)=1 THEN BEGIN 
0055 ! _CUSTOMER$=%CUSTOMER$
0060 PROCESS "ARGMAA","../ARG/AR.EN"
0070 ! IF TCB(13)=1 THEN RUN "ZMENU"
0075 END 
0100 INIT:! ^100,5 - Initialization routine
0200 ! ^100,5 - Constants & functions
0205 DIM Q$[1:23],P$[1:23],SPA$[1:50] ! SSP 191604, WO237196, increase DIM from 22 to 23
0208 %FTWCUST$="" ! SSP232039 jdf
0300 ! ^100,5 - Iolists and templates
0305 GLOBAL_IOL:IOLIST %CUST_NAME$,%CUSTOMER$,%BILL_TO_CUST$,%LIST$,%CONTACT$
0500 ! 500,5 - Open files
0503 GOSUB SETUP_MESSAGES
0505 _FILE_NAME$="AR1"+%C$; _AR1=HFN; OPEN (_AR1,ERR=OPEN_ERR,IOL=*)_FILE_NAME$ ! Customer master
0510 _FILE_NAME$="ARH"+%C$; _ARH=HFN; OPEN (_ARH,ERR=OPEN_ERR,IOL=*)_FILE_NAME$ ! Division master
0515 _FILE_NAME$="AS5"+%C$; _AS5=HFN; OPEN (_AS5,ERR=OPEN_ERR,IOL=*)_FILE_NAME$ ! Customer messages
0520 _FILE_NAME$="ZZPARM"; ZZPARM=HFN; OPEN (ZZPARM,ERR=OPEN_ERR)_FILE_NAME$
0525 ! 
0565 GOTO CONTINUE
0570 OPEN_ERR: MSGBOX _MSG_FILOPNERR1$+QUO+_FILE_NAME$+QUO+SEP+_MSG_DIRECTORY$+LWD+SEP+_MSG_PREFIX$+PFX,MSG(ERR),"!"
0575 CMD_STR$="E"
0580 EXIT 
0585 ! 
0590 CONTINUE:
0595 ! 
0600 ! ^100,5 - Get parameters
0602 IF %NEW_CUST THEN %NEW_CUST=0
0605 IF X1>1 THEN CMD_STR$="E"; EXIT 
0610 READ (ZZPARM,KEY=%C$+"A/R",DOM=*NEXT)P0$; GOTO 0620
0615 CMD_STR$="E"; EXIT 
0620 READ (ZZPARM,KEY=%C$+"F/M",DOM=*NEXT)P1$
0625 SEC$=""; READ (ZZPARM,KEY="osec"+%X3_OP_ID$,DOM=*NEXT)SEC$
0630 TM_PARMS$=""; CALL "RTGPRM",ERR=*NEXT,TM_PARMS$
0635 %CUST_CAT_LEN=NUM(P0$(20,1)),%CUST_CAT_MASK$=DIM(%CUST_CAT_LEN,"X")
0637 %ARGMAA_FLG=1 ! SSP238505 jdf
0638 IF FN%HAS_MODULE("DE") THEN READ (ZZPARM,KEY=%C$+"ARGDBE",DOM=*NEXT); ADD_DBE_OPTION=1 ! WO237196, if has DB/e and parameters for Add DBE Customer setup then we will add an option to the grid to call ARGDBE.
0640 RETURN 
0645 ! 
0700 POST_DISPLAY:! ^100,5 - Do stuff after the panel is created
0703 IF P0$(15,1)<>"Y" THEN CALL "*wingrp;Hide",BILLTO.GRP$
0705 IF %ASSISTANT THEN PRINT 'CAPTION'("Customer Setup/Inquiry (Assistant)")
0707 %NEW_CUST=0
0710 IF %CUSTOMER$<>"" THEN {
0715 CUSTOMER$=%CUSTOMER$
0716 DISABLE CONTROL CUSTOMER.CTL
0718 CALL "*WINGRP;HIDE",CUST_1.GRP$; CALL "*WINGRP;HIDE",NAV_1.GRP$; CALL "*WINGRP;HIDE",CLEAR_1.GRP$
0720 GOSUB PROCESS_CUSTOMER
0725  }
0730 GOSUB BUILD_OPTIONS
0735 RETURN 
0740 ! 
1000 PROCESS_CUSTOMER:! ^1000,5 - Get customer code and associated fields
1003 IF STP(CUSTOMER$,3)="" THEN RETURN 
1004 IF STP(%FTWCUST$)<>"" THEN CUSTOMER$=%FTWCUST$; %FTWCUST$="" ! SSP232039 jdf
1005 CALL "ZZWLKU;Parse_cust",CUSTOMER$,CUST_DIV$,CUST_CODE$
1006 IF %X3$(333,2)<>"  " AND %X3$(333,2)<>CUST_DIV$ THEN MSGBOX "You are only authorized for division "+%X3$(333,2),MSG("WARNING"),"stop,tim=10"; CUSTOMER$=%X3$(333,3),NEXT_ID=CUSTOMER.CTL; EXIT ! SSP 196358
1010 READ (_AR1,KEY=CUST_DIV$+CUST_CODE$,DOM=NEW_CUSTOMER,ERR=CHECK_ERR)
1015 BILL_TO_CUST$=BILL_TO_CUST_DIV$+BILL_TO_CUST_CODE$,CUSTOMER$=CUST_DIV$+CUST_CODE$
1016 GOSUB VALIDATE_REP; IF NOT(VAL_REP) THEN MSGBOX MSG("SEC_CUST"); NEXT_ID=CUSTOMER.CTL; EXIT 
1017 CALL "*wingrp;enable",GRID.GRP$
1020 GOSUB CHECK_AR_MESSAGE
1025 NEXT_ID=OPTION.CTL
1030 ! LIST_BOX WRITE OPTION.CTL,1,ERR=*NEXT
1035 REFRESH_FLG=1,%CUSTOMER$=CUSTOMER$,%BILL_TO_CUST$=BILL_TO_CUST$,%LIST$=CONT_LIST_CODE$,%CONTACT$=CONT_CONT_CODE$
1040 RETURN 
1045 ! 
1100 NEW_CUSTOMER:! ^100,5 - Go directly to customer setup panel
1110 MSGBOX MSG("ADD_RECORD"),MSG("VERIFY"),"?,YESNO",ANS$
1120 IF ANS$="NO" THEN CUSTOMER$=""; NEXT_ID=CUSTOMER.CTL; REFRESH_FLG=1; EXIT 
1130 IF MID(SEC$,44,1)="Y" THEN MSGBOX MSG("ACCESS_DEN")+" "+MSG("ADD_CUST"),MSG("ATTN"),"!"; CUSTOMER$=""; NEXT_ID=CUSTOMER.CTL; REFRESH_FLG=1; RETURN 
1145 READ (_ARH,KEY=CUST_DIV$,DOM=MISSING_DIV)
1150 CUSTOMER$=CUST_DIV$+CUST_CODE$; %CUSTOMER$=CUSTOMER$
1152 %TM_CONTACT$=P0$(115,1),%LIST$="",%CONTACT$="" ! SSP238505 jdf
1155 %NEW_CUST=1,%GRID=0 ! SSP#232819
1157 CALL "*wingrp;enable",GRID.GRP$
1160 OPT_NUM=1; GOTO 2365
1190 NEW_CUSTOMER_END:RETURN 
1195 ! 
1200 MISSING_DIV:! ^100,5 - Can't process this new customer code
1205 MSGBOX MSG("DIV_NOF",CUST_DIV$),MSG("ATTN"),"!"
1210 NEXT_ID=CUSTOMER.CTL,REFRESH_FLG=1,CUSTOMER$="",CUST_DIV$="",CUST_CODE$=""
1215 EXIT 
1220 ! 
2000 BUILD_OPTIONS:! ^1000,5 - Get data to populate the list boxes
2005 ! LIST_BOX LOAD OPTION.CTL,""
2010 MAX_OPT=1
2015 W8$="Y",Q$[MAX_OPT]="Customer Constant Data",P$[MAX_OPT]="ARGMAB",MAX_OPT+=1; GOSUB 2200
2020 W8$="Y",Q$[MAX_OPT]="Credit Analysis",P$[MAX_OPT]="ARGMAC",MAX_OPT+=1; GOSUB 2200
2024 IF NOT(%ASSISTANT) AND STP(P1$,2)<>"" THEN W8$="Y",Q$[MAX_OPT]="Customer Default Data",P$[MAX_OPT]="FMGSHA",MAX_OPT+=1; GOSUB 2200
2025 W8$="Y",Q$[MAX_OPT]="Open Invoice Inquiry",P$[MAX_OPT]="ARGMAD",MAX_OPT+=1; GOSUB 2200
2030 W8$="Y",Q$[MAX_OPT]="Invoice History Inquiry",P$[MAX_OPT]="ARGMAH",MAX_OPT+=1; GOSUB 2200
2034 IF STP(P1$,2)<>"" THEN W8$="Y",Q$[MAX_OPT]="Open Order Inquiry",P$[MAX_OPT]="FMGODI",MAX_OPT+=1; GOSUB 2200
2040 IF STP(P1$,2)<>"" THEN W8$="N",Q$[MAX_OPT]="Open Order Inquiry by Item",P$[MAX_OPT]="FMGOOI",MAX_OPT+=1; GOSUB 2200
2044 IF STP(P1$,2)<>"" THEN W8$="N",Q$[MAX_OPT]="Order History by Order Number",P$[MAX_OPT]="ARGMAO",MAX_OPT+=1; GOSUB 2200
2049 IF STP(P1$,2)<>"" THEN W8$="N",Q$[MAX_OPT]="Order History by Item",P$[MAX_OPT]="FMGIAH",MAX_OPT+=1; GOSUB 2200
2064 IF NOT(%ASSISTANT) AND STP(P1$,2)<>"" THEN W8$="Y",Q$[MAX_OPT]="Invoiced Order History by Date",P$[MAX_OPT]="FMGIAL",MAX_OPT+=1; GOSUB 2200
2065 ! IF %ASSISTANT THEN GOTO 2162
2075 W8$="Y",Q$[MAX_OPT]="Statistical Data Inquiry",P$[MAX_OPT]="ZZGMDA",MAX_OPT+=1; GOSUB 2200
2079 IF STP(P1$,2)<>"" THEN W8$="Y",Q$[MAX_OPT]="Custom Item Setup/Inquiry",P$[MAX_OPT]="FMGIAZ",MAX_OPT+=1; GOSUB 2200
2084 IF NOT(%ASSISTANT) AND STP(P1$,2)<>"" THEN W8$="N",Q$[MAX_OPT]=MSG("SHIP_T_LC"),P$[MAX_OPT]="FMGLAA",MAX_OPT+=1; GOSUB 2200 ! SSP#227949
2085 IF NOT(%ASSISTANT) AND STP(P1$,2)<>"" THEN W8$="N",Q$[MAX_OPT]="Customer Message Setup",P$[MAX_OPT]="ARGMAE",MAX_OPT+=1; GOSUB 2200
2089 IF NOT(%ASSISTANT) AND STP(P1$,2)<>"" THEN W8$="N",Q$[MAX_OPT]="Item Management Fiscal Year",P$[MAX_OPT]="FMGFYE",MAX_OPT+=1; GOSUB 2200 ! SSP#230760
2095 IF P0$(114,1)="Y" THEN {
2100 IF NOT(%ASSISTANT) AND STP(P1$,2)<>"" THEN W8$="N",Q$[MAX_OPT]="Comment Printing Setup",P$[MAX_OPT]="ARGMAU",MAX_OPT+=1; GOSUB 2200
2105  }
2110 IF P1$(22,1)="Y" THEN {
2115 ! W8$="N",Q$[MAX_OPT]="Production Work Order Setup",P$[MAX_OPT]="FWGMAA",MAX_OPT+=1; GOSUB 2200
2120 ! W8$="N",Q$[MAX_OPT]="Production Order Tracking",P$[MAX_OPT]="FWGMBA",MAX_OPT+=1; GOSUB 2200
2125  }
2130 ! W8$="N",Q$[MAX_OPT]="Requisition/Release Entry",P$[MAX_OPT]="FSGDAX",MAX_OPT+=1; GOSUB 2200
2135 IF NOT(%ASSISTANT) THEN W8$="N",Q$[MAX_OPT]="Order Entry",P$[MAX_OPT]="FMGODM",MAX_OPT+=1; GOSUB 2200
2140 IF NOT(%ASSISTANT) THEN W8$="N",Q$[MAX_OPT]="Contact Management Setup",P$[MAX_OPT]="ZPGMEA",MAX_OPT+=1; GOSUB 2200
2145 IF MID(SEC$,61,1)="Y" THEN {
2150 IF NOT(%ASSISTANT) THEN W8$="Y",Q$[MAX_OPT]="Credit Card Information Setup",P$[MAX_OPT]="ARGMAV",MAX_OPT+=1; GOSUB 2200
2155  }
2158 IF NOT(%ASSISTANT) THEN W8$="Y",Q$[MAX_OPT]="Special Pricing",P$[MAX_OPT]="SPGSP0",MAX_OPT+=1; GOSUB 2200 ! SSP 191604
2159 IF FN%HAS_MODULE("EB") AND NOT(%ASSISTANT) THEN W8$="Y",Q$[MAX_OPT]="Budgets",P$[MAX_OPT]="FMGFTW",MAX_OPT+=1; GOSUB 2200 ! WO 215368
2160 IF NOT(%ASSISTANT) AND ADD_DBE_OPTION THEN W8$="Y",Q$[MAX_OPT]="Setup DBE Records",P$[MAX_OPT]="ARGDBE",MAX_OPT+=1; GOSUB 2200 ! WO237196, add option to lauch utility to setup DBE buyer and DR filter records, and replicate cust recs.
2161 IF NOT(%ASSISTANT) THEN W8$="N",Q$[MAX_OPT]="Change Customer Status/Delete",P$[MAX_OPT]="DELETE",MAX_OPT+=1; GOSUB 2200 ! WO237196, renumbered to 2161 from 2160!SSP#240337
2162 GOSUB CREATE_GRID
2165 REFRESH_FLG=1
2170 RETURN 
2200 ! ^100,5 - Add option to list box
2201 RETURN 
2205 W8=0
2210 IF TM_PARMS$>"" AND MID(TM_PARMS$,7,1)<>"H" AND W8$<>"Y" THEN W8=1 ! if remote T/M & not host, then if feature is not marked "Y",  set w8 to 1 to suppress option
2215 ! IF NOT(W8) THEN LIST_BOX LOAD OPTION.CTL,0,Q$[X-1] ELSE X-=1
2220 RETURN 
2225 ! 
2300 PROCESS_OPTION:! ^100,5 - Option selected see if it can be run
2305 IF CVS(CUSTOMER$,3)="" THEN {
2310 MSGBOX MSG("CUST_MISS"),MSG("ATTN"),"!"
2315 NEXT_ID=CUSTOMER.CTL,REFRESH_FLG=1
2320 ! LIST_BOX WRITE OPTION.CTL,0
2325 EXIT 
2330  }
2335 OPT_NUM=OPTION.CTL'SELECTITEM
2340 IF NOT(OPT_NUM) THEN {
2345 NEXT_ID=OPTION.CTL,REFRESH_FLG=1
2350 EXIT 
2355  }
2360 IF P$[OPT_NUM]="DELETE" THEN GOTO 6500
2365 _CUSTOMER$=%CUSTOMER$; CALL P$[OPT_NUM],ERR=CALL_ERROR; %CUSTOMER$=_CUSTOMER$
2370 CONTINUE_AFTER_OPTION:
2375 NEXT_ID=CLEAR.CTL,REFRESH_FLG=1
2380 ! LIST_BOX WRITE OPTION.CTL,0
2385 RETURN 
2390 ! 
2400 CALL_ERROR:! ^100,5 - Can't run the option
2405 MSGBOX MSG("UNABLE_PER")+" "+Q$[OPT_NUM]+"."+SEP+MSG("UNDER_CON1"),MSG("ATTN"),"!"
2410 NEXT_ID=OPTION.CTL,REFRESH_FLG=1
2415 RETURN 
2420 ! 
2500 PROCESS_CLEAR:! ^100,5 - Clear Customer Code
2505 CUSTOMER$="",CUST_NAME$="",BILL_TO_CUST$="",%CUST_NAME1$=""
2510 CUST_DIV$="",CUST_CODE$=""
2515 NEXT_ID=CUSTOMER.CTL,REFRESH_FLG=1
2520 ! LIST_BOX WRITE OPTION.CTL,0
2523 CALL "*WINGRP;DISABLE",GRID.GRP$
2525 RETURN 
2530 ! 
4400 CHECK_ERR:! ^100,5 - Error on file read
4405 IF ERR<>0 THEN EXIT ERR
4410 MSGBOX MSG("REC_BUSY"),MSG("CONFIRM"),"?,YESNO",ANSWER$
4415 IF ANSWER$="NO" THEN EXIT ELSE RETRY 
4420 ! 
6490 DELETE_OPT:
6500 REM "DELETE
6502 IF CUST_STAT$=DIM(1) THEN CUST_STAT$="A"
6505 IF MID(SEC$,44,1)="Y" OR MID(SEC$,70,1)="Y" THEN MSGBOX MSG("ACCESS_DEN")+" "+MSG("DEL_CUST"),MSG("ATTN"),"!"; GOTO CONTINUE_AFTER_OPTION
6510 REM "IF CUST_STAT$="I" THEN A8$="Reactivate" ELSE A8$="Delete"
6520 REM "MSGBOX MSG("VERIFY_WSH",A8$),MSG("CONFIRM"),"?,YESNO",ANSWER$
6521 REM "IF ANSWER$<>"YES" THEN GOTO CONTINUE_AFTER_OPTION
6522 %CUST_DEL_STAT$=CUST_STAT$
6523 PROCESS "ARGDEL","../ARG/AR.EN",CUST_STAT$
6525 CUST_STAT$=%CUST_DEL_STAT$; WRITE (_AR1,KEY=CUST_DIV$+CUST_CODE$)
6530 Q2$="",Q0$="A/R",Q1$=CUST_DIV$+CUST_CODE$,Q3$="M"
6540 IF CUST_STAT$="I" THEN CALL "ARGUAB",ERR=*NEXT,X3$,X4$,Q0$,Q1$,Q2$,Q3$
6545 Q0$=""; %CUST_DEL_STAT$=""
6550 IF Q2$="Y" AND POS("I"=CUST_STAT$)<>0 THEN A8$=MSG("REC_DEL"); GOTO 6580
6560 IF Q2$<>"Y" AND POS("I"=CUST_STAT$)<>0 THEN A8$="Activity detected. "+Q2$+" - Record set to inactive for later removal."
6570 REM "CUST_STAT$="I"; WRITE (_AR1,KEY=CUST_DIV$+CUST_CODE$)
6580 IF POS("I"=CUST_STAT$)<>0 THEN MSGBOX A8$,"Attention","!"
6590 IF Q2$="Y" THEN GOSUB PROCESS_CLEAR; EXIT ELSE GOTO CONTINUE_AFTER_OPTION ! SSP#240356
7700 CHECK_AR_MESSAGE:! 7700,5 - Check for customer entry in _AS5
7705 LOCAL X$
7710 READ (_AS5,KEY=CUST_DIV$+CUST_CODE$,DOM=*RETURN)
7715 IF STP(CUST_INQ_MESS_1$+CUST_INQ_MESS_2$+CUST_INQ_MESS_3$,2)="" THEN RETURN 
7720 X$=STP(CUST_INQ_MESS_1$,2)+" "+STP(CUST_INQ_MESS_2$,2)+" "+STP(CUST_INQ_MESS_3$)
7725 MSGBOX X$,MSG("IMP_MSG"),""
7730 RETURN 
7735 ! 
9200 SETUP_MESSAGES:
9210 _MSG_FILOPNERR1$=MSG("FILOPNERR1")
9220 _MSG_DIRECTORY$=MSG("DIRECTORY")
9230 _MSG_PREFIX$=MSG("PREFIX")
9290 SETUP_MESSAGES_END:RETURN 
9295 ! 
9700 WRAPUP:! 9700,5 - Close files and clean up
9705 READ DATA FROM "" TO IOL=GLOBAL_IOL
9710 IF _AR1 THEN CLOSE (_AR1)
9715 IF _ARH THEN CLOSE (_ARH)
9720 IF _AS5 THEN CLOSE (_AS5)
9725 IF ZZPARM THEN CLOSE (ZZPARM)
9730 %CUSTOMER$=""
9740 %TM_CONTACT$="",%ARGEAA$="",%CUST_DIV$="",%CUST_CODE$="",%ARGMAA_FLG=0 ! SSP238505 jdf
9790 EXIT 
9999 END 
10000 VALIDATE_REP:
10010 VAL_REP=0
10020 IF SPER_CODE$<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,%X3$,SPER_CODE$,OK; IF OK THEN VAL_REP=1; GOTO *RETURN
10030 IF SPER_CODE_2$<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,%X3$,SPER_CODE_2$,OK; IF OK THEN VAL_REP=1; GOTO *RETURN
10040 IF SPER_CODE_3$<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,%X3$,SPER_CODE_3$,OK; IF OK THEN VAL_REP=1; GOTO *RETURN
10050 IF SPER_CODE_4$<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,%X3$,SPER_CODE_4$,OK; IF OK THEN VAL_REP=1; GOTO *RETURN
10060 IF SPER_CODE_5$<>DIM(4) THEN CALL "UPDSYO;VAL_REP",ERR=*NEXT,%X3$,SPER_CODE_5$,OK; IF OK THEN VAL_REP=1; GOTO *RETURN
10080 IF SPER_CODE$=DIM(4) AND SPER_CODE_2$=DIM(4) AND SPER_CODE_3$=DIM(4) AND SPER_CODE_4$=DIM(4) AND SPER_CODE_5$=DIM(4) THEN VAL_REP=1
10090 RETURN 
10098 ! 
10099 ! 
40000 ! !40000,10
40010 SEL_GRID:
40012 REFRESH_FLG=1
40020 GRID SELECT READ X,COL,ROW
40030 GRID FIND X,COL,ROW,VAL$
40035 IF STP(VAL$)="" THEN RETURN 
40036 IF AROW+ACOL<>0 THEN X'COLUMN=ACOL,X'ROW=AROW,X'TEXTCOLOR$="Black",X'BACKCOLOR$="Gray",X'BITMAP$=""
40038 X'COLUMN=COL,X'ROW=ROW,X'TEXTCOLOR$="Light Blue",X'BITMAP$="!Arrow_Right" ! ,X'BACKCOLOR$="White"
40040 AROW=ROW,ACOL=COL
40045 OPT=INT(POS(STP(VAL$)=ALL_MESS$)/40)+1; %GRID=0; GOSUB PROCESS_OPT; GRID GOTO X,COL,ROW; %GRID=1; RETURN 
40050 CREATE_GRID:
40060 GOSUB CREATE_OPT
40070 X=X.CTL,BOPT$=OPT$,ROW=1,COLUMN=0; GRID DELETE X,-1,-1
40080 HIDE CONTROL X
40082 HI=OPT_LN/3; IF FPT(HI)<>0 THEN HI=INT(HI)+1
40085 ! GRID REMOVE X; GRID X,@(2,9.6,75,HI)
40090 XX=POS(SEP=OPT$); IF XX=0 THEN GOTO END_LOAD
40100 COLUMN=COLUMN+1; IF COLUMN>3 THEN COLUMN=1,ROW=ROW+1; GRID ADD X,0,ROW; GRID LOCK X,0,ROW
40110 TX$=OPT$(1,XX-1),OPT$=OPT$(XX+1),X'ROW=ROW,X'COLUMN=COLUMN,X'ALIGN$="L",X'COLUMNWIDTH=25,X'VALUE$=TX$,X'BACKCOLOR$="Gray",X'CELLTYPE$="Button",X'LOCK=1,X'ROWHEIGHT=1.6,X'BITMAP$=""
40120 ! GRID LOAD X,ROW,COLUMN,TX$
40125 IF TCB(29)>=7100000 THEN X'CELLTIP$=X'VALUE$ ! SSP 212883
40130 GOTO 40090
40140 END_LOAD: HIG=HI*X'HEIGHT/5,X'HEIGHT=HIG; SHOW CONTROL X; RETURN 
40150 CREATE_OPT:
40160 SE$="123456789ABCDEFG"
40170 FOR A=1 TO MAX_OPT-1
40182 MESS$=STP(Q$[A]),MESS$=PAD(MESS$,40," "),ALL_MESS$=ALL_MESS$+MESS$
40183 IF A=2 THEN MESS$=MESS$+"       "
40190 OPT$=OPT$+STP(MESS$)+SEP,OPT_LN=OPT_LN+1
40191 NXT_OPT:
40195 NEXT A
40200 RETURN 
42000 PROCESS_OPT:IF VAL$="Order Entry" THEN GOSUB CHECK_ORDER_ENTRY; GOSUB CHECK_SECURITY; IF NO_ENTRY THEN PRINT 'RB',; MSGBOX MSG("ACCESS_DEN"),MSG("WARNING"),"!,TIM=3"; RETURN ! SSP 212883
42001 %CUSTOMER$=CUSTOMER$,%ITEM_CODE$="" ! SSP 205228! SSP 212883
42002 IF VAL$="Custom Item Setup/Inquiry" THEN GOSUB CHECK_CUSTOM_ITEM; GOSUB CHECK_SECURITY; IF NO_ENTRY THEN PRINT 'RB',; MSGBOX MSG("ACCESS_DEN"),MSG("WARNING"),"!,TIM=3"; RETURN ! SSP 212883
42005 IF P$[OPT]="DELETE" THEN GOSUB 6500; RETURN 
42008 IF VAL$="Special Pricing" THEN CALL P$[OPT],X3$,X4$,%CUSTOMER$,"","","C"; GOTO 42012 ! SSP 210376
42010 CALL P$[OPT]
42012 READ (_AR1,KEY=CUST_DIV$+CUST_CODE$,DOM=NEW_CUSTOMER,ERR=CHECK_ERR) ! SSP 213460
42013 BILL_TO_CUST$=BILL_TO_CUST_DIV$+BILL_TO_CUST_CODE$,REFRESH_FLG=1 ! SSP 213460
42020 RETURN 
43000 CHECK_ORDER_ENTRY:! SSP 212883
43010 _SEC=FN%FFN("ZZWSEC"); IF _SEC<0 THEN RETURN 
43030 ORDER_ENTRY=0; READ (_SEC,KEY="DB Distributor"+SEP+"Order Processing"+SEP+"Order Processing"+SEP+"Order Entry",DOM=*NEXT)*,*,*,*,*,PASSWORD$,LVL$,ROLE$; ORDER_ENTRY=1
43040 RETURN 
43050 CHECK_SECURITY: NO_ENTRY=0 ! SSP 212883
43060 IF STP(PASSWORD$)<>"" THEN PROCESS "PASSWORD","../ZZG/ZZ.EN",PASS$; IF PASS$<>PASSWORD$ THEN NO_ENTRY=1; RETURN 
43070 IF %X3_OP_PRTY$<LVL$ THEN NO_ENTRY=1; RETURN 
43080 IF STP(ROLE$)<>"" AND POS(%ROLE$=ROLE$)=0 THEN NO_ENTRY=1
43090 RETURN 
43100 CHECK_CUSTOM_ITEM:! SSP 212883
43110 _SEC=FN%FFN("ZZWSEC"); IF _SEC<0 THEN RETURN 
43120 ORDER_ENTRY=0; READ (_SEC,KEY="DB Distributor"+SEP+"Print Management"+SEP+"Setup"+SEP+"Custom Items",DOM=*NEXT)*,*,*,*,*,PASSWORD$,LVL$,ROLE$; ORDER_ENTRY=1
43130 RETURN 
43500 RETURN 
50000 ! 50000 - Browsing mode
50010 ! - Next
50020 NEXT_REC:
50040 CUSTOMER$=KEY(_AR1,END=END_OF_FILE)
50050 READ (_AR1,KEY=CUSTOMER$,ERR=*NEXT); GOSUB PROCESS_CUSTOMER; GOTO DISP_REC
50060 CHK_ERR_NXT:IF ERR<>0 THEN EXIT ERR
50070 _D$=_MSG_NEXT$; GOSUB BUSY_CHK
50080 CUSTOMER$=KEN(_AR1,KEY=CUSTOMER$,END=END_OF_FILE); READ (_AR1,KEY=CUSTOMER$,ERR=CHK_ERR_NXT); GOSUB PROCESS_CUSTOMER; GOTO DISP_REC
50090 END_OF_FILE: MSGBOX "End of File","Eof","!"; EXIT 
50100 ! 2200 - Prior record
50110 PRIOR_REC:
50130 CUSTOMER$=KEP(_AR1,END=START_OF_FILE); READ (_AR1,KEY=CUSTOMER$,ERR=*NEXT); GOSUB PROCESS_CUSTOMER; GOTO DISP_REC
50140 CHK_ERR_PRE:IF ERR<>0 THEN EXIT ERR
50150 _D$=_MSG_PRECEDING$; GOSUB BUSY_CHK
50160 CUSTOMER$=KEP(_AR1,KEY=CUSTOMER$,END=START_OF_FILE); READ (_AR1,KEY=CUSTOMER$,ERR=CHK_ERR_PRE); GOSUB PROCESS_CUSTOMER; GOTO DISP_REC
50170 START_OF_FILE: MSGBOX "Start of File","Info","!,tim=3"; EXIT 
50180 ! 2400 - First record
50190 FIRST_REC:
50210 CUSTOMER$=KEF(_AR1,ERR=NO_FIRST)
50220 READ (_AR1,KEY=CUSTOMER$,ERR=CHK_ERR_NXT); GOSUB PROCESS_CUSTOMER; GOTO DISP_REC
50230 NO_FIRST: MSGBOX _MSG_REC_NO_1ST$,_FYI$,"!"; EXIT 
50240 ! 2600 - Last record
50250 LAST_REC:
50270 CUSTOMER$=KEL(_AR1,ERR=NO_LAST)
50280 READ (_AR1,KEY=CUSTOMER$,ERR=CHK_ERR_PRE); GOSUB PROCESS_CUSTOMER; GOTO DISP_REC
50290 NO_LAST: MSGBOX _MSG_REC_NO_LST$,_FYI$,"!"; EXIT 
50300 BUSY_CHK:
50310 MSGBOX _MSG_REC_LOCKED$+SEP+SEP+_MSG_REC_VIEW1$+_D$+_MSG_REC_VIEW2$,_MSG_REC_ACCESS$,"YESNO,!",_D$
50320 IF _D$="NO" THEN EXIT 
50330 RETURN 
50340 DISP_REC:RETURN 
56000 REM "191822-Work order to allow sales reps to only see their own        
56002 REM "193458-Customers not printing ONACCTS on the open invoice          
56003 ! "196358-Restricted users by division in security by operator. No one
56004 REM "210376/191604 -OLD 3799 Special Pricing Plan needs to be tested in both    
56006 REM "215368-Monthly Budget feature by customer location
56007 REM "232819-Setting up a new customer and cannot enter pass the         
56008 REM "232039-The budget maintenance screen within db/d has some          
56009 REM "238505-GUI: set up a new customer we would get a message asking if 
56010 REM "047384-Show where the activity is when a delete is not allowed.    
56012 REM "237196-Create utility to auto setup DB/e EC buyer, data rep filter
56013 REM "240337-Wording of panel ARGDEL needs work                          
56014 REM "240356-After deleting customer, and customer # field is clear.     
56015 REM "263584-Grid size issues Upgrade from Pvx 7.1 to PxPlus 2017        
