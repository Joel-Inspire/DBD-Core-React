0010 REM "A/R Enhanced Freight Markup <ARGFRT>
0020 SETESC 9300; SETERR 9000
0035 ! "6.0 - 04/21/03 - 9.510277 - dmm
0040 ! "Copyright 2003 Computer Software Inc.; Norcross, Georgia
0041 ! "        Licensed Software - All Rights Reserved.
0085 REM "If DONT_PRINT$ = 'DELETE' then we are deleting the invoice, so go to a routine to clear the current marked up frt from order history, if needed
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,AR_PARM$,DONT_PRINT$,INV$,INV{ALL}
0100 SETERR 9000
0110 X0$="ARGFRT",X1$="A/R Enhanced Freight Markup"
0200 REM "
0245 IF %X3_PREC$>"2" THEN PRECISION NUM(%X3_PREC$,ERR=0246)
0290 IOLIST Y[0],Y[1],Y[2],Y[3],Y[4]
0300 REM "IOLISTS
0321 IOLIST R$(1),R[0],R[1],R[2],R[3],R[4],R[5],R[6],R[7],R[8],R[9],R[10],R[11]
0440 IOLIST H$,H[0],H[1],H[2],H[3],H[4],H[5],H[6],H[7],H[8],H[9],H[10],H[11],H[12],H[13],H[14],H[15]
0450 IOLIST FS6$,FS6[0],FS6[1],FS6[2],FS6[3],FS6[4],FS6[5],FS6[6],FS6[7]
0480 IOLIST LINK,LINE$,LINE[0],LINE[1],LINE[2],LINE[3],LINE[4],LINE[5],LINE[6],LINE[7],LINE[8],LINE[9],LINE[10]
0490 IOLIST OLD_LINK,OLD_LINE$,OLD_LINE[0],OLD_LINE[1],OLD_LINE[2],OLD_LINE[3],OLD_LINE[4],OLD_LINE[5],OLD_LINE[6],OLD_LINE[7],OLD_LINE[8],OLD_LINE[9],OLD_LINE[10]; REM SSP# 131719
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O FMP... 13O ZZPARM 14O AR4...  15O FS6...  16O FS7... 17O FS8... 18O ART... 19O ASC... 20O AR5... 25OSFS1...  "
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0525 _FMP=Z[2],_ZZPARM=Z[13],_AR4=Z[14],_FS6=Z[15],_FS7=Z[16],_FS8=Z[17],_ART=Z[18],_ASC=Z[19],_AR5=Z[20],_FS1=Z[25]
0585 RT_PARM$=""; CALL "RTGPRM",ERR=0586,RT_PARM$
0600 REM "
0605 DIM Y[4]
0650 READ (_ZZPARM,KEY=%C$+"F/M")OP_PARMS$; REM " SSP# 147488
0700 IF DONT_PRINT$="DELETE" THEN GOSUB 3000; GOTO 9900
2000 REM "Freight Markup
2002 REM "OLD_FRT IS BUCKET 17, NEW_FRT IS BUCKET 2
2004 OLD_FRT=INV[2],NEW_FRT=INV[2]
2005 DIM R[11],R$(241); Z=0,Q=0; FIND (_FMP,KEY="D"+INV$(15,10),DOM=2006)R$
2008 GOSUB 2100
2010 DIM R$(46); FIND (_FMP,KEY="M"+MARKUP_CODE$,DOM=2095)IOL=0321
2040 IF R[1]=0 THEN GOTO 2060
2050 FOR X=1 TO 5 STEP 2; IF ABS(INV[1])<R[X] THEN Z=SGN(INV[1])*R[X+1]; EXITTO 2051 ELSE NEXT X
2055 IF R$(35,1)="F" THEN Q=Q+INV[2]*Z*.01 ELSE Q=Q+INV[1]*Z*.01
2060 Q=Q+(Q+INV[2])*R[0]*.01; IF R[11]<>0 AND Q<R[11] AND INV[2]<>0 THEN Q=R[11]
2065 IF DONT_PRINT$<>"Y" AND R$(34,1)<>"Y" AND Q<>0 THEN X$=MSG("ADD_TO_FRT",STR(Q:"$$$.00")); MSGBOX X$,MSG("CONFIRM"),"?,YESNO",ANSWER$; IF ANSWER$<>"YES" THEN GOTO 2095
2070 IF R$(46,1)="Y" THEN OLD_FRT=INV[2]+Q,INV[2]=0,NEW_FRT=0 ELSE INV[2]=INV[2]+Q,NEW_FRT=INV[2]; REM "If cost only line then old_frt is marked up value, new_frt is 0, and inv freight is 0,else adjust invoice freight by markup, set new freight to freight marked up amount, old_frt remains orginal frt amount
2072 INV[2]=NEW_FRT
2078 REM "IF DONT_PRINT$="RETAIN" THEN INV[2]=0,DONT_PRINT$=""; GOTO 9900; REM "SSP# 131719
2079 REM "IF INV[17]<>0 AND DONT_PRINT$="RETAIN" THEN LET INV[2]=0,DONT_PRINT$="";GOTO 9900;REM " SSP# 131719
2080 IF OLD_FRT=0 AND NEW_FRT=0 AND INV[2]=0 THEN GOTO 9900; REM "SSP# 131719
2082 IF R$(46,1)="N" AND INV[2]=0 THEN GOTO 9900; REM " Nothing in freight bucket - exit - SSP# 131719
2090 IF R$(36,1)="Y" THEN GOSUB 2200; REM "setup Order history if desired
2091 IF R$(37,1)="Y" THEN GOSUB 2300; REM "setup invoice line for frt
2095 GOTO 9900
2100 REM "Get freight markup code to use
2101 IF OP_PARMS$(299,1)<>"Y" THEN GOTO 2104
2102 IF INV$(92,8)<>DIM(8) THEN READ (_FS1,KEY=INV$(92,8),DOM=2104)ORDER$; REM " SSP# 147448
2103 IF INV$(92,8)<>DIM(8) THEN IF ORDER$(244,2)<>"  " THEN MARKUP_CODE$=ORDER$(244,2); IF MARKUP_CODE$<>"  " THEN GOTO 2121; REM " SSP# 147448
2104 MARKUP_CODE$="  "
2105 IF STP(R$(50,2),3," ")="" AND STP(R$(93,6),3," ")="" THEN MARKUP_CODE$="ZZ"; GOTO 2140; REM "Special case, if no codes set default to ZZ
2110 IF R$(50,2)<>"  " THEN MARKUP_CODE$=R$(50,2); GOTO 2140; REM "If old code is set then use it always
2114 REM "Set to R or W type, else use I type code
2120 IF INV$(343,1)="R" THEN MARKUP_CODE$=R$(95,2) ELSE IF INV$(343,1)="W" THEN MARKUP_CODE$=R$(97,2) ELSE MARKUP_CODE$=R$(93,2)
2145 RETURN 
2200 REM "Create order history line if requested
2205 IF STP(INV$(92,8),3," ")="" THEN GOTO 2295; REM "If no order, don't do anything
2210 DIM FS6$(113),FS6[7]
2215 FS6$(1,10)=INV$(15,10),FS6$(11,1)="J",FS6$(12,10)="FREIGHT",FS6$(22,8)=INV$(92,8),FS6$(30,1)="0"; REM "Cust #, special charge code (j), freight, order number, line 0 on the invoice
2217 FS6$(31,3)=R$(43,3),FS6$(34,6)=INV$(86,6); REM "prod code, order date set to inv date
2218 FS6$(40,8)=INV$(7,8),FS6$(48,6)=INV$(86,6); REM "Invoice #, invoice date
2219 FS6$(54,15)=INV$(100,15),FS6$(101,4)="EACH"; REM "cust p/o num, u/m
2221 FS6[0]=1,FS6[1]=NEW_FRT,FS6[2]=1; REM "qty, price, qty/u/m
2223 FS6[4]=OLD_FRT; REM " cost from old_frt
2230 WRITE (_FS6,KEY=FS6$(1,30),DOM=2231)IOL=0450; GOTO 2235
2231 EXTRACT (_FS6,KEY=FS6$(1,30),DOM=2230)IOL=0450; FS6[1]=FS6[1]+NEW_FRT,FS6[4]=FS6[4]+OLD_FRT; WRITE (_FS6,KEY=FS6$(1,30))IOL=0450; REM "if EXISTS then read old record and update price and cost only
2235 IF RT_PARM$<>"" THEN CUST$=FS6$(1,10); CALL "RTGWOC",ERR=2236,CUST$,"FS6...","U",FS6$(1,30)
2240 WRITE (_FS7,KEY=FS6$(31,3)+FS6$(11,1)+FS6$(12,10)+FS6$(1,10)+FS6$(22,8)+FS6$(30,1))
2245 WRITE (_FS8,KEY=FS6$(1,10)+FS6$(22,8)+FS6$(11,11)+FS6$(30,1))
2250 WRITE (_ASC,KEY=FS6$(1,10)+FS6$(54,15)+FS6$(11,20))
2295 RETURN 
2300 REM "Create invoice line if commissioned
2301 REM "Create printing line using commission from R$ or INV$ if freight markup is blank
2305 GOSUB 4000; REM " Check to see if frt line already exists - SSP# 131719
2310 DIM LINE$(144),LINE[10]
2312 FINDER$=""; FINDER=0; FIND (_FMP,KEY="D"+INV$(15,10),DOM=2315)FINDER$,FINDER; REM " SSP# 132459 - NEW TYPE A FINDERS FEE
2313 IF FINDER$(12,1)<>"A" THEN GOTO 2315 ELSE LINE$(152,1)="A"; REM "SSP# 132459 - NEW TYPE A FINDERS FEE
2315 IF STP(R$(38,5),3," ")="" THEN GOSUB 2400 ELSE COMM$=R$(38,5)
2317 DIM PROD$(46); FIND (_FMP,KEY="T"+R$(43,3),DOM=2318)PROD$
2320 LINE$(1,1)="S",LINE$(3,3)=R$(43,3); IF R$(46,1)="Y" THEN LINE$(6,1)="N" ELSE LINE$(6,1)="Y" END_IF ; REM "type = S, product code from frt markup record, print on invoice = "Y" UNLESS cost only line then it is NO
2321 DIM TAX$(180); FIND (_AR5,KEY=INV$(67,10),DOM=2322)TAX$
2322 LINE$(2,1)="Y",LINE$(7,40)="Freight",LINE$(54,1)="Y"; IF INV$(115,1)="Y" THEN LINE$(54,1)="N" END_IF ; IF PROD$(23,1)="N" THEN LINE$(54,1)="N" END_IF ; IF TAX$(36,1)="N" THEN LINE$(54,1)="N"; REM "costed freight, desc, taxable, If tax exempt Y line = taxable N  (ssp#131263)
2323 LINE$(47,3)="FRT"; REM "Set order line # to FRT to mark this as frt markup line
2324 LINE$(75,9)=PROD$(24,9),LINE$(84,5)=COMM$,LINE$(125,10)=INV$(67,10); REM "sales dist code, commission code, salestax code
2326 LINE$(50,4)="EACH",LINE[6]=1; REM "Set unit of measuer
2328 LINE[0]=1,LINE[1]=1; REM "orderd & shippd = 1
2330 IF R$(46,1)="Y" THEN LINE[2]=0,LINE[4]=0 ELSE LINE[2]=OLD_LINE[2]+NEW_FRT,LINE[4]=OLD_LINE[4]+NEW_FRT; REM "Sale price/extd amt from new freight - SSP# 131719
2332 LINE[3]=OLD_LINE[3]+OLD_FRT,LINE[5]=OLD_LINE[5]+OLD_FRT; REM "SSP146872
2334 IF MID(AR_PARM$,69,1)="Y" THEN LINE[8]=LINE[5]-LINE[4]; REM "If exclude freight gp from invoice gp then set gp adj to make adjust out gp on this line
2340 IF MID(FINDER$,12,1)="A" THEN LINE[8]=(LINE[5]-LINE[4])*FINDER*.01; REM " SSP# 132459
2375 REM "Adjust buckets to reflect costed freight line
2376 GOSUB 8600; REM "Calculate commission
2377 INV[2]=INV[2]-NEW_FRT; REM " SSP146872
2378 INV[11]=INV[11]+OLD_FRT,INV[18]=INV[18]-OLD_LINE[7]+LINE[7],INV[19]=INV[19]+LINE[8]
2379 IF R$(46,1)<>"Y" THEN INV[1]=INV[1]+NEW_FRT
2380 IF R$(46,1)<>"Y" AND DONT_PRINT$<>"RETAIN" THEN INV[17]=INV[17]+NEW_FRT; REM "ELSE INV[17]=NEW_FRT; REM " SSP# 131719
2381 REM "Write out line as first line of invoice. 
2382 IF FRT_EXIST THEN A9=FRT_EXIST; GOTO 2389; REM " SSP# 131719
2385 LINK=INV[20]
2387 GOSUB 5200; INV[20]=A9; GOTO 2390
2390 WRITE (_ART,IND=A9)IOL=0480
2395 RETURN 
2400 REM "Set COMM$ based on normal defaulting
2405 COMM$=INV$(128,5)
2410 C3$="*"+LINE$(1,1)+%C$+INV$(128,5); REM "'*'= no p/o code processing
2415 CALL "ARGCOM","DEFAULT",C3$,C{ALL},_ZZPARM,0,0
2420 COMM$=C3$
2445 RETURN 
3000 REM "Delete Frt amounts from FS6 record
3005 INDEX=INV[20]
3010 DIM LINE$(144),LINE[10]; IF INDEX<=0 THEN GOTO 3095 ELSE READ (_ART,IND=INDEX)IOL=0480; INDEX=LINK; REM "point to next line on list
3012 IF MID(LINE$,47,3)<>"FRT" THEN GOTO 3010; REM "If this is not a marked up frt line then ignore it
3015 DIM FS6$(113),FS6[7]; FS6$(1,10)=INV$(15,10),FS6$(11,1)="J",FS6$(12,10)="FREIGHT",FS6$(22,8)=INV$(92,8),FS6$(30,1)="0"; REM "Cust #, special charge code (j), freight, order number, line 0 on the invoice
3040 EXTRACT (_FS6,KEY=FS6$(1,30),DOM=3090)IOL=0450; FS6[1]=FS6[1]-LINE[2],FS6[4]=FS6[4]-LINE[3]; REM "if EXISTS then read record and update price and cost only
3045 IF FS6[1]>0 OR FS6[4]>0 THEN WRITE (_FS6,KEY=FS6$(1,30))IOL=0450; IF RT_PARM$<>"" THEN CUST$=FS6$(1,10); CALL "RTGWOC",ERR=3090,CUST$,"FS6...","U",FS6$(1,30) END_IF ; GOTO 3090; REM "If no more frt left then remove record, else write back updated record
3050 REM "If here we need to delete the record
3055 REMOVE (_FS6,KEY=FS6$(1,30),DOM=3056)
3056 IF RT_PARM$<>"" THEN CUST$=FS6$(1,10); CALL "RTGWOC",ERR=3057,CUST$,"FS6...","D",FS6$(1,30)
3060 REMOVE (_FS7,KEY=FS6$(31,3)+FS6$(11,1)+FS6$(12,10)+FS6$(1,10)+FS6$(22,8)+FS6$(30,1),DOM=3061)
3062 REMOVE (_FS8,KEY=FS6$(1,10)+FS6$(22,8)+FS6$(11,11)+FS6$(30,1),DOM=3063)
3064 REMOVE (_ASC,KEY=FS6$(1,10)+FS6$(54,15)+FS6$(11,20),DOM=3065)
3090 GOTO 3010
3095 RETURN 
4000 REM " Check to see if freight line already exists - SSP# 131719
4005 INDEX=INV[20]; OLD_INDEX=INV[20]
4011 DIM OLD_LINE$(144),OLD_LINE[10]; IF OLD_INDEX<=0 THEN GOTO 4080 ELSE READ (_ART,IND=OLD_INDEX)IOL=0490
4012 IF OLD_LINE$(47,3)<>"FRT" THEN OLD_INDEX=OLD_LINK; GOTO 4010; REM "If this is not a marked up frt line then ignore it
4015 FRT_EXIST=OLD_INDEX; LINK=OLD_LINK; RETURN 
4080 FRT_EXIST=0
4095 RETURN 
5200 REM "GET NEXT INDEX TO FILE IN A9
5210 EXTRACT (_ART,IND=0,ERR=5295)IOL=0290
5215 IF Y[4]=-2 THEN READ (_ART); WAIT 0; GOTO 5210
5220 Y[4]=-2; WRITE (_ART,IND=0)IOL=0290
5230 IF Y[1]<1 THEN A9=Y[2],Y[2]=Y[2]+1; GOTO 5260
5255 A9=Y[1]; READ (_ART,IND=A9)Y[1]
5260 IF Y[0]<Y[3] AND Y[2]<=Y[3] THEN Y[0]=Y[0]+1,Y[4]=-1 ELSE Y[2]=Y[2]-1; GOTO 4900
5270 IF U1>0 THEN U1=U1-1,A9$=A9$+BIN(A9,Y9); IF U1>0 THEN GOTO 5230
5280 WRITE (_ART,IND=0)IOL=0290
5285 IF A0=0 THEN A0=A9
5290 RETURN 
5295 IF ERR<>0 THEN GOTO 9000 ELSE RETRY 
8600 REM "Calculate commission
8604 DIM H$(33),H[15]; READ (_AR4,KEY=LINE$(101,5),DOM=8605)IOL=0440
8605 IF H$(32,1)<>"L" THEN LINE[7]=0; GOTO 8630
8607 IF H$(31,1)="T" THEN Q=0; IF POS(LINE$(1,1)="IC")>0 THEN CALL "FM2EXT",_ZZPARM,0,LINE$(50,4),LINE[6],LINE[1],E9,Q,2 ELSE Q=(LINE[5]-LINE[8])/.6
8611 X$="  "+LINE$(112,4)+"[X3$B]"+X3$+"[X3$E]"+"[A$]"+INV$+"[B$]"+LINE$
8613 CALL "ARGCOM",X$,H$,H{ALL},Q,LINE[4],LINE[5]-LINE[8]
8615 LINE[7]=Q
8640 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF %X3_OP_ESC_ENBD$="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
