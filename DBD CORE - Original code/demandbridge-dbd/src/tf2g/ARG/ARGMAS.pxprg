0010 ! ARGMAS - A/R Telemarketing Interface
0035 REM "5.7 - 06/12/09 - 7.748333 - tma - SSP# 230604
0037 REM "230604-Prompt displays as Telemarketing should be Contact          
0040 REM "Copyright 2009 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 RETRIEVE:
0055 MSGBOX "Retrieve name and address from Contact Management?","Confirm","?,YESNO",ANSWER$ ! ssp#230604
0060 IF ANSWER$="NO" THEN RETURN 
0065 AR_DATA$=REC(IOL(_AR1))
0070 PROCESS "ARGMAS","../ARG/AR.EN",AR_DATA$,STR(_AR1)
0075 READ DATA FROM AR_DATA$ TO IOL=IOL(_AR1)
0080 REFRESH_FLG=1
0085 EXIT 
0100 ! 
0200 INIT:! ^100,5 - Process the panel
0500 ! 500,5 - Open files
0503 GOSUB SETUP_MESSAGES
0505 _FILE_NAME$="ZP4"+%C$; _ZP4=HFN; OPEN (_ZP4,ERR=OPEN_ERR,IOL=*)_FILE_NAME$
0510 RETURN 
0515 ! 
0570 OPEN_ERR: MSGBOX _MSG_FILOPNERR1$+QUO+_FILE_NAME$+QUO+SEP+_MSG_DIRECTORY$+LWD+SEP+_MSG_PREFIX$+PFX,MSG(ERR),"!"
0575 CMD_STR$="E"
0580 EXIT 
0585 ! 
1000 POST_DISPLAY:! ^1000,5 - Read the customer record in
1005 READ DATA FROM ARG_1$ TO IOL=IOL(NUM(ARG_2$))
1010 NEXT_ID=LIST_TYPE_CODE.CTL
1015 EXIT 
1020 ! 
1100 ACCEPT_RECORD:! ^100,5
1105 READ (_ZP4,KEY=%LIST_TYPE_CODE$+CONT_CODE$,REC=TM$)
1110 CUST_NAME$=TM.CONT_NAME$
1115 CUST_ADDR_1$=TM.CONT_ADDR_1$
1120 CUST_ADDR_2$=TM.CONT_ADDR_2$
1125 CUST_CITY$=TM.CONT_CITY$
1130 CUST_ST$=TM.CONT_ST$
1135 CUST_ZIP_CODE$=TM.CONT_ZIP_CODE$
1137 CUST_FAX$=TM.CONT_FAX$
1140 CUST_LOOKUP$=TM.CONT_LOOKUP$
1145 PURCH_AGENT$=TM.CONT_PERSON$
1150 CUST_GREETING$=TM.CONT_GREETING$
1155 CUST_PHONE$=TM.CONT_PHONE$
1160 CONT_LIST_CODE$=TM.LIST_TYPE_CODE$
1165 CONT_CONT_CODE$=TM.CONT_CODE$
1170 ! Note:  the existing AR2MAS program reset the city and customer lookup
1175 ! with the following statement
1180 ! let q0$(116,16)=a$(391,28),q0$(155,10)=a$(419,20)
1185 ! the reason for this is unclear as the original data is longer than
1190 ! the destination fields.  In order to maintain this, the following
1195 ! has been added.
1200 CUST_CITY$=TM.CONT_LONG_CITY$
1205 CUST_LOOKUP$=TM.CONT_LONG_LOOKUP_SEQ$
1210 ! Customer Category is variable length, TM doesn't follow this standard
1215 CUST_CATEGORY$=PAD(TM.CONT_CATEGORY$,9,"0")
1300 ! ^100,5 - Finished getting data - return to program
1305 ARG_1$=REC(IOL(NUM(ARG_2$)))
1310 CMD_STR$="E"
1315 RETURN 
1320 ! 
2000 SYNC:! ^1000,5 - Synchronize TM files with AR
2005 IF P2$(67,1)="A" THEN {
2010 MSGBOX "Update corresponding Contact Management Record?","Confirm","?,YESNO",ANSWER$
2015 IF ANSWER$="NO" THEN RETURN 
2020  }
2025 GOSUB INIT
2026 ! ^100,5 - Get the record from ZP4
2030 READ DATA FROM "",REC=TM$ TO IOL=IOL(_ZP4)
2035 EXTRACT (_ZP4,KEY=CONT_LIST_CODE$+CONT_CONT_CODE$,DOM=WRAPUP,REC=TM$)
2040 TM.CONT_ADDR_1$=CUST_ADDR_1$
2045 TM.CONT_ADDR_2$=CUST_ADDR_2$
2050 TM.CONT_CITY$=CUST_CITY$
2055 TM.CONT_ST$=CUST_ST$
2060 TM.CONT_ZIP_CODE$=CUST_ZIP_CODE$
2065 TM.CONT_FAX$=CUST_FAX$
2070 TM.CONT_PERSON$=PURCH_AGENT$
2075 TM.CONT_GREETING$=CUST_GREETING$
2080 TM.CONT_PHONE$=CUST_PHONE$
2085 TM.CONT_LONG_CITY$=CUST_CITY$
2090 TM.CUST_DIV$=CUST_DIV$
2095 TM.CUST_CODE$=CUST_CODE$
2100 ! ^100,5 - For Superior (067) update contact name, category, & salesrep
2105 IF MID(P8$,7,1)="Y" THEN {
2110 TM.CONT_NAME$=CUST_NAME$
2115 TM.CONT_CATEGORY$=CUST_CATEGORY$(1,3)
2120 TM.SPER_CODE$=SPER_CODE$
2125  }
2200 ! ^100,5 - Write the record to ZP4
2205 WRITE (_ZP4,REC=TM$)
2290 GOTO WRAPUP
2295 ! 
9200 SETUP_MESSAGES:
9210 _MSG_FILOPNERR1$=MSG("FILOPNERR1")
9220 _MSG_DIRECTORY$=MSG("DIRECTORY")
9230 _MSG_PREFIX$=MSG("PREFIX")
9290 SETUP_MESSAGES_END:RETURN 
9295 ! 
9700 WRAPUP:! 9700,5 - Close files
9705 IF _ZP4 THEN CLOSE (_ZP4)
9710 RETURN 
9998 ! 9998,1 - End
9999 END 
