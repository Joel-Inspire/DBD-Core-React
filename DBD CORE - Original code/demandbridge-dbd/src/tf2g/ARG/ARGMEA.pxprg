0010 ! ARGMEA - Sales tax code setup
0035 REM "5.7 - 03/27/14 - 16.268888 - tma - SSP# 269247
0037 REM "269247-ARGMEA-GUI sales tax will allow you to setup a 3part Tax    
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 PROCESS "ARGMEA","../ARG/AR.EN"
0060 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0115 ! %FLMAINT_MSG$="./lib/_tfmsglib.en"
0120 X0$="AR2MEA",X1$="Sales Tax Code Setup"
0125 REM %SALES_TAX_MASK$="XX-XXXX-XXXX";REM SSP 220726
0130 GOSUB SETUP_LIBRARY; IF %FLMAINT_MSG$<>"" THEN MESSAGE_LIB %FLMAINT_MSG$
0140 ! 
0150 X2=-1,X0=-1
0160 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2; IF X1>0 THEN GOTO WRAPUP
0170 ! 
0190 DIM Z[NUM(X3$(60,3))],AR5[15],GL1[3],AR5_2[3],AR5$(400),B_AR5[5],C(7),B(7) ! WO249775
0200 ! Files
0210 Z$="01X AR5...  02O GL1...  06O ZZPARM  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
0300 ! Iolist
0310 IOLIST AR5$,AR5{ALL}
0320 IOLIST GL1$,GL1{ALL}
0330 IOLIST AR5_2$,AR5_2{ALL}
0340 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7]
0350 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7]
0400 ! 
0410 GOSUB GET_SYS_PARAM
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0511 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
0530 IF PARAM1$(119,1)="Y" THEN CALL "*WINGRP;ENABLE",GL_CODES.GRP$
0570 RETURN 
0600 ! Set up the message library
0605 SETUP_LIBRARY:
0610 _SV_MSGLIB$=MSG(*)
0615 _LANG$=LCS(%NOMAD_DEF_SFX$); IF _LANG$="" THEN _LANG$=LCS(ENV("PVXLANG")); IF _LANG$="" THEN _LANG$=LCS(ENV("LANG")); IF _LANG$="" THEN _LANG$="en"
0620 MESSAGE_LIB "*msglib."+_LANG$,ERR=*NEXT; GOTO 0628
0625 MESSAGE_LIB "*msglib.en"
0630 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0930 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0950 MESSAGE_LIB _SV_MSGLIB$
0970 RETURN 
1000 ! Find Record
1010 FIND_REC:
1015 IF STAX_CODE$="" THEN RETURN 
1016 NEW_RECORD=0,SETUP_DONE=0
1017 TR_TOTAL=0
1020 CODE_LEN=LEN(STP(STAX_CODE$))
1025 IF PARAM1$(119,1)="Y" AND (CODE_LEN=2) THEN CALL "*WINGRP;SHOW",GL_CODES.GRP$ ! SSP210253 jdf
1030 IF CODE_LEN=2 THEN {
1040 STAX_CODE$=STAX_CODE$(1,2); STAX_CODE$=PAD(STAX_CODE$,10,1," "); REFRESH_FLG=1
1050 SETUP_DONE=1; GOSUB GET_STATE_TAX
1060 GOSUB LOAD_TAX_LIST
1063 IF PARAM1$(119,1)="N" THEN CALL "*WINGRP;HIDE",GL_CODES.GRP$ ! SSP210253 jdf
1065  }
1070 ! 
1080 IF CODE_LEN>2 AND CODE_LEN<7 THEN {! SSP#231852
1090 KEY_1$=STAX_CODE$(1,CODE_LEN); KEY_1$=PAD(KEY_1$,10,1," "),STAX_CODE$=PAD(STAX_CODE$,10,1," "); REFRESH_FLG=1
1100 GOSUB GET_STATE_TAX ! SSP#214567
1110 GOSUB LOAD_TAX_LIST
1120 SETUP_DONE=1; GOSUB GET_COUNTY_TAX
1130 GOSUB LOAD_TAX_LIST
1132 CALL "*WINGRP;HIDE",GL_CODES.GRP$ ! SSP210253  jdf
1135  }
1140 ! 
1150 IF CODE_LEN>6 THEN {
1160 GOSUB GET_STATE_TAX
1170 GOSUB LOAD_TAX_LIST
1180 GOSUB GET_COUNTY_TAX
1190 SETUP_DONE=1; GOSUB LOAD_TAX_LIST
1200 GOSUB GET_SPECIAL_TAX
1210 GOSUB LOAD_TAX_LIST
1212 CALL "*WINGRP;HIDE",GL_CODES.GRP$ ! SSP210253 jdf
1215  }
1260 LIST_BOX LOAD TAX_LIST.CTL,0,""+SEP+""+SEP+"------------"+SEP+""
1270 LIST_BOX LOAD TAX_LIST.CTL,0,""+SEP+""+SEP+STR(TR_TOTAL:"###.000")+SEP+"" ! SSP#233428
1275 AR5[7]=TR_TOTAL
1280 REFRESH_FLG=1; CALL "*wingrp;Enable",BUTTON_1.GRP$
1285 IF CODE_LEN<6 THEN CALL "*wingrp;Enable",STATE.GRP$ ELSE CALL "*wingrp;Enable",TAX.GRP$
1287 NEXT_ID=STAX_DESC.CTL
1290 RETURN 
1500 ! Get State Tax
1505 GET_STATE_TAX:
1510 KEY_1$=STAX_CODE$(1,2); KEY_1$=PAD(KEY_1$,10,1," ")
1520 PRECISION 3; READ (Z[1],KEY=KEY_1$,DOM=STATE_MSG)IOL=0310 ! PRECISION 2
1530 STAX_DESC$=AR5$(11,25); TAX_FRT$=AR5$(36,1)
1531 USE_CANADA$=AR5$(106,1),TAX_SPL_CHG$=AR5$(104,1),TAX_TAX$=AR5$(105,1)
1532 STAX_P_A$=AR5$(55,12); ACCR_S_A$=AR5$(67,12)
1534 STAX_RATE=AR5[0]; MAX_STAX=AR5[1]
1540 TR_TOTAL+=AR5[0]
1560 B_AR5$=AR5$; B_AR5[0]=AR5[0]; B_AR5[1]=AR5[1]
1580 IDX=0; STATE_TAX=AR5[0]
1582 LIST_BOX FIND TAX_LIST.CTL,1,VV$,ERR=*RETURN; LIST_BOX LOAD TAX_LIST.CTL,1,*; GOTO *SAME
1590 RETURN 
1600 ! Get County Tax
1605 GET_COUNTY_TAX:
1610 KEY_1$=STAX_CODE$(1,6); KEY_1$=PAD(KEY_1$,10,1," "),STAX_CODE$=PAD(STAX_CODE$,10,1," ")
1620 PRECISION 3; READ (Z[1],KEY=KEY_1$,DOM=COUNTY_MSG)IOL=0310 ! PRECISION 2
1630 STAX_DESC$=AR5$(11,25); TAX_FRT$=AR5$(36,1)
1631 USE_CANADA$=AR5$(106,1),TAX_SPL_CHG$=AR5$(104,1),TAX_TAX$=AR5$(105,1)
1632 STAX_P_A$=AR5$(55,12); ACCR_S_A$=AR5$(67,12)
1634 STAX_RATE=AR5[0]; MAX_STAX=AR5[1]
1640 TR_TOTAL+=AR5[0]
1660 B_AR5$=AR5$; B_AR5[0]=AR5[0]; B_AR5[1]=AR5[1]
1680 COUNTY_TAX=AR5[0]
1690 RETURN 
1700 ! Get Special Tax
1705 GET_SPECIAL_TAX:
1710 KEY_1$=STAX_CODE$(1,10); KEY_1$=PAD(KEY_1$,10,1," ")
1720 PRECISION 3; READ (Z[1],KEY=KEY_1$,DOM=SPECIAL_MSG)IOL=0310 ! PRECISION 2
1730 STAX_DESC$=AR5$(11,25); TAX_FRT$=AR5$(36,1)
1731 USE_CANADA$=AR5$(106,1),TAX_SPL_CHG$=AR5$(104,1),TAX_TAX$=AR5$(105,1)
1732 STAX_P_A$=AR5$(55,12); ACCR_S_A$=AR5$(67,12)
1734 STAX_RATE=AR5[0]; MAX_STAX=AR5[1]
1740 TR_TOTAL+=AR5[0]
1760 B_AR5$=AR5$; B_AR5[0]=AR5[0]; B_AR5[1]=AR5[1]
1770 SPECIAL_TAX=AR5[0]
1790 RETURN 
2000 ! write record
2010 WRITE_REC:
2020 ! IF NEW_RECORD THEN MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"?,YesNo",ANS$
2030 ! IF ANS$="NO" THEN RETURN
2040 PRECISION 3; AR5[7]=AR5[0]+AR5[2]+AR5[3]; WRITE (Z[1],KEY=KEY_1$)IOL=0310 ! PRECISION 2
2060 B_AR5$=AR5$; B_AR5[0]=AR5[0]; B_AR5[1]=AR5[1]
2080 CALL "*wingrp;Disable",STATE.GRP$
2085 GOSUB UPDATE_CUMULATIVE
2090 RETURN 
2500 ! load Info
2510 LOAD_INFO:
2515 DIM AR5$(400) ! WO249775
2520 AR5$(1,10)=STAX_CODE$
2530 AR5$(11,25)=STAX_DESC$; AR5$(36,1)=TAX_FRT$
2540 AR5$(55,12)=STAX_P_A$; AR5$(67,12)=ACCR_S_A$
2545 AR5$(104,1)=TAX_SPL_CHG$,AR5$(105,1)=TAX_TAX$,AR5$(106,1)=USE_CANADA$
2570 AR5[0]=STAX_RATE; AR5[1]=MAX_STAX
2580 IF CODE_LEN>=6 AND CODE_LEN<10 THEN AR5[3]=STATE_TAX
2585 IF CODE_LEN>6 THEN AR5[2]=COUNTY_TAX; AR5[3]=STATE_TAX
2590 RETURN 
3000 ! delete record
3010 DELETE_REC:
3020 MSGBOX MSG("DELETE_REC"),MSG("VERIFY"),"?,YesNo",ANS$
3030 IF ANS$="NO" THEN RETURN 
3035 IF CODE_LEN>6 THEN GOTO 3090
3040 DEL_FLG=0
3050 KEY_2$=KEY_1$
3060 KEY_2$=KEY(1,END=3090)
3070 READ (Z[1],KEY=KEY_2$)
3080 IF KEY_1$(1,CODE_LEN)=KEY_2$(1,CODE_LEN) THEN DEL_FLG=1; GOSUB DEL_MSG; RETURN 
3090 REMOVE (Z[1],KEY=KEY_1$,DOM=*NEXT)
3180 NEXT_ID=STAX_CODE.CTL; REFRESH_FLG=1
3190 RETURN 
5000 ! check if any changes made
5010 CHECK_CHANGES:
5015 IF B_AR5$="" THEN RETURN 
5020 IF B_AR5$=AR5$ AND B_AR5[0]=AR5[0] AND B_AR5[1]=AR5[1] THEN RETURN 
5030 MSGBOX MSG("REC_ALTERD"),MSG("UPDATE"),"?,YesNo",ANS$
5040 IF ANS$="NO" THEN RETURN 
5050 GOSUB WRITE_REC
5090 RETURN 
6000 ! load List box
6010 LOAD_TAX_LIST:
6020 LIST_BOX LOAD TAX_LIST.CTL,0,AR5$(11,25)+SEP+AR5$(36,1)+SEP+STR(AR5[0]:"###.000")+SEP+STR(AR5[1]:"###.000") ! ssp#233428
6190 RETURN 
7000 ! First Record
7010 FIRST_REC:
7020 KEY_1$=KEF(Z[1],END=NO_FIRST)
7030 STAX_CODE$=KEY_1$; REFRESH_FLG=1
7040 GOSUB FIND_REC
7090 RETURN 
7100 ! Last Record
7110 LAST_REC:
7120 KEY_1$=KEL(Z[1],ERR=NO_LAST)
7130 STAX_CODE$=KEY_1$; REFRESH_FLG=1
7140 GOSUB FIND_REC
7190 RETURN 
7200 ! Prior Record
7210 PRIOR_REC:
7220 KEY_1$=KEP(Z[1],END=START_FILE)
7230 STAX_CODE$=KEY_1$; REFRESH_FLG=1
7240 GOSUB FIND_REC
7290 RETURN 
7300 ! Next Record
7310 NEXT_REC:
7320 KEY_1$=KEY(Z[1],END=END_FILE) ! ssp 198441
7330 STAX_CODE$=KEY_1$; REFRESH_FLG=1
7340 GOSUB FIND_REC
7350 RETURN 
8000 ! messages
8010 STATE_MSG:
8015 ! PRECISION 2
8020 MSG_1$=STAX_CODE$(1,2)+" "+MSG("STAX_MSG")+SEP+MSG("ADD_RECORD")
8030 MSGBOX MSG_1$,MSG("CONFIRM"),"?,YesNo",ANS$; IF ANS$="NO" THEN GOTO DONT_SETUP ELSE NEW_RECORD=1
8040 STATE_TAX=0,STAX_CODE$=STAX_CODE$(1,2)+DIM(8); REFRESH_FLG=1 ! SSP#269247
8050 CALL "*wingrp;Enable",STATE.GRP$; CALL "*wingrp;Enable",BUTTON_1.GRP$
8060 NEXT_ID=STAX_DESC.CTL
8090 EXIT 
8110 COUNTY_MSG:
8115 ! PRECISION 2
8120 MSG_1$=STAX_CODE$(1,2)+"-"+STAX_CODE$(3,4)+" "+MSG("STAX_MSG_C")+SEP+MSG("ADD_RECORD")
8130 MSGBOX MSG_1$,MSG("CONFIRM"),"?,YesNo",ANS$
8140 IF ANS$="NO" THEN GOTO DONT_SETUP ELSE NEW_RECORD=1
8150 CALL "*wingrp;Enable",TAX.GRP$; CALL "*wingrp;Enable",BUTTON_1.GRP$
8160 NEXT_ID=STAX_DESC.CTL
8180 COUNTY_TAX=0,STAX_CODE$=STAX_CODE$(1,6)+DIM(4); REFRESH_FLG=1 ! SSP#269247
8190 EXIT 
8210 SPECIAL_MSG:
8215 ! PRECISION 2
8220 MSG_1$=STAX_CODE$(1,2)+"-"+STAX_CODE$(3,4)+"-"+STAX_CODE$(7,4)+" "+MSG("STAX_MSG_S")+SEP+MSG("ADD_RECORD")
8230 MSGBOX MSG_1$,MSG("CONFIRM"),"?,YesNo",ANS$
8240 IF ANS$="NO" THEN GOTO DONT_SETUP ELSE NEW_RECORD=1
8250 CALL "*wingrp;Enable",TAX.GRP$; CALL "*wingrp;Enable",BUTTON_1.GRP$
8260 NEXT_ID=STAX_DESC.CTL
8290 EXIT 
8300 ! Cannot delete message
8310 DEL_MSG:
8320 MSG1$=MSG("TAX_EX")+SEP+MSG("TAX_EX_2")
8330 MSGBOX MSG1$,MSG("FYI"),"INFO"
8390 RETURN 
8500 ! No First Record
8510 NO_FIRST:
8520 MSGBOX MSG("REC_NO_1ST"),"FYI","!"
8530 RETURN 
8600 ! No Last Record
8610 NO_LAST:
8620 MSGBOX MSG("REC_NO_LST$"),"FYI:," ! "
8630 RETURN 
8700 ! Start of File
8710 START_FILE:
8720 MSGBOX MSG("START_FILE"),"FYI","!"
8730 RETURN 
8800 ! End of File
8810 END_FILE:
8820 MSGBOX MSG("END_OF_FIL"),"FYI","!"
8830 RETURN 
9000 ! Parameters
9010 GET_SYS_PARAM:
9020 READ (Z[6],KEY=X3$(9,3)+"A/R")PARAM1$
9030 IF MID(PARAM1$,134,1)="Y" THEN CMD_STR$="END"; GOTO WRAPUP ! Change because this flag calling another program SSP 219276 - added MID to avoid potential pe
9040 IF PARAM1$(119,1)="Y" THEN RETURN ! Look at the formating for this Flag
9090 RETURN 
9500 ! Clear ALl Fields
9510 CLEAR_FIELDS:
9520 STAX_CODE$=""
9530 STAX_DESC$=""; TAX_FRT$=""
9535 USE_CANADA$="",TAX_SPL_CHG$="",TAX_TAX$=""
9540 STAX_P_A$=""; ACCR_S_A$=""
9550 STAX_RATE=0; MAX_STAX=0
9560 DIM AR5$(400),AR5[15],B_AR5[3]; B_AR5$="" ! WO249775
9570 CALL "*wingrp;Disable",STATE.GRP$
9575 REFRESH_FLG=1
9580 LIST_BOX FIND TAX_LIST.CTL,1,VV$,ERR=*RETURN; LIST_BOX LOAD TAX_LIST.CTL,1,*; GOTO *SAME
9590 RETURN 
9600 DONT_SETUP:! ^100 Answered NO to setup new record
9605 NEXT_ID=STAX_CODE.CTL
9695 EXIT 
9699 ! **********************************
9999 END 
10000 UPDATE_CUMULATIVE:! 10000,10 ssp 200490
10010 REM "Update cumulative totals"
10020 REM "V2 is set @ 1518 C(0) is set in 6300's
10030 X5$="Now updating other tax rates that are based on "+MID(AR5$,11,25)
10040 PRINT 'MESSAGE'('_RED'+'WHITE'+X5$),
10050 V2=POS(" "=KEY_1$); IF V2=0 THEN GOTO 10180
10060 IF V2>7 OR V2=0 THEN V1=2 ELSE IF V2>3 THEN V1=1 ELSE V1=0
10070 K$=KEY(Z[1],END=10180)
10080 DIM C[7]
10090 IF K$(1,V2-1)<>AR5$(1,V2-1) THEN GOTO 10180
10100 IF V1=1 THEN FIND (Z[1],KEY=K$(1,2)+"        ",DOM=10115)IOL=0340; GOTO 10115
10110 IF POS(" "<>K$(7,4))<>0 THEN FIND (Z[1],KEY=K$(1,6)+"    ",DOM=10115)IOL=0340
10120 EXTRACT (Z[1],KEY=K$,END=10180)IOL=0350
10130 XX$=B$(1,10),XX$=STR(XX$:%SALES_TAX_MASK$); PRINT 'MESSAGE'('_RED'+'WHITE'+XX$),
10140 IF V=1 THEN B[2]=AR5[0],B[3]=C[0] ELSE B[2]=C[0],B[3]=AR5[0],B[7]=B[0]+B[2]+B[3]
10150 WRITE (Z[1],KEY=K$)IOL=0350
10160 IF W9$>"" THEN IF W9$(7,1)="H" THEN CALL "RT2WOR",ERR=10165,X3$,X4$,"","AR5...","U",K$,B$,3,B{ALL}; REM "Record remote T/M change if this is the host
10170 GOTO 10070
10180 PRINT 'MESSAGE'('F8'+'BLACK'+"",0),
10190 RETURN 
56000 REM "220726-Possibly change AR parameter to use Sales Tax Code stored 
56005 REM "5.4 - 03/21/06 - 17.18 - kmc - SSP# 194087
56006 ! "198441-Panel ARGMEA - Sales Tax Setup, using the next record button
56007 ! "       will only show every other record"
56010 ! "200490-Sales tax setup if you update a change to the Sales tax
56012 REM "210253-Sales tax GL account should only be assessible at state     
56013 REM "219276-Request for conference call to discuss Veratax and XML  
56014 REM "231852-Total Sales Tax Rate does not display correctly on the      
56015 REM "233428-Display issue on Sales Tax Codes in GUI                     
56016 REM "249775-Changes to sales tax parameters and tax calculations to     
56017 REM "269247-ARGMEA-GUI sales tax will allow you to setup a 3part Tax    
