0010 ! ARGMAD - Open Invoice Inquiry
0035 REM "5.7 - 01/31/23 - 23.346803 - crg - SSP# 307435
0037 REM "307435-DBD-337: Capture/store notes with Cash Receipts             
0040 REM "Copyright 2023 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 IF TCB(13)=1 THEN BEGIN 
0055 _CUSTOMER$=%CUSTOMER$
0060 PROCESS "ARGMAD","../ARG/AR.EN"
0065 %CUSTOMER$=_CUSTOMER$
0070 GOTO 9800
0075 ! 
0100 INIT:! ^100,5 - Initialization routine
0105 DIM I[15],D[2]
0110 K0=10,K1=1,X2=1
0115 C9=-1,V9=-2,Q1=0,RANGE_FLG=0
0120 M0$="#,###,###.00-"
0122 MSGBOX MSG("DSP_INV"),MSG("CONFIRMING"),"?,YESNO",OPT$ ! SSP 190142
0125 DEF FND$(LOCAL X$)=X$(3,2)+"/"+X$(5,2)+"/"+STR(DEC(X$(1,1))+125)+X$(2,1)
0130 ! 
0135 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2; IF X1>0 THEN GOTO 9750
0140 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=*NEXT)
0145 K9$="",K9=LEN(K9$)
0300 ! 300,5 - Iolists & templates
0305 DETAILS_IOL:IOLIST INVOICE$,TRAN_DATE$,TF_REF$,DUE_DATE$,AMOUNT$,INV_BAL$,NOTE_IND$,NOTE_KEY$
0310 GLOBAL_IOL:IOLIST 
0315 RANGE_IOL:IOLIST START_INV$:[LEN(8,SEP=SEP)],END_INV$:[LEN(8,SEP=SEP)],START_DATE$:[LEN(6,SEP=SEP)],END_DATE$:[LEN(6,SEP=SEP)],START_PO$:[LEN(15,SEP=SEP)],END_PO$:[LEN(15,SEP=SEP)]
0320 IOLIST I$,I[0],I[1],I[2],I[3],I[4],I[5],I[6],I[7],I[8],I[9],I[10],I[11]
0325 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15]
0330 IOLIST C$
0335 IOLIST I1,D$(1),D[0],D[1]
0340 IOLIST D$(1),D[0],D[1]
0500 ! 500,5 - Open files
0503 GOSUB SETUP_MESSAGES
0505 _FILE_NAME$="AP4"+%C$; _AP4=HFN; OPEN (_AP4,ERR=OPEN_ERR)_FILE_NAME$
0510 _FILE_NAME$="AR1"+%C$; _AR1=HFN; OPEN (_AR1,ERR=OPEN_ERR,IOL=*)_FILE_NAME$
0515 _FILE_NAME$="AR6"+%C$; _AR6=HFN; OPEN (_AR6,ERR=OPEN_ERR)_FILE_NAME$
0520 _FILE_NAME$="AR7"+%C$; _AR7=HFN; OPEN (_AR7,ERR=OPEN_ERR)_FILE_NAME$
0525 _FILE_NAME$="ARH"+%C$; _ARH=HFN; OPEN (_ARH,ERR=OPEN_ERR)_FILE_NAME$
0530 _FILE_NAME$="FS8"+%C$; _FS8=HFN; OPEN (_FS8,ERR=OPEN_ERR)_FILE_NAME$
0535 _FILE_NAME$="FS6"+%C$; _FS6=HFN; OPEN (_FS6,ERR=OPEN_ERR)_FILE_NAME$
0540 _FILE_NAME$="ZZPARM"; ZZPARM=HFN; OPEN (ZZPARM,ERR=OPEN_ERR)_FILE_NAME$
0565 GOTO CONTINUE
0570 OPEN_ERR: MSGBOX _MSG_FILOPNERR1$+QUO+_FILE_NAME$+QUO+SEP+_MSG_DIRECTORY$+LWD+SEP+_MSG_PREFIX$+PFX,MSG(ERR),"!"
0575 CMD_STR$="E"
0580 EXIT 
0585 ! 
0590 CONTINUE:
0595 ! 
0600 ! ^100,5 - Create iolists from data dictionary
0605 CALL "ZZWIOL",_AR1,"B",AR1_IOL$
0610 ! 
0620 CALL "ZZINFO",_AR7,STATUS,X3$,REC_USED,TOT_REC,KEY_SZ,DISC,BYTE,TYPE,TOT_SEC,FILE_NM$
0700 ! ^100,5 - Finish up initialization
0705 W9$=""; CALL "RT2PRM",ERR=*NEXT,X3$,X4$,W9$
0710 FIND (ZZPARM,KEY=X3$(9,3)+"A/R",DOM=9708)P0$
0715 DIM B$:CPL(AR1_IOL$),B[15]
0720 DIM A$(21+K9)
0725 A$(1,0+K9)=K9$+A1$
0730 DIM R$:IOL=RANGE_IOL
0735 R0=-9999999,R1=9999999
0740 GOSUB GET_END_DATE
0750 END_DATE$=R.END_DATE$; REM " rem'd out because this caused the total not to display in the upper right hand corner !R.END_DATE$=""
0800 ! ^100,5 \\End of initialization
0805 EXIT 
0810 ! 
0900 POST_DISPLAY:! ^100,5 - Do stuff after the panel is created
0903 IF P0$(15,1)<>"Y" THEN CALL "*wingrp;Hide",BILLTO.GRP$
0905 IF %CUSTOMER$<>"" THEN {
0910 CUSTOMER$=%CUSTOMER$
0912 DISABLE CONTROL CUSTOMER.CTL
0915 GOTO PROCESS_CUSTOMER
0920  }
0925 NEXT_ID=CUSTOMER.CTL,REFRESH_FLG=1
0930 RETURN 
0935 ! 
1000 READ_RECORDS:! ^1000,5 - Display invoices
1005 LIST_BOX LOAD DETAILS.CTL,""
1010 READ (_AR6,KEY=A$(1,10),DOM=*NEXT)
1015 IF K0$="" THEN K0$=KEY(_AR6,END=2000),K0$(LEN(K0$),1)=" "; GOTO 1025
1020 K0$=KEY(_AR6,END=2000)
1025 READ (_AR6,KEY=K0$,DOM=1020,END=2000)IOL=0320
1030 IF I$(1,10)<>A$(1,10) THEN GOTO 2000
1032 IF OPT$="NO" AND I(3)=0 THEN GOTO 1020 ! SSP 190142
1033 IF RANGE_FLG=0 THEN GOTO 1100
1035 IF NOT(NUL(R.START_INV$)) OR NOT(NUL(R.END_INV$)) THEN IF I$(11,8)<R.START_INV$ OR I$(11,8)>R.END_INV$ THEN GOTO 1020
1040 IF NOT(NUL(R.START_DATE$)) OR NOT(NUL(R.END_DATE$)) THEN IF I$(20,6)<R.START_DATE$ OR I$(20,6)>R.END_DATE$ THEN GOTO 1020
1045 IF NOT(NUL(R.START_PO$)) OR NOT(NUL(R.END_PO$)) THEN IF I$(36,15)<R.START_PO$ OR I$(36,15)>R.END_PO$ THEN GOTO 1020
1050 IF I[2]<R0 OR I[2]>R1 THEN GOTO 1020
1055 IF CUST_PO_OPTION$>"" THEN GOTO 1020
1100 ! ^100,5 - Print
1105 IF I$(11,7)="ONACCNT" THEN INVOICE$="On Account" ELSE INVOICE$=I$(11,8)
1110 TRAN_DATE$=I$(20,6),DUE_DATE$=I$(54,6),AMOUNT$=STR(I[6]:M0$); CALL "ZZWDTE;DISPLAY",TRAN_DATE$; CALL "ZZWDTE;DISPLAY",DUE_DATE$ ! SSP#227947
1112 IF POS(" "<>I$(26,15))=0 AND POS(" "<>I$(66,8))<>0 THEN TF_REF$="Ord: "+I$(66,8) ELSE TF_REF$="P/O: "+I$(36,15)
1113 HIDDEN_INV$=INVOICE$
1115 GOSUB LOAD_LIST_BOX
1120 ! 
1125 IF POS(" "<>MID(I$,160,8))>0 THEN {
1130 TF_REF$="Summary Bill: "+I$(160,8)
1135 GOSUB LOAD_LIST_BOX
1140  }
1145 IF I[5]<>0 THEN {
1150 TF_REF$="Cash in progress"; AMOUNT$=STR(-I[5]:M0$)
1155 GOSUB LOAD_LIST_BOX
1160  }
1165 IF POS(" "<>I$(26,10))>0 THEN {
1168 CALL "ZZDISP","AX",I$(26,10),"A/R",%X3$,BILL_FROM$,"",0,0,%X4$ ! SSP#291330
1170 TF_REF$="Bill from customer: "+BILL_FROM$ ! SSP#291330
1175 GOSUB LOAD_LIST_BOX
1180  }
1200 ! ^100,5 - Do lines and/or end
1202 I0=I[1]
1204 IF KEY_SZ THEN {
1205 KEY_AR7$=K0$
1206 READ (_AR7,KEY=KEY_AR7$,DOM=*NEXT)
1207 KEY_AR7$=KEY(_AR7,END=1500); DIM D$(70); READ (_AR7,KEY=KEY_AR7$)IOL=0340 ! SSP#209664
1208 IF KEY_AR7$(1,18)<>K0$ THEN GOTO 1500
1209 IF MID(D$,49,1)="H" THEN GOTO 1207 ! SSP#209664
1211 GOTO 1300 } ! SSP#209664
1212 IF I0<=0 THEN GOTO 1500 ! SSP#209664
1215 ! Read will set I1 to be used to set i0
1220 DIM D$(70); READ (_AR7,IND=I0,ERR=1500)IOL=0335 ! SSP#209664
1225 I0=I1
1300 ! ^100,5 - Print detail
1302 IF NOT(NUL(MID(D$,50,5))) THEN NOTE_KEY$=MID(D$,50,5),NOTE_IND$="**"
1305 ON POS(D$(19,1)="PACD") GOTO 1388,1310,1330,1355,1375
1310 ! Payment
1315 TRAN_DATE$=D$(20,6); CALL "ZZWDTE;DISPLAY",TRAN_DATE$; TF_REF$="Chk: "+D$(26,8)+" AC#: "+D$(34,4)+"."+D$(38,2); IF (D[0]-D[1])<0 THEN AMOUNT$='RED'+STR(D[0]-D[1]:M0$) ELSE AMOUNT$=STR(D[0]-D[1]:M0$) ! SSP#227947
1320 GOSUB LOAD_LIST_BOX
1325 GOTO 1390
1330 ! Adjustment
1340 TRAN_DATE$=D$(20,6); CALL "ZZWDTE;DISPLAY",TRAN_DATE$; TF_REF$="Adj:"+D$(26,8)+"AC#: "+D$(34,4)+"."+D$(38,2); IF D[0]<0 THEN AMOUNT$='RED'+STR(D[0]:M0$) ELSE AMOUNT$=STR(D[0]:M0$) ! SSP#227947
1345 GOSUB LOAD_LIST_BOX
1350 GOTO 1390
1355 ! Credit memo
1360 TRAN_DATE$=D$(20,6); CALL "ZZWDTE;DISPLAY",TRAN_DATE$; TF_REF$="CM:"+D$(26,8)+"AC#: "+D$(34,4)+"."+D$(38,2); IF D[0]<0 THEN AMOUNT$='RED'+STR(D[0]:M0$) ELSE AMOUNT$=STR(D[0]:M0$) ! SSP#227947
1365 GOSUB LOAD_LIST_BOX
1370 GOTO 1390
1375 ! Debit memo
1380 TRAN_DATE$=D$(20,6); CALL "ZZWDTE;DISPLAY",TRAN_DATE$; TF_REF$="DM:"+D$(26,8)+" "+"AC#: "+D$(34,4)+"."+D$(38,2); IF D[0]<0 THEN AMOUNT$='RED'+STR(D[0]:M0$) ELSE AMOUNT$=STR(D[0]:M0$) ! SSP#227947
1385 GOSUB LOAD_LIST_BOX
1390 IF D$(19,1)="P" AND D[1]<>0 THEN {
1395 TF_REF$="Discount",AMOUNT$=STR(D[1]:M0$)
1400 GOSUB LOAD_LIST_BOX
1405  }
1410 IF KEY_SZ THEN GOTO 1207 ELSE GOTO 1212 ! SSP#225923
1500 ! ^100,5 - Finish up and print invoice balance
1505 TF_REF$='BLUE'+"Invoice balance",INV_BAL$=STR(I[3]-I[5]:M0$)
1510 IF I[3]-I[5]<0 THEN INV_BAL$='RED'+INV_BAL$
1515 GOSUB LOAD_LIST_BOX
1520 GOSUB LOAD_LIST_BOX ! Print a blank line between invoices
1525 TOTAL+=I[3]-I[5]
1530 GOSUB 3200
1535 GOTO 1015
2000 ! ^1000,5 - End of display
2005 DETAIL_LINES=DETAILS.CTL'ITEMCOUNT
2010 IF NOT(DETAIL_LINES) THEN {
2015 TF_REF$='BLUE'+"No invoices to display"
2020 GOSUB LOAD_LIST_BOX
2025  }
2026 IF STP(R.START_DATE$,2)="" AND R.END_DATE$=END_DATE$ THEN {
2030 TOTAL$=STP(STR(TOTAL:M0$),2)
2031  }
2035 NEXT_ID=EXIT.CTL,REFRESH_FLG=1
2040 EXIT 
2045 ! 
2050 IF NOT(DETAIL_LINES) THEN GOTO 2065
2055 CUST_PO_OPTION$=" "
2060 GOSUB CUST_PO; GOTO 1013
2065 CALL "ZZPROM","X1AR2MAD",X3$,Z,A9$,"","",0
2070 IF Q1$<>"" THEN GOTO 9708 ELSE GOTO 0713
3000 PROCESS_CUSTOMER:! ^1000,5 - Get customer code and associated fields
3005 READ DATA FROM "",REC=B$ TO IOL=IOL(_AR1)
3010 READ DATA FROM "",REC=BILL$ TO IOL=IOL(_AR1)
3015 CALL "ZZWLKU;Parse_cust",CUSTOMER$,B.CUST_DIV$,B.CUST_CODE$
3016 CUSTOMER$=B.CUST_DIV$+B.CUST_CODE$
3020 READ (_AR1,KEY=B.CUST_DIV$+B.CUST_CODE$,DOM=MISSING_CUST,REC=B$)
3025 IF STP(B$(46,10),2)<>"" THEN {
3030 READ (_AR1,KEY=B$(46,10),DOM=*NEXT,REC=BILL$); BILL.CUSTOMER$=BILL.CUST_DIV$+BILL.CUST_CODE$
3035  }
3040 REFRESH_FLG=1,NEXT_ID=OK.CTL,K1$=KEC(_AR1),A$(1,10)=B$(1,10)
3045 IF STP(%CUSTOMER$,2)<>"" THEN GOTO READ_RECORDS
3050 RETURN 
3055 ! 
3100 MISSING_CUST:! ^100,5 - Can't process this customer code
3105 MSGBOX MSG("CUST_NOF"),MSG("ATTN"),"!"
3110 NEXT_ID=CUSTOMER.CTL,REFRESH_FLG=1,CUSTOMER$="",B.CUST_DIV$="",B.CUST_CODE$=""
3115 RETURN 
3120 ! 
3200 ! ^100,5 - Job data
3205 IF POS(X3$(9,3)="019025")=0 THEN RETURN 
3210 READ (_FS8,KEY=I$(1,10)+I$(66,8)+"B",DOM=*NEXT)
3215 Q$=KEY(_FS8,END=3240); IF Q$(1,18)<>I$(1,10)+I$(66,8) THEN GOTO 3240 ELSE IF Q$(19,1)<>"B" THEN GOTO 3240
3220 READ (_FS6,KEY=Q$(1,10)+Q$(19,11)+Q$(11,8)+Q$(30,1),DOM=3240)E$
3225 DIM F$(60); FIND (_AP4,KEY=E$(87,10),DOM=*NEXT)F$
3230 TF_REF$="Vendor: "+F$(1,10)+" "+STP(F$(11,30),2)+"\ Job: "+E$(69,10)+" Order: "+E$(22,2)+"-"+E$(24,6)
3235 GOSUB LOAD_LIST_BOX
3240 RETURN 
3245 ! 
3250 CUST_PO:REM 
3260 DIM CUST_PO$(15); CUST_PO_OPTION$=I$(1,18)
3265 CALL "ZZENTR","S",A{ALL},CUST_PO$,X4$,X3$,45,22,1,15,C0,"","","","AR2MAD22","AR1","AR2MAB",""
3270 READ (_AR6,KEY=A$(1,10),DOM=3273)
3275 READ (_AR6,END=3290)IOL=0320
3280 IF A$(1,10)<>I$(1,10) THEN GOTO 3290 ELSE IF I$(36,15)<>CUST_PO$ THEN GOTO 3275
3285 CUST_PO_OPTION$=I$(1,18); GOTO 3295
3290 CALL "ZZPROM",".4",X3$,0,"Customer P/O Number Not Found!","","",0
3295 EXTRACT (_AR6,KEY=CUST_PO_OPTION$,DOM=3298)
3300 END_CUST_PO:RETURN 
3305 DEF FNS$(X$)=X$(1,POS(S$=X$+S$)-1)
3310 ! 
4000 CLEAR_REC:! ^1000,5 - Clear screen
4005 IF STP(%CUSTOMER$,2)<>"" THEN CMD_STR$="E"; EXIT 
4010 READ DATA FROM "",REC=B$ TO IOL=IOL(_AR1)
4015 READ DATA FROM "",REC=BILL$ TO IOL=IOL(_AR1)
4020 LIST_BOX LOAD DETAILS.CTL,""
4025 REFRESH_FLG=1,CUSTOMER$="",END_DATE$="",NEXT_ID=CUSTOMER.CTL,TOTAL=0,TOTAL$=""
4030 EXIT 
4035 ! 
5000 LOAD_LIST_BOX:! ^1000,5 - Update list box
5005 LIST_BOX LOAD DETAILS.CTL,0,REC(IOL=DETAILS_IOL)+HIDDEN_INV$
5010 READ DATA FROM "" TO IOL=DETAILS_IOL
5015 RETURN 
5020 ! 
5100 GET_END_DATE:! ^100,5 - Get todays date and set to TopForm format
5105 R.END_DATE$=DTE(0:"%Yl%Mz%Dz")
5110 R.END_DATE$=CHR(NUM(R.END_DATE$(1,3))-125)+R.END_DATE$(4)
5115 RETURN 
5120 ! 
6000 RANGE:! ^1000,5 - Adjust invoice ranges
6010 REPLACEMENT_SCRN$="ARGMAD_1"
6020 PERFORM "*winproc;overlay_screen"
6031 TOTAL=0,TOTAL$=""
6033 RANGE_FLG=1
6035 GOTO READ_RECORDS
6040 RETURN 
6045 ! 
6100 INVOICE:
6110 REPLACEMENT_SCRN$="ARGMAD_2"
6120 PERFORM "*winproc;overlay_screen"
6130 IF NUL(INV_NUM$) THEN RETURN 
6150 PROCESS "ARGCAE","../ARG/AR.EN",%CUSTOMER$+INV_NUM$,INV_NUM$
6190 RETURN 
7000 ! ^100 - Show invoice details
7010 SHOW_DETAILS:
7020 IF DETAILS$="" THEN RETURN 
7030 READ DATA FROM DETAILS$ TO _Y1$,_Y2$,_Y3$,_Y4$,_Y5$,_Y6$,_Y7$,NOTE_KEY$,INVOICE$
7032 IF NOT(NUL(_Y7$)) THEN PROCESS "ARGAT8","../ARG/AR.EN",NOTE_KEY$; GOTO *RETURN
7035 IF NUL(_Y1$) THEN RETURN 
7037 IF STP(_Y1$)="On Account" THEN MSGBOX MSG("CANNOT_SEL"),MSG("FYI"),"STOP"; RETURN 
7040 PROCESS "ARGCAE","../ARG/AR.EN",B.CUST_DIV$+B.CUST_CODE$+INVOICE$,INVOICE$
7050 RETURN 
9200 SETUP_MESSAGES:
9210 _MSG_FILOPNERR1$=MSG("FILOPNERR1")
9220 _MSG_DIRECTORY$=MSG("DIRECTORY")
9230 _MSG_PREFIX$=MSG("PREFIX")
9290 SETUP_MESSAGES_END:RETURN 
9295 ! 
9700 WRAPUP:! 9700,5 - Close files and clean up
9705 READ DATA FROM "" TO IOL=GLOBAL_IOL
9710 IF _AP4 THEN CLOSE (_AP4)
9715 IF _AR1 THEN CLOSE (_AR1)
9720 IF _AR6 THEN CLOSE (_AR6)
9725 IF _AR7 THEN CLOSE (_AR7)
9730 IF _FS8 THEN CLOSE (_FS8)
9735 IF _FS6 THEN CLOSE (_FS6)
9740 IF ZZPARM THEN CLOSE (ZZPARM)
9745 EXIT 
9800 ! ^100,5 - Return to previous
9805 IF TCB(13)=1 THEN RUN "ARGMEN"
9998 ! 9998,1 - End
9999 END 
56000 ! "190142-In open Invoice and Invoice History, have the ability to    
56002 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
