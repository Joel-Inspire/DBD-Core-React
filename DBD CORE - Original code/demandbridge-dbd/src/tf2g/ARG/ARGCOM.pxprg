0010 REM "A/R Commission calculation program <ARGCOM>
0035 REM "5.5 - 03/01/07 - 14.548611 - mhe - SSP# 205471
0040 REM "Copyright 2007 TopForm Software Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0070 REM "C0$=control,C$ & C(ALL)=commission stuff, C=calc comm amt,
0071 REM "S=sales amount, C0=cost, R=Rate
0072 REM "stmt 3080 modified 10/31/91 for 0 sell
0073 REM "House charge table logic added 8/10/92
0074 REM "Modified to do defaulting of comm code as well
0075 REM " C0='DEFAULT' means default comm code & return as C$
0076 REM "C$ input is PO Code (1,1)['*' means ignore P/O code], line type(2,1)('FICNS')+Company code (3,3)+Comm code from header or customer(6,6)
0077 REM "C is file slot of ZZPARM
0078 REM "3/14/96 -Invoice Header and Invoice Line passed in C0$ then split into A$ & B$  -LINk to storage charge, ALSO, X3$ is passed independently
0100 SETERR 9000
0110 ENTER C0$,C$,C{ALL},C,S,C0
0112 P0=POS("[X3$B]"=C0$),P1=POS("[X3$E]"=C0$); IF P0>0 THEN X3$=C0$(P0+6,P1-P0-6),C0$=C0$(1,P0-1)+C0$(P1+6)
0115 P0=POS("[A$]"=C0$),P1=POS("[B$]"=C0$); IF P0>0 THEN A$=C0$(P0+4,P1-P0-4),B$=C0$(P1+4),C0$=C0$(1,P0-1)
0120 IF C0$="DEFAULT" THEN F0$="*"; GOSUB 7500; GOTO 7600
0140 IF LEN(C0$)<6 THEN C0$=C0$+" "; GOTO 0140
0145 IF LEN(C$)<44 THEN C$=C$+" "; GOTO 0145
0150 GOSUB 7500
0160 IF C0$(1,2)="HC" OR C0$(1,2)="CC" THEN GOSUB 8200; C=Q2; GOTO 9900
0199 REM "Passed by calling program - for Lancer
0200 S1=C
0230 C=0
0300 REM 
0310 IOLIST Q$,Q[0],Q[1],Q[2],Q[3],Q[4],Q[5],Q[6],Q[7],Q[8],Q[9],Q[10],Q[11],Q[12],Q[13],Q[14],Q[15],Q[16]
0600 REM "Calculate commission
0605 REM "calculate amount(A)and rate R
0610 ON POS(C$(31,1)="GSPNICTQ") GOSUB 9900,1000,2000,3000,4000,5000,1200,1900,1250,9900
0620 IF POS(C$(6,1)="-!&")>0 AND A<0 THEN IF C$(6,1)="-" THEN R=50 ELSE R=100; REM "1st char of comm code desc = - means commission rate is 50%, !=100%
0640 IF C$(6,1)="!" AND A<15 AND A>=0 THEN R=0
0650 IF C$(6,1)="<" AND ABS(A)<100 THEN R=30
0700 REM "Run through table
0710 C=A*R*.01; IF POS(C$(33,1)="123456789")>0 AND Q$(32,1)="Y" THEN C=C-Q3
0760 IF C<>0 AND A<>0 THEN IF SGN(C)<>SGN(A) THEN C=0
0800 REM "Exit
0820 IF C>0 AND NUM(C$(35,10))>0 AND C<NUM(C$(35,10)) THEN C=0; REM "*** IF DOLLAR AMT < MIN DOLLAR AMT
0890 GOTO 9900
1000 REM "By gross profit
1010 A=S-C0
1020 GOSUB 4100
1079 REM "Get rate R
1080 T=A; GOSUB 8000
1090 RETURN 
1200 REM "By cost - Fleet 4/29/90
1210 A=C0
1220 GOTO 1020
1250 REM "Q=Buckeye Special Type / uses House charge if entered!
1255 A=C0; GOSUB 4100; A=C0+(C0-A); REM "Must ADD to cost
1260 IF A<250 THEN L9=.55; GOTO 1300
1270 IF A<500 THEN L9=.6; GOTO 1300
1280 IF A<750 THEN L9=.65 ELSE L9=.7
1300 L9=A/L9; REM "List price now in L9
1320 IF L9<>0 THEN T=(L9-S)*100/L9 ELSE T=0
1330 IF T<1 THEN T=0
1340 GOSUB 8000
1345 A=S
1350 RETURN 
1900 REM "T= Lancer
1904 IF S=0 THEN A=-C0,R=50; GOTO 1934
1906 IF S1=0 THEN IF C0=0 THEN A=S,R=C[1]; GOTO 1934 ELSE S1=C0/.6
1910 PRECISION 4
1920 IF C0/S>.6 THEN S2=(100-C0*100/S)/2 ELSE IF ABS(S1)>ABS(S) THEN S2=C[1]-(100-S*100/S1)/2 ELSE S2=C[1]
1930 PRECISION 2; A=S+0,R=S2+0
1940 RETURN 
2000 REM "By sales
2010 A=S
2020 IF C$(33,1)=" " THEN C$(33,1)="S"
2030 GOSUB 4100
2079 REM "Get rate R
2080 T=A; GOSUB 8000
2090 RETURN 
3000 REM "Gross profit percentage based
3010 A=S-C0
3079 REM "Get rate R
3080 REM "IF S<> THEN LET T=A*100/S: GOSUB 8000 ELSE LET R=C[1]; THis line of code causes the Basic Commission rate to be used if the Sale Price is zero. Removed by SSP 117692 and line 3081 added to use the rate in the table based on the abs of the gp% 
3081 IF S<>0 THEN T=A*100/S; GOSUB 8000 ELSE T=A*100/1; GOSUB 8000
3084 REM "Take out house charge after figuring rate
3085 GOSUB 4100
3090 RETURN 
4000 REM "% of sales determined by gross profit table
4010 A=S-C0
4020 GOSUB 4100
4079 REM "Get rate R
4080 IF S<>0 THEN T=A*100/S; GOSUB 8000 ELSE R=0
4085 IF POS(C$(6,1)="-!")<>0 AND S-C0<0 THEN A=S-C0 ELSE A=S; REM "If using special 50% or 100% of gp on negative GP sales, then set amount to the gp amount so that the check comparing the sign of A to the sign of the commission will pass. Otherwise set to the sale amount
4086 IF POS(C$(6,1)="@")<>0 THEN IF S-C0<0 THEN A=0
4088 IF POS(C$(7,1)="#")>0 AND I0=2 AND S-C0>=0 THEN A=S-C0; REM "# In second position reverts 1st table entry to a GP calculation w/o house charge"
4090 RETURN 
4100 REM "CALC House Charge
4105 IF POS(C$(33,1)="123456789")>0 THEN GOSUB 8200; GOTO 4190
4110 IF C[0]=0 THEN GOTO 4190
4120 IF C$(33,1)="S" THEN A=A-S*C[0]/100; GOTO 4190
4130 IF C$(33,1)="F" THEN A=A-C[0]; GOTO 4190
4180 IF POS(C$(33,1)=" C")>0 THEN A=A-C0*C[0]/100
4190 REM "Calc Storage Charge
4210 IF C$(34,1)=" " THEN GOTO 4290
4215 REM PRINT 'CS',"X3$=",X3$,'LF',"A$=",A$,'LF',"B$=",B$,; INPUT *
4220 ! CALL "AR2EAZ",X3$,A$,B$,P0,C$
4240 A=A-P0
4290 RETURN 
5000 REM "I type means Gp% rounded to .x times GP
5010 A=S-C0
5020 IF S<>0 THEN T=A*100/S
5079 REM "Get rate R
5080 R=T/10*10
5085 IF ABS(R)<30 THEN R=SGN(R)*30 ELSE IF ABS(R)>40 THEN R=SGN(R)*40
5086 IF SGN(R)<0 THEN C=A*ABS(R)*.01; EXITTO 9900
5090 RETURN 
7500 REM "Get AR4 file device and read new numerics
7505 ! IF POS("7."=SYS)=0 THEN J9=100 ELSE J9=14
7510 ! FOR F0=1 TO J9
7520 ! Q$=FID(F0,ERR=7540); IF LEN(Q$)=2 THEN GOTO 7540 ELSE Q$=Q$(4,6)+Q$(21,2)
7530 ! IF POS("AR4"=Q$)=1 THEN EXITTO 7580
7540 ! NEXT F0
7545 F0=FN%FFN("AR4"+%C$); IF F0=-1 THEN F0=HFN; OPEN (F0,ERR=7550)"AR4"+$C0$ ELSE GOTO 7580 ! [205471]-changed FFN() to FN%FFN()
7550 ! X$="ARGCOM cannot find the file slot on which AR4 is open"
7560 MSGBOX MSG("AR4_NOT_OPEN"),MSG("ATTN"),"!"
7570 GOTO 9900
7580 IF F0$<>"*" THEN DIM D[6]; X$="Missing Commission Code "+C$(1,5); READ (F0,KEY=C$(1,5),DOM=7570)*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,*,D[0],D[1],D[2],D[3],D[4],D[5],D[6]
7590 RETURN 
7600 REM "Default Commission code
7601 REM "Mod cwj 4/27/98 SSP# 096774
7602 REM "Break C$ into P0$-P/O code, T$-filetype and D$-default comm coder from header or customer,C9$-Company code
7605 P0$=C$(1,1),T$=C$(2,1),C9$=C$(3,3); IF LEN(C$)>5 THEN D$=C$(6)
7607 IF P0$<>"*" THEN IF P0$<>" " AND POS(C9$="136148",3)=0 THEN T$="F"; REM "P0$='*'means came from line, don't mess with type, a non blank p/o code means use DIRect ship, unless it is company 136 which uses I substition even if DIRect ship.
7610 DIM C$(5); REM " return value in c$, default to blanks"
7612 FIND (F0,KEY=D$,DOM=7613); C$=D$; REM "SET COMM TO VALID DEFAULT"
7614 REM "Read A/R commission params
7615 READ (C,KEY=C9$+"AR1")P$; REM "Find appropriate param based on line type"
7619 IF P0$="!" THEN X$=P$(70,5); GOTO 7621; REM "If ! then use DIRect ship comm code
7620 I=POS(T$="FICNS"); IF I=0 THEN X$="" ELSE X$=P$(32+I*5,5)
7629 REM "IF C$ is blanks, no default supplied so get from params IF: 1) line type gets a param comm code, 2) the comm code is not blank nor a sub type param (with '#'s), 3) the param comm code is in the comm code file
7630 IF X$>"" AND POS(" "<>X$)>0 AND POS("#"=X$)=0 THEN FIND (F0,KEY=X$,DOM=7631); C$=X$; GOTO 7695; REM "We can leave, we're done
7639 REM "Do substitution if needed
7640 IF X$>"" AND POS("#"=X$)<>0 THEN C1$=""; FOR I0=1 TO 5; IF X$(I0,1)<>"#" THEN C1$=C1$+X$(I0,1); NEXT I0 ELSE C1$=C1$+C$(I0,1); NEXT I0
7644 REM "Check newly created C1$, if ok use it, else keep c$ the same
7645 FIND (F0,KEY=C1$,DOM=7646); C$=C1$
7695 GOTO 9900
8000 REM "Run value T though table to get R
8005 R=0
8010 FOR I0=2 TO 14 STEP 2
8015 IF C[I0]=0 OR C[I0]>ABS(T) THEN R=C[I0-1]; IF I0>2 THEN R=R+INT(ABS(T)-C[I0-2])*D[I0/2-2]; EXITTO 8030 ELSE EXITTO 8030
8020 NEXT I0
8025 R=C[15]+INT(ABS(T)-C[14])*D[6]
8040 RETURN 
8200 REM "Compute house charge
8210 Q2=0,Q3=0
8250 REM "Get House Charge Table
8260 IF LEN(C$)>=33 AND POS(C$(33,1)="1234567890")>0 THEN DIM Q[16]; X$="Missing House Charge Code "+C$(33,1); READ (F0,KEY="@"+C$(33,1)+"   ",DOM=7560)IOL=0310 ELSE GOTO 8290
8265 ON NUM(Q$(31,1),ERR=8235)-1 GOTO 8270,8350,8400,8450,8500
8270 REM "Cost Table
8275 Q1=C0; GOSUB 8300; Q2=Q2*C0*.01
8277 REM "Don't apply min/max by line if comm code is Inv Total Type
8278 IF C0$(1,2)="HC" AND C$(32,1)="I" THEN GOTO 8284
8280 IF Q[0]>0 AND Q2<Q[0] THEN Q2=Q[0] ELSE IF Q[16]>0 AND Q2>Q[16] THEN Q2=Q[16]
8284 IF C0$(1,2)="CC" THEN IF Q$(32,2)="YY" THEN GOTO 8290 ELSE Q2=0; GOTO 8290
8285 IF Q$(32,1)<>"Y" THEN A=A-Q2 ELSE IF Q$(33,1)<>"Y" THEN Q3=Q2,Q2=0 ELSE Q2=0
8290 RETURN 
8300 REM "Run value Q1 though table to get Q2
8305 Q2=0
8310 FOR I0=2 TO 14 STEP 2
8315 IF Q[I0]=0 OR Q[I0]>ABS(Q1) THEN Q2=Q[I0-1]; EXITTO 8330
8320 NEXT I0
8325 Q2=Q[15]
8340 RETURN 
8350 REM "House chg based on number of cartons
8360 Q1=0,Q1=NUM(C0$(3,4),ERR=8361)
8370 GOSUB 8300; Q2=Q1*Q2
8390 GOTO 8280
8400 REM "Based on line items
8410 IF POS(" "<>C0$(3,4))>0 THEN Q1=1
8420 GOSUB 8300; Q2=Q1*Q2
8440 GOTO 8280
8450 REM "Based on sales
8460 Q1=S; GOSUB 8300; Q2=Q2*S*.01
8490 GOTO 8280
8500 REM "Based on gross profit % table then charge % of sale amount
8505 A=S-C0
8507 IF S<>0 THEN Q1=A*100/S ELSE Q1=0; REM "was A/S*100 Caused rounding prob
8510 GOSUB 8300
8515 Q2=Q2*S*.01
8520 GOTO 8280
9000 REM "errors
9005 PRINT @(0,20),'CL',"ERR=",ERR," LINE=",TCB(5),; INPUT *
9010 EXIT ERR
9900 REM "End
9930 EXIT 
9999 END 
56000 REM + Modification History
56002 REM "205471-Oracle - FFN usage in tf2g progs. to be replaced by FN%FFN  
