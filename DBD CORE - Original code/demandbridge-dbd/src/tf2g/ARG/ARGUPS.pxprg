0010 ! CPGUPS - Credit Card Processing Selection
0015 ! Credit Card Processing Plus
0020 ! 
0030 ! ************************************************************************
0035 REM "5.7 - 08/05/14 - 16.057777 - jvv - SSP# 197952
0037 REM "197952-Streamline processing by user - Chained journal updates     
0040 REM "Copyright 2014 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0070 IF TCB(88)=0 THEN MSGBOX "You must be using Windx to access this program" ELSE PROCESS "CPGUPS","../ARG/AR.EN"
0080 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0081 ! EXIT
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 ! B_FLAG$=ARG_1$
0130 X0$="ARGUPS",X1$="Credit Card Processing Plus"
0140 X0=-1,X2=-1
0150 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2
0160 ! 
0170 DIM ZZS$(320),PRT$(125),RNG[4],B.RNG[4]
0180 ! iolist
0190 IOLIST ZZS$(1)
0200 IOLIST PRT$(1),RNG$,RNG{ALL}
0210 ! 
0220 ! Open Files
0230 DIM Z[NUM(X3$(60,3))]
0240 Z$="01O ZZS...  06O ZZPARM  07O ZZP     "
0250 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
0260 ! 
0262 GOSUB DO_VALIDATION; IF OK_TO_CONTINUE$="N" THEN CMD_STR$="END"; RETURN ! SSP 197952
0280 ! 
0281 P0$="AR2EAA" ! Batch lookup string for Invoice batches
0290 CALL "ZGBATC",X3$,X4$,P0$,X9; IF X9=1 THEN CMD_STR$="END"; EXIT 
0300 %NO_NEW_BATCH=1
0310 ! 
0320 GOSUB GET_PARM
0330 %ARB$="ARB"+X3$(174,4)
0340 ! 
0345 GOSUB CHECK_FOR_CCPLUS; IF OK_TO_CONTINUE$="N" THEN CMD_STR$="END"; RETURN ! SSP 197952
0350 RPT_SELEC$="            "; VIEW$=""
0360 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 KEY_1$="ARGUPS"+RPT_SELEC$; %K9$="AR2UPA"
0530 READ (Z[1],KEY=KEY_1$,DOM=ADD_OPT)IOL=0190,IOL=0200
0540 B_ZZS$=ZZS$; B_PRT$=PRT$; B_RNG$=RNG$; CHG_FLG=0 ! MSGBOX "PRT$ for key "+KEY_1$+" is :|"+PRT$+"|"
0580 GOSUB SELECT_OPTIONS
0585 NEXT_FOLDER=NEXTFOLDER_FLG; REFRESH_FLG=1
0590 RETURN 
0600 ! Selection Options
0610 SELECT_OPTIONS:
0620 PRG_NAME$=ZZS$(1,6); RPT_SELEC$=ZZS$(7,12); RPT_DESC$=ZZS$(19,40)
0630 RPT_OPT$=ZZS$(59,12); RPT_SEQ$=ZZS$(71,1)
0631 RPT_BILLADDR$=ZZS$(72,1) ! 218074
0640 ! 
0800 RPT_NUM1=NUM(ZZS$(303,3))
0810 PRT_MODE$=PRT$(1,1); RPT_OUTDEV$=PRT$(2,6) ! MSGBOX "SELECT_OPTIONS: PRT$(2,6)"+PRT$(2,6)
0820 RPT_VERT$=PRT$(9,1); RPT_HORIZ$=PRT$(10,1)
0830 RPT_AUX1$=PRT$(11,1); RPT_AUX2$=PRT$(12,1); RPT_AUX3$=PRT$(13,1); RPT_AUX4$=PRT$(14,1)
0840 RPT_LINES$=PRT$(15,2); RPT_FORMCODE$=PRT$(17,1)
0850 RPT_JOBCARD$=PRT$(65,60); RPT_JOBCARD_FLG$=PRT$(125,1)
0860 GOSUB LOAD_RANGE
0870 NEXT_ID=RPT_VEIW.CTL
0880 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0930 %NO_NEW_BATCH=0
0970 RETURN 
1000 ! load the sequence range
1010 LOAD_RANGE:
1495 REFRESH_FLG=1
1500 RETURN 
2000 ! Get Parameters
2010 GET_PARM:
2020 READ (Z[6],KEY=X3$(9,3)+"A/R",DOM=*RETURN)PP1$
2030 RETURN 
3800 CHECK_CHANGES:
3805 GOSUB LOAD_OPTIONS
3810 IF CHG_FLG=0 THEN RETURN 
3820 MSGBOX MSG("UPD_RPT_1")+SEP+MSG("UPD_RPT_2"),MSG("CONFIRM"),"?,YESNO",ANS$
3840 IF ANS$="NO" THEN RETURN 
3850 WRITE (Z[1],KEY=KEY_1$)IOL=0190,IOL=0200
3860 B_ZZS$=ZZS$; B_PRT$=PRT$; B_RNG$=RNG$; CHG_FLG=0
3870 RETURN 
8000 ! 
8010 ADD_OPT:
8020 MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"?,YesNo",ANS$
8030 IF ANS$="NO" THEN CMD_STR$="END"; RETURN 
8040 KEY_1$="ARGUPS"+RPT_SELEC$
8050 DIM ZZS$(320),RNG$(100),PRT$(126); ZZS$(1,18)=KEY_1$; ZZS$(71,1)="1"
8060 GOSUB SELECT_OPTIONS
8070 REFRESH_FLG=1; NEXT_ID=RPT_DESC.CTL
8090 RETURN 
9999 END 
10000 ! enable, disable buttons
10010 ENABLE_DISABLE_OPTIONS:
10020 IF PP1$(118,1)<>"y" THEN {
10030 CALL "*wingrp;hide",PARM1.GRP$
10040  } ELSE {
10050 CALL "*wingrp;show",PARM1.GRP$
10060 IF RPT_AGEINV$<>"Y" THEN CALL "*wingrp;hide",PARM3.GRP$ }
10070 REFRESH_FLG=1
10080 RETURN 
11000 ! show/hide buttons
11010 HIDE_SHOW_PARM2:
11020 IF NUL(RPT_OPT$) THEN RETURN 
11030 IF RPT_OPT$(1,1)="4" THEN {
11040 CALL "*wingrp;hide",PARM2.GRP$
11045 IF NOT(NUL(RPT_OPT$(2,1))) THEN READ (Z[6],KEY="FORMATIN"+RPT_OPT$(2,1),DOM=NO_FORMAT)
11050  } ELSE {
11060 CALL "*wingrp;show",PARM2.GRP$ }
11070 REFRESH_FLG=1
11080 RETURN 
12000 ! INVALID FORMAT CODE
12010 NO_FORMAT:
12020 MSGBOX MSG("INV_FF")+"!",MSG("WARNING"),"STOP"
12030 RPT_OPT$=""
12040 NEXT_ID=RPT_OPT.CTL
12080 REFRESH_FLG=1
12090 RETURN 
17000 ! 
20000 LOAD_OPTIONS:
20010 ! View Report Routine.
20020 ! 
20030 ZZS$(1,6)=PRG_NAME$
20040 ZZS$(7,12)=RPT_SELEC$; ZZS$(19,40)=RPT_DESC$
20050 ZZS$(59,12)=RPT_OPT$; ZZS$(71,1)=RPT_SEQ$
20051 ZZS$(72,1)=RPT_BILLADDR$ ! 218074
20100 ! 
20110 PRT$(1,1)=PRT_MODE$; PRT$(2,2)=RPT_OUTDEV$
20120 PRT$(9,1)=RPT_VERT$; PRT$(10,1)=RPT_HORIZ$
20130 PRT$(11,1)=RPT_AUX1$; PRT$(12,1)=RPT_AUX2$
20140 PRT$(13,1)=RPT_AUX3$; PRT$(14,1)=RPT_AUX4$
20150 PRT$(15,2)=RPT_LINES$; PRT$(17,1)=RPT_FORMCODE$
20160 PRT$(65,60)=RPT_JOBCARD$; PRT$(125,1)=RPT_JOBCARD_FLG$
20170 ! 
20175 RNG$=""
20180 ! 
20300 IF B_ZZS$=ZZS$ AND B_PRT$=PRT$ AND B_RNG$=RNG$ THEN CHG_FLG=0 ELSE CHG_FLG=1
20390 RETURN 
25000 ! 
25030 PRINT_REPORT:
25060 ! IF NOT(NUL(VIEW$)) THEN PRT$(2,2)=VIEW$
25090 ! Print Report Routine.
25152 K$=X3$(1,8); DIM P0$(128)
25154 IOLIST X3$,MM1$,X4$,D0$,XX$,AA9$
25156 %B_WINDATE_FMT$=%WINDATE_FMT$,%WINDATE_FMT$="YYYYMMDD",JJ$=DTE(0:"%Yl%Mz%Dz"); CALL "ZZWDTE;VALIDATE",JJ$,JJJ$,JJJJ$; X3$(21,6)=JJ$
25158 Q$="ARGUPS"; X3$(146,6)=Q$; CALL "ZZAUDT",X3$,Q$+P0$(78),Z[6],X9; X3$(185,1)="A",X3$(188,9)=P0$(23,9); WRITE (Z[6],KEY=X3$(1,8))IOL=25154
25180 WRITE (Z[7],KEY=K$)X3$,X4$,T1$,PRT$,RNG$,ZZS$
25210 ! 
25240 GOSUB LOAD_PROG
25270 ! PRINT 'DIALOGUE'(5,5,80,24,"Processing Credit Card Plus Invoices")
25300 CALL K9$,X3$,X4$,"ARGUPS",Q1$
25330 ! PRINT 'POP'
25340 CALL "ZZGCHN;PROCESS_RELATED_BATCHES",ERR=*NEXT,MID(X3$,174,4),"AR2EAA" ! 197952
25360 CMD_STR$="END"
25390 RETURN 
26000 ! Load the correct call program
26010 LOAD_PROG:
26120 K9$="AR2UPA"
26190 RETURN 
28000 ! 
35000 DO_VALIDATION:! SSP 197952
35005 OK_TO_CONTINUE$="N",OK_TYPE=0
35008 ! Check if either of the credit card modules -"CV", "CP" or "CC", has been activated
35009 CALL "ZZ2PRP","CV",CP_ENABLED$,""; IF CP_ENABLED$="Y" THEN GOTO 35020 ! SSP 229993
35010 CALL "ZZ2PRP","CP",CP_ENABLED$,""; IF CP_ENABLED$<>"Y" THEN CALL "ZZ2PRP","CC",CC_ENABLED$,""; IF CC_ENABLED$<>"Y" THEN RETURN 
35012 ! Check if processing has been turned on based on which modules are activated - "CP" has priority over "CC"
35020 DIM ARPARAM$(255); FIND (Z[6],KEY=X3$(9,3)+"A/R")ARPARAM$
35030 IF ARPARAM$(241,1)="Y" THEN CCMODE=1 ELSE IF ARPARAM$(242,1)="Y" THEN CCMODE=0 ELSE RETURN 
35080 OK_TYPE=1 ! Continue no errors or warnings
35090 OK_TO_CONTINUE$="Y"
35095 RETURN 
35099 ! 
35100 CHAINED_UPDATE:! SSP 197952
35105 ENTER X3$,X4$,Q0$,Q1$,OK_TO_CONTINUE$,OK_TYPE
35110 GOSUB INIT
35115 IF OK_TO_CONTINUE$<>"Y" THEN GOTO 35130
35120 GOSUB MAIN_POST_DISPLAY
35130 GOSUB WRAPUP
35195 EXIT 
35199 ! 
35200 CHECK_FOR_CCPLUS:
35205 ! Call AR2XCP to read thru ARB records in batch to find if there are any credit card invoices to process. User will then have option to bypass printing report if none are found.
35208 OK_TO_CONTINUE$="N",OK_TYPE=0
35210 ! if credit card module installed then check to see if batch is an open credit card batch in ZYW file, if so display message and leave.
35212 Z$="02O ZYW...  "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
35215 READ (Z[2],END=*NEXT)ZYW$; IF ZYW$(16,5)=X3$(174,4)+X3$(85,1) THEN MSGBOX MSG("ARGDBS_02")+SEP+MSG("ARGDBS_03"),MSG("FYI"),"!"; RETURN ELSE GOTO *SAME
35220 Z$="10OSARB...  "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
35230 CALL "AR2XCP",X3$,X4$,Z[10],CC0,CC1
35240 Z$="10C ARB...  "; CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
35250 IF CC1=0 THEN MSGBOX MSG("CCPLUS_02"),MSG("FYI"),"?,YESNO",_YESNO$; IF _YESNO$<>"YES" THEN OK_TYPE=2; RETURN 
35280 OK_TYPE=1 ! Continue no errors or warnings
35290 OK_TO_CONTINUE$="Y"
35295 RETURN 
35299 ! 
40030 EXIT 
56002 REM "218074-Changes to handle missing billing address, read from order 
56004 REM "270859-Credit card batch not showing in Credit Card Processing. Did
