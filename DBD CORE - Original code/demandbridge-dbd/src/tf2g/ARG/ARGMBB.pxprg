0010 ! ARGMBB - Salesperson constant Data
0035 REM "5.7 - 08/26/11 - 14.338888 - jdf - SSP# 248893
0037 REM "248893-Unable to get to the Lookup field under the AR salesperson  
0040 REM "Copyright 2011 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 _SLSMAN$=%SLSMAN$
0060 PROCESS "ARGMBB","../ARG/AR.EN"
0070 %SLSMAN$=_SLSMAN$
0080 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 ! 
0130 X0$="AP2MBB",X1$="SLSMAN Constant Data"
0140 X0=-1,X2=-1
0150 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2; IF X1>0 THEN CMD_STR$="END"; RETURN 
0160 ! 
0190 DIM Z[NUM(X3$(60,3))],AR3$(426),AR3[2],B_AR3[2],ASV[3]
0200 ! Files
0210 Z$="01X AR3...  02O GL1...  03O ZYB...  04O AP2...  05O AP3...  06O AP1...  06O ZZPARM  07O AP6...  08O AP7...  09O AP1...  10O AP0...  11O ASV...  12O APB...  15O ARF...  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; IF Z0>0 THEN CMD_STR$="END"; RETURN 
0300 ! IOList
0310 IOLIST AR3$
0380 IOLIST ASV$,ASV{ALL}
0400 ! Get system Parameters...
0410 GOSUB GET_PARAM
0430 ENABLE_FLG=0
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 IF %SLSMAN$<>"" THEN {
0530 SLSMAN$=%SLSMAN$
0540 DISABLE CONTROL SLSMAN.CTL
0545 ! GOSUB CHECK_PARAM
0547 CALL "*wingrp;hide",BUTTON_1.GRP$
0550 GOTO FIND_REC }
0560 NEXT_ID=SLSMAN.CTL
0570 ! GOSUB CHECK_PARAM
0790 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0930 %TEMP$=""
0970 RETURN 
1000 ! find SLSMAN record
1010 FIND_REC:
1015 IF NUL(SLSMAN$) THEN RETURN ELSE SLSMAN$=PAD(SLSMAN$,4)
1020 KEY_1$=SLSMAN$
1030 READ (Z[1],KEY=KEY_1$,DOM=NEW_SLSMAN)IOL=0310
1040 GOSUB LOAD_SCREEN; SAVE_LOOKUP$=SPER_LOOKUP$
1050 B_AR3$=AR3$
1080 CALL "*wingrp;Enable",BUTTONS.GRP$; CALL "*WINGRP;HIDE",BUTTON_1.GRP$; CALL "*WINGRP;ENABLE",FIELDS.GRP$; ENABLE_FLG=1
1090 RETURN 
1500 ! load SLSMAN screen
1510 LOAD_SCREEN:
1520 ! First Folder
1530 SPER_ADDR_1$=AR3$(40,30); SPER_ADDR_2$=AR3$(70,30)
1540 SPER_CITY$=AR3$(100,16); SPER_ST$=AR3$(116,2); SPER_ZIP_CODE$=AR3$(118,9)
1550 SPER_FAX$=AR3$(127,12); SPER_PHONE$=AR3$(189,14)
1560 SPER_LOOKUP$=AR3$(139,10); SPER_ABBREVIATED$=AR3$(149,20); SPER_GREETING$=AR3$(169,20)
1600 ! Second Folder
1605 IF NUL(AR3$(203,1)) THEN AR3$(203,1)="A"
1610 STATUS$=AR3$(203,1); DATE_SETUP$=AR3$(204,6); COMM_CODE$=AR3$(219,5)
1620 COMM_STMT_CODE$=AR3$(224,1); VENDOR$=AR3$(225,10)
1630 HOME_DIV$=AR3$(235,2)
1670 MAILING_LABEL_TYPE_1$=AR3$(210,3); MAILING_LABEL_TYPE_2$=AR3$(213,3); MAILING_LABEL_TYPE_3$=AR3$(216,3)
1680 MAILING_LABEL_TYPE_4$=AR3$(237,3); MAILING_LABEL_TYPE_5$=AR3$(240,3); MAILING_LABEL_TYPE_6$=AR3$(243,3)
1690 MAILING_LABEL_TYPE_7$=AR3$(246,3); MAILING_LABEL_TYPE_8$=AR3$(249,3); MAILING_LABEL_TYPE_9$=AR3$(252,3)
1695 ALT_DIV_PO_RFQ$=AR3$(255,2) ! WO238337
1780 REFRESH_FLG=1
1790 RETURN 
2000 ! Load the information
2010 LOAD_INFO:
2020 AR3$(1,4)=SLSMAN$; AR3$(5,35)=%SALES_NAME$
2030 AR3$(40,30)=SPER_ADDR_1$; AR3$(70,30)=SPER_ADDR_2$
2040 AR3$(100,16)=SPER_CITY$; AR3$(116,2)=SPER_ST$; AR3$(118,9)=SPER_ZIP_CODE$
2050 AR3$(127,12)=SPER_FAX$; AR3$(189,14)=SPER_PHONE$
2060 AR3$(139,10)=SPER_LOOKUP$; AR3$(149,20)=SPER_ABBREVIATED$; AR3$(169,20)=SPER_GREETING$
2100 ! Second Folder
2110 AR3$(203,1)=STATUS$; AR3$(204,6)=DATE_SETUP$; AR3$(219,5)=COMM_CODE$
2120 AR3$(224,1)=COMM_STMT_CODE$; AR3$(225,10)=VENDOR$
2130 AR3$(235,2)=HOME_DIV$
2170 AR3$(210,3)=MAILING_LABEL_TYPE_1$; AR3$(213,3)=MAILING_LABEL_TYPE_2$; AR3$(216,3)=MAILING_LABEL_TYPE_3$
2180 AR3$(237,3)=MAILING_LABEL_TYPE_4$; AR3$(240,3)=MAILING_LABEL_TYPE_5$; AR3$(243,3)=MAILING_LABEL_TYPE_6$
2190 AR3$(246,3)=MAILING_LABEL_TYPE_7$; AR3$(249,3)=MAILING_LABEL_TYPE_8$; AR3$(252,3)=MAILING_LABEL_TYPE_9$
2195 AR3$(255,2)=ALT_DIV_PO_RFQ$ ! WO238337
2290 RETURN 
3000 ! Write out the Record...
3010 WRITE_REC:
3020 REM MSGBOX MSG("UPDATE_REC"),MSG("CONFIRM"),"?,YESNO",ANS$ ! SSP248893 jdf
3030 REM IF ANS$="NO" THEN RETURN  ! SSP248893 jdf
3040 WRITE (Z[1],KEY=KEY_1$)IOL=0310
3080 B_AR3$=AR3$
3082 REMOVE (Z(15),KEY=SAVE_LOOKUP$+KEY_1$,DOM=*NEXT)
3085 WRITE (Z(15),KEY=AR3$(139,10)+KEY_1$)
3090 RETURN 
4000 ! Delete the Record
4010 DELETE_REC:
4020 MSGBOX MSG("DELETE_REC"),MSG("VERIFY"),"?,YesNo",ANS$
4030 IF ANS$="NO" THEN RETURN 
4040 REMOVE (Z[1],KEY=KEY_1$)
4190 RETURN 
5000 ! Check Changes
5010 CHECK_CHANGES:
5020 IF B_AR3$=AR3$ THEN RETURN 
5025 IF NUL(AR3$) THEN RETURN 
5030 MSGBOX MSG("REC_ALTERD"),MSG("UPDATE"),"?,YESNO",ANS$
5040 IF ANS$="NO" THEN CMD_STR$="END"; RETURN 
5060 GOSUB WRITE_REC
5090 RETURN 
5500 ! CLEAR FIELDS
5510 CLEAR_REC:
5520 IF NOT(NUL(%SLSMAN$)) THEN RETURN 
5525 SLSMAN$=""; %SALES_NAME$=""
5530 SPER_ADDR_1$=""; SPER_ADDR_2$=""
5540 SPER_CITY$=""; SPER_ST$=""; SPER_ZIP_CODE$=""
5550 SPER_FAX$=""; SPER_PHONE$=""
5560 SPER_LOOKUP$=""; SPER_ABBREVIATED$=""; SPER_GREETING$=""
5570 STATUS$=""; DATE_SETUP$=""; COMM_CODE$=""
5580 COMM_STMT_CODE$=""; VENDOR$=""
5590 HOME_DIV$=""
5600 MAILING_LABEL_TYPE_1$=""; MAILING_LABEL_TYPE_2$=""; MAILING_LABEL_TYPE_3$=""
5610 MAILING_LABEL_TYPE_4$=""; MAILING_LABEL_TYPE_5$=""; MAILING_LABEL_TYPE_6$=""
5620 MAILING_LABEL_TYPE_7$=""; MAILING_LABEL_TYPE_8$=""; MAILING_LABEL_TYPE_9$=""
5670 CALL "*wingrp;dISABLE",BUTTONS.GRP$; CALL "*WINGRP;DISABLE",FIELDS.GRP$; ENABLE_FLG=0
5680 NEXT_ID=SLSMAN.CTL; REFRESH_FLG=1
5690 RETURN 
8000 ! Setup New Information...
8010 NEW_SLSMAN:
8020 DIM AR3$(319)
8022 SAVE_LOOKUP$=""
8025 IF %NEW_SLSMAN THEN %NEW_SLSMAN=0; GOTO 8050
8030 MSGBOX MSG("ADD_RECORD"),MSG("VERIFY"),"?,YESNO",ANS$
8040 IF ANS$="NO" THEN SLSMAN$=""; NEXT_ID=SLSMAN.CTL; EXIT 
8050 NEXT_ID=%SALES_NAME.CTL
8060 STATUS$="A"; DATE_SETUP$=X3_TODAY$
8070 CALL "*wingrp;Enable",BUTTONS.GRP$; CALL "*WINGRP;HIDE",BUTTON_1.GRP$; CALL "*WINGRP;ENABLE",FIELDS.GRP$; ENABLE_FLG=1
8080 REFRESH_FLG=1
8090 RETURN 
8300 ! Mandatory Fields
8310 MANDATORY_FIELDS:
8320 IF NUL(TERMS_CODE$) THEN MSGBOX MSG("TERM_C_M")+SEP+MSG("CANN_U_R"),MSG("FYI"),"!"; RETURN 
8330 IF NUL(SPER_CATEGORY$) THEN MSGBOX MSG("SPER_C_M")+SEP+MSG("CANN_U_R"),MSG("FYI"),"!"; RETURN 
8390 RETURN 
9000 ! Get all system parameters....
9010 GET_PARAM:
9020 DIM SEC$(255); FIND (Z[6],KEY="osec"+X3$(40,3),DOM=*NEXT)SEC$
9030 FIND (Z[6],KEY=X3$(9,3)+"A/R")AR_PARM$
9080 REFRESH_FLG=1
9090 RETURN 
9100 ! check parameters
9110 CHECK_PARAM:
9120 IF POS(X3$(9,2)="500538118130",3)>0 THEN CALL "*wingrp;Show",FILE_OUT.GRP$
9130 IF F0$(16,1)<>"Y" THEN CALL "*wingrp;Hide",BANK_C.GRP$
9140 IF F0$(66,1)<>"Y" THEN CALL "*wingrp;Hide",DISC.GRP$
9150 IF F0$(78,1)<>"Y" THEN CALL "*wingrp;Hide",INTER.GRP$
9190 RETURN 
9999 END 
21040 ! 
21100 LOAD_GRID:! ^100,5 - Load user defined fields and SLSMAN data
21105 _CHANGE_FLG=CHANGE_FLG
21110 GRID CLEAR USER_GRID.CTL,0,0; _ROW=0; DIM ROW_ARRAY[50]
21115 GRID_ROWS=NUM(FN%FIN$(Z[11],"NUMREC"))
21120 IF NOT(GRID_ROWS) THEN {
21125 NEXT_FOLDER=FLDR.APGMAB.1.CTL,REFRESH_FLG=1,CHANGE_FLG=0
21130 RETURN 
21135  }
21140 USER_GRID.CTL'COLUMN=-1,USER_GRID.CTL'COLUMNWIDTH=0
21145 USER_GRID.CTL'ROW=-1,USER_GRID.CTL'ROWHEIGHT=0
21150 USER_GRID.CTL'ROWSHIGH=GRID_ROWS
21155 USER_GRID.CTL'COLUMNSWIDE=2
21160 GRID LOCK USER_GRID.CTL,1,0
21165 USER_GRID.CTL'COLUMN=2
21170 USER_GRID.CTL'COLUMNWIDTH=51
21175 USER_GRID.CTL'COLUMN=1
21180 USER_GRID.CTL'COLUMNWIDTH=25
21183 READ (Z[11],IND=0,DOM=*NEXT)
21185 ROW=IND(Z[11],END=21250); READ (Z[11],IND=ROW)ASV$
21190 USER_GRID.CTL'COLUMN=1
21197 IF ASV$(1,1)<>"V" THEN GOTO 21185
21198 _ROW+=1; ROW_ARRAY[_ROW]=ROW
21200 USER_GRID.CTL'ROW=_ROW,USER_GRID.CTL'VALUE$=STP(ASV$(6,75),2)+":",USER_GRID.CTL'CELLTYPE$="ellipsis",USER_GRID.CTL'ALIGN$="Right"
21205 APB$=""
21210 READ (Z[12],KEY=SLSMAN$+ASV$(2,4),DOM=21245)APB$
21220 IF STP(APB$,2)<>"" THEN FIELD_DATA$=APB$(15,75)
21225 IF STP(FIELD_DATA$,2)<>"" THEN {
21230 USER_GRID.CTL'COLUMN=2
21235 USER_GRID.CTL'VALUE$=STP(FIELD_DATA$,2),USER_GRID.CTL'CELLTYPE$="ellipsis"
21240  }
21245 GOTO 21185
21250 USER_GRID.CTL'ROWSHIGH=_ROW
21255 RETURN 
21260 ! 
21300 UPDATE_FIELD:! ^100,5 - User defined field has been accessed
21305 EDIT_ROW=USER_GRID.CTL'CURRENTROW
21310 GRID FIND USER_GRID.CTL,2,EDIT_ROW,FIELD_DATA$
21315 READ (Z[11],IND=ROW_ARRAY[EDIT_ROW])IOL=0380
21317 FIELD_LENGTH=ASV[0]
21320 IF LEN(FIELD_DATA$)>FIELD_LENGTH THEN {
21325 MSGBOX MSG("MAX_LEN",STR(FIELD_LENGTH)),MSG("ATTN"),"!"
21330 FIELD_DATA$=PAD(FIELD_DATA$,FIELD_LENGTH)
21335 USER_GRID.CTL'COLUMN=2
21340 USER_GRID.CTL'ROW=EDIT_ROW
21345 USER_GRID.CTL'VALUE$=FIELD_DATA$
21350  }
21352 DIM APB$(126); APB$(1,10)=SLSMAN$; APB$(11,4)=ASV$(2,4); APB$(15,75)=FIELD_DATA$
21355 WRITE (Z[12],KEY=SLSMAN$+ASV$(2,4))APB$
21360 REFRESH_FLG=1,NEXT_ID=USER_GRID.CTL
21362 TAB_GRID: EDIT_ROW=USER_GRID.CTL'CURRENTROW
21363 EDIT_ROW+=1
21364 IF EDIT_ROW>_ROW THEN %GRID=0 ELSE GRID GOTO USER_GRID.CTL,2,EDIT_ROW
21365 RETURN 
21370 ! 
21400 LOOKUP_ZIP:! Lookup zipcode, set city & state if found, set NEW_ZIP for later use
21405 NEW_ZIP=0
21410 CALL "ZYGMBA;CHECK_ZIP",SPER_ZIP_CODE$,ZIP_FOUND,ZIP_CITY$,ZIP_ST$,"",ZIP_AREA_CODE$,"" ! SSP247904 jdf
21415 IF LEN(STP(SPER_PHONE$))<7 THEN SPER_PHONE$=DIM(14)
21420 IF ZIP_FOUND THEN SPER_CITY$=ZIP_CITY$,SPER_ST$=ZIP_ST$; IF STP(MID(SPER_PHONE$,1,3))="" THEN SPER_PHONE$(1,3)=ZIP_AREA_CODE$; END_IF ; IF LEN(SPER_FAX$)=0 THEN SPER_FAX$=DIM(3); END_IF ; IF STP(MID(SPER_FAX$,1,3))="" THEN SPER_FAX$(1,3)=ZIP_AREA_CODE$; END_IF ; REFRESH_FLG=1,NEXT_ID=SPER_PHONE.CTL ELSE NEW_ZIP=1 ! SSP247904 jdf
21495 RETURN 
21499 ! 
21500 UPDATE_ZIP:! If new_zip then update the zip code table
21510 IF NEW_ZIP THEN CALL "ZYGMBA;UPDATE_ZIP",SPER_ZIP_CODE$,SPER_CITY$,SPER_ST$,"",SPER_PHONE$(1,3),SPER_CITY$ ! get area code from phone,long city from regular city  ! SSP247904 jdf    
21520 NEW_ZIP=0
21595 RETURN 
21599 ! 
21600 ALT_DIV_PO_RFQ:! WO238337
21610 IF STP(ALT_DIV_PO_RFQ$,3)="" THEN GOTO *RETURN
21620 IF FN%NMV(ALT_DIV_PO_RFQ$)=1 THEN ALT_DIV_PO_RFQ$=STR(NUM(ALT_DIV_PO_RFQ$):"00")
21630 REFRESH_FLG=1
21645 RETURN 
21649 ! 
56002 REM "205460-Oracle - FIN(CHANNEL,"NUMREC") changed to use FN%FIN$()     
56004 REM "238337-AR3 - alternate division for PO/RFQ printing certain flds
56006 REM "247904-Area code disappears when zip code/tax code is updated...?? 
56008 REM "248893-Unable to get to the Lookup field under the AR salesperson  
