0010 ! ARGRAS - A/R Open Invoice
0035 REM "5.7 - 03/28/11 - 10.449444 - jvv - SSP# 240570
0037 REM "240570-GUI Printing - Support for queueing and scheduling reports. 
0040 REM "Copyright 2011 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 PROCESS "ARGRAS","../ARG/AR.EN"
0060 EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 _SV_KR=PRM('KR'); SET_PARAM 'KR'=0 ! Ensure native handling of KEP()
0130 X0=-1,X2=-1
0140 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,X0,X1,X2
0150 ! 
0155 DIM ZZS$(96),PRT$(125),RNG[4],B.RNG[4] ! SSP#029715
0160 ! iolist
0170 IOLIST ZZS$(1)
0180 IOLIST PRT$(1),RNG$,RNG{ALL}
0300 ! Open Files
0310 DIM Z[NUM(X3$(60,3))]
0320 Z$="01O ZZS...  06O ZZPARM  07O ZZP     "
0330 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1
0400 ! 
0410 RPT_SELEC$="            "; VIEW$=""
0420 READ (Z[6],KEY=X3$(9,3)+"A/R")PARAM$
0430 BAL_TYPE$="All Invoices including Futures"+"/"
0440 BAL_TYPE$+="Only "+STP(PARAM$(48,8))+" & Older Invoices"+"/"; BAL_TYPE$+="Only "+STP(PARAM$(56,8))+" & Older Invoices"+"/"
0450 BAL_TYPE$+="Only "+STP(PARAM$(64,8))+" & Older Invoices"+"/"; BAL_TYPE$+="Only "+STP(PARAM$(72,8))+" & Older Invoices"+"/"
0460 BAL_TYPE$+="Only "+STP(PARAM$(80,8))+" & Older Invoices"+"/"
0470 BAL_TYPE_VAL$="123456"
0480 GOSUB GET_PARAM
0485 PRT_MODES$="QS" ! SSP 240570
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0520 KEY_1$="AR2RAA"+RPT_SELEC$; %K9$="AR2RAA"
0530 READ (Z[1],KEY=KEY_1$,DOM=ADD_OPT)IOL=0170,IOL=0180
0570 B_ZZS$=ZZS$; B_PRT$=PRT$; B_RNG$=RNG$; CHG_FLG=0
0580 GOSUB SELECT_OPTIONS
0585 IF P0$(130,1)="Y" THEN CALL "*wingrp;show",PRIN_CC.GRP$
0590 RETURN 
0600 ! selection Options
0610 SELECT_OPTIONS:
0630 PRG_NAME$=ZZS$(1,6); RPT_SELEC$=ZZS$(7,12); RPT_DESC$=ZZS$(19,40)
0640 RPT_OPT$=ZZS$(59,12); RPT_SEQ$=ZZS$(71,1)
0650 RPT_AGEDATE$=ZZS$(72,6); RPT_AGEINV$=ZZS$(78,1); RPT_REPTYPE$=ZZS$(79,1)
0660 RPT_ZEROBAL$=ZZS$(80,1); RPT_POSTAGE$=ZZS$(81,1); RPT_NAMEADD$=ZZS$(82,1)
0670 RPT_INVRPT$=ZZS$(83,1); RPT_FISYR$=ZZS$(84,4)
0680 RPT_PER$=ZZS$(88,2); RPT_CCINV$=ZZS$(90,1),RPT_CREDSTAT$=ZZS$(91,1) ! SSP# 029715
0700 IF RPT_INVRPT$="1" THEN CALL "*WINGRP;ENABLE",CUST_MAST.GRP$ ELSE CALL "*WINGRP;DISABLE",CUST_MAST.GRP$,RPT_POSTAGE$="N"
0800 ! Output Options
0810 PRT_MODE$=PRT$(1,1); RPT_OUTDEV$=PRT$(2,6)
0820 RPT_VERT$=PRT$(9,1); RPT_HORIZ$=PRT$(10,1)
0830 RPT_AUX1$=PRT$(11,1); RPT_AUX2$=PRT$(12,1); RPT_AUX3$=PRT$(13,1); RPT_AUX4$=PRT$(14,1)
0840 RPT_LINES$=PRT$(15,2); RPT_FORMCODE$=PRT$(17,1)
0850 RPT_JOBCARD$=PRT$(65,60); RPT_JOBCARD_FLG$=PRT$(125,1)
0860 GOSUB LOAD_RANGE
0870 NEXT_ID=RPT_VEIW.CTL
0880 REFRESH_FLG=1
0890 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0960 SET_PARAM 'KR'=_SV_KR
0970 EXIT 
1000 ! load the sequence range
1010 LOAD_RANGE:
1015 IF NUL(RNG$) THEN CUST_START$=""; CUST_END$=""; NEXTFOLDER_FLG=FLDR.AR1.1.CTL
1020 SWITCH RPT_SEQ$
1030 CASE "1"
1040 IF NUL(RNG$(1,10)) THEN CUST_START$="" ELSE CUST_START$=RNG$(1,10)
1050 IF RNG$(11,3)="~~~" THEN CUST_END$="" ELSE CUST_END$=RNG$(11,10)
1055 NEXTFOLDER_FLG=FLDR.AR1.1.CTL
1058 RETURN 
1060 CASE "2"
1070 IF NUL(RNG$(1,4)) THEN SLS_START$="" ELSE SLS_START$=RNG$(1,4)
1080 IF RNG$(5,3)="~~~" THEN SLS_END$="" ELSE SLS_END$=RNG$(5,4)
1083 IF NUL(RNG$(9,10)) THEN CUST_START$="" ELSE CUST_START$=RNG$(9,10)
1086 IF RNG$(19,3)="~~~" THEN CUST_END$="" ELSE CUST_END$=RNG$(19,10)
1090 NEXTFOLDER_FLG=FLDR.AR1.2.CTL
1095 RETURN 
1100 CASE "3"
1102 RNG$=PAD(RNG$,55)
1103 IF NUL(RNG$(1,10)) THEN CUST_START$="" ELSE CUST_START$=RNG$(1,10)
1106 IF RNG$(11,3)="~~~" THEN CUST_END$="" ELSE CUST_END$=RNG$(11,10)
1110 IF NUL(RNG$(21,9)) THEN CAT_START$="" ELSE CAT_START$=RNG$(21,9)
1120 IF RNG$(30,3)="~~~" THEN CAT_END$="" ELSE CAT_END$=RNG$(30,9)
1130 NEXTFOLDER_FLG=FLDR.AR1.3.CTL
1135 RETURN 
1140 CASE "4"
1150 IF NUL(RNG$(1,10)) THEN CUST_START$="" ELSE CUST_START$=RNG$(1,10)
1160 IF RNG$(11,3)="~~~" THEN CUST_END$="" ELSE CUST_END$=RNG$(11,10)
1170 IF NUL(RNG$(21,4)) THEN SHIPTO_START$="" ELSE SHIPTO_START$=RNG$(21,4)
1180 IF RNG$(25,3)="~~~" THEN SHIPTO_END$="" ELSE SHIPTO_END$=RNG$(25,4)
1190 NEXTFOLDER_FLG=FLDR.AR1.4.CTL
1200 RETURN 
1300 CASE "5"
1310 IF NUL(RNG$(1,4)) THEN SLS_START$="" ELSE SLS_START$=RNG$(1,4)
1320 IF RNG$(5,3)="~~~" THEN SLS_END$="" ELSE SLS_END$=RNG$(5,4)
1330 IF NUL(RNG$(9,10)) THEN CUST_START$="" ELSE CUST_START$=RNG$(9,10)
1340 IF RNG$(19,3)="~~~" THEN CUST_END$="" ELSE CUST_END$=RNG$(19,10)
1350 IF NUL(RNG$(29,8)) THEN SB_START$="" ELSE SB_START$=RNG$(29,8)
1360 IF RNG$(37,3)="~~~" THEN SB_END$="" ELSE SB_END$=RNG$(37,8)
1380 NEXTFOLDER_FLG=FLDR.AR1.20.CTL
1390 RETURN 
1490 END SWITCH 
1500 RETURN 
3800 CHECK_CHANGES:
3805 GOSUB LOAD_OPTIONS
3810 IF CHG_FLG=0 THEN RETURN 
3820 MSGBOX MSG("UPD_RPT_1")+SEP+MSG("UPD_RPT_2"),MSG("CONFIRM"),"?,YESNO",ANS$
3840 IF ANS$="NO" THEN RETURN 
3845 RNG$=PAD(RNG$,55)
3850 WRITE (Z[1],KEY=KEY_1$)IOL=0170,IOL=0180
3860 B_ZZS$=ZZS$; B_PRT$=PRT$; B_RNG$=RNG$; CHG_FLG=0
3870 RETURN 
3880 ! 
8000 ! Messages
8010 ADD_OPT:
8020 MSGBOX MSG("ADD_RECORD"),MSG("CONFIRM"),"?,YesNo",ANS$
8030 IF ANS$="NO" THEN CMD_STR$="END"; RETURN 
8040 KEY_1$="AR2RAA"+RPT_SELEC$
8050 DIM ZZS$(96),RNG$(100),PRT$(126); ZZS$(1,18)=KEY_1$,ZZS$(71,1)="1",ZZS$(72,6)=X3$(21,6),ZZS$(78,6)="DIYNN1",RNG$(11,10)=DIM(10,"~"),SB_START$="",SB_END$="",SLS_START$="",SLS_END$="" ! SSP#029715
8060 GOSUB SELECT_OPTIONS
8070 REFRESH_FLG=1; NEXT_ID=RPT_DESC.CTL
8090 RETURN 
9999 END 
17000 ! 
20000 LOAD_OPTIONS:
20010 ! View Report Routine.
20020 ! 
20030 ZZS$(1,6)=PRG_NAME$
20040 ZZS$(7,12)=RPT_SELEC$; ZZS$(19,40)=RPT_DESC$
20050 ZZS$(59,12)=RPT_OPT$; ZZS$(71,1)=RPT_SEQ$
20060 ZZS$(72,6)=RPT_AGEDATE$; ZZS$(78,1)=RPT_AGEINV$
20070 ZZS$(79,1)=RPT_REPTYPE$; ZZS$(80,1)=RPT_ZEROBAL$; ZZS$(81,1)=RPT_POSTAGE$
20080 ZZS$(82,1)=RPT_NAMEADD$; ZZS$(83,1)=RPT_INVRPT$,ZZS$(84,4)=RPT_FISYR$
20090 ZZS$(88,2)=RPT_PER$; ZZS$(90,1)=RPT_CCINV$,ZZS$(91,1)=RPT_CREDSTAT$ ! SSP# 029715
20100 ! 
20110 PRT$(1,1)=PRT_MODE$; PRT$(2,2)=RPT_OUTDEV$
20120 PRT$(9,1)=RPT_VERT$; PRT$(10,1)=RPT_HORIZ$
20130 PRT$(11,1)=RPT_AUX1$; PRT$(12,1)=RPT_AUX2$
20140 PRT$(13,1)=RPT_AUX3$; PRT$(14,1)=RPT_AUX4$
20150 PRT$(15,2)=RPT_LINES$; PRT$(17,1)=RPT_FORMCODE$
20160 PRT$(65,60)=RPT_JOBCARD$; PRT$(125,1)=RPT_JOBCARD_FLG$
20170 ! 
20175 RNG$=""
20180 ! 
20190 IF RPT_SEQ$="1" THEN {
20200 T1$="AR1..."
20210 GOSUB GET_CUST_RANGE }
20220 IF RPT_SEQ$="2" THEN {
20230 T1$="ARU..."
20240 GOSUB GET_SLSMAN_RANGE
20250 GOSUB GET_CUST_RANGE }
20260 IF RPT_SEQ$="3" THEN {
20270 T1$="ARY..."; GOSUB GET_CUST_RANGE; GOSUB GET_CAT_RANGE }
20280 IF RPT_SEQ$="4" THEN {
20290 T1$="ASJ..."; GOSUB GET_CUST_RANGE; GOSUB GET_SHIPTO_RANGE }
20300 IF RPT_SEQ$="5" THEN {
20310 T1$="ASZ..."
20320 GOSUB GET_SLSMAN_RANGE; GOSUB GET_CUST_RANGE; GOSUB GET_SB_RANGE }
20380 IF B_ZZS$=ZZS$ AND B_PRT$=PRT$ AND B_RNG$=RNG$ THEN CHG_FLG=0 ELSE CHG_FLG=1
20390 RETURN 
25000 ! 
25010 PRINT_REPORT:
25020 IF NOT(NUL(VIEW$)) THEN PRT$(2,2)=VIEW$
25030 K$=X3$(1,8)
25040 %B_WINDATE_FMT$=%WINDATE_FMT$,%WINDATE_FMT$="YYYYMMDD",JJ$=DTE(0:"%Yl%Mz%Dz"); CALL "ZZWDTE;VALIDATE",JJ$,JJJ$,JJJJ$; X3$(21,6)=JJ$
25045 RNG$=PAD(RNG$,55)
25048 IF POS(PRT_MODE$="QS")>0 THEN GOSUB REPORT_PRINT_MODE; CMD_STR$="END"; RETURN ! SSP 240570
25050 WRITE (Z[7],KEY=K$)X3$,X4$,T1$,PRT$,RNG$,ZZS$
25060 IF RPT_SEQ$<>"5" THEN PRG_TO_CALL$="AR2RAA" ELSE PRG_TO_CALL$="AR2RAB" ! SSP 228228
25065 IF RB_PRT$>"0" THEN CALL "ZZ2DRV",PRG_TO_CALL$ ELSE CALL PRG_TO_CALL$ ! SSP 228228
25070 CMD_STR$="END"
25080 RETURN 
27010 GET_PARAM:
27020 READ (Z[6],KEY=X3$(9,3)+"A/R")P0$
27090 RETURN 
28000 ! 
28010 GET_CUST_RANGE:
28020 IF NUL(CUST_START$) THEN RNG$+="          " ELSE RNG$+=PAD(CUST_START$,10," ") ! SSP 196358
28030 IF NUL(CUST_END$) THEN RNG$=RNG$+"~~~~~~~~~~" ELSE RNG$=RNG$+PAD(CUST_END$,10," ") ! SSP 196358
28040 RETURN 
28050 ! 
28060 GET_SLSMAN_RANGE:
28070 IF NUL(SLS_START$) THEN RNG$+="    " ELSE RNG$+=SLS_START$
28080 IF NUL(SLS_END$) THEN RNG$+="~~~~" ELSE RNG$+=SLS_END$
28090 RETURN 
28100 ! 
28110 GET_CAT_RANGE:
28120 IF NUL(CAT_START$) THEN RNG$+="         " ELSE RNG$=RNG$+CAT_START$
28130 IF NUL(CAT_END$) THEN RNG$=RNG$+"~~~~~~~~~" ELSE RNG$=RNG$+CAT_END$
28140 RETURN 
28150 ! 
28160 GET_SHIPTO_RANGE:
28170 IF NUL(SHIPTO_START$) THEN RNG$=RNG$+"    " ELSE RNG$=RNG$+SHIPTO_START$
28180 IF NUL(SHIPTO_END$) THEN RNG$=RNG$+"~~~~" ELSE RNG$=RNG$+SHIPTO_END$
28190 RETURN 
28200 ! 
28210 GET_SB_RANGE:
28220 IF NUL(SB_START$) THEN RNG$+=DIM(8) ELSE RNG$+=SB_START$
28230 IF NUL(SB_END$) THEN RNG$+="~~~~~~~~" ELSE RNG$+=SB_END$
28240 RETURN 
28250 ! 
30180 LOAD_FISCAL_YEAR:
30190 ZZPARM=HFN
30200 OPEN (ZZPARM)"ZZPARM"
30210 READ (ZZPARM,KEY=%C$+"A/R",DOM=*NEXT)GL_REC$; CR_YEAR$=GL_REC$(7,4),CR_PD$=GL_REC$(11,2)
30215 IF NUL(RPT_FISYR$)=0 THEN CR_YEAR$=RPT_FISYR$
30220 KE$=%C$+"G/LYE",RPT_FISYR$="    "+"/"
30230 READ (ZZPARM,KEY=KE$,DOM=*NEXT)
30240 K$=KEY(ZZPARM,END=30280); READ (ZZPARM)Z9$
30250 IF LEN(K$)<8 OR KE$<>K$(1,8) THEN GOTO END_LOAD_FISCAL_YEAR
30260 RPT_FISYR$=RPT_FISYR$+K$(9)+"/"
30265 IF K$(9,4)=CR_YEAR$ THEN CC$=Z9$(21)
30270 GOTO 30240
30280 END_LOAD_FISCAL_YEAR:
30290 DROP_BOX LOAD RPT_FISYR.CTL,RPT_FISYR$
30300 DROP_BOX WRITE RPT_FISYR.CTL,CR_YEAR$
30310 DROP_BOX LOAD END_FISCAL.CTL,RPT_FISYR$
30320 DROP_BOX WRITE END_FISCAL.CTL,CR_YEAR$
30321 RPT_FISYR$=CR_YEAR$
30322 IF NUL(RPT_PER$)=0 THEN CR_PD$=RPT_PER$
30325 RPT_PER$="  "+"/"
30330 FOR ACCT=1 TO 12; RPT_PER$=RPT_PER$+STR(ACCT:"00")+"/"; NEXT ACCT
30340 DROP_BOX LOAD RPT_PER.CTL,RPT_PER$
30350 DROP_BOX WRITE RPT_PER.CTL,CR_PD$
30355 RPT_PER$=CR_PD$
30360 END_ACCTPD$=RPT_PER$
30370 DROP_BOX LOAD END_ACCTPD.CTL,RPT_PER$
30380 DROP_BOX WRITE END_ACCTPD.CTL,CR_PD$
30390 RETURN 
32000 LOAD_ENDING_DATE:
32005 IF LEN(RPT_FISYR$)>4 THEN VAL$=CR_YEAR$ ELSE VAL$=RPT_FISYR$
32010 FIND (ZZPARM,KEY=KE$+VAL$)Z9$; CC$=Z9$(21)
32011 IF LEN(RPT_PER$)>2 THEN VAL=NUM(CR_PD$) ELSE VAL=NUM(RPT_PER$)
32020 CC$=CC$(VAL*6-5,6),RPT_AGEDATE$=CC$,REFRESH_FLG=1
32030 RETURN 
40000 SEL_QRY:ENTER PRIME_KEY$
40010 IF NUL(MID(PRIME_KEY$,7,6)) THEN EXIT 123
40020 EXIT 
45000 ! 
45005 REPORT_PRINT_MODE:
45010 RPT_PGM$="" ! Report Print Program Name
45015 RPT_SPLIT$="" ! Separate File Option
45020 DIM RPT_OPTIONS$[10,20]
45030 ! Set variable names X3$,X4$,T1$,PRT$,RNG$,ZZS$ if required
45060 IF RPT_SEQ$<>"5" THEN RPT_PGM$="AR2RAA" ELSE RPT_PGM$="AR2RAB"
45070 IF NOT(NUL(RB_PRT$)) AND RB_PRT$>"0" THEN RPT_SPLIT$="Y"
45075 GOSUB SCHD_DATE_VARS
45080 PERFORM "ZGXOPO;PROCESS_PRINT_MODE"
45090 RETURN 
45099 ! 
46000 SCHD_DATE_VARS:
46010 RPT_OPTIONS_COUNT=3 ! Maximum 9 allowed
46015 RPT_OPTIONS$[1,0]=MSG("AGE_INV")
46020 RPT_OPTIONS$[1,1]="6"
46025 RPT_OPTIONS$[1,2]="72"
46030 RPT_OPTIONS$[1,3]="ZZS"
46032 RPT_OPTIONS$[1,4]=""
46035 RPT_OPTIONS$[2,0]=MSG("FISC_Y")
46040 RPT_OPTIONS$[2,1]="4"
46045 RPT_OPTIONS$[2,2]="84"
46050 RPT_OPTIONS$[2,3]="ZZS"
46052 RPT_OPTIONS$[2,4]="AR"
46055 RPT_OPTIONS$[3,0]=MSG("ACCTPD")
46060 RPT_OPTIONS$[3,1]="2"
46065 RPT_OPTIONS$[3,2]="88"
46070 RPT_OPTIONS$[3,3]="ZZS"
46072 RPT_OPTIONS$[3,4]="AR"
46095 RETURN 
46099 ! 
56000 ! "196358-Restricted users by division in security by operator. No one
56001 ! "       can print AR Open Invoice Report except for Richard."
56002 ! "228228-Ability to split a report into multiple PDFs based on sales 
56003 REM "029715-EH: Would like the message CREDIT HOLD to appear on the     
