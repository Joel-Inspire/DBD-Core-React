0010 ! Selling Price Change Utility - Multi Files<ICGUTP>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 01/06/17 - 10.484166 - tma - SSP# 288700
0037 REM "288700-MHC:  I'm trying to update sales price on a range of items  
0040 REM "Copyright 2017 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0052 IF TCB(88)=0 THEN {
0055 MSGBOX "You must be using Windx to access this program"
0057  } ELSE {
0058 IF %GUI THEN BB$=FIN(0) ! SSP 197975
0060 PROCESS "ICGUTP","../ICG/IC.EN"
0061 IF %GUI THEN PRINT 'GOTO'(DEC(BB$(10,1))), ! SSP 197975
0065  }
0075 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0095 INIT:
0100 SETERR 9000
0110 X0$="ICGUTP",X1$="Multi File Selling Price Change Utility "
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1,T0$="ZZXIC0",L0=10
0200 REM "
0240 IF X3$="" THEN CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19],B[20],B[21],B[22],B[23],B[24],B[25],B[26],B[27],B[28],B[29],B[30],B[31],B[32],B[33],B[34],B[35],B[36],B[37],B[38],B[39],B[40]
0321 IOLIST B$,STR(B[0]),B[1],STR(B[2]),B[3],STR(B[4]),B[5],STR(B[6]),B[7],STR(B[8]),B[9],STR(B[10]),B[11],STR(B[12]),STR(B[13]),B[14],B[15],B[16],STR(B[17]),STR(B[18]),B[19],STR(B[20]),B[21],STR(B[22]),B[23],STR(B[24]),B[25],STR(B[26]),B[27],STR(B[28]),B[29],STR(B[30]),B[31],STR(B[32]),B[33],B[34],B[35],B[36],B[37],B[38],B[39],B[40]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O IC0...  03O FM1...  04O ICF...  05O FS2...  06O FS1...  13O ZZPARM  "
0515 IC0=Z[2],FM1=Z[3],ICF=Z[4],FS2=Z[5]
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0525 IC0=Z[2],FM1=Z[3],ICF=Z[4],FS2=Z[5],FS1=Z[6]
0550 DIM M6[1]; CALL "IC2PRE",X3$,Z[13],"",M6{ALL},"",0,"",0
0600 REM "
0605 DIM A$(1),A[1]
0610 GOSUB 6000
0620 GOSUB 8100
0630 RETURN 
0640 CALL T0$,ERR=0641,X3$,X4$,Y0$,W$,W{ALL},0,C0,L9
0660 GOSUB 2350
0661 PROCEED:DIM W$(41),A(1); A$=DB_OPTION$; PRECISION 6; A(0)=ML_AMOUNT; PRECISION M6[1] ! SSP#288700
0662 ! IF ML_CUST_END$="" THEN ML_CUST_END$="~~~~~~~~~~" END_IF ; W$(21,10)=ML_CUST_END$
0663 IF ML_CUST_END$="" THEN ML_CUST_END$=DIM(10) END_IF ; W$(21,10)=ML_CUST_END$
0665 IF ML_ITEM_END$="" THEN ML_ITEM_END$="~~~~~~~~~~" END_IF ; W$(31,10)=ML_ITEM_END$
0667 IF ML_CUST_START$<>"" THEN W$(1,10)=ML_CUST_START$
0669 IF ML_ITEM_START$<>"" THEN W$(11,10)=ML_ITEM_START$
0670 IF POS(" "<>W$(21,10))>0 THEN MSGBOX MSG("UTG_ICP"),"** Warning **","?,YESNO",OPT$; IF OPT$="NO" THEN RETURN 
0890 PRINT 'DIALOGUE'(5,5,80,27,MSG("ICGUTG"),OPT="c"),'CS'
0900 DIM B[40]
0905 FOUNDIC0=0
0910 IF UP_ICF$="Y" OR UP_IC0$="Y" THEN {
0950 CALL "UPDIC0;READBYKEY",IC0,0,W$(1,10),SEG2$,SEG3$,SEG4$,SEG5$,B$,B{ALL},FOUNDIC0
0990 CALL "UPDIC0;READNEXT",IC0,B$,B{ALL},FOUNDIC0; IF FOUNDIC0 AND B$(1,10)>W$(21,10) THEN FOUNDIC0=0 ELSE IF B$(11,10)<W$(11,10) OR B$(11,10)>W$(31,10) THEN GOTO *SAME
0992  }
0995 IF A[0]=0 THEN A[0]=1
1010 WHILE FOUNDIC0 ! 1297
1040 C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
1110 IF UP_IC0$="Y" THEN {
1115 FOR I=0 TO 12 STEP 2; GOSUB 2000; NEXT I
1120 FOR I=22 TO 32 STEP 2; GOSUB 2000; NEXT I
1180 CALL "UPDIC0;UPDATE",IC0,B$,B{ALL},BUSYIC0
1190  }
1200 IF UP_ICF$="Y" THEN {
1205 CALL "UPDICF;READBYKEY",ICF,0,B$(1,20),SEG2$,SEG3$,SEG4$,SEG5$,ICF$,ICF{ALL},FOUNDICF
1210 CALL "UPDICF;READNEXT",ICF,ICF$,ICF{ALL},FOUNDICF; IF FOUNDICF THEN IF ICF$(1,20)<>B$(1,20) THEN FOUNDICF=0
1220 WHILE FOUNDICF
1225 IF ICF[5]>0 THEN {
1230 PRECISION 6
1240 IF A$(1,1)="D" THEN ICF[5]=ICF[5]/A[0] ELSE ICF[5]=ICF[5]*A[0]
1250 PRECISION M6[1]; ICF[5]=ICF[5]+0; PRECISION 2
1270 CALL "UPDICF;UPDATE",ICF,ICF$,ICF{ALL},BUSYICF
1275  } ! 1225
1280 CALL "UPDICF;READNEXT",ICF,ICF$,ICF{ALL},FOUNDICF; IF FOUNDICF THEN IF ICF$(1,20)<>B$(1,20) THEN FOUNDICF=0
1285 WEND 
1290  }
1295 CALL "UPDIC0;READNEXT",IC0,B$,B{ALL},FOUNDIC0; IF FOUNDIC0 AND B$(1,10)>W$(21,10) THEN FOUNDIC0=0 ELSE IF B$(11,10)<W$(11,10) OR B$(11,10)>W$(31,10) THEN GOTO *SAME
1297 WEND ! 1010
1300 IF UP_FM1$="Y" THEN {
1302 C=0
1305 CALL "UPDFM1;READBYKEY",FM1,0,W$(1,10),SEG2$,SEG3$,SEG4$,SEG5$,FM1$,FM1{ALL},FOUNDFM1
1310 CALL "UPDFM1;READNEXT",FM1,FM1$,FM1{ALL},FOUNDFM1; IF FOUNDFM1 AND FM1$(1,10)>W$(21,10) THEN FOUNDFM1=0 ELSE IF FM1$(11,10)<W$(11,10) OR FM1$(11,10)>W$(31,10) THEN GOTO *SAME
1320 WHILE FOUNDFM1
1330 PRECISION 6
1340 IF A$(1,1)="D" THEN FM1[3]=FM1[3]/A[0] ELSE FM1[3]=FM1[3]*A[0]
1345 C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
1350 PRECISION M6[1]; FM1[3]=FM1[3]+0; PRECISION 2
1370 CALL "UPDFM1;UPDATE",FM1,FM1$,FM1{ALL},BUSYFM1
1380 CALL "UPDFM1;READNEXT",FM1,FM1$,FM1{ALL},FOUNDFM1; IF FOUNDFM1 AND FM1$(1,10)>W$(21,10) THEN FOUNDFM1=0 ELSE IF FM1$(11,10)<W$(11,10) OR FM1$(11,10)>W$(31,10) THEN GOTO *SAME
1385 WEND 
1390  }
1400 IF UP_FS2$="Y" THEN {
1402 C=0
1405 CALL "UPDFS2;READBYKEY",FS2,0,SEG1$,SEG2$,SEG3$,SEG4$,SEG5$,FS2$,FS2{ALL},FOUNDFS2
1410 CALL "UPDFS2;READNEXT",FS2,FS2$,FS2{ALL},FOUNDFS2; IF FOUNDFS2 THEN CALL "UPDFS1;READBYKEY",FS1,0,FS2$(147,8),SEG2$,SEG3$,SEG4$,SEG5$,FS1$,FS1{ALL},FOUNDFS1; IF NOT(FOUNDFS1) THEN GOTO *SAME ELSE TF_ITEM$=FS2$(161,10)+FS2$(19,10); IF FS2$(155,1)=" " AND FS2$(14,1)="Y" AND FS2$(161,10)=DIM(10) THEN TF_ITEM$=FS1$(6,10)+FS2$(19,10) ! chui bug in o.e. does not set customer code in fs2
1412 C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
1415 IF FOUNDFS2 THEN IF TF_ITEM$(1,10)<W$(1,10) OR TF_ITEM$(1,10)>W$(21,10) OR TF_ITEM$(11,10)<W$(11,10) OR TF_ITEM$(11,10)>W$(31,10) THEN GOTO 1410
1420 WHILE FOUNDFS2
1430 PRECISION 6
1435 OLD_EXT=FS2[6]
1440 IF A$(1,1)="D" THEN FS2[4]=FS2[4]/A[0],FS2[6]=FS2[6]/A[0] ELSE FS2[4]=FS2[4]*A[0],FS2[6]=FS2[6]*A[0]
1450 PRECISION M6[1]; FS2[4]=FS2[4]+0; PRECISION 2
1460 CALL "UPDFS2;UPDATE",FS2,FS2$,FS2{ALL},BUSYFS2
1470 CALL "UPDFS1;READBYKEY",FS1,0,FS2$(147,8),SEG2$,SEG3$,SEG4$,SEG5$,FS1$,FS1{ALL},FOUNDFS1; IF FOUNDFS1 THEN FS1[6]=FS1[6]-OLD_EXT+FS2[6]; CALL "UPDFS1;UPDATE",FS1,FS1$,FS1{ALL},BUSY
1480 CALL "UPDFS2;READNEXT",FS2,FS2$,FS2{ALL},FOUNDFS2; IF FOUNDFS2 THEN CALL "UPDFS1;READBYKEY",FS1,0,FS2$(147,8),SEG2$,SEG3$,SEG4$,SEG5$,FS1$,FS1{ALL},FOUNDFS1; IF NOT(FOUNDFS1) THEN GOTO *SAME ELSE TF_ITEM$=FS2$(161,10)+FS2$(19,10); IF FS2$(155,1)=" " AND FS2$(14,1)="Y" AND FS2$(161,10)=DIM(10) THEN TF_ITEM$=FS1$(6,10)+FS2$(19,10) ! chui bug in o.e. does not set customer code in fs2
1482 C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
1485 IF FOUNDFS2 THEN IF TF_ITEM$(1,10)<W$(1,10) OR TF_ITEM$(1,10)>W$(21,10) OR TF_ITEM$(11,10)<W$(11,10) OR TF_ITEM$(11,10)>W$(31,10) THEN GOTO 1480
1487 WEND 
1490  }
1900 GOTO 5000
2000 REM "Compute New Price"
2010 PRECISION 6
2020 IF A$(1,1)="D" THEN B[I]=B[I]/A[0] ELSE B[I]=B[I]*A[0]
2030 PRECISION M6[1]; B[I]=B[I]+0; PRECISION 2
2035 IF X3$(9,3)="011" THEN IF FPT(B[I]/.25)<>0 THEN B[I]=INT((B[I]+.25)/.25)*.25
2040 RETURN 
2350 REM "Factor
2352 PRECISION 4
2360 CALL "ZZENTR","N&",A{ALL},A$,X4$,X3$,55,11,0,140,C0,"#.0000","{3"+X$,"","IC2UTG00","","",""
2400 REM "Multiply or Divide?
2405 X$="M=multiply, D=divide"
2410 CALL "ZZENTR","SRU",A{ALL},A$,X4$,X3$,55,14,1,1,C0,"","{3"+X$,"MD","IC2UTG04","","",""
2420 IF A$(1,1)="D" AND A[0]=0 THEN GOTO 2350
2430 PRECISION 2
2440 RETURN 
5000 REM "EOJ"
5010 C=T; GOSUB 8150
5030 CALL "ZZPROM",".4",X3$,Z,"Process complete!!","","",0
5040 GOTO 9900
6000 REM "BACKGROUND
6005 PRINT (0,ERR=6016)'SB',
6010 ! PRINT @(0,3),"This program will change the selling price of all inventory items within the",'LF',"selected range by a multiplier.  The multipler will be applied to the base",'LF',"selling price and all quantity break prices.",
6015 ! CALL T0$,ERR=6231,X3$,X4$,Y0$,W$,W{ALL},1,0,L0
6020 ! CALL T0$,ERR=6021,X3$,X4$,Y0$,W$,W{ALL},2,0,L0
6040 ! PRINT @(5,11),"Multiplier by which to adjust selling prices:",
6050 ! PRINT @(5,12),"(to increase, enter 1.10 and Multiply)",
6060 ! PRINT @(7,14),"Multiply or Divide price using this factor?",
6165 ! PRINT (0,ERR=6166)'SF',
6190 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[2],T9,X3$,T,T0,K,B,D,S0,S1,F$
8115 REM IF I$="" THEN PRINT @(0,9),"There are "+STR(T)+" records to process"
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8155 CALL "ZZBARG",X3$,"HG",19,10,50,T1,T,C
8195 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9901 PRINT 'POP',
9905 END_PROG: CMD_STR$="END"
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
56000 REM "198656-For Company 474. Need to create a new utility that will     
56002 REM "211380-TopForm file feldnames which are SQL keywords break Oracle. 
56003 REM "288700-MHC:  I'm trying to update sales price on a range of items  
