0010 ! Zero Ship Correction Utility -<ICGZSU>
0020 SETESC 9300; SETERR 9000
0035 REM "5.7 - 09/06/18 - 10.438333 - jvv - SSP# 302176
0037 REM "302176-Utility for External Warehouse Zero Ship correction.        
0040 REM "Copyright 2018 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 IF TCB(88)=0 THEN MSGBOX "You must be using Windx to access this program" ELSE PROCESS "ICGZSU","../ICG/IC.EN"
0060 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0090 ! 
0095 INIT:
0100 SETERR 9000
0110 X0$="ICGZSU",X1$="Zero Ship Correction Utility "
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1,T0$="ZZXIC0",L0=10
0200 REM "
0240 IF X3$="" THEN CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0310 IOLIST FS1$,FS1[0],FS1[1],FS1[2],FS1[3],FS1[4],FS1[5],FS1[6],FS1[7],FS1[8],FS1[9],FS1[10],FS1[11],FS1[12],FS1[13]
0320 IOLIST FS2$,FS2[0],FS2[1],FS2[2],FS2[3],FS2[4],FS2[5],FS2[6],FS2[7],FS2[8],FS2[9],FS2[10],FS2[11],FS2[12],FS2[13],FS2[14],FS2[15],FS2[16],FS2[17],FS2[18],FS2[19],FS2[20],FS2[21],FS2[22],FS2[23],FS2[24],FS2[25],FS2[26],FS2[27],FS2[28],FS2[29]
0400 IOLIST IC1$,IC1[0],IC1[1],IC1[2],IC1[3],IC1[4],IC1[5],IC1[6],IC1[7],IC1[8],IC1[9],IC1[10],IC1[11],IC1[12],IC1[13],IC1[14],IC1[15],IC1[16],IC1[17],IC1[18],IC1[19],IC1[20]
0401 IOLIST IC1$,IC1[0],STR(IC1[1]),STR(IC1[2]),IC1[3],IC1[4],IC1[5],IC1[6],IC1[7],IC1[8],IC1[9],IC1[10],IC1[11],IC1[12],IC1[13],IC1[14],IC1[15],IC1[16],IC1[17],IC1[18],IC1[19],IC1[20]
0410 IOLIST ICF$,ICF[0],ICF[1],ICF[2],ICF[3],ICF[4],ICF[5],ICF[6],ICF[7],ICF[8],ICF[9],ICF[10],ICF[11],ICF[12]
0415 IOLIST ICF0$,ICF[0],ICF[1],ICF[2],ICF[3],ICF[4],ICF[5],ICF[6],ICF[7],ICF[8],ICF[9],ICF[10],ICF[11],ICF[12]
0460 LOG_OPTIONS$="A"; CALL "ZZ2LOG;OPEN_LOG",ERR=*NEXT,X3$,X4$,"ICGZSU"+"."+FID(0)+".log",LOG_OPTIONS$,LOG_CHANNEL ! 252868
0470 CALL "ZZ2LOG;LOG_MSG",ERR=*NEXT,X3$,X4$,LOG_CHANNEL,"START|",%LOG_INFO ! 252868
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="01O FM0...  02O IC1...  03O ICF...  04OSICF...  05O FS2...  06O FS1...  13O ZZPARM  "
0515 ! C0=Z[2],FM1=Z[3],ICF=Z[4],FS2=Z[5]
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0525 IC1=Z[2],ICF=Z[3],ICFC=Z[4],FS2=Z[5],FS1=Z[6]
0550 DIM M6[1]; CALL "IC2PRE",X3$,Z[13],"",M6{ALL},"",0,"",0
0570 _FILE_NAME$=FIN(Z[5],"FILENAME"); FS2=Z[5]; CLOSE (FS2); OPEN (FS2,IOL=*)_FILE_NAME$
0580 %QRY_ORDER$="",QORD$=""
0600 REM "
0605 DIM A$(1),A[1]
0610 FIND (Z[13],KEY=%C$+"ICGZSU",DOM=*NEXT)HIDDEN$; GOTO 0630
0615 WRITE (Z[13],KEY=%C$+"ICGZSU")"9999"; GOTO 0610
0620 MSGBOX (MSG("Hidden Warehouse Parameter record not set up")); GOTO END_PROG
0630 FIND (1,KEY="D"+DIM(10)+HIDDEN$,DOM=*NEXT)FM0$; GOTO 0650
0640 MSGBOX (MSG("Hidden Warehouse record not set up in FM0")); GOTO END_PROG
0650 IF MID(FM0$,234,1)<>"Y" THEN MSGBOX (MSG("The warehouse is not set up as HIDDEN")); GOTO END_PROG
0685 INIT_END:
0688 CALL "ZZ2LOG;LOG_MSG",ERR=*NEXT,X3$,X4$,LOG_CHANNEL,"START|"+"HIDDEN WAREHOUSE |"+HIDDEN$+"|SET UP CORRECTLY|",%LOG_INFO ! 252868                
0690 RETURN 
0699 ! 
2999 ! 
3000 GET_ORDER:
3005 IF STP(ORDER$,2)="" THEN ORDER_LINE_NUM$=""; RETURN 
3006 ! IF QRY_VAL$="" THEN ORDER_LINE_NUM$=""; GOTO *NEXT ELSE ORDER$=QRY_VAL$(1,8) ! ,ORDER_LINE_NUM$=QRY_VAL$(9,3)
3010 CALL "ZZWLKU;PARSE_ORD",ORDER$,ORDER_DIV$,ORDER_NUM$
3015 ORDER$=ORDER_DIV$+ORDER_NUM$; IF QORD$="" THEN %QRY_ORDER$=ORDER$
3020 GOSUB GET_FS1_INFO
3030 REFRESH_FLG=1
3050 RETURN 
3099 ! 
3100 GET_LINE_NO:
3110 IF STP(ORDER$,2)="" THEN NEXT_ID=ORDER.CTL; EXIT 
3115 IF STP(ORDER_LINE_NUM$,2)="" THEN ORDER$="",NEXT_ID=ORDER.CTL; REFRESH_FLG=1; RETURN 
3120 ! IF QRY_VAL$="" THEN GOTO *NEXT ELSE ORDER_LINE_NUM$=QRY_VAL$(1,3)
3130 GOTO FIND_REC
3190 RETURN 
3499 ! 
3500 FIND_REC:
3530 FIND (FS2,KEY=ORDER$+ORDER_LINE_NUM$,DOM=INVALID_FS2)
3540 IF WHSE$<"8999" OR WHSE$>"9999" THEN GOTO WHSE_ERR
3550 IF NOT(NUL(PO_CODE$)) THEN GOTO NOT_CUST_OWNED_ERR
3552 GOSUB GET_FS1_INFO
3558 GOSUB DISP_REC
3560 CALL "*wingrp;Disable",FIELDS.GRP$
3570 REFRESH_FLG=1
3580 NEXT_ID=PROCEED.CTL
3590 RETURN 
3599 ! 
3800 GET_FS1_INFO:
3820 FIND (FS1,KEY=ORDER$,DOM=INVALID_ORDER)FS1$
3825 IF STP(FS1$(63,1),2)<>"" THEN GOTO INVALID_ORDER_STATUS
3830 CUSTOMER$=FS1$(6,2)+FS1$(8,8)
3840 FS1_CUSTOMER$=CUSTOMER$
3850 IF STP(CUSTOMER$,3)="" THEN %CUST_NAME_FS2$="" ELSE CALL "ZZWLKU;AR_CUST_VAL",CUSTOMER$,"","FS2"
3860 IF STP(CUSTOMER$,3)<>"" THEN CALL "ZZWLKU;PARSE_CUST",CUSTOMER$,CUST_DIV$,CUST_CODE$
3870 REFRESH_FLG=1
3890 RETURN 
3899 ! 
4300 INVALID_ORDER:
4320 MSGBOX MSG("INVALID_ON")
4330 GOSUB CLEAR_FIELDS
4350 NEXT_ID=ORDER.CTL; REFRESH_FLG=1
4390 RETURN 
4399 ! 
4400 INVALID_ORDER_STATUS:
4410 STYPE$=MID(FS1$,63,1)
4420 IF STYPE$="Y" THEN MSGBOX MSG("FMGODM_31")
4430 IF STYPE$="S" THEN MSGBOX MSG("FMGODM_S")
4440 IF STYPE$="P" THEN MSGBOX MSG("FMGODM_P")
4470 GOSUB CLEAR_FIELDS
4480 NEXT_ID=ORDER.CTL; REFRESH_FLG=1
4490 RETURN 
4499 ! 
4500 INVALID_FS2:
4520 MSGBOX MSG("UTZ_02"); ORDER_LINE_NUM$=""; NEXT_ID=ORDER_LINE_NUM.CTL
4525 REFRESH_FLG=1
4530 EXIT 
4599 ! 
5200 DISP_REC:
5220 OPEN_QTY$=STR(OPEN_QTY)
5230 UNIT_COST$=STR(UNIT_COST)
5240 COST_QTY_PER$=STR(COST_QTY_PER)
5250 COST_EXT$=STR(COST_EXT)
5260 UNIT_SELL_PRICE$=STR(UNIT_SELL_PRICE)
5270 QTY_IN_SELL_UM$=STR(QTY_IN_SELL_UM)
5280 SELL_PRICE_EXT$=STR(SELL_PRICE_EXT)
5290 QTY_UNBILLED_SHIP$=STR(QTY_UNBILLED_SHIP)
5300 QTY_BO_RCV_FS3$=STR(QTY_BO_RCV_FS3)
5310 UNBILL_SHIP_UNIT_CST$=STR(UNBILL_SHIP_UNIT_CST)
5320 UNBILLED_SHIP_EXT$=STR(UNBILLED_SHIP_EXT)
5330 DISPLAY_SELL_QTY=FN%QTY_IN_UM(OPEN_QTY,SELL_UM$,QTY_IN_SELL_UM),DISPLAY_SELL_QTY$=STR(DISPLAY_SELL_QTY)
5340 DISPLAY_COST_QTY=FN%QTY_IN_UM(OPEN_QTY,COST_UM$,COST_QTY_PER),DISPLAY_COST_QTY$=STR(DISPLAY_COST_QTY:"##,###,###.00-")
5390 RETURN 
5399 ! 
6000 SEL_QUERY:
6005 %QRY_ORDER$="",QORD$=""
6010 PROCESS "QRY_FS2.ZS","../ICG/IC.EN",QORD$
6020 IF QORD$="" THEN ORDER$="",ORDER_LINE_NUM$=""; GOTO *NEXT ELSE ORDER$=QORD$(1,8),ORDER_LINE_NUM$=QORD$(9,3); GOTO FIND_REC
6050 EXIT 
6090 ! 
7000 ERROR_MSGS:
7050 WHSE_ERR: MSG$="This is not a Customer owned warehouse"; GOTO END_ERROR_MSG
7060 NOT_CUST_OWNED_ERR: MSG$="This is not a cust owned release line"; GOTO END_ERROR_MSG
7065 NO_INV_ERR: MSG$="There is no inventory associated with this order line"; GOTO END_ERROR_MSG
7090 END_ERROR_MSG:
7095 MSGBOX (MSG(MSG$))
7096 GOSUB CLEAR_DISP; NEXT_ID=ORDER_LINE_NUM.CTL
7098 EXIT 
7099 ! 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9905 END_PROG: CMD_STR$="END"
9910 CALL "ZZ2LOG;LOG_MSG",ERR=*NEXT,X3$,X4$,LOG_CHANNEL,"END|",%LOG_INFO ! 252868
9915 CALL "ZZ2LOG;CLOSE_LOG",ERR=*NEXT,X3$,X4$,LOG_CHANNEL ! 252868
9920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9925 RETURN 
9929 ! 
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9998 END 
9999 END 
10000 PROCEED:
10006 ! HIDDEN$="9999"
10010 DIM FS1$(350),FS1[13],FS2$(356),FS2[29],ICF[12]; TOT_QTY=0
10040 EXTRACT (Z[6],KEY=ORDER$,KNO=0,DOM=INVALID_ORDER)IOL=0310
10050 READ (Z[5],KEY=ORDER$+ORDER_LINE_NUM$,DOM=INVALID_FS2)IOL=0320
10090 ORDER_NUM$=ORDER$; OPEN_QTY=FS2[0],QTY_UNBILLED_SHIP=FS2[7]
10100 CALL "FM2ODH",X3$,-1,FS1$,FS1{ALL}
10110 CALL "ZZ2LOG;LOG_MSG",ERR=*NEXT,X3$,X4$,LOG_CHANNEL,"ORDER|"+ORDER$+"|LINE NUM|"+ORDER_LINE_NUM$+"|READY FOR PROCESSING|",%LOG_INFO ! 252868
10120 GOSUB UNCOMMIT_LOTS
10130 GOSUB MOVE_WAREHOUSE
10140 ! Set Back order Amount
10150 QTY_BO_RCV_FS3=OPEN_QTY,FS2[8]=QTY_BO_RCV_FS3
10160 FS2[7]=0 ! Qty Unbilled ship
10170 WRITE (Z[5],KEY=ORDER$+ORDER_LINE_NUM$,ERR=INVALID_FS2)IOL=0320
10180 CALL "FM2ODH",X3$,1,FS1$,FS1{ALL}
10199 ! 
10200 MSGBOX "Process complete",MSG("CONFIRMING"),"!,TIM=5"
10210 NEXT_ID=ORDER.CTL
10220 GOSUB CLEAR_FIELDS
10290 RETURN 
10299 ! 
11000 UNCOMMIT_LOTS:
11005 ! ICF records are  cleared of the order number+line num (110,11), IC1 record is modified to remove committed and backordered qty
11010 IF FS2$(9,1)<>" " OR POS(FS2$(155,1)="IC")=0 THEN RETURN 
11020 IF POS(" "<>FS2$(10,4))=0 OR FS2$(10,2)="DS" THEN RETURN 
11030 IF FS2$(155,1)="I" THEN DIM C0$(10) ELSE C0$=FS2$(161,10)
11035 CCUST$=C0$
11038 GOSUB CHECK_COMMITTED; IF TOT_QTY=0 THEN RETURN 
11040 IC1$=CCUST$+FS2$(19,10)+FS2$(10,4)+FS2$(147,8)+FS2$(6,3)
11050 IF FS2[0]<0 THEN GOSUB 3200 ELSE CALL "IC2LCA",X3$,"","D",0,IC1$,0
11060 CALL "ZZ2LOG;LOG_MSG",ERR=*NEXT,X3$,X4$,LOG_CHANNEL,"ORDER|"+ORDER$+"|LINE NUM|"+ORDER_LINE_NUM$+"|WAREHOUSE|"+WHSE$+"|LOTS UNCOMMITTED|",%LOG_INFO ! 252868
11080 DIM IC1$(58),IC1[20]; C0=-1
11200 IC1$(1)=CCUST$+FS2$(19,10)+FS2$(10,4)
11210 EXTRACT (Z[2],KEY=IC1$(1,24),DOM=*NEXT)IOL=0400
11220 IC1[7]=IC1[7]+SGN(C0)*FS2[0],IC1[11]=IC1[11]+SGN(C0)*FS2[8]
11230 IF FS2[7]>FS2[0] THEN IC1[7]=IC1[7]+SGN(C0)*(FS2[7]-FS2[0]) ! SSP# 151754 if shipped > ordered then committed was understated, add in the difference here if needed
11240 WRITE (Z[2],KEY=IC1$(1,24))IOL=0401
11250 IF RT_PARM$>"" THEN IF RT_PARM$(7,1)="H" THEN CUST$=IC1$(1,10); CALL "RT2WOC",ERR=8636,X3$,X4$,CUST$,"IC1...","U",IC1$(1,24)
11260 IF GB$<>"" THEN ORDER_LINE$=A$(147,8)+A$(6,3); CALL "BM2ICU",X3$,X4$,1*SGN(C0),ORDER_LINE$,0,"",0
11270 RETURN 
11299 ! 
12200 CHECK_COMMITTED:
12204 TOT_QTY=0
12210 IKEY$=ORDER$+ORDER_LINE_NUM$+CUSTOMER$+ITEM_CODE$+WHSE$
12220 READ (ICF,KEY=IKEY$,KNO=3,ERR=*NEXT)
12230 KK$=KEY(ICF,KNO=3,END=COMMITTED_END); IF KK$(1,LEN(IKEY$))<>IKEY$ THEN GOTO COMMITTED_END
12240 READ (ICF,KEY=KK$,KNO=3)IOL=0410
12250 LOT_QTY=ICF[4]*ICF[3]; IF ICF[2]<>0 THEN LOT_QTY=LOT_QTY*ICF[2]
12260 TOT_QTY=TOT_QTY+LOT_QTY,COMMITTED=TOT_QTY
12265 CALL "ZZ2LOG;LOG_MSG",ERR=*NEXT,X3$,X4$,LOG_CHANNEL,"ORDER|"+ORDER$+"|LINE NUM|"+ORDER_LINE_NUM$+"|WAREHOUSE|"+KK$+"|LOT QTY OF|"+STR(LOT_QTY)+"|WILL BE UNCOMMITTED|",%LOG_INFO ! 252868
12270 GOTO 12230
12275 COMMITTED_END:
12280 IF TOT_QTY=0 THEN CALL "ZZ2LOG;LOG_MSG",ERR=*NEXT,X3$,X4$,LOG_CHANNEL,"ORDER|"+ORDER$+"|LINE NUM|"+ORDER_LINE_NUM$+"|WAREHOUSE|"+WHSE$+"|DOES NOT HAVE ANY LOTS TO UNCOMMIT|",%LOG_INFO ! 252868
12285 KIC$=KEC(ICF,KNO=0,ERR=*NEXT)
12290 RETURN 
12299 ! 
12500 MOVE_WAREHOUSE:
12505 ! All uncommitted ICF records for the warehouse are moved to the 'hidden warehouse (9999), and then deleted. The total qty moved is added to the adjustment field in IC1 records for the original whse and the hidden whse.
12510 ! ICFC=HFN; OPEN (ICFC)"ICF"+%C$
12520 TOT=0,COMMITTED=0,TOT_QTY=0
12530 READ (Z[3],KEY=CUSTOMER$+ITEM_CODE$+WHSE$,KNO=0,ERR=*NEXT)
12539 ! 
12550 READ_NEXT:
12560 K$=KEY(Z[3],KNO=0,END=UPD_IC1); IF K$(1,24)<>CUSTOMER$+ITEM_CODE$+WHSE$ THEN GOTO UPD_IC1
12570 READ (Z[3],KNO=0,KEY=K$)IOL=0410
12580 IF POS(" "<>ICF$(110,11))>0 AND ICF$(110,11)<>ORDER$+ORDER_LINE_NUM$ THEN GOTO READ_NEXT
12590 LOT_KEY$=K$
12600 LOT_QTY=ICF[4]*ICF[3]; IF ICF[2]<>0 THEN LOT_QTY=LOT_QTY*ICF[2]
12610 TOT_QTY=TOT_QTY+LOT_QTY,COMMITTED=TOT_QTY
12660 ICF0$=ICF$,ICF0$(21,4)=HIDDEN$
12690 ! 
12700 WRITE_NEW_ICF:
12710 WRITE (ICFC,KEY=ICF0$(1,32),DOM=*NEXT)IOL=0415; GOTO ICF_LOG
12720 KEY$=ICF0$(1,32); CALL "IC2LCC",X3$,X4$,ICFC,31,2,LAST_ONE$,KEY$; ICF0$(1,32)=KEY$; GOTO WRITE_NEW_ICF
12749 ! 
12750 ICF_LOG:
12780 CALL "ZZ2LOG;LOG_MSG",ERR=*NEXT,X3$,X4$,LOG_CHANNEL,"ORDER|"+ORDER$+"|LINE NUM|"+ORDER_LINE_NUM$+"|WAREHOUSE|"+LOT_KEY$+"|LOT QTY OF|"+STR(LOT_QTY)+"|HAS BEEN MOVED TO WAREHOUSE|"+HIDDEN$+"|"+KEY$+"|",%LOG_INFO ! 252868
12790 ! 
12800 DEL_OLD_ICF:
12810 REMOVE (Z[3],KEY=K$)
12820 CALL "ZZ2LOG;LOG_MSG",ERR=*NEXT,X3$,X4$,LOG_CHANNEL,"ORDER|"+ORDER$+"|LINE NUM|"+ORDER_LINE_NUM$+"|WAREHOUSE|"+LOT_KEY$+"|HAS BEEN REMOVED|",%LOG_INFO ! 252868
12850 GOTO READ_NEXT
12899 ! 
13000 UPD_IC1:
13010 IF TOT_QTY=0 THEN CALL "ZZ2LOG;LOG_MSG",ERR=*NEXT,X3$,X4$,LOG_CHANNEL,"ORDER|"+ORDER$+"|LINE NUM|"+ORDER_LINE_NUM$+"|WAREHOUSE|"+WHSE$+"|DOES NOT HAVE ANY UNCOMMITTED LOTS TO MOVE TO THE HIDDEN WAREHOUSE|",%LOG_INFO; RETURN 
13040 DIM IC1$(58),IC1[20]; C0=-1
13050 IC1$(1)=CCUST$+FS2$(19,10)+FS2$(10,4)
13060 EXTRACT (Z[2],KEY=IC1$(1,24),DOM=*NEXT)IOL=0400
13070 Q=IC1[3]+IC1[4]-IC1[5]+IC1[6]
13075 TOTQ=TOT_QTY*C0
13080 IC1[6]=IC1[6]+TOTQ
13082 ! F Q<>TOTQ THEN IC1[6]=IC1[6]+TOTQ-Q
13090 WRITE (Z[2],KEY=IC1$(1,24))IOL=0401
13099 ! 
13100 UPD_NEW_IC1:
13110 DIM IC1$(58),IC1[20]
13120 IC1$(1)=CCUST$+FS2$(19,10)+HIDDEN$
13130 EXTRACT (Z[2],KEY=IC1$(1,24),DOM=*NEXT)IOL=0400
13135 IC1$(1)=CCUST$+FS2$(19,10)+HIDDEN$
13140 Q=IC1[3]+IC1[4]-IC1[5]+IC1[6]
13150 ! IF F9$="Y" THEN B[3]=T,B$(53,6)=X3$(21,6); GOTO 1240
13160 IC1[6]=IC1[6]+TOT_QTY
13180 WRITE (Z[2],KEY=IC1$(1,24))IOL=0401
13190 RETURN 
13999 ! 
14000 CLEAR_FIELDS:
14010 ORDER$=""
14015 CLEAR_DISP:
14020 CTN_LBL_FORMAT_CD$=""
14030 LOT_INV_ORDER_LINE$=""
14040 ORDER_LINE_COMPLETE$=""
14050 CUSTOMER$="",ITEM_CODE$=""
14060 CSTM_ITEM_LINE_TYPE$=" "
14070 ORDER_LINE_NUM$=""
14080 PO_CODE$=""
14090 WHSE$=""
14100 CUSTOM$=""
14110 QUOTE_LINE_NUM$=""; QSA.CUST_REF$=""
14120 SPECIAL_SHIPPING$=""
14140 PROD_CODE$=""
14150 LEFT_RIGHT$=""
14160 TOP_BOTTOM$=""
14170 PLYS$=""
14180 TF_DESC$=""
14190 PLANT_CODE$=""
14200 VEND_DIV$=""
14210 VEND_CODE$=""
14220 STARTING_NUM$=""
14230 RO_DATE$=""
14240 SPECS_REQ$=""
14250 COST_UM$=""
14260 SELL_UM$=""
14270 COMM_CODE$=""
14280 SKIP_ORDER_HIST$=""
14290 RUSH_CHARGE$=""
14300 FIND_FEE$=""
14310 IS_LINE_TAXABLE$=""
14320 FIND_FEE_VEND_DIV$=""
14330 FIND_FEE_VEND_CODE$=""
14340 ! IF _CLR_FLG$="R" THEN ORDER_DIV$=""
14350 ! IF _CLR_FLG$="R" THEN ORDER_NUM$=""
14360 LINE_TYPE$="",%LINE_TYPE$=""
14370 PRINT_FLAGS$=""
14380 CUST_DIV$=""
14390 CUST_CODE$=""
14400 JOB_NUM$=""
14410 JOB_DATE$=""
14420 ENDING_NUM$=""
14430 CTN_WT$=""
14440 COST_CODE$=""
14450 ITEM_CLASS$=""
14460 WHSE_CODE_PKLIST$=""
14470 PO_LINE_RCV_COMP$=""
14480 CTN_LBL_CODE_2ND$=""
14490 QUOTE$=""
14495 PRIOR_OPEN_QTY=0 ! SSP245209 jdf
14500 OPEN_QTY=0
14510 UNIT_COST=0
14520 COST_QTY_PER=0,COST_QTY_PER$=""
14530 COST_EXT=0
14540 UNIT_SELL_PRICE=0
14550 QTY_IN_SELL_UM=0,QTY_IN_SELL$=""
14560 SELL_PRICE_EXT=0
14570 QTY_UNBILLED_SHIP=0
14580 QTY_BO_RCV_FS3=0
14590 NUM_OF_CTN_LBL_PRNT=0
14600 ORIG_QTY_ORDERED=0
14610 FIND_FEE_PRCNT=0
14620 CTN_PACK=0
14630 COMBO_QTY=0
14640 QTY_INV_TO_DATE=0
14650 UNBILL_SHIP_UNIT_CST=0
14660 INVD_TO_DATE_QTY=0
14670 FRT_RETURNS_FROM_LOT=0
14680 REV_UNIT_SELL_PRICE=0
14690 UNBILLED_SHIP_EXT=0
14700 INVD_TO_DATE_EXTCST=0
14710 INVD_TO_DATE_EXT=0
14720 UNIT_LIST_PRICE=0
14730 LINE_ITEM_DISC_PRCNT=0
14740 NUM_OF_CART_LBLS_2=0
14985 SPECIAL_SHIP_QTY=0,SPECIAL_SHIP_BUTTON_PRESSED=0
14990 REFRESH_FLG=1
14992 RC_NUMBER$="" ! SSP 184532
14993 HAS_COMP_ITEM$="" ! SSP 225781
14994 Q9=0,Q8=0,S1=0 ! SSP 224867
14995 DISPLAY_SELL_QTY=0,DISPLAY_SELL_QTY$="" ! SSP 227656
14997 DISPLAY_COST_QTY=0,DISPLAY_COST_QTY$="" ! SSP242299 jdf
14998 REFRESH_FLG=1
14999 RETURN 
56000 REM "302176-Utility for External Warehouse Zero Ship correction.        
