0010 REM "EFGMAQ - Review of Unform Email Queue
0035 REM "5.7 - 07/21/22 - 12.059797 - jvv - SSP# 000001
0037 REM "000001-Internal call for timekeeping                               
0040 REM "Copyright 2022 Demand Bridge, LLC.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 ! 
0070 IF TCB(88)=0 THEN MSGBOX "You must be using Windx to access this program"; GOTO EXIT_PGM
0071 IF %C$="500" THEN GOTO 0075
0072 IF TCB(29)<14000000 THEN MSGBOX "This program is not available"; GOTO EXIT_PGM
0075 PROCESS "EFGMAQ","../EFG/EF.EN"
0082 EXIT_PGM:
0085 IF TCB(13)=1 THEN RUN "ZMENU" ELSE EXIT 
0090 ! 
0100 ! 100 - Initialization
0110 INIT:
0120 X0=-1
0160 CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,X5$,X0,X1,X2; IF X1>0 THEN CMD_STR$="END"; EXIT 
0190 DIM Z[NUM(X3$(60,3))]
0200 ! Files
0210 Z$="01O EFQ...  02O EF4...  03O EF5...  13O ZZPARM  "
0220 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,0
0240 _FILE_NAME$=FIN(Z[1],"FILENAME"),_FIL_NO=Z[1]
0250 CLOSE (_FIL_NO); OPEN (_FIL_NO,IOL=*)_FILE_NAME$
0300 ! IOList Section
0310 IOL_EFQ:IOLIST EFQ$
0315 DIM EFQ$[1:20]
0320 IOL_DR0:IOLIST DR0$
0400 ! 
0410 _FYI$=MSG("FYI"),_ERROR$=MSG("ERROR")
0420 CTAB$=DIM(2)
0490 RETURN 
0500 ! 500 - Main panel post_display logic
0510 MAIN_POST_DISPLAY:
0570 RETURN 
0900 ! 900 - Wrapup
0910 WRAPUP:
0920 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
0970 RETURN 
1600 ! 1600 - Delete
1610 DELETE_REC:
1690 CHANGE_FLG=0
1710 EXIT 
1720 NO_SUCH: MSGBOX _MSG_REC_NOTFND$,_FYI$,"!"; EXIT 
1800 ! 1800 - Clear record
1810 CLEAR_REC:
1820 DB_FFP$="",FAX_NUM_TYPE$="",EML_STAT$="",FILTER_CODE$="",FILTER_DESC$="",EML_STAT$=""
1825 BATCH_FILTER$="",START_DATE$="",END_DATE$=""
1830 GRID LOAD EMAIL_GRID.CTL,0,0,""; FILT_CNT=0
1840 DROP_BOX WRITE DB_FFP.CTL," "
1845 DROP_BOX WRITE EML_STAT.CTL," "
1850 REFRESH_FLG=1
1860 RETURN 
1899 ! 
1900 SET_HDG:
1910 EMAIL_GRID.CTL'COLUMN=-1,EMAIL_GRID.CTL'COLUMNWIDTH=.1,EMAIL_GRID.CTL'ROW=-1,EMAIL_GRID.CTL'COLUMN=0,EMAIL_GRID.CTL'FONT$="MS Sans Serif,-9,B"
1920 RETURN 
1999 ! 
2000 LOAD_FILTERS:
2002 TIME_MASK$="00:00:00"; IF MID(%X3$,56,3)="DMY" THEN DATE_FORMAT$="%Dz-%Mz-%Y" ELSE DATE_FORMAT$="%Mz-%Dz-%Y"
2005 GRID LOAD EMAIL_GRID.CTL,0,0,""; FILT_CNT=0
2015 IF FCHAN THEN CLOSE (FCHAN); FCHAN=0
2020 SKEY$=FDOC$,KNUM=0; IF STP(BATCH_FILTER$,2)<>"" THEN SKEY$=PAD(BATCH_FILTER$,5),BATCH_FILTER$=SKEY$,KNUM=1
2040 READ (_FIL_NO,KEY=SKEY$,KNO=KNUM,DOM=*NEXT)
2110 NEXT_EFQ:
2125 KQ$=KEY(_FIL_NO,KNO=KNUM,END=END_LOAD_FILTERS)
2130 READ (Z[1],KEY=KQ$,KNO=KNUM,END=END_LOAD_FILTERS)
2132 IF FDOC$="  " THEN GOTO *NEXT ELSE IF FDOC$<>DOC_TYPE$ THEN GOTO NEXT_EFQ
2134 IF FTYP$=" " THEN GOTO *NEXT ELSE IF FTYP$<>EML_TYPE$ THEN GOTO NEXT_EFQ
2140 IF EML_STAT$="1" AND EML_PROCESSED$="1" THEN GOTO NEXT_EFQ ! Select only unprocessed
2142 IF EML_STAT$="2" AND EML_PROCESSED$<>"1" THEN GOTO NEXT_EFQ ! Select only processed
2150 IF STP(START_DATE$,2)<>"" THEN IF EML_DATE$<START_DATE$ THEN GOTO NEXT_EFQ
2160 IF STP(END_DATE$,2)<>"" THEN IF EML_DATE$>END_DATE$ THEN GOTO NEXT_EFQ
2165 IF STP(BATCH_FILTER$,2)<>"" THEN IF PAD(BATCH$,5)<>BATCH_FILTER$ THEN GOTO NEXT_EFQ
2170 EMLPATH$=EMAIL_PATH$+"/"+EMAIL_FILE$
2200 LOAD_GRID_LINE:
2220 GOSUB CLEAR_READONLY ! SSP#253613
2230 DOC_NUM$=DOC_REF$; IF BATCH$<>DIM(5) THEN DOC_NUM$=DOC_REF$+":"+BATCH$
2235 GRID LOCK EMAIL_GRID.CTL,0,0; IF FILT_CNT THEN GRID UNLOCK EMAIL_GRID.CTL,1,0; GRID UNLOCK EMAIL_GRID.CTL,4,0
2240 GOSUB SET_TYPE; EDESC$=EDESC$+":"+CUSTOMER$
2250 GRID LOAD EMAIL_GRID.CTL,1,0,SEP+DOC_TYPE$+SEP+EDESC$+SEP+EML_TO_ADDR$+SEP+DOC_NUM$+SEP+CTAB$+COPY_NUM$+SEP+MSG("VIEW")+SEP+EML_PROCESSED$+SEP+FN%PRINT_DATETIME$(FN%GET_DATETIME(EML_DATE$,0),DATE_FORMAT$)+" - "+STR(EML_TIME$:TIME_MASK$)+SEP+EMLPATH$+SEP+KQ$+SEP; ++FILT_CNT
2260 EMAIL_GRID.CTL'ROW=GRID_ROW,EMAIL_GRID.CTL'COLUMN=7,EMAIL_GRID.CTL'ROWHEIGHT=1.8,EMAIL_GRID.CTL'FONT$="MS Sans Serif,-9,B",EMAIL_GRID.CTL'ALIGN$="C",EMAIL_GRID.CTL'BACKCOLOUR$="Light Gray",EMAIL_GRID.CTL'TEXTCOLOUR$="Black" ! Set BUTTON    column                         
2290 GOTO NEXT_EFQ
2299 ! 
2300 END_LOAD_FILTERS:
2310 IF NOT(FILT_CNT) THEN MSGBOX ("No records to Process"); GOSUB CLEAR_REC; GRID LOAD EMAIL_GRID.CTL,1,0,SEP
2315 CLEAR_GRID:! SSP 304343
2320 GRID LOCK EMAIL_GRID.CTL,0,0; IF FILT_CNT THEN GRID UNLOCK EMAIL_GRID.CTL,1,0; GRID UNLOCK EMAIL_GRID.CTL,4,0
2340 REFRESH_FLG=1
2350 RETURN 
2599 ! 
2600 SET_TYPE:
2610 ETYPE$=EML_TYPE$(1,1),EDESC$=""
2620 SWITCH ETYPE$
2625 CASE "A"
2630 EDESC$="CSR"
2635 BREAK
2640 CASE "C"
2645 EDESC$="Cust"
2650 BREAK
2655 CASE "P"
2660 EDESC$="Plant"
2665 BREAK
2670 CASE "R"
2675 EDESC$="Reorder"
2680 BREAK
2685 CASE "S"
2690 EDESC$="Slsman"
2695 BREAK
2700 CASE "T"
2705 EDESC$="Ship To"
2710 BREAK
2715 CASE "U"
2720 EDESC$="TY Ship To"
2725 BREAK
2730 CASE "V"
2735 EDESC$="Vendor"
2740 BREAK
2745 CASE "W"
2750 EDESC$="Whse"
2755 BREAK
2760 CASE "Y"
2765 EDESC$="TY Cust"
2770 BREAK
2775 DEFAULT 
2780 END SWITCH 
2790 RETURN 
2799 ! 
3000 ! Apply new filter to datarep target
3010 APPLY_FILTER:
3042 IF STP(DB_FFP$,2)="" THEN DB_FFP$="  "
3044 IF STP(FAX_NUM_TYPE$,2)="" THEN FAX_NUM_TYPE$=" "
3050 FDOC$=DB_FFP$,FTYP$=FAX_NUM_TYPE$
3090 GOSUB LOAD_FILTERS
3095 RETURN 
3099 ! 
4000 ! Delete selected filters
4010 DELETE_FILTERS:
4012 MSGBOX MSG("This option is currently unavailable"),MSG("FYI"),"!" ! 232963
4013 RETURN 
4014 IF ANS$<>"YES" THEN GOTO END_DELETE_FILTERS
4020 IF NOT(FN%ISOPEN(FCHAN)) THEN GOTO END_DELETE_FILTERS
4030 READ (FCHAN,KEY="",DOM=*NEXT)
4040 DKEY$=KEY(FCHAN,END=END_DELETE_FILTERS); READ (FCHAN,KEY=DKEY$)C_FILTER$
4050 REMOVE (Z[1],KEY=PAD(DATA_REP_TARGET$,20)+PAD("CUST_DIV",30)+PAD(STP(C_FILTER$,2),30),ERR=*NEXT)
4060 GOTO 4040
4105 END_DELETE_FILTERS:
4110 GOSUB LOAD_FILTERS
4195 RETURN 
4199 ! 
4200 ! Process selected emails
4210 SELECT_TO_SEND:
4215 SEL_CNT=0 ! SSP 304343
4220 FOR XX=1 TO FILT_CNT
4225 EMAIL_GRID.CTL'ROW=XX
4230 READ DATA FROM EMAIL_GRID.CTL'ROWDATA$ TO IOL=EMAIL_GRID.CTL'LOADIOLIST$
4232 IF C_PROC$="1" THEN IF EML_STAT$<>"2" THEN GOTO 4240 ! SSP 304343
4235 IF C_DELETE$="1" THEN SEL_CNT=SEL_CNT+1 ! SSP 304343
4240 NEXT XX
4250 IF SEL_CNT=0 THEN MSGBOX ("No unprocessed emails selected to process"),MSG("WARNING"),"OK",X$; GOSUB CLEAR_REC; GRID LOAD EMAIL_GRID.CTL,1,0,SEP; GOTO CLEAR_GRID ! RETURN ! SSP 304343
4258 OK_TO_PROCESS:
4260 MSGBOX MSG("Process Selected documents"),MSG("CONFIRM"),"YESNO,2",ANS$
4270 IF ANS$<>"YES" THEN GOSUB LOAD_FILTERS; REFRESH_FLG=1; RETURN 
4272 FRM$="",FBATCH$=""; IF STP(BATCH_FILTER$,2)<>"" THEN FRM$="BT",FBATCH$=BATCH_FILTER$
4275 CALL "EFGMAQ;SEND_EMAILS",X3$,X4$,FCHAN,FRM$,FBATCH$,PROGRESS1.CTL,1 ! 239169
4280 END_SELECT_TO_SEND:
4285 GOSUB LOAD_FILTERS; REFRESH_FLG=1 ! 232963
4290 MSGBOX MSG("Emails have been processed"),MSG("FYI"),"!" ! 232963
4295 RETURN 
4299 ! 
4300 SET_READONLY:! SSP#253613   
4310 SET_PARAM 'XI'
4320 SET_READONLY_END:RETURN 
4330 CLEAR_READONLY:! SSP#253613             
4340 SET_PARAM -'XI'
4350 CLEAR_READONLY_END:RETURN 
4359 ! 
4500 ! Process emails to resend
4510 SELECT_TO_RESEND:
4520 FOR XX=1 TO FILT_CNT
4525 EMAIL_GRID.CTL'ROW=XX
4530 READ DATA FROM EMAIL_GRID.CTL'ROWDATA$ TO IOL=EMAIL_GRID.CTL'LOADIOLIST$
4540 IF C_DELETE$<>"1" THEN GOTO NEXT_TO_RESEND
4550 IF C_PROC$<>"1" THEN GOTO ADD_TO_SEND ! Ignore unprocessed emails
4560 ! GRID WRITE EMAIL_GRID.CTL,8,XX,""
4562 ADD_TO_SEND:
4565 GOSUB SELECT_FILTER
4575 NEXT_TO_RESEND:
4580 NEXT XX
4590 GOTO SELECT_TO_SEND
4599 ! 
5005 SELECT_FILTER:
5010 READ DATA FROM EMAIL_GRID.CTL'ROWDATA$ TO IOL=EMAIL_GRID.CTL'LOADIOLIST$
5015 IF NOT(FN%ISOPEN(FCHAN)) THEN FCHAN=HFN; OPEN (FCHAN)"*memory*"
5030 IF EML_STAT$="2" THEN IF C_PROC$<>"1" THEN C_DELETE$=""; GOTO UPDATE_FCHAN
5040 IF EML_STAT$<>"2" THEN IF C_PROC$="1" THEN C_DELETE$=""
5100 UPDATE_FCHAN:! SSP 304343
5120 IF C_DELETE$="1" THEN {
5130 WRITE (FCHAN,KEY=C_KQ$,ERR=*NEXT)C_KQ$,C_EMAIL_TO$
5140  } ELSE {
5150 REMOVE (FCHAN,KEY=C_KQ$,ERR=*NEXT)
5160  }
5195 RETURN 
5199 ! 
6000 SELECT_ALL:
6010 FOR XX=1 TO FILT_CNT
6020 GRID WRITE EMAIL_GRID.CTL,1,XX,"1"
6050 EMAIL_GRID.CTL'ROW=XX
6060 GOSUB SELECT_FILTER
6070 NEXT XX
6090 RETURN 
6099 ! 
6100 DESELECT_ALL:
6110 FOR XX=1 TO FILT_CNT
6120 GRID WRITE EMAIL_GRID.CTL,1,XX,""
6150 EMAIL_GRID.CTL'ROW=XX
6160 GOSUB SELECT_FILTER
6170 NEXT XX
6190 RETURN 
6199 ! 
9999 END 
13000 ! 
13004 SEND_EMAILS:
13010 ENTER X3$,X4$,FL_CHL,FROM$,BATCH_NO$,(PROG1),(PROG2)
13015 SAVE_PERCENT_GUI=%GUI; %GUI=1
13020 IF FL_CHL=0 THEN GOTO END_PROG ! No records to process in incoming file
13030 REM "FILES
13035 DIM ZM[NUM(X3$(60,3))]
13040 ZM$="01O EF5...  13O ZZPARM  "
13050 CALL "ZZFLES",X3$,Y1$,Y0$,ZM$,ZM{ALL},Z0,Z1; ON Z0 GOTO *NEXT,END_PROG
13060 _EFIL_NO=HFN; OPEN (_EFIL_NO,IOL=*)"EFQ"+%C$
13070 ZE_KEY$=PAD(%C$,3)+"E/F"; READ (ZZPARM,ERR=*NEXT,KEY=ZE_KEY$,DOM=*NEXT)EFPARAM$
13080 DBCOMPNAME$=STP(MID(EFPARAM$,1,30),2); IF STP(DBCOMPNAME$,2)="" THEN DBCOMPNAME$="DemandBridge"
13090 IF %DRGQRF_NO_PROG_DISP$="Y" THEN PROG1=0,PROG2=0 ! 232963
13100 ! 
13300 FILE_SOURCE:
13310 KNUM=0
13340 IF FROM$="IN" THEN KNUM=1; GOTO BATCH_EMAIL ! SSP 304343
13345 GOSUB INIT_ALL
13348 IF FROM$="BT" THEN KNUM=1 ELSE KNUM=0 ! SSP 304343
13350 READ (FL_CHL,KEY="",DOM=*NEXT)
13400 NEXT_READ:
13410 MK$=KEY(FL_CHL,END=END_PROG); READ (FL_CHL,KEY=MK$)*,ALT_EML$
13420 FIND (_EFIL_NO,KEY=MK$,KNO=KNUM,DOM=NEXT_READ)
13422 IF UID="jvv" THEN ESCAPE 
13425 IF STP(ALT_EML$,2)<>"" THEN EML_TO_ADDR$=ALT_EML$
13430 GOSUB PROCESS_EMAIL
13435 KS$=KEC(_EFIL_NO,KNO=0) ! 304343
13440 WRITE (_EFIL_NO,KEY=KS$) ! SSP 304343
13480 GOTO NEXT_READ
13499 ! 
13500 BATCH_EMAIL:
13505 EMAIL_SENT=0 ! SSP 305069
13510 IF STP(BATCH_NO$,2)="" THEN GOTO END_PROG ELSE BATCH_NO$=PAD(BATCH_NO$,5)
13520 READ (_EFIL_NO,KEY=BATCH_NO$+"",KNO=KNUM,DOM=*NEXT)
13525 NEXT_BATCH_READ:
13530 BK$=KEY(_EFIL_NO,KNO=KNUM,END=CHECK_SJ_EML) ! SSP 305069
13535 READ (_EFIL_NO)
13540 IF BK$(1,LEN(BATCH_NO$))<>BATCH_NO$ THEN GOTO CHECK_SJ_EML ! SSP 305069
13545 IF EML_PROCESSED$="1" THEN GOTO NEXT_BATCH_READ ! Ignore if email previously sent
13550 GOSUB PROCESS_EMAIL
13555 KC$=KEC(_EFIL_NO,KNO=0)
13560 WRITE (_EFIL_NO,KEY=KC$)
13565 EMAIL_SENT=1 ! SSP 305069
13570 GOTO NEXT_BATCH_READ
13579 ! 
13580 CHECK_SJ_EML:! SSP 305069
13582 IF EMAIL_SENT=1 THEN EMAIL_SENT=0; MSGBOX ("Emails have been processed") ! SSP 305069
13585 GOTO END_PROG
13599 ! 
13600 PROCESS_EMAIL:
13605 GOSUB GET_EMAIL_HEADER ! SSP 307212
13607 CLOSE (TXTCHN,ERR=*NEXT) ! SSP 307212
13608 GOTO 13640 ! SSP 307212
13610 TMP_TO$=EML_TO_ADDR$
13620 TMP_SUB$="You have received "+DOC_TYPE$+" :"+DOC_REF$+" from "+DBCOMPNAME$
13630 TMP_MSG$="Please find attached email"
13640 TMP_ATTACH$=EMAIL_PATH$+"/"+EMAIL_FILE$+".pdf"
13645 IF TMP_ATTCH$<>"" THEN TMP_ATTACH$=TMP_ATTACH$+";"+TMP_ATTCH$
13650 TMP_RESPMSG$=""; TMP_RESPMSG$=FN%SENDMAIL$(TMP_TO$,TMP_FROM$,TMP_CC$,TMP_BCC$,TMP_SUB$,TMP_MSG$,TMP_ATTACH$,"")
13660 IF NUL(TMP_RESPMSG$) THEN GOTO *NEXT ELSE RETURN 
13680 EML_PROCESSED$="1"
13690 RETURN 
13699 ! 
13700 GET_EMAIL_HEADER:
13710 HDRFILE$=EMAIL_PATH$+"/"+EMAIL_FILE$+".txt"
13720 TXTCHN=HFN; OPEN (TXTCHN,ERR=*RETURN)HDRFILE$
13730 READ RECORD (TXTCHN)REC$; TMP_ADDR$=REC$(POS(":"=REC$)+2)
13740 READ RECORD (TXTCHN)REC$; TMP_FROM$=REC$(POS(":"=REC$)+2)
13750 READ RECORD (TXTCHN)REC$; TMP_SUB$=REC$(POS(":"=REC$)+2)
13760 READ RECORD (TXTCHN)REC$; TMP_MSG$=REC$(POS(":"=REC$)+2)
13765 TMP_MSG$=SUB(TMP_MSG$,"\n",CHR(10))
13770 READ RECORD (TXTCHN)REC$; TMP_ATTACH$=REC$(POS(":"=REC$)+2)
13780 TMP_ATTCH$=""; IF STP(TMP_ATTACH$,2)<>"" THEN TMP_ATTCH$=TMP_ATTACH$
13782 TMP_FLD2$="",TMP_FLD3$="",TMP_CC$="",TMP_BCC$=""
13784 M1=POS(";"=TMP_ADDR$,1,1); IF M1=0 THEN TMP_TO$=TMP_ADDR$; RETURN ELSE TMP_TO$=TMP_ADDR$(1,M1-1),TMP_FLD$=STP(TMP_ADDR$(M1+1),2)
13785 M2=POS(";"=TMP_FLD$,1,1); IF M2=0 THEN TMP_FLD2$=TMP_FLD$ ELSE TMP_FLD2$=TMP_FLD$(1,M2-1),TMP_FLD3$=STP(TMP_FLD$(M2+1),2)
13788 IF TMP_FLD2$<>"" THEN IF MID(TMP_FLD2$,1,3)="cc=" THEN TMP_CC$=MID(TMP_FLD2$,4) ELSE IF MID(TMP_FLD2$,1,4)="bcc=" THEN TMP_BCC$=MID(TMP_FLD2$,5) ELSE TMP_TO$=TMP_TO$+";"+TMP_FLD2$
13790 IF TMP_FLD3$<>"" THEN IF MID(TMP_FLD3$,1,3)="cc=" THEN TMP_CC$=MID(TMP_FLD3$,4) ELSE IF MID(TMP_FLD3$,1,4)="bcc=" THEN TMP_BCC$=MID(TMP_FLD3$,5) ELSE TMP_TO$=TMP_TO$+";"+TMP_FLD3$
13798 RETURN 
13799 ! 
17800 INIT_ALL:
17810 CALL "ZZINFO",ZM[1],T9,X3$,T,T0,K,B,D,S0,S1,F$
17820 T0=INT(T*.02); IF T0<=1 THEN T0=2
17830 RETURN 
17899 ! 
17900 UPDATE_PROG_ALL:
17910 ! IF PROG1 THEN CALL "ZZBARG",X3$,"HG",PROG1,10,50,T1,T,C
17920 IF PROG1 THEN IF C<>T THEN PROG1'VALUE$="Processing files: "+STR(C)+" of "+STR(T) ELSE PROG1'VALUE$="Processing files: "+STR(C)+" files done!"
17930 RETURN 
17999 ! 
18299 ! 
18300 END_PROG:
18305 %GUI=SAVE_PERCENT_GUI
18310 CALL "ZZFLES",X3$,Y1$,Y0$,"END",ZM{ALL},0,0
18315 CLOSE (_EFIL_NO)
18320 EXIT 
18999 ! 
20100 VIEW_IMAGE:! Selection made from grid, view the image
20105 SET_FOCUS READ CURR_FOCUS; IF CURR_FOCUS<>EMAIL_GRID.CTL THEN RETURN 
20110 SWITCH EMAIL_GRID.CTL'CURRENTCOLUMN
20115 CASE 7 ! hit BUTTON column
20120 EMAIL_GRID.CTL'ROW=EMAIL_GRID.CTL'CURRENTROW,EMAIL_GRID.CTL'COLUMN=10,TMPP$=EMAIL_GRID.CTL'VALUE$
20125 CALL "ILGFUN;VIEW_IMAGE",TMPP$+".pdf",""
20130 BREAK ! 1
20185 END SWITCH 
20190 VIEW_IMAGE_END: EMAIL_GRID.CTL'CURRENTCOLUMN=1,NEXT_ID=BUTTON_3.CTL
20195 RETURN 
20199 ! **********************************************************
20200 UPD_TO_EMAIL:
20205 SET_FOCUS READ CURR_FOCUS; IF CURR_FOCUS<>EMAIL_GRID.CTL THEN RETURN 
20210 EMAIL_GRID.CTL'ROW=EMAIL_GRID.CTL'CURRENTROW,EMAIL_GRID.CTL'COLUMN=4,TMPEML$=EMAIL_GRID.CTL'VALUE$
20220 CROW=EMAIL_GRID.CTL'CURRENTROW
20222 IF UID="jvv" THEN ESCAPE 
20225 C_EMAIL_TO$=TMPEML$
20230 GRID WRITE EMAIL_GRID.CTL,4,CROW,TMPEML$; EMAIL_GRID.CTL'COLUMN=4,EMAIL_GRID.CTL'ROW=CROW
20250 RETURN 
20340 TMPEML$=""
30000 SEL_DB:
30010 OPT$=DB_FFP$,LB$="",SS$=""
30015 IF OPT$="FX" THEN FFU_FILE=_QSX ELSE FFU_FILE=_ZZPAR ! SSP236948
30020 READ (FFU_FILE,KEY="FORMAT"+OPT$,DOM=*NEXT) ! SSP236948
30030 _KOPT$=KEY(FFU_FILE,END=END_SEL_DB); READ (FFU_FILE)ZZPAR$ ! SSP236948
30040 IF MID(_KOPT$,1,8)<>"FORMAT"+OPT$ THEN GOTO END_SEL_DB
30050 LB$=LB$+ZZPAR$(9,1)+" - "+ZZPAR$(10,30)+SEP
30055 IF STP(OPTT$)<>"" AND OPTT$=_KOPT$(9,1) THEN SS$=ZZPAR$(9,1)+" - "+ZZPAR$(10,30)
30060 GOTO 30030
30070 END_SEL_DB:
30080 LB$="All"+SEP+LB$
30085 IF SS$="" THEN SS$="All"
30090 DROP_BOX LOAD DB_TYPE.CTL,LB$
30100 DB_TYPE$=SS$ ! DROP_BOX WRITE DB_TYPE.CTL,SS$; DB_TYPE$=SS$ ! SSP236948
30110 RETURN 
30119 ! 
31000 DELETE_EML_INVOICE:
31010 ENTER X3$,X4$,INV_KEY$,FROM$,BATCH_NO$
31015 ! Ths routine is called from ARGEAA and AR2EAA if an invoice is being deleted and the A/R parm to only send emails at sales jnl update time is set.
31020 IF STP(INV_KEY$,2)="" THEN GOTO END_DELETE_EML ! No invoice to delete
31030 REM "FILES
31035 DIM ZM[NUM(X3$(60,3))]
31060 _EFIL_NO=HFN; OPEN (_EFIL_NO,IOL=*)"EFQ"+%C$
31070 CHL=HFN; OPEN (CHL)"EFQ"+%C$
31310 KNUM=1
31340 INV$=INV_KEY$(1,8),CUST$=INV_KEY$(9)
31342 ! CALL "ZZDISP","AX",CUST$,"A/R",X3$,TMP$,"",0,0,X4$
31345 DTYPE$="IN",EMLT$="C"+PAD(CUST$,20),INV$=PAD(INV$,12)
31350 BATCH_NO$=PAD(BATCH_NO$,5)
31360 EKEY$=BATCH_NO$+DTYPE$+INV$ ! DTYPE$+INV$
31370 ! 
31500 FIND_EMAIL:
31520 READ (_EFIL_NO,KEY=EKEY$,KNO=KNUM,DOM=*NEXT)
31525 NEXT_INV_READ:
31530 BK$=KEY(_EFIL_NO,END=END_DELETE_EML)
31535 READ (_EFIL_NO,KEY=BK$)
31540 IF BK$(1,LEN(EKEY$))<>EKEY$ THEN GOTO END_DELETE_EML
31542 IF EML_PROCESSED$="1" THEN GOTO NEXT_INV_READ
31545 KK$=DOC_TYPE$+EML_TYPE$+KEYDATA$+DOC_REF$+COPY_NUM$
31550 REMOVE (CHL,KEY=KK$,ERR=*NEXT); GOTO 31570
31570 GOTO NEXT_INV_READ
31599 ! 
31680 END_DELETE_EML:
31685 CLOSE (CHL,ERR=*NEXT)
31688 CLOSE (_EFIL_NO,ERR=*NEXT)
31690 EXIT 
31699 ! 
56001 ! 
56002 REM "229312-Datareplication enhancement for selective replication of    
56003 REM "232963-Ability to not show progress, slows processing remotely
56004 REM "239169-Use of two progress bars not working properly
56005 REM "253613-Data Rep error                                              - ERROR 0 2096
56006 REM "277590-Issues with Data Replication Filters.                       
56007 REM "298391-Ability to send E/F emails of Invoices after SJ Update      
56008 REM "304343-EFGMAQ - Batch filter issue                                 
56009 REM "305069-Change to Defer Email in SJ process                         
56010 REM "307312-Too many files open in EFGMAQ                               
