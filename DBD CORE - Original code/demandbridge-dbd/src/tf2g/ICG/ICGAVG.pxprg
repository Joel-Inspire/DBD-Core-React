0010 ! ICGAVG - Recompute IC0 overall average cost 
0020 SETESC 9300; SETERR 9000
0035 REM "5.6 - 09/03/08 - 14.271388 - jvv - SSP# 191604
0037 REM "191604-Add special pricing modifications to gui                    
0040 REM "Copyright 2008 DemandBridge, Inc.; Norcross, Georgia
0041 REM "        Licensed Software - All Rights Reserved.
0050 PROCESS "ICGAVG","../ICG/IC.EN"
0060 EXIT 
0090 CLEAR ; SETERR 0100; ENTER X3$,X4$,Q0$,Q1$
0095 F$="CALLED",FF$=F$
0100 SETERR 9000
0105 INIT:
0110 X0$="IC2AVG",X1$="Recompute IC0 Overall Average Cost"
0120 DIM Z0$(80,"-")
0130 K0=20,K1=1
0135 C9=-1
0200 REM "
0240 IF X3$="" THEN CALL "ZZCOMP",X0$,X1$,X2$,X3$,X4$,M9$,-1,X1,X2; IF X1>0 THEN GOTO 9920
0245 IF X3$(66,1)>"2" THEN PRECISION NUM(X3$(66,1),ERR=0246)
0295 DIM Z0$(80,X3$(235,1)); Z0$=MNM('GS')+Z0$+MNM('GE')
0300 REM "IOLISTS
0320 IOLIST B$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14],B[15],B[16],B[17],B[18],B[19],B[20],B[21],B[22],B[23],B[24],B[25],B[26],B[27],B[28],B[29],B[30],B[31],B[32],B[33],B[34],B[35],B[36],B[37],B[38],B[39],B[40]
0321 IOLIST B$,STR(B[0]),B[1],STR(B[2]),B[3],STR(B[4]),B[5],STR(B[6]),B[7],STR(B[8]),B[9],STR(B[10]),B[11],STR(B[12]),STR(B[13]),B[14],B[15],B[16],STR(B[17]),STR(B[18]),B[19],STR(B[20]),B[21],STR(B[22]),B[23],STR(B[24]),B[25],STR(B[26]),B[27],STR(B[28]),B[29],STR(B[30]),B[31],STR(B[32]),B[33],B[34],B[35],B[36],B[37],B[38],B[39],B[40]
0330 IOLIST C$,C[0],C[1],C[2],C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20]
0331 IOLIST C$,C[0],STR(C[1]),STR(C[2]),C[3],C[4],C[5],C[6],C[7],C[8],C[9],C[10],C[11],C[12],C[13],C[14],C[15],C[16],C[17],C[18],C[19],C[20]
0500 REM "FILES
0505 DIM Z[NUM(X3$(60,3))]
0510 Z$="02O IC0...  03O IC1...  04O SP0...  13O ZZPARM  " ! SSP 191604
0520 CALL "ZZFLES",X3$,Y1$,Y0$,Z$,Z{ALL},Z0,Z1; ON Z0 GOTO 0521,9900
0550 DIM M6[1]; CALL "IC2PRE",X3$,Z[13],"",M6{ALL},"",0,"",0
0560 IF FF$="CALLED" THEN GOSUB 8100; GOTO 0700
0585 RT$=""; CALL "RT2PRM",ERR=0586,X3$,X4$,RT$
0600 REM "
0610 GOSUB 6000
0620 GOSUB 8100
0625 RETURN 
0640 CALL "ZZPROM",".Y",X3$,Z,"Proceed?","","",0; ON Z GOTO 0641,9900
0650 PROCEED:
0700 I$=Q0$
0900 DIM B[40],C[20]
0990 IF I$="" THEN READ (Z[2],KEY="",DOM=0991)
1000 REM "
1020 IF I$="" THEN EXTRACT (Z[2],END=5000)IOL=0320 ELSE EXTRACT (Z[2],KEY=I$,DOM=9900)IOL=0320
1030 IF I$="" THEN C=C+1; IF MOD(C,T0)=1 THEN GOSUB 8150
1040 GOSUB 3000
1060 IF T[0]=0 THEN GOTO 1200
1080 PRECISION 8
1100 B[18]=T[1]/T[0]
1120 PRECISION M6[1]
1140 B[18]=B[18]+0
1160 PRECISION 2
1190 GOSUB SP0_PRICE ! SSP 191604
1200 WRITE (Z[2])IOL=0321
1210 IF RT$>"" THEN IF RT$(7,1)="H" THEN CUST$=B$(1,10); CALL "RT2WOC",ERR=1211,X3$,X4$,CUST$,"IC0...","U",B$(1,20)
1220 IF I$="" THEN GOTO 1020 ELSE GOTO 9900
3000 REM "Compute Average Cost from Customer Owned Inventory Locations
3010 DIM T[1]
3020 READ (Z[3],KEY=B$(1,20),DOM=3021)
3040 READ (Z[3],END=3170)IOL=0330
3060 IF B$(1,20)<>C$(1,20) THEN GOTO 3170
3080 IF C$(21,1)>"8" THEN GOTO 3170
3100 Q=C[3]+C[4]-C[5]+C[6]
3120 E=Q*C[2]
3140 T[0]=T[0]+Q,T[1]=T[1]+E
3160 GOTO 3040
3170 RETURN 
5000 REM "EOJ"
5005 IF I$="" THEN C=T; GOSUB 8150
5010 IF FF$="CALLED" THEN GOTO 9900; REM "ssp089855 cwj 7/15/98
5030 MSGBOX MSG("PROC_C"),MSG("CONFIRMING"),"INFO,TIM=3"
5040 GOTO 9900
6000 REM "BACKGROUND
6001 RETURN 
6005 PRINT (0,ERR=6016)'SB',
6020 PRINT @(0,4),"This program will re-compute for each inventory item its overall average cost which is stored in file IC0 by extending the inventory value of the distributor owned inventory warehouse location IC1 records",
6165 PRINT (0,ERR=6166)'SF',
6190 RETURN 
8100 REM "GOSUB here, once, at the beginning, after the background is set and before the Proceed? question. Set T to total # to do (by calling ZZINFO) and T0 to the reporting interval (report every T0 number of records) this is typically 2% of total; This also prints a message (@8115),so adjust accordingly
8110 CALL "ZZINFO",Z[2],T9,X3$,T,T0,K,B,D,S0,S1,F$
8115 REM IF I$="" THEN PRINT @(0,9),"There are "+STR(T)+" records to process"
8130 T0=INT(T*.02); IF T0<=1 THEN T0=2
8145 RETURN 
8150 REM "Call this each time to update the bar graph. Display horiz. bar graph given total #, T and current #, C, and time counter T1 (T1 is init'ed here and used here, just don't use it somewhere else)
8151 REM "line reset to 21 from 19 for ssp#089855 cwj 7/15/98
8155 CALL "ZZBARG",X3$,"HG",6.5,1,40,T1,T,C
8195 RETURN 
9000 REM "ERROR PROCESSING
9005 IF ERR=69 THEN GOTO 9500
9010 Y5=ERR,Y6=TCB(5)
9015 SETERR 9016; Y8$=LST(PGM(Y6))
9016 SETERR 9000
9040 CALL "ZZERRM",Y8$,X0$,Y7$,X3$,Y5,Y6,Y7,Y8,0
9045 REM 
9050 ON Y7 GOTO 9060,9100,9800,9070,9090
9055 REM 
9060 RETRY 
9070 SETERR 9080
9075 EXECUTE Y7$
9080 SETERR 9000; RETRY 
9090 SETERR 0000; RETRY 
9100 REM " TRANSFER CONTROL
9180 GOTO 0990
9190 GOTO 9800
9300 SETESC 9350
9310 SETERR 9350
9315 IF X3$(47,1)="N" THEN RETURN ELSE SETESC 0000; RETURN 
9350 SETERR 9000; RETURN 
9500 REM "CTRL LOGIC
9510 SETERR 9000; GOSUB 6400
9520 ON C9 GOTO 1140,2040
9900 REM "END PROGRAM
9902 CMD_STR$="E"
9910 CALL "ZZFLES",X3$,Y1$,Y0$,"END",Z{ALL},0,0
9930 SETERR 9940; Q1$=A1$; EXIT 
9940 SETESC 9350
9950 RUN "ZMENU"
9999 END 
10000 SP0_PRICE:! SSP 191604 (10000-10090)
10005 PRICE=B[0],COST=B[18],PRICE_UM=1,COST_UM=1
10010 CALL "UPDSP0;GET_IGP",Z[4],B$(1,20),PRICE,COST,NEWPRICE,COST_UM,PRICE_UM,FOUNDSP0
10020 IF FOUNDSP0 THEN B[0]=NEWPRICE
10090 RETURN 
10098 ! 
10099 ! 
